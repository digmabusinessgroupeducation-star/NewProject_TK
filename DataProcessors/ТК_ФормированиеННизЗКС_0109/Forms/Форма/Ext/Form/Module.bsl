

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контрагент = Справочники.Контрагенты.НайтиПоКоду("D100434");
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидыННПоУмолчанию();
	
	ОтразитьТоварыТекущейСтроки();
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТекущееДействие = "Выгрузить" Тогда 
		
		//ПроверяемыеРеквизиты.Добавить("КаталогВыгрузки");
		
	ИначеЕсли ТекущееДействие = "ОбновитьСписок" Тогда 
		
		ПроверяемыеРеквизиты.Добавить("Организация");				
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКаталогаДокументовЗавершение", ЭтаФорма);
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);		
	ДиалогОткрытия.Заголовок = НСтр("ru = 'Выберите каталог сохранения .XML файлов'; uk = 'Оберіть каталог збереження .XML файлів'");
	ДиалогОткрытия.Каталог = КаталогВыгрузки;
	
	ДиалогОткрытия.Показать(ОписаниеОповещения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаДокументовЗавершение(ИмяКаталога, ДопПараметры) Экспорт 
	
	Если ТипЗнч(ИмяКаталога) = Тип("Массив") И ИмяКаталога.Количество() > 0 Тогда 
		КаталогВыгрузки = ИмяКаталога[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоННПриАктивизацииСтроки(Элемент)
	
	//ПодключитьОбработчикОжидания("ОтразитьТоварыТекущейСтроки", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыНалоговыхНакладныхПометкаПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("УстановитьВидимостьЭлементов", 0.2, Истина);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ОчиститьСообщения();
	
	ТекущееДействие = "ОбновитьСписок";
	
	Если ПроверитьЗаполнение() Тогда
		ЗапуститьОперацию();
	КонецЕсли;
	
	ТекущееДействие = "";
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	ОчиститьСообщения();
	
	ТекущееДействие = "СоздатьДокументы";
	
	Если ПроверитьЗаполнение() Тогда 
		ЗапуститьОперацию();
	КонецЕсли;
	
	ТекущееДействие = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьННвXML(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ТекущееДействие = "Выгрузить";
	
	СтруктураВидовНН = ПолучитьСтруктуруВидовНН(Отказ);
	
	Если ПроверитьЗаполнение() И Не Отказ Тогда 		
		СохранитьФайлыНН(СтруктураВидовНН);
	КонецЕсли;
	
	ТекущееДействие = "";	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВБухгалтерию(Команда)
	
	Результат = ВыгрузитьВБухгалтериюНаСервере();	
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Выгрузка в бухгалтерию'"),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выгружено %1 документов'"),
			Результат.Выгружено));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздаватьННБезНДСПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьОперацию()
	
	Отказ = Ложь;
	СтруктураВидовНН = ПолучитьСтруктуруВидовНН(Отказ);
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ФормированиеННизЗКС_0109";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	

	Если ТекущееДействие = "ОбновитьСписок" Тогда 
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Обновления списка налоговых накладных'; uk = 'Оновлення списку податкових накладних'"); 		
	ИначеЕсли ТекущееДействие = "СоздатьДокументы" Тогда 
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Создание налоговых накладных'; uk = 'Створення податкових накладних'"); 		
	ИначеЕсли ТекущееДействие = "Выгрузить" Тогда 
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка налоговых накладных в XML'; uk = 'Вивантаження податкових накладних в XML'"); 		
	Иначе
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьВыполнениеОперации(СтруктураВидовНН);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, ТекущееДействие);		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);	
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеОперации(СтруктураВидовНН)
	
	Перем ДлительнаяОперация;
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыОперации.Вставить("ДатаОкончания", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("ОрганизацияФС", Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("eb5c09df-0b80-11e6-80bf-005056a71b84")));
	ПараметрыОперации.Вставить("Контрагент", Контрагент);
	ПараметрыОперации.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	ПараметрыОперации.Вставить("СтруктураВидовНН", СтруктураВидовНН);
	ПараметрыОперации.Вставить("ПерезаполнитьНН", ПерезаполнитьНН);

	ДеревоЗначений 	= РеквизитФормыВЗначение("ДеревоНН", Тип("ДеревоЗначений"));		
	ДеревоМРЦ	 	= РеквизитФормыВЗначение("ДеревоННМРЦ", Тип("ДеревоЗначений"));
	ДеревоБезНДС 	= РеквизитФормыВЗначение("ДеревоННБезНДС", Тип("ДеревоЗначений"));
	ДеревоКомпенс 	= РеквизитФормыВЗначение("ДеревоННКомпенсирующие", Тип("ДеревоЗначений"));
	
	Приложения2 	= ТаблицаП2кНН.Выгрузить();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Если ТекущееДействие = "ОбновитьСписок" Тогда 
		
		ДеревоНН.ПолучитьЭлементы().Очистить();
		ДеревоННМРЦ.ПолучитьЭлементы().Очистить();
		ДеревоННБезНДС.ПолучитьЭлементы().Очистить();
		ДеревоННКомпенсирующие.ПолучитьЭлементы().Очистить();
		
		Приложения2.Очистить();
		
		ДеревоЗначений.Строки.Очистить();
		ДеревоМРЦ.Строки.Очистить();
		ДеревоБезНДС.Строки.Очистить();
		ДеревоКомпенс.Строки.Очистить();
		
		ТаблицаП2кНН.Очистить();		
		Объект.ТоварыВыгрузки.Очистить();
		
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
								ПараметрыВыполнения, 
								"Обработки.ТК_ФормированиеННизЗКС_0109.ПолучитьСпискиНалоговыхНакладных",
								ДеревоЗначений,
								ДеревоМРЦ,
								ДеревоБезНДС,
								ДеревоКомпенс,
								Приложения2,
								ПараметрыОперации);
								
	ИначеЕсли ТекущееДействие = "СоздатьДокументы" Тогда 
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
								ПараметрыВыполнения,
								"Обработки.ТК_ФормированиеННизЗКС_0109.СоздатьНалоговыеНакладные",
								ДеревоЗначений,
								ДеревоМРЦ,
								ДеревоБезНДС,
								ДеревоКомпенс,
								Приложения2,
								ПараметрыОперации);
								
	ИначеЕсли ТекущееДействие = "Выгрузить" Тогда 
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
								ПараметрыВыполнения, 
								"Обработки.ТК_ФормированиеННизЗКС_0109.ПолучитьВыгруженныеФайлы",
								ДеревоЗначений,
								ДеревоМРЦ,
								ДеревоБезНДС,
								Приложения2,
								ПараметрыОперации);
																																
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, Знач ВидОперации) Экспорт
		
	ОписаниеРезультата = ОбработатьРезультатОперации(Операция);
	
	ЕстьОшибки = ОписаниеРезультата.ЕстьОшибки;
	Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Ошибки", НСтр("ru = 'Обнаружена ошибка при выполнении операции'; uk = 'Виявлена помилка при виконанні операції'"));
	ОповещениеТекст = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеТекст", НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'"));
	ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеПояснение", НСтр(""));
	Картинка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Картинка", БиблиотекаКартинок.Информация32);	
	СообщенияПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "СообщенияПользователю", Неопределено);
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
	Если Не ЕстьОшибки Тогда 
		
		Если ОписаниеРезультата.Свойство("МассивФайлов") Тогда 
			НачатьПолучениеФайловССервера(, ОписаниеРезультата.МассивФайлов, КаталогВыгрузки);
		КонецЕсли;
			
	ИначеЕсли ЕстьОшибки Тогда 		
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибки);

	Иначе
		
		Если ЕстьОшибки Тогда 
			ПоказатьПредупреждение(, Ошибки,, НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'"));	
		КонецЕсли;
	КонецЕсли;		
		
	   
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатОперации(Операция)

	ОповещениеТекст 	= "";
	ОповещениеПояснение = "";
	ЕстьОшибки 			= Ложь;
	Ошибки 				= "";
	Результат 			= Неопределено;
	Картинка			= Неопределено;
	
	ОписаниеРезультата = Новый Структура;
	
	Если Операция = Неопределено Тогда 
		
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		Картинка = БиблиотекаКартинок.Ошибка32;
		                                                  
	Иначе
		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда 		
			
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				
				Если Результат.Свойство("ДеревоЗначений") Тогда 					
					ЗначениеВРеквизитФормы(Результат.ДеревоЗначений, "ДеревоНН");					
				КонецЕсли;
				
				Если Результат.Свойство("ДеревоННМРЦ") Тогда 					
					ЗначениеВРеквизитФормы(Результат.ДеревоННМРЦ, "ДеревоННМРЦ");					
				КонецЕсли;
				
				Если Результат.Свойство("ДеревоБезНДС") Тогда 
					ЗначениеВРеквизитФормы(Результат.ДеревоБезНДС, "ДеревоННБезНДС");
				КонецЕсли;
				
				Если Результат.Свойство("ДеревоКомпенсирующие") Тогда 
					ЗначениеВРеквизитФормы(Результат.ДеревоКомпенсирующие, "ДеревоННКомпенсирующие");
				КонецЕсли;
				
				Если Результат.Свойство("Товары") Тогда 					
					Объект.ТоварыВыгрузки.Загрузить(Результат.Товары);
				КонецЕсли;		
				
				Если Результат.Свойство("ТаблицаПриложения2") Тогда 
					ТаблицаП2кНН.Загрузить(Результат.ТаблицаПриложения2);
				КонецЕсли;
				
				Если Результат.Свойство("МассивФайлов") Тогда 
					ОписаниеРезультата.Вставить("МассивФайлов", Результат.МассивФайлов);
					ОповещениеТекст = НСтр("ru = 'Выгрузка в XML'; uk = 'Вивантаження у XML'");							
					ОповещениеПояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Сохранено %1 файл(ов)'; uk = 'Збережено %1 файл(ів)'"),
											Результат.МассивФайлов.Количество());				
				КонецЕсли;

			КонецЕсли;						
												
			Картинка = БиблиотекаКартинок.Успешно32;
						
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 	
			
			ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");
			Картинка = БиблиотекаКартинок.Ошибка32;
			ЕстьОшибки = Истина;
			
			ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");				
			Ошибки = Операция.КраткоеПредставлениеОшибки;
			
		Иначе			
			
			ЕстьОшибки = Истина;
		
		КонецЕсли;
		
		Если Операция.Свойство("Сообщения") И ТипЗнч(Операция.Сообщения) = Тип("ФиксированныйМассив") Тогда 
			Для Каждого Сообщение Из Операция.Сообщения Цикл 
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщенияПользователю") Тогда 
			СообщенияПользователю = Результат.СообщенияПользователю;
		КонецЕсли;		
	КонецЕсли;
	
	//Добавил условие, так как при формировании xml  очищался массив файлов
	Если  НЕ ТипЗнч(Результат) = Тип("Структура") Тогда
		ОписаниеРезультата = Новый Структура;
	КонецЕсли;
	
	ОписаниеРезультата.Вставить("ЕстьОшибки", ЕстьОшибки);
	
	Если Не ПустаяСтрока(Ошибки) Тогда 
		ОписаниеРезультата.Вставить("Ошибки", Ошибки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Картинка) Тогда 
		ОписаниеРезультата.Вставить("Картинка", Картинка);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеТекст) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеТекст", ОповещениеТекст);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеПояснение) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеПояснение", ОповещениеПояснение);
	КонецЕсли;
	
	Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
		ОписаниеРезультата.Вставить("СообщенияПользователю", СообщенияПользователю);
	КонецЕсли;
	
	Возврат ОписаниеРезультата;		
КонецФункции


&НаКлиенте
Процедура СохранитьФайлыНН(СтруктураВидовНН)
	
	МассивФайлов = ПолучитьВыгруженныеФайлы(СтруктураВидовНН);
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов();
	ПараметрыДиалога.ВыборКаталога = Истина;

	ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите каталог сохранения .XML файлов'; uk = 'Оберіть каталог збереження .XML файлів'");
	
	НачатьПолучениеФайловССервера(МассивФайлов, ПараметрыДиалога);	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлыНН_Старый()
	
	ВсеОк = Истина;
	МассивФайлов = ПолучитьВыгруженныеФайлы(Неопределено);
	УдаленоФайлов = 0;
	
	Если МассивФайлов <> Неопределено И МассивФайлов.Количество() > 0 Тогда 
		
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеОкончания", ЭтотОбъект);
		Для Каждого Файл Из МассивФайлов Цикл
			Путь = КаталогВыгрузки+"\"+Файл.Имя;
			НачатьПолучениеФайлаССервера(Оп, Файл.Хранение, Путь);
		КонецЦикла;		
		
		//НачатьПолучениеФайловССервера(, МассивФайлов, КаталогВыгрузки);	
		//		
		Для Каждого ОписаниеФайла Из МассивФайлов Цикл 
			Попытка
				УдалитьИзВременногоХранилища(ОписаниеФайла.Хранение);
				УдаленоФайлов = УдаленоФайлов + 1;
			Исключение
			КонецПопытки;
		КонецЦикла;		
	Иначе
		ВсеОк = Ложь;
	КонецЕсли;
	
	Если ВсеОк Тогда 
		Картинка = БиблиотекаКартинок.Успешно32;
		ОповещениеТекст = НСтр("ru = 'Успешно выгружено'; uk = 'Успішно вивантажено'");
		ОповещениеПояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Выгружено %1 файлов'; uk = 'Вивантажено %1 файлів'"),
								МассивФайлов.Количество());
	Иначе
		Картинка = БиблиотекаКартинок.Ошибка32;
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Файлы не выгружены'; uk = 'Файли не вивантажені'");		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеОкончания(ПолученныйФайл, Параметры) Экспорт
КонецПроцедуры


&НаСервере
Функция ПолучитьВыгруженныеФайлы(СтруктураВидовНН)
		
	
	//ПараметрыОперации.Вставить("ДатаНачала", Период.ДатаНачала);
	//ПараметрыОперации.Вставить("ДатаОкончания", Период.ДатаОкончания);
	//ПараметрыОперации.Вставить("Организация", Организация);
	//ПараметрыОперации.Вставить("Контрагент", Контрагент);
	//ПараметрыОперации.Вставить("ПерезаполнитьНН", ПерезаполнитьНН);
	//ПараметрыОперации.Вставить("СоздаватьННБезНДС", СоздаватьННБезНДС);

	ДеревоЗначений 	= РеквизитФормыВЗначение("ДеревоНН", Тип("ДеревоЗначений"));		
	ДеревоМРЦ 		= РеквизитФормыВЗначение("ДеревоННМРЦ", Тип("ДеревоЗначений"));		
	ДеревоБезНДС 	= РеквизитФормыВЗначение("ДеревоННБезНДС", Тип("ДеревоЗначений"));
	ДеревоКомпенс 	= РеквизитФормыВЗначение("ДеревоННКомпенсирующие", Тип("ДеревоЗначений"));
	
	//Объект.ТоварыВыгрузки.Выгрузить(), 
	МассивФайлов = Обработки.ТК_ФормированиеННизЗКС_0109.ПолучитьВыгруженныеФайлы(ДеревоЗначений, ДеревоМРЦ, ДеревоБезНДС, ДеревоКомпенс, ТаблицаП2кНН.Выгрузить(), СтруктураВидовНН);
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоНН");
	ЗначениеВРеквизитФормы(ДеревоБезНДС, "ДеревоННБезНДС");
	
	Возврат МассивФайлов;
		
КонецФункции

&НаСервере
Функция ВыгрузитьВБухгалтериюНаСервере()
	
	Выгружено = 0;
	
	ПланОбмена = ПланыОбмена.ОбменБухгалтерия.НайтиПоКоду("BUH");
	
	Для Каждого СтрОрганизация Из ДеревоНН.ПолучитьЭлементы() Цикл 
		Для Каждого СтрДата Из СтрОрганизация.ПолучитьЭлементы() Цикл 
			Если Не СтрДата.ДокументНН.Пустая() Тогда 				
				ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, СтрДата.ДокументНН);
				Выгружено = Выгружено + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СтрОрганизация Из ДеревоННМРЦ.ПолучитьЭлементы() Цикл 
		Для Каждого СтрДата Из СтрОрганизация.ПолучитьЭлементы() Цикл 
			Если Не СтрДата.ДокументНН.Пустая() Тогда 				
				ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, СтрДата.ДокументНН);
				Выгружено = Выгружено + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	//Если СоздаватьННБезНДС Тогда 
		Для Каждого СтрОрганизация Из ДеревоННБезНДС.ПолучитьЭлементы() Цикл 
			Для Каждого СтрДата Из СтрОрганизация.ПолучитьЭлементы() Цикл 
				Если Не СтрДата.ДокументНН.Пустая() Тогда 				
					ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, СтрДата.ДокументНН);
					Выгружено = Выгружено + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	//КонецЕсли;
	
	Для Каждого Стр Из ТаблицаП2кНН Цикл 
		Если Не Стр.Документ.Пустая() Тогда 
			ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, Стр.Документ);
			Выгружено = Выгружено + 1;
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", Выгружено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти



#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьСтруктуруВидовНН(Отказ = Ложь)
	
	СтруктураВидовНН = Новый Структура;	
	Для Каждого ВидНН Из ВидыНалоговыхНакладных Цикл 
		Если ВидНН.Пометка Тогда 
			СтруктураВидовНН.Вставить(ВидНН.Значение);
		КонецЕсли;
	КонецЦикла;	

	Если СтруктураВидовНН.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выбранных видов налоговых накладных'; uk = 'Немає обраних видів податкових накладних'"));
		Отказ = Истина;
	КонецЕсли;
	
	Возврат СтруктураВидовНН;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидыННПоУмолчанию()
	
	ВидыНалоговыхНакладных.Добавить("НалоговыеИзЗКС", НСтр("ru = 'Налоговые накладные (ЗКС)'"), Истина);
	ВидыНалоговыхНакладных.Добавить("НалоговыеИзЗКСМРЦ", НСтр("ru = 'Налоговые накладные (ЗКС МРЦ)'"), Истина);
	ВидыНалоговыхНакладных.Добавить("НалоговыеБезНДС", НСтр("ru = 'Налоговые накладные (Без НДС)'"), Истина);
	
	//ВидыНалоговыхНакладных.Добавить("НалоговыеКомпенсирующие", НСтр("ru = 'Налоговые накладные (Компенсирующие)'"), Ложь);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьТоварыТекущейСтроки()
	
	ТекущаяСтрока = Элементы.ДеревоНН.ТекущиеДанные;
	ГруппаТовары = Элементы.ГруппаТовары;
	
	Если ТекущаяСтрока = Неопределено ИЛИ Не ЗначениеЗаполнено(ТекущаяСтрока.ИД_Строки) Тогда 
		ГруппаТовары.Видимость = Ложь;
	Иначе
		ГруппаТовары.Видимость = Истина;	
		
		Отбор = Новый Структура("ИД_Строки", ТекущаяСтрока.ИД_Строки);
		Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
		
		мСтрок = Объект.ТоварыВыгрузки.НайтиСтроки(Отбор);		
		ГруппаТовары.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Товары (%1)'; uk = 'Товари (%1)'"),
									мСтрок.Количество());
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СтраницаНН");
	МассивЭлементов.Добавить("СтраницаННМРЦ");
	МассивЭлементов.Добавить("СтраницаННБезНДС");
	МассивЭлементов.Добавить("СтраницаННКомпенсирующие");
	МассивЭлементов.Добавить("СтраницаП2кНН");
	
	МассивВидимыхЭлементов = Новый Массив;
	
	Для Каждого ВидНН Из ВидыНалоговыхНакладных Цикл 
		Если ВидНН.Пометка Тогда 
			Если ВидНН.Значение = "НалоговыеИзЗКС" Тогда 
				МассивВидимыхЭлементов.Добавить("СтраницаНН");
				МассивВидимыхЭлементов.Добавить("СтраницаП2кНН");
			ИначеЕсли ВидНН.Значение = "НалоговыеИзЗКСМРЦ" Тогда 
				МассивВидимыхЭлементов.Добавить("СтраницаННМРЦ");
				МассивВидимыхЭлементов.Добавить("СтраницаП2кНН");
			ИначеЕсли ВидНН.Значение = "НалоговыеБезНДС" Тогда 
				МассивВидимыхЭлементов.Добавить("СтраницаННБезНДС");
				МассивВидимыхЭлементов.Добавить("СтраницаП2кНН");							
			ИначеЕсли ВидНН.Значение = "НалоговыеКомпенсирующие" Тогда 
				МассивВидимыхЭлементов.Добавить("СтраницаННКомпенсирующие");
			КонецЕсли;
		КонецЕсли;				
	КонецЦикла;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(
		Элементы, 
		МассивЭлементов, 
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивВидимыхЭлементов));
	
КонецПроцедуры


#КонецОбласти
 


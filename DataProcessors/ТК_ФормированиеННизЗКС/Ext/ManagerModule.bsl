
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

Функция ЗаполнитьДанныеИзЗКС(
			Знач ДеревоСтроки, 
			Знач Товары,  
			Знач ПринадлежностьКГруппамАкциза,
			Знач ОписаниеГруппАкциза,
			Знач ПараметрыОперации) Экспорт 
		
	ДанныеОрганизаций = Неопределено;
	ТекДата = ТекущаяДата();
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Организация", Справочники.Организации.ПустаяСсылка());
	Контрагент  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаНачала	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", НачалоДня(ТекДата));
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаОкончания", КонецДня(ТекДата));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппыАкциза", ПринадлежностьКГруппамАкциза);
	Запрос.УстановитьПараметр("ОписаниеГруппАкциза", ОписаниеГруппАкциза);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	                                                              
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГруппыАкциза.ГруппаТовара КАК ГруппаТовара,
	               |	ГруппыАкциза.ГруппаАкциза КАК ГруппаАкциза
	               |ПОМЕСТИТЬ ГруппыАкциза
	               |ИЗ
	               |	&ГруппыАкциза КАК ГруппыАкциза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОписаниеГруппАкциза.Порядок КАК Порядок,
	               |	ОписаниеГруппАкциза.ГруппаАкциза КАК ГруппаАкциза,
	               |	ОписаниеГруппАкциза.ПредставлениеАкциза КАК ПредставлениеАкциза,
	               |	ОписаниеГруппАкциза.КодУКТВЕД КАК КодУКТВЕД
	               |ПОМЕСТИТЬ ОписаниеАкциза
	               |ИЗ
	               |	&ОписаниеГруппАкциза КАК ОписаниеГруппАкциза
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_ЗакрытиеСменыЗКСТовары.Ссылка КАК ДокументЗКС,
	               |	НАЧАЛОПЕРИОДА(ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Организация КАК Организация,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.Артикул КАК Артикул,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура КАК Номенклатура,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.Наименование2 КАК НоменклатураНаименование2,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.КодУКТВЭД КАК НоменклатураКодУКТВЭД,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдИзмКлассификатор,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдИзмКлассификаторКод,
	               |	ТК_ЗакрытиеСменыЗКСТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_ЗакрытиеСменыЗКСТовары.СтавкаНДС.Ставка КАК чСтавкаНДС,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.КоличествоБазовое) КАК Количество,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего - ТК_ЗакрытиеСменыЗКСТовары.СуммаНДС - ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК СуммаБезНДС,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.Сумма - ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК Сумма,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего - ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК СуммаВсего,
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаСкидки) КАК СуммаСкидки,
	               |	ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога <> 0 КАК ПодакцизныйТовар
	               |ПОМЕСТИТЬ тчТовары
	               |ИЗ
	               |	Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК ТК_ЗакрытиеСменыЗКСТовары
	               |ГДЕ
	               |	ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Проведен
	               |	И ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Организация = &Организация
	               |	И ТК_ЗакрытиеСменыЗКСТовары.СтавкаНДС.Ставка <> 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТК_ЗакрытиеСменыЗКСТовары.СтавкаНДС,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Ссылка,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Организация,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.Наименование2,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.Артикул,
	               |	НАЧАЛОПЕРИОДА(ТК_ЗакрытиеСменыЗКСТовары.Ссылка.Дата, ДЕНЬ),
	               |	ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога <> 0,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.КодУКТВЭД,
	               |	ТК_ЗакрытиеСменыЗКСТовары.ХарактеристикаНоменклатуры,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
	               |	ТК_ЗакрытиеСменыЗКСТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |	ТК_ЗакрытиеСменыЗКСТовары.СтавкаНДС.Ставка
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего - ТК_ЗакрытиеСменыЗКСТовары.СуммаНДС - ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) > 0 И
	               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.КоличествоБазовое) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	тчТовары.Дата КАК Дата,
	               |	тчТовары.Организация КАК Организация,
	               |	тчТовары.ДокументЗКС КАК ДокументЗКС,
	               |	тчТовары.Артикул КАК Артикул,
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.НоменклатураНаименование2 КАК НоменклатураНаименование2,
	               |	тчТовары.НоменклатураКодУКТВЭД КАК НоменклатураКодУКТВЭД,
	               |	тчТовары.ЕдИзмКлассификатор КАК ЕдиницаИзмерения,
	               |	тчТовары.ЕдИзмКлассификаторКод КАК ЕдиницаИзмеренияКод,
	               |	тчТовары.СтавкаНДС КАК СтавкаНДС,
	               |	тчТовары.чСтавкаНДС КАК чСтавкаНДС,
	               |	СУММА(тчТовары.Количество) КАК Количество,
	               |	СУММА(тчТовары.СуммаБезНДС) КАК СуммаБезНДС,
	               |	СУММА(тчТовары.Сумма) КАК Сумма,
	               |	СУММА(тчТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(тчТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога,
	               |	СУММА(тчТовары.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(тчТовары.СуммаСкидки) КАК СуммаСкидки,
	               |	тчТовары.СуммаАкцизногоНалога <> 0 КАК ПодакцизныйТовар,
	               |	ЕСТЬNULL(ГруппыАкциза.ГруппаАкциза, ""Прочее"") КАК ГруппаАкциза
	               |ПОМЕСТИТЬ втТоварыЗКС
	               |ИЗ
	               |	тчТовары КАК тчТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ГруппыАкциза КАК ГруппыАкциза
	               |		ПО тчТовары.Номенклатура.Родитель = ГруппыАкциза.ГруппаТовара
	               |			И (тчТовары.СуммаАкцизногоНалога <> 0)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тчТовары.СтавкаНДС,
	               |	тчТовары.Номенклатура,
	               |	тчТовары.СуммаАкцизногоНалога <> 0,
	               |	ЕСТЬNULL(ГруппыАкциза.ГруппаАкциза, ""Прочее""),
	               |	тчТовары.НоменклатураНаименование2,
	               |	тчТовары.НоменклатураКодУКТВЭД,
	               |	тчТовары.ЕдИзмКлассификатор,
	               |	тчТовары.ЕдИзмКлассификаторКод,
	               |	тчТовары.Организация,
	               |	тчТовары.ДокументЗКС,
	               |	тчТовары.Дата,
	               |	тчТовары.Артикул,
	               |	тчТовары.чСтавкаНДС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_НалоговаяНакладная.Ссылка КАК ДокументНН,
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладная.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Номер КАК Номер,
	               |	ТК_НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
	               |	ТК_НалоговаяНакладная.Комментарий КАК Комментарий
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	НЕ ТК_НалоговаяНакладная.ПометкаУдаления
	               |	И ТК_НалоговаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладная.Организация = &Организация
	               |	И ТК_НалоговаяНакладная.Контрагент = &Контрагент
	               |	И ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяОбыкновенняПродажа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТоварыЗКС.Дата КАК Дата,
	               |	втТоварыЗКС.Организация КАК Организация,
	               |	ЕСТЬNULL(ОписаниеАкциза.ПредставлениеАкциза, ""Скретч-карта"") КАК ПредставлениеАкциза,
	               |	ЕСТЬNULL(ОписаниеАкциза.КодУКТВЕД, ""4911"") КАК КодУКТВЭД,
	               |	ЕСТЬNULL(ОписаниеАкциза.Порядок, 0) КАК Порядок,
	               |	СУММА(втТоварыЗКС.СуммаБезНДС) КАК СуммаБезНДС,
	               |	СУММА(втТоварыЗКС.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(втТоварыЗКС.СуммаВсего) КАК Сумма,
	               |	""шт"" КАК ЕдиницаИзмерения,
	               |	""2009"" КАК КодЕИ,
	               |	СУММА(втТоварыЗКС.Количество) КАК Количество,
	               |	""4911"" КАК Артикул,
	               |	втТоварыЗКС.ГруппаАкциза КАК ГруппаАкциза
	               |ИЗ
	               |	втТоварыЗКС КАК втТоварыЗКС
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ОписаниеАкциза КАК ОписаниеАкциза
	               |		ПО втТоварыЗКС.ГруппаАкциза = ОписаниеАкциза.ГруппаАкциза
	               |ГДЕ
	               |	втТоварыЗКС.ГруппаАкциза = ""Прочее""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТоварыЗКС.Организация,
	               |	втТоварыЗКС.Дата,
	               |	ЕСТЬNULL(ОписаниеАкциза.ПредставлениеАкциза, ""Скретч-карта""),
	               |	ЕСТЬNULL(ОписаниеАкциза.КодУКТВЕД, ""4911""),
	               |	ЕСТЬNULL(ОписаниеАкциза.Порядок, 0),
	               |	втТоварыЗКС.ГруппаАкциза
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	втТоварыЗКС.Дата,
	               |	втТоварыЗКС.Организация,
	               |	втТоварыЗКС.НоменклатураНаименование2,
	               |	втТоварыЗКС.НоменклатураКодУКТВЭД,
	               |	ЕСТЬNULL(ОписаниеАкциза.Порядок, 100),
	               |	СУММА(втТоварыЗКС.СуммаБезНДС),
	               |	СУММА(втТоварыЗКС.СуммаНДС),
	               |	СУММА(втТоварыЗКС.СуммаВсего),
	               |	втТоварыЗКС.ЕдиницаИзмерения,
	               |	втТоварыЗКС.ЕдиницаИзмеренияКод,
	               |	СУММА(втТоварыЗКС.Количество),
	               |	втТоварыЗКС.Артикул,
	               |	втТоварыЗКС.ГруппаАкциза
	               |ИЗ
	               |	втТоварыЗКС КАК втТоварыЗКС
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ОписаниеАкциза КАК ОписаниеАкциза
	               |		ПО втТоварыЗКС.ГруппаАкциза = ОписаниеАкциза.ГруппаАкциза
	               |ГДЕ
	               |	втТоварыЗКС.ГруппаАкциза <> ""Прочее""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТоварыЗКС.Организация,
	               |	втТоварыЗКС.Дата,
	               |	втТоварыЗКС.НоменклатураНаименование2,
	               |	втТоварыЗКС.НоменклатураКодУКТВЭД,
	               |	втТоварыЗКС.Артикул,
	               |	ЕСТЬNULL(ОписаниеАкциза.Порядок, 100),
	               |	втТоварыЗКС.ЕдиницаИзмерения,
	               |	втТоварыЗКС.ЕдиницаИзмеренияКод,
	               |	втТоварыЗКС.ГруппаАкциза
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Организация,
	               |	Дата,
	               |	Порядок
	               |ИТОГИ
	               |	СУММА(СуммаБезНДС),
	               |	СУММА(СуммаНДС),
	               |	СУММА(Сумма),
	               |	СУММА(Количество)
	               |ПО
	               |	Организация,
	               |	Дата";
				   	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КолВоРез = МассивРезультатов.ВГраница();
	
	ТаблицаНН = МассивРезультатов[КолВоРез-1].Выгрузить();
	ВыборкаОрганизация = МассивРезультатов[КолВоРез].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//ВыборкаТоварыНН = МассивРезультатов[КолВоРез].Выбрать();	
		
	Пока ВыборкаОрганизация.Следующий() Цикл 
		СтрОрганизация = ДеревоСтроки.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрОрганизация, ВыборкаОрганизация, "Организация, СуммаБезНДС, СуммаНДС, Сумма");
		
		ВыборкаДата = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДата.Следующий() Цикл 
			ИД_Строки = Новый УникальныйИдентификатор;
			СтрДата = СтрОрганизация.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрДата, ВыборкаДата, "Дата, Организация, СуммаБезНДС, СуммаНДС, Сумма");
			СтрДата.ИД_Строки = ИД_Строки;
			
			ВыборкаТовары = ВыборкаДата.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл 
				СтрТовары = Товары.Добавить();				
				ЗаполнитьЗначенияСвойств(СтрТовары, ВыборкаТовары, "ПредставлениеАкциза, КодУКТВЭД, Порядок, СуммаБезНДС, СуммаНДС, Сумма, ГруппаАкциза, Артикул, Количество, КодЕИ, ЕдиницаИзмерения");
				СтрТовары.Ставка = 20;
				СтрТовары.ИД_Строки = ИД_Строки;
			КонецЦикла;
			
			ОтборТоварыНН = Новый Структура("Дата, Организация", ВыборкаДата.Дата, ВыборкаОрганизация.Организация);					
			
			мНалоговые = ТаблицаНН.НайтиСтроки(ОтборТоварыНН);
			Если мНалоговые.Количество() > 0 Тогда 
				текНН = мНалоговые[0];
				СтрДата.ДокументНН = текНН.ДокументНН;
				СтрДата.НомерНакладной = ПолучитьНомерДокумента(ВыборкаДата.Дата, текНН.НомерДляБухгалтерии);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ДеревоЗначений", ДеревоСтроки);
	Результат.Вставить("Товары", Товары);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьВыгруженныеФайлы(
			ДеревоНалоговые, 
			Знач Товары, 
			Знач ПараметрыОперации) Экспорт 
			
	ДанныеОрганизаций = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДанныеОрганизаций", Неопределено);			
	Контрагент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", КонецДня(ТекущаяДата()));
	ГУИД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ГУИД", Неопределено);
	
	МассивФайлов = Новый Массив;                               
	МассивДанных = Новый Массив;
	
	Для Каждого СтрОрганизация Из ДеревоНалоговые.Строки Цикл 
		Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 
			
			СтруктураОтбор = Новый Структура("ИД_Строки", ИтогиДня.ИД_Строки);
			
			Если ИтогиДня.ДокументНН.Пустая() Тогда 
				СоздатьДокументНН(ИтогиДня, Контрагент);
			КонецЕсли;
			
			РеквизитыОрганизации = ПолучитьРеквизитыОрганизации(ИтогиДня.Дата, ИтогиДня.Организация, ДанныеОрганизаций, ДатаОкончания);
			мТовары = Товары.НайтиСтроки(СтруктураОтбор);
			ОписаниеФайла = ВыгрузитьВXMLНаСервере(ИтогиДня, мТовары, РеквизитыОрганизации);			
			
			МассивДанных.Добавить(ОписаниеФайла);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СтруктураФайла Из МассивДанных Цикл 
		ОписаниеФайла = Новый ОписаниеПередаваемогоФайла;
		ОписаниеФайла.Имя = СтруктураФайла.Имя;
		ОписаниеФайла.Хранение = ПоместитьВоВременноеХранилище(СтруктураФайла.ДвоичныеДанные, ГУИД);
		
		МассивФайлов.Добавить(ОписаниеФайла);
	КонецЦикла;
		
	Возврат МассивФайлов;
КонецФункции

#КонецОбласти
	
		    
#Область СлужебныеПроцедурыИФункции

Процедура СоздатьДокументНН(ДанныеДня, Контрагент);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Дата", КонецДня(ДанныеДня.Дата));
	ДанныеЗаполнения.Вставить("Организация", ДанныеДня.Организация);
	ДанныеЗаполнения.Вставить("Контрагент", Контрагент);
	ДанныеЗаполнения.Вставить("Комментарий", "Документ создан обработкой ""Формирование НН из ЗКС"" на дату " + Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy"));
	ДанныеЗаполнения.Вставить("ХозОперация", Справочники.ХозОперации.НалоговаяНакладнаяОбыкновенняПродажа);
	
	Док = Документы.ТК_НалоговаяНакладная.СоздатьДокумент();
	Док.Заполнить(ДанныеЗаполнения);
	
	СписокВидовДоговора = Новый СписокЗначений;
	СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.Продажа"));
	
	Док.ДоговорВзаиморасчетов = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Док.Контрагент, Док.Организация, Док.ПодразделениеКомпании, СписокВидовДоговора, , Док.Дата);
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ДанныеДня.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДанныеДня.Дата));
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	СУММА(тчТовары.Количество) КАК КоличествоБазовое,
	               |	тчТовары.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(тчТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(тчТовары.Сумма) КАК Сумма,
	               |	СУММА(тчТовары.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(тчТовары.Сумма - тчТовары.СуммаНДС - тчТовары.СуммаАкцизногоНалога) КАК СуммаБезНалогов
	               |ИЗ
	               |	Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК тчТовары
	               |ГДЕ
	               |	тчТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И тчТовары.Ссылка.Проведен
	               |	И тчТовары.Ссылка.Организация = &Организация
	               |	И тчТовары.СтавкаНДС.Ставка <> 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тчТовары.СтавкаНДС,
	               |	тчТовары.Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(тчТовары.Сумма) > 0";
	
	тчТовары = Запрос.Выполнить().Выгрузить();
				  	
	Док.Товары.Загрузить(тчТовары);

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать, ПересчитыватьПроцентСкидки", Ложь, Истина));	
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для Каждого Стр Из Док.Товары Цикл 	
		Если Стр.Коэффициент > 0 Тогда 
			Стр.Количество = Стр.КоличествоБазовое / Стр.Коэффициент;		
		КонецЕсли;
		Если Стр.КоличествоБазовое > 0 Тогда 
			Стр.Цена = Стр.Сумма / Стр.КоличествоБазовое;
		КонецЕсли;
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
	Если Док.ПроверитьЗаполнение() Тогда 
		Попытка
			Док.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Док.Записать();						
		КонецПопытки;
	Иначе
		Док.Записать();
	КонецЕсли;
	
	ДанныеДня.ДокументНН = Док.Ссылка;
	ДанныеДня.НомерНакладной = ПолучитьНомерДокумента(Док.Дата, Док.НомерДляБухгалтерии);
	
	ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%2 документ %1'; uk = '%2 документ %1'"),
			Строка(Док.Ссылка),
			?(Док.Проведен, НСтр("ru = 'Проведен'; uk = 'Проведений'"), НСтр("ru = 'Записан'; uk = 'Записаний'"))),
			Док.Ссылка);
			
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ВыгрузитьВXMLНаСервере(ДанныеДня, мТовары, РеквизитыОрганизации)	
	
	ПотокФайла = Новый ПотокВПамяти();
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.ОткрытьПоток(ПотокФайла);
	
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLAR");
	
		Схема = "J1201010.xsd";
	
		ОбъектXML.ЗаписатьНачалоАтрибута("xmlns:xsi"); 
			ОбъектXML.ЗаписатьТекст("http://www.w3.org/2001/XMLSchema-instance"); 
		ОбъектXML.ЗаписатьКонецАтрибута();
		
		ОбъектXML.ЗаписатьНачалоАтрибута("xsi:noNamespaceSchemaLocation");
			ОбъектXML.ЗаписатьТекст(Схема);
		ОбъектXML.ЗаписатьКонецАтрибута();
		
		Схема = СтрЗаменить(Схема, ".xsd", "");
		
		///DECLARHEAD	
		ОбъектXML.ЗаписатьНачалоЭлемента("DECLARHEAD");		
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодПоЕДРПОУ, "Строка"), "TIN");
			ЗаписатьXML(ОбъектXML, Лев(Схема, 3), "C_DOC");
			ЗаписатьXML(ОбъектXML, Сред(Схема, 4, 3), "C_DOC_SUB");
			ЗаписатьXML(ОбъектXML, Прав(Схема, 2), "C_DOC_VER");
			ЗаписатьXML(ОбъектXML, Прав(Схема, 1), "C_DOC_TYPE");
			ЗаписатьXML(ОбъектXML, "1242", "C_DOC_CNT");
			ЗаписатьXML(ОбъектXML, Месяц(ДанныеДня.Дата),"PERIOD_MONTH");
			ЗаписатьXML(ОбъектXML, 1,"PERIOD_TYPE");
			ЗаписатьXML(ОбъектXML, Год(ДанныеДня.Дата), "PERIOD_YEAR");
			ЗаписатьXML(ОбъектXML, 1,"C_DOC_STAN");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Дата, "Дата"), "D_FILL");
		ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARHEAD	
		
		///DECLARBODY	
		ОбъектXML.ЗаписатьНачалоЭлемента("DECLARBODY");
			ЗаписатьXML(ОбъектXML, "1", "HORIG1");	
			ЗаписатьXML(ОбъектXML, "11", "HTYPR");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Дата, "Дата"), "HFILL");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.НомерНакладной), "HNUM");	//номер документа
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.НаименованиеПолное, "Строка"), "HNAMESEL");	
			ЗаписатьXML(ОбъектXML, "Неплатник", "HNAMEBUY");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.ИНН, "Строка"), "HKSEL");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодПоЕДРПОУ, "Строка"), "HTINSEL");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодИсточникаНалоговогоНомера, "Число"), "HKS");
			ЗаписатьXML(ОбъектXML, "100000000000", "HKBUY");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Сумма, "Число"), "R04G11");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.СуммаНДС, "Число"), "R03G11");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.СуммаНДС, "Число"), "R03G7");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.СуммаБезНДС, "Число"), "R01G7");	
			
			СоответствиеКолонок = Новый Соответствие;
			СоответствиеКолонок.Вставить("RXXXXG3S",	"ПредставлениеАкциза");
			СоответствиеКолонок.Вставить("RXXXXG4", 	"КодУКТВЭД");
			СоответствиеКолонок.Вставить("RXXXXG008", 	"СтавкаНДС");
			СоответствиеКолонок.Вставить("RXXXXG010", 	"СуммаБезНДС");
			СоответствиеКолонок.Вставить("RXXXXG11_10", "СуммаНДС");
			//Новые
			СоответствиеКолонок.Вставить("RXXXXG5", 	"Объем");
			СоответствиеКолонок.Вставить("RXXXXG6", 	"ЦенаБез");
			СоответствиеКолонок.Вставить("RXXXXG4S", 	"ЕдиницаИзмерения");
			СоответствиеКолонок.Вставить("RXXXXG105_2S","КодЕИ");			
			
			СтруктураЕдИзм = Новый Структура;
			СтруктураЕдИзм.Вставить("шт", "2009");
			СтруктураЕдИзм.Вставить("пляш", "2061");
			СтруктураЕдИзм.Вставить("пач", "2112");
			
			Для Каждого Параметр Из СоответствиеКолонок Цикл
				Номер = 0;
				Для Каждого Стр Из мТовары Цикл					
					Значение = Неопределено;
					
					Номер = Номер + 1;
					
					Если Параметр.Значение = "КодУКТВЭД" Тогда
						Значение = СтрЗаменить(Стр.КодУКТВЭД, " ", ""); 
					ИначеЕсли Параметр.Значение = "СтавкаНДС" Тогда
						Значение = Строка(Формат(Стр.Ставка, "ЧДЦ=0"));
					ИначеЕсли Параметр.Значение = "Объем" Тогда
						Значение = Стр.Количество;
					ИначеЕсли Параметр.Значение = "ЦенаБез" Тогда
						Значение = Формат((Стр.СуммаБезНДС/Стр.Количество),"ЧДЦ=6; ЧГ=0");						
					Иначе
						Значение = Стр[Параметр.Значение];
					КонецЕсли;
										
					Значение = ФорматВыгрузки(Значение);
										
					ОбъектXML.ЗаписатьНачалоЭлемента(Параметр.Ключ);											
						ОбъектXML.ЗаписатьАтрибут("ROWNUM", ФорматВыгрузки(Номер, "Число"));
						ОбъектXML.ЗаписатьТекст(Строка(Значение));						
					ОбъектXML.ЗаписатьКонецЭлемента();
				КонецЦикла;
			КонецЦикла;			
			
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.Руководитель), "HBOS");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.РуководительКод), "HKBOS");	
			
		ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARBODY	
	ОбъектXML.ЗаписатьКонецЭлемента(); //DECLAR
	
	ОбъектXML.Закрыть();	
	
		
	ИмяФайла = СокрЛП(Строка(ДанныеДня.Организация)) + "_" + Формат(ДанныеДня.Дата, "ДФ=yyyyMMdd") + "_НН.xml"; 	
	ДвоичныеДанные = ПотокФайла.ЗакрытьИПолучитьДвоичныеДанные();
	
	//ОписаниеФайла = Новый ОписаниеПередаваемогоФайла;
	//ОписаниеФайла.Имя = ИмяФайла;
	//ОписаниеФайла.Хранение = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Имя", ИмяФайла);
	ОписаниеФайла.Вставить("ДвоичныеДанные", ДвоичныеДанные);
	
	Возврат ОписаниеФайла;
	
КонецФункции

#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьНомерДокумента(Дата, НомерДляБухгалтерии)
	ТекНомерНН = "";
	ТекНомерФилиала = "";
	МесячныйПрефиксВНомереДляБух = ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата);
	ТекНомерННСФилиалом = ?(Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) = 0, НомерДляБухгалтерии, Прав(НомерДляБухгалтерии, СтрДлина(НомерДляБухгалтерии) - Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) - 1)); 

	ПозицияСлеша = Найти(ТекНомерННСФилиалом, "/");
	Если ПозицияСлеша = 0 Тогда
		ТекНомерНН = ТекНомерННСФилиалом;
	Иначе
		ТекНомерНН = Лев(ТекНомерННСФилиалом, ПозицияСлеша - 1);
		ТекНомерФилиала = Сред(ТекНомерННСФилиалом, ПозицияСлеша + 1, СтрДлина(ТекНомерННСФилиалом)-ПозицияСлеша);			
	КонецЕсли;
	
	Возврат ТекНомерНН;
КонецФункции

Функция ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата)

	НомерМесяца = Месяц(Дата);
	Индекс = НомерМесяца*3 - 2;
	
	Возврат Сред("Ян|Фв|Мр|Ап|Ма|Ин|Ил|Ав|Сн|Ок|Но|Дк", Индекс, 2);	

КонецФункции

Функция ПолучитьРеквизитыОрганизации(ТекДата, текОрганизация, ДанныеОрганизаций, ДатаОкончания);
	
	Если ТипЗнч(ДанныеОрганизаций) <> Тип("Структура") Тогда 
		ДанныеОрганизаций = Новый Структура;
	КонецЕсли;
	
	ИД_Организации = "Орг_" + СтрЗаменить(Строка(текОрганизация.УникальныйИдентификатор()), "-", "_");
	
	СохранитьДанные = Ложь;	
	СтруктураРеквизитов = Неопределено;
	СтрокаПараметровИНН = "ПлательщикЕН, ПлательщикНДС, ИНН, НомерСвидетельства, ПлательщикНалогаНаПрибыль";
	СтрокаПараметровРуководители = "Руководитель, РуководительКод";
	
	Если Не ДанныеОрганизаций.Свойство(ИД_Организации, СтруктураРеквизитов) ИЛИ СтруктураРеквизитов = Неопределено Тогда 
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", текОрганизация);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	Организации.НаименованиеПолное КАК НаименованиеПолное,
		               |	Организации.ЮрФизЛицо КАК ЮрФизЛицо,
		               |	Организации.КодПоЕДРПОУ КАК КодПоЕДРПОУ
		               |ИЗ
		               |	Справочник.Организации КАК Организации
		               |ГДЕ
		               |	Организации.Ссылка = &Организация
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УчетнаяПолитикаОрганизаций.Период КАК Период,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикЕН КАК ПлательщикЕН,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикНДС КАК ПлательщикНДС,
		               |	УчетнаяПолитикаОрганизаций.ИННПлательщикаНДС КАК ИНН,
		               |	УчетнаяПолитикаОрганизаций.НомерСвидетельстваПлательщикаНДС КАК НомерСвидетельства,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль
		               |ИЗ
		               |	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		               |ГДЕ
		               |	УчетнаяПолитикаОрганизаций.Организация = &Организация
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОтветственныеЛицаОрганизаций.Период КАК Период,
		               |	ОтветственныеЛицаОрганизаций.ФизическоеЛицо.Представление КАК Руководитель,
		               |	ОтветственныеЛицаОрганизаций.ФизическоеЛицо.Код КАК РуководительКод
		               |ИЗ
		               |	РегистрСведений.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
		               |ГДЕ
		               |	ОтветственныеЛицаОрганизаций.СтруктурнаяЕдиница = &Организация
		               |	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период";
		
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Реквизиты = МассивРезультатов[0].Выбрать();
		ВыборкаИНН = МассивРезультатов[1].Выбрать();
		ВыборкаРуководители = МассивРезультатов[2].Выбрать();
						
		Реквизиты.Следующий();
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("НаименованиеПолное", Реквизиты.НаименованиеПолное);
		СтруктураРеквизитов.Вставить("ЮрЛицо", Реквизиты.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
		СтруктураРеквизитов.Вставить("КодПоЕДРПОУ", Реквизиты.КодПоЕДРПОУ);		
		
		ДатаОкончания = ДобавитьМесяц(ДатаОкончания, 1);
		
		МассивПериодовИНН = Новый Массив;
		ЗаполнитьПараметрыРекурсивно(ВыборкаИНН, СтрокаПараметровИНН, МассивПериодовИНН, ДатаОкончания);
		СтруктураРеквизитов.Вставить("МассивПериодовИНН", МассивПериодовИНН);
		
		МассивРуководителей = Новый Массив;		
		ЗаполнитьПараметрыРекурсивно(ВыборкаРуководители, СтрокаПараметровРуководители, МассивРуководителей, ДатаОкончания);
		СтруктураРеквизитов.Вставить("МассивРуководителей", МассивРуководителей);
		
		УстановитьПривилегированныйРежим(Ложь);	
		
		ДанныеОрганизаций.Вставить(ИД_Организации, СтруктураРеквизитов);	
	КонецЕсли;
	
	Если Не (СтруктураРеквизитов.Свойство("ТекПериодИНН") 
		И СтруктураРеквизитов.ТекПериодИНН.ДатаНачало <= ТекДата
		И СтруктураРеквизитов.ТекПериодИНН.ДатаКонец > ТекДата) Тогда 
		
		СтруктураИНН = Новый Структура(СтрокаПараметровИНН);
		Для Каждого ПериодИНН Из СтруктураРеквизитов.МассивПериодовИНН Цикл 
			Если ПериодИНН.ДатаНачало <= ТекДата И ПериодИНН.ДатаКонец > ТекДата Тогда 
				ЗаполнитьЗначенияСвойств(СтруктураИНН, ПериодИНН, СтрокаПараметровИНН);
				СтруктураИНН.Вставить("ТекПериодИНН", Новый Структура("ДатаНачало, ДатаКонец", ПериодИНН.ДатаНачало, ПериодИНН.ДатаКонец));
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		СтруктураИНН.Вставить("КодИсточникаНалоговогоНомера", Документы.ТК_НалоговаяНакладная.КодИсточникаНалоговогоНомера(СтруктураРеквизитов.КодПоЕДРПОУ, СтруктураРеквизитов.ЮрЛицо));
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, СтруктураИНН, Истина);
	КонецЕсли;
	
	Если Не (СтруктураРеквизитов.Свойство("ТекПериодРуководитель")
		И СтруктураРеквизитов.ТекПериодРуководитель.ДатаНачало <= ТекДата
		И СтруктураРеквизитов.ТекПериодРуководитель.ДатаКонец > ТекДата) Тогда 
		
		СтруктураРуководитель = Новый Структура(СтрокаПараметровРуководители);
		Для Каждого ПериодРуководитель Из СтруктураРеквизитов.МассивРуководителей Цикл 
			Если ПериодРуководитель.ДатаНачало <= ТекДата И ПериодРуководитель.ДатаКонец > ТекДата Тогда 
				ЗаполнитьЗначенияСвойств(СтруктураРуководитель, ПериодРуководитель, СтрокаПараметровРуководители);
				СтруктураРуководитель.Вставить("ТекПериодРуководитель", Новый Структура("ДатаНачало, ДатаКонец", ПериодРуководитель.ДатаНачало, ПериодРуководитель.ДатаКонец)); 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, СтруктураРуководитель, Истина);
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
КонецФункции

Функция ЗаполнитьПараметрыРекурсивно(Выборка, Знач СтрокаПараметров, Массив, Знач ДатаОкончания)	
	
	Если Выборка.Следующий() Тогда  
		СтруктураПараметров = Новый Структура(СтрокаПараметров);			
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка, СтрокаПараметров);
		СтруктураПараметров.Вставить("ДатаНачало", НачалоДня(Выборка.Период));
		СтруктураПараметров.Вставить("ДатаКонец", ЗаполнитьПараметрыРекурсивно(Выборка, СтрокаПараметров, Массив, ДатаОкончания));
		
		Массив.Добавить(СтруктураПараметров);
		
		ДатаОкончания = СтруктураПараметров.ДатаНачало-1;
	КонецЕсли;
	
	Возврат ДатаОкончания;
КонецФункции

Функция ФорматВыгрузки(Значение, Тип = Неопределено)
	ОтформатированноеЗначение = Неопределено;
	
	Если Тип = "Дата" Тогда
		ОтформатированноеЗначение = Формат(Значение, "ДФ=ddMMyyyy");		
	ИначеЕсли Тип = "Число" Тогда
		ОтформатированноеЗначение = Формат(Значение, "ЧРД=.; ЧГ=0");	
	ИначеЕсли Тип = "Строка" Тогда
		ОтформатированноеЗначение = СокрЛП(Строка(Значение));
	ИначеЕсли Тип = Неопределено Тогда
		ФорматЗнч = ТипЗнч(Значение);
		ОтформатированноеЗначение = ФорматВыгрузки(Значение, Строка(ФорматЗнч));
	Иначе
		ОтформатированноеЗначение = Значение;
	КонецЕсли;
	
	Возврат ОтформатированноеЗначение;
КонецФункции


#КонецОбласти



#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

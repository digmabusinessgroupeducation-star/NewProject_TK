

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контрагент = Справочники.Контрагенты.НайтиПоКоду("D100434");
	ЗаполнитьГруппыАкциза();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОтразитьТоварыТекущейСтроки();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТекущееДействие = "Выгрузить" Тогда 
		
		ПроверяемыеРеквизиты.Добавить("КаталогВыгрузки");
		
	ИначеЕсли ТекущееДействие = "Сформировать" Тогда 
		
		ПроверяемыеРеквизиты.Добавить("Организация");				
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки = ПолучитьТекущиеНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ЗагрузитьНастройкиНаСервере(Настройки);	
	Настройки = Неопределено;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКаталогаДокументовЗавершение", ЭтаФорма);
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);		
	ДиалогОткрытия.Заголовок = НСтр("ru = 'Выберите каталог сохранения .XML файлов'; uk = 'Оберіть каталог збереження .XML файлів'");
	ДиалогОткрытия.Каталог = КаталогВыгрузки;
	
	ДиалогОткрытия.Показать(ОписаниеОповещения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаДокументовЗавершение(ИмяКаталога, ДопПараметры) Экспорт 
	
	Если ТипЗнч(ИмяКаталога) = Тип("Массив") И ИмяКаталога.Количество() > 0 Тогда 
		КаталогВыгрузки = ИмяКаталога[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоННПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОтразитьТоварыТекущейСтроки", 0.3, Истина);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиСохранить(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗагрузить(Команда)

	ОчиститьСообщения();
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("НастройкиЗагрузитьПродолжение", ЭтаФорма);
	
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);		
	ДиалогОткрытия.Заголовок = "Выберите xml-файл для сохранения";
	ДиалогОткрытия.Фильтр = "Файл настроек (*.xml)|*.xml";
	ДиалогОткрытия.МножественныйВыбор = Ложь;
	
	ДиалогОткрытия.Показать(ОповещениеЗавершения);	
		
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗагрузитьПродолжение(ВыбранныеФайлы, ДопПараметры) Экспорт 

	Если ВыбранныеФайлы = Неопределено ИЛИ ВыбранныеФайлы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;                        
	
	ИмяФайла = ВыбранныеФайлы[0];
	
	ТекстXML = Новый ТекстовыйДокумент;
	ТекстXML.Прочитать(ИмяФайла);
	
	ПолучитьНастройкиНаСервере(ТекстXML.ПолучитьТекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	Элементы.СтраницыОсновные.ТекущаяСтраница = Элементы.СтраницаНастройки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)

	Элементы.СтраницыОсновные.ТекущаяСтраница = Элементы.СтраницаДанные;	
	
КонецПроцедуры


&НаКлиенте
Процедура Сформировать(Команда)
	
	ОчиститьСообщения();
	
	ТекущееДействие = "Сформировать";
	
	Если ПроверитьЗаполнение() Тогда
		ЗапуститьОперацию();
	КонецЕсли;
	
	ТекущееДействие = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьННвXML(Команда)
	
	ОчиститьСообщения();
		
	ТекущееДействие = "Выгрузить";
	
	Если ПроверитьЗаполнение() И ДеревоНН.ПолучитьЭлементы().Количество() > 0 Тогда 
		//ЗапуститьОперацию();
		СохранитьФайлыНН();
	КонецЕсли;
	
	ТекущееДействие = "";	
	
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьОперацию()
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ФормированиеННизЗКС";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	

	Если ТекущееДействие = "Сформировать" Тогда 
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Формирование списка товаров по документам ЗКС'; uk = 'Формування списку товарів по документам ЗКС'"); 		
	ИначеЕсли ТекущееДействие = "Выгрузить" Тогда 
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка налоговых накладных в XML'; uk = 'Вивантаження податкових накладних в XML'"); 		
	Иначе
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьВыполнениеОперации();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, ТекущееДействие);		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);	
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеОперации()
	
	Перем ДлительнаяОперация;
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыОперации.Вставить("ДатаОкончания", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("Контрагент", Контрагент);
	ПараметрыОперации.Вставить("КаталогВыгрузки", КаталогВыгрузки);

	ДеревоЗначений = РеквизитФормыВЗначение("ДеревоНН", Тип("ДеревоЗначений"));		
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Если ТекущееДействие = "Сформировать" Тогда 
		
		ДеревоНН.ПолучитьЭлементы().Очистить();
		
		ДеревоЗначений.Строки.Очистить();
		Объект.ТоварыВыгрузки.Очистить();
				
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
								ПараметрыВыполнения, 
								"Обработки.ТК_ФормированиеННизЗКС.ЗаполнитьДанныеИзЗКС",
								ДеревоЗначений, 
								Объект.ТоварыВыгрузки.Выгрузить(), 								
								ПринадлежностьКГруппамАкциза.Выгрузить(), 
								ОписаниеГруппАкциза.Выгрузить(), 
								ПараметрыОперации);
		
	ИначеЕсли ТекущееДействие = "Выгрузить" Тогда 
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
								ПараметрыВыполнения, 
								"Обработки.ТК_ФормированиеННизЗКС.ПолучитьВыгруженныеФайлы",
								ДеревоЗначений, 
								Объект.ТоварыВыгрузки.Выгрузить(), 
								ПараметрыОперации);
																																
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, Знач ВидОперации) Экспорт
		
	ОписаниеРезультата = ОбработатьРезультатОперации(Операция);
	
	ЕстьОшибки = ОписаниеРезультата.ЕстьОшибки;
	Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Ошибки", НСтр("ru = 'Обнаружена ошибка при выполнении операции'; uk = 'Виявлена помилка при виконанні операції'"));
	ОповещениеТекст = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеТекст", НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'"));
	ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеПояснение", НСтр(""));
	Картинка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Картинка", БиблиотекаКартинок.Информация32);	
	СообщенияПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "СообщенияПользователю", Неопределено);
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
	Если Не ЕстьОшибки Тогда 
		
		Если ОписаниеРезультата.Свойство("МассивФайлов") Тогда 
			НачатьПолучениеФайловССервера(, ОписаниеРезультата.МассивФайлов, КаталогВыгрузки);
		КонецЕсли;
			
	ИначеЕсли ЕстьОшибки Тогда 		
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибки);

	Иначе
		
		Если ЕстьОшибки Тогда 
			ПоказатьПредупреждение(, Ошибки,, НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'"));	
		КонецЕсли;
	КонецЕсли;		
		
	   
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатОперации(Операция)

	ОповещениеТекст 	= "";
	ОповещениеПояснение = "";
	ЕстьОшибки 			= Ложь;
	Ошибки 				= "";
	Результат 			= Неопределено;
	Картинка			= Неопределено;
	
	ОписаниеРезультата = Новый Структура;
	
	Если Операция = Неопределено Тогда 
		
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		Картинка = БиблиотекаКартинок.Ошибка32;
		                                                  
	Иначе
		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда 		
			
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				
				Если Результат.Свойство("ДеревоЗначений") Тогда 					
					ЗначениеВРеквизитФормы(Результат.ДеревоЗначений, "ДеревоНН");					
				КонецЕсли;
				
				Если Результат.Свойство("Товары") Тогда 					
					Объект.ТоварыВыгрузки.Загрузить(Результат.Товары);
				КонецЕсли;		
								
				Если Результат.Свойство("МассивФайлов") Тогда 
					ОписаниеРезультата.Вставить("МассивФайлов", Результат.МассивФайлов);
					ОповещениеТекст = НСтр("ru = 'Выгрузка в XML'; uk = 'Вивантаження у XML'");							
					ОповещениеПояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Сохранено %1 файл(ов)'; uk = 'Збережено %1 файл(ів)'"),
											Результат.МассивФайлов.Количество());				
				КонецЕсли;

			КонецЕсли;						
												
			Картинка = БиблиотекаКартинок.Успешно32;
						
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 	
			
			ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");
			Картинка = БиблиотекаКартинок.Ошибка32;
			ЕстьОшибки = Истина;
			
			ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");				
			Ошибки = Операция.КраткоеПредставлениеОшибки;
			
		Иначе			
			
			ЕстьОшибки = Истина;
		
		КонецЕсли;
		
		Если Операция.Свойство("Сообщения") И ТипЗнч(Операция.Сообщения) = Тип("ФиксированныйМассив") Тогда 
			Для Каждого Сообщение Из Операция.Сообщения Цикл 
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщенияПользователю") Тогда 
			СообщенияПользователю = Результат.СообщенияПользователю;
		КонецЕсли;		
	КонецЕсли;
	
	//Добавил условие, так как при формировании xml  очищался массив файлов
	Если  НЕ ТипЗнч(Результат) = Тип("Структура") Тогда
		ОписаниеРезультата = Новый Структура;
	КонецЕсли;
	
	ОписаниеРезультата.Вставить("ЕстьОшибки", ЕстьОшибки);
	
	Если Не ПустаяСтрока(Ошибки) Тогда 
		ОписаниеРезультата.Вставить("Ошибки", Ошибки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Картинка) Тогда 
		ОписаниеРезультата.Вставить("Картинка", Картинка);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеТекст) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеТекст", ОповещениеТекст);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеПояснение) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеПояснение", ОповещениеПояснение);
	КонецЕсли;
	
	Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
		ОписаниеРезультата.Вставить("СообщенияПользователю", СообщенияПользователю);
	КонецЕсли;
	
	Возврат ОписаниеРезультата;		
КонецФункции


&НаКлиенте
Процедура СохранитьФайлыНН()
	
	ВсеОк = Истина;
	МассивФайлов = ПолучитьВыгруженныеФайлы();
	УдаленоФайлов = 0;
	
	Если МассивФайлов <> Неопределено И МассивФайлов.Количество() > 0 Тогда 
		НачатьПолучениеФайловССервера(, МассивФайлов, КаталогВыгрузки);	
				
		Для Каждого ОписаниеФайла Из МассивФайлов Цикл 
			Попытка
				УдалитьИзВременногоХранилища(ОписаниеФайла.Хранение);
				УдаленоФайлов = УдаленоФайлов + 1;
			Исключение
			КонецПопытки;
		КонецЦикла;		
	Иначе
		ВсеОк = Ложь;
	КонецЕсли;
	
	Если ВсеОк Тогда 
		Картинка = БиблиотекаКартинок.Успешно32;
		ОповещениеТекст = НСтр("ru = 'Успешно выгружено'; uk = 'Успішно вивантажено'");
		ОповещениеПояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Выгружено %1 файлов'; uk = 'Вивантажено %1 файлів'"),
								МассивФайлов.Количество());
	Иначе
		Картинка = БиблиотекаКартинок.Ошибка32;
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Файлы не выгружены'; uk = 'Файли не вивантажені'");		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВыгруженныеФайлы()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыОперации.Вставить("ДатаОкончания", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Организация", Организация);
	ПараметрыОперации.Вставить("Контрагент", Контрагент);
	ПараметрыОперации.Вставить("КаталогВыгрузки", КаталогВыгрузки);
	ПараметрыОперации.Вставить("ГУИД", УникальныйИдентификатор);

	ДеревоЗначений = РеквизитФормыВЗначение("ДеревоНН", Тип("ДеревоЗначений"));		
	
	Возврат Обработки.ТК_ФормированиеННизЗКС.ПолучитьВыгруженныеФайлы(ДеревоЗначений, Объект.ТоварыВыгрузки.Выгрузить(), ПараметрыОперации);
		
КонецФункции


#КонецОбласти



#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура ОтразитьТоварыТекущейСтроки()
	
	ТекущаяСтрока = Элементы.ДеревоНН.ТекущиеДанные;
	ГруппаТовары = Элементы.ГруппаТовары;
	
	Если ТекущаяСтрока = Неопределено ИЛИ Не ЗначениеЗаполнено(ТекущаяСтрока.ИД_Строки) Тогда 
		ГруппаТовары.Видимость = Ложь;
	Иначе
		ГруппаТовары.Видимость = Истина;	
		
		Отбор = Новый Структура("ИД_Строки", ТекущаяСтрока.ИД_Строки);
		Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
		
		мСтрок = Объект.ТоварыВыгрузки.НайтиСтроки(Отбор);		
		ГруппаТовары.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Товары (%1)'; uk = 'Товари (%1)'"),
									мСтрок.Количество());
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущиеНастройки()
	
	СжатиеДанных = Новый СжатиеДанных(9);
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ГруппыАкциза", ГруппыАкциза);
	СтруктураНастроек.Вставить("ПринадлежностьКГруппамАкциза", Новый ХранилищеЗначения(ПринадлежностьКГруппамАкциза.Выгрузить(), СжатиеДанных)); 
	СтруктураНастроек.Вставить("Организация", Организация);
	СтруктураНастроек.Вставить("КаталогДанных", КаталогВыгрузки);
	СтруктураНастроек.Вставить("ОписаниеГруппАкциза", Новый ХранилищеЗначения(ПринадлежностьКГруппамАкциза.Выгрузить(), СжатиеДанных));
	
	Возврат СтруктураНастроек;
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиНаСервере(Настройки)
	
	Если ТипЗнч(Настройки) <> Тип("Структура") Тогда 
		Возврат;
	КонецЕсли;
	
	МассивРеквизитов = ЭтаФорма.ПолучитьРеквизиты();
	СтруктураРеквизитов = Новый Структура;
	Для Каждого Стр Из МассивРеквизитов Цикл 
		СтруктураРеквизитов.Вставить(Стр.Имя);
	КонецЦикла;
	
	Для Каждого стрНастройка Из Настройки Цикл 
		ИмяРеквизита = стрНастройка.Ключ;						
		
		Если Не СтруктураРеквизитов.Свойство(ИмяРеквизита) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ИмяРеквизита = "ПринадлежностьКГруппамАкциза" 
			ИЛИ ИмяРеквизита = "ОписаниеГруппАкциза" Тогда 
			
			ТипРеквизита = ТипЗнч(стрНастройка.Значение);
			Если ТипРеквизита = Тип("ТаблицаЗначений") Тогда 
				Таблица = стрНастройка.Значение;
			ИначеЕсли ТипРеквизита = Тип("ХранилищеЗначения") Тогда 
				Таблица = стрНастройка.Значение.Получить();
			Иначе
				Продолжить;
			КонецЕсли;
			
			ЭтаФорма[ИмяРеквизита].Загрузить(Таблица);
		Иначе
			ЭтаФорма[ИмяРеквизита] = стрНастройка.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройкиНаСервере(ТекстXML)
	
	Попытка		
		Чтение = Новый ЧтениеXML;
		Чтение.УстановитьСтроку(ТекстXML);
		
		Настройки = СериализаторXDTO.ПрочитатьXML(Чтение); 
		ЗагрузитьНастройкиНаСервере(Настройки);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(			
				НСтр("ru = 'Не удалось загрузить настройки из выбранного файла!
                    |%1'; uk = 'Не вдалося завантажити налаштування із обраного файлу!
                    |%1'"),
				ОписаниеОшибки()));
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГруппыАкциза()
	
	МассивАкцизов = Новый Массив;
	МассивАкцизов.Добавить("Алкоголь");
	МассивАкцизов.Добавить("Вино");
	МассивАкцизов.Добавить("Пиво");
	МассивАкцизов.Добавить("Табак");
	МассивАкцизов.Добавить("Прочее");
	
	ГруппыАкциза.ЗагрузитьЗначения(МассивАкцизов);
	Элементы.ПринадлежностьКГруппамАкцизаГруппаАкциза.СписокВыбора.ЗагрузитьЗначения(МассивАкцизов);
	Элементы.ОписаниеГруппАкцизаГруппаАкциза.СписокВыбора.ЗагрузитьЗначения(МассивАкцизов);

КонецПроцедуры


#КонецОбласти
 


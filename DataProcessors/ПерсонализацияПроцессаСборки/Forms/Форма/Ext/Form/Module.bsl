

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Инициализация
	текСтатус = "ОжиданиеСканирования";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");								
		//ОповещениеПриПодключении = НСтр("ru = 'Отсканируйте накладную'; uk = 'Отскануйте накладну'");
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекКод = Параметр[0];
			Иначе
				ТекКод = Параметр[1][1];
			КонецЕсли;
			
			ОбработатьПолученныйШК(ТекКод);
			Возврат;
			
		ИначеЕсли ИмяСобытия = "ScanDataBase64" Тогда
			
			ШтрихкодBase64 = "";
			СимволыШтрихкодBase64 = Base64Значение(Параметр[0]); // Декодируем строку
			// Преобразуем в строку символов
			МассивСимволов = СтрРазделить(СимволыШтрихкодBase64, " ");
			Для Каждого СимволШтрихкода Из МассивСимволов Цикл
				Если СимволШтрихкода = "1D" Тогда // Код символа GS в HEX
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
				Иначе
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияКлиентСервер.ПреобразоватьHEXВСтроку(СимволШтрихкода);
				КонецЕсли;
			КонецЦикла;
			ОбработатьПолученныйШК(ШтрихкодBase64);
			Возврат;

			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(, УникальныйИдентификатор, ПоддерживаемыеТипыВО);	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры


#КонецОбласти

&НаКлиенте
Процедура УстановитьСтраницуПоСтатусу()
	
	ИзменитьДоступностьСтраниц = Ложь;
	ИмяТекущейСтраницы = "";
	
	стрСканировки 			= Элементы.СтраницаСканировки;
	стрНакладнаяВыдана 	    = Элементы.СтраницаНакладнаяВыдана;
	
	Если текСтатус = "ОжиданиеСканирования" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрСканировки Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрСканировки;
			
			ИмяТекущейСтраницы = стрСканировки.Имя;
			ИзменитьДоступностьСтраниц = Истина;
		КонецЕсли;
	ИначеЕсли текСтатус = "НакладнаяВыдана" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрНакладнаяВыдана Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрНакладнаяВыдана;
			
			ИмяТекущейСтраницы = стрНакладнаяВыдана.Имя;
			ИзменитьДоступностьСтраниц = Истина;
		КонецЕсли;
	КонецЕсли;

	Если ИзменитьДоступностьСтраниц И Не ПустаяСтрока(ИмяТекущейСтраницы) Тогда 
		МассивВсехСтраниц = Новый Массив;
		МассивВсехСтраниц.Добавить("СтраницаСканировки");
		МассивВсехСтраниц.Добавить("СтраницаНакладнаяВыдана");
		
		МассивВидимыхСтраниц = Новый Массив;
		МассивВидимыхСтраниц.Добавить(ИмяТекущейСтраницы);
		
		ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехСтраниц, МассивВидимыхСтраниц);
	КонецЕсли;
	
КонецПроцедуры


#Область ОбработкаОшибокСканирования

&НаКлиенте
Процедура ПоказатьОшибкуСканировния()		
	Если ПустаяСтрока(ОшибкаСканирования) Тогда 
		Возврат;
	КонецЕсли;	
			
	Элементы.ДекорацияНадписьОшибкаСканирования.Заголовок = ОшибкаСканирования;
	Элементы.ГруппаОшибкиСканирования.Видимость = Истина;
	
	текСтатус = "ОжиданиеСканирования";
	УстановитьСтраницуПоСтатусу();
	
	ПодключитьОбработчикОжидания("ОчиститьОшибкиСканирования", 5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОшибкиСканирования()	
	ГруппаОшибкиСканирования = Элементы.ГруппаОшибкиСканирования;
	
	Если ГруппаОшибкиСканирования.Видимость Тогда 
		ГруппаОшибкиСканирования.Видимость = Ложь;
	КонецЕсли;
	
	ОшибкаСканирования = "";	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаТекСтраницу()	
	текСтатус = "ОжиданиеСканирования";
	УстановитьСтраницуПоСтатусу();
	
	ОшибкаСканирования = "";	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбработатьПолученныйШК(ТекКод)
	

	ДанныеСканирования = ПроверитьИД(СокрЛП(ТекКод));
	
	Если текСтатус = "ОжиданиеСканирования" Тогда 		
		
		Печать = ОбработатьПоискНакладной(ДанныеСканирования);		
		
		Если Печать Тогда 
			ПечатьНаПринтер();
			ПоказатьОшибкуСканировния();
		Иначе
			УстановитьСтраницуПоСтатусу();
			ПоказатьОшибкуСканировния();
			Если текСтатус = "НакладнаяВыдана" Тогда 
				ПодключитьОбработчикОжидания("ПерейтиНаТекСтраницу", 5, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ОбработатьПоискНакладной(Код)
		
	УстановитьПривилегированныйРежим(Истина);

	текДействие = "Сканирование документа";
	
	текСотрудник = НайтиСотрудника(Код);
	Если Не текСотрудник Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НакладнаяВыдана = ПроверитьСборкуНакладной(Код);
	Если НЕ ОшибкаСканирования="" Тогда
		Возврат Ложь;
	КонецЕсли;	
	   
	Если НЕ НакладнаяВыдана Тогда 
		Выдать = ЗаписатьВыдачу(Код);
		Если Выдать Тогда 
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		текСтатус = "НакладнаяВыдана";
		
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ = дНакладная;
		ЗаписьВОчереди.Статус = Перечисления.СтатусыСборкиРезервов.ВСборке;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ЗаписьВОчереди.Статус    = Перечисления.СтатусыСборкиРезервов.Собран;
			ЗаписьВОчереди.КонецСборки = ТекущаяДата();
			ЗаписьВОчереди.Записать(Истина);
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьИД(Знач ИД)
	тНовИД = "";
	  
	Символица = "АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЬЮЯЫЭ";
	Наим=СокрЛП(ВРег(ИД));
	Для й=1 По СтрДлина(Наим) Цикл
		тСимвол = Сред(Наим,й,1);
		Если Найти(Символица,тСимвол)>0 Тогда
			
			Если тСимвол ="А" Тогда 
				тНовИД = тНовИД + "f";
			ИначеЕсли тСимвол="Б" Тогда 
				тНовИД=тНовИД+",";
			ИначеЕсли тСимвол="В" Тогда 
				тНовИД=тНовИД+"d";
			ИначеЕсли тСимвол="Г" Тогда 
				тНовИД=тНовИД+"u";
			ИначеЕсли тСимвол="Д" Тогда 
				тНовИД=тНовИД+"l";
			ИначеЕсли тСимвол="Е" Тогда 
				тНовИД=тНовИД+"t";
			ИначеЕсли тСимвол="Ё" Тогда 
				тНовИД=тНовИД+"`";
			ИначеЕсли тСимвол="Ж" Тогда 
				тНовИД=тНовИД+";";
			ИначеЕсли тСимвол="З" Тогда 
				тНовИД=тНовИД+"p";
			ИначеЕсли тСимвол="И" Тогда 
				тНовИД=тНовИД+"b";
			ИначеЕсли тСимвол="Й" Тогда 
				тНовИД=тНовИД+"q";
			ИначеЕсли тСимвол="К" Тогда 
				тНовИД=тНовИД+"r";
			ИначеЕсли тСимвол="Л" Тогда 
				тНовИД=тНовИД+"k";
			ИначеЕсли тСимвол="М" Тогда 
				тНовИД=тНовИД+"v";
			ИначеЕсли тСимвол="Н" Тогда 
				тНовИД=тНовИД+"y";
			ИначеЕсли тСимвол="О" Тогда 
				тНовИД=тНовИД+"j";
			ИначеЕсли тСимвол="П" Тогда 
				тНовИД=тНовИД+"g";
			ИначеЕсли тСимвол="Р" Тогда 
				тНовИД=тНовИД+"h";
			ИначеЕсли тСимвол="С" Тогда 
				тНовИД=тНовИД+"c";
			ИначеЕсли тСимвол="Т" Тогда 
				тНовИД=тНовИД+"n";
			ИначеЕсли тСимвол="У" Тогда 
				тНовИД=тНовИД+"e";
			ИначеЕсли тСимвол="Ф" Тогда 
				тНовИД=тНовИД+"a";
			ИначеЕсли тСимвол="Х" Тогда 
				тНовИД=тНовИД+"[";
			ИначеЕсли тСимвол="Ц" Тогда 
				тНовИД=тНовИД+"w";
			ИначеЕсли тСимвол="Ч" Тогда 
				тНовИД=тНовИД+"x";
			ИначеЕсли тСимвол="Ш" Тогда 
				тНовИД=тНовИД+"i";
			ИначеЕсли тСимвол="Щ" Тогда 
				тНовИД=тНовИД+"o";
			ИначеЕсли тСимвол="Ъ" Тогда 
				тНовИД=тНовИД+"]";
			ИначеЕсли тСимвол="Ь" Тогда 
				тНовИД=тНовИД+"m";
			ИначеЕсли тСимвол="Ю" Тогда 
				тНовИД=тНовИД+".";
			ИначеЕсли тСимвол="Я" Тогда 
				тНовИД=тНовИД+"z";
			ИначеЕсли тСимвол="Ы" Тогда 
				тНовИД=тНовИД+"s";
			ИначеЕсли тСимвол="Э" Тогда 
				тНовИД=тНовИД+"'";
			КонецЕсли;	
			
			
		Иначе
			тНовИД = тНовИД + тСимвол;
		КонецЕсли;
		
	КонецЦикла;
	
	НовИД = НРег(тНовИД);
	Возврат ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(НовИД);
КонецФункции

&НаКлиенте
Процедура ПечатьНаПринтер()
	
	Если ТипЗнч(дНакладная) = Тип("ДокументСсылка.РезервированиеЗаказовПокупателя") Тогда 
		СтрокаПечФорм = "РезервированиеЗаказовПоМаршруту";
		ТК_УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.РезервированиеЗаказовПокупателя", СтрокаПечФорм, дНакладная, Новый Структура("Сборщик", Сотрудник));
	Иначе
		СтрокаПечФорм = "ПеремещениеТоваровПорядокСигаретный";
		ТК_УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Документ.ПеремещениеТоваров", СтрокаПечФорм, дНакладная, Новый Структура("Порядок", "Сигареты"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиСотрудника(Код)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СборщикиРезервов.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.СборщикиРезервов КАК СборщикиРезервов
		|ГДЕ
		|	СборщикиРезервов.Сотрудник.Код = &Код
		|	И СборщикиРезервов.Должность = &Должность";
	
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Должность", Справочники.Должности.НайтиПоРеквизиту("КодКП","9153"));

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник; 
		Возврат Истина;
	Иначе
		ОшибкаСканирования = "Сотрудник не является сборщиком!!!";
		Возврат Ложь;
	КонецЕсли;;
	
	
КонецФункции

&НаСервере
Функция ЗаписатьВыдачу(Код)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТК_ОчередьНаСборкуРезервов.Документ КАК Документ,
			|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
			|	ВЫБОР
			|		КОГДА ТК_ОчередьНаСборкуРезервов.Приоритет = ЗНАЧЕНИЕ(Перечисление.ПриоритетыСборки.ОченьНизкий)
			|			ТОГДА 1
			|		КОГДА ТК_ОчередьНаСборкуРезервов.Приоритет = ЗНАЧЕНИЕ(Перечисление.ПриоритетыСборки.Низкий)
			|			ТОГДА 2
			|		КОГДА ТК_ОчередьНаСборкуРезервов.Приоритет = ЗНАЧЕНИЕ(Перечисление.ПриоритетыСборки.Средний)
			|			ТОГДА 3
			|		КОГДА ТК_ОчередьНаСборкуРезервов.Приоритет = ЗНАЧЕНИЕ(Перечисление.ПриоритетыСборки.Высокий)
			|			ТОГДА 4
			|		КОГДА ТК_ОчередьНаСборкуРезервов.Приоритет = ЗНАЧЕНИЕ(Перечисление.ПриоритетыСборки.ОченьВысокий)
			|			ТОГДА 5
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Приоритет,
			|	ВложенныйЗапрос.ДопСборщик КАК ДопСборщик,
			|	ВложенныйЗапрос.Склад.ТорговаяПлощадка КАК ТорговаяПлощадка,
			|	НАЧАЛОПЕРИОДА(ТК_ОчередьНаСборкуРезервов.Документ.Дата, ДЕНЬ) КАК ДеньРезерва
			|ИЗ
			|	РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			СборщикиРезервов.Склад КАК Склад,
			|			СборщикиРезервов.Сотрудник КАК Сотрудник,
			|			СборщикиРезервов.ДопСборщик КАК ДопСборщик
			|		ИЗ
			|			РегистрСведений.СборщикиРезервов КАК СборщикиРезервов
			|		ГДЕ
			|			СборщикиРезервов.Сотрудник.Код = &Код
			|			И СборщикиРезервов.Должность = &Должность) КАК ВложенныйЗапрос
			|		ПО ТК_ОчередьНаСборкуРезервов.Документ.СкладКомпании = ВложенныйЗапрос.Склад
			|ГДЕ
			|	ТК_ОчередьНаСборкуРезервов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборкиРезервов.ГотовКсборке)
			|"+?(ЗначениеЗаполнено(текСклад), "И ТК_ОчередьНаСборкуРезервов.Документ.СкладКомпании = &СкладКомпании","")+"
			|
			|УПОРЯДОЧИТЬ ПО
			|	Приоритет УБЫВ,
			|	ДеньРезерва,
			|	ТК_ОчередьНаСборкуРезервов.Документ.ТочкаДоставки.ПорядокСигаретный УБЫВ,
			|	ТК_ОчередьНаСборкуРезервов.Документ.Дата";
	
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("СкладКомпании", текСклад);
	Запрос.УстановитьПараметр("Должность", Справочники.Должности.НайтиПоРеквизиту("КодКП","9153"));

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		дНакладная = ВыборкаДетальныеЗаписи.Документ;
		
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ = ВыборкаДетальныеЗаписи.Документ;
		ЗаписьВОчереди.Статус = Перечисления.СтатусыСборкиРезервов.ГотовКСборке;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ЗаписьВОчереди.Сотрудник  = ВыборкаДетальныеЗаписи.Сотрудник;
			ЗаписьВОчереди.ДопСборщик = ВыборкаДетальныеЗаписи.ДопСборщик;
			ЗаписьВОчереди.Статус     = Перечисления.СтатусыСборкиРезервов.ВСборке;
			ЗаписьВОчереди.НачалоСборки = ТекущаяДата();
			ЗаписьВОчереди.Записать(Истина);
			
			СтруктураИзменения = Новый Структура("Состояние,АвторИзменения,ТорговаяПлощадка",Перечисления.СтатусыСборкиРезервов.ВСборке,ВыборкаДетальныеЗаписи.Сотрудник,ВыборкаДетальныеЗаписи.ТорговаяПлощадка);
			РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(ВыборкаДетальныеЗаписи.Документ,СтруктураИзменения);
			
		КонецЕсли;
		ТекстОшибки = НСтр("ru = 'Идет выдача накладной на сборку! Подождите!!!'; uk = 'Відбувається видача накладної для збирання! Зачекайте!'");
		ОшибкаСканирования = ТекстОшибки;				
		Возврат истина;
	Иначе
		ТекстОшибки = НСтр("ru = 'Нет накладных на сборку!'; uk = 'Відсутні накладні для збирання!'");
		ОшибкаСканирования = ТекстОшибки;				
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПроверитьСборкуНакладной(Код)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_ОчередьНаСборкуРезервов.Документ КАК Документ,
		|	ТК_ОчередьНаСборкуРезервов.НачалоСборки КАК НачалоСборки,
		|	ТК_ОчередьНаСборкуРезервов.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
		|ГДЕ
		|	ТК_ОчередьНаСборкуРезервов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборкиРезервов.ВСборке)
		|	И ТК_ОчередьНаСборкуРезервов.Сотрудник.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", Код);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВремяСборки = ТекущаяДата() -ВыборкаДетальныеЗаписи.НачалоСборки;
		Если ВремяСборки < 10 Тогда
			ОшибкаСканирования = "Повторное сканирование!";
			Возврат Ложь;
		КонецЕсли;
		дНакладная = ВыборкаДетальныеЗаписи.Документ;
		НачалоСканирования = ВыборкаДетальныеЗаписи.НачалоСборки;
		КонецСканирования = ТекущаяДата();
		Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;;
	
КонецФункции

&НаСервере
Процедура ЗавершитьСборкуНаСервере()
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = дНакладная;
	ЗаписьВОчереди.Статус = Перечисления.СтатусыСборкиРезервов.ВСборке;
	ЗаписьВОчереди.Прочитать();
	Если ЗаписьВОчереди.Выбран() Тогда
		ЗаписьВОчереди.Статус    = Перечисления.СтатусыСборкиРезервов.Собран;
		ЗаписьВОчереди.КонецСборки = ТекущаяДата();
		ЗаписьВОчереди.Записать(Истина);
	КонецЕсли;
	дНакладная = Документы.РезервированиеЗаказовПокупателя.ПустаяСсылка();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьСборку(Команда)
	ЗавершитьСборкуНаСервере();
		
	Элементы.Декорация1.Заголовок = НСтр("ru = 'Просканируйте накладную или шк сотрудника!'; uk = 'Проскануйте накладну чи шк комплектувальника!'");
	
	текСтатус = "ОжиданиеСканирования"; 
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Если текСтатус <> "ОжиданиеСканирования" Тогда 
		Возврат;
	КонецЕсли;
	
	ЗавершитьРаботуСистемы(Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтчеОстаткиСРезерва(Команда)
	
	ВариантОтчета = ПолучитьВариантОтчетаОстаткиСРезервом();
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, ВариантОтчета);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВариантОтчетаОстаткиСРезервом()
	
	ВариантОтчета = Неопределено;
	
	Попытка
		Отчет = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", "ОтчетОстатокСРезервом");
		Если Отчет <> Неопределено И Не Отчет.Пустая() Тогда
			ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, "ce3025ed-b11e-4034-bc1b-28fdddc960d4");				
		КонецЕсли;
	Исключение
		//Значит нету прав на запуск
	КонецПопытки;
	
	Возврат ВариантОтчета;
КонецФункции


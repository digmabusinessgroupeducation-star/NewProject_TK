
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ВыполнитьВыгрузкуНоменклатуры(ПараметрыВыгрузки) Экспорт 
	
	УзелОбмена 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "УзелОбмена", Неопределено);
	ПоУзлуОбмена 	= ЗначениеЗаполнено(УзелОбмена);	
	НеУдалятьРегистрацию = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НеУдалятьРегистрацию", Ложь);
	
	ТаблТовары = ВнешниеИсточникиДанных.PowerBi.Таблицы.dbo_NOMENCLATURE;	
	
	ИдентификаторОбработки = "ВыгрузкаИзменений_PowerBi";		
	КлючОбработки = "Выгрузка_Номенклатура";
	
	регРезультаты = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписиЖурнала = регРезультаты.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	регРезультаты.ДобавитьКомментарий("Начало выгрузки", СтруктураЗаписиЖурнала);
	
	Попытка
	
		грАлкоголь 		= Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("a16b3b48-08ca-11de-9f01-080027bd85d2")); //Алкогольные напитки
		грБезалкоголь 	= Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("a05af895-09b6-11de-9f01-080027bd85d2")); //Безалкогольные напитки
		грТабак 		= Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("b8c89b03-0979-11de-9f01-080027bd85d2")); //Табачные изделия
		
		мПродукты = Новый Массив;
		мПродукты.Добавить(Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("88a16d0f-097a-11de-9f01-080027bd85d2"))); //Продукты питания
		мПродукты.Добавить(Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("29e966a0-0957-11de-9f01-080027bd85d2"))); //Прочая номенклатура
		
		мНепродовольственные = Новый Массив;
		мНепродовольственные.Добавить(Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("aefd25fb-0993-11de-9f01-080027bd85d2"))); //Непродовольственные товары
		мНепродовольственные.Добавить(Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("4fea2871-097b-11de-9f01-080027bd85d2"))); //Тара и упаковка
			
		ТекстЗапроса = "ВЫБРАТЬ
		               |	""Алкогольные напитки"" КАК НоменклатурнаяГруппа,
		               |	Номенклатура.Ссылка КАК Номенклатура
		               |ПОМЕСТИТЬ втНапитки
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Алкоголь)
		               |	И НЕ Номенклатура.ЭтоГруппа
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	""Безалкогольные напитки"",
		               |	Номенклатура.Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Безалкоголь)
		               |	И НЕ Номенклатура.ЭтоГруппа
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втНапитки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		               |	втНапитки.Номенклатура КАК Номенклатура
		               |ПОМЕСТИТЬ втНоменклатурныеГруппы
		               |ИЗ
		               |	втНапитки КАК втНапитки
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	""Табачные изделия и аксессуары"",
		               |	Номенклатура.Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Табак)
		               |	И НЕ Номенклатура.ЭтоГруппа
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	""Продукты питания"",
		               |	Номенклатура.Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Продукты)
		               |	И НЕ Номенклатура.ЭтоГруппа
		               |	И НЕ Номенклатура.Ссылка В
		               |				(ВЫБРАТЬ
		               |					втНапитки.Номенклатура КАК Номенклатура
		               |				ИЗ
		               |					втНапитки КАК втНапитки)
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	""Непродовольственные товары"",
		               |	Номенклатура.Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК Номенклатура
		               |ГДЕ
		               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Непродовольственные)
		               |	И НЕ Номенклатура.ЭтоГруппа
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Номенклатура
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ втНапитки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка,
		               |	спрНоменклатура.ЭтоГруппа КАК ЭтоГруппа,
		               |	спрНоменклатура.Наименование КАК Наименование,
		               |	спрНоменклатура.Родитель КАК Родитель,
		               |	спрНоменклатура.Артикул КАК Артикул,
		               |	ВЫБОР
		               |		КОГДА НЕ спрНоменклатура.ЭтоГруппа
		               |				И НЕ втНоменклатурныеГруппы.НоменклатурнаяГруппа ЕСТЬ NULL
		               |			ТОГДА втНоменклатурныеГруппы.НоменклатурнаяГруппа
		               |		КОГДА НЕ спрНоменклатура.ЭтоГруппа
		               |				И втНоменклатурныеГруппы.НоменклатурнаяГруппа ЕСТЬ NULL
		               |			ТОГДА ""Прочая номенклатура""
		               |		ИНАЧЕ NULL
		               |	КОНЕЦ КАК НоменклатурнаяГруппа
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатурныеГруппы КАК втНоменклатурныеГруппы
		               |		ПО спрНоменклатура.Ссылка = втНоменклатурныеГруппы.Номенклатура
		               |ГДЕ
		               |	НЕ &ПоУзлуОбмена
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка,
		               |	спрНоменклатура.ЭтоГруппа,
		               |	спрНоменклатура.Наименование,
		               |	спрНоменклатура.Родитель,
		               |	спрНоменклатура.Артикул,
		               |	ВЫБОР
		               |		КОГДА НЕ спрНоменклатура.ЭтоГруппа
		               |				И НЕ втНоменклатурныеГруппы.НоменклатурнаяГруппа ЕСТЬ NULL
		               |			ТОГДА втНоменклатурныеГруппы.НоменклатурнаяГруппа
		               |		КОГДА НЕ спрНоменклатура.ЭтоГруппа
		               |				И втНоменклатурныеГруппы.НоменклатурнаяГруппа ЕСТЬ NULL
		               |			ТОГДА ""Прочая номенклатура""
		               |		ИНАЧЕ NULL
		               |	КОНЕЦ
		               |ИЗ
		               |	Справочник.Номенклатура.Изменения КАК НоменклатураИзменения
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		               |			ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатурныеГруппы КАК втНоменклатурныеГруппы
		               |			ПО спрНоменклатура.Ссылка = втНоменклатурныеГруппы.Номенклатура
		               |		ПО НоменклатураИзменения.Ссылка = спрНоменклатура.Ссылка
		               |ГДЕ
		               |	&ПоУзлуОбмена
		               |	И НоменклатураИзменения.Узел = &УзелОбмена";
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр("Алкоголь", грАлкоголь);
		Запрос.УстановитьПараметр("Безалкоголь", грБезалкоголь);
		Запрос.УстановитьПараметр("Табак", грТабак);
		Запрос.УстановитьПараметр("Продукты", мПродукты);
		Запрос.УстановитьПараметр("Непродовольственные", мНепродовольственные);	
		
		Запрос.УстановитьПараметр("ПоУзлуОбмена", ПоУзлуОбмена);
		Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
		
		Выборка = Запрос.Выполнить().Выбрать();	
		
		Записано = 0;
		СОшибками = 0;
		
		Пока Выборка.Следующий() Цикл 			
			ГУИД = Выборка.Ссылка.УникальныйИдентификатор();
			Записать = Ложь;
			ВсеОк = Истина;
			
			нЗапись = ТаблТовары.ПолучитьСсылку(ГУИД);
			
			Если Не ОбщегоНазначения.СсылкаСуществует(нЗапись) Тогда 
				тЗапись = ТаблТовары.СоздатьОбъект();			
				тЗапись.GUID = ГУИД;
				Записать = Истина;
			Иначе
				тЗапись = нЗапись.ПолучитьОбъект();
			КонецЕсли;
			
			Если тЗапись.ISFOLDER <> Выборка.ЭтоГруппа Тогда 
				тЗапись.ISFOLDER = Выборка.ЭтоГруппа;
				Записать = Истина;
			КонецЕсли;
			
			Если тЗапись.DESCRIPTION <> Выборка.Наименование Тогда
				тЗапись.DESCRIPTION = Выборка.Наименование;
				Записать = Истина;
			КонецЕсли;
			
			Если тЗапись.ARTNUM <> Выборка.Артикул Тогда 
				тЗапись.ARTNUM = Выборка.Артикул;
				Записать = Истина;
			КонецЕсли;
			
			Если тЗапись.GROUP_1 <> Выборка.НоменклатурнаяГруппа Тогда 
				тЗапись.GROUP_1 = Выборка.НоменклатурнаяГруппа;
				Записать = Истина;
			КонецЕсли;
			
			истЕстьРодитель = ЗначениеЗаполнено(Выборка.Родитель);
					
			Если истЕстьРодитель И тЗапись.PARENT <> Выборка.Родитель.УникальныйИдентификатор() Тогда
				тЗапись.PARENT = Выборка.Родитель.УникальныйИдентификатор();
				Записать = Истина;
			ИначеЕсли Не истЕстьРодитель И ЗначениеЗаполнено(тЗапись.PARENT) Тогда 
				тЗапись.PARENT = Null;
				Записать = Истина;
			КонецЕсли;
			
			Если Записать Тогда 
				Попытка
					тЗапись.Записать();	
					Записано = Записано + 1;										
				Исключение
					СОшибками = СОшибками + 1;
					ВсеОк = Ложь;
					
					регРезультаты.СообщитьОбОшибке(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось выгрузить номенклатуру [%1] %2
	                              |Ошибка: %3
	                              |'; uk = 'Не вдалося вивантажити номенклатуру [%1] %2
	                              |Помилка: %3
	                              |'"),
							Выборка.Артикул,
							Выборка.Наименование,
							ОписаниеОшибки()), 
						СтруктураЗаписиЖурнала);
				КонецПопытки;
			КонецЕсли;
			
			Если ПоУзлуОбмена И ВсеОк И Не НеУдалятьРегистрацию Тогда 
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Выборка.Ссылка);
			КонецЕсли;			
		КонецЦикла;
		
		регРезультаты.ДобавитьКомментарий(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 записей'; uk = 'Вивантажено %1 записів'"),
				Записано),
			СтруктураЗаписиЖурнала);
			
	Исключение
		СОшибками = 1;
		регРезультаты.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписиЖурнала);
	КонецПопытки;
	
	регРезультаты.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СОшибками = 0);
		
КонецПроцедуры

Процедура ВыполнитьВыгрузкуОбъекты(ПараметрыВыгрузки) Экспорт 
		
	УзелОбмена 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "УзелОбмена", Неопределено);
	ПоУзлуОбмена 	= ЗначениеЗаполнено(УзелОбмена);	
	НеУдалятьРегистрацию = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НеУдалятьРегистрацию", Ложь);
	
	Подразделение	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "Подразделение", Неопределено);
	
	
	ИдентификаторОбработки = "ВыгрузкаИзменений_PowerBi";		
	КлючОбработки = "Выгрузка_ТорговыеПлощадки";
	
	регРезультаты = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписиЖурнала = регРезультаты.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	регРезультаты.ДобавитьКомментарий("Начало выгрузки", СтруктураЗаписиЖурнала);
	
	ПоУзлуОбмена = ЗначениеЗаполнено(УзелОбмена);	
	ТаблОбъекты = ВнешниеИсточникиДанных.PowerBi.Таблицы.dbo_OUTLETS;
	
	Попытка		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ПоУзлуОбмена", ПоУзлуОбмена);
		Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТорговыеПлощадки.Ссылка КАК Ссылка,
		               |	ТорговыеПлощадки.ПодразделениеКомпании КАК Подразделение,
		               |	ТорговыеПлощадки.Наименование КАК Наименование
		               |ИЗ
		               |	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |ГДЕ
		               |	НЕ &ПоУзлуОбмена
		               |			И ТорговыеПлощадки.ПодразделениеКомпании = &Подразделение
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ТорговыеПлощадки.Ссылка,
		               |	ТорговыеПлощадки.ПодразделениеКомпании,
		               |	ТорговыеПлощадки.Наименование
		               |ИЗ
		               |	Справочник.ТорговыеПлощадки.Изменения КАК ТорговыеПлощадкиИзменения
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |		ПО ТорговыеПлощадкиИзменения.Ссылка = ТорговыеПлощадки.Ссылка
		               |ГДЕ
		               |	&ПоУзлуОбмена
		               |	И ТорговыеПлощадкиИзменения.Узел = &УзелОбмена";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Записано = 0;
		СОшибками = 0;
		
		Пока Выборка.Следующий() Цикл 
			ГУИД = Выборка.Ссылка.УникальныйИдентификатор();
			ГУИД_Подразделение = Выборка.Подразделение.УникальныйИдентификатор();
			Записать = Ложь;
			ВсеОк = Истина;
			
			нЗапись = ТаблОбъекты.ПолучитьСсылку(ГУИД);
			Если Не ОбщегоНазначения.СсылкаСуществует(нЗапись) Тогда 
				тЗапись = ТаблОбъекты.СоздатьОбъект();			
				тЗапись.GUID = ГУИД;
				Записать = Истина;
			Иначе
				тЗапись = нЗапись.ПолучитьОбъект();
			КонецЕсли;

			Если тЗапись.DEPARTMENT <> ГУИД_Подразделение Тогда 
				тЗапись.DEPARTMENT = ГУИД_Подразделение;
				Записать = Истина;
			КонецЕсли;
			
			Если тЗапись.DESCRIPTION <> Выборка.Наименование Тогда 
				тЗапись.DESCRIPTION = Выборка.Наименование;
				Записать = Истина;
			КонецЕсли;
		
			Если Записать Тогда 
				Попытка
					тЗапись.Записать();	
					Записано = Записано + 1;					
				Исключение
					ВсеОк = Ложь;
					СОшибками = СОшибками + 1;
					
					регРезультаты.СообщитьОбОшибке(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось выгрузить ТП %1
	                              |Ошибка: %2
	                              |'; uk = 'Не вдалося вивантажити номенклатуру %1
	                              |Помилка: %2
	                              |'"),
							Выборка.Наименование,
							ОписаниеОшибки()), 
						СтруктураЗаписиЖурнала);
				КонецПопытки;
			КонецЕсли;			
			
			Если ПоУзлуОбмена И ВсеОк И Не НеУдалятьРегистрацию Тогда 
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Выборка.Ссылка);
			КонецЕсли;
		КонецЦикла;		
		
		регРезультаты.ДобавитьКомментарий(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 записей'; uk = 'Вивантажено %1 записів'"),
				Записано),
			СтруктураЗаписиЖурнала);		
		
	Исключение
		СОшибками = 1;
		регРезультаты.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписиЖурнала);
	КонецПопытки;
	
	регРезультаты.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СОшибками = 0);	
	
КонецПроцедуры

Процедура ВыполнитьВыгрузкуПродажи(ПараметрыВыгрузки, Отказ = Ложь) Экспорт 
	
	УзелОбмена 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "УзелОбмена", Неопределено);
	ПоУзлуОбмена 	= ЗначениеЗаполнено(УзелОбмена);	
	
	НачалоПериода 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НачалоПериода", Неопределено);
	КонецПериода 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "КонецПериода", Неопределено);
	Подразделение	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "Подразделение", Неопределено);
	НеУдалятьРегистрацию = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НеУдалятьРегистрацию", Ложь);	
	
	ИдентификаторОбработки = "ВыгрузкаИзменений_PowerBi";		
	КлючОбработки = "Выгрузка_Продажи";
	
	регРезультаты = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписиЖурнала = регРезультаты.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	регРезультаты.ДобавитьКомментарий("Начало выгрузки", СтруктураЗаписиЖурнала);
	
	Попытка
		ТаблПродажи = ВнешниеИсточникиДанных.PowerBi.Таблицы.dbo_SALES;
		
		Если ПоУзлуОбмена Тогда 			
			МенеджерВТ = Новый МенеджерВременныхТаблиц;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
			Запрос.МенеджерВременныхТаблиц = МенеджерВТ;			
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(ПродажиИзменения.Регистратор.Дата, ДЕНЬ) КАК Период,
			               |	ПродажиИзменения.Регистратор.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	ПродажиИзменения.Регистратор.ТорговаяПлощадка КАК ТорговаяПлощадка
			               |ПОМЕСТИТЬ втОтборы
			               |ИЗ
			               |	РегистрНакопления.Продажи.Изменения КАК ПродажиИзменения
			               |ГДЕ
			               |	ПродажиИзменения.Узел = &УзелОбмена
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ПродажиИзменения.Регистратор.ПодразделениеКомпании,
			               |	НАЧАЛОПЕРИОДА(ПродажиИзменения.Регистратор.Дата, ДЕНЬ),
			               |	ПродажиИзменения.Регистратор.ТорговаяПлощадка
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	МИНИМУМ(втОтборы.Период) КАК Период
			               |ИЗ
			               |	втОтборы КАК втОтборы";
			
			ВыборкаПериод = Запрос.Выполнить().Выбрать();
						
			Если ВыборкаПериод.Следующий() Тогда 
				Период = ВыборкаПериод.Период;
			Иначе
				Период = НачалоМесяца(ТекущаяДата());
			КонецЕсли;
				  
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Период", Период);
			Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) КАК Период,
			               |	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка КАК ТорговаяПлощадка,
			               |	Продажи.Номенклатура КАК Номенклатура,
			               |	СУММА(Продажи.Количество) КАК Количество,
			               |	СУММА(Продажи.Сумма) КАК Сумма,
			               |	СУММА(Продажи.СуммаНДС) КАК СуммаНДС,
			               |	СУММА(Продажи.Себестоимость) КАК Себестоимость
			               |ИЗ
			               |	РегистрНакопления.Продажи КАК Продажи
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОтборы КАК втОтборы
			               |		ПО (НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) = втОтборы.Период)
			               |			И Продажи.ПодразделениеКомпании = втОтборы.ПодразделениеКомпании
			               |			И Продажи.ТорговаяПлощадка = втОтборы.ТорговаяПлощадка
			               |ГДЕ
			               |	Продажи.Период >= &Период
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	Продажи.Номенклатура,
			               |	Продажи.ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка,
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ)
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период
			               |ИТОГИ ПО
			               |	Период,
			               |	ПодразделениеКомпании,
			               |	ТорговаяПлощадка";
			
		Иначе
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
			Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
			Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) КАК Период,
			               |	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка КАК ТорговаяПлощадка,
			               |	Продажи.Номенклатура КАК Номенклатура,
			               |	СУММА(Продажи.Количество) КАК Количество,
			               |	СУММА(Продажи.Сумма) КАК Сумма,
			               |	СУММА(Продажи.СуммаНДС) КАК СуммаНДС,
			               |	СУММА(Продажи.Себестоимость) КАК Себестоимость
			               |ИЗ
			               |	РегистрНакопления.Продажи КАК Продажи
			               |ГДЕ
			               |	Продажи.Период МЕЖДУ &НачалоПериода И &КонецПериода
			               |	И Продажи.ПодразделениеКомпании = &ПодразделениеКомпании
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	Продажи.Номенклатура,
			               |	Продажи.ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка,
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ)
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период
			               |ИТОГИ ПО
			               |	Период,
			               |	ПодразделениеКомпании,
			               |	ТорговаяПлощадка";
		КонецЕсли;
		
		Записано = 0;
		СОшибками = 0;
		ВыборкаПериод = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПериод.Следующий() Цикл 
			ВыборкаПодразделение = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПодразделение.Следующий() Цикл 
				
				ВыборкаТТ = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТТ.Следующий() Цикл 
					ПодразделениеГУИД = ВыборкаТТ.ПодразделениеКомпании.УникальныйИдентификатор();
					ТТГУИД = ВыборкаТТ.ТорговаяПлощадка.УникальныйИдентификатор();
					ВсеОк = Истина;
					
					НаборЗаписей = ТаблПродажи.СоздатьНаборЗаписей();			
					НаборЗаписей.Отбор.PERIOD.Установить(ВыборкаТТ.Период);
					НаборЗаписей.Отбор.DEPARTMENT.Установить(ПодразделениеГУИД);
					НаборЗаписей.Отбор.OUTLET.Установить(ТТГУИД);
					НаборЗаписей.Прочитать();				
					НаборЗаписей.Очистить();
					
					Выборка = ВыборкаТТ.Выбрать();			
					Пока Выборка.Следующий() Цикл 
						тЗапись = НаборЗаписей.Добавить();
						тЗапись.PERIOD = Выборка.Период;
						тЗапись.DEPARTMENT = ПодразделениеГУИД;
						тЗапись.OUTLET = ТТГУИД;
						тЗапись.NOMENCLATURE = Выборка.Номенклатура.УникальныйИдентификатор();
						тЗапись.QTY = Выборка.Количество;
						тЗапись.SUM = Выборка.Сумма;
						тЗапись.VAT = Выборка.СуммаНДС;
						тЗапись.COSTS = Выборка.Себестоимость;
					КонецЦикла;
					
					Попытка
						НаборЗаписей.Записать(Истина);
						Записано = Записано + НаборЗаписей.Количество();
					Исключение
						СОшибками = СОшибками + 1;
						
						регРезультаты.ДобавитьКомментарий(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Выгружено %1 записей'; uk = 'Вивантажено %1 записів'"),
								Записано),
							СтруктураЗаписиЖурнала);		
					КонецПопытки;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		регРезультаты.ДобавитьКомментарий(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 записей'; uk = 'Вивантажено %1 записів'"),
				Записано),
			СтруктураЗаписиЖурнала);		
		
		Если СОшибками = 0 И Не НеУдалятьРегистрацию Тогда 
			УдалитьРегистрациюПродажи(УзелОбмена);
		КонецЕсли;		
		
	Исключение
		СОшибками = 1;
		регРезультаты.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписиЖурнала);
	КонецПопытки;
	
	регРезультаты.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СОшибками = 0);	
	
	Отказ = СОшибками > 0;
	
КонецПроцедуры

Процедура ВыполнитьВыгрузкуЧеков(ПараметрыВыгрузки, Отказ = Ложь) Экспорт 
	
	УзелОбмена 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "УзелОбмена", Неопределено);
	ПоУзлуОбмена 	= ЗначениеЗаполнено(УзелОбмена);	
	
	НачалоПериода 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НачалоПериода", Неопределено);
	КонецПериода 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "КонецПериода", Неопределено);
	Подразделение	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "Подразделение", Неопределено);
	НеУдалятьРегистрацию = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыВыгрузки, "НеУдалятьРегистрацию", Ложь);	
	
	ИдентификаторОбработки = "ВыгрузкаИзменений_PowerBi";		
	КлючОбработки = "Выгрузка_Чеки";
	
	регРезультаты = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписиЖурнала = регРезультаты.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	регРезультаты.ДобавитьКомментарий("Начало выгрузки", СтруктураЗаписиЖурнала);
	
	Попытка
		ТаблЧеки = ВнешниеИсточникиДанных.PowerBi.Таблицы.dbo_RECEIPTS;
		
		Если ПоУзлуОбмена Тогда 			
			МенеджерВТ = Новый МенеджерВременныхТаблиц;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
			Запрос.МенеджерВременныхТаблиц = МенеджерВТ;			
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(ПродажиИзменения.Регистратор.Дата, ДЕНЬ) КАК Период,
			               |	ПродажиИзменения.Регистратор.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	ПродажиИзменения.Регистратор.ТорговаяПлощадка КАК ТорговаяПлощадка
			               |ПОМЕСТИТЬ втОтборы
			               |ИЗ
			               |	РегистрНакопления.Продажи.Изменения КАК ПродажиИзменения
			               |ГДЕ
			               |	ПродажиИзменения.Узел = &УзелОбмена
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ПродажиИзменения.Регистратор.ПодразделениеКомпании,
			               |	НАЧАЛОПЕРИОДА(ПродажиИзменения.Регистратор.Дата, ДЕНЬ),
			               |	ПродажиИзменения.Регистратор.ТорговаяПлощадка
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	МИНИМУМ(втОтборы.Период) КАК Период
			               |ИЗ
			               |	втОтборы КАК втОтборы";
			
			ВыборкаПериод = Запрос.Выполнить().Выбрать();
						
			Если ВыборкаПериод.Следующий() Тогда 
				Период = ВыборкаПериод.Период;
			Иначе
				Период = НачалоМесяца(ТекущаяДата());
			КонецЕсли;
				  
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Период", Период);
			Запрос.МенеджерВременныхТаблиц = МенеджерВТ;			
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) КАК Период,
			               |	Продажи.Регистратор КАК Регистратор,
			               |	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка КАК ТорговаяПлощадка
			               |ПОМЕСТИТЬ втЗаписи
			               |ИЗ
			               |	РегистрНакопления.Продажи КАК Продажи
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОтборы КАК втОтборы
			               |		ПО (НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) = втОтборы.Период)
			               |			И Продажи.ПодразделениеКомпании = втОтборы.ПодразделениеКомпании
			               |			И Продажи.ТорговаяПлощадка = втОтборы.ТорговаяПлощадка
			               |ГДЕ
			               |	Продажи.Период >= &Период
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	Продажи.ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка,
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ),
			               |	Продажи.Регистратор
			               |
			               |ИНДЕКСИРОВАТЬ ПО
			               |	Регистратор
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ЗакрытиеСменыСнятыеКассы.Ссылка КАК Регистратор,
			               |	СУММА(ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КоличествоЧеков) КАК КоличествоЧеков
			               |ПОМЕСТИТЬ втЧеки
			               |ИЗ
			               |	Документ.ЗакрытиеСмены.СнятыеКассы КАК ЗакрытиеСменыСнятыеКассы
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаписи КАК втЗаписи
			               |		ПО ЗакрытиеСменыСнятыеКассы.Ссылка = втЗаписи.Регистратор
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ЗакрытиеСменыСнятыеКассы.Ссылка
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втЗаписи.Период КАК Период,
			               |	втЗаписи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	втЗаписи.ТорговаяПлощадка КАК ТорговаяПлощадка,
			               |	СУММА(ВЫБОР
			               |			КОГДА втЗаписи.Регистратор ССЫЛКА Документ.ЗакрытиеСмены
			               |				ТОГДА втЧеки.КоличествоЧеков
			               |			КОГДА втЗаписи.Регистратор ССЫЛКА Документ.ВозвратОтПокупателя
			               |				ТОГДА -1
			               |			ИНАЧЕ 1
			               |		КОНЕЦ) КАК КоличествоЧеков
			               |ИЗ
			               |	втЗаписи КАК втЗаписи
			               |		ЛЕВОЕ СОЕДИНЕНИЕ втЧеки КАК втЧеки
			               |		ПО втЗаписи.Регистратор = втЧеки.Регистратор
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	втЗаписи.Период,
			               |	втЗаписи.ТорговаяПлощадка,
			               |	втЗаписи.ПодразделениеКомпании
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период
			               |ИТОГИ
			               |	СУММА(КоличествоЧеков)
			               |ПО
			               |	Период,
			               |	ПодразделениеКомпании";			
			
		Иначе
				
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
			Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
			Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);
			Запрос.Текст = "ВЫБРАТЬ
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) КАК Период,
			               |	Продажи.Регистратор КАК Регистратор,
			               |	Продажи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка КАК ТорговаяПлощадка
			               |ПОМЕСТИТЬ втЗаписи
			               |ИЗ
			               |	РегистрНакопления.Продажи КАК Продажи
			               |ГДЕ
			               |	Продажи.Период МЕЖДУ &НачалоПериода И &КонецПериода
			               |	И Продажи.ПодразделениеКомпании = &ПодразделениеКомпании
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	Продажи.ПодразделениеКомпании,
			               |	Продажи.ТорговаяПлощадка,
			               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ),
			               |	Продажи.Регистратор
			               |
			               |ИНДЕКСИРОВАТЬ ПО
			               |	Регистратор
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ЗакрытиеСменыСнятыеКассы.Ссылка КАК Регистратор,
			               |	СУММА(ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КоличествоЧеков) КАК КоличествоЧеков
			               |ПОМЕСТИТЬ втЧеки
			               |ИЗ
			               |	Документ.ЗакрытиеСмены.СнятыеКассы КАК ЗакрытиеСменыСнятыеКассы
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втЗаписи КАК втЗаписи
			               |		ПО ЗакрытиеСменыСнятыеКассы.Ссылка = втЗаписи.Регистратор
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	ЗакрытиеСменыСнятыеКассы.Ссылка
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	втЗаписи.Период КАК Период,
			               |	втЗаписи.ПодразделениеКомпании КАК ПодразделениеКомпании,
			               |	втЗаписи.ТорговаяПлощадка КАК ТорговаяПлощадка,
			               |	СУММА(ВЫБОР
			               |			КОГДА втЗаписи.Регистратор ССЫЛКА Документ.ЗакрытиеСмены
			               |				ТОГДА втЧеки.КоличествоЧеков
			               |			КОГДА втЗаписи.Регистратор ССЫЛКА Документ.ВозвратОтПокупателя
			               |				ТОГДА -1
			               |			ИНАЧЕ 1
			               |		КОНЕЦ) КАК КоличествоЧеков
			               |ИЗ
			               |	втЗаписи КАК втЗаписи
			               |		ЛЕВОЕ СОЕДИНЕНИЕ втЧеки КАК втЧеки
			               |		ПО втЗаписи.Регистратор = втЧеки.Регистратор
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	втЗаписи.Период,
			               |	втЗаписи.ТорговаяПлощадка,
			               |	втЗаписи.ПодразделениеКомпании
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период
			               |ИТОГИ
			               |	СУММА(КоличествоЧеков)
			               |ПО
			               |	Период,
			               |	ПодразделениеКомпании";
		КонецЕсли;
			
		Записано = 0;
		СОшибками = 0;
		ВыборкаПериод = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПериод.Следующий() Цикл 
			ВыборкаПодразделение = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПодразделение.Следующий() Цикл 
				
				ВыборкаТТ = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаТТ.Следующий() Цикл 
					
					ПодразделениеГУИД = ВыборкаТТ.ПодразделениеКомпании.УникальныйИдентификатор();
					ТТГУИД = ВыборкаТТ.ТорговаяПлощадка.УникальныйИдентификатор();
					
					НаборЗаписей = ТаблЧеки.СоздатьНаборЗаписей();			
					НаборЗаписей.Отбор.PERIOD.Установить(ВыборкаТТ.Период);
					НаборЗаписей.Отбор.DEPARTMENT.Установить(ПодразделениеГУИД);
					НаборЗаписей.Отбор.OUTLET.Установить(ТТГУИД);
					НаборЗаписей.Прочитать();				
					НаборЗаписей.Очистить();
					
					тЗапись = НаборЗаписей.Добавить();
					тЗапись.PERIOD = ВыборкаТТ.Период;
					тЗапись.DEPARTMENT = ПодразделениеГУИД;
					тЗапись.OUTLET = ТТГУИД;
					тЗапись.QTY = ВыборкаТТ.КоличествоЧеков;
									
					Попытка
						НаборЗаписей.Записать(Истина);
						Записано = Записано + НаборЗаписей.Количество();
					Исключение
						СОшибками = СОшибками + 1;
						ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
					КонецПопытки;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		Если СОшибками = 0 И Не НеУдалятьРегистрацию Тогда 
			УдалитьРегистрациюПродажи(УзелОбмена);
		КонецЕсли;

		регРезультаты.ДобавитьКомментарий(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 записей'; uk = 'Вивантажено %1 записів'"),
				Записано),
			СтруктураЗаписиЖурнала);		
		
	Исключение
		СОшибками = 1;
		регРезультаты.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписиЖурнала);
	КонецПопытки;
	
	регРезультаты.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СОшибками = 0);	
	
	Отказ = СОшибками > 0;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура УдалитьРегистрациюПродажи(УзелОбмена) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелОбмена", УзелОбмена);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПродажиИзменения.Регистратор КАК Регистратор
	               |ИЗ
	               |	РегистрНакопления.Продажи.Изменения КАК ПродажиИзменения
	               |ГДЕ
	               |	ПродажиИзменения.Узел = &УзелОбмена";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, Выборка.Регистратор);
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции


#КонецОбласти



#КонецЕсли
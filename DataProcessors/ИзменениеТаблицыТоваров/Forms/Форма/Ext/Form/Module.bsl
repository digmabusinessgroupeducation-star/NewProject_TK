
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

 &НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент И Объект.Товары.Количество() > 0 Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru='Перенести изменения в документ?';uk='Перенести зміни в документ?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиИзмененияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииФормыНаСервере();

	Если ПеренестиВДокумент Тогда
		
		ПараметрОповещения = Новый Структура;
		ПараметрОповещения.Вставить("ИдентификаторВызывающейФормы", 					ВладелецФормы.УникальныйИдентификатор);
		ПараметрОповещения.Вставить("АдресОбработаннойТабличнойЧастиТоварыВХранилище", 	АдресХранилищаТовары);
		
		Оповестить("ОбработанаТабличнаяЧастьТовары", ПараметрОповещения, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриИзмененииТекущегоДействия();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.АдресХранилищаТовары) Тогда
		ТекстСообщения = НСтр("ru='Непосредственное открытие обработки изменения таблицы товаров не предусмотрено. 
|Для открытия обработки можно воспользоваться командой ""Изменить"" в формах документов';uk='Безпосереднє відкриття обробки зміни таблиці товарів не передбачено. 
|Для відкриття обробки можна скористатися командою ""Змінити"" в формах документів'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	//заполним реквизиты формы из переданных параметров
	СписокСвойств = "АдресХранилищаТовары, ДокументСсылка, ДокументДата, ДокументОрганизация,
		|ДокументВалюта, ДокументКурс, ДокументСуммаВключаетНДС, ДокументТипЦен, ДокументСклад";
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, СписокСвойств);
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаТовары);
	Объект.Товары.Загрузить(ТаблицаТоваров);
	
	//Установим пометку и заполним номер строки документа
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		СтрокаТабличнойЧасти.Пометка 					= Истина;
		СтрокаТабличнойЧасти.НомерСтрокиДокумента 		= СтрокаТабличнойЧасти.НомерСтроки;
		СтрокаТабличнойЧасти.НоменклатураДоИзменения 	= СтрокаТабличнойЧасти.Номенклатура;
		Если СтрокаТабличнойЧасти.СуммаСкидки = 0 Тогда
			СтрокаТабличнойЧасти.СуммаБезСкидки = СтрокаТабличнойЧасти.Сумма;
		Иначе
			СтрокаТабличнойЧасти.СуммаБезСкидки = СтрокаТабличнойЧасти.Сумма-СтрокаТабличнойЧасти.СуммаСкидки;
		КонецЕсли;	
	КонецЦикла;
	
	ИмяТаблицы = "Товары";
	
	ПодготовитьФормуНаСервере(); 	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ТекущееДействиеПриИзменении(Элемент)

	// Установим элементы формы в зависимости от выбранного действия
	ПриИзмененииТекущегоДействия();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Товары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)

	Модифицированность = Истина;
	ОбновитьИтоги(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элементы.Товары.ТекущиеДанные.Пометка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");

	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	

	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
	
КонецПроцедуры  

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)

	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
			
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);

	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	Если ПоказыватьСуммуНДС Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
		ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));

КонецПроцедуры   

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействие(Команда)

	Если ТекущееДействие = "ДобавитьИзДокумента" Тогда   		
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияДокумент) Тогда
			ТекстСообщения = НСтр("ru='Не указан документ, из которого добавляются строки списка';uk='Не вказаний документ, з якого додаються рядки списку'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияДокумент");
			Возврат;
		КонецЕсли;
		
		ДобавитьДанныеИзДокумента(ВариантЗначенияДокумент);
		
		
	ИначеЕсли ТекущееДействие = "УстановитьЦеныПоТипу" Тогда     
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияТипЦенНоменклатуры) Тогда
			ТекстСообщения = НСтр("ru='Не указан тип цен номенклатуры';uk='Не вказаний тип цін номенклатури'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияТипЦенНоменклатуры");
			Возврат;
		КонецЕсли;
		
		УстановитьЦеныПоТипу(ВариантЗначенияТипЦенНоменклатуры);
		
	ИначеЕсли ТекущееДействие = "ИзменитьРеквизит" Тогда     
		
		//Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧисло) Тогда
		//	ТекстСообщения = НСтр("ru='Не указано значение изменения';uk='Не вказано значення для зміни'");
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧисло");
		//	Возврат;
		//КонецЕсли;
		
		ИзменитьРеквизит(ВариантЗначенияЧисло, Операция);

		
	ИначеЕсли ТекущееДействие = "УстановитьЦеныВРозницеПоТипу" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияТипЦенНоменклатуры) Тогда
			ТекстСообщения = НСтр("ru='Не указан тип цен номенклатуры';uk='Не вказаний тип цін номенклатури'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияТипЦенНоменклатуры");
			Возврат;
		КонецЕсли;
		
		УстановитьЦеныВРозницеПоТипу(ВариантЗначенияТипЦенНоменклатуры);     		

	ИначеЕсли ТекущееДействие = "РаспределитьСуммуПоСуммам" Тогда    	
	
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧислоСумма) Тогда
			ТекстСообщения = НСтр("ru='Не указана сумма распределения! Распределение невозможно';uk='Не вказана сума розподілу! Розподіл неможливий'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧислоСумма");
			Возврат;
		КонецЕсли;
		
		РаспределитьСуммуПоКолонке("Сумма", ВариантЗначенияЧислоСумма);
		
	ИначеЕсли ТекущееДействие = "РаспределитьСуммуПоКоличеству" Тогда 		
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧислоСумма) Тогда
			ТекстСообщения = НСтр("ru='Не указана сумма распределения! Распределение невозможно';uk='Не вказана сума розподілу! Розподіл неможливий'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧислоСумма");
			Возврат;
		КонецЕсли;
		
		РаспределитьСуммуПоКолонке("Количество", ВариантЗначенияЧислоСумма);
		
	ИначеЕсли ТекущееДействие = "ИзменитьЦеныНаПроцент" Тогда    
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧислоПроцент) Тогда
			ТекстСообщения = НСтр("ru='Не указан процент';uk='Не вказаний відсоток'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧислоПроцент");
			Возврат;
		КонецЕсли;
		
		ИзменитьЦенуНаПроцент(ВариантЗначенияЧислоПроцент);
		
		
	ИначеЕсли ТекущееДействие = "ОкруглитьЦеныДо" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияПорядокОкругления) Тогда
			ТекстСообщения = НСтр("ru='Не указан порядок округления';uk='Не вказаний порядок округлення'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияПорядокОкругления");
			Возврат;
		КонецЕсли;
		
		ОкруглитьЦену(ВариантЗначенияПорядокОкругления);
			
	ИначеЕсли ТекущееДействие = "ОкруглитьКоличествоДо" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияПорядокОкругления) Тогда
			ТекстСообщения = НСтр("ru='Не указан порядок округления';uk='Не вказаний порядок округлення'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияПорядокОкругления");
			Возврат;
		КонецЕсли;
		
		ОкруглитьКоличетсво(ВариантЗначенияПорядокОкругления);
	
	ИначеЕсли ТекущееДействие = "УстановитьСтавкуНДС" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияСтавкаНДС) Тогда
			ТекстСообщения = НСтр("ru='Не указана ставка НДС';uk='Не вказана ставка ПДВ'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияСтавкаНДС");
			Возврат;
		КонецЕсли;
		
		УстановитьСтавкуНДС(ВариантЗначенияСтавкаНДС);
		
	ИначеЕсли ТекущееДействие = "УстановитьСкидкуПроцентом" Тогда
				
		УстановитьСкидкуПроцентом(ВариантЗначенияЧислоПроцент);
		
	ИначеЕсли ТекущееДействие = "УстановитьСкидкуСуммойПоСуммамм" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧислоСумма) Тогда
			ТекстСообщения = НСтр("ru='Не указана сумма скидки! Распределение невозможно';uk='Не зазначена сума знижки! Розподіл неможливий'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧислоСумма");
			Возврат;
		КонецЕсли;
		
		РаспределитьСуммуСкидкиПоКолонке("Сумма", ВариантЗначенияЧислоСумма);

	ИначеЕсли ТекущееДействие = "УстановитьСкидкуСуммойПоКоличеству" Тогда
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияЧислоСумма) Тогда
			ТекстСообщения = НСтр("ru='Не указана сумма скидки! Распределение невозможно';uk='Не зазначена сума знижки! Розподіл неможливий'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияЧислоСумма");
			Возврат;
		КонецЕсли;
		
		РаспределитьСуммуСкидкиПоКолонке("Количество", ВариантЗначенияЧислоСумма);
		
	ИначеЕсли ТекущееДействие = "УстановитьЗакупочныеЦеныПоТипу" Тогда     
		
		Если НЕ ЗначениеЗаполнено(ВариантЗначенияТипЦенНоменклатуры) Тогда
			ТекстСообщения = НСтр("ru='Не указан тип цен номенклатуры';uk='Не вказаний тип цін номенклатури'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВариантЗначенияТипЦенНоменклатуры");
			Возврат;
		КонецЕсли;
		
		УстановитьЦеныЗакупкиПоТипу(ВариантЗначенияЧисло, Операция, ВариантЗначенияТипЦенНоменклатуры);
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ТекущееДействие) Тогда
		
		ТекстСообщения = НСтр("ru='Не указано действие';uk='Не вказана дія'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ТекущееДействие");
		Возврат;
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Действие указано не верно!';uk='Дія вказана не вірно!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ТекущееДействие");
		Возврат;
		
	КонецЕсли;
	
	Модифицированность = Истина;
	
	УправлениеФормой(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)

	УстановитьПометкуВСтрокахТабличнойЧасти(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)

	УстановитьПометкуВСтрокахТабличнойЧасти(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБезПереноса(Команда)

	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

///////////////////////////////////////////////////////////////////////////////
// Операции с формой и элементами формы

&НаКлиенте
Процедура ПриИзмененииТекущегоДействия()

	Если ТекущееДействие = "ДобавитьИзДокумента" Тогда     			
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияДокумент;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		ВариантЗначенияДокумент = Неопределено;
		
	ИначеЕсли ТекущееДействие = "УстановитьЦеныПоТипу" Тогда       
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияТипЦенНоменклатуры;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияТипЦенНоменклатуры = ДокументТипЦен;
		
	ИначеЕсли ТекущееДействие = "РаспределитьСуммуПоСуммам" Тогда      	
	
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоСумма;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоСумма = Неопределено;
		  
	ИначеЕсли ТекущееДействие = "РаспределитьСуммуПоКоличеству" Тогда         
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоСумма;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоСумма = Неопределено;
		
	ИначеЕсли ТекущееДействие = "ИзменитьРеквизит" Тогда         
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧисловогоРеквизита;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоСумма = Неопределено;

	ИначеЕсли ТекущееДействие = "ИзменитьЦеныНаПроцент" Тогда       
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоПроцент;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоПроцент = Неопределено;
		
	ИначеЕсли ТекущееДействие = "ОкруглитьЦеныДо" Тогда
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияПорядокОкругления;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияПорядокОкругления = ПорядокОругленияПоУмолчанию;
		
	ИначеЕсли ТекущееДействие = "ОкруглитьКоличествоДо" Тогда
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияПорядокОкругления;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияПорядокОкругления = ПорядокОругленияПоУмолчанию;
			
	ИначеЕсли ТекущееДействие = "УстановитьСтавкуНДС" Тогда
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияСтавкаНДС;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияСтавкаНДС = ПредопределенноеЗначение("Справочник.СтавкиНДС.ОсновнаяСтавкаНДС");
		
	ИначеЕсли ТекущееДействие = "УстановитьСкидкуПроцентом" Тогда
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоПроцент;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоПроцент = Неопределено;
		
	ИначеЕсли ТекущееДействие = "УстановитьСкидкуСуммойПоСуммамм"  Тогда
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоСумма;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоСумма = Неопределено;
		
	ИначеЕсли ТекущееДействие = "УстановитьСкидкуСуммойПоКоличеству" Тогда
		  
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияЧислоСумма;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияЧислоСумма = Неопределено;
		
	ИначеЕсли ТекущееДействие = "УстановитьЗакупочныеЦеныПоТипу" Тогда       
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияТипЦенЗакупкиНоменклатуры;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		ВариантЗначенияТипЦенНоменклатуры = ДокументТипЦен;
	Иначе          
		
		Элементы.ГруппаСтраницыПоляВводаВариантаЗначения.ТекущаяСтраница = Элементы.ГруппаПолеВводаВариантаЗначенияПустаяСтраница;
		Элементы.ТекущееДействие.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		

	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ИнициализироватьРеквизитыФормы();
	СформироватьСписокДействий(Элементы.ТекущееДействие.СписокВыбора);

	//загрузим текущее действие из сохраненных настроек
	ВидДокумента = ДокументСсылка.Метаданные().Имя;
	ТекущееДействие = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОбработкаТабличнойЧастиТекущееДействие", ВидДокумента);
	Если Элементы.ТекущееДействие.СписокВыбора.НайтиПоЗначению(ТекущееДействие) = Неопределено Тогда
		ТекущееДействие = Элементы.ТекущееДействие.СписокВыбора[0].Значение;
	КонецЕсли;
	
	УстановитьЗаголовкиКолонок();
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ЗаполнитьОграниченияТипаПолейВводаВариантаЗначения();
	
	Элементы.ГруппаПодвал.Видимость = ПоказыватьСуммуНДС;
	
	СформироватьНадписьЦеныИВалюта(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ОбновитьИтоги(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОграниченияТипаПолейВводаВариантаЗначения()

	// В случае добавления из документа должны выбираться только документы с таб. частью "Товары",
	// в которой есть реквизиты "Номенклатура", "Количество".
	
	МассивНужныхТипов = Новый Массив();
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		
		Если Документ.ТабличныеЧасти.Найти(ИмяТаблицы) <> Неопределено Тогда
			Если ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("Номенклатура", Документ, ИмяТаблицы)
				И ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("Количество",  Документ, ИмяТаблицы) Тогда
				МассивНужныхТипов.Добавить(Тип("ДокументСсылка." + Документ.Имя));
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ВариантЗначенияДокумент.ОграничениеТипа = Новый ОписаниеТипов(МассивНужныхТипов);

КонецПроцедуры 

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	
	ПеренестиВДокумент = Ложь;

	ДокументМетаданные = ДокументСсылка.Метаданные();
	
	ВалютаРегламентированногоУчета 	= ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ОбрабатыватьПоТипуЦен = ОбщегоНазначенияТК.ЕстьРеквизитДокумента("ТипЦен", ДокументМетаданные);
	Если ЗначениеЗаполнено(ДокументСклад) Тогда
		ДокументТипЦенРозничнойТорговли = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСклад, "ТипЦенРозничнойТорговли");
	КонецЕсли;
	ПорядокОругленияПоУмолчанию = Перечисления.ПорядкиОкругления.Окр0_01;
	ПорядокОругленияПоУмолчаниюВРознице = Перечисления.ПорядкиОкругления.Окр0_01;
	
	ПоказыватьКоличество 			= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("Количество", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьЦену 					= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("Цена", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьСумму 				= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("Сумма", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьСтавкуНДС 			= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьСуммуНДС 				= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("СуммаНДС", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьВалюту 				= ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("ВалютаДокумента", 
													ДокументМетаданные, ИмяТаблицы);
	ПоказыватьСкидки                = ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("СуммаСкидки", 
													ДокументМетаданные, ИмяТаблицы);

	ПоказыватьСуммуАкцизногоНалога  = ОбщегоНазначенияТК.ЕстьРеквизитТабЧастиДокумента("СуммаАкцизногоНалога", 
													ДокументМетаданные, ИмяТаблицы);
	
	СписокОпераций = Элементы.Операция.СписокВыбора;
	СписокОпераций.Добавить("Добавить", "Добавить");
	СписокОпераций.Добавить("Вычесть", "Вычесть");
	СписокОпераций.Добавить("Умножить", "Умножить");
	СписокОпераций.Добавить("Разделить", "Разделить");
	
	СписокОпераций = Элементы.Операция1.СписокВыбора;
	СписокОпераций.Добавить("Добавить", "Добавить");
	СписокОпераций.Добавить("Вычесть", "Вычесть");
	СписокОпераций.Добавить("Умножить", "Умножить");
	СписокОпераций.Добавить("Разделить", "Разделить");

	СписокВыбораРеквизитов = Элементы.ВыборРеквизита.СписокВыбора;
	//ищем числовые колонки
	МассивТипов = ПолучитьРеквизиты("Объект.Товары");
	
	СписокДоступныхКолонок = Новый Массив;
	Для Каждого Реквизит Из Элементы.Товары.ПодчиненныеЭлементы Цикл
		Если Реквизит.Доступность = Истина Тогда
			СписокДоступныхКолонок.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Колонка Из МассивТипов Цикл
		//проверяем колонки на доступность изменения
		МожноМенять = СписокДоступныхКолонок.Найти("Товары"+Колонка.Имя);
		Если МожноМенять <> Неопределено Тогда
			Если Колонка.ТипЗначения.СодержитТип(Тип("Число")) И Колонка.Имя <> "НомерСтроки" Тогда					
				СписокВыбораРеквизитов.Добавить(Колонка.Имя, Колонка.Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ЗначениеЗаполнено(ДокументОрганизация) Тогда
		ОтборОрганизация = ДокументОрганизация;
	Иначе
		// случай вызова из документа УстановкаЦенНоменклатуры или без выбранной организации в документе
		ОтборОрганизация = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(),"ОсновнаяОрганизация",,);
		Если НЕ ЗначениеЗаполнено(ОтборОрганизация) Тогда
			ОтборОрганизация = ПолучитьСписокОрганизаций();	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// Обработчики действий обработки

&НаСервере
Процедура ДобавитьДанныеИзДокумента(ДокументИсточник)

	Если НЕ ЗначениеЗаполнено(ДокументИсточник) Тогда
		Возврат;
	КонецЕсли; 
	
	МетаданныеДокументаИсточника 	= ДокументИсточник.Метаданные();
	
	// получим валюту документа-источника
	Если ОбщегоНазначенияТК.ЕстьРеквизитДокумента("ВалютаДокумента", МетаданныеДокументаИсточника) Тогда 
		ВалютаДокументаИсточника 	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументИсточник, "ВалютаДокумента");
		Если ОбщегоНазначенияТК.ЕстьРеквизитДокумента("КурсДокумента", МетаданныеДокументаИсточника) Тогда
			КурсДокументаИсточника		= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументИсточник, "КурсДокумента");
		Иначе
			КратностьДокументаИсточника	= 1;
		КонецЕсли;
	Иначе
		ВалютаДокументаИсточника 	= ВалютаРегламентированногоУчета; 
		КурсДокументаИсточника		= 1;
	КонецЕсли;
	
	СуммаВключаетНДСИсточника	= Истина;
	
	// получим табличную часть "Товары" документа-источника
	ТекстПолей = "";
	РеквизитыТоваровИсточника = МетаданныеДокументаИсточника.ТабличныеЧасти.Товары.Реквизиты;
	СтруктураРеквизитов = ПолучитьСтруктуруОбрабатываемыхРеквизитовТабличнойЧасти(РеквизитыТоваровИсточника); 
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		ИмяПоля   = ?(ЗначениеЗаполнено(КлючИЗначение.Значение), СокрЛП(КлючИЗначение.Значение), СокрЛП(КлючИЗначение.Ключ));
		Псевдоним = СокрЛП(КлючИЗначение.Ключ);
		ТекстПолей  = ТекстПолей + ?(ПустаяСтрока(ТекстПолей), "", ",") + "
			|	" + ИмяПоля + " КАК " + Псевдоним;
	КонецЦикла;
 	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ТекстПолей
	|ИЗ
	|	&ТабличнаяЧастьТоварыДокументаИсточника КАК ПсевдонимТовары
	|ГДЕ
	|	ПсевдонимТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолей", ТекстПолей);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТабличнаяЧастьТоварыДокументаИсточника", 
													МетаданныеДокументаИсточника.ПолноеИмя() + ".Товары");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументИсточник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаТоваровДокумента = РезультатЗапроса.Выгрузить();

	// добавим полученную таблицу в табличную часть обработки
	Для Каждого СтрокаТовара ИЗ ТаблицаТоваровДокумента Цикл
		
		// Ищем добавляемую позицию в существующей таблице по номенклатуре и по цене, в случае, когда цена видима.
		// Если найдем - увеличим количество; не найдем - добавим новую строку.
		СтруктураОтбора = Новый Структура("Номенклатура");
		СтруктураОтбора.Номенклатура  = СтрокаТовара.Номенклатура;
		
		// производим поиск строк по структуре отбора
		СтрокаТабличнойЧасти = ОбработкаТабличныхЧастейКлиентСервер.НайтиСтрокуТабЧасти(Объект, ИмяТаблицы, СтруктураОтбора);
		Если НЕ СтрокаТабличнойЧасти = Неопределено Тогда
			
			// Нашли, увеличиваем количество в первой найденной строке
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
			// Рассчитаем связанные реквизиты табличной части.
			РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);
			
			//Если ПоказыватьСуммуВРознице И ПоказыватьКоличество Тогда
			//	СтрокаТабличнойЧасти.СуммаВРознице = СтрокаТабличнойЧасти.ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
			//КонецЕсли;
			
		Иначе
			
			// добавляем новую строку.
			НоваяСтрока = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
			
			// новые строки будут по умолчанию помечены.
			НоваяСтрока.Пометка = Истина;
			
			// пересчитаем связанные реквизиты табличной части.
			
			Если ПоказыватьКоличество Тогда
				
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.ЕдиницаИзмерения) Тогда
	            	НоваяСтрока.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Номенклатура, "БазоваяЕдиницаИзмерения"); 
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(НоваяСтрока.Коэффициент) Тогда
					НоваяСтрока.Коэффициент = 1;	
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПоказыватьСтавкуНДС Тогда
				
				ПараметрыОбъекта = Новый Структура("Дата, Организация", ДокументДата, ДокументОрганизация);
				ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(НоваяСтрока, ПараметрыОбъекта, ИмяТаблицы, ДокументСсылка.Метаданные());
				
			КонецЕсли;
			
			Если ПоказыватьВалюту Тогда
				
				Если СтруктураРеквизитов.Свойство("Валюта") И ЗначениеЗаполнено(СтрокаТовара.Валюта) Тогда
					НоваяСтрока.Валюта = СтрокаТовара.Валюта;
				ИначеЕсли ЗначениеЗаполнено(ВалютаДокументаИсточника) Тогда
					НоваяСтрока.Валюта = ВалютаДокументаИсточника;	
				Иначе
					НоваяСтрока.Валюта = ВалютаРегламентированногоУчета;
				КонецЕсли;
				
			КонецЕсли;
						
			РассчитатьСуммыСтрокиТабличнойЧасти(НоваяСтрока, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныПоТипу(УстанавливаемыйТипЦен)

	Если НЕ ЗначениеЗаполнено(УстанавливаемыйТипЦен) Тогда
		Возврат;
	КонецЕсли; 
	
	//	Получим цены нужного типа
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивНоменклатуры", 	Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ТипЦен", 				УстанавливаемыйТипЦен);
	Запрос.УстановитьПараметр("ДатаЦен", 				ДокументДата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
	|				&ДатаЦен,
	|				Номенклатура В (&МассивНоменклатуры)
	|					И ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	|		ПО (ЦеныСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
	|ГДЕ
	|	СправочникНоменклатура.Ссылка В(&МассивНоменклатуры)";

	ВыборкаИзЗапроса = Запрос.Выполнить().Выбрать();

	// Найдем у данной номенклатуры значение цены выбранного типа.
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда

			// Сбросим выборку для последующего поиска.
			ВыборкаИзЗапроса.Сбросить();
			
			// Ищем в выборке номенклатуру.
			Если ВыборкаИзЗапроса.НайтиСледующий(СтрокаТабличнойЧасти.Номенклатура, "Номенклатура")  Тогда
				ЦенаБазовая = ВыборкаИзЗапроса.Цена;
			Иначе
				ЦенаБазовая = 0;
			КонецЕсли;

			СтрокаТабличнойЧасти.Цена = ЦенаБазовая * ?(СтрокаТабличнойЧасти.Коэффициент = 0, 1, СтрокаТабличнойЧасти.Коэффициент);

			// пересчитаем связанные реквизиты
			РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры  

&НаСервере
Процедура ИзменитьРеквизит(Значение, Операция)
	

	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		
		Если СтрокаТаблицы.Пометка Тогда
			ИмяВыбранногоЗначения = ВыборРеквизита;
			Если ВПроцентах Тогда
				Если Операция = "Добавить" Тогда 
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * (100 + Значение) /100
				ИначеЕсли Операция = "Вычесть" Тогда
					Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] - Процент;
				ИначеЕсли Операция = "Умножить" Тогда
					Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * Процент;
				ИначеЕсли Операция = "Разделить" Тогда
					Если Значение = 0 Тогда
						Ошибки = "Операция деления на 0 запрещена!";
					Иначе
						Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
						СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] / Процент;
					КонецЕсли;
				КонецЕсли;																													
			ИначеЕсли ВПроцентах = Ложь Тогда
				Если Операция = "Добавить" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] + Значение;
				ИначеЕсли Операция = "Вычесть" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] - Значение;
				ИначеЕсли Операция = "Умножить" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * Значение;
				ИначеЕсли Операция = "Разделить" Тогда
					Если Значение = 0 Тогда
						Ошибки = "Операция деления на 0 запрещена!";
					Иначе
						СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] / Значение;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		Если ИмяВыбранногоЗначения ="Количество" Тогда
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		ИначеЕсли ИмяВыбранногоЗначения ="Цена" Тогда 
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		ИначеЕсли ИмяВыбранногоЗначения ="ЦенаБезНалогов" Тогда
			СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		ИначеЕсли ИмяВыбранногоЗначения ="СуммаСкидки" Тогда
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
		ИначеЕсли ИмяВыбранногоЗначения ="СтавкаНДС" Тогда
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		ИначеЕсли ИмяВыбранногоЗначения ="Сумма" Тогда
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		ИначеЕсли ИмяВыбранногоЗначения ="СуммаБезНалогов" Тогда
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
			СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		КонецЕсли;
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, Неопределено);		

	КонецЦикла; 

	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныЗакупкиПоТипу(Значение,Операция,УстанавливаемыйТипЦен)
	
	Если НЕ ЗначениеЗаполнено(УстанавливаемыйТипЦен) Тогда
		Возврат;
	КонецЕсли; 
	
	//	Получим цены нужного типа
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивНоменклатуры", 	Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ТипЦен", 				УстанавливаемыйТипЦен);
	Запрос.УстановитьПараметр("ДатаЦен", 				ДокументДата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена,
	|	ЦеныСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ Вт_Цены
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныКонтрагентов.СрезПоследних(
	|				&ДатаЦен,
	|				Номенклатура В (&МассивНоменклатуры)
	|					И ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	|		ПО (ЦеныСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
	|ГДЕ
	|	СправочникНоменклатура.Ссылка В(&МассивНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	Вт_Цены.Цена КАК Цена
	|ИЗ
	|	(ВЫБРАТЬ
	|		Вт_Цены.Номенклатура КАК Номенклатура,
	|		МАКСИМУМ(Вт_Цены.Период) КАК Период
	|	ИЗ
	|		Вт_Цены КАК Вт_Цены
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Вт_Цены.Номенклатура) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Цены КАК Вт_Цены
	|		ПО ВложенныйЗапрос.Номенклатура = Вт_Цены.Номенклатура
	|			И ВложенныйЗапрос.Период = Вт_Цены.Период";
	
	ВыборкаИзЗапроса = Запрос.Выполнить().Выбрать();
	
	// Найдем у данной номенклатуры значение цены выбранного типа.
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			// Сбросим выборку для последующего поиска.
			ВыборкаИзЗапроса.Сбросить();
			
			// Ищем в выборке номенклатуру.
			Если ВыборкаИзЗапроса.НайтиСледующий(СтрокаТабличнойЧасти.Номенклатура, "Номенклатура")  Тогда
				ЦенаБазовая = ВыборкаИзЗапроса.Цена;
			Иначе
				ЦенаБазовая = 0;
			КонецЕсли;
			
			СтрокаТабличнойЧасти.Цена = ЦенаБазовая * ?(СтрокаТабличнойЧасти.Коэффициент = 0, 1, СтрокаТабличнойЧасти.Коэффициент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		
		Если СтрокаТаблицы.Пометка Тогда
			ИмяВыбранногоЗначения = "Цена";
			Если ВПроцентах Тогда
				Если Операция = "Добавить" Тогда 
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * (100 + Значение) /100
				ИначеЕсли Операция = "Вычесть" Тогда
					Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] - Процент;
				ИначеЕсли Операция = "Умножить" Тогда
					Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * Процент;
				ИначеЕсли Операция = "Разделить" Тогда
					Если Значение = 0 Тогда
						Ошибки = "Операция деления на 0 запрещена!";
					Иначе
						Процент = СтрокаТаблицы[ИмяВыбранногоЗначения] / 100 * Значение;
						СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] / Процент;
					КонецЕсли;
				КонецЕсли;																													
			ИначеЕсли ВПроцентах = Ложь Тогда
				Если Операция = "Добавить" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] + Значение;
				ИначеЕсли Операция = "Вычесть" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] - Значение;
				ИначеЕсли Операция = "Умножить" Тогда
					СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] * Значение;
				ИначеЕсли Операция = "Разделить" Тогда
					Если Значение = 0 Тогда
						Ошибки = "Операция деления на 0 запрещена!";
					Иначе
						СтрокаТаблицы[ИмяВыбранногоЗначения] = СтрокаТаблицы[ИмяВыбранногоЗначения] / Значение;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, Неопределено);		
		
	КонецЦикла; 


КонецПроцедуры  

&НаСервере
Процедура УстановитьЦеныВРозницеПоТипу(УстанавливаемыйТипЦенВРознице)

	

КонецПроцедуры

&НаСервере
Процедура ОкруглитьЦену(ПорядокОкругленияЦены)

	Если НЕ ЗначениеЗаполнено(ПорядокОкругленияЦены) Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			СтрокаТабличнойЧасти.Цена = Обработки.ИзменениеТаблицыТоваров.ОкруглитьЦену(СтрокаТабличнойЧасти.Цена, 
														ПорядокОкругленияЦены, ОкруглятьВБольшуюСторону); 
			
			//// пересчитаем связанные реквизиты.
			РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОкруглитьКоличетсво(ПорядокОкругленияЦены)

	Если НЕ ЗначениеЗаполнено(ПорядокОкругленияЦены) Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			СтрокаТабличнойЧасти.Количество = Обработки.ИзменениеТаблицыТоваров.ОкруглитьЦену(СтрокаТабличнойЧасти.Количество, 
			ПорядокОкругленияЦены, ОкруглятьВБольшуюСторону); 
			
			СтруктураДействий = Новый Структура;
			
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, Неопределено);
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ИзменитьЦенуНаПроцент(ПроцентИзменения)

	Если НЕ ЗначениеЗаполнено(ПроцентИзменения) Тогда
		Возврат;
	КонецЕсли; 

	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * (100 + ПроцентИзменения) / 100;
			
			// пересчитаем связанные реквизиты.
			РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);
			
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры 

&НаСервере
Процедура РаспределитьСуммуПоКолонке(ИмяКолонки, СуммаРаспределения)
	
	Если НЕ ЗначениеЗаполнено(СуммаРаспределения) Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ (ИмяКолонки = "Количество" ИЛИ ИмяКолонки = "Сумма") Тогда
		Возврат;
	КонецЕсли; 
	
	// Посчитаем общую сумму помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти[ИмяКолонки];
		КонецЕсли; 
	КонецЦикла; 
	
	Если ОбщаяСумма = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Общая сумма помеченных строк нулевая! 
		|Распределение невозможно';uk='Загальна сума позначених рядків нульова! 
		|Розподіл неможливий'"));
		Возврат;
	КонецЕсли; 
		
	// Распределение
	СтрокаМаксимальнойСуммы = Неопределено;  // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма       = 0; 			 // Значение максимальной суммы.
	НепогашеннаяСумма       = СуммаРаспределения;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда

			Дельта = СуммаРаспределения * СтрокаТабличнойЧасти[ИмяКолонки] / ОбщаяСумма;
			
			// Если Дельта по модулю оказалась больше, чем осталось погасить
			Если Дельта < 0 Тогда
				Дельта = Макс(НепогашеннаяСумма, Дельта)
			Иначе
				Дельта = Мин(НепогашеннаяСумма, Дельта)
			КонецЕсли; 

			// Проверим текущую сумму на максимум.
			Если СтрокаТабличнойЧасти.Сумма > МаксимальнаяСумма  Тогда
				МаксимальнаяСумма       = СтрокаТабличнойЧасти.Сумма;
				СтрокаМаксимальнойСуммы = СтрокаТабличнойЧасти;
			КонецЕсли;

			// Увеличиваем значение и запоминаем старое.
			ТекущаяСумма             	= СтрокаТабличнойЧасти.Сумма;
			СтрокаТабличнойЧасти.Сумма 	= СтрокаТабличнойЧасти.Сумма + Дельта;
			
			// Остаток нераспределенной суммы надо уменьшать на значение реального изменения
			НепогашеннаяСумма = НепогашеннаяСумма - (СтрокаТабличнойЧасти.Сумма - ТекущаяСумма);

			// Пересчитываем связанные реквизиты.
			ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммыТабЧасти(СтрокаТабличнойЧасти, ПоказыватьСкидки);

			Если ПоказыватьСуммуНДС Тогда
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
				ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЦикла;  
		
	// Если что-то осталось, относим на строку с максимальной суммой.
	Если НепогашеннаяСумма > 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда

		СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НепогашеннаяСумма;
		ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммыТабЧасти(СтрокаМаксимальнойСуммы, ПоказыватьСкидки);
		Если ПоказыватьСуммуНДС Тогда
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаМаксимальнойСуммы, ДокументСуммаВключаетНДС);
			ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаМаксимальнойСуммы, ДокументСуммаВключаетНДС);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтавкуНДС(НоваяСтавкаНДС)

	Если НЕ ЗначениеЗаполнено(НоваяСтавкаНДС) Тогда
		Возврат;
	КонецЕсли; 

	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			СтрокаТабличнойЧасти.СтавкаНДС = НоваяСтавкаНДС;
				СтруктураДействий = Новый Структура;

			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, Неопределено);
			
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// Прочие операции

&НаСервере
Процедура СформироватьСписокДействий(СписокДействий)

	// Заполним список возможных действий с табличной частью.
	СписокДействий.Очистить();

	СписокДействий.Добавить("ДобавитьИзДокумента",						НСтр("ru='Добавить товары из документа';uk='Додати товари з документа'"));
	СписокДействий.Добавить("ИзменитьРеквизит",							НСтр("ru='Изменить реквизит';uk='Змінити реквізит'"));
	СписокДействий.Добавить("ОкруглитьКоличествоДо", 					НСтр("ru='Округлить количество до';uk='Округлити кількість до'"));

	Если ПоказыватьЦену И ОбрабатыватьПоТипуЦен Тогда
		СписокДействий.Добавить("УстановитьЦеныПоТипу", 				НСтр("ru='Установить цены по типу';uk='Встановити ціни по типу'"));
	КонецЕсли;
	
	Если ПоказыватьСтавкуНДС Тогда
		СписокДействий.Добавить("УстановитьСтавкуНДС", 					НСтр("ru='Установить ставку НДС';uk='Встановити ставку ПДВ'"));
	КонецЕсли;
	
	Если ПоказыватьЦену Тогда
		СписокДействий.Добавить("ИзменитьЦеныНаПроцент",				НСтр("ru='Изменить цены на процент';uk='Змінити ціни на відсоток'"));
	КонецЕсли;
	СписокДействий.Добавить("ОкруглитьКоличествоДо", 					НСтр("ru='Округлить количество до';uk='Округлити кількість до'"));

	Если ПоказыватьЦену И ОбрабатыватьПоТипуЦен Тогда
		СписокДействий.Добавить("ОкруглитьЦеныДо", 						НСтр("ru='Округлить цены до';uk='Округлити ціни до'"));
	КонецЕсли;
	
	Если ПоказыватьСумму Тогда
		
		СписокДействий.Добавить("РаспределитьСуммуПоКоличеству", 		НСтр("ru='Распределить сумму по количеству';uk='Розподілити суму по кількості'"));
		
		Если ПоказыватьСуммуНДС Тогда
			Если ДокументСуммаВключаетНДС Тогда 
				СписокДействий.Добавить("РаспределитьСуммуПоСуммам", 	НСтр("ru='Распределить сумму по сумме с НДС';uk='Розподілити суму по сумі з ПДВ'"));
			Иначе
				СписокДействий.Добавить("РаспределитьСуммуПоСуммам", 	НСтр("ru='Распределить сумму по сумме без НДС';uk='Розподілити суму по сумі без ПДВ'"));
			КонецЕсли;
		Иначе
			СписокДействий.Добавить("РаспределитьСуммуПоСуммам", 		НСтр("ru='Распределить сумму по сумме';uk='Розподілити суму по сумі'"));
		КонецЕсли;

	КонецЕсли;
	
	Если ПоказыватьСкидки Тогда
		СписокДействий.Добавить("УстановитьСкидкуПроцентом",            НСтр("ru='Установить скидку процентом';uk='Встановити знижку відсотком'"));
		СписокДействий.Добавить("УстановитьСкидкуСуммойПоСуммамм",      НСтр("ru='Установить скидку суммой (по суммам)';uk='Встановити знижку сумою (по сумах)'"));
		СписокДействий.Добавить("УстановитьСкидкуСуммойПоКоличеству",   НСтр("ru='Установить скидку суммой (по количеству)';uk='Встановити знижку сумою (по кількості)'"));
	КонецЕсли;

	СписокДействий.Добавить("УстановитьЗакупочныеЦеныПоТипу", 			НСтр("ru='Установить закупочные цены по типу';uk='Встановити ціни закупівлі по типу'"));

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, СуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога)

	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);

	Если ПоказыватьСуммуАкцизногоНалога Тогда
        ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСиАкцизногоНалогаТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
		ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
	ИначеЕсли ПоказыватьСуммуНДС Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
		ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС)

	СтрокаТабличнойЧасти.СуммаВсего = СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);

КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруОбрабатываемыхРеквизитовТабличнойЧасти(РеквизитыТабличнойЧасти)

	//ключ - псевдоним в запросе, значение - имя реквизита
	СтруктураРеквизитов = Новый Структура;
	
	СтруктураРеквизитов.Вставить("Номенклатура", 	"Номенклатура");
	СтруктураРеквизитов.Вставить("Количество", 		"Количество");
	
	Если РеквизитыТабличнойЧасти.Найти("КоличествоМест") <> Неопределено Тогда
		СтруктураРеквизитов.Вставить("КоличествоМест", "КоличествоМест");
	КонецЕсли;

	Если РеквизитыТабличнойЧасти.Найти("ЕдиницаИзмерения") <> Неопределено Тогда
		СтруктураРеквизитов.Вставить("ЕдиницаИзмерения", "ЕдиницаИзмерения");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("Коэффициент") <> Неопределено Тогда
		СтруктураРеквизитов.Вставить("Коэффициент", "Коэффициент");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("Цена") <> Неопределено Тогда
		СтруктураРеквизитов.Вставить("Цена", "Цена");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("Сумма")  <> Неопределено И ПоказыватьСумму Тогда
		СтруктураРеквизитов.Вставить("Сумма", "Сумма");
		СтруктураРеквизитов.Вставить("СуммаБезСкидки", "Сумма");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("СтавкаНДС")  <> Неопределено И ПоказыватьСтавкуНДС Тогда
		СтруктураРеквизитов.Вставить("СтавкаНДС", "СтавкаНДС");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("СуммаНДС")  <> Неопределено И ПоказыватьСуммуНДС Тогда
		СтруктураРеквизитов.Вставить("СуммаНДС", "СуммаНДС");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("ВалютаДокумента")  <> Неопределено И ПоказыватьВалюту Тогда
		СтруктураРеквизитов.Вставить("ВалютаДокумента", "ВалютаДокумента");
	КонецЕсли;
	
	Если РеквизитыТабличнойЧасти.Найти("СуммаСкидки")  <> Неопределено И ПоказыватьСкидки Тогда
		СтруктураРеквизитов.Вставить("СуммаСкидки", "СуммаСкидки");
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;

КонецФункции 
 
&НаСервере
Процедура УстановитьПометкуВСтрокахТабличнойЧасти(НоваяПометка)

	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		СтрокаТабличнойЧасти.Пометка = НоваяПометка;
	КонецЦикла; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

	Если ПоказыватьСуммуНДС Тогда
		Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
			ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Если Форма.ПоказыватьСуммуНДС Тогда
		Форма.ИтогиВсего = Форма.Объект.Товары.Итог("СуммаВсего");
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьЦеныИВалюта(Форма)
	
	СтруктураНадписи = Новый Структура(
		"ВалютаДокумента, Курс,  СуммаВключаетНДС, ВалютаРегламентированногоУчета",
		Форма.ДокументВалюта,
		Форма.ДокументКурс,
		Форма.ДокументСуммаВключаетНДС,
		Форма.ВалютаРегламентированногоУчета);
	Форма.ЦеныИВалюта = ОбщегоНазначенияТККлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере(ПараметрыСтроки)

	МетаданныеДокумента = ДокументСсылка.Метаданные();

	Если ПоказыватьСтавкуНДС Тогда
		ПараметрыОбъекта = Новый Структура("Дата, Организация", ДокументДата, ДокументОрганизация);
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(ПараметрыСтроки, ПараметрыОбъекта, ИмяТаблицы, МетаданныеДокумента);
	КонецЕсли;
	
	
КонецПроцедуры 

&НаСервере
Процедура ПриЗакрытииФормыНаСервере()

	ВидДокумента = ДокументСсылка.Метаданные().Имя;
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбработкаТабличнойЧастиТекущееДействие", ВидДокумента, ТекущееДействие);

	Если ПеренестиВДокумент Тогда
		ТаблицаТовары = СформироватьТаблицуДляПереносаВДокумент();
		ПоместитьВоВременноеХранилище(ТаблицаТовары, АдресХранилищаТовары);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция СформироватьТаблицуДляПереносаВДокумент()

	ТаблицаОбработки = Объект.Товары.Выгрузить();
	
	ТаблицаДокумента = ПолучитьИзВременногоХранилища(АдресХранилищаТовары);
		
	// Создадим структуру, которая будет содержать имена колонок присутствующих в 
	// документе, но не присутствующих в ТЧ обработки. 
	СтруктураНовыхКолонок = Новый Структура;
	
	Для Каждого КолонкаТаблицыДокумента Из ТаблицаДокумента.Колонки Цикл
		
		// Если колонка есть в таблице документа, но ее нет в таблице, выгруженной
		// из обработки и нет в структуре новых колонок, тогда добавим ее в таблицу
		// и в структуру
		Если ТаблицаОбработки.Колонки.Найти(КолонкаТаблицыДокумента.Имя) = Неопределено
			И НЕ СтруктураНовыхКолонок.Свойство(КолонкаТаблицыДокумента.Имя) Тогда
			ТаблицаОбработки.Колонки.Добавить(КолонкаТаблицыДокумента.Имя);
			СтруктураНовыхКолонок.Вставить(КолонкаТаблицыДокумента.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	// Идем по строкам табличной части и обрабатываем строки, в которых заполнен
	// реквизит НомерДокумента. Эти строки были выгружены из документа.
	Для каждого СтрокаТаблицы Из ТаблицаОбработки Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.НомерСтрокиДокумента) Тогда
			
			// Если номер строки заполнен, находим соответствующую строку в табличной 
			// части документа
			СтрокаТаблицыДокумента = ТаблицаДокумента[СтрокаТаблицы.НомерСтрокиДокумента - 1];

			// Во избежание проблем с подчиненными реквизитами, строка ТЧ обработки 
			// считается соответствующей строке ТЧ документа, только в том случае,если
			// номенклатура не изменилась. Если номенклатура в строке изменилась, 
			// считается, что это новая строка.
			Если СтрокаТаблицыДокумента.Номенклатура = СтрокаТаблицы.Номенклатура Тогда
				
				// Теперь пройдем по колонкам структуры новых колонок
				Для Каждого НоваяКолонкаТаблицы Из СтруктураНовыхКолонок Цикл
					СтрокаТаблицы[НоваяКолонкаТаблицы.Ключ] = СтрокаТаблицыДокумента[НоваяКолонкаТаблицы.Ключ];
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаОбработки;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокОрганизаций()
	
	СписокОрганизаций = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокОрганизаций.Добавить(Выборка.Организация);
	КонецЦикла;
	
	Возврат СписокОрганизаций;
	
КонецФункции

&НаКлиенте
Процедура ВопросПеренестиИзмененияЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт

	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезСкидкиПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаБезСкидкиТабЧасти(СтрокаТабличнойЧасти);

	Если ПоказыватьСуммуНДС Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
		ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаСкидкиТабЧасти(СтрокаТабличнойЧасти);

	Если ПоказыватьСуммуНДС Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
		ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ДокументСуммаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	
   	СтандартнаяОбработка = Ложь;
	
	СтрокаТаблицы = Элементы[ИмяТаблицы].ТекущиеДанные;

	СписокВыбора = Новый СписокЗначений();	
	СписокВыбора.Добавить(1, НСтр("ru='Скидка процентом';uk='Знижка відсотком'"));
	СписокВыбора.Добавить(2, НСтр("ru='Скидка суммой';uk='Знижка сумою'"));
	
	ДополнительныеПараметры = Новый Структура("Форма , ИмяТаблицы, СтрокаТаблицы", ЭтаФорма, ИмяТаблицы, СтрокаТаблицы);
	Оповещение = Новый ОписаниеОповещения("СуммаСкидкиНачалоВыбораВыбратьИзСпискаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ЭтаФорма.ПоказатьВыборИзСписка(Оповещение, СписокВыбора, Элемент, СписокВыбора[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСкидкиНачалоВыбораВыбратьИзСпискаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
    Форма = ДополнительныеПараметры.Форма;
    ИмяТаблицы = ДополнительныеПараметры.ИмяТаблицы;
    СтрокаТаблицы = ДополнительныеПараметры.СтрокаТаблицы;
	
	Если НЕ РезультатВыбора = Неопределено Тогда
		
		ВариантСкидки = РезультатВыбора.Значение;
		
		ЭтоВводПроцента = (ВариантСкидки = 1);
		НачальноеЗначение = ?(ЭтоВводПроцента, 0, СтрокаТаблицы.СуммаСкидки);
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Введите %1 скидки';uk='Уведіть %1 знижки'"), 
				?(ЭтоВводПроцента, НСтр("ru='процент';uk='відсоток'"),НСтр("ru='сумму';uk='суму'")));
				
		ДополнительныеПараметры = Новый Структура("Форма, ИмяТаблицы, СтрокаТаблицы, ВариантСкидки,  НачальноеЗначение", Форма, ИмяТаблицы, СтрокаТаблицы, ВариантСкидки, НачальноеЗначение);
		Оповещение = Новый ОписаниеОповещения("СуммаСкидкиНачалоВыбораВводЧислаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВводЧисла(Оповещение, НачальноеЗначение, Заголовок);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСкидкиНачалоВыбораВводЧислаЗавершение(Число, ДополнительныеПараметры) Экспорт
    
    Форма = ДополнительныеПараметры.Форма;
    ИмяТаблицы = ДополнительныеПараметры.ИмяТаблицы;
    СтрокаТаблицы = ДополнительныеПараметры.СтрокаТаблицы;
    ВариантСкидки = ДополнительныеПараметры.ВариантСкидки;
	НачальноеЗначение = ДополнительныеПараметры.НачальноеЗначение;
	
    Если (Число <> Неопределено) Тогда
        
    	ВведенноеЗначение = ?(Число = Неопределено, НачальноеЗначение, Число);
        Если ВариантСкидки = 2 Тогда
            СтрокаТаблицы.СуммаСкидки = ВведенноеЗначение;
        Иначе// процент	
            СтрокаТаблицы.СуммаСкидки = СтрокаТаблицы.СуммаБезСкидки * ВведенноеЗначение/100;
        КонецЕсли;	
        
		ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаСкидкиТабЧасти(СтрокаТаблицы);

		Если ПоказыватьСуммуНДС Тогда
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, ДокументСуммаВключаетНДС);
			ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТаблицы, ДокументСуммаВключаетНДС);
		КонецЕсли;
        
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)
	
	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресОбработаннойТабличнойЧастиТоварыВХранилище);
	Объект.Товары.Загрузить(ТаблицаОбработки);
	
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиКолонок()
	
	Если ПоказыватьСуммуНДС Тогда
		ЗаголовокЦена = ?(ПлательщикНДС, ?(ДокументСуммаВключаетНДС, НСтр("ru='Цена с НДС';uk='Ціна з ПДВ'"), НСтр("ru='Цена без НДС';uk='Ціна без ПДВ'")), НСтр("ru='Цена';uk='Ціна'"));
		ЗаголовокСумма = ?(ПлательщикНДС, ?(ДокументСуммаВключаетНДС, НСтр("ru='Сумма с НДС';uk='Сума з ПДВ'"), НСтр("ru='Сумма без НДС';uk='Сума без ПДВ'")), НСтр("ru='Сумма';uk='Сума'"));
	Иначе
		ЗаголовокЦена   = НСтр("ru='Цена';uk='Ціна'");
		ЗаголовокСумма	= НСтр("ru='Сумма';uk='Сума'");
	КонецЕсли;
	
	Элементы.ТоварыЦена.Заголовок 	= ЗаголовокЦена;
	Элементы.ТоварыСумма.Заголовок 	= ЗаголовокСумма;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСкидкуПроцентом(ПроцентСкидки)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда
			
			СтрокаТабличнойЧасти.СуммаСкидки = СтрокаТабличнойЧасти.СуммаБезСкидки * ПроцентСкидки /100;
			
			// пересчитаем связанные реквизиты.
			РассчитатьСуммыСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПоказыватьСуммуНДС, ДокументСуммаВключаетНДС, ПоказыватьСуммуАкцизногоНалога);
			
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьСуммуСкидкиПоКолонке(ИмяКолонки, СуммаРаспределения)
	
	Если НЕ ЗначениеЗаполнено(СуммаРаспределения) Тогда
		Возврат;
	КонецЕсли; 
	
	Если НЕ (ИмяКолонки = "Количество" ИЛИ ИмяКолонки = "Сумма") Тогда
		Возврат;
	КонецЕсли; 
	
	// Посчитаем общую сумму помеченных позиций
	ОбщаяСумма = 0;
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда
			ОбщаяСумма = ОбщаяСумма + СтрокаТабличнойЧасти[ИмяКолонки];
		КонецЕсли; 
	КонецЦикла; 
	
	Если ОбщаяСумма = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Общая сумма помеченных строк нулевая! 
|Распределение невозможно';uk='Загальна сума позначених рядків нульова! 
|Розподіл неможливий'"));
		Возврат;
	КонецЕсли; 
		
	// Распределение
	СтрокаМаксимальнойСуммы = Неопределено;  // На эту строку будем относить остаток после распределения (ошибки округления)
	МаксимальнаяСумма       = 0; 			 // Значение максимальной суммы.
	НепогашеннаяСумма       = СуммаРаспределения;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		
		Если СтрокаТабличнойЧасти.Пометка Тогда

			Дельта = СуммаРаспределения * СтрокаТабличнойЧасти[ИмяКолонки] / ОбщаяСумма;
			
			// Если Дельта по модулю оказалась больше, чем осталось погасить
			Если Дельта < 0 Тогда
				Дельта = Макс(НепогашеннаяСумма, Дельта)
			Иначе
				Дельта = Мин(НепогашеннаяСумма, Дельта)
			КонецЕсли; 

			// Проверим текущую сумму на максимум.
			Если СтрокаТабличнойЧасти[ИмяКолонки] > МаксимальнаяСумма  Тогда
				МаксимальнаяСумма       = СтрокаТабличнойЧасти[ИмяКолонки];
				СтрокаМаксимальнойСуммы = СтрокаТабличнойЧасти;
			КонецЕсли;

			// Увеличиваем значение и запоминаем старое.
			СтрокаТабличнойЧасти.СуммаСкидки = Дельта;
			
			// Остаток нераспределенной суммы надо уменьшать на значение реального изменения
			НепогашеннаяСумма = НепогашеннаяСумма - СтрокаТабличнойЧасти.СуммаСкидки;

			// Пересчитываем связанные реквизиты.
			ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаСкидкиТабЧасти(СтрокаТабличнойЧасти);
			Если ПоказыватьСуммуНДС Тогда
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Истина);
				ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, Истина);
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЦикла;  
		
	// Если что-то осталось, относим на строку с максимальной суммой.
	Если НепогашеннаяСумма > 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда

		СтрокаМаксимальнойСуммы.СуммаСкидки = СтрокаМаксимальнойСуммы.СуммаСкидки + НепогашеннаяСумма;
		ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаСкидкиТабЧасти(СтрокаМаксимальнойСуммы);
		Если ПоказыватьСуммуНДС Тогда
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаМаксимальнойСуммы, ДокументСуммаВключаетНДС);
			ПересчитатьВсегоСтрокиТабличнойЧасти(СтрокаМаксимальнойСуммы, ДокументСуммаВключаетНДС);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЕдиницаИзмеренияПриИзмененииНаСервере(СтрокаТабличнойЧасти)
	
	ОбработкаТабличныхЧастей.ЗаполнитьКоэффициентТабЧасти(СтрокаТабличнойЧасти, Неопределено, "Товары", Метаданные.Обработки.ИзменениеТаблицыТоваров);
	
КонецПроцедуры

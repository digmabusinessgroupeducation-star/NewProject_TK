#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДлительныеОперации

Функция ПолучитьДокументыПоВебСервису(Знач ТаблицаЗагрузки, Знач Параметры) Экспорт 
	
	ТаблицаДляЗапроса = Новый ТаблицаЗначений;
	ТаблицаДляЗапроса.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	ТаблицаДляЗапроса.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
	ТаблицаДляЗапроса.Колонки.Добавить("ИДДокументаОтгрузки", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	
	спрОрганизация 		= Справочники.Организации;
	спрТорговаяПлощадка = Справочники.ТорговыеПлощадки;
	спрТочкиДоставки 	= Справочники.ТочкиДоставки;
	спрСклад 			= Справочники.СкладыКомпании;
	спрНоменклатура 	= Справочники.Номенклатура;
	спрХарактеристики	= Справочники.ХарактеристикиНоменклатуры;
	спрКонтрагенты		= Справочники.Контрагенты;
	
	ВидДокумента = Параметры.ВидДокумента;
	
	ДлительныеОперации.СообщитьПрогресс(1, НСтр("ru = 'Получаем данные из базы источника'; uk = 'Отримуємо дані з бази джерела'"));
	
	Определение = Новый WSОпределения(Параметры.ВебСервис);
	WS1 = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			
	Попытка
		тзРезультат = WS1.ReturnArrayDocumentsRT(
			Параметры.ДатаНачала, 
			Параметры.ДатаОкончания, 
			"", 
			Параметры.ВидДокумента, 
			Истина, 
			Параметры.Источник,
			Строка(Параметры.Приемник.УникальныйИдентификатор()),
			"00000000-0000-0000-0000-000000000000",
			"3a282943-1d75-11e0-b223-00505684000c");
	Исключение
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Ошибка выполнения Веб-сервиса: %1'; uk = 'Помилка виконання Веб-сервісу: %1'"),
			СтрЗаменить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), Символы.ПС, Символы.ПС + Символы.Таб));
	КонецПопытки;
			
	Если тзРезультат.Description <> "Ок" Тогда
		
		Если Найти(тзРезультат.Description, "Пустой результат") > 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Данные по указанным параметрам не найдены'; uk = 'Дані по вказаним параметрам не знайдені'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Ошибка выполнения Веб-сервиса'; uk = 'Помилка виконання Веб-сервісу'");
			Если Не ПустаяСтрока(тзРезультат.Description) Тогда 
				ТекстСообщения = ТекстСообщения + ": " + тзРезультат.Description;
			КонецЕсли;
		КонецЕсли;
		
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	
	
	//Временное решение
	Контрагент = спрКонтрагенты.НайтиПоКоду("43242503"); //СТК ООО.
	
	
	ОбщКолво = тзРезультат.ArrayDocumentsRT.Количество();
	ТекДок = 0;
	Для каждого стрДокумент Из тзРезультат.ArrayDocumentsRT Цикл
		ВсеОк = Истина;
		
		РеквизитыДокумента = Новый Структура;
		
		НомерДокумента = СокрЛП(стрДокумент.NumberDoc);
		ДатаДокумена = стрДокумент.DateTimeDoc;					
		
		РеквизитыДокумента.Вставить("ВхДокНомер", НомерДокумента);
		РеквизитыДокумента.Вставить("ВхДокДата", ДатаДокумена);
		
		Если ВидДокумента = "РеализацияТоваров" Тогда			
			МестоДоставки = спрТочкиДоставки.ПолучитьСсылку(Новый УникальныйИдентификатор(стрДокумент.GuidDELIVERYPLACE));
			СкладКомпании = ВернутьСкладТочкиДоставки(МестоДоставки);
		Иначе
			СкладКомпании = спрСклад.ПустаяСсылка();
		КонецЕсли;		
										
		УчетХарактеристики = Не Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(СкладКомпании, ДатаДокумена);
		ОшибкиТчТовары = "";
		СуммаДокумента = 0;
		МассивСтрокТовары = Новый Массив;	
		Ит = 1;
		Для Каждого стрТовары Из стрДокумент.TableList Цикл 	
			
			СтруктураСтроки = Новый Структура;
			
			Номенклатура = спрНоменклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(стрТовары.GuidProduct));
			Если Не ОбщегоНазначения.СсылкаСуществует(Номенклатура) Тогда 
				ВсеОк = Ложь;
				ТекстСообщения = НСтр("ru = 'Не найдена номенклатура по идентификатору ""%1""'; uk = 'Не знайдена номенклатура по ідентифікатору ""%1""'");
				ДобавитьСтроку(ОшибкиТчТовары, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, стрТовары.GuidProduct));
			КонецЕсли;
						
			СтруктураСтроки.Вставить("Номенклатура", Номенклатура);			
			Если УчетХарактеристики И стрТовары.GuidSpecification <> "00000000-0000-0000-0000-000000000000" Тогда 
				Характеристика = спрХарактеристики.ПолучитьСсылку(Новый УникальныйИдентификатор(стрТовары.GuidSpecification));
				Если ОбщегоНазначения.СсылкаСуществует(Характеристика) Тогда 
					СтруктураСтроки.Вставить("ХарактеристикаНоменклатуры", Характеристика);	
				Иначе 
					ВсеОк = Ложь;					
					ТекстСообщения = НСтр("ru = 'Не найдена характеристика по идентификатору ""%1""'; uk = 'Не знайдена характеристика по ідентифікатору ""%1""'");
					ДобавитьСтроку(ОшибкиТчТовары, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, стрТовары.GuidSpecification));
				КонецЕсли;				
			КонецЕсли;
			ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиЕдиницуИзмеренийПоКоф(Номенклатура,Число(стрТовары.Coefficient));
			ЕдиницаИзмеренияКоф = стрТовары.Coefficient;
			КоличествоТовара    = стрТовары.Quantity;
			Если ЕдиницаИзмерения = Неопределено Тогда
				ЕдиницаИзмерения     = Номенклатура.ОсновнаяЕдиницаИзмерения;
				ЕдиницаИзмеренияКоф  = ЕдиницаИзмерения.Коэффициент;
				КоличествоТовара     = КоличествоТовара * ЕдиницаИзмеренияКоф;
			КонецЕсли;
			 
			СтруктураСтроки.Вставить("ЕдиницаИзмерения",ЕдиницаИзмерения);  //Справочники.ЕдиницыИзмерения.ПолучитьСсылку(Новый УникальныйИдентификатор(стрТовары.GuidUnit))
			СтруктураСтроки.Вставить("СтавкаНДС", 		Справочники.СтавкиНДС.ПолучитьСсылку(Новый УникальныйИдентификатор(стрТовары.GuidVAT)));
			СтруктураСтроки.Вставить("Коэффициент", 	ЕдиницаИзмеренияКоф);
			СтруктураСтроки.Вставить("Цена", 			стрТовары.Price);
			СтруктураСтроки.Вставить("Количество", 		КоличествоТовара);
			СтруктураСтроки.Вставить("СуммаВсего", 		стрТовары.Cost);
			
			МассивСтрокТовары.Добавить(СтруктураСтроки);
			
			СуммаДокумента = СуммаДокумента + стрТовары.Cost;
			Ит = Ит + 1;
		КонецЦикла;	
		
		РеквизитыДокумента.Вставить("МассивСтрокТовары", МассивСтрокТовары);
		РеквизитыДокумента.Вставить("СкладКомпании", СкладКомпании);
		РеквизитыДокумента.Вставить("Контрагент", Контрагент);
		РеквизитыДокумента.Вставить("ИдДокументаОснования", стрДокумент.IDDocFoundation);
		РеквизитыДокумента.Вставить("ИдДокументаОтгрузки", стрДокумент.GuidDoc);
		РеквизитыДокумента.Вставить("РегламентированныйУчет", стрДокумент.Reglam);
					
		ПредставлениеДокумента = "";		
		Если ВидДокумента = "РеализацияТоваров" Тогда 
			ПредставлениеДокумента = НСтр("ru = 'Реализация товаров №%1 от %2'; uk = 'Реалізація товарів №%1 від %2'");
		ИначеЕсли ВидДокумента = "ПеремещениеТоваров" Тогда 
			ПредставлениеДокумента = НСтр("ru = 'Перемещение товаров №%1 от %2'; uk = 'Переміщення товарів №%1 від %2'");
		Иначе
			ПредставлениеДокумента = ВидДокумента + " №%1 " + НСтр("ru = 'от'; uk = 'від'") + " %2";
		КонецЕсли;		
		
		НоваяСтрока = ТаблицаЗагрузки.Добавить();
		НоваяСтрока.Дата = ДатаДокумена;
		НоваяСтрока.Номер = НомерДокумента;
		НоваяСтрока.ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДокумента, НомерДокумента, ДатаДокумена);
		НоваяСтрока.СуммаДокумента = СуммаДокумента;
		НоваяСтрока.ВидДокумента = ВидДокумента;
		НоваяСтрока.ДанныеДокумента = РеквизитыДокумента;
		НоваяСтрока.ЕстьОшибки = Не ВсеОк;
		
		Если Не ПустаяСтрока(ОшибкиТчТовары) Тогда 
			НоваяСтрока.ТекстОшибок = ОшибкиТчТовары;
		КонецЕсли;			
		
		новСтрПроверка = ТаблицаДляЗапроса.Добавить();
		новСтрПроверка.Индекс = ТаблицаЗагрузки.Индекс(НоваяСтрока);
		новСтрПроверка.Склад  = СкладКомпании;
		новСтрПроверка.ИДДокументаОтгрузки = стрДокумент.GuidDoc;		
		
		//Прогресс	
		ТекДок = ТекДок + 1;
		ДлительныеОперации.СообщитьПрогресс(
			Цел(ТекДок / ОбщКолво * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обработано %1 из %2'; uk = 'Виконано %1 з %2'"), 
				ТекДок, 
				ОбщКолво));
	КонецЦикла;	
	
	
	ДлительныеОперации.СообщитьПрогресс(98, НСтр("ru = 'Идет проверка данных'; uk = 'Йде перевірка даних'"));
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВТ.Индекс КАК Индекс,
	               |	ВТ.Склад КАК Склад,
	               |	ВТ.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	&ВТ КАК ВТ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Склад,
	               |	ИДДокументаОтгрузки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	               |	СкладыКомпании.Ссылка КАК СкладКомпании,
	               |	СкладыКомпании.Подразделение КАК ПодразделениеКомпании,
	               |	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	СкладыКомпании.ТипЦенСебестоимости КАК ТипЦен
	               |ПОМЕСТИТЬ ДанныеПоСкладам
	               |ИЗ
	               |	Справочник.СкладыКомпании КАК СкладыКомпании
	               |ГДЕ
	               |	СкладыКомпании.Ссылка В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				ВТ.Склад КАК Ссылка
	               |			ИЗ
	               |				ВТ КАК ВТ
	               |			ГДЕ
	               |				ВТ.Склад <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка))
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СкладКомпании
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация
	               |ПОМЕСТИТЬ Организации
	               |ИЗ
	               |	РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(
	               |			&КонДата,
	               |			ТорговаяПлощадка В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					ДанныеПоСкладам.ТорговаяПлощадка КАК ТорговаяПлощадка
	               |				ИЗ
	               |					ДанныеПоСкладам КАК ДанныеПоСкладам
	               |				ГДЕ
	               |					ДанныеПоСкладам.ТорговаяПлощадка <> ЗНАЧЕНИЕ(Справочник.ТорговыеПлощадки.ПустаяСсылка))) КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Док.Ссылка КАК Документ,
	               |	Док.Проведен КАК Проведен,
	               |	Док.ПометкаУдаления КАК ПометкаУдаления,
	               |	Док.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки
	               |ПОМЕСТИТЬ ЗагруженныеДокументы
	               |ИЗ
	               |	Документ.%1 КАК Док
	               |ГДЕ
	               |	Док.Дата МЕЖДУ &НачДата И &КонДата
	               |	И Док.ИДДокументаОтгрузки В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				ВТ.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки
	               |			ИЗ
	               |				ВТ КАК ВТ
	               |			ГДЕ
	               |				ВТ.ИДДокументаОтгрузки <> """")
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ИДДокументаОтгрузки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Индекс КАК Индекс,
	               |	ВТ.Склад КАК СкладКомпании,
	               |	ЕСТЬNULL(ДанныеПоСкладам.ПодразделениеКомпании, ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка)) КАК ПодразделениеКомпании,
	               |	ЕСТЬNULL(ДанныеПоСкладам.ТорговаяПлощадка, ЗНАЧЕНИЕ(Справочник.ТорговыеПлощадки.ПустаяСсылка)) КАК ТорговаяПлощадка,
	               |	ЕСТЬNULL(Организации.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	               |	ЕСТЬNULL(ДанныеПоСкладам.ТипЦен, ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)) КАК ТипЦен,
	               |	ВТ.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки,
	               |	ЕСТЬNULL(ЗагруженныеДокументы.Документ, НЕОПРЕДЕЛЕНО) КАК ДокументЗагрузки,
	               |	ЕСТЬNULL(ЗагруженныеДокументы.Проведен, ЛОЖЬ) КАК Проведен,
	               |	ЕСТЬNULL(ЗагруженныеДокументы.ПометкаУдаления, ЛОЖЬ) КАК ПометкаУдаления
	               |ИЗ
	               |	ВТ КАК ВТ
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоСкладам КАК ДанныеПоСкладам
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Организации КАК Организации
	               |			ПО ДанныеПоСкладам.ТорговаяПлощадка = Организации.ТорговаяПлощадка
	               |		ПО ВТ.Склад = ДанныеПоСкладам.СкладКомпании
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ЗагруженныеДокументы КАК ЗагруженныеДокументы
	               |		ПО ВТ.ИДДокументаОтгрузки = ЗагруженныеДокументы.ИДДокументаОтгрузки";
	
	ДокументНазначение = "";
	Если ВидДокумента = "РеализацияТоваров" Тогда 
		ДокументНазначение = "ПоступлениеТоваров";
	ИначеЕсли ВидДокумента = "ПеремещениеТоваров" Тогда 
		ДокументНазначение = "ПеремещениеТоваров";
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ДокументНазначение);
	
	Запрос.УстановитьПараметр("ВТ", ТаблицаДляЗапроса);
	Запрос.УстановитьПараметр("НачДата", Параметры.ДатаНачала);
	Запрос.УстановитьПараметр("КонДата", ТекущаяДата());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КартинкаПроведен = БиблиотекаКартинок.СинтаксическийКонтроль;
	КартинкаЗаписан  = БиблиотекаКартинок.Документ;
	КартинкаУдален   = БиблиотекаКартинок.ПометитьНаУдаление;
	
	КартЗагружено 	 = БиблиотекаКартинок.ВнешняяКомпонентаДоступна;
	КартОшибкаЗагр   = БиблиотекаКартинок.Предупреждение;
	
	Пока Выборка.Следующий() Цикл 		
		Ошибки = "";
		УжеЗагружен = Ложь;
		СтрокаТаблицы = ТаблицаЗагрузки.Получить(Выборка.Индекс);
		ВсеОк = Не СтрокаТаблицы.ЕстьОшибки;
		
		ДопРеквизиты = Новый Структура("СкладКомпании, Организация, ПодразделениеКомпании, ТипЦен, ТорговаяПлощадка");
		ЗаполнитьЗначенияСвойств(ДопРеквизиты, Выборка);
		
		ТекстСообщения = НСтр("ru = 'Не заполнен реквизит %1'; uk = 'Не заповнений реквізит %1'");
		Для Каждого Реквизит Из ДопРеквизиты Цикл 
			Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда 
				ВсеОк = Ложь;
				ДобавитьСтроку(Ошибки, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Реквизит.Ключ));
			КонецЕсли;
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтрокаТаблицы.ДанныеДокумента, ДопРеквизиты, Истина);
		
		Если ЗначениеЗаполнено(Выборка.ДокументЗагрузки) Тогда 
			УжеЗагружен = Истина;
			СтрокаТаблицы.ДокументЗагрузки = Выборка.ДокументЗагрузки;
			Если Выборка.Проведен Тогда 
				СтрокаТаблицы.КартинкаДокументЗагрузки = КартинкаПроведен;
			ИначеЕсли Выборка.ПометкаУдаления Тогда 
				СтрокаТаблицы.КартинкаДокументЗагрузки = КартинкаУдален;
			Иначе 
				СтрокаТаблицы.КартинкаДокументЗагрузки = КартинкаЗаписан;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаТаблицы.ЕстьОшибки = Не ВсеОк;
		Если Не ПустаяСтрока(Ошибки) Тогда 
			ДобавитьСтроку(Ошибки, СтрокаТаблицы.ТекстОшибок);
			СтрокаТаблицы.ТекстОшибок = Ошибки;
		КонецЕсли;
		
		СтрокаТаблицы.Выбор = ВсеОк И Не УжеЗагружен;
		Если УжеЗагружен И ВсеОк Тогда 
			СтрокаТаблицы.Картинка = КартЗагружено;
		ИначеЕсли Не ВсеОк Тогда 
			СтрокаТаблицы.Картинка = КартОшибкаЗагр;
		КонецЕсли;
	КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(100, НСтр("ru = 'Готово'; uk = 'Готово'"));
	
	ТаблицаЗагрузки.Сортировать("Дата");
	
	Результат = Новый Структура;
	Результат.Вставить("ТаблицаЗагрузки", ТаблицаЗагрузки);
	Результат.Вставить("СообщениеЗавершения", 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Получено %1 документ(ов)'; uk = 'Отримано %1 документ(ів)'"), 
			ТаблицаЗагрузки.Количество()));
	
	Возврат Результат;
	
КонецФункции


Функция СформироватьДокументы(Знач ТаблицаЗагрузки, Знач Параметры) Экспорт 
		
	ВремяНачала = ТекущаяДата();
	
	МассивДокументов = ТаблицаЗагрузки.НайтиСтроки(Новый Структура("Выбор", Истина));
	
	ОбрабатыватьПо = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОбрабатыватьПо",  10);
	ВсегоДокументов = МассивДокументов.Количество();	

	МассивЗаданий = Новый Массив;
	
	текМассив = Неопределено;
	Этап = 0;
	
	ПараметрыФункции = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	
	ДлительныеОперации.СообщитьПрогресс(1, НСтр("ru = 'Подготовка данных'; uk = 'Підготовка даних'"));
	
	Для Ит = 0 По МассивДокументов.ВГраница() Цикл		
		Если текМассив = Неопределено Тогда 
			текМассив = Новый Массив;
		КонецЕсли;
		
		текДок = МассивДокументов[Ит];
		
		СтруктураДокумента = Новый Структура;
		СтруктураДокумента.Вставить("ИндексСтроки", ТаблицаЗагрузки.Индекс(текДок));
		СтруктураДокумента.Вставить("ВидДокумента", текДок.ВидДокумента);
		СтруктураДокумента.Вставить("ДанныеДокумента", текДок.ДанныеДокумента);
		СтруктураДокумента.Вставить("ДокументЗагрузки", текДок.ДокументЗагрузки);
				
		текМассив.Добавить(СтруктураДокумента);		
		
		Если текМассив.Количество() = ОбрабатыватьПо ИЛИ Ит = МассивДокументов.ВГраница() Тогда
			АдресХранилища = ПоместитьВоВременноеХранилище(Новый Структура);
			Операция = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыФункции, "Обработки.ЗагрузкаДокументовПоВебСервису.СформироватьДокументыВФоне", текМассив, АдресХранилища, Параметры);
			
			СтруктрураОперации = Новый Структура;
			СтруктрураОперации.Вставить("Операция", Операция);
			СтруктрураОперации.Вставить("АдресХранилища", АдресХранилища);
			СтруктрураОперации.Вставить("Статус", Операция.Статус);
			СтруктрураОперации.Вставить("ИдентификаторЗадания", Операция.ИдентификаторЗадания);
			СтруктрураОперации.Вставить("Обработано", Ложь);			
			СтруктрураОперации.Вставить("КоличествоДокументовОбработано", 0);
			СтруктрураОперации.Вставить("КоличествоДокументовВЗадании", текМассив.Количество());
			МассивЗаданий.Добавить(СтруктрураОперации);
			
			текМассив = Неопределено;
		КонецЕсли;
	КонецЦикла;
		
	ОжидатьРезультат = Истина;
	Ошибки = "";	
	
	СОшибками = 0;
	
	ИнтервалПаузы = ?(ВсегоДокументов < 3, 1, 2);
	Пока ОжидатьРезультат Цикл 
		Обработано = 0;
		ОжидатьРезультат = Ложь;
		Пауза(ИнтервалПаузы);			
		
		Для Каждого Задание Из МассивЗаданий Цикл 			
			Если Не Задание.Обработано Тогда 				
				текРезультат = ДлительныеОперации.ОперацияВыполнена(Задание.ИдентификаторЗадания, Ложь, Истина);
				текСтатус = текРезультат.Статус;
				
				Если текРезультат.Прогресс <> Неопределено Тогда 
					Задание.КоличествоДокументовОбработано = текРезультат.Прогресс.Процент;
				КонецЕсли;
				
				Если текСтатус = "Выполняется" Тогда
					ОжидатьРезультат = Истина;
					Продолжить;
				ИначеЕсли текСтатус = "Выполнено" Тогда 
					ОбработатьРезультат(Задание, ТаблицаЗагрузки, Ошибки);
				Иначе			
					Если Не ПустаяСтрока(текРезультат.КраткоеПредставлениеОшибки) Тогда 
						Ошибки = Ошибки + Символы.ПС + текРезультат.КраткоеПредставлениеОшибки;
					КонецЕсли;
					СОшибками = СОшибками + Задание.КоличествоДокументовВЗадании;
					Задание.КоличествоДокументовОбработано = Задание.КоличествоДокументовВЗадании;
				КонецЕсли;
				
				Задание.Обработано = Истина;	
			КонецЕсли;
			
			Обработано = Обработано + Задание.КоличествоДокументовОбработано;
		КонецЦикла;				
		
		ДлительныеОперации.СообщитьПрогресс(
			Цел(Обработано / ВсегоДокументов * 100),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обработано %1 из %2'; uk = 'Виконано %1 з %2'"), 
				Обработано + СОшибками, 
				ВсегоДокументов));
	КонецЦикла;	
	

	ТаблицаЗагрузки.ЗаполнитьЗначения(Ложь, "Выбор");
	
	Если Не ПустаяСтрока(Ошибки) Тогда 
		ВызватьИсключение Ошибки;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТаблицаЗагрузки", ТаблицаЗагрузки);
	
	ТекстСообщения = НСтр("ru = 'Длительность операции %1.
                          |Обработано %2 документа(ов).
                          |из них с ошибками %3 шт.'; uk = 'Тривалість операції %1.
                          |Оброблено %2 документа(ів).
                          |з них з помилками %3 шт.'");			
	
	ПродолжительностьСек = ТекущаяДата() - ВремяНачала;
	ПродолжительностьМин = Цел(ПродолжительностьСек/ 60);
	ПредставлениеДлительности = ?(ПродолжительностьМин > 0, ПродолжительностьМин + НСтр("ru = ' мин.'; uk = ' хв.'"), "") + Формат(ПродолжительностьСек % 60, "ЧЦ=2; ЧДЦ=0; ЧВН=") + " сек.";
	
	Результат.Вставить("СообщениеЗавершения", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПредставлениеДлительности, ВсегоДокументов, СОшибками));
	
	Возврат Результат;
КонецФункции

Процедура тСформироватьДокументы (МассивДокументов,Параметры) Экспорт
	
	
	Обработано = 0;
	Договор = Неопределено;
	Параметры.Свойство("Договор",Договор);
	
	Для Каждого СтрДок Из МассивДокументов Цикл  		
		ВидДокумента = СтрДок.ВидДокумента;		
		РеквизитыДокумента = СтрДок.ДанныеДокумента;
		ДокументЗагрузки = СтрДок.ДокументЗагрузки;
		
		ДокументПриемник = "";
		Если ВидДокумента = "РеализацияТоваров" Тогда 
			ДокументПриемник = "ПоступлениеТоваров"	
		Иначе
			ДокументПриемник = ВидДокумента;
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(ДокументЗагрузки) Тогда 
			ДокОбъект = ДокументЗагрузки.ПолучитьОбъект();
			Если ДокОбъект.ПометкаУдаления Тогда 
				ДокОбъект.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			ДокОбъект.Товары.Очистить();

		Иначе 
			ДокОбъект = Документы[ДокументПриемник].СоздатьДокумент();
			ДокОбъект.Дата = ТекущаяДата();

		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(ДокОбъект, РеквизитыДокумента);
		
		Попытка
			ГУИДОснования = Новый УникальныйИдентификатор(РеквизитыДокумента.ИдДокументаОснования);
		Исключение
			  ГУИДОснования = Неопределено;
		КонецПопытки;
		
		
		Если ВидДокумента = "РеализацияТоваров" Тогда 
			ДокОбъект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваров");					
			
			Если Договор = Неопределено Тогда
				ДокОбъект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(ДокОбъект, ДокОбъект.Контрагент, ДокОбъект.Организация, ДокОбъект.ПодразделениеКомпании, ДокОбъект.ХозОперация, ДокОбъект.Дата);
			Иначе
				ДокОбъект.ДоговорВзаиморасчетов   = Договор;
				ДокОбъект.Контрагент = Договор.Контрагент;
			КонецЕсли;			
		
			Если ЗначениеЗаполнено(ГУИДОснования) Тогда 
				ДокОснование = Документы.ЗаказПоставщику.ПолучитьСсылку(ГУИДОснования);
				Если ОбщегоНазначения.СсылкаСуществует(ДокОснование) Тогда 
					ДокОбъект.ДокументОснование = ДокОснование;
					
					ДокОбъект.РаспределениеПоставки.Очистить();
					новРаспределение = ДокОбъект.РаспределениеПоставки.Добавить();
					новРаспределение.ЗаказПоставщику = ДокОснование;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВидДокумента = "ПеремещениеТоваров" Тогда 
			ДокОбъект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала");
		КонецЕсли;
		
		ДокОбъект.Автор = Пользователи.ТекущийПользователь();
		ДокОбъект.ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ДокОбъект.ВалютаДокумента);
		ДокОбъект.КурсДокумента       = 1;
		ДокОбъект.ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ДокОбъект.ПодразделениеКомпании);		
		
		//тч Товары
		МассивСтрок = РеквизитыДокумента.МассивСтрокТовары;
		Для Каждого Стр Из МассивСтрок Цикл 
			ТекущаяСтрока = ДокОбъект.Товары.Добавить();
			
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Стр);
			ТекущаяСтрока.Сумма =  ТекущаяСтрока.СуммаВсего;
			СтруктураДействий = Новый Структура;
			//СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");

			Если ВидДокумента = "РеализацияТоваров" Тогда 
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
				СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
				СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
				СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
				СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");				
			КонецЕсли;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		КонецЦикла;
		
		Если Параметры.Проводить ИЛИ  ДокОбъект.Проведен Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе 
			РежимЗаписи = РежимЗаписиДокумента.Запись;	
		КонецЕсли;
		
		Попытка
			ДокОбъект.Записать(РежимЗаписи);
		Исключение
			Ошибки =  ОписаниеОшибки();
		КонецПопытки;		
		
		Обработано = Обработано + 1;
	КонецЦикла;
	

	
КонецПроцедуры


Процедура СформироватьДокументыВФоне(Знач МассивДокументов, Знач АдресХранилища, Знач Параметры) Экспорт 
	Обработано = 0;
	
	Договор = Неопределено;
	Параметры.Свойство("Договор",Договор);

	Для Каждого СтрДок Из МассивДокументов Цикл  		
		ВидДокумента = СтрДок.ВидДокумента;		
		РеквизитыДокумента = СтрДок.ДанныеДокумента;
		ДокументЗагрузки = СтрДок.ДокументЗагрузки;
		
		ДокументПриемник = "";
		Если ВидДокумента = "РеализацияТоваров" Тогда 
			ДокументПриемник = "ПоступлениеТоваров"	
		Иначе
			ДокументПриемник = ВидДокумента;
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(ДокументЗагрузки) Тогда 
			ДокОбъект = ДокументЗагрузки.ПолучитьОбъект();
			Если ДокОбъект.ПометкаУдаления Тогда 
				ДокОбъект.УстановитьПометкуУдаления(Ложь);
				ДокОбъект.Товары.Очистить();
			КонецЕсли;
			ДокОбъект.Товары.Очистить();
	
		Иначе 
			ДокОбъект = Документы[ДокументПриемник].СоздатьДокумент();
			ДокОбъект.Дата = ТекущаяДата();

		КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ДокОбъект, РеквизитыДокумента);
		
		Попытка
			ГУИДОснования = Новый УникальныйИдентификатор(РеквизитыДокумента.ИдДокументаОснования);
		Исключение
			  ГУИДОснования = Неопределено;
		КонецПопытки;
		
		Если ВидДокумента = "РеализацияТоваров" Тогда 
			ДокОбъект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваров");					
			
			Если Договор = Неопределено Тогда
				ДокОбъект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(ДокОбъект, ДокОбъект.Контрагент, ДокОбъект.Организация, ДокОбъект.ПодразделениеКомпании, ДокОбъект.ХозОперация, ДокОбъект.Дата);
			Иначе
				ДокОбъект.ДоговорВзаиморасчетов   = Договор;
				ДокОбъект.Контрагент = Договор.Контрагент;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ГУИДОснования) Тогда 
				ДокОснование = Документы.ЗаказПоставщику.ПолучитьСсылку(ГУИДОснования);
				Если ОбщегоНазначения.СсылкаСуществует(ДокОснование) Тогда 
					ДокОбъект.ДокументОснование = ДокОснование;
					
					ДокОбъект.РаспределениеПоставки.Очистить();
					новРаспределение = ДокОбъект.РаспределениеПоставки.Добавить();
					новРаспределение.ЗаказПоставщику = ДокОснование;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВидДокумента = "ПеремещениеТоваров" Тогда 
			ДокОбъект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала");
		КонецЕсли;
		
		ДокОбъект.Автор = Пользователи.ТекущийПользователь();
		ДокОбъект.ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ДокОбъект.ВалютаДокумента);
		ДокОбъект.КурсДокумента       = 1;
		ДокОбъект.ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ДокОбъект.ПодразделениеКомпании);		
		
		//тч Товары
		МассивСтрок = РеквизитыДокумента.МассивСтрокТовары;
		Для Каждого Стр Из МассивСтрок Цикл 
			ТекущаяСтрока = ДокОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Стр);
			
			ТекущаяСтрока.Сумма =  ТекущаяСтрока.СуммаВсего;
			СтруктураДействий = Новый Структура;
			//СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");

			Если ВидДокумента = "РеализацияТоваров" Тогда 
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
				СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
				СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
				СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
				СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");				
			КонецЕсли;

			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		КонецЦикла;
		
		Если Параметры.Проводить Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе 
			РежимЗаписи = РежимЗаписиДокумента.Запись;	
		КонецЕсли;
		
		Попытка
			ДокОбъект.Записать(РежимЗаписи);
			СтрДок.ДокументЗагрузки = ДокОбъект.Ссылка;
			
			СтрДок.Вставить("Проведен", ДокОбъект.Проведен);
		Исключение
			СтрДок.Вставить("Ошибки", ОписаниеОшибки());
		КонецПопытки;		
		
		Обработано = Обработано + 1;
		ДлительныеОперации.СообщитьПрогресс(Обработано);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(МассивДокументов, АдресХранилища);
КонецПроцедуры


#КонецОбласти


#КонецОбласти
	
	
	
#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьРезультат(Операция, ТаблицаЗагрузки, Ошибки);
	
	Если Не Операция.Свойство("АдресХранилища") Тогда 
		Ошибки = Ошибки + Символы.ПС + "Нет результата!";
		Возврат;
	КонецЕсли;
	
	АдресХранилища = Операция.АдресХранилища;
		
	КартинкаЗагружено = БиблиотекаКартинок.ВнешняяКомпонентаДоступна;
	КартинкаОшибкаЗагрузки = БиблиотекаКартинок.ВнешняяКомпонентаНеДоступна;
	
	КартинкаПроведен = БиблиотекаКартинок.СинтаксическийКонтроль;
	КартинкаЗаписан  = БиблиотекаКартинок.Документ;
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Для Каждого стрРез Из Результат Цикл 
		текСтрока = ТаблицаЗагрузки.Получить(стрРез.ИндексСтроки);
		текСтрока.ДокументЗагрузки = стрРез.ДокументЗагрузки;
		
		Если ЗначениеЗаполнено(текСтрока.ДокументЗагрузки) Тогда 
			Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрРез, "Проведен", Ложь) Тогда
				текСтрока.КартинкаДокументЗагрузки = КартинкаПроведен;
			Иначе
				текСтрока.КартинкаДокументЗагрузки = КартинкаЗаписан;
			КонецЕсли;
			
			текСтрока.Картинка = КартинкаЗагружено;
		ИначеЕсли стрРез.Свойство("Ошибки") Тогда 
			текСтрока.Картинка = КартинкаОшибкаЗагрузки;
			текСтрока.ЕстьОшибки = Истина;
			
			ДобавитьСтроку(текСтрока.ТекстОшибок, стрРез.Ошибки);
		КонецЕсли;
	КонецЦикла;
	
	УдалитьИзВременногоХранилища(АдресХранилища);
КонецПроцедуры

#КонецОбласти



#Область ПрочиеПроцедурыИФункции

Функция ВернутьСкладТочкиДоставки(МестоДоставки)
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	|	СкладыКомпании.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.ТочкаДоставки = &ТочкаДоставки";
	
	Запрос.УстановитьПараметр("ТочкаДоставки", МестоДоставки);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		СкладКомпании = Выборка.Ссылка;
		
		Если Выборка.Количество() > 1 Тогда 
			ТекстСообщения = НСтр("ru = 'Найдено несколько складов для точки доставки ""%ТочкаДоставки%""'; uk = 'Знайдено декілька складів для точки доставки ""%ТочкаДоставки%""'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТочкаДоставки%", Строка(Выборка.Ссылка));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	Иначе
		СкладКомпании = Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
		
	Возврат СкладКомпании;
	
КонецФункции

Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	//СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, , , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции


Процедура ДобавитьСтроку(Текст, НоваяСтрока)
	
	Если ПустаяСтрока(НоваяСтрока) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Текст) Тогда 
		Текст = Текст + Символы.ПС + НоваяСтрока;
	Иначе
		Текст = НоваяСтрока;
	КонецЕсли;
	
КонецПроцедуры
	
Процедура Пауза(ИнтервалОжидания)
	ПараметрыЗапуска = ФайловаяСистема.ПараметрыЗапускаПрограммы();
	ПараметрыЗапуска.ДождатьсяЗавершения = Истина;
	ФайловаяСистема.ЗапуститьПрограмму("timeout /t " + Формат(ИнтервалОжидания, "ЧГ=0"), ПараметрыЗапуска);
КонецПроцедуры


#КонецОбласти



#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
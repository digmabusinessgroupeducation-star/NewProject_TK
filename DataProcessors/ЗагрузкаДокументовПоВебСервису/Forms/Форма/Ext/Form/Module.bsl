
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	Период.Вариант = ВариантСтандартногоПериода.Сегодня;
	ДокументовВЗадании = 10;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьКоличествоОтмеченных();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаблицаДокументовВыборПриИзменении(Элемент)
	ОбновитьКоличествоОтмеченных();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.ТаблицаДокументов.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено ИЛИ Не ТекСтрока.ЕстьОшибки ИЛИ Поле.Имя = "ТаблицаДокументовДокументЗагрузки" Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекСтрока.ТекстОшибок) Тогда 
		ПоказатьПредупреждение(, ТекСтрока.ТекстОшибок,, ТекСтрока.ПредставлениеДокумента);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Не ЭтаФорма.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ИдентификаторКоманды = "ПолучитьДокументы";
	
	ОчиститьСообщения();
	ТаблицаДокументов.Очистить();	
	
	ДлительнаяОперация = ЗапуститьОперациюЗагрузки();

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загрузка данных по Web-сервису'; uk = 'Завантаження даних по Web-сервісу'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 2;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ЗагрузкаДокументовПоВебСервису";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, "");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументы(Команда)	
	
	Если ТаблицаДокументов.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Список документов пуст'; uk = 'Список документів пустий'"));
		Возврат;
	КонецЕсли;
	
	МассивВыбранных = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор", Истина));
	Если МассивВыбранных.Количество() = 0 Тогда 
		ТекстСообщения = НСтр("ru = 'Нет выбранных документов'; uk = 'Немає обраних документів'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Параметры.ИдентификаторКоманды = "СформироватьДокументы";
	ОчиститьСообщения();
	
	ДлительнаяОперация = ЗапуститьОперациюФормирования();

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Формируем документы'; uk = 'Формуємо документи'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 2;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ЗагрузкаДокументовПоВебСервису";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, "");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры


&НаКлиенте
Процедура тСформироватьДокументы(Команда)
	СформироватьНаСервере();
КонецПроцедуры


&НаСервере
Процедура СформироватьНаСервере()
	
	мдок = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор", Истина));
//	МассивДокументов = ТаблицаЗагрузки.НайтиСтроки(Новый Структура("Выбор", Истина));
	тПараметры = Новый Структура("Проводить,Договор",Ложь,Договор);
	
	Обработки.ЗагрузкаДокументовПоВебСервису.тСформироватьДокументы(мдок,тПараметры);
КонецПроцедуры


&НаСервере
Функция ЗапуститьОперациюЗагрузки()	
	
	ТаблЗагрузки = ТаблицаДокументов.Выгрузить();
	
	СтруктураВебСервиса = Новый Структура;
	СтруктураВебСервиса.Вставить("ДатаНачала", Период.ДатаНачала);
	СтруктураВебСервиса.Вставить("ДатаОкончания", Период.ДатаОкончания);	
	СтруктураВебСервиса.Вставить("ВидДокумента", ВидДокумента);
	СтруктураВебСервиса.Вставить("Источник", Источник);
	СтруктураВебСервиса.Вставить("Приемник", Приемник);
	СтруктураВебСервиса.Вставить("ВебСервис", ВебСервис);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ЗагрузкаДокументовПоВебСервису.ПолучитьДокументыПоВебСервису", ТаблЗагрузки, СтруктураВебСервиса);
	
КонецФункции

&НаСервере
Функция ЗапуститьОперациюФормирования()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	ПараметрыФункции = Новый Структура("ОбрабатыватьПо, Проводить,Договор", ДокументовВЗадании, ПроводитьДокументы,Договор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ЗагрузкаДокументовПоВебСервису.СформироватьДокументы", ТаблицаДокументов.Выгрузить(), ПараметрыФункции);
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, СопровождающийТекст) Экспорт
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		ЗаполнитьТаблицуДокументов(Операция, СообщениеЗавершения);
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
	Иначе
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	Параметры.ИдентификаторКоманды = "";
	
	ОбновитьКоличествоОтмеченных();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументов(Операция, СообщениеЗавершения) 
	
	Если Не Операция.Свойство("АдресРезультата") Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);	
	
	ТаблицаДокументов.Загрузить(Результат.ТаблицаЗагрузки);
	
	СообщениеЗавершения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СообщениеЗавершения", СообщениеЗавершения);	

КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьКоличествоОтмеченных()
	Количество = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор", Истина)).Количество();
	ТекстКнопки = НСтр("ru = 'Сформировать документы'; uk = 'Сформувати документи'");
	Если Количество > 0 Тогда 
		ТекстКнопки = ТекстКнопки + " (" + Количество + ")";
	КонецЕсли;
	
	Элементы.ФормаСформироватьДокументы.Заголовок = ТекстКнопки;
КонецПроцедуры



#КонецОбласти
 
 
 
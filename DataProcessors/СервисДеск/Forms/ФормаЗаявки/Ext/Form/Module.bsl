
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.УзелПланаОбмена = ПланыОбмена.ОбменFirestore.НайтиПоКоду("DBG");
	НоваяЗаявка = Параметры.Свойство("НоваяЗаявка");
	ДанныеЗаявки = Параметры.ДанныеЗаявки;
	ПлановаяЗаявка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаявки, "Плановая", Ложь);
	
	Если НоваяЗаявка Тогда
		
		Инициатор = Пользователи.ТекущийПользователь().ФизическоеЛицо;
		Статус = Перечисления.СтатусыСервисДеск.opened;
		Услуга = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗаявки, "Услуга", "");
		Если ЗначениеЗаполнено(Услуга) Тогда
			Срочная = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга,"Срочная");
			СрокВыполнения = Обработки.СервисДеск.СрокВыполнения(Срочная, ТекущаяДатаСеанса(), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга,"ВремяВыполнения"));
		КонецЕсли;
		
		Элементы.Исполнитель.Видимость = НЕ ПлановаяЗаявка;
		Элементы.Исполнитель.ПодсказкаВвода = "Не забудьте выбрать сотрудника";
		Элементы.Инициатор.ТолькоПросмотр = ЗначениеЗаполнено(Инициатор);
		Элементы.Инициатор.ПодсказкаВвода = "Сотрудник не найден, выберите самостоятельно";
		Элементы.Точки.ПодсказкаВвода = "Необходимо выбрать точки";
	Иначе
		
		ID = ДанныеЗаявки.ID;
		Номер = ДанныеЗаявки.Номер;
		Дата = ДанныеЗаявки.Дата;
		Статус = ДанныеЗаявки.Статус;
		ДатаВыполнения = ДанныеЗаявки.ДатаВыполнения;
		Услуга = ДанныеЗаявки.Услуга;
		//Категория = Услуга.ПолноеНаименование();
		Категория = ?(ЗначениеЗаполнено(Услуга.Родитель.Родитель),Строка(Услуга.Родитель.Родитель)+" / ","")+Услуга.Родитель;
		СрокВыполнения = ДанныеЗаявки.СрокВыполнения;
		Срочная = ДанныеЗаявки.Срочная;
		Точка = ДанныеЗаявки.Точка;
		Куратор = ДанныеЗаявки.Куратор;
		Роль = ДанныеЗаявки.Роль;
		Инициатор = ДанныеЗаявки.Инициатор;
		Исполнитель = ДанныеЗаявки.Исполнитель;
		Описание = ДанныеЗаявки.Описание;
		КомментарийВыполнения = ДанныеЗаявки.КомментарийВыполнения;
		Фото = ДанныеЗаявки.Фото;
		ФотоВыполнения = ДанныеЗаявки.ФотоВыполнения;
		
		СоответствиеКартинки = Новый Соответствие;
		СоответствиеКартинки.Вставить(Перечисления.СтатусыСервисДеск.opened,3);
		СоответствиеКартинки.Вставить(Перечисления.СтатусыСервисДеск.completed,5);
		СоответствиеКартинки.Вставить(Перечисления.СтатусыСервисДеск.closed,7);
		СоответствиеКартинки.Вставить(Перечисления.СтатусыСервисДеск.reopened,8);
		СоответствиеКартинки.Вставить(Перечисления.СтатусыСервисДеск.not_assigned,1);
		
		Если ДанныеЗаявки.Срочная Тогда
			стрИстории = История.Добавить();
			стрИстории.Дата = Дата;
			стрИстории.Сотрудник = Инициатор;
			стрИстории.Статус = Перечисления.СтатусыСервисДеск.not_assigned;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = Описание;
			стрИстории.Фото = ДанныеЗаявки.Фото;
		Иначе		
			стрИстории = История.Добавить();
			стрИстории.Дата = Дата;
			стрИстории.Сотрудник = Инициатор;
			стрИстории.Статус = Перечисления.СтатусыСервисДеск.opened;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = Описание;
			стрИстории.Фото = ДанныеЗаявки.Фото;
		КонецЕсли;
		
		Если Статус = Перечисления.СтатусыСервисДеск.completed Тогда
			Если ДанныеЗаявки.Срочная Тогда
				стрИстории = История.Добавить();
				//стрИстории.Дата = Дата;
				стрИстории.Сотрудник = Инициатор;
				стрИстории.Статус = Перечисления.СтатусыСервисДеск.opened;
				стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
				//стрИстории.Комментарий = Описание;
				//стрИстории.Фото = ДанныеЗаявки.Фото;
			КонецЕсли;
			стрИстории = История.Добавить();
			стрИстории.Дата = ДатаВыполнения;
			стрИстории.Сотрудник = Исполнитель;
			стрИстории.Статус = Статус;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = КомментарийВыполнения;
			стрИстории.Фото = ФотоВыполнения;
		КонецЕсли;
		
		Если Статус = Перечисления.СтатусыСервисДеск.closed Тогда
			Если ДанныеЗаявки.Срочная Тогда
				стрИстории = История.Добавить();
				//стрИстории.Дата = Дата;
				стрИстории.Сотрудник = Инициатор;
				стрИстории.Статус = Перечисления.СтатусыСервисДеск.opened;
				стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
				//стрИстории.Комментарий = Описание;
				//стрИстории.Фото = ДанныеЗаявки.Фото;
			КонецЕсли;
			стрИстории = История.Добавить();
			стрИстории.Дата = ДатаВыполнения;
			стрИстории.Сотрудник = Исполнитель;
			стрИстории.Статус = Перечисления.СтатусыСервисДеск.completed;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = КомментарийВыполнения;
			стрИстории.Фото = ФотоВыполнения;
			
			стрИстории = История.Добавить();
			//стрИстории.Дата = ДатаСтатуса;
			//стрИстории.Сотрудник = Исполнитель;
			стрИстории.Статус = Статус;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = ?(ДанныеЗаявки.АвтоЗакрытая,"АвтоЗакрыта по истечению 24 часов","");
			//стрИстории.Фото = "https://photosvisitors.s3.amazonaws.com/external_0000017";
		КонецЕсли;
		
		Если Статус = Перечисления.СтатусыСервисДеск.reopened Тогда
			Если ДанныеЗаявки.Срочная Тогда
				стрИстории = История.Добавить();
				//стрИстории.Дата = Дата;
				стрИстории.Сотрудник = Инициатор;
				стрИстории.Статус = Перечисления.СтатусыСервисДеск.opened;
				стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
				//стрИстории.Комментарий = Описание;
				//стрИстории.Фото = ДанныеЗаявки.Фото;
			КонецЕсли;
			стрИстории = История.Добавить();
			стрИстории.Дата = ДатаВыполнения;
			стрИстории.Сотрудник = Исполнитель;
			стрИстории.Статус = Перечисления.СтатусыСервисДеск.completed;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			стрИстории.Комментарий = КомментарийВыполнения;
			стрИстории.Фото = ФотоВыполнения;
			
			стрИстории = История.Добавить();
			//стрИстории.Дата = ДатаСтатуса;
			//стрИстории.Сотрудник = Исполнитель;
			стрИстории.Статус = Статус;
			стрИстории.ИндексСтатуса = СоответствиеКартинки.Получить(стрИстории.Статус);
			//стрИстории.Комментарий = "Поздно увидел заявку";
			//стрИстории.Фото = "https://photosvisitors.s3.amazonaws.com/external_0000017";
		КонецЕсли;
		
		Элементы.Инициатор.ПодсказкаВвода = "Сотрудник не найден!";
		Элементы.Исполнитель.ПодсказкаВвода = "Сотрудник не найден!";
		
		Если ДеньНедели(Дата) > 5 Тогда
			Элементы.ДеньНедели.ЦветТекста = ЦветаСтиля.ВидДняПроизводственногоКалендаряВоскресеньеЦвет;
		Иначе
			Элементы.ДеньНедели.ЦветТекста = ЦветаСтиля.ВидДняПроизводственногоКалендаряПредпраздничныйЦвет;
		КонецЕсли;
		
		
		
	КонецЕсли;
	
	СоответствиеСтатусов = Новый Соответствие;
	СоответствиеСтатусов.Вставить(Перечисления.СтатусыСервисДеск.opened,WebЦвета.Коралловый);
	СоответствиеСтатусов.Вставить(Перечисления.СтатусыСервисДеск.completed,WebЦвета.Зеленый);
	СоответствиеСтатусов.Вставить(Перечисления.СтатусыСервисДеск.closed,WebЦвета.КоролевскиГолубой);
	СоответствиеСтатусов.Вставить(Перечисления.СтатусыСервисДеск.reopened,WebЦвета.ОрхидеяНейтральный);
	СоответствиеСтатусов.Вставить(Перечисления.СтатусыСервисДеск.not_assigned,WebЦвета.Оранжевый);
	
	Элементы.Статус.ЦветТекста = СоответствиеСтатусов.Получить(Статус);
	Элементы.СрокВыполнения.ЦветРамки = ?(Срочная,ЦветаСтиля.ЦветОсобогоТекста,ЦветаСтиля.ЦветРамки);
	Элементы.ГруппаКураторРоль.Видимость = НЕ НоваяЗаявка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ГруппаДатаНомер.Видимость = Не НоваяЗаявка;
	Элементы.ГруппаОсновное.ТолькоПросмотр = Не НоваяЗаявка;
	Элементы.Описание.РедактированиеТекста = НоваяЗаявка;
	Элементы.ФормаКомандаОтправитьЗаявку.Видимость = НоваяЗаявка;
	Элементы.Фото.Видимость = НЕ НоваяЗаявка;
	Элементы.Картинка.Видимость = НоваяЗаявка;
	Элементы.Срочная.Видимость = Срочная;
	Элементы.СсылкиНаФото.ИзменятьСоставСтрок = НоваяЗаявка;
	Элементы.КомандаЗагрузитьФото.Видимость = НоваяЗаявка;
	Элементы.Точка.Видимость = НЕ ПлановаяЗаявка;
	Элементы.Точки.Видимость = ПлановаяЗаявка;
	
	Если НоваяЗаявка Тогда
		Заголовок =	?(ПлановаяЗаявка, "Плановая заявка", "Новая заявка");
	Иначе
		Заголовок = СтрШаблон("%1 № %2 от %3 г. %4",?(Срочная,"Срочная заявка","Заявка"),Номер,Формат(Дата,"ДФ=dd.MM.yyyy"),Статус);
	КонецЕсли;
	
	Если ПлановаяЗаявка Тогда
		ЗаполнитьПоСписку("ТорговыеПлощадки");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("Услуга");
	Если НЕ ПлановаяЗаявка Тогда
		ПроверяемыеРеквизиты.Добавить("Точка");
	КонецЕсли;
	ПроверяемыеРеквизиты.Добавить("Инициатор");
	ПроверяемыеРеквизиты.Добавить("Исполнитель");
	ПроверяемыеРеквизиты.Добавить("СрокВыполнения");
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура УслугаПриИзменении(Элемент)
	УслугаПриИзмененииНаСервере();
	Заголовок =	?(Срочная,"Новая срочная заявка","Новая заявка");
	Элементы.Срочная.Видимость = Срочная;
	
	Исполнитель = Исполнитель(Услуга, Точка);
	ДоступностьКомандаОтправитьЗаявку()
КонецПроцедуры

&НаКлиенте
Процедура ТочкаПриИзменении(Элемент)
	Исполнитель = Исполнитель(Услуга, Точка);
	ДоступностьКомандаОтправитьЗаявку()
КонецПроцедуры

&НаКлиенте
Процедура ТочкиПриИзменении(Элемент)
	ДоступностьКомандаОтправитьЗаявку();
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	ДоступностьКомандаОтправитьЗаявку();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура КомандаОтправитьЗаявку(Команда)
	
	Если ПлановаяЗаявка Тогда
		НеОкТочки = Новый СписокЗначений;
		МассивСсылокФото = МассивСсылокФото(СсылкиНаФото,"plan");
		
		ВсеОК = ИСтина;
		Для Каждого стрТочка Из Точки Цикл
			ОК = КомандаОтправитьЗаявкуНаСервере(Истина, стрТочка.Значение, МассивСсылокФото);
			Если НЕ ОК Тогда
				НеОкТочки.Добавить(стрТочка.Значение);	
			КонецЕсли;
			ВсеОК = ВсеОК И ОК;
		КонецЦикла;
	Иначе
		ВсеОК = КомандаОтправитьЗаявкуНаСервере(Ложь, Точка);
	КонецЕсли;
	
	Если ВсеОК Тогда
		ЭтаФорма.Закрыть(ВсеОК);
	ИначеЕсли ПлановаяЗаявка И НеОкТочки.Количество() > 1 Тогда
		Точки = НеОкТочки;
		ОбщегоНазначенияКлиент.СообщитьПользователю("Проверьте привязку исполнителей и их территорий. Отправте заявку на оставшиеся точки.");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьФото(Команда)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьФайлыЗавершение", ЭтотОбъект);
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов("Выберите файлы картинок", Истина, "Файл JPG, PNG, JPEG |*.jpg; *.png; *.jpeg");
	НачатьПомещениеФайловНаСервер(ОповещениеОЗавершении, , , ПараметрыДиалога, УникальныйИдентификатор);
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыИстория


&НаКлиенте
Процедура ИсторияПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.История.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		//При активизации строки ТекДанные не заполонены
	Иначе
		СсылкиФото = ТекДанные.Фото;
		СсылкиНаФото.Очистить();
		мСсылокнафото = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СсылкиФото,";",Истина);
		Для Каждого СсылкаНаФото Из мСсылокнафото Цикл
			строка = СсылкиНаФото.Добавить();
			строка.Представление = СсылкаНаФото;			
		КонецЦикла;
		Если НЕ НоваяЗаявка И мСсылокнафото.Количество() = 0 Тогда
			строка = СсылкиНаФото.Добавить();
			строка.Представление = "https://tasksphoto.s3.amazonaws.com/no_photos";			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСсылкиНаФото


&НаКлиенте
Процедура СсылкиНаФотоПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.СсылкиНаФото.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если НоваяЗаявка Тогда
			Картинка = ТекущиеДанные.Адрес;
		Иначе
			Фото = ТекущиеДанные.Представление;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура УслугаПриИзмененииНаСервере()
	Срочная = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга,"Срочная");
	СрокВыполнения = Обработки.СервисДеск.СрокВыполнения(Срочная, ТекущаяДатаСеанса(), ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Услуга,"ВремяВыполнения"));
	Элементы.СрокВыполнения.ЦветРамки = ?(Срочная,ЦветаСтиля.ЦветОсобогоТекста,ЦветаСтиля.ЦветРамки);
КонецПроцедуры

&НаКлиенте
Процедура ДоступностьКомандаОтправитьЗаявку()
	Элементы.ФормаКомандаОтправитьЗаявку.Доступность = ЗначениеЗаполнено(Услуга) И ЗначениеЗаполнено(Инициатор) И ЗначениеЗаполнено(СрокВыполнения)
	И ?(ПлановаяЗаявка,ЗначениеЗаполнено(Точки),ЗначениеЗаполнено(Точка)) И ?(ПлановаяЗаявка,Истина,ЗначениеЗаполнено(Исполнитель)) ;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСписку(Данные)
	Подсказка = "Введите новый код с новой строки";
	Строка = "";
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма, Новый Структура("Справочник" ,Данные));
	ПоказатьВводСтроки(ОписаниеОповещения, Строка, Подсказка,,Истина);
КонецПроцедуры

&НаСервере
Процедура ПослеВводаСтроки(ПолученноеЗначение, Данные) Экспорт
	
	Если Данные.Справочник = "ТорговыеПлощадки" Тогда
		Для й=1 По СтрЧислоСтрок(ПолученноеЗначение) Цикл
			зн = СокрЛП(СтрПолучитьСтроку(ПолученноеЗначение,й));
			Если ПустаяСтрока(зн) Тогда
				Продолжить;
			КонецЕсли;
			
			Элем = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(зн);
			Если Элем = Неопределено Тогда
				Сообщить("Точка не найдена, код: " +зн);
				Продолжить;
			КонецЕсли;
			
			Если Точки.НайтиПоЗначению(Элем) = Неопределено Тогда
				Точки.Добавить(Элем);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(Исполнитель(Услуга, Элем)) Тогда
				ОбщегоНазначения.СообщитьПользователю("Для (" + зн + ") " + СокрЛП(Элем) + " не установлен исполнитель!"+Символы.ПС+
				"Проверьте привязку исполнителей и их территорий, затем отправляйте заявки.");
			КонецЕсли;
		КонецЦикла;
		Точки.СортироватьПоЗначению();
	КонецЕсли;
	
	Элементы.ФормаКомандаОтправитьЗаявку.Доступность = ЗначениеЗаполнено(Услуга) И ЗначениеЗаполнено(Инициатор) И ЗначениеЗаполнено(СрокВыполнения)
	И ?(ПлановаяЗаявка,ЗначениеЗаполнено(Точки),ЗначениеЗаполнено(Точка)) И ?(ПлановаяЗаявка,Истина,ЗначениеЗаполнено(Исполнитель)) ;
КонецПроцедуры

&НаСервере
Функция КомандаОтправитьЗаявкуНаСервере(Плановая, ТочкаЗаявки, МассивСсылокФото = Неопределено)
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	
	ТочкаКод = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТочкаЗаявки,"Код"));
	Если Не Плановая Тогда
		МассивСсылокФото = МассивСсылокФото(СсылкиНаФото,ТочкаКод);
	КонецЕсли;
	ПоляЗаявки = "Инициатор, Исполнитель, КомментарийВыполнения, Описание, Срочная, Статус, Услуга";
	ЗаявкаСтруктура = Новый Структура(ПоляЗаявки);
	ЗаполнитьЗначенияСвойств(ЗаявкаСтруктура,ЭтотОбъект,ПоляЗаявки);
	ЗаявкаСтруктура.Вставить("Точка",ТочкаЗаявки);
	ЗаявкаСтруктура.Вставить("Плановая",Плановая);
	ЗаявкаСтруктура.Вставить("МассивСсылокФото",МассивСсылокФото);
	ОК = Обработки.СервисДеск.ПодготовитьОтправитьЗаявку(ПараметрыДляЗапроса, ЗаявкаСтруктура);
	
	Возврат ОК;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьФайлыЗавершение(ПомещенныеФайлы, Дополнительно) Экспорт
	Если ПомещенныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ПомещенныйФайл Из ПомещенныеФайлы Цикл
		ПолноеИмя = ПомещенныйФайл.СсылкаНаФайл.Файл.ПолноеИмя;
		Если СсылкиНаФото.НайтиСтроки(Новый Структура("Значение",ПолноеИмя)).Количество() = 0 Тогда
			строка = СсылкиНаФото.Добавить();
			строка.Значение = ПолноеИмя;			
			строка.Представление = ПомещенныйФайл.СсылкаНаФайл.Имя;			
			строка.РасширениеФайла = ПомещенныйФайл.СсылкаНаФайл.Расширение;			
			строка.Адрес = ПомещенныйФайл.Адрес;
		КонецЕсли;
	КонецЦикла;
	Картинка = ПомещенныйФайл.Адрес;
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивСсылокФото(Знач СсылкиНаФото,КодТочки)
	
	МассивСсылокФото = Новый Массив;
	
	Для Каждого стрФото Из СсылкиНаФото Цикл
		Адрес = стрФото.Адрес;
		РасширениеФайла = СтрЗаменить(стрФото.РасширениеФайла,".","");
		
		ИмяВСсылке = Формат(ТекущаяДата(),"ДФ=ггггММддЧЧммсс_") + (1 + СсылкиНаФото.Индекс(стрФото))+ "_" +  КодТочки;
		//ИмяВСсылке = "problem";
		
		СсылкаНаФото = ОбработатьФайлНаСервере(Адрес, РасширениеФайла, ИмяВСсылке);
		МассивСсылокФото.Добавить(СсылкаНаФото);
	КонецЦикла;
	
	Возврат МассивСсылокФото;
КонецФункции

&НаСервереБезКонтекста
Функция ОбработатьФайлНаСервере(Адрес, РасширениеФайла, ИмяВСсылке)
	
	// Получение данных из временного хранилища
	Данные = ПолучитьИзВременногоХранилища(Адрес);
	// Получение имени временного файла
	ФайлФото = ПолучитьИмяВременногоФайла(РасширениеФайла);
	// Сохранение данных во временный файл
	Данные.Записать(ФайлФото);
	
	//ФайлЛога = "C:\Users\g1Csrv$\AppData\Local\Temp\LogFile_S3_task.txt";
	ФайлЛога = ПолучитьИмяВременногоФайла("txt");
	
	// Обработка файла…
	ИмяБакета		= "tasksphoto";
	КлючДоступа		= "AKIA5IASEGZYFNOJFZJG";
	СекретныйКлюч	= "uZPLq17LTiyb9KnBgt8xluGwIkBAeb4vYrHj+Oz9";
	ИмяЗаписи		= ИмяВСсылке;
	ПутьКФайлу		= ФайлФото;
	ТипКонтента		= "image/" + РасширениеФайла;
	ЗаписьЛога		= " -ErrorVariable err -InformationVariable info -WarningVariable warn; ($err.ErrorRecord, $warn, $info) | Out-File " + ФайлЛога;
	
	ТекстКоманды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"C:\Program Files\PowerShell\7\pwsh.exe -Command ""& {Write-S3Object -BucketName %1 -Region us-east-1 -AccessKey %2 -SecretKey %3 -Key %4 -File %5 -CannedACLName public-read -PublicReadOnly -ContentType %6%7}""", 
	ИмяБакета,
	КлючДоступа,
	СекретныйКлюч,
	ИмяЗаписи,
	ПутьКФайлу,
	ТипКонтента,
	ЗаписьЛога);
	
	КодВозврата = Неопределено;
	
	Попытка
		ЗапуститьПриложение(ТекстКоманды,,Истина,КодВозврата);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
	// Хорошим тоном будет удалить временный файл
	УдалитьФайлы(ФайлФото);
	
	ТекстДок = Новый ТекстовыйДокумент;
	Файл = Новый Файл(ФайлЛога);
	Если Файл.Существует() Тогда
		ТекстДок.Прочитать(ФайлЛога,КодировкаТекста.UTF8);
		ТекстЛога = ТекстДок.ПолучитьТекст();
		УдалитьФайлы(ФайлЛога);
	Иначе
		ТекстЛога = "";
	КонецЕсли;
	Если НЕ ПустаяСтрока(ТекстЛога) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка: " + ТекстЛога);
		ВызватьИсключение(ТекстЛога);
	КонецЕсли;
	
	Ответ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"https://%1.s3.amazonaws.com/%2",
	ИмяБакета,
	ИмяЗаписи);
	
	Возврат Ответ;
	
КонецФункции

&НаСервереБезКонтекста
Функция Исполнитель(Услуга, Точка)
	Возврат Обработки.СервисДеск.Исполнитель(Услуга, Точка);
КонецФункции


#КонецОбласти


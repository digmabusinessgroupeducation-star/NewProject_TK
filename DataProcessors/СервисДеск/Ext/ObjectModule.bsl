
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область СлужебныйПрограммныйИнтерфейс
	
	
	Функция ВыгрузитьДанныеВFirestore(Данные)	Экспорт
		
		ВсеОК = Истина;
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		ТаблицаПодразделений = Новый ТаблицаЗначений;
		ТаблицаТочек = Новый ТаблицаЗначений;
		
		ТекстОшибок = "";
		РезВО = РегистрыСведений.РезультатыВыполненияОбработок;	
		СтрЗР = РезВО.СформироватьСтруктуруЗаписиРезультатов();
		СтрЗР.ИдентификаторОбработки = "ВыгрузкаДанныхВFirestore";
		СтрЗР.КлючОбработки = "Выгрузка данных в Firestore";
		
		ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
		ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, УзелПланаОбмена);
		СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		
		СоответствиеКоллекций = Новый Соответствие;
		СоответствиеКоллекций.Вставить("Справочник.ПодразделенияКомпании","subdivision");
		СоответствиеКоллекций.Вставить("Справочник.Посетители","sotrudniki");
		СоответствиеКоллекций.Вставить("Справочник.РолиСервисДеск","roli");
		СоответствиеКоллекций.Вставить("Справочник.ТерриторииИсполнителей","territories");
		СоответствиеКоллекций.Вставить("Справочник.ТорговыеПлощадки","markets");
		СоответствиеКоллекций.Вставить("Справочник.УслугиСервисДеск","task_categories");
		СоответствиеКоллекций.Вставить("Справочник.ФизическиеЛица","sotrudniki");
		
		Для Каждого стрМета Из Данные Цикл
			Если Не стрМета.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если стрМета.МетаПолноеИмя = "Справочник.Посетители" ИЛИ стрМета.МетаПолноеИмя = "Справочник.ФизическиеЛица" Тогда
				ПолучитьТаблицаСотрудников(ТаблицаСотрудников);
			КонецЕсли;
			
			Если стрМета.МетаПолноеИмя = "Справочник.ПодразделенияКомпании" Тогда
				ПолучитьТаблицаПодразделений(ТаблицаПодразделений);
			КонецЕсли;
			
			Если стрМета.МетаПолноеИмя = "Справочник.ТорговыеПлощадки" Тогда
				ПолучитьТаблицаТочек(ТаблицаТочек);
			КонецЕсли;
			
			Если стрМета.МетаПолноеИмя = "Справочник.УслугиСервисДеск" Тогда
				ЗарегистрироватьУслуги();
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	МетаданныеИзменения.Ссылка КАК Ссылка
			|ИЗ
			|	" + стрМета.МетаПолноеИмя + ".Изменения КАК МетаданныеИзменения
			|ГДЕ
			|	Узел = &Узел";
			
			Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			МассивЭлементов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			ПланыОбмена.ВыбратьИзменения(УзелПланаОбмена, 1, МассивЭлементов);
			
			Коллекция = СоответствиеКоллекций.Получить(стрМета.МетаПолноеИмя);
			
			кэ = 0;
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если НужноВыгружать(Выборка.Ссылка, стрМета.МетаПолноеИмя, ТаблицаСотрудников, ТаблицаПодразделений, ТаблицаТочек) Тогда
					Если стрМета.МетаПолноеИмя = "Справочник.ТерриторииИсполнителей" Тогда
						document_ID	= Строка(Выборка.Ссылка.УникальныйИдентификатор());
						//document_ID	= Строка(Выборка.Ссылка.Владелец.УникальныйИдентификатор());
					Иначе
						document_ID	= Строка(Выборка.Ссылка.УникальныйИдентификатор());
					КонецЕсли;
					АдресРесурса= Обработки.СервисДеск.ПодготовитьАдресРесурса("PATCH", ПараметрыДляЗапроса.Project_ID, Коллекция, document_ID);
					ТелоЗапроса = Обработки.СервисДеск.ПодготовитьТелоЗапроса(ПараметрыДляЗапроса, СоединениеFire, Коллекция, Выборка.Ссылка);
					
					ОК = Обработки.СервисДеск.ИзменитьДокументы_PATCH(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса, ТелоЗапроса);
					ВсеОК = ВсеОК И ОК;	
					
					Если ОК Тогда
						кэ = 1 + кэ;
					Иначе
						ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Выборка.Ссылка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			РезВО.ДобавитьКомментарий("Выгружено " + кэ + " " + стрМета.МетаПолноеИмя, СтрЗР);
			
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелПланаОбмена,1);
			
		КонецЦикла;
		
		СоединениеFire = Неопределено;
		
		РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР,НЕ ЗначениеЗаполнено(ТекстОшибок));
		
		Возврат ВсеОК;
		
	КонецФункции
	
	Функция ВыгрузитьКалендарьВFirestore(Дата1, Дата2)	Экспорт
		
		ВсеОК = Истина;
		ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
		ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, УзелПланаОбмена);
		СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь КАК ПроизводственныйКалендарь,
		|	ДанныеПроизводственногоКалендаря.Дата КАК Дата,
		|	ДанныеПроизводственногоКалендаря.Год КАК Год,
		|	ДанныеПроизводственногоКалендаря.ВидДня КАК ВидДня,
		|	ДанныеПроизводственногоКалендаря.ДатаПереноса КАК ДатаПереноса
		|ИЗ
		|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
		|ГДЕ
		|	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &Дата1 И &Дата2";
		
		Запрос.УстановитьПараметр("Дата1", Дата1);
		Запрос.УстановитьПараметр("Дата2", Дата2);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			document_ID = Формат(Выборка.Дата,"ДФ=yyyy_MM_dd");
			АдресРесурса= Обработки.СервисДеск.ПодготовитьАдресРесурса("PATCH", ПараметрыДляЗапроса.Project_ID, "working_time_calendar", document_ID);
			ТелоЗапроса = Обработки.СервисДеск.ПодготовитьТелоЗапроса(ПараметрыДляЗапроса, СоединениеFire, "working_time_calendar", Выборка);
			
			ОК = Обработки.СервисДеск.ИзменитьДокументы_PATCH(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса, ТелоЗапроса);
			ВсеОК = ВсеОК И ОК;	
		КонецЦикла;
		
		СоединениеFire = Неопределено;
		
		Возврат ВсеОК;
		
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область СлужебныеПроцедурыИФункции
	
	
	Функция НужноВыгружать(Ссылка, МетаПолноеИмя, ТаблицаСотрудников, ТаблицаПодразделений, ТаблицаТочек)
		
		Если МетаПолноеИмя = "Справочник.РолиСервисДеск" ИЛИ МетаПолноеИмя = "Справочник.ТерриторииИсполнителей" ИЛИ  МетаПолноеИмя = "Справочник.УслугиСервисДеск" Тогда 
			Возврат Истина;
		ИначеЕсли МетаПолноеИмя = "Справочник.Посетители" ИЛИ  МетаПолноеИмя = "Справочник.ФизическиеЛица" Тогда
			Нужон = ТаблицаСотрудников.Найти(Ссылка, "Сотрудник") <> Неопределено;	
			Возврат Нужон;
		ИначеЕсли МетаПолноеИмя = "Справочник.ТорговыеПлощадки" Тогда
			ДатаОткрытия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"ДатаОткрытия");
			ДатаЗакрытия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"ДатаЗакрытия");
			ТочкаЗакрыта = ДатаЗакрытия > ДатаОткрытия И ТекущаяДата() > ДатаЗакрытия;
			Если ТочкаЗакрыта Тогда
				Возврат НЕ ТочкаЗакрыта;
			Иначе
				Нужон = ТаблицаТочек.Найти(Ссылка, "Точка") <> Неопределено;	
				Возврат Нужон;
			КонецЕсли;
		ИначеЕсли МетаПолноеИмя = "Справочник.ПодразделенияКомпании" Тогда
			Нужон = ТаблицаПодразделений.Найти(Ссылка, "Подразделение") <> Неопределено;	
			Возврат Нужон;
		КонецЕсли;
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область ВспомогательныеПроцедурыИФункции
	
	
	Процедура ЗарегистрироватьУслуги()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	УслугиСервисДеск.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УслугиСервисДеск КАК УслугиСервисДеск
		|ГДЕ
		|	НЕ УслугиСервисДеск.ЭтоГруппа
		|	И УслугиСервисДеск.Родитель В
		|			(ВЫБРАТЬ
		|				УслугиСервисДескИзменения.Ссылка КАК Ссылка
		|			ИЗ
		|				Справочник.УслугиСервисДеск.Изменения КАК УслугиСервисДескИзменения
		|			ГДЕ
		|				УслугиСервисДескИзменения.Ссылка.ЭтоГруппа
		|				И УслугиСервисДескИзменения.Узел = &Узел)";
		
		Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
		
		
		//РезВО.ДобавитьКомментарий("Зарегистрировано " + ВыборкаДетальныеЗаписи.Количество() + " номенклатуры", СтрЗР);
		
	КонецПроцедуры
	
	Процедура ПолучитьТаблицаСотрудников(ТаблицаСотрудников)
		
		Если ЗначениеЗаполнено(ТаблицаСотрудников) Тогда Возврат; КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Сотрудник
		|ИЗ
		|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		|ГДЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РолиСервисДескСотрудники.Сотрудник
		|ИЗ
		|	Справочник.РолиСервисДеск.Сотрудники КАК РолиСервисДескСотрудники";
		
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаСотрудников = РезультатЗапроса.Выгрузить();
		
	КонецПроцедуры
	
	Процедура ПолучитьТаблицаПодразделений(ТаблицаПодразделений)
		
		Если ЗначениеЗаполнено(ТаблицаПодразделений) Тогда Возврат; КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.ПодразделениеКомпании КАК Подразделение
		|ИЗ
		|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		|ГДЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""";
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаПодразделений = РезультатЗапроса.Выгрузить();
		
	КонецПроцедуры
	
	Процедура ПолучитьТаблицаТочек(ТаблицаТочек)
		
		Если ЗначениеЗаполнено(ТаблицаТочек) Тогда Возврат; КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка КАК Точка
		|ИЗ
		|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		|ГДЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""";
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаТочек = РезультатЗапроса.Выгрузить();
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
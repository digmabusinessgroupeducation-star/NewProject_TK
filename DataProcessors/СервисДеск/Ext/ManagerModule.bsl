
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область СлужебныйПрограммныйИнтерфейс


Процедура ЗаполнитьТаблицуДокументами (мДокументы, Заявки, Кратко) Экспорт
	СоответствиеКартинки = Новый Соответствие;
	СоответствиеКартинки.Вставить("0",3);
	СоответствиеКартинки.Вставить("1",5);
	СоответствиеКартинки.Вставить("2",7);
	СоответствиеКартинки.Вставить("3",8);
	СоответствиеКартинки.Вставить("4",1);
	СоответствиеКартинки.Вставить("5",0);
	
	Для каждого Документ Из мДокументы Цикл
		нСтрока = Заявки.Добавить();
		Поля = Документ.fields;
		нСтрока.ID = ГуидПоля(Документ.name);
		нСтрока.СрокВыполнения = МестноеВремя(Дата(1970,1,1) + Поля.datePlaneCompletion.integerValue/1000);
		нСтрока.Статус =  Перечисления.СтатусыСервисДеск[Число(Поля.status.integerValue)];
		нСтрока.Услуга = Справочники.УслугиСервисДеск.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.categoryRef.referenceValue)));
		Если Кратко Тогда Продолжить; КонецЕсли;
		нСтрока.Номер = Поля.number.integerValue;
		//нСтрока.Дата = ДатаИзСтроки(Поля.dateCreation.timestampValue);
		нСтрока.Дата = МестноеВремя(Дата(1970,1,1) + Поля.dateCreation.integerValue/1000);
		стрДатаВыполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля, "dateCompletion", Новый Структура("integerValue",0));
		нСтрока.ДатаВыполнения =  МестноеВремя(Дата(1970,1,1) + стрДатаВыполнения.integerValue/1000);
		нСтрока.ИндексСтатуса = СоответствиеКартинки.Получить(Поля.status.integerValue);
		нСтрока.Срочная = Поля.urgent.booleanValue;
		нСтрока.Просроченная = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля, "isExpiredCompletion", Новый Структура("booleanValue",Ложь)).booleanValue;
		нСтрока.АвтоЗакрытая = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля, "closedAfter24hours",  Новый Структура("booleanValue",Ложь)).booleanValue;
		нСтрока.Точка = Справочники.ТорговыеПлощадки.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.marketRef.referenceValue)));
		Если Поля.Свойство("roleRef") Тогда
			нСтрока.Роль = Справочники.РолиСервисДеск.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.roleRef.referenceValue)));
		Иначе
		КонецЕсли;
		Если Поля.kuratorRef.Свойство("referenceValue") Тогда
			нСтрока.Куратор = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.kuratorRef.referenceValue)));
		КонецЕсли;
		
		Инициатор = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.authorRef.referenceValue)));
		Если ПустаяСтрока(Инициатор.КоД) Тогда
			Инициатор = Справочники.Посетители.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.authorRef.referenceValue)));
		КонецЕсли;
		Если нСтрока.Статус = Перечисления.СтатусыСервисДеск.not_assigned Тогда
			Исполнитель = Неопределено;
		Иначе
			Исполнитель = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.responsibleUserRef.referenceValue)));
			Если ПустаяСтрока(Исполнитель.Код) Тогда
				Исполнитель = Справочники.Посетители.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.responsibleUserRef.referenceValue)));
			КонецЕсли;
		КонецЕсли;
		
		нСтрока.Инициатор = Инициатор;
		нСтрока.Исполнитель = Исполнитель;
		
		КомментарийВыполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля, "commentCompletion", Новый Структура("stringValue"));
		мФото = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля.urlsCreation.arrayValue, "values", Новый Массив);
		ФотоВыполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Поля, "urlsCompletion", Новый Структура("arrayValue"));
		стрФотоВыполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ФотоВыполнения, "arrayValue", Новый Структура("arrayValue"));
		мФотоВыполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрФотоВыполнения, "values", Новый Массив);
		
		нСтрока.Описание = Поля.commentCreation.stringValue;
		нСтрока.КомментарийВыполнения = КомментарийВыполнения.stringValue;
		нСтрока.Фото = СтрокаИзМассиваСтрок(мФото);
		нСтрока.ФотоВыполнения = СтрокаИзМассиваСтрок(мФотоВыполнения);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПолучитьКоллекцию (мДокументы, ТЗ) Экспорт
	
	//ТЗ = Новый ТаблицаЗначений;
	СоответствиеКолонок = Новый Соответствие;
	
	Для Каждого Документ Из мДокументы Цикл
		Поля = Документ.fields;
		Для Каждого Поле из Поля Цикл 
			нКолонка = ТЗ.Колонки.Найти(Поле.Ключ);
			Если нКолонка = Неопределено Тогда
				ТЗ.Колонки.Добавить(Поле.Ключ);
			КонецЕсли;
		КонецЦикла; 
	КонецЦикла;
	
	Для Каждого Документ Из мДокументы Цикл
		нСтрока = ТЗ.Добавить();
		Поля = Документ.fields;
		Для Каждого Поле из Поля Цикл
			Для Каждого ПолеПоля из Поле.Значение Цикл
				нСтрока[Поле.Ключ] = ПолеПоля.Значение;	
			КонецЦикла; 
		КонецЦикла; 
		//ТЗ.количество()
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьОтправитьЗаявку(Параметры, Заявка) Экспорт
	СоединениеFire = Обработки.СервисДеск.СоединениеFire(Параметры.Service_CloudFirestoreAPI);
	
	
	ТочкаКод = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Точка,"Код"));
	ТочкаНомер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Точка,"Global_ID");
	Если Заявка.Плановая Тогда
		Заявка.Исполнитель = Исполнитель(Заявка.Услуга, Заявка.Точка);
		Если НЕ ЗначениеЗаполнено(Заявка.Исполнитель) Тогда
			ОбщегоНазначения.СообщитьПользователю("Для (" + ТочкаКод + ") " + СокрЛП(Заявка.Точка) + " не установлен исполнитель!");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Заявка.Исполнитель) Тогда
		Статус = 4;
	ИначеЕсли Заявка.Срочная Тогда
		Статус = 5; 
	Иначе
		Статус = 0;
	КонецЕсли;
	Дата = ТекущаяДатаСеанса();
	ДатаВМиллисекундах = (УниверсальноеВремя(Дата) - Дата(1970,1,1)) * 1000;
	ВремяВыполнения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Услуга,"ВремяВыполнения");
	СрокВыполнения = Обработки.СервисДеск.СрокВыполнения(Заявка.Срочная, Дата, ВремяВыполнения);
	СрокВыполненияВМиллисекундах = (УниверсальноеВремя(СрокВыполнения) - Дата(1970,1,1)) * 1000;
	НомерЗаявки = НомерЗаявки(Заявка.Точка, Параметры, СоединениеFire);
	
	ДанныеЗаявки = Новый Структура;
	ДанныеЗаявки.Вставить("authorRef",			Заявка.Инициатор);
	ДанныеЗаявки.Вставить("categoryName",		СокрЛП(Заявка.Услуга));
	ДанныеЗаявки.Вставить("categoryParentName",	СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Услуга,"Родитель")));
	ДанныеЗаявки.Вставить("categoryRef",		Заявка.Услуга);
	ДанныеЗаявки.Вставить("commentCompletion",	Заявка.КомментарийВыполнения);
	ДанныеЗаявки.Вставить("commentCreation",	Заявка.Описание);
	ДанныеЗаявки.Вставить("dateCreation",		ДатаВМиллисекундах);
	ДанныеЗаявки.Вставить("datePlaneCompletion",СрокВыполненияВМиллисекундах);
	ДанныеЗаявки.Вставить("dateCompletion",		0);
	ДанныеЗаявки.Вставить("roleRef ",			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Услуга,"РольИсполнители"));
	ДанныеЗаявки.Вставить("marketName",			СокрЛП(Заявка.Точка));
	ДанныеЗаявки.Вставить("marketNumber",		Формат(ТочкаНомер,"ЧГ=0"));
	ДанныеЗаявки.Вставить("marketRef",			Заявка.Точка);
	ДанныеЗаявки.Вставить("regionRef",			ОбластьТочки(Заявка.Точка));
	ДанныеЗаявки.Вставить("month",				Формат(СрокВыполнения,"ДФ=ггг_ММ"));
	ДанныеЗаявки.Вставить("number",				НомерЗаявки);
	ДанныеЗаявки.Вставить("responsibleUserRef",	Заявка.Исполнитель);
	ДанныеЗаявки.Вставить("responsibleUserName",СокрЛП(Заявка.Исполнитель));
	ДанныеЗаявки.Вставить("status",				Статус);
	ДанныеЗаявки.Вставить("subdivisionRef",		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Точка,"ПодразделениеКомпании"));
	ДанныеЗаявки.Вставить("urgent",				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявка.Услуга,"Срочная"));
	ДанныеЗаявки.Вставить("urlsCompletion",		Новый Массив);
	ДанныеЗаявки.Вставить("urlsCreation",		Заявка.МассивСсылокФото);
	ДанныеЗаявки.Вставить("vremya_vypolneniya",	ВремяВыполнения);
	
	АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("POST", Параметры.Project_ID, "tasks");
	ТелоЗапроса = Обработки.СервисДеск.ПодготовитьТелоЗапроса(Параметры, СоединениеFire, "tasks", ДанныеЗаявки);
	
	//ОтветHTTP = Обработки.СервисДеск.СоздатьДокумент_POST(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса);	// Это же тестовая база
	
	СоединениеFire = Неопределено;
	
	//Возврат ОтветHTTP.КодСостояния = 200;
	Возврат Истина; // Это же тестовая база
	
КонецФункции

Функция Токен(Параметры, СоединениеFire) Экспорт
	
	Результат = РегистрыСведений.ТокеныАутентификацииFirebase.Получить(Новый Структура("ProjectID, UserID", Параметры.Project_ID, Параметры.UserUID));
	Если Результат.ДействителенДо > ТекущаяДатаСеанса() Тогда
		idToken = Результат.Токен;
		Возврат idToken;
	Иначе
		СтрокаКоманды = "c:\install\firebaseshit\FirebaseShit.exe " + Параметры.UserUID;
		ФайлТокен = ПолучитьИмяВременногоФайла("txt");
		
		Попытка
			ЗапуститьПриложение("cmd /c "+СтрокаКоманды+" >"+ФайлТокен,,Истина);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
		ТекстДок = Новый ТекстовыйДокумент;
		Файл = Новый Файл(ФайлТокен);
		Если Файл.Существует() Тогда
			ТекстДок.Прочитать(ФайлТокен,КодировкаТекста.UTF8);
			ТекстЛога = ТекстДок.ПолучитьТекст();
			УдалитьФайлы(ФайлТокен);
			CustomToken = Прав(ТекстЛога,СтрДлина(ТекстЛога)-20);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(CustomToken) Тогда
			Resource = "v1/accounts:signInWithCustomToken";
			АдресРесурса = Resource + "/?key=" + Параметры.Web_API_Key;
			ТелоЗапроса = ПодготовитьТелоЗапроса(Параметры, СоединениеFire, "SecureToken", CustomToken);
			СоединениеIdentity = СоединениеFire(Параметры.Service_IdentityToolkitAPI);
			
			ОтветHTTP = СоздатьДокумент_POST(Параметры, СоединениеIdentity, АдресРесурса, ТелоЗапроса, Истина);
			Если ОтветHTTP.КодСостояния = 200 Тогда
				ОтветДанные = Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
				idToken = ОтветДанные.idToken;
				Записать_idToken(Параметры, ОтветДанные.expiresIn, ОтветДанные.idToken);
			КонецЕсли;
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Не получен CustomToken.");
		КонецЕсли;
		
		СоединениеIdentity = Неопределено;
		
		Возврат idToken;
	КонецЕсли;
КонецФункции

Функция ПолучитьДокументы_GET(Параметры, СоединениеFire, АдресРесурса, nextPageToken = "") Экспорт
	
	idToken = Токен(Параметры, СоединениеFire);
	
	ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса);
	ЗапросHTTP.Заголовки.Вставить("Authorization","Bearer "+idToken);
	
	ОтветHTTP = СоединениеFire.Получить(ЗапросHTTP);
	
	//Если ОтветHTTP.КодСостояния <> 200 Тогда
	//	Ошибка400("GET", АдресРесурса, ОтветHTTP);
	//КонецЕсли;
	
	Возврат ОтветHTTP;
	
КонецФункции

Функция СоздатьДокумент_POST(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса, get_token = Ложь) Экспорт
	
	ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса);
	Если Не get_token ТоГда
		idToken = Токен(Параметры, СоединениеFire);
		ЗапросHTTP.Заголовки.Вставить("Authorization", "Bearer " + idToken);
	КонецЕсли;
	
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = СоединениеFire.ОтправитьДляОбработки(ЗапросHTTP);
	
	Если ОтветHTTP.КодСостояния <> 200 Тогда
		Ошибка400("POST", АдресРесурса, ОтветHTTP);
	КонецЕсли;
	
	Возврат ОтветHTTP;
	
КонецФункции

Функция ИзменитьДокументы_PATCH(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса, ТекстОшибок = "") Экспорт
	
	idToken = Токен(Параметры, СоединениеFire);
	
	ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса);
	ЗапросHTTP.Заголовки.Вставить("Authorization", "Bearer " + idToken);
	ЗапросHTTP.УстановитьТелоИзСтроки(ТелоЗапроса);
	
	ОтветHTTP = СоединениеFire.Изменить(ЗапросHTTP);
	
	Если ОтветHTTP.КодСостояния <> 200 Тогда
		Ошибка400("PATCH", АдресРесурса, ОтветHTTP, ТекстОшибок);
	КонецЕсли;
	
	Возврат ОтветHTTP.КодСостояния = 200;
	
КонецФункции

Функция УдалитьДокумент_DELETE(Параметры, СоединениеFire, АдресРесурса) Экспорт
	
	idToken = Токен(Параметры, СоединениеFire);
	
	ЗапросHTTP = Новый HTTPЗапрос(АдресРесурса);
	ЗапросHTTP.Заголовки.Вставить("Authorization","Bearer "+idToken);
	
	ОтветHTTP = СоединениеFire.Удалить(ЗапросHTTP);
	
	Если ОтветHTTP.КодСостояния <> 200 Тогда
		Ошибка400("DELETE", АдресРесурса, ОтветHTTP);
	КонецЕсли;
	
	Возврат ОтветHTTP;
	
КонецФункции


#КонецОбласти




#Область СлужебныеПроцедурыИФункции


Функция ГуидПоля(name)
	мCтрок = СтрРазделить(name,"/");
	ГуидПоля = мCтрок[мCтрок.Количество()-1];
	Возврат ГуидПоля;
КонецФункции

Функция СрокВыполнения(Срочная, Дата, ВремяВыполнения) Экспорт
	Если Срочная Тогда
		Результат = Дата + 3600 * ВремяВыполнения;
	Иначе
		ВремяНачалоРаботы	= Дата(1,1,1,09,0,0);
		ВремяНачалоПерерыва	= Дата(1,1,1,13,0,0);
		ВремяКонецПерерыва	= Дата(1,1,1,14,0,0);
		ВремяКонецРаботы	= Дата(1,1,1,18,0,0);
		мРежимРаботы = Новый Массив;
		мРежимРаботы.Добавить(Новый Структура("ВремяНачалоПолуСмены,ВремяКонцаПолуСмены,ВремяНачалоСледующейПолуСмены",ВремяНачалоРаботы,ВремяНачалоПерерыва,ВремяКонецПерерыва));
		мРежимРаботы.Добавить(Новый Структура("ВремяНачалоПолуСмены,ВремяКонцаПолуСмены,ВремяНачалоСледующейПолуСмены",ВремяКонецПерерыва,ВремяКонецРаботы,Дата(1,1,2,9,0,0)));
		
		Календарь = Справочники.ПроизводственныеКалендари.НайтиПоКоду("УК");
		кДата = Дата;
		ДнейБезКалендаря = 0;
		ОставшеесяВремяВСекундах = ВремяВыполнения * 3600;
		Пока ОставшеесяВремяВСекундах > 0 Цикл
			ВыборкаДеньКалендаря = РегистрыСведений.ДанныеПроизводственногоКалендаря.Получить(Новый Структура("ПроизводственныйКалендарь,Дата,Год",Календарь,НачалоДня(кДата),Год(кДата)));
			Если ВыборкаДеньКалендаря.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий Или ВыборкаДеньКалендаря.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный Тогда
				Для Каждого СтрПолДня Из мРежимРаботы Цикл
					ТекущееНачалоПолуСмены = НачалоДня(кДата) + (СтрПолДня.ВремяНачалоПолуСмены - Дата(1,1,1));
					ТекущееОкончаниеПолуСмены = НачалоДня(кДата) + (СтрПолДня.ВремяКонцаПолуСмены - Дата(1,1,1));
					СледующееОкончаниеПолуСмены = НачалоДня(кДата) + (СтрПолДня.ВремяНачалоСледующейПолуСмены - Дата(1,1,1));
					Если кДата < ТекущееНачалоПолуСмены Тогда
						кДата = ТекущееНачалоПолуСмены;	
					КонецЕсли;
					Если кДата <= ТекущееОкончаниеПолуСмены Тогда
						ОставшеесяВремяВСекундах = ОставшеесяВремяВСекундах - (ТекущееОкончаниеПолуСмены - кДата);
						Если  ОставшеесяВремяВСекундах <= 0 Тогда
							Прервать;
						Иначе
							кДата = СледующееОкончаниеПолуСмены;
						КонецЕсли;
					ИначеЕсли кДата < СледующееОкончаниеПолуСмены Тогда
						кДата = СледующееОкончаниеПолуСмены;	
					КонецЕсли;
				КонецЦикла;
			Иначе
				кДата = НачалоДня(кДата) + 86400;
				ДнейБезКалендаря = 1 + ДнейБезКалендаря;
			КонецЕсли;
			Если ДнейБезКалендаря > 30 Тогда
				ОбщегоНазначения.СообщитьПользователю("Не занружен календарь по " + кДата);
				Возврат 0;
			КонецЕсли;
		КонецЦикла;
		
		Результат = ТекущееОкончаниеПолуСмены + ОставшеесяВремяВСекундах;
		
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция НомерЗаявки(Точка, Параметры, СоединениеFire)
	АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("POST", Параметры.Project_ID, "", , , ":runQuery");
	ТелоЗапроса = Обработки.СервисДеск.ПодготовитьТелоЗапроса(Параметры, СоединениеFire, "ПоследнийНомерЗаявки", Точка);
	ОтветHTTP = Обработки.СервисДеск.СоздатьДокумент_POST(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		JSONСтрока = ОтветHTTP.ПолучитьТелоКакСтроку();
		СтруктураОтвета = Обработки.СервисДеск.Из_JSON(JSONСтрока);
		Если СтруктураОтвета[0].Свойство("document") Тогда
			НомерЗаявки = СтруктураОтвета[0].document.fields.number.integerValue;
		Иначе
			НомерЗаявки = 0;
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьПользователю(ОтветHTTP.ПолучитьТелоКакСтроку());
	КонецЕсли;
	
	Возврат 1 + НомерЗаявки;
КонецФункции

Функция Исполнитель(Услуга, Точка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Услуга) ИЛИ НЕ ЗначениеЗаполнено(Точка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УслугиСервисДеск.Ссылка КАК Услуга,
	|	РолиСервисДескСотрудники.Сотрудник КАК Исполнитель,
	|	ТерриторииИсполнителей.Ссылка КАК Территория
	|ИЗ
	|	Справочник.УслугиСервисДеск КАК УслугиСервисДеск
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РолиСервисДеск.Сотрудники КАК РолиСервисДескСотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТерриторииИсполнителей КАК ТерриторииИсполнителей
	|			ПО РолиСервисДескСотрудники.Сотрудник = ТерриторииИсполнителей.Владелец
	|		ПО УслугиСервисДеск.РольИсполнители = РолиСервисДескСотрудники.Ссылка
	|ГДЕ
	|	УслугиСервисДеск.Ссылка = &Услуга";
	
	Запрос.УстановитьПараметр("Услуга", Услуга);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗапросТ = Новый Запрос;
		ЗапросТ.Текст = 
		"ВЫБРАТЬ
		|	ТорговыеПлощадки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		|ГДЕ
		|	ТорговыеПлощадки.Ссылка = &Точка
		|	И ТорговыеПлощадки.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ТерриторииИсполнителейТочки.Точка КАК Точка
		|			ИЗ
		|				Справочник.ТерриторииИсполнителей.Точки КАК ТерриторииИсполнителейТочки
		|			ГДЕ
		|				ТерриторииИсполнителейТочки.Ссылка = &Территория)";
		
		ЗапросТ.УстановитьПараметр("Территория", Выборка.Территория);
		ЗапросТ.УстановитьПараметр("Точка", Точка);
		
		РезультатЗапросаТ = ЗапросТ.Выполнить();
		
		Если НЕ РезультатЗапросаТ.Пустой() Тогда
			Возврат Выборка.Исполнитель;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПулИсполнителей(Услуга)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	УслугиСервисДескРоли.Роль КАК Роль
	|ИЗ
	|	Справочник.УслугиСервисДеск.Роли КАК УслугиСервисДескРоли
	|ГДЕ
	|	УслугиСервисДескРоли.Ссылка = &Услуга
	|	И УслугиСервисДескРоли.Роль.Выполнение";
	
	Запрос.УстановитьПараметр("Услуга", Услуга);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.Роль;
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

Функция СтрокаИзМассиваСтрок(МассивСтрок)
	СтрокаССылками = "";
	Для Каждого СсылкаНаФото Из МассивСтрок Цикл
		СтрокаССылками = СтрокаССылками + СсылкаНаФото.stringValue + ";";
	КонецЦикла;
	Возврат СтрокаССылками;
КонецФункции

Функция СоединениеFire(Сервис) Экспорт
	Возврат Новый HTTPСоединение(Сервис,,,,,,Новый ЗащищенноеСоединениеOpenSSL());
КонецФункции

Функция В_JSON(Структура, Сериализатор = Ложь) Экспорт
	ЗаписьJSON	= Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	Если Сериализатор Тогда
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, Структура, НазначениеТипаXML.Явное);
	Иначе
		ЗаписатьJSON(ЗаписьJSON, Структура);
	КонецЕсли;
	Возврат ЗаписьJSON.Закрыть(); 
КонецФункции

Функция Из_JSON(JSONСтрока) Экспорт
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(JSONСтрока);
	Структура = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	Возврат Структура;
КонецФункции

Процедура Ошибка400(Метод, Адрес, ОтветHTTP, ТекстОшибок = "") Экспорт
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Ошибка %1 %2 %3%4%5'; uk = 'Помилка %1%1 %2 %3%4%5'"),
	Метод,
	ОтветHTTP.КодСостояния,
	Адрес,
	Символы.ПС,
	ОтветHTTP.ПолучитьТелоКакСтроку());
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	ТекстОшибок = ТекстОшибок + ?(ПустаяСтрока(ТекстОшибок),"",Символы.ПС) + ТекстОшибки;
КонецПроцедуры


Функция ОбластьТочки(Точка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТорговыеПлощадки.ТерриторияПоКОАТУУ.Область КАК Область
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|ГДЕ
	|	ТорговыеПлощадки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Точка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Область;
	КонецЕсли;
	
КонецФункции

Функция ГеоТочка(стрГеометка)
	НачальныйНомер = СтрНайти(стрГеометка,">");
	ЧислоСимволов = СтрДлина(стрГеометка) - НачальныйНомер - 4;
	ЗначениеГеометка = Сред(стрГеометка,1 + НачальныйНомер,ЧислоСимволов);
	ЗначениеГеометка = СтрЗаменить(ЗначениеГеометка," ",",");
	мГеометка = СтрРазделить(ЗначениеГеометка,",",Ложь);
	Если мГеометка.Количество() = 2 Тогда
		Широта = СокрЛП(мГеометка[0]);
		Долгота = СокрЛП(мГеометка[1]);
	Иначе
		Широта = 90;
		Долгота = 0;
	КонецЕсли;
	
	Возврат Новый Структура("latitude, longitude", Широта, Долгота);
	
КонецФункции

Функция ТелефонСотрудника(Сотрудник)
	
	МассивПосетителей = Новый Массив;
	
	Посетитель = Новый Структура();
	Посетитель.Вставить("Наименование",(Сотрудник.Наименование));
	//Посетитель.Вставить("ДоступРазрешен",стрПосетитель.ДоступРазрешен);
	Посетитель.Вставить("КодПоДРФО",СокрЛП(Сотрудник.Код));
	//Посетитель.Вставить("ТелефонТК",СокрЛП(стрПосетитель.Телефон));
	Посетитель.Вставить("ЭтоКуратор",Ложь);
	МассивПосетителей.Добавить(Посетитель);
	
	СтрокаJSON = В_JSON(МассивПосетителей);
	
	Определение = Новый WSОпределения("https://orders-1c.digma.ua/zuUU/ws/cwd2.1cws?wsdl","ObmenSSL","bynthghbpf",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
	WS = Новый WSПрокси(Определение,"http://www.digma.ua","CommunicationWithDatabases","CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
	WS.Пользователь="ObmenSSL";
	WS.Пароль ="bynthghbpf";
	
	Рез = WS.RegisterVisitors(СтрокаJSON, Истина);
	
	ОтветРезультат = Из_JSON(Рез);
	Телефон = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОтветРезультат[0], "Телефон", "");
	
	Возврат Телефон; 
	
КонецФункции

Функция ЭтоРуководитель(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РолиСервисДескСотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РолиСервисДеск.Сотрудники КАК РолиСервисДескСотрудники
	|ГДЕ
	|	РолиСервисДескСотрудники.Сотрудник = &Сотрудник
	|	И РолиСервисДескСотрудники.Ссылка.ТипРоли = ЗНАЧЕНИЕ(Перечисление.ТипыРолейСотрудников.Руководители)";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЭтоСпамер(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РолиСервисДескСотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РолиСервисДеск.Сотрудники КАК РолиСервисДескСотрудники
	|ГДЕ
	|	РолиСервисДескСотрудники.Сотрудник = &Сотрудник
	|	И РолиСервисДескСотрудники.Ссылка.ТипРоли = ЗНАЧЕНИЕ(Перечисление.ТипыРолейСотрудников.Спамеры)";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЭтоКуратор(Сотрудник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
	|ГДЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
	|	И ТорговыеПлощадкиДополнительныеРеквизиты.Значение = &Сотрудник";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЭтоИсполнитель(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УслугиСервисДеск.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РолиСервисДеск.Сотрудники КАК РолиСервисДескСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УслугиСервисДеск КАК УслугиСервисДеск
	|		ПО РолиСервисДескСотрудники.Ссылка = УслугиСервисДеск.РольИсполнители
	|			И (РолиСервисДескСотрудники.Ссылка.ТипРоли = ЗНАЧЕНИЕ(Перечисление.ТипыРолейСотрудников.Исполнители))
	|ГДЕ
	|	РолиСервисДескСотрудники.Сотрудник = &Сотрудник
	|	И УслугиСервисДеск.Ссылка ЕСТЬ НЕ NULL ";
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ТипСотрудника(Сотрудник)
	
	ЭтоРуководитель = ЭтоРуководитель(Сотрудник);	//	VIP
	ЭтоКуратор = ЭтоКуратор(Сотрудник);
	ЭтоИсполнитель = ЭтоИсполнитель(Сотрудник);
	ЭтоСпамер = ЭтоСпамер(Сотрудник);
	
	Если ЭтоРуководитель Тогда
		ТипСотрудника = 4;
	ИначеЕсли ЭтоСпамер Тогда
		ТипСотрудника = 3;
	ИначеЕсли ЭтоКуратор Тогда
		ТипСотрудника = 2;
	ИначеЕсли ЭтоИсполнитель Тогда
		ТипСотрудника = 1;
	Иначе 
		ТипСотрудника = 0;
	КонецЕсли;
	
	Возврат ТипСотрудника;
	
КонецФункции

Функция ТочкиСотрудника1(Сотрудник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТерриторииИсполнителейТочки.Точка КАК Точка
	|ИЗ
	|	Справочник.ТерриторииИсполнителей.Точки КАК ТерриторииИсполнителейТочки
	|ГДЕ
	|	ТерриторииИсполнителейТочки.Ссылка.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Сотрудник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции
Функция ТочкиСотрудника(тчТочки)
	
	текДата = ТекущаяДата();
	мТочки = тчТочки.ВыгрузитьКолонку("Точка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка КАК Точка
	|ИЗ
	|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
	|ГДЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ(&Ссылка)
	|	И ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
	|	И НЕ(ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.ДатаЗакрытия > ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.ДатаОткрытия
	|				И &текДата > ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.ДатаЗакрытия)";
	
	Запрос.УстановитьПараметр("Ссылка", мТочки);
	Запрос.УстановитьПараметр("текДата", текДата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция МассивСсылок(Параметры,СоединениеFire,ВидДанных,Данные)
	
	МассивСсылок = Новый Массив;
	
	Если ВидДанных = "Календарь" Тогда
		ВремяНачалоРаботы = 9; 
		ДлительностьДоПерерыва = 4; 
		ВремяОкончанияПерерыва = 14; 
		ДлительностьПослеПерерыва = 4;
		Если Данные.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Рабочий Или Данные.ВидДня = Перечисления.ВидыДнейПроизводственногоКалендаря.Предпраздничный Тогда
			МассивСсылок.Добавить(Новый Структура("integerValue", ВремяНачалоРаботы));
			МассивСсылок.Добавить(Новый Структура("integerValue", ДлительностьДоПерерыва));
			МассивСсылок.Добавить(Новый Структура("integerValue", ВремяОкончанияПерерыва));
			МассивСсылок.Добавить(Новый Структура("integerValue", ДлительностьПослеПерерыва));
		Иначе
			МассивСсылок.Добавить(Новый Структура("integerValue", 0));
			МассивСсылок.Добавить(Новый Структура("integerValue", 0));
			МассивСсылок.Добавить(Новый Структура("integerValue", 0));
			МассивСсылок.Добавить(Новый Структура("integerValue", 0));
		КонецЕсли;
		
	ИначеЕсли ВидДанных = "Сотрудники" Тогда
		Для Каждого стрСотрудник Из Данные Цикл
			Структура_reference = СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",стрСотрудник.Сотрудник, Истина);
			МассивСсылок.Добавить(Структура_reference);
		КонецЦикла;
		
	ИначеЕсли ВидДанных = "Роли" Тогда
		Структура_reference = СсылкаНаДокумент(Параметры,СоединениеFire,"roli",Данные);
		МассивСсылок.Добавить(Структура_reference);
		//Для Каждого стрРоль Из Данные Цикл
		//	Структура_reference = СсылкаНаДокумент(Параметры,СоединениеFire,"roli",стрРоль.Роль);
		//	МассивСсылок.Добавить(Структура_reference);
		//КонецЦикла;
		
	ИначеЕсли ВидДанных = "Точки" Тогда
		Для Каждого стрТочки Из Данные Цикл
			Структура_reference = СсылкаНаДокумент(Параметры,СоединениеFire,"markets",стрТочки.Точка);
			МассивСсылок.Добавить(Структура_reference);
		КонецЦикла;
		
	ИначеЕсли ВидДанных = "СсылкиНаФото" Тогда
		Для Каждого СсылкаНаФото Из Данные Цикл
			МассивСсылок.Добавить(Новый Структура("stringValue",СсылкаНаФото));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Новый Структура("values",МассивСсылок);
	
КонецФункции

Функция СсылкаНаДокумент(Параметры, СоединениеFire, Коллекция, Ссылка, ДопПризнак = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Ссылка) Тогда	
		documentId = Строка(Ссылка.УникальныйИдентификатор());
		АдресРесурса = ПодготовитьАдресРесурса("GET", Параметры.Project_ID, Коллекция, documentId);
		
		ОтветHTTP = ПолучитьДокументы_GET(Параметры, СоединениеFire, АдресРесурса);
		Если ОтветHTTP.КодСостояния = 200 И ДопПризнак = Истина И Коллекция = "sotrudniki" Тогда
			ОтветДанные = Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
			ТипСотрудника = ТипСотрудника(Ссылка);
			ЕстьВБазе = ОтветДанные.fields.type.integerValue = Строка(ТипСотрудника);
		Иначе
			ЕстьВБазе = ОтветHTTP.КодСостояния = 200;
		КонецЕсли;
		Если Не ЕстьВБазе Тогда
			ТелоЗапроса = ПодготовитьТелоЗапроса(Параметры, СоединениеFire, Коллекция, Ссылка, ДопПризнак);
			Если ДопПризнак = Истина И Коллекция = "sotrudniki" Тогда
				АдресРесурса = ПодготовитьАдресРесурса("PATCH", Параметры.Project_ID, Коллекция, documentId);
				ЕстьВБазе = ИзменитьДокументы_PATCH(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса);
			Иначе
				АдресРесурса = ПодготовитьАдресРесурса("POST", Параметры.Project_ID, Коллекция, documentId);
				ОтветHTTP = СоздатьДокумент_POST(Параметры, СоединениеFire, АдресРесурса, ТелоЗапроса);
				ЕстьВБазе = ОтветHTTP.КодСостояния = 200;
			КонецЕсли;
		КонецЕсли;
		Если ЕстьВБазе Тогда
			СтруктураОтвета = Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
			Возврат Новый Структура("referenceValue", СтруктураОтвета.name);
		КонецЕсли;
	Иначе
		Resource = "projects/"+"%1"+"/databases/(default)/documents/";
		Возврат Новый Структура("referenceValue", СтрШаблон(Resource,Параметры.Project_ID) + Коллекция + "/" + "00000000-0000-0000-0000-000000000000");
	КонецЕсли;
КонецФункции

Функция ПодготовитьТелоЗапроса(Параметры, СоединениеFire, Коллекция, Ссылка, ДопПризнак = Неопределено) Экспорт
	
	Если Коллекция = "SecureToken" Тогда
		Поля = Новый Структура("token, returnSecureToken",Ссылка,Истина);
		
	ИначеЕсли Коллекция = "ПоследнийНомерЗаявки" Тогда
		мОтбора = Новый Массив;
		мОтбора.Добавить(Новый Структура("collectionId", "tasks"));
		
		мСортировка = Новый Массив;
		мСортировка.Добавить(Новый Структура("direction, field", "DESCENDING", Новый Структура("fieldPath", "number")));
		
		СтруктураОтбора = Новый Структура("fieldFilter",
		Новый Структура("field, op, value", Новый Структура("fieldPath", "marketRef"), "EQUAL", СсылкаНаДокумент(Параметры, СоединениеFire, "markets", Ссылка)));
		
		Поля = Новый Структура("structuredQuery", Новый Структура("from, limit, orderBy, where", мОтбора, 1, мСортировка, СтруктураОтбора));
		
	ИначеЕсли Коллекция = "ЗаявкиВРаботеНаДату" Тогда
		мОтбора = Новый Массив;
		мОтбора.Добавить(Новый Структура("collectionId", "tasks", Истина));
		
		мЗначений = Новый Массив; 
		мЗначений.Добавить(Новый Структура("integerValue",0));
		мЗначений.Добавить(Новый Структура("integerValue",3));
		
		мФильтров = Новый Массив;
		ФильтрПоСтвтусу = Новый Структура("fieldFilter",
		Новый Структура("field, op, value", Новый Структура("fieldPath", "status"), "IN", Новый Структура("arrayValue", Новый Структура("values",мЗначений))));
		
		ФильтрПоДате = Новый Структура("fieldFilter",
		Новый Структура("field, op, value", Новый Структура("fieldPath", "dateCreation"), "LESS_THAN_OR_EQUAL", Новый Структура("integerValue",Ссылка)));
		
		мФильтров.Добавить(ФильтрПоСтвтусу);
		мФильтров.Добавить(ФильтрПоДате);
		
		СтруктураОтбора = Новый Структура("compositeFilter", Новый Структура("filters, op", мФильтров, "AND"));
		
		Поля = Новый Структура("structuredQuery", Новый Структура("from, where", мОтбора, СтруктураОтбора));			
		
	ИначеЕсли Коллекция = "markets" Тогда
		Реквизиты = Новый Структура("Код, Global_ID, Наименование, ПодразделениеКомпании, Область, Куратор, Телефон, Геометка");
		ЗаполнитьЗначенияСвойств(Реквизиты, Ссылка);
		Реквизиты.Куратор = УправлениеСвойствами.ЗначениеСвойства(Ссылка, "КураторПлощадки");
		Телефон = УправлениеСвойствами.ЗначениеСвойства(Ссылка, "НомерТелефонаОбъекта");
		Реквизиты.Телефон = ?(ЗначениеЗаполнено(Телефон),Телефон,"");
		Реквизиты.Геометка = УправлениеСвойствами.ЗначениеСвойства(Ссылка, "Геометка");
		Реквизиты.Область = ОбластьТочки(Ссылка);
		
		Поля = Новый Структура("fields", Новый Структура("code, number, name, kurator, subdivisionRef, regionRef, telephone, location",
		Новый Структура("stringValue", Реквизиты.Код),
		Новый Структура("stringValue", Формат(Реквизиты.Global_ID,"ЧГ=0")),
		Новый Структура("stringValue", Реквизиты.Наименование),
		?(ЗначениеЗаполнено(Реквизиты.Куратор),СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Реквизиты.Куратор),Новый Структура("nullValue")),
		СсылкаНаДокумент(Параметры,СоединениеFire,"subdivision",Реквизиты.ПодразделениеКомпании),
		?(ЗначениеЗаполнено(Реквизиты.Область),СсылкаНаДокумент(Параметры,СоединениеFire,"regions",Реквизиты.Область),Новый Структура("nullValue")),
		Новый Структура("stringValue", Реквизиты.Телефон),
		?(ЗначениеЗаполнено(Реквизиты.Геометка), Новый Структура("geoPointValue", ГеоТочка(Реквизиты.Геометка)), Новый Структура("nullValue"))
		));
		
	ИначеЕсли Коллекция = "sotrudniki" Тогда
		ЭтоТерритория = ТипЗнч(Ссылка) = Тип("СправочникСсылка.ТерриторииИсполнителей");
		СотрудникСсылка = ?(ЭтоТерритория,Ссылка.Владелец,Ссылка);
		ЭтоВнешнийСотрудник = ТипЗнч(СотрудникСсылка) = Тип("СправочникСсылка.Посетители");
		ТелефонСотрудника = ?(ЭтоВнешнийСотрудник, СотрудникСсылка.Телефон, ТелефонСотрудника(СотрудникСсылка));
		ТипСотрудника = ТипСотрудника(СотрудникСсылка);
		Область = УправлениеСвойствами.ЗначениеСвойства(СотрудникСсылка, "Покрытие");
		
		Поля = Новый Структура("fields", Новый Структура("external, inn, markets, name, regionRef, telephone, type",
		Новый Структура("booleanValue", ЭтоВнешнийСотрудник),
		Новый Структура("stringValue", ?(ЭтоВнешнийСотрудник, "ext", "") + СотрудникСсылка.Код),
		//Новый Структура("arrayValue",	МассивСсылок(Параметры,СоединениеFire,"Точки", ?(ЭтоТерритория,Ссылка.Точки,ТочкиСотрудника1(СотрудникСсылка)))),
		Новый Структура("arrayValue",	Новый Структура()),
		Новый Структура("stringValue", СокрЛП(СотрудникСсылка)),
		?(ЗначениеЗаполнено(Область),СсылкаНаДокумент(Параметры,СоединениеFire,"regions",Область),Новый Структура("nullValue")),
		Новый Структура("stringValue", ТелефонСотрудника),
		Новый Структура("integerValue",ТипСотрудника)
		));
		
	ИначеЕсли Коллекция = "task_categories" Тогда
		ЭтоГруппа = Ссылка.ЭтоГруппа;
		Если ЭтоГруппа Тогда
			Реквизиты = Новый Структура("ЭтоГруппа, Родитель, Наименование");
		Иначе
			Реквизиты = Новый Структура("ЭтоГруппа, Родитель, Наименование, Плановая, Срочная, ДоступнаРеализатору, ЗакрыватьПоВыполнении, ВремяВыполнения, РольИсполнители");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Реквизиты, Ссылка);
		Если ЭтоГруппа Тогда
			Поля = Новый Структура("fields", Новый Структура("group, parentRef, name",
			Новый Структура("booleanValue", Реквизиты.ЭтоГруппа),
			?(ЗначениеЗаполнено(Реквизиты.Родитель),СсылкаНаДокумент(Параметры,СоединениеFire,"task_categories",Реквизиты.Родитель),Новый Структура("nullValue")),
			Новый Структура("stringValue", Реквизиты.Наименование)
			));
		Иначе
			Поля = Новый Структура("fields", Новый Структура("group, parentRef, parentName, name, planovaya, srochnaya, dostupna_realizatoru, zakrytiye_po_vypolnenii, vremya_vypolneniya, roli",
			Новый Структура("booleanValue", Реквизиты.ЭтоГруппа),
			?(ЗначениеЗаполнено(Реквизиты.Родитель),СсылкаНаДокумент(Параметры,СоединениеFire,"task_categories",Реквизиты.Родитель),Новый Структура("nullValue")),
			Новый Структура("stringValue",	Реквизиты.Родитель.Наименование),
			Новый Структура("stringValue",	Реквизиты.Наименование),
			Новый Структура("booleanValue", Реквизиты.Плановая),
			Новый Структура("booleanValue", Реквизиты.Срочная),
			Новый Структура("booleanValue", Реквизиты.ДоступнаРеализатору),
			Новый Структура("booleanValue", Реквизиты.ЗакрыватьПоВыполнении),
			Новый Структура("integerValue", Реквизиты.ВремяВыполнения),
			Новый Структура("arrayValue",	МассивСсылок(Параметры,СоединениеFire,"Роли", Реквизиты.РольИсполнители))
			));
		КонецЕсли;
		
	ИначеЕсли Коллекция = "tasks" Тогда
		Куратор = УправлениеСвойствами.ЗначениеСвойства(Ссылка.marketRef, "КураторПлощадки");
		Поля = Новый Структура("fields", Новый Структура("authorRef, categoryName, categoryParentName, categoryRef, commentCompletion, commentCreation, dateCreation, datePlaneCompletion, dateCompletion, kuratorRef, marketName, marketNumber, marketRef, regionRef, month, number, responsibleUserRef, responsibleUserName, roleRef, status, subdivisionRef, urgent, urlsCompletion, urlsCreation, vremya_vypolneniya",
		СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Ссылка.authorRef),
		Новый Структура("stringValue",		Ссылка.categoryName),
		Новый Структура("stringValue",		Ссылка.categoryParentName),
		СсылкаНаДокумент(Параметры,СоединениеFire,"task_categories",Ссылка.categoryRef),
		Новый Структура("stringValue",		Ссылка.commentCompletion),
		Новый Структура("stringValue",		Ссылка.commentCreation),
		Новый Структура("integerValue",		Ссылка.dateCreation),
		Новый Структура("integerValue",		Ссылка.datePlaneCompletion),
		Новый Структура("integerValue",		Ссылка.dateCompletion),
		?(ЗначениеЗаполнено(Куратор),СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Куратор),Новый Структура("nullValue")),
		Новый Структура("stringValue",		Ссылка.marketName),
		Новый Структура("stringValue",		Ссылка.marketNumber),
		СсылкаНаДокумент(Параметры,СоединениеFire,"markets",Ссылка.marketRef),
		?(ЗначениеЗаполнено(Ссылка.regionRef),СсылкаНаДокумент(Параметры,СоединениеFire,"regions",Ссылка.regionRef),Новый Структура("nullValue")),
		Новый Структура("stringValue",		Ссылка.month),
		Новый Структура("integerValue",		Ссылка.number),
		?(ЗначениеЗаполнено(Ссылка.responsibleUserRef),СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Ссылка.responsibleUserRef),Новый Структура("nullValue")),
		?(ЗначениеЗаполнено(Ссылка.responsibleUserName),Новый Структура("stringValue", Ссылка.responsibleUserName),Новый Структура("nullValue")),
		?(ЗначениеЗаполнено(Ссылка.roleRef),СсылкаНаДокумент(Параметры,СоединениеFire,"roli",Ссылка.roleRef),Новый Структура("nullValue")),
		Новый Структура("integerValue",		Ссылка.status),
		СсылкаНаДокумент(Параметры,СоединениеFire,"subdivision",Ссылка.subdivisionRef),
		Новый Структура("booleanValue",		Ссылка.urgent),
		Новый Структура("arrayValue",		Новый Структура("values",Ссылка.urlsCompletion)),
		Новый Структура("arrayValue",		МассивСсылок(Параметры,СоединениеFire,"СсылкиНаФото",Ссылка.urlsCreation)),
		Новый Структура("integerValue",		Ссылка.vremya_vypolneniya),
		));
		
	ИначеЕсли Коллекция = "working_time_calendar" Тогда
		Поля = Новый Структура("fields", Новый Структура("date, hours",
		Новый Структура("timestampValue",	Формат(УниверсальноеВремя(Ссылка.Дата),"ДФ=ггг-ММ-ддTЧЧ:мм:сс.Z")),
		Новый Структура("arrayValue",		МассивСсылок(Параметры,СоединениеFire,"Календарь",Ссылка))
		));
		
	ИначеЕсли Коллекция = "roli" Тогда
		ТипРоли = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"ТипРоли");
		Поля = Новый Структура("fields", Новый Структура("completion, creation, notification, name, managerRef, sotrudniki",
		Новый Структура("booleanValue",		ТипРоли = Перечисления.ТипыРолейСотрудников.Исполнители),
		Новый Структура("booleanValue",		ТипРоли = Перечисления.ТипыРолейСотрудников.Руководители),
		Новый Структура("booleanValue",		ТипРоли = Перечисления.ТипыРолейСотрудников.Спамеры),
		Новый Структура("stringValue",		Ссылка.Наименование),
		?(ЗначениеЗаполнено(Ссылка.Ответственный),СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Ссылка.Ответственный),Новый Структура("nullValue")),
		Новый Структура("arrayValue",		МассивСсылок(Параметры,СоединениеFire,"Сотрудники",Ссылка.Сотрудники))
		));
		
	ИначеЕсли Коллекция = "subdivision" Тогда
		Реквизиты = Новый Структура("Код, Наименование");
		ЗаполнитьЗначенияСвойств(Реквизиты, Ссылка);
		Поля = Новый Структура("fields", Новый Структура("code, name",
		Новый Структура("stringValue",		Реквизиты.Код),
		Новый Структура("stringValue",		Реквизиты.Наименование),
		));
		
	ИначеЕсли Коллекция = "territories" Тогда
		Реквизиты = Новый Структура("Код, Владелец, Точки");
		ЗаполнитьЗначенияСвойств(Реквизиты, Ссылка);
		Поля = Новый Структура("fields", Новый Структура("code, name, sotrudnikRef, markets",
		Новый Структура("stringValue",		Реквизиты.Код),
		Новый Структура("stringValue",		СокрЛП(Реквизиты.Владелец)),
		СсылкаНаДокумент(Параметры,СоединениеFire,"sotrudniki",Реквизиты.Владелец, Истина),
		Новый Структура("arrayValue",		МассивСсылок(Параметры,СоединениеFire,"Точки", ТочкиСотрудника(Реквизиты.Точки)))
		));
		
	ИначеЕсли Коллекция = "regions" Тогда
		Реквизиты = Новый Структура("Код, Наименование");
		ЗаполнитьЗначенияСвойств(Реквизиты, Ссылка);
		Поля = Новый Структура("fields", Новый Структура("code, name",
		Новый Структура("stringValue",		Реквизиты.Код),
		Новый Структура("stringValue",		Реквизиты.Наименование),
		));
		
	КонецЕсли;
	
	Возврат В_JSON(Поля);
	
КонецФункции

Функция ПодготовитьАдресРесурса(МетодHTTP, Project_ID, Коллекция, documentId = "", nextPageToken = "", runQuery = "") Экспорт
	
	Resource = "/v1/projects/"+"%1"+"/databases/(default)/documents/";
	АдресРесурса = СтрШаблон(Resource, Project_ID) + "%1%2%3%4%5";
	
	Если МетодHTTP = "GET" Тогда		
		АдресРесурса = СтрШаблон(АдресРесурса,
		Коллекция,
		?(ПустаяСтрока(documentId), "", "/" + documentId),
		runQuery,
		//"?key=" + ПараметрыДляЗапроса.Web_API_Key,
		"?pageSize=100",
		?(ПустаяСтрока(nextPageToken), "", "&pageToken=" + nextPageToken));
		
	ИначеЕсли МетодHTTP = "POST" Тогда 
		АдресРесурса = СтрШаблон(АдресРесурса,
		Коллекция,
		?(ПустаяСтрока(documentId), "", "/?documentId=" + documentId),
		runQuery,
		//"?key=AIzaSyAPTAInQrcO7SJbxR4jjxJ9B2r2agrjwaA",// + ПараметрыДляЗапроса.Web_API_Key,
		"",
		"");
		
	ИначеЕсли МетодHTTP = "PATCH" Тогда 
		АдресРесурса = СтрШаблон(АдресРесурса,
		Коллекция,
		?(ПустаяСтрока(documentId), "", "/" + documentId),
		runQuery,
		//"?key=" + ПараметрыДляЗапроса.Web_API_Key,
		"",
		"");
		
	ИначеЕсли МетодHTTP = "DELETE" Тогда 
		АдресРесурса = СтрШаблон(АдресРесурса,
		Коллекция,
		?(ПустаяСтрока(documentId), "", "/" + documentId),
		runQuery,
		//"?key=" + ПараметрыДляЗапроса.Web_API_Key,
		"",
		"");
		
	КонецЕсли;
	
	Возврат АдресРесурса;
	
КонецФункции

Процедура Записать_idToken(Параметры, expiresIn, idToken)
	МенеджерЗаписи = РегистрыСведений.ТокеныАутентификацииFirebase.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ProjectID = Параметры.Project_ID;
	МенеджерЗаписи.UserID = Параметры.UserUID;
	МенеджерЗаписи.Токен = idToken;
	МенеджерЗаписи.ДействителенДо = ТекущаяДатаСеанса() + Число(expiresIn);
	
	МенеджерЗаписи.Записать();
КонецПроцедуры


#КонецОбласти


#КонецЕсли
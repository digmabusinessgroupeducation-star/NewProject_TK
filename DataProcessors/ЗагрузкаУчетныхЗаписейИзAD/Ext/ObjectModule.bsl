Процедура ЗагрузитьПользователей(ТаблицаПользователей, ПараметрыПользователя, ЕстьОшибки) Экспорт
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ДоступенПоискПользователейПоИД", Метаданные.Справочники.Пользователи.Реквизиты.Найти("ИдентификаторПользователяИБ") <> Неопределено);
	ПараметрыЗагрузки.Вставить("ДоступнаТЧКонтактнаяИнформация", Метаданные.Справочники.Пользователи.ТабличныеЧасти.Найти("КонтактнаяИнформация") <> Неопределено);
	ПараметрыЗагрузки.Вставить("ДоступенРегистрНастроекПользователя", Метаданные.РегистрыСведений.Найти("НастройкиПользователей") <> Неопределено);
	ПараметрыЗагрузки.Вставить("ДоступенРегистрКонтактнаяИнформация", Метаданные.РегистрыСведений.Найти("КонтактнаяИнформация") <> Неопределено);
	
	Для Каждого Стр Из ТаблицаПользователей Цикл
		Если Стр.Пометка Тогда
			#Если Клиент Тогда
			ОбработкаПрерыванияПользователя();
			#КонецЕсли
			Стр.Загружено = СоздатьПользователя(Стр.Имя, Стр.Аккаунт, Стр.Домен, Стр.Email, ПараметрыПользователя, ПараметрыЗагрузки);
			Если Стр.Загружено Тогда
				Стр.Пометка = Ложь;
			Иначе
				ЕстьОшибки = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры //ЗагрузитьПользователей

Функция СоздатьПользователя(ИмяПользователя, АккаунтОСИмя, АккаунтОСДомен, АдресЭлектроннойПочты, ПараметрыПользователя, ПараметрыЗагрузки)
	
	АккаунтОС = "\\" + АккаунтОСДомен + "\" + АккаунтОСИмя;
	
	ДоступенПоискПользователейПоИД = ПараметрыЗагрузки.Свойство("ДоступенПоискПользователейПоИД") И ПараметрыЗагрузки.ДоступенПоискПользователейПоИД;
	ДоступнаТЧКонтактнаяИнформация = ПараметрыЗагрузки.Свойство("ДоступнаТЧКонтактнаяИнформация") И ПараметрыЗагрузки.ДоступнаТЧКонтактнаяИнформация;
	ДоступенРегистрКонтактнаяИнформация = ПараметрыЗагрузки.Свойство("ДоступенРегистрКонтактнаяИнформация") И ПараметрыЗагрузки.ДоступенРегистрКонтактнаяИнформация; 
	ДоступенРегистрНастроекПользователя = ПараметрыЗагрузки.Свойство("ДоступенРегистрНастроекПользователя") И ПараметрыЗагрузки.ДоступенРегистрНастроекПользователя; 

	Попытка
		
		///////////////////////////////////////////////////////// Пользователь БД
		ПользовательБД = НайтиПользователяБазыДанных(АккаунтОСИмя);
		Если ПользовательБД = Неопределено Тогда 
			ПользовательБД = ПользователиИнформационнойБазы.СоздатьПользователя(); 
		КонецЕсли; 
		
		ПользовательБД.Имя = АккаунтОСИмя;
		ПользовательБД.ПолноеИмя = ИмяПользователя;
		
		ПользовательБД.АутентификацияСтандартная = Ложь;
		ПользовательБД.ЗапрещеноИзменятьПароль = Истина;
		ПользовательБД.ПоказыватьВСпискеВыбора = Ложь;
		
		ПользовательБД.АутентификацияОС = Истина; 
		ПользовательБД.ПользовательОС = АккаунтОС; 
		
		Если ПараметрыПользователя.Свойство("Интерфейс") И ЗначениеЗаполнено(ПараметрыПользователя.Интерфейс) Тогда 
			ПользовательБД.ОсновнойИнтерфейс = Метаданные.Интерфейсы[ПараметрыПользователя.Интерфейс];
		КонецЕсли;
		
		Если ПараметрыПользователя.Свойство("Язык") И ЗначениеЗаполнено(ПараметрыПользователя.Язык) Тогда 
			ПользовательБД.Язык = Метаданные.Языки[ПараметрыПользователя.Язык];
		КонецЕсли;
		
		Если ПараметрыПользователя.Свойство("Роли") Тогда
			ПользовательБД.Роли.Очистить();
			Для Каждого Роль Из ПараметрыПользователя.Роли Цикл
				ПользовательБД.Роли.Добавить(Метаданные.Роли[Роль]);
			КонецЦикла;
		КонецЕсли;
		
		ПользовательБД.Записать();
		
		
		///////////////////////////////////////////////////////// Пользователь
		Если ДоступенПоискПользователейПоИД Тогда
			Пользователь = Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ПользовательБД.УникальныйИдентификатор);  
		Иначе
			Пользователь = Справочники.Пользователи.НайтиПоКоду(АккаунтОСИмя); 
		КонецЕсли;
		Если Пользователь = Неопределено Или Пользователь.Пустая() Тогда
			Пользователь = Справочники.Пользователи.СоздатьЭлемент();
			Пользователь.Код = АккаунтОСИмя;
		Иначе
			Пользователь = Пользователь.ПолучитьОбъект();
		КонецЕсли;
		
		Пользователь.Наименование = ИмяПользователя;
		Если ПараметрыПользователя.Свойство("Группа") Тогда
			Пользователь.Родитель = ПараметрыПользователя.Группа;
		КонецЕсли;
		Если ПараметрыПользователя.Свойство("Физлицо") Тогда
			Пользователь.ФизЛицо = ПараметрыПользователя.ФизЛицо;
		КонецЕсли;
		Если ДоступенПоискПользователейПоИД Тогда
			Пользователь.ИдентификаторПользователяИБ = ПользовательБД.УникальныйИдентификатор;
		КонецЕсли;
		Если ДоступнаТЧКонтактнаяИнформация Тогда
			Если Не ПустаяСтрока(АдресЭлектроннойПочты) Тогда
				СтрокиEmail = Пользователь.КонтактнаяИнформация.НайтиСтроки(Новый Структура("Вид", Справочники.ВидыКонтактнойИнформации.EmailПользователя));
				Если СтрокиEmail.Количество() > 0 Тогда
					СтрокаEmail = СтрокиEmail[0].АдресЭП = АдресЭлектроннойПочты;
				Иначе
					СтрокаEmail = Пользователь.КонтактнаяИнформация.Добавить();
					СтрокаEmail.Вид = Справочники.ВидыКонтактнойИнформации.EmailПользователя;
				КонецЕсли;
				СтрокаEmail.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				СтрокаEmail.Представление = АдресЭлектроннойПочты;
				СтрокаEmail.АдресЭП = АдресЭлектроннойПочты;
			КонецЕсли;
		КонецЕсли;
				
		Пользователь.Записать();
		
		
		///////////////////////////////////////////////////////// Связанные данные
		
		Если ПараметрыПользователя.Свойство("ГруппаПользователей") 
			И Не ПараметрыПользователя.ГруппаПользователей = Неопределено И Не ПараметрыПользователя.ГруппаПользователей.Пустая() Тогда
			Если ПараметрыПользователя.ГруппаПользователей.ПользователиГруппы.Найти(Пользователь.Ссылка) = Неопределено Тогда
				ГруппаПользователейОбъект = ПараметрыПользователя.ГруппаПользователей.ПолучитьОбъект();
				НоваяСтрока = ГруппаПользователейОбъект.ПользователиГруппы.Добавить();
				НоваяСтрока.Пользователь = Пользователь.Ссылка;
				ГруппаПользователейОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
		
		Если ДоступенРегистрНастроекПользователя Тогда
			
			//-->НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ-->
			Если ПараметрыПользователя.Свойство("ДеревоНастроек") Тогда
				ОбновитьНастройкиПользователя(ПараметрыПользователя.ДеревоНастроек, Пользователь.Ссылка);
			КонецЕсли;
			//<--НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ<--
			
			//пропишем основного ответственного
			МенеджерЗаписи = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Пользователь = Пользователь.Ссылка;
			МенеджерЗаписи.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный;
			МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Значение = Пользователь.Ссылка;
			Иначе
				МенеджерЗаписи.Пользователь = Пользователь.Ссылка;
				МенеджерЗаписи.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный;
				МенеджерЗаписи.Значение = Пользователь.Ссылка;
			КонецЕсли;
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
		//адрес электронной почты
		Если ДоступенРегистрКонтактнаяИнформация Тогда
			
			Если Не ПустаяСтрока(АдресЭлектроннойПочты) Тогда
				МенеджерЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Объект = Пользователь.Ссылка;
				МенеджерЗаписи.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
				МенеджерЗаписи.Вид = Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя;
				МенеджерЗаписи.Прочитать();
				Если МенеджерЗаписи.Выбран() Тогда
					МенеджерЗаписи.Представление = АдресЭлектроннойПочты;
				Иначе
					МенеджерЗаписи.Объект = Пользователь.Ссылка;
					МенеджерЗаписи.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
					МенеджерЗаписи.Вид = Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя;
					МенеджерЗаписи.Представление = АдресЭлектроннойПочты;
				КонецЕсли;
				МенеджерЗаписи.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
		Сообщить("Пользователь '" + ИмяПользователя + " (" + АккаунтОСИмя + ")' успешно загружен.", СтатусСообщения.Информация);
		Возврат Истина;
		
	Исключение 
		
		Сообщить("Ошибка при загрузке пользователя '" + ИмяПользователя + " (" + АккаунтОСИмя + ")': " + ОписаниеОшибки() + "!", СтатусСообщения.Важное); 
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции  //СоздатьПользователя

Функция НайтиПользователяБазыДанных(ИмяПользователя) Экспорт
	
	Если ИмяПользователя = "НеАвторизован" Тогда
		ПользовательИБ = Неопределено
	Иначе
		// ищем пользователя БД по имени
		Попытка
			ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя);
		Исключение
			ПользовательИБ = Неопределено;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат ПользовательИБ;
	
КонецФункции //НайтиПользователяБазыДанных


//-->НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ-->

// Процедура выполняет запись значений настроек в регистр сведений
Процедура ОбновитьНастройкиПользователя(ДеревоНастроек, Пользователь)

	Набор = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();

	Набор.Отбор.Пользователь.Использование = Истина;
	Набор.Отбор.Пользователь.Значение      = Пользователь;
	
	ЗаполнитьНаборЗаписей(Пользователь, ДеревоНастроек.Строки, Набор);
	
	Набор.Записать();
	
КонецПроцедуры //ОбновитьНастройкиПользователя

// Процедура заполняет набор записей регистра сведений значениями настроек
Процедура ЗаполнитьНаборЗаписей(Пользователь, СтрокиДерева, НаборЗаписей)

	Для Каждого СтрокаДерева Из СтрокиДерева Цикл

		Если Не СтрокаДерева.ЭтоГруппа Тогда

			Запись = НаборЗаписей.Добавить();

			Запись.Пользователь = Пользователь;
			Запись.Настройка    = СтрокаДерева.Настройка;
			Запись.Значение     = СтрокаДерева.Настройка.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);

			Если Запись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация Тогда
				ТекущаяОрганизация = Запись.Значение;
			КонецЕсли;

		Иначе
			ЗаполнитьНаборЗаписей(Пользователь, СтрокаДерева.Строки, НаборЗаписей)

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры //ЗаполнитьНаборЗаписей

//<--НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ<--


Процедура ЗаполнитьТаблицуПользователей(ТаблицаПользователей, LDAPText, Рекурсивно = Ложь, СтатусВыполнения = "") Экспорт             
	
	#Если Клиент Тогда
	Состояние("Заполнение таблицы пользователей AD...");
	#КонецЕсли

	ТаблицаПользователей.Очистить();
	
	СтатусВыполнения = "";

	Если ПустаяСтрока(LDAPText) Тогда
		Возврат;
	КонецЕсли;
	
	////////////////////////////////////
	Попытка 
		conn = Новый COMОбъект("ADODB.Connection");
		conn.Provider = "ADSDSOObject";
		conn.Open("ADs Provider");
		
		rs =  Новый COMОбъект("ADODB.recordset");
		//rs.CursorLocation = 0; //adUseClient
		//rs.PageSize = 1000;	
		//rs.StayInSync = Ложь;
		//rs.CacheSize = 0;
		//objRecordset.Open source,actconn,cursortyp,locktyp,opt
		rs.Open("<" + СокрЛП(LDAPText) + ">;(&(objectCategory=person)(objectClass=user));ADsPath, Name, DisplayName, Mail, sAMAccountName;" + ?(Рекурсивно, "subtree", "onelevel"), conn, 0, 1);  //source,actconn,cursortyp,locktyp,opt
		//примечание: если необходимо отобрать по непустым: (DisplayName=*)		
		
	Исключение
 		СтатусВыполнения = "Не удалось выполнить LDAP запрос!";
		conn = Неопределено;
		rs = Неопределено;
		Возврат;
	КонецПопытки;
	
	Сч = 0;
	Если rs.RecordCount > 0 Тогда
		Пока Не rs.EOF Цикл  
			
			//obj = ПолучитьCOMОбъект(rs.Fields("ADsPath").Value);
			
			АккаунтИмя = rs.Fields("sAMAccountName").Value;
			
			ADsPath = rs.Fields("ADsPath").Value;
			Поз = Найти(ADsPath, "DC=");
			Если Поз > 0 Тогда
				АккаунтДомен = Сред(ADsPath, Поз+3);
				Поз = Найти(АккаунтДомен, ",");
				Если Поз >  0 Тогда
					АккаунтДомен = Лев(АккаунтДомен, Поз-1);
				КонецЕсли;
			//Иначе
			//	АккаунтДомен = ДоменПоУмолчанию;
			КонецЕсли;
			
			Если Не ПустаяСтрока(АккаунтИмя) Тогда
				НоваяСтрока = ТаблицаПользователей.Добавить();
				Пользователь = Справочники.Пользователи.НайтиПоКоду(АккаунтИмя);
				НоваяСтрока.Загружено = Истина;
				Если Пользователь = Неопределено Или Пользователь = Справочники.Пользователи.ПустаяСсылка() Тогда
					НоваяСтрока.Загружено = Ложь;
				КонецЕсли;
				НоваяСтрока.Имя = rs.Fields("DisplayName").Value;
				НоваяСтрока.Аккаунт = АккаунтИмя;
				НоваяСтрока.Домен = АккаунтДомен;
				НоваяСтрока.Email =  rs.Fields("Mail").Value;
			КонецЕсли;
			
			Попытка
				rs.MoveNext();
				Сч = Сч + 1;
			Исключение
				//делаем заглушку на ограничение MaxPageSize в политике ADSI
				СтатусВыполнения = "Данные получены не полностью (" + Сч + " записей). Ограничьте размер выборки.";
				Прервать;
			КонецПопытки;
			
		КонецЦикла;
	КонецЕсли;
	
	rs.Close();
	rs = Неопределено;
	
	conn.Close();
	conn = Неопределено;
	
	ТаблицаПользователей.Сортировать("Имя Возр");
	
	#Если Клиент Тогда
	Состояние("");
	#КонецЕсли
	
КонецПроцедуры  //ЗаполнитьТаблицуПользователей

Процедура СоздатьДеревоAD(ДеревоAD) Экспорт
	
	#Если Клиент Тогда
	Состояние("Формирование дерева Active Directory...");
	#КонецЕсли
	
	ДеревоAD.Строки.Очистить();
	
	DSE = ПолучитьCOMОбъект("LDAP://rootDSE"); 
	LDAP_DNC  = DSE.Get("defaultNamingContext");
	LDAPText = "GC://" + LDAP_DNC;

	conn = Новый COMОбъект("ADODB.Connection");;
	conn.Provider = "ADSDSOObject";
	conn.Open("ADs Provider");
	
	////////////  - можно было превратить в дерево одним запросом полученную плоскую таблицу, но как выяснилось могут быть проблемы с получением рекордсета с большим количеством записей (больше 1000)
	// и было решено оставить вариант с рекурсивными подзапросами через ADO (см. ниже)
	//
	//КопияДерева = ДеревоAD.Скопировать();
	//КопияДерева.Колонки.Добавить("ПутьДляСортировки");
	//КопияДерева.Колонки.Добавить("Уровень");
	//
	//query = "<" + LDAPText + ">;(|(objectClass=domain)(objectClass=organizationalUnit));ADsPath, Name, distinguishedName;subtree";

	//rs = conn.Execute(query);
	//Если rs.RecordCount > 0 Тогда 
	//	Пока Не rs.EOF Цикл  
	//		
	//		НоваяСтрока = КопияДерева.Строки.Добавить();
	//		
	//		НоваяСтрока.Имя			 = rs.Fields("Name").Value;
	//		НоваяСтрока.LDAP		 = rs.Fields("distinguishedName").Value; 
	//		НоваяСтрока.Path		 = rs.Fields("ADsPath").Value;  //берем путь "как есть", в т.ч. GC
	//		
	//		ЗаполнитьСтрокуДляСортировки(НоваяСтрока);
	//		
	//		rs.MoveNext();
	//		
	//	КонецЦикла;
	//КонецЕсли;
	//
	//КопияДерева.Строки.Сортировать(ПутьДляСортировки);
	//
	//Возврат;

	////////////////////

	//запрос по доменам из глобального каталога
	//rs = conn.Execute(query);
	rs =  Новый COMОбъект("ADODB.recordset");
	//objRecordset.Open source,actconn,cursortyp,locktyp,opt
	rs.Open("<" + LDAPText + ">;(objectClass=domain);ADsPath, Name, distinguishedName;subtree", conn, 0, 1);  //source,actconn,cursortyp,locktyp,opt

	Если rs.RecordCount > 0 Тогда 
		Пока Не rs.EOF Цикл  
			
			//obj = ПолучитьCOMОбъект(rs.Fields("ADsPath").Value);
			
			НоваяСтрока = ДеревоAD.Строки.Добавить();
			
			//НоваяСтрока.ParentPath	 = obj.Parent;
			НоваяСтрока.Name			 = rs.Fields("Name").Value;
			//НоваяСтрока.LDAP		 = rs.Fields("distinguishedName").Value; 
			НоваяСтрока.Path		 = rs.Fields("ADsPath").Value;  //берем путь "как есть", в т.ч. GC
			
			ДобавитьВетвьВДеревоAD(conn, НоваяСтрока);
			
			Попытка
				rs.MoveNext();
			Исключение
				Сообщить("Превышен допустимый размер получаемых данных.");
				Прервать;
			КонецПопытки;
			
		КонецЦикла;
	КонецЕсли;
	
	rs.Close();
	rs = Неопределено;

	conn.Close();
	conn = Неопределено;
		
	#Если Клиент Тогда
	Состояние();
	#КонецЕсли
	
КонецПроцедуры  //СоздатьДеревоAD

Процедура ДобавитьВетвьВДеревоAD(conn, СтрокаДерева)
	
	#Если Клиент Тогда
	ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	//rs = conn.Execute(query);
	rs =  Новый COMОбъект("ADODB.recordset");
	//objRecordset.Open source,actconn,cursortyp,locktyp,opt
	rs.Open("<" + СтрокаДерева.Path + ">;(objectClass=organizationalUnit);ADsPath, Name, distinguishedName;onelevel", conn, 0, 1);  //source,actconn,cursortyp,locktyp,opt
	
	Если rs.RecordCount > 0 Тогда 
		
		Пока Не rs.EOF Цикл
			
			НоваяСтрока = СтрокаДерева.Строки.Добавить();
			
			НоваяСтрока.Path		 = rs.Fields("ADsPath").Value;
			НоваяСтрока.Name		 = rs.Fields("Name").Value;
			//НоваяСтрока.LDAP		 = rs.Fields("distinguishedName").Value; 
			
			ДобавитьВетвьВДеревоAD(conn, НоваяСтрока);
			
			Попытка
				rs.MoveNext();
			Исключение
				Сообщить("Превышен допустимый размер получаемых данных.");
				Прервать;
			КонецПопытки;
			
		КонецЦикла;
		
		СтрокаДерева.Строки.Сортировать("Name");
		
	КонецЕсли;
	
	rs.Close();
	rs = Неопределено;

КонецПроцедуры //ДобавитьВетвьВДеревоAD

Функция ПолучитьДоменПоУмолчанию() Экспорт
	
	//определяем текущий домен
	Попытка 
		//КорневойДомен = ПолучитьCOMОбъект("GC:"); 
		adsi = ПолучитьCOMОбъект("LDAP://RootDSE");
		//ДоменПоУмолчанию = adsi.Get("defaultNamingContext");
		Возврат adsi.Get("rootDomainNamingContext");
	Исключение
		Сообщить("Не удалось определить текущий домен!");
		Возврат "";
	КонецПопытки;
	
КонецФункции //ПолучитьДоменПоУмолчанию

//-->не используются

//Процедура ЗаполнитьСтрокуДляСортировки(СтрокаДерева, Разделитель = ",") Экспорт
//	
//	LDAP = СтрокаДерева.LDAP;
//	СтрокаДерева.Уровень = 0;
//	СтрокаДерева.ПутьДляСортировки = "";
//	
//	ДлинаРазделителя = СтрДлина(Разделитель);
//	Пока 1=1 Цикл
//		
//		Поз = Найти(LDAP,Разделитель);
//		Если Поз=0 Тогда
//			СтрокаДерева.ПутьДляСортировки = LDAP + "," + СтрокаДерева.ПутьДляСортировки;
//			Возврат;
//		КонецЕсли;
//		СтрокаДерева.ПутьДляСортировки = Лев(LDAP,Поз-1) + "," + СтрокаДерева.ПутьДляСортировки;
//		СтрокаДерева.Уровень = СтрокаДерева.Уровень + 1;
//		
//		LDAP = Сред(LDAP,Поз+ДлинаРазделителя);
//		
//	КонецЦикла;
//	
//КонецПроцедуры //ЗаполнитьСтрокуДляСортировки 

//<--не используются

////////////////////////////////////////////////////////////////////////////////////////////////////

GC_HDR = "GC://";
LDAP_HDR = "LDAP://"; 
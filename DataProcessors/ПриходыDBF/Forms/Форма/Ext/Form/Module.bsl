
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НаДату = ТекущаяДата();
	ОКПОИзФайла = Истина;
	ЕстьНДС = Истина;
	НесколькоФайлов = Истина;
	
	//СписокОКПО
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.Договор.Видимость = ФлагФиксированныйДоговор;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПутьКДаннымНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	Фильтр = ""; 
//	Заголовок = "ru = 'Выберите каталог с данными'; uk = 'Оберіть каталог з даними'"; 

	Диалог = Новый ДиалогВыбораФайла(Режим); 
	Диалог.ПолноеИмяФайла = Объект.ПутьКДанным;
	
	Если Диалог.Выбрать() Тогда 
		Объект.ПутьКДанным = Диалог.Каталог;  
	КонецЕсли; 

	//Объект.ПутьКДанным = "Z:\1. Projects\!ТК_SSL_УФ\Обработки\Аттика\Файлы DBF\Тест";
	//ОбменДаннымиКлиент.ОбработчикОткрытияФайлаИлиКаталога(Объект, "ПутьКДанным");
	
КонецПроцедуры

&НаКлиенте
Процедура НескольконФайлвПриИзменении(Элемент)
	Контрагент = ПолучитьКонтрагентаПоКоду("39496546");
КонецПроцедуры

&НаКлиенте
Процедура ФлагТранзитПриИзменении(Элемент)
	Договор = Неопределено;
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Контрагенты.Код КАК Код
	                      |ИЗ
	                      |	Справочник.Контрагенты КАК Контрагенты
	                      |ГДЕ
	                      |	Контрагенты.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И (СокрЛП(Выборка.Код) = "33872438" ИЛИ СокрЛП(Выборка.Код) = "42719755") Тогда 
		ЕстьНДС = Ложь;
	Иначе
		ЕстьНДС = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ФлагФиксированныйДоговорПриИзменении(Элемент)
	Элементы.Договор.Видимость = ФлагФиксированныйДоговор;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выполниь(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Проверка", Ложь);
	
	ВыбратьИОбработатьФайлыDBF(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура Проверка(Команда)
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Проверка", Истина);
	
	ВыбратьИОбработатьФайлыDBF(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиНаДату(Команда)
	ПеренестиНаДатуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПеренестиНаДатуНаСервере()
	
	тДата = ДатаДок;
	Сек = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПоступлениеТоваров.Проведен
	|	И ПоступлениеТоваров.Контрагент = &Контрагент";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(НаДату));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НаДату));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаДок = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДок.Следующий() Цикл
		Если ВремяСоздания <> Дата(1, 1, 1) Тогда
			НоваяДата=Дата(Год(тДата),Месяц(тДата),День(тДата),Час(ВремяСоздания),Минута(ВремяСоздания),Секунда(ВремяСоздания)) + Сек; Сек = Сек + 1;
		Иначе
			НоваяДата=ТекущаяДата();
		КонецЕсли; 		
		
		ДокОбъект = ВыборкаДок.Ссылка.ПолучитьОбъект();
		ДокОбъект.Дата = НоваяДата;
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Запись,РежимПроведенияДокумента.Неоперативный);
			
			ТекстСообщения = НСтр("ru = 'Документ проведен'; uk = 'Документ проведений'");			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + ДокОбъект);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИОбработатьФайлыDBF(ДополнительныеПараметры)	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	диалог.Заголовок = НСтр("ru = 'Выберите файлы DBF'; uk = 'Оберіть файли DBF'");
	Диалог.МножественныйВыбор = Истина;
	Диалог.Фильтр = НСтр("ru = 'Файл'; uk = 'Файл'") + "(*.dbf)|*dbf";
	Диалог.Расширение = "dbf";
		
	ОповещениеЗавершения = Новый ОписаниеОповещения("ЗавершениеПомещенияФайлов", ЭтаФорма, ДополнительныеПараметры);
	
	НачатьПомещениеФайлов(ОповещениеЗавершения, Диалог, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПомещенияФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПомещенныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	МассивDBF = Новый Массив;
	Для Каждого нФайл Из ПомещенныеФайлы Цикл 
		ТекРасширение = ВРег(Прав(нФайл.Имя, 3));
		Если ТекРасширение = "DBF" Тогда 
			МассивDBF.Добавить(нФайл);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивDBF.Количество() > 0 Тогда 
		СформироватьНаСервере(МассивDBF, ДополнительныеПараметры.Свойство("Проверка") И ДополнительныеПараметры.Проверка);
	Иначе 
		ТекстСообщения = НСтр("ru = 'Нет выбранных файлов формата DBF'; uk = 'Немає обраних файлів формату DBF'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура СформироватьНаСервере(МассивДБФ, Проверка)
	ТаблДБФ = ПолучитьМассивСтрокИзФайлов(МассивДБФ);
	СоздатьДокументы(ТаблДБФ, Проверка);
КонецПроцедуры


&НаСервере
Функция ПолучитьМассивСтрокИзФайлов(МассивДБФ)
	СоответствиеДанных = Новый Соответствие;
	ГенСлЧ = Новый ГенераторСлучайныхЧисел;
	
	Для Каждого ОписаниеФайла Из МассивДБФ Цикл		
		врФайл = ПолучитьИзВременногоХранилища(ОписаниеФайла.Хранение);
		имяВрФайла = Прав("00000000" + Строка(ГенСлЧ.СлучайноеЧисло(1, 99999)), 7);
		ИмяФайла = КаталогВременныхФайлов() + "a" + имяВрФайла + ".dbf";		
		врФайл.Записать(ИмяФайла);		
		ДБФ = Новый XBase(ИмяФайла);

		текФайл = ОписаниеФайла.Имя;
		
		СписокПолей = "";
		Если ПустаяСтрока(СписокПолей) И ДБФ.Поля.Количество() > 0 Тогда 
			Для Каждого Поле Из ДБФ.Поля Цикл 
				СписокПолей = СписокПолей + ?(Не ПустаяСтрока(СписокПолей), ", ", "") + Поле.Имя;
			КонецЦикла;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ДБФ.Первая() Тогда 
			Пока Истина Цикл 
				СтруктураСтроки = Новый Структура(СписокПолей);
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ДБФ);
				СтруктураСтроки.Вставить("FILE", текФайл);
				
				НомерДокумента = "";
				Если Не СтруктураСтроки.Свойство("invoice", НомерДокумента) Тогда 
					ОбщегоНазначения.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В файле <%1> не найдено поле ""INVOICE"".
                                  |Файл <%1> не загружен.'; uk = 'У файлі <%1> не знайдене поле ""INVOICE"".
                                  |Файл <%1> не завантажений.'"),
							текФайл));
					Прервать;
				КонецЕсли;
				
				НомерДокумента = СокрЛП(НомерДокумента);				
				МассивСтрок = СоответствиеДанных.Получить(НомерДокумента);
				Если МассивСтрок = Неопределено Тогда 
					МассивСтрок = Новый Массив;
				КонецЕсли;				
				МассивСтрок.Добавить(СтруктураСтроки);
				СоответствиеДанных.Вставить(НомерДокумента, МассивСтрок);
				
				Если Не ДБФ.Следующая() Тогда 
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ДБФ.ЗакрытьФайл();
		
		Попытка
			УдалитьФайлы(ИмяФайла);
		Исключение
		КонецПопытки;
		
		УдалитьИзВременногоХранилища(ОписаниеФайла.Хранение);
	КонецЦикла;
	
	Возврат СоответствиеДанных;
КонецФункции


&НаСервере
Функция НайтиОрганизацию(ОКПО)
	
	Если ПустаяСтрока(ОКПО) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.КодПоЕДРПОУ = &КодПоЕДРПОУ";
	
	Запрос.УстановитьПараметр("КодПоЕДРПОУ", ОКПО);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий()  ;
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
КонецФункции


&НаСервере
Процедура СоздатьДокументы(тзДБФ, Проверка)
	Бутылка = НайтиПоКодуПоставщика("99999992");
	
	СписокОКПО = Новый СписокЗначений;
	СписокОКПО.Добавить("38397793");
	СписокОКПО.Добавить("34630835");
	СписокОКПО.Добавить("36224758");
	СписокОКПО.Добавить("40735487");
	СписокОКПО.Добавить("36224758");
	СписокОКПО.Добавить("43242503");
	СписокОКПО.Добавить("42919413");
	СписокОКПО.Добавить("42024212");
	СписокОКПО.Добавить("43648523");  //ОСА ТОВ
	СписокОКПО.Добавить("43646133");  //ТОРІНО ЛТД+ ТОВ 
	
	//сортКонтрагент = Организация.Контрагент;
	
	Сек = 0;			
	Для Каждого загрДБФ Из тзДБФ Цикл 
		Выборка = загрДБФ.Значение;
		Если Выборка.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		
		
		//фОрганизация  = НайтиОрганизацию(Выборка[0].OKPO);
		Если ФлагОКПОИзФайла Тогда
			ОКОП = СокрЛП(Выборка[0].OKPO);
		Иначе
			ОКОП = СписокОКПО;
		КонецЕсли;
		
		СтруктураСклад  = НайтиСклад(СокрЛП(Выборка[0].KIOSK),ОКОП);
		Склад = СтруктураСклад.Склад;
		КонтрагентТочки = СтруктураСклад.Контрагент;
		НомерДокумента = СокрЛП(Выборка[0].INVOICE);
		Если Склад = Справочники.СкладыКомпании.ПустаяСсылка() Тогда
			ТекстОшибки = НСтр("ru = 'Не найден склад <%Склад%>, документ не загружен'; uk = 'Не знайдений склад  <%Склад%>, документ не завантажений'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Склад%", СокрЛП(Выборка[0].KIOSK));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецЕсли;		
		
		тДата = НаДату;
		Если ВремяСоздания <> Дата(1, 1, 1) Тогда
			НоваяДата=Дата(Год(тДата),Месяц(тДата),День(тДата),Час(ВремяСоздания),Минута(ВремяСоздания),Секунда(ВремяСоздания)) + Сек; Сек = Сек + 1;
		Иначе
			НоваяДата=ТекущаяДата();
		КонецЕсли; 	
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоступлениеТоваров.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.СкладКомпании = &Склад
		|	И ПоступлениеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваров.ВхДокНомер = &ВхДокНомер
		|	И ПоступлениеТоваров.ПометкаУдаления = ЛОЖЬ";
		
		Запрос.УстановитьПараметр("КонецПериода", КонецДня(НаДату));
		Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НаДату));
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("ВхДокНомер", НомерДокумента);
		
		ВыборкаДок = Запрос.Выполнить().Выбрать();
		Если ВыборкаДок.Следующий() тогда
			ДокОбъект = ВыборкаДок.Ссылка.ПолучитьОбъект();
			Попытка
				Если ДокОбъект.ПометкаУдаления тогда
					ДокОбъект.УстановитьПометкуУдаления(Ложь);
				КонецЕсли;
				Если ДокОбъект.Проведен Тогда
					ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;			
			
			ДокОбъект.Товары.Очистить();			
		Иначе
			ДокОбъект = Документы.ПоступлениеТоваров.СоздатьДокумент();
		КонецЕсли;
		
		текТорговаяПлощадка = Склад.ТорговаяПлощадка;
		
		ТекОрганизация = Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(КонтрагентТочки);
		//текОрганизация = Справочники.СкладыКомпании.ОрганизацияСклада(Склад, НоваяДата);
		
		
		// Это
		//Если фОрганизация <>  текОрганизация Тогда
		//	 ТекстСообщения = "Организация файл" + Строка(фОрганизация) + " организация тп "+Строка(текОрганизация);
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + текТорговаяПлощадка);

		//КонецЕсли;
		
		
		Если Не ЗначениеЗаполнено(текОрганизация) Тогда 
			ТекстСообщения = НСтр("ru = 'Не установлена организация для торговой площадки'; uk = 'Не встановлена організація для торгового майданчика'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + текТорговаяПлощадка);
			Продолжить;
		КонецЕсли;
		
		
		ДокОбъект.Дата = НоваяДата;
		ДокОбъект.ПодразделениеКомпании = Склад.Подразделение;
		ДокОбъект.Организация = текОрганизация;
		ДокОбъект.УстановитьНовыйНомер();
		ДокОбъект.ТорговаяПлощадка = текТорговаяПлощадка;
		ДокОбъект.СкладКомпании = Склад;		
		ДокОбъект.РегламентированныйУчет = ФлагРегламентированныйУчет;
		ДокОбъект.Контрагент = Контрагент;
				
		Если Не ФлагФиксированныйДоговор Тогда 
			МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
			
			СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(ДокОбъект.Ссылка, ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваров"));
			Договор = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, ТекОрганизация, ДокОбъект.ПодразделениеКомпании, СписокВидовДоговоров, , НаДату);
		КонецЕсли;		
		
		ДокОбъект.ДоговорВзаиморасчетов = Договор;
		
		ЭтоТранзит = Ложь;	
		Если текОрганизация <> Договор.Организация И ЗначениеЗаполнено(Договор) Тогда 
			ЭтоТранзит = Истина;
			ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваровТранзиты");			
		Иначе
			ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваров");
		КонецЕсли;
		
		ДокОбъект.ХозОперация = ХозОперация;
		ДокОбъект.ВалютаДокумента = Договор.ВалютаВзаиморасчетов;
		ДокОбъект.КурсДокумента = 1;
		ДокОбъект.ТипЦен = Склад.ТипЦенСебестоимости;
		
		ДокОбъект.Автор = Пользователи.ТекущийПользователь();
		
		ДокОбъект.ВалютаДокумента = ДокОбъект.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
		ДокОбъект.КурсДокумента = 1;		
		ДокОбъект.ВхДокНомер = НомерДокумента; 
		ТзТовары = ДокОбъект.Товары.Выгрузить();
		ТзТовары.Очистить();
		ТзТовары.Колонки.Добавить("флаг");
		
		Для Каждого стрВыборки из Выборка Цикл			
			Попытка
				Если стрВыборки.OBMEN Тогда
					Продолжить;
				КонецЕсли;
			Исключение
			КонецПопытки;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу");
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));			
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
			
			Если СокрЛП(стрВыборки.KOD)="" И СокрЛП(стрВыборки.kodsiu)="" Тогда
				ТекстСообщения = НСтр("ru = 'Не указан товар'; uk = 'Не вказаний товар'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + СокрЛП(стрВыборки.kodsiu)+" "+ стрВыборки.name);
				
				строка = ТзТовары.Добавить();
				строка.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
				строка.Количество = стрВыборки.KVO;
				строка.КоличествоПоставщика = стрВыборки.KVO;

				строка.Цена = ?(ЕстьНДС,стрВыборки.CENABNDS,стрВыборки.CENABNDS*1.20);			
				строка.Флаг = стрВыборки.flag;
				
			Иначе 
				нТовар = ?(СокрЛП(стрВыборки.KOD)="",НайтиПоКодуПоставщика(СокрЛП(стрВыборки.kodsiu)), НайтиПоШтрихкоду(СокрЛП(стрВыборки.KOD)));
				Если нТовар = Справочники.Номенклатура.ПустаяСсылка() Тогда 
					нТовар = НайтиПоКодуПоставщика(СокрЛП(стрВыборки.kodsiu));
				КонецЕсли;
				
				Если нТовар = Справочники.Номенклатура.ПустаяСсылка() Тогда
					ТекстСообщения = НСтр("ru = 'Товар не найден'; uk = 'Товар не знайдений'");					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + СокрЛП(стрВыборки.kodsiu) + " " + стрВыборки.name);
					
					строка = ТзТовары.Добавить();
					строка.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
					строка.Количество = стрВыборки.KVO;
					строка.КоличествоПоставщика = стрВыборки.KVO;
					
					строка.Цена = ?(ЕстьНДС,стрВыборки.CENABNDS,стрВыборки.CENABNDS*1.20);//стрВыборки.CENABNDS;
					строка.Флаг = стрВыборки.flag;
				Иначе
					строка = ТзТовары.Добавить();
					строка.Номенклатура = нТовар;
					строка.Количество = стрВыборки.KVO;
					строка.КоличествоПоставщика = стрВыборки.KVO;				
					строка.Цена = ?(ЕстьНДС,стрВыборки.CENABNDS,стрВыборки.CENABNDS*1.20);//стрВыборки.CENABNDS;
					строка.Флаг = стрВыборки.flag;
				КонецЕсли;
			КонецЕсли;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(строка, СтруктураДействий, Неопределено);
		КонецЦикла;
		
		стрБутылка =  ТзТовары.Найти(Бутылка,"Номенклатура");
		Если стрБутылка<> Неопределено Тогда
			ЦенаБут = стрБутылка.Цена;
			мСтроки = ТзТовары.НайтиСтроки(Новый Структура("флаг",1));
			Для Каждого стрмСтоки из мСтроки Цикл 
				стрмСтоки.Цена = стрмСтоки.Цена+ЦенаБут;
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(стрмСтоки, СтруктураДействий, Неопределено);				
			КонецЦикла;
			ТзТовары.Удалить(стрБутылка);
			
		КонецЕсли;
		
		ДокОбъект.Товары.Загрузить(ТзТовары);
		Рез = ДокОбъект.ПроверитьЗаполнение();
		Если Не Рез Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ не прошел проверку " + ДокОбъект);

			Продолжить;
		КонецЕсли;
		
		Если НЕ Проверка Тогда
			РежимЗап = ?(НЕ ФлагПроводить,РежимЗаписиДокумента.Запись,РежимЗаписиДокумента.Проведение);
			
			Попытка
				ДокОбъект.Записать(РежимЗап,РежимПроведенияДокумента.Неоперативный);
				Если ФлагПроводить Тогда
					ТекстСообщения = НСтр("ru = 'Документ проведен'; uk = 'Документ проведений'");
				Иначе
					ТекстСообщения = НСтр("ru = 'Документ записан'; uk = 'Документ записан'");
					
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + " " + ДокОбъект);
			Исключение
				Если ДокОбъект.Проведен Тогда
					ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				Иначе
					ДокОбъект.Записать();
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Готово!");
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтрагентаПоКоду(Код = "")
	Возврат Справочники.Контрагенты.НайтиПоКоду(Код);
КонецФункции

&НаСервере
Функция НайтиПоКодуПоставщика(Код)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НоменклатураПоставщиков.Номенклатура КАК Номенклатура
	               |ИЗ
	               |	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	               |ГДЕ
	               |	НоменклатураПоставщиков.Владелец = &Владелец
	               |	И НоменклатураПоставщиков.Артикул = &Артикул";
	
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	Запрос.УстановитьПараметр("Артикул", Код);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Номенклатура;
	Иначе
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция НайтиСклад(НомерСклада,Окпо)
	
	НовыйКиоск = Формат(НомерСклада,"ЧГ=");
	НовыйКиоск = СтрЗаменить(НовыйКиоск, " ", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкладыКомпании.Ссылка КАК Ссылка,
	|	СкладыКомпании.ТочкаДоставки.Владелец КАК ТочкаДоставкиВладелец
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	(СкладыКомпании.Код = &Код
	|			ИЛИ СкладыКомпании.ТочкаДоставки.Код = &Код
	|				И СкладыКомпании.ТочкаДоставки.Владелец.КодПоЕДРПОУ В (&КодПоЕДРПОУ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкладыКомпании.ТочкаДоставки.Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Код", Строка(Прав("000000000"+НовыйКиоск,8)));	
	Запрос.УстановитьПараметр("КодПоЕДРПОУ",Окпо);	
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Новый Структура("Склад,Контрагент",ВыборкаДетальныеЗаписи.Ссылка,ВыборкаДетальныеЗаписи.ТочкаДоставкиВладелец);
	Иначе
		Возврат Новый Структура("Склад,Контрагент",Справочники.СкладыКомпании.ПустаяСсылка(),Справочники.Контрагенты.ПустаяСсылка());
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиПоШтрихкоду(Штрих)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод"; 
	
	Запрос.УстановитьПараметр("Штрихкод", Штрих);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Номенклатура;
	Иначе
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти
 
 
 
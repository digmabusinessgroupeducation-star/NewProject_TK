#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

Процедура КорневоеЗаданиеВыгрузкиВБухгалтерию(ТаблицаДокументов, ПараметрыЗадания) Экспорт 
		
	АдресРезультата 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗадания, "АдресРезультата", Неопределено);
	КоличествоВыборки 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗадания, "КоличествоВыборки", 500);
	КоличествоПотоков	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗадания, "КоличествоПотоков", 10);
	ПредставленияДокументов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗадания, "ПредставленияТиповДокументов", Новый Структура);
	
	//Результат = Новый Структура;
	//Результат.Вставить("ВходящиеДанные", Новый Структура("Отмена", Ложь));
	//Результат.Вставить("ИсходящиеДанные", Новый Структура);
	
	ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
	
	ЗаданияОбмена = Новый ТаблицаЗначений;
	ЗаданияОбмена.Колонки.Добавить("НомерОперации");
	ЗаданияОбмена.Колонки.Добавить("Операция");
	ЗаданияОбмена.Колонки.Добавить("Статус");
	ЗаданияОбмена.Колонки.Добавить("КоличествоВыборки");
	ЗаданияОбмена.Колонки.Добавить("Запущено", ОписаниеТипаБулево);
	ЗаданияОбмена.Колонки.Добавить("Завершено", ОписаниеТипаБулево);
	ЗаданияОбмена.Колонки.Добавить("Ошибки");

	СтатистикаВыгрузки = Новый ТаблицаЗначений;
	СтатистикаВыгрузки.Колонки.Добавить("ТипДокумента");
	СтатистикаВыгрузки.Колонки.Добавить("Представление");
	СтатистикаВыгрузки.Колонки.Добавить("Количество");
	СтатистикаВыгрузки.Колонки.Добавить("Выгружено");
	СтатистикаВыгрузки.Колонки.Добавить("ПроцентВыгрузки");
	
	РаспределитьДокументыПоОперациям(ТаблицаДокументов, ЗаданияОбмена, КоличествоВыборки);
	//ЗаполнитьТаблицуСтатистики(СтатистикаВыгрузки, ПредставленияДокументов);
	
	Отмена = Ложь;
	ОжидатьРезультат = Истина;
	
	Пока ОжидатьРезультат Цикл 
		
		ОжидающиеОперации = ЗаданияОбмена.НайтиСтроки(Новый Структура("Запущено, Завершено,", Ложь, Ложь));	
		КоличествоОжидающихОпераций = ОжидающиеОперации.Количество();
		
		АктивныеОперации = ОбновитьСостояниеАктивныхОпераций(ЗаданияОбмена);		
		КоличествоАктивныхОпераций = АктивныеОперации.Количество();
		
		Если Не Отмена Тогда 
			Попытка
				РезОтмена = ПолучитьИзВременногоХранилища(АдресРезультата);
				Если ТипЗнч(РезОтмена) = Тип("Структура") Тогда 
					Отмена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезОтмена, "Отмена", Ложь);
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;		
		
		ОжидатьРезультат = (КоличествоОжидающихОпераций > 0 ИЛИ КоличествоАктивныхОпераций > 0) И Не Отмена;
		
		Если Отмена Тогда 
			Если КоличествоАктивныхОпераций > 0 Тогда 
				Для Каждого Стр Из АктивныеОперации Цикл 
					ДлительныеОперации.ОтменитьВыполнениеЗадания(Стр.Операция.ИдентификаторЗадания);
					Стр.Статус = "Отменено";
				КонецЦикла;
			КонецЕсли;
			
			Если КоличествоОжидающихОпераций > 0 Тогда 
				Для Каждого Стр Из ОжидающиеОперации Цикл 
					Стр.Статус = "Отменено";
				КонецЦикла;
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
		
		Если КоличествоОжидающихОпераций > 0 И КоличествоАктивныхОпераций < КоличествоПотоков Тогда 
			
			Для Каждого Стр Из ОжидающиеОперации Цикл 											
				Если КоличествоАктивныхОпераций >= КоличествоПотоков Тогда 
					Прервать;
				КонецЕсли;
				
				Стр.Запущено  = Истина;
				ТаблицаСсылок = ТаблицаДокументов.Скопировать(Новый Структура("НомерОперации", Стр.НомерОперации));												
				
				ПараметрыВыгрузки = ПолучитьПараметрыДляВыгрузки(ТаблицаСсылок);			
				Если ПараметрыВыгрузки.Количество() = 0 Тогда 			
					Стр.Завершено = Истина;
					Стр.Статус = "Ошибка";
					Стр.Ошибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Ошибка выполнения операции №%1. Список параметров пуст!'; uk = 'Помилка виконання операції №%1. Список параметрів пустий!'"),
									Стр.НомерОперации);
							
					КоличествоОжидающихОпераций = КоличествоОжидающихОпераций - 1;
					Продолжить;
				КонецЕсли;
				
				ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
				
				Попытка
					ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(
											ПараметрыВыполнения, 
											"Обработки.ВыгрузкаДокументовВБухгалтерию.СлужебнаяВыгрузкаДокументов",
											ПараметрыВыгрузки);			
											
					Стр.Операция = ДлительнаяОперация;
					Если ДлительнаяОперация.Статус ="Выполнено" Тогда
						Стр.Завершено = Истина;	
						Стр.Статус = "Завершено"
					Иначе
						Стр.Статус = "Выполняется";
					КонецЕсли;

					КоличествоАктивныхОпераций = КоличествоАктивныхОпераций + 1;
				Исключение				
					Стр.Завершено = Истина;
					Стр.Статус = "Ошибка";
					Стр.Ошибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Ошибка выполнения операции №%1. Список параметров пуст!'; uk = 'Помилка виконання операції №%1. Список параметрів пустий!'"),
									Стр.НомерОперации);
					
					КоличествоОжидающихОпераций = КоличествоОжидающихОпераций - 1;					
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Прогресс = ПолучитьПрогрессВыгрузки(ЗаданияОбмена); 				
		ДлительныеОперации.СообщитьПрогресс(Прогресс.Процент, Прогресс.Текст, Прогресс.МассивЗаданий);
		
		Если ОжидатьРезультат Тогда 
			// задержка перед новой итерацией 
			// 5 секунд
			Пауза(5);
		КонецЕсли;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ПолучитьПрогрессВыгрузки(ЗаданияОбмена), АдресРезультата);	
	
КонецПроцедуры

Функция ВыгрузитьДокументыВБухгалтерию(ЗначенияПараметров, ДлительнаяОперация = Ложь) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);	

	СвойстваПодключения = ТК_БУХ_РаботаWebСервиса.ПолучитьWSСсылкуБухгалтерия();
	
	Если Не СвойстваПодключения.Успешно Тогда 
		Сообщение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваПодключения, "Сообщение", НСтр("ru = 'Не удалось подключится к базе ""Бухгалтерия""'; uk = 'Не вдалося підключитися до бази ""Бухгалтерія""'"));
		
		Результат = Новый Структура;
		Результат.Вставить("Статус", Ложь);
		Результат.Вставить("Сообщение", Сообщение);
		Возврат Результат;	
	КонецЕсли;
	
	WS = СвойстваПодключения.WS;	
		
	ПравилаОбмена = ПолучитьМакет("Правило");	
		
	ЧтениеХМЛ = Новый ЧтениеXML;	
	ЧтениеХМЛ.УстановитьСтроку(ПравилаОбмена.ПолучитьТекст());
	ЧтениеХМЛ.Прочитать();
	
	Обмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	
	ФайлПротокола = ПолучитьИмяВременногоФайла(".txt");
	Обмен.ИмяФайлаПротоколаОбмена = ФайлПротокола;

	Обмен.РежимОбмена = "Выгрузка";
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	Обмен.ИмяФайлаОбмена = ИмяВремФайла;
	Обмен.ИмяФайлаПравилОбмена ="ЧтениеXML";	
	Обмен.ЗагрузитьПравилаОбмена(ЧтениеХМЛ,"ЧтениеXML");
	
	Для каждого КлючИЗнач Из ЗначенияПараметров Цикл
		Обмен.УстановитьЗначениеПараметраВТаблице(КлючИЗнач.Ключ, КлючИЗнач.Значение);
	КонецЦикла; 
	
	Обмен.ЗагружатьДанныеВРежимеОбмена = Истина;
	Обмен.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
	
	Если ДлительнаяОперация Тогда 
		ДлительныеОперации.СообщитьПрогресс(10, НСтр("ru = 'Идет процесс выгрузки документов...'; uk = 'Іде процес вивантаження документів...'"));
	КонецЕсли;	
	
	Обмен.ВыполнитьВыгрузку();
	
	ЧтениеТекстаПротокола = Новый ЧтениеТекста;
	ЧтениеТекстаПротокола.Открыть(ФайлПротокола, КодировкаТекста.Системная);
	
	ТекстПротокола = ЧтениеТекстаПротокола.Прочитать();

	ВсеОк = Истина;
	Выгружено = 0;
	Для ст = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл 
		тст  = СтрПолучитьСтроку(ТекстПротокола,ст);
		Если  СтрНайти(тст,"Выгружено объектов:") > 0 Тогда
			Попытка
				Выгружено = Число(СокрЛП(СтрЗаменить(тст,"Выгружено объектов:","")));
			Исключение
			КонецПопытки;
		ИначеЕсли СтрНайти(ВРег(тст),ВРег("Ошибка"))> 0 Тогда
			ВсеОк = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	ЧтениеТекстаПротокола.Закрыть();
	УдалитьФайлы(ФайлПротокола);

	Если Не ВсеОк Тогда 
		Результат = Новый Структура;
		Результат.Вставить("Статус", ВсеОк);
		Результат.Вставить("Сообщение", ТекстПротокола);
		Возврат Результат;	
	КонецЕсли;	
	
	Если ДлительнаяОперация Тогда 
		ДлительныеОперации.СообщитьПрогресс(
			60, 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 объектов.
                      |Идет процесс загрузки документов...'; uk = 'Вивантажено %1 об''єктів.
                      |Іде процес завантаження документів...'"),
				Выгружено));
	КонецЕсли;	
		
	///Результат выгрузки данных
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ИмяВремФайла, КодировкаТекста.UTF8);
	ФайлВыгрузки = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ИмяВремФайла);
	
	ХранилищеДанных = Новый ХранилищеЗначения(ФайлВыгрузки, Новый СжатиеДанных(9));
	
	Попытка
		ОтветБух = WS.DownloadUO(ХранилищеДанных);
		
		Результат = Новый Структура;
		Результат.Вставить("Статус", ОтветБух.Success);
		Результат.Вставить("Сообщение", ОтветБух.Description);
		Возврат Результат;		

	Исключение
		Результат = Новый Структура;
		Результат.Вставить("Статус", Ложь);
		Результат.Вставить("Сообщение", ОписаниеОшибки());
		Возврат Результат;		
	КонецПопытки;
	
КонецФункции

Процедура ВыгрузитьДанные() Экспорт 
	
	ИдентификаторОбработки = "ТК_ВыгрузкаИзменений_Бухгалтерия";		
	КлючОбработки = "ВыгрузкаДокументы_В_Бухгалтерия";
	
	СтруктураЗаписиЖурнала = РегистрыСведений.РезультатыВыполненияОбработок.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиЖурнала);
	
	
	СвойстваПодключения = ТК_БУХ_РаботаWebСервиса.ПолучитьWSСсылкуБухгалтерия();
	
	Если Не СвойстваПодключения.Успешно Тогда 
		Сообщение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваПодключения, "Сообщение", НСтр("ru = 'Не удалось подключится к базе ""Бухгалтерия""'; uk = 'Не вдалося підключитися до бази ""Бухгалтерія""'"));						
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
		
		КодСостояния = "ОбработкаОбмена.ИнициализацияОбработки - Не удалось подключится к базе ""Бухгалтерия""";						
		
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание		"+Сообщение, СтруктураЗаписиЖурнала);												
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		
		Возврат;
	КонецЕсли;
	
	WS = СвойстваПодключения.WS;	
		
	//ПравилаОбмена	= ПолучитьМакет("Правило").ПолучитьТекст();
	//ХранилищеДанных	= Новый ХранилищеЗначения(ПравилаОбмена, Новый СжатиеДанных(9));	
	ХранилищеДанных	= ОбменДаннымиПереопределяемый.ПравилаКонвертацииОбъектов("ОбменБухгалтерия");
	
	Если ХранилищеДанных = Неопределено Тогда
		КодСостояния = "РегистрСведений.ПравилаДляОбменаДанными - Не удалось найти ПравилаКонвертацииОбъектов для ПланыОбмена.ОбменБухгалтерия";						
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		Возврат;
	КонецЕсли;
	
	УзлыОбмена		= ПланыОбмена.ОбменБухгалтерия.НайтиПоКоду("BUH");
	СтатусВыгрузки	= Ложь;
	Ошибки			= "";
	Выгружено		= 0;
	
	Попытка
		ДанныеОбмена = СформироватьПакетДанных(ХранилищеДанных, УзлыОбмена, СтатусВыгрузки, Выгружено, Ошибки);
	Исключение 
		ОшибкиИнициализации = ОписаниеОшибки();
		
		ОбщегоНазначения.СообщитьПользователю(ОшибкиИнициализации);
		
		КодСостояния = "ОбработкаОбмена.ФормированиеПакетаДанных - Не удалось выгрузить данные";						
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание		"+ОшибкиИнициализации, СтруктураЗаписиЖурнала);												
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		
		Возврат;
	КонецПопытки;
	
	Если СтатусВыгрузки Тогда
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(Ошибки, СтруктураЗаписиЖурнала);
	Иначе
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке(Ошибки, СтруктураЗаписиЖурнала);												
	КонецЕсли;
	
	Если Выгружено Тогда

		Попытка
			ОтветБух = WS.DownloadUO(ДанныеОбмена);
			СтатусВыгрузки = ОтветБух.Success;
			ОписаниеВыгрузки = ОтветБух.Description;
			
			Если СтатусВыгрузки Тогда
				ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбмена, 1);
				
				РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(ОписаниеВыгрузки, СтруктураЗаписиЖурнала);
				РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиЖурнала);
			Иначе
				РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке(ОписаниеВыгрузки, СтруктураЗаписиЖурнала);												
			КонецЕсли;
			
			РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СтатусВыгрузки);
		Исключение 
			ОшибкиИнициализации = ОписаниеОшибки();
			
			ОбщегоНазначения.СообщитьПользователю(ОшибкиИнициализации);
			
			КодСостояния = "ОбработкаОбмена.ЗагрузкаДокументов - Не удалось загрузить данные";						
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния "+КодСостояния, СтруктураЗаписиЖурнала);
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание "+ОшибкиИнициализации, СтруктураЗаписиЖурнала);												
			РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
			
			Возврат;
		КонецПопытки;
	Иначе
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СтатусВыгрузки);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
	
	
	
#Область СлужебныеПроцедурыИФункции

Процедура СлужебнаяВыгрузкаДокументов(ЗначенияПараметров) Экспорт 
	
	Результат = ВыгрузитьДокументыВБухгалтерию(ЗначенияПараметров, Ложь);
	
	Если Не Результат.Статус Тогда 
		ВызватьИсключение Результат.Сообщение;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПакетДанных(ПравилаОбмена, УзелОбмена, СтатусВыгрузки, Выгружено, Ошибки)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	
	ЧтениеХМЛ = Новый ЧтениеXML;
	
	ЧтениеХМЛ.УстановитьСтроку(ПравилаОбмена.Получить());
	ЧтениеХМЛ.Прочитать();
	
	Обмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	
	ФайлПротокола = ПолучитьИмяВременногоФайла(".txt");
	Обмен.ИмяФайлаПротоколаОбмена = ФайлПротокола;
	
	Обмен.РежимОбмена = "Выгрузка";
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	Обмен.ИмяФайлаОбмена = ИмяВремФайла;
	Обмен.ИмяФайлаПравилОбмена ="ЧтениеXML";	
	Обмен.ЗагрузитьПравилаОбмена(ЧтениеХМЛ,"ЧтениеXML");
	
	//УстановитьУзелОбменаУСтрокДерева(Обмен.ТаблицаПравилВыгрузки.Строки,УзелОбмена);
	
	Обмен.ЗагружатьДанныеВРежимеОбмена = Истина;
	Обмен.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
	Обмен.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 0;
	
	//Если УзелОбмена.Код = "BUH" Тогда
	//	Обмен.УстановитьЗначениеПараметраВТаблице("Получатель", УзелОбмена);
	//КонецЕсли;
	
	Обмен.УстановитьЗначениеПараметраВТаблице("ВыгрузкаДокументы", Истина);
	
	Обмен.ВыполнитьВыгрузку();
	
	/// читаем логи
	ЧтениеТекстаПротокола = Новый ЧтениеТекста;
	ЧтениеТекстаПротокола.Открыть(ФайлПротокола, КодировкаТекста.Системная);
	ТекстПротокола = ЧтениеТекстаПротокола.Прочитать();
	
	ВсеОк = Истина;
	Для ст = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл 
		тст  = СтрПолучитьСтроку(ТекстПротокола,ст);
		Если  СтрНайти(тст,"Выгружено объектов:") > 0 Тогда
			Попытка
				Выгружено = Число(СокрЛП(СтрЗаменить(тст,"Выгружено объектов:","")));
			Исключение
			КонецПопытки;
		ИначеЕсли СтрНайти(ВРег(тст),ВРег("Ошибка"))> 0 Тогда
			ВсеОк = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Ошибки = ТекстПротокола;
	СтатусВыгрузки = ВсеОк;	
	ЧтениеТекстаПротокола.Закрыть();
	УдалитьФайлы(ФайлПротокола);
	
	///Результат выгрузки данных
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ИмяВремФайла, КодировкаТекста.UTF8);
	Результат = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ИмяВремФайла);
	
	ХранилищеДанных = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));
	
	Возврат ХранилищеДанных;
	
КонецФункции

Процедура РаспределитьДокументыПоОперациям(ТаблицаДокументов, ЗаданияОбмена, КоличествоВыборки);
	
	НомерОперации = 1;
	КоличествоОперации = 0;
	
	Для Каждого Док Из ТаблицаДокументов Цикл 
		Если КоличествоОперации >= КоличествоВыборки Тогда 
			ЗарегистрироватьОперацию(НомерОперации, КоличествоОперации, ЗаданияОбмена);
		КонецЕсли;			
		
		Док.НомерОперации = НомерОперации;				
		КоличествоОперации = КоличествоОперации + 1;	
	КонецЦикла;
		
	Если КоличествоОперации > 0 Тогда 
		ЗарегистрироватьОперацию(НомерОперации, КоличествоОперации, ЗаданияОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьОперацию(НомерОперации, КоличествоОперации, ЗаданияОбмена)
	
	НовоеЗадание = ЗаданияОбмена.Добавить();
	НовоеЗадание.НомерОперации 		= НомерОперации;
	НовоеЗадание.Статус 			= "Ожидает";
	НовоеЗадание.КоличествоВыборки 	= КоличествоОперации;
												
	//Сбросим параметры												
	НомерОперации 			= НомерОперации + 1;
	КоличествоОперации 		= 0;
	
КонецПроцедуры

Функция ОбновитьСостояниеАктивныхОпераций(ЗаданияОбмена)
	
	ЕстьИзменения = Ложь;	
	АктивныеОперации = ЗаданияОбмена.НайтиСтроки(Новый Структура("Запущено, Завершено", Истина, Ложь));	
	
	Для Каждого Стр Из АктивныеОперации Цикл 
		Результат = ДлительныеОперации.ОперацияВыполнена(Стр.Операция.ИдентификаторЗадания, Ложь, Ложь, Ложь);
		Стр.Статус = Результат.Статус;
		
		Если Результат.Статус <> "Выполняется" Тогда 				
			Стр.Завершено = Истина;
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если Результат.КраткоеПредставлениеОшибки <> Неопределено Тогда 
			СообщениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Ошибка выполнения задания №%1.
										|%2'; uk = 'Помилка виконання завдання №%1.
										|%2'"),
										Стр.НомерОперации,
										Результат.КраткоеПредставлениеОшибки);
										
			Стр.Ошибки = СообщениеОшибки;																				
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда 
		АктивныеОперации = ЗаданияОбмена.НайтиСтроки(Новый Структура("Запущено, Завершено", Истина, Ложь));	
	КонецЕсли;
		
	Возврат АктивныеОперации;
КонецФункции


#КонецОбласти



#Область ПрочиеПроцедурыИФункции
	
Процедура Пауза(ИнтервалОжидания)
	ПараметрыЗапуска = ФайловаяСистема.ПараметрыЗапускаПрограммы();
	ПараметрыЗапуска.ДождатьсяЗавершения = Истина;
	ФайловаяСистема.ЗапуститьПрограмму("timeout /t " + Формат(ИнтервалОжидания, "ЧГ=0"), ПараметрыЗапуска);
КонецПроцедуры

Функция ПолучитьПараметрыДляВыгрузки(МассивДляВыгрузки) Экспорт 
	
	ПоступлениеТоваровМассив 	= Новый Массив;
	ВозвратПоставщикуМассив 	= Новый Массив;
	РеализацияТоваровМассив 	= Новый Массив;
	ВозвратОтПокупателяМассив 	= Новый Массив;
	КорректировкаДолгаМассив 	= Новый Массив;
	НалоговаяНакладнаяМассив 	= Новый Массив;
	Приложение2кННМассив		= Новый Массив;
	ЗКСМассив					= Новый Массив;
	КорректировкиЗКСМассив		= Новый Массив;
	КорректировкиЗКСАкцизМассив = Новый Массив;
	РКОМассив					= Новый Массив;

	Для Каждого Стр Из МассивДляВыгрузки Цикл 
		Док = Стр.Ссылка;
		ТипЗначения = ТипЗнч(Док);
		
		Если ТипЗначения = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
			ПоступлениеТоваровМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ВозвратПоставщику") Тогда 
			ВозвратПоставщикуМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
			РеализацияТоваровМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда 
			ВозвратОтПокупателяМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ТК_КорректировкаДолга") Тогда 
			КорректировкаДолгаМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ТК_НалоговаяНакладная") Тогда 
			НалоговаяНакладнаяМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ТК_Приложение2КНалоговойНакладной") Тогда 
			Приложение2кННМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.ТК_ЗакрытиеСменыЗКС") Тогда 
			ЗКСМассив.Добавить(Док);
		ИначеЕсли ТипЗначения = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда	
			РКОМассив.Добавить(Док);
		Иначе
			Если Стр.ТипДокумента = "КорректировкиЗКС" Тогда
				СтруктураКорректировки = Новый Структура("Дата,Организация,GUID_Поиска");
				ЗаполнитьЗначенияСвойств(СтруктураКорректировки, Стр, "Дата,Организация,GUID_Поиска");
				Если Стр.СуммаНДС <> 0 Тогда 
					СтруктураКорректировки.Вставить("СуммаНДС", Стр.СуммаНДС);
				КонецЕсли;
				Если Стр.СуммаАкцизногоНалога <> 0 Тогда 
					СтруктураКорректировки.Вставить("СуммаАкциз", Стр.СуммаАкцизногоНалога);
				КонецЕсли;				
				КорректировкиЗКСМассив.Добавить(СтруктураКорректировки);
			ИначеЕсли Стр.ТипДокумента = "КорректировкиЗКСАкциз" Тогда 						
				
			//Дописать добавление корректировок
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВыгрузки = Новый Структура;
	
	Если ПоступлениеТоваровМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("ПоступлениеТоваровМассив", ПоступлениеТоваровМассив);
	КонецЕсли;
	
	Если ВозвратПоставщикуМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("ВозвратПоставщикуМассив", ВозвратПоставщикуМассив);
	КонецЕсли;
	
	Если РеализацияТоваровМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("РеализацияТоваровМассив", РеализацияТоваровМассив);
	КонецЕсли;
	
	Если ВозвратОтПокупателяМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("ВозвратОтПокупателяМассив", ВозвратОтПокупателяМассив);
	КонецЕсли;
	
	Если КорректировкаДолгаМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("КорректировкаДолгаМассив", КорректировкаДолгаМассив);
	КонецЕсли;
	
	Если НалоговаяНакладнаяМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("НалоговаяНакладнаяМассив", НалоговаяНакладнаяМассив);
	КонецЕсли;
	
	Если ЗКСМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("ЗКСМассив", ЗКСМассив);
	КонецЕсли;
	
	Если Приложение2кННМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("Приложение2кННМассив", Приложение2кННМассив);
	КонецЕсли;
	
	Если КорректировкиЗКСМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("КорректировкиЗКСМассив", КорректировкиЗКСМассив);
	КонецЕсли;
	
	Если КорректировкиЗКСАкцизМассив.Количество() > 0 Тогда 
		ПараметрыВыгрузки.Вставить("КорректировкиЗКСАкцизМассив", КорректировкиЗКСАкцизМассив);		
	КонецЕсли;
	
	Если РКОМассив.Количество()>0 Тогда
		ПараметрыВыгрузки.Вставить("РКОМассив", РКОМассив);		
	КонецЕсли;
	
	
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции

Функция ПолучитьПрогрессВыгрузки(ЗаданияОбмена)

	Количество = ЗаданияОбмена.Итог("КоличествоВыборки");
	Завершено = ЗаданияОбмена.Скопировать(Новый Структура("Завершено", Истина), "КоличествоВыборки").Итог("КоличествоВыборки");	
	
	Процент = Цел(Завершено / Количество * 100);
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 из %2...'; uk = 'Вивантажено %1 із %2...'"),
				Завершено,
				Количество);
				
	МассивЗаданий = Новый Массив;
	Для Каждого Стр Из ЗаданияОбмена Цикл 
		СтруктураЗадания = Новый Структура("НомерОперации,Статус,КоличествоВыборки,Ошибки");
		ЗаполнитьЗначенияСвойств(СтруктураЗадания, Стр, "НомерОперации,Статус,КоличествоВыборки,Ошибки");		
		МассивЗаданий.Добавить(СтруктураЗадания);
	КонецЦикла;
	
	Возврат Новый Структура("Процент, Текст, МассивЗаданий", Процент, Текст, МассивЗаданий);
	
КонецФункции


#КонецОбласти



#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
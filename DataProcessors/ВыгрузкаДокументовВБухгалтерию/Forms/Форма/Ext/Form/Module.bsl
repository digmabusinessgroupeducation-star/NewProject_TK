

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПравоДоступа("Чтение",Метаданные.Документы.ПоступлениеТоваров) Тогда
		СписокДокументов.Добавить("ПоступлениеТоваров", "Поступление товаров", Ложь);
		СписокДокументов.Добавить("ПоступлениеТоваровТранзит", "Поступление товаров (транзит)", Ложь);
	КонецЕсли;
	Если ПравоДоступа("Чтение",Метаданные.Документы.ВозвратПоставщику) Тогда
		СписокДокументов.Добавить("ВозвратПоставщику", "Возврат поставщику", Ложь);
		СписокДокументов.Добавить("ВозвратПоставщикуТранзит", "Возврат поставщику (транзит)", Ложь);
	КонецЕсли;
	Если ПравоДоступа("Чтение",Метаданные.Документы.РеализацияТоваров) Тогда
		СписокДокументов.Добавить("РеализацияТоваров", "Реализация товаров", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ВозвратОтПокупателя) Тогда
		СписокДокументов.Добавить("ВозвратОтПокупателя", "Возврат от покупателя", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_КорректировкаДолга) Тогда
		СписокДокументов.Добавить("КорректировкаДолга", "Корректировка долга", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_НалоговаяНакладная) Тогда
		СписокДокументов.Добавить("НалоговыеНакладные", "Налоговые накладные", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_Приложение2КНалоговойНакладной) Тогда
		СписокДокументов.Добавить("Приложение2кНН", "Приложение 2 к НН", Ложь);		
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_ЗакрытиеСменыЗКС) Тогда
		СписокДокументов.Добавить("ЗКС", "Закрытие смены ЗКС", Ложь);
		СписокДокументов.Добавить("КорректировкиЗКС", "Корректировки ЗКС (ежедневные)", Ложь);
		СписокДокументов.Добавить("КорректировкиЗКСАкциз", "Корректировки ЗКС (Акциз)", Ложь);
	КонецЕсли;			
	
	Если ПравоДоступа("Чтение",Метаданные.Документы.РасходныйКассовыйОрдер) Тогда		
		СписокДокументов.Добавить("РКОБанк", "РКО (Банк)", Ложь);
	КонецЕсли;			

	ОбновитьОтборСтрок();
	
	ВыгрузитьВФоне = Истина;
	КоличествоВыборки = 100;
	КоличествоПотоков = 7;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостьюЛистов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если РежимФормы = "Выгрузка" И ВыгрузитьВФоне Тогда 
		ПроверяемыеРеквизиты.Добавить("КоличествоВыборки");
		ПроверяемыеРеквизиты.Добавить("КоличествоПотоков");
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокДокументовПометкаПриИзменении(Элемент)
	
	УправлениеВидимостьюЛистов();
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыборПриИзменении(Элемент)
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ПересчитатьФлажки(ТипДокумента);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьДокументы(Команда)
	
	РежимФормы = "Загрузка";
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	ПолучитьДокументыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДокументыНаСервере()
	
	ТаблицаДокументов.Очистить();
	
	Для Каждого ТипДокумента Из СписокДокументов Цикл 
		Если ТипДокумента.Пометка Тогда 
			ЗаполнитьДокументыПоТипу(ТипДокумента.Значение);			
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаДокументов.Количество() > 0 Тогда 
		ОбновитьОтборСтрок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументы(Команда)

	РежимФормы = "Выгрузка";
	
	ОчиститьСообщения();		
	ЗаданияОбмена.Очистить();
	
	Если ВыгрузитьВФоне Тогда 
		ЗапуститьВыгрузку();
	Иначе
		ВыгрузитьДокументыНепосредственно();
		РежимФормы = "";
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура ФлажкиУстановить(Команда)
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ЗаполнитьФлажки(ТипДокумента, Истина);	
	ПересчитатьФлажки(ТипДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиСнять(Команда)

	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ЗаполнитьФлажки(ТипДокумента, Ложь);	
	ПересчитатьФлажки(ТипДокумента);
	
КонецПроцедуры


&НаКлиенте
Процедура ВернутьсяКДанным()
	
	Если РежимФормы = "Выгрузка" Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаданияОбмена.Очистить();
	
	Элементы.ДлительнаяОперация.Видимость = Ложь;
	Элементы.РезультатВыполнения.Видимость = Истина;
	Элементы.РежимыФормы.ТекущаяСтраница = Элементы.РежимДанных;

	Для Каждого Стр Из СписокДокументов Цикл 
		Если Стр.Пометка Тогда 
			Стр.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	УправлениеВидимостьюЛистов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОперацию()
	
	Если РежимФормы <> "Отмена" Тогда 
		РежимФормы = "Отмена";
		ПоместитьВоВременноеХранилище(Новый Структура("Отмена", Истина), АдресОбмена);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДокументыПоТипу(ТипДокумента)

	Если ТипДокумента = "КорректировкиЗКС" Тогда 
		ЗаполнитьКорректировкиЗКС();
		Возврат;
	ИначеЕсли ТипДокумента = "КорректировкиЗКСАкциз" Тогда 
		//ЗаполнитьКорректировкиЗКС();
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);		
	Запрос.УстановитьПараметр("ТипДокумента", ТипДокумента);
	
	Отбор = Новый ТекстовыйДокумент;
	ЕстьКонтрагент = ТипДокумента <> "ЗКС";
	            
	Если Не Подразделение.Пустая() Тогда 
		Отбор.ДобавитьСтроку("И Документ.ПодразделениеКомпании = &Подразделение");
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	КонецЕсли;
	
	Если Не Организация.Пустая() Тогда 
		Отбор.ДобавитьСтроку("И Документ.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если Не Контрагент.Пустая() И ЕстьКонтрагент Тогда 
		Отбор.ДобавитьСтроку("И Документ.Контрагент = &Контрагент");
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
	КонецЕсли;
	
	ТипДок = ТипДокумента;
	
	Если ТипДокумента = "ПоступлениеТоваров" Тогда				
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)");
		
	ИначеЕсли ТипДокумента = "ПоступлениеТоваровТранзит" Тогда 
		
		ТипДок = "ПоступлениеТоваров";	
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		Отбор.ДобавитьСтроку("И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)");
		
	ИначеЕсли ТипДокумента = "ВозвратПоставщику" Тогда 
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)");
		
	ИначеЕсли ТипДокумента = "ВозвратПоставщикуТранзит" Тогда 
		
		ТипДок = "ВозвратПоставщику";	
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)");
		
	ИначеЕсли ТипДокумента = "РеализацияТоваров" Тогда 		
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОбОказанииУслуг)");
		
	ИначеЕсли ТипДокумента = "ВозвратОтПокупателя" Тогда 		
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		
	ИначеЕсли ТипДокумента = "КорректировкаДолга" Тогда 
		
		ТипДок = "ТК_КорректировкаДолга";
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		
	ИначеЕсли ТипДокумента = "НалоговыеНакладные" Тогда 		
		
		ТипДок = "ТК_НалоговаяНакладная";
		//Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		
	ИначеЕсли ТипДокумента = "Приложение2кНН" Тогда 
		
		ТипДок = "ТК_Приложение2КНалоговойНакладной";
		
	ИначеЕсли ТипДокумента = "ЗКС" Тогда 
		
		ТипДок = "ТК_ЗакрытиеСменыЗКС";
	ИначеЕсли ТипДокумента = "РКОБанк" Тогда
		
		ТипДок = "РасходныйКассовыйОрдер";
		Отбор.ДобавитьСтроку("И Документ.СтатьяДДС = &СтатьяДДС");
	  	Запрос.УстановитьПараметр("СтатьяДДС", Справочники.СтатьиДДС.НайтиПоКоду("DG000001"));

	КонецЕсли;
			
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Документ.Ссылка КАК Ссылка,
				   |	Документ.Дата КАК Дата,
				   |	Документ.Организация КАК Организация,
	               |	%2
	               |	Документ.ПодразделениеКомпании КАК Подразделение,
	               |	Документ.СуммаДокумента КАК СуммаВсего
	               |ИЗ
	               |	Документ.%1 КАК Документ
	               |ГДЕ
	               |	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И Документ.Проведен
				   |	%3
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстЗапроса, 
						ТипДок, 
						?(ЕстьКонтрагент, "Документ.Контрагент КАК Контрагент,", ""),
						Отбор.ПолучитьТекст());
						
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);		
		НоваяСтрока.ТипДокумента = ТипДокумента;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКорректировкиЗКС()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);		
	
	ОтборПодразделение = "";
	ОтборОрганизация = "";	
	
	Если Не Подразделение.Пустая() Тогда 
		ОтборПодразделение = "ГДЕ
	               			|	ПогрешностиДокументовОбороты.Регистратор.ПодразделениеКомпании = &Подразделение";
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	КонецЕсли;
	
	Если Не Организация.Пустая() Тогда 
		ОтборОрганизация = "Организация = &Организация";
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;

	ТекстЗапроса = "ВЫБРАТЬ
	               |	КОНЕЦПЕРИОДА(ПогрешностиДокументовОбороты.Период, ДЕНЬ) КАК Дата,
	               |	ПогрешностиДокументовОбороты.Организация КАК Организация,
	               |	(ВЫРАЗИТЬ(СУММА(ПогрешностиДокументовОбороты.СуммаОборот) КАК ЧИСЛО(32, 2))) - СУММА(ПогрешностиДокументовОбороты.ОкрСуммаОборот) КАК СуммаВсего,
	               |	(ВЫРАЗИТЬ(СУММА(ПогрешностиДокументовОбороты.СуммаНДСОборот) КАК ЧИСЛО(32, 2))) - СУММА(ПогрешностиДокументовОбороты.ОкрСуммаНДСОборот) КАК СуммаНДС,
	               |	(ВЫРАЗИТЬ(СУММА(ПогрешностиДокументовОбороты.СуммаАкцизногоНалогаОборот) КАК ЧИСЛО(32, 2))) - СУММА(ПогрешностиДокументовОбороты.ОкрСуммаАкцизногоНалогаОборот) КАК СуммаАкцизногоНалога
	               |ИЗ
	               |	РегистрНакопления.ПогрешностиДокументов.Обороты(&НачалоПериода, &КонецПериода, День, %2) КАК ПогрешностиДокументовОбороты
	               |%1
	               |СГРУППИРОВАТЬ ПО
	               |	ПогрешностиДокументовОбороты.Организация,
	               |	КОНЕЦПЕРИОДА(ПогрешностиДокументовОбороты.Период, ДЕНЬ)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
		
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ОтборПодразделение, ОтборОрганизация);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);		
		НоваяСтрока.Выбор = Истина;
		НоваяСтрока.ТипДокумента = "КорректировкиЗКС";
		НоваяСтрока.GUID_Поиска = ЗначениеВСтрокуВнутр(Строка(Выборка.Организация.УникальныйИдентификатор()) + "_DATE" + Формат(Выборка.Дата, "ДФ=ггггММдд"));
	КонецЦикла;
		
КонецПроцедуры



#Область ВыгрузкаВФоне

&НаКлиенте
Процедура ЗапуститьВыгрузку()
	
	АдресОбмена = Новый УникальныйИдентификатор;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("АдресРезультата", АдресОбмена);
	
	КорневоеЗадание = ЗапуститьКорневоеЗаданиеВыгрузки(ПараметрыВыгрузки);
	
	ВсеОк = Истина;
	
	Если КорневоеЗадание = Неопределено Тогда 
		ВсеОк = Ложь;		
	ИначеЕсли ТипЗнч(КорневоеЗадание) = Тип("Структура") И КорневоеЗадание.Статус = "Ошибка" Тогда 
		ВсеОк = Ложь;		
		ОбщегоНазначенияКлиент.СообщитьПользователю(КорневоеЗадание.ПодробноеПредставлениеОшибки);
				
		Для каждого СообщениеПользователю Из КорневоеЗадание.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если ВсеОк Тогда 
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеОперации", ЭтотОбъект, ПараметрыВыгрузки);
		ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ОповещениеОПрогрессеВыполнения", ЭтотОбъект, ПараметрыВыгрузки);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		//ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Пожалуйста, подождите...'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
		ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
		ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ВыгрузкаДокументовВБухгалтерию";
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ВыводитьСообщения = Истина;		
		ПараметрыОжидания.Интервал = 2;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(КорневоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);		
		
		Элементы.РежимыФормы.ТекущаяСтраница = Элементы.РежимОжидания;
		Элементы.ДлительнаяОперация.Видимость = Истина;
		Элементы.РезультатВыполнения.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьКорневоеЗаданиеВыгрузки(Знач ПараметрыВыгрузки)
	
	Если КоличествоВыборки > 0 Тогда 
		ПараметрыВыгрузки.Вставить("КоличествоВыборки", КоличествоВыборки);
	КонецЕсли;
	Если КоличествоПотоков > 0 Тогда 
		ПараметрыВыгрузки.Вставить("КоличествоПотоков", КоличествоПотоков);	
	КонецЕсли;
	
	тчДокументы = ТаблицаДокументов.Выгрузить();	
	
	ТаблицаДокументов.Очистить();
	
	МассивСтрокВыгрузки = Новый Массив;		
	
	//ПредставленияТиповДокументов = Новый Структура;
	
	Для Каждого Стр Из СписокДокументов Цикл 
		Если Не Стр.Пометка Тогда 
			Продолжить;
		КонецЕсли;				
				
		мСтрок = тчДокументы.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Истина, Стр.Значение));		
		Если мСтрок.Количество() > 0 Тогда 
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВыгрузки, мСтрок);
			
			//ПредставленияТиповДокументов.Вставить(Стр.Значение, Стр.Представление);
		КонецЕсли;				
	КонецЦикла;	
	
	Если МассивСтрокВыгрузки.Количество() = 0 Тогда 
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Нет отмеченных документов для выгрузки'; uk = 'Немає відмічених документів для вивантаження'"));			
		Возврат Неопределено;
	КонецЕсли;	
	
	//ПараметрыВыгрузки.Вставить("ПредставленияТиповДокументов", ПредставленияТиповДокументов);
	ТаблицаВыгрузки = тчДокументы.Скопировать(МассивСтрокВыгрузки);	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	
	Попытка
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(
								ПараметрыВыполнения, 
								"Обработки.ВыгрузкаДокументовВБухгалтерию.КорневоеЗаданиеВыгрузкиВБухгалтерию",
								ТаблицаВыгрузки,
								ПараметрыВыгрузки);
								
		Возврат ДлительнаяОперация;
		
	Исключение
		ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка выполнения операции №%1. Список параметров пуст!'; uk = 'Помилка виконання операції №%1. Список параметрів пустий!'"),
					Стр.НомерОперации));
					
		Возврат Неопределено;
	КонецПопытки;						
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОПрогрессеВыполнения(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс.Прогресс = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Прогресс.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Прогресс.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ПрогрессСтрокой(Прогресс.Прогресс);

	Если ТипЗнч(Прогресс.Прогресс.ДополнительныеПараметры) = Тип("Массив") Тогда 
		
		МассивОпераций = Прогресс.Прогресс.ДополнительныеПараметры;			
		ЗаданияЗаполнены = ЗаданияОбмена.Количество() > 0;
		
		Для Ит = 0 По МассивОпераций.ВГраница() Цикл 
			Задание = МассивОпераций.Получить(Ит);
			
			Если ЗаданияЗаполнены Тогда 
				фЗадание = ЗаданияОбмена.Получить(Ит);								
			Иначе
				фЗадание = ЗаданияОбмена.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(фЗадание, Задание);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗавершениеОперации(Операция, ДопПараметры) Экспорт

	Результат = Неопределено;
	РежимФормы = "";
	
	ЗаданияСОшибками = ЗаданияОбмена.НайтиСтроки(Новый Структура("Статус", "Ошибка"));		
	ЕстьОшибки = ЗаданияСОшибками.Количество() > 0;
	
	Элементы.ДлительнаяОперация.Видимость = Ложь;
	Элементы.РезультатВыполнения.Видимость = Истина;
	
	Попытка
		Результат = ПолучитьИзВременногоХранилища(АдресОбмена);
	Исключение
	КонецПопытки;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		Если Результат.Свойство("МассивЗаданий") Тогда 
			Для Ит = 0 По Результат.МассивЗаданий.ВГраница() Цикл 	
				Задание = Результат.МассивЗаданий.Получить(Ит);
				
				Попытка
					фЗадание = ЗаданияОбмена.Получить(Ит);
				Исключение
					фЗадание = ЗаданияОбмена.Добавить();
				КонецПопытки;
				
				ЗаполнитьЗначенияСвойств(фЗадание, Задание);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда 
		Для Каждого Задание Из ЗаданияСОшибками Цикл 
			ОбщегоНазначенияКлиент.СообщитьПользователю(Задание.Ошибки);
		КонецЦикла;
	Иначе
		ВернутьсяКДанным();		
	КонецЕсли;
		
КонецПроцедуры


#КонецОбласти



#Область ВыгрузкаНепосредственно

&НаКлиенте
Процедура ВыгрузитьДокументыНепосредственно()
		
	ВыгрузитьДокументыНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДокументыНаСервере()
	
	тчДокументы = ТаблицаДокументов.Выгрузить();		
	ТаблицаДокументов.Очистить();
	
	МассивСтрокВыгрузки = Новый Массив;		
	
	Для Каждого Стр Из СписокДокументов Цикл 
		Если Не Стр.Пометка Тогда 
			Продолжить;
		КонецЕсли;				
		
		мСтрок = тчДокументы.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Истина, Стр.Значение));		
		Если мСтрок.Количество() > 0 Тогда 
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВыгрузки, мСтрок);
		КонецЕсли;				
	КонецЦикла;	
	
	Если МассивСтрокВыгрузки.Количество() = 0 Тогда 
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Нет отмеченных документов для выгрузки'; uk = 'Немає відмічених документів для вивантаження'"));			
		Возврат;
	КонецЕсли;	
	
	ПараметрыВыгрузки = Обработки.ВыгрузкаДокументовВБухгалтерию.ПолучитьПараметрыДляВыгрузки(МассивСтрокВыгрузки);
	
	Если ПараметрыВыгрузки.Количество() = 0 Тогда 
		Возврат;	
	КонецЕсли;
	
	Результат = Обработки.ВыгрузкаДокументовВБухгалтерию.ВыгрузитьДокументыВБухгалтерию(ПараметрыВыгрузки);	
	Сообщение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Сообщение", "");
	
	Если Результат.Статус Тогда 
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 документов'; uk = 'Вивантажено %1 документів'"),
				МассивСтрокВыгрузки.Количество()));
	Иначе	
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Ошибка выгрузки документов'; uk = 'Помилка вивантаження документів'"));
	КонецЕсли;
	
	Если Не ПустаяСтрока(Сообщение) Тогда 
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти



#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаСервере
Процедура ОбновитьОтборСтрок()
	
	Элементы.ТаблицаПоступлениеТоваров.ОтборСтрок 		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ПоступлениеТоваров"));
	Элементы.ТаблицаПоступлениеТоваровТранзит.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ПоступлениеТоваровТранзит"));
	Элементы.ТаблицаВозвратПоставщику.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратПоставщику"));
	Элементы.ТаблицаВозвратПоставщикуТранзит.ОтборСтрок	 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратПоставщикуТранзит"));
	Элементы.ТаблицаРеализацияТоваров.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "РеализацияТоваров"));	
	Элементы.ТаблицаВозвратОтПокупателя.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратОтПокупателя"));	
	Элементы.ТаблицаКорректировкаДолга.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "КорректировкаДолга"));	
	Элементы.ТаблицаНалоговыеНакладные.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "НалоговыеНакладные"));	
	Элементы.ТаблицаПриложение2кНН.ОтборСтрок			 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "Приложение2кНН"));
	Элементы.ТаблицаЗКС.ОтборСтрок						 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ЗКС"));
	Элементы.ТаблицаКорректировкиЗКС.ОтборСтрок			 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "КорректировкиЗКС"));
	Элементы.ТаблицаКорректировкиЗКСАкциз.ОтборСтрок	 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "КорректировкиЗКСАкциз"));
	Элементы.ТаблицаРКО.ОтборСтрок						 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "РКОБанк"));

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФлажки(ТипДокумента, ЗначениеФлажка)
	
	мСтрок = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Не ЗначениеФлажка, ТипДокумента));
	
	Для Каждого Стр Из мСтрок Цикл 
		Стр.Выбор = ЗначениеФлажка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьФлажки(ТипДокумента)
	
	Количество = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Истина, ТипДокумента)).Количество();
	
	Стр = СписокДокументов.НайтиПоЗначению(ТипДокумента);
	Если Стр = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекСтраница = Элементы["Страница" + ТипДокумента];	
	ТекСтраница.Заголовок = Стр.Представление + ?(Количество > 0, " (" + Количество + ")", "");
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьюЛистов()

	МассивЛистов = Новый Массив;	
	Для Каждого Стр Из СписокДокументов Цикл 
		МассивЛистов.Добавить("Страница" + Стр.Значение);
	КонецЦикла;
	
	МассивВидимыхЛистов = Новый Массив;
	Для Каждого Стр Из СписокДокументов Цикл 
		Если Стр.Пометка Тогда 
			МассивВидимыхЛистов.Добавить("Страница" + Стр.Значение);
		КонецЕсли;
	КонецЦикла;
		
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивЛистов, МассивВидимыхЛистов);
КонецПроцедуры
	
&НаКлиенте
Функция ПрогрессСтрокой(Прогресс)
	
	Результат = "";
	Если Прогресс = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Процент = 0;
	Если Прогресс.Свойство("Процент", Процент) Тогда
		Результат = Строка(Процент) + "%";
	КонецЕсли;
	Текст = 0;
	Если Прогресс.Свойство("Текст", Текст) Тогда
		Если Не ПустаяСтрока(Результат) Тогда
			Результат = Результат + " (" + Текст + ")";
		Иначе
			Результат = Текст;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции


#КонецОбласти


#Область НеиспользуемыеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, ДопПараметры) Экспорт
	
	ВсеОк = Истина;
	
	ОповещениеТекст = "";
	ОповещениеПояснение = "";
	Ошибки = "";
	
	Если Операция = Неопределено Тогда 
		
		ВсеОк = Ложь;
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		                                                  
	Иначе
		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда 		
			
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				ВсеОк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус", Ложь);				
				Если Не ВсеОк И Результат.Свойство("Сообщение") Тогда 							
					Ошибки = Результат.Сообщение;
				КонецЕсли;
			КонецЕсли;
			
			Если ВсеОк Тогда 
				ОповещениеТекст 	= НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'");
				ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Сообщение", НСтр("ru = 'Успешно выполнена операция'; uk = 'Успішно виконана операція'"));
			Иначе
				ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");			
				ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");				
			КонецЕсли;
						
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 
			ВсеОк = Ложь;
			
			ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");			
			ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");				
			
			Ошибки = Операция.КраткоеПредставлениеОшибки;						
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ВсеОк Тогда 
		Картинка = БиблиотекаКартинок.Успешно32;	
	Иначе
		Картинка = БиблиотекаКартинок.Ошибка32;
	КонецЕсли;
		
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
	Если Не ВсеОк И Не ПустаяСтрока(Ошибки) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибки);
	КонецЕсли;
	
	   
КонецПроцедуры


#КонецОбласти


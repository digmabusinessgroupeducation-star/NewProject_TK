

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПравоДоступа("Чтение",Метаданные.Документы.ПоступлениеТоваров) Тогда
		СписокДокументов.Добавить("ПоступлениеТоваров", "Поступление товаров", Ложь);
		СписокДокументов.Добавить("ПоступлениеТоваровТранзит", "Поступление товаров (транзит)", Ложь);
	КонецЕсли;
	Если ПравоДоступа("Чтение",Метаданные.Документы.ВозвратПоставщику) Тогда
		СписокДокументов.Добавить("ВозвратПоставщику", "Возврат поставщику", Ложь);
		СписокДокументов.Добавить("ВозвратПоставщикуТранзит", "Возврат поставщику (транзит)", Ложь);
	КонецЕсли;
	Если ПравоДоступа("Чтение",Метаданные.Документы.РеализацияТоваров) Тогда
		СписокДокументов.Добавить("РеализацияТоваров", "Реализация товаров", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ВозвратОтПокупателя) Тогда
		СписокДокументов.Добавить("ВозвратОтПокупателя", "Возврат от покупателя", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_КорректировкаДолга) Тогда
		СписокДокументов.Добавить("КорректировкаДолга", "Корректировка долга", Ложь);	//Vov41k 24.10.2023 по просьбе Василенко
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_НалоговаяНакладная) Тогда
		СписокДокументов.Добавить("НалоговыеНакладные", "Налоговые накладные", Ложь);
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_Приложение2КНалоговойНакладной) Тогда
		СписокДокументов.Добавить("Приложение2кНН", "Приложение 2 к НН", Ложь);		
	КонецЕсли;			
	Если ПравоДоступа("Чтение",Метаданные.Документы.ТК_ЗакрытиеСменыЗКС) Тогда
		//СписокДокументов.Добавить("ЗКС_Документ", "Закрытие смены ЗКС", Ложь);
		//СписокДокументов.Добавить("КорректировкиЗКС", "Корректировки ЗКС (ежедневные)", Ложь);
		//СписокДокументов.Добавить("КорректировкиЗКСАкциз", "Корректировки ЗКС (Акциз)", Ложь);
		СписокДокументов.Добавить("ЗКС_Общий", "ЗКС (Общий)", Ложь);
	КонецЕсли;			
	
	Если ПравоДоступа("Чтение",Метаданные.Документы.РасходныйКассовыйОрдер) Тогда		
		СписокДокументов.Добавить("РКОБанк", "РКО (Банк)", Ложь);
	КонецЕсли;			

	Если ПравоДоступа("Чтение", Метаданные.Документы.КонсолидированныеОстаткиОрганизации) Тогда 
		СписокДокументов.Добавить("КонсПоступленияОрг", "Конс. поступления товаров", Ложь);
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.СчетНаОплату) Тогда 
		СписокДокументов.Добавить("СчетНаОплату", "Счет на оплату (маркетинг)", Ложь);
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваров) Тогда 
		СписокДокументов.Добавить("РеализацияТоваровМаркетинг", "Реализация товаров и услуг (маркетинг)", Ложь);
	КонецЕсли;
	
	ОбновитьОтборСтрок();	
	ЗагрузитьНастройкиОтбораПоУмолчанию();
	
	ПланОбменаБухгалтерия = ПолучитьПланОбменаБухгалтерия();
	
	ПоказатьПрогрессВыгрузки = Ложь;
	
	КешПредставлений = Новый Структура;
	Для Каждого Стр Из СписокДокументов Цикл 
		КешПредставлений.Вставить(Стр.Значение, Стр.Представление);
	КонецЦикла;
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.РегистрацияИзмененийДляОбменаДанными) Тогда 
		Элементы.ФормаПоказатьРегистрациюПланаОбмена.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостьЭлементов();
	УправлениеВидимостьюЛистов();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокДокументовПометкаПриИзменении(Элемент)
	
	УправлениеВидимостьюЛистов();
	
	ТекСтрока = Элементы.СписокДокументов.ТекущиеДанные;
	
	Если Не ТекСтрока.Пометка Тогда 
		МассивСтрок = ТаблицаДокументов.НайтиСтроки(Новый Структура("ТипДокумента", ТекСтрока.Значение));
		
		Для Каждого Стр Из МассивСтрок Цикл 
			ТаблицаДокументов.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПометкаПриИзменении()
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ПересчитатьФлажки(ТипДокумента);	
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьДокументы(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;		
	КонецЕсли;
	
	ПолучитьДокументыНаСервере();
	
	Если ПоказатьПрогрессВыгрузки Тогда 			
		ПоказатьПрогрессВыгрузки = Ложь;
		ОтключитьОбработчикОжидания("ОбновитьИнформациюПрогресса");	
	КонецЕсли;
	
	УправлениеВидимостьЭлементов();
	УправлениеВидимостьюЛистов();
	
	Для Каждого Стр Из СписокДокументов Цикл 
		Если Стр.Пометка Тогда 
			ПересчитатьФлажки(Стр.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДокументыНаСервере()
	
	ТаблицаДокументов.Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);	
	
	СтруктураОтборов = ПолучитьСтруктуруОтборов();
	МенеджерВТ = ПолучитьМенеджерВтПодразделения();
	
	Для Каждого ТипДокумента Из СписокДокументов Цикл 
		Если ТипДокумента.Пометка Тогда 
			ЗаполнитьДокументыПоТипу(ТипДокумента.Значение, СтруктураОтборов, МенеджерВТ);			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументы(Команда)
	
	КоличествоНаВыгрузку = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор", Истина)).Количество();
	
	Если КоличествоНаВыгрузку = 0 Тогда 
		Возврат;	
	КонецЕсли;
	
	ЗарегистрироватьВПланеОбмена();
	
	ПоказатьПрогрессВыгрузки = Истина;
	
	УправлениеВидимостьЭлементов();
	УправлениеВидимостьюЛистов();
	
	ПоказатьОповещениеПользователя(		
			НСтр("ru = 'Выполнено'; uk = 'Виконано'"),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Зарегистрировано %1 на выгрузку'; uk = 'Зареєстровано %1 на вивантаження'"),
			КоличествоНаВыгрузку),
		БиблиотекаКартинок.Успешно32,
		СтатусОповещенияПользователя.Информация);
		
	ПодключитьОбработчикОжидания("ОбновитьИнформациюПрогресса", 5, Ложь);			
		
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьВПланеОбмена()
	
	МассивСтрокВыгрузки = Новый Массив;
	
	Для Каждого ТипДокумента Из СписокДокументов Цикл 
		Если ТипДокумента.Пометка Тогда 
			МассивТипа = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Истина, ТипДокумента.Значение));
			
			Если МассивТипа.Количество() > 0 Тогда 
				ЗарегистрироватьДокументыПоТипу(ТипДокумента.Значение, МассивТипа);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрокВыгрузки, МассивТипа);
			КонецЕсли;
						
			//ТипДокумента.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаВыгрузки = ТаблицаДокументов.Выгрузить(МассивСтрокВыгрузки);		
	ТаблицаДокументов.Очистить();
	ТаблицаДокументов.Загрузить(ТаблицаВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиУстановить(Команда)
	
	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ЗаполнитьФлажки(ТипДокумента, Истина);	
	ПересчитатьФлажки(ТипДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиСнять(Команда)

	ТекСтраница = Элементы.Страницы.ТекущаяСтраница;
	Если ТекСтраница = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТипДокумента = СтрЗаменить(ТекСтраница.Имя, "Страница", "");
	
	ЗаполнитьФлажки(ТипДокумента, Ложь);	
	ПересчитатьФлажки(ТипДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРегистрациюПланаОбмена(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УзелОбмена", ПланОбменаБухгалтерия);
	ПараметрыФормы.Вставить("ЗапрещеноВыбиратьУзелОбмена", Истина);
	
	ФормаПланаОбмена = ПолучитьФорму("Обработка.РегистрацияИзмененийДляОбменаДанными.Форма.Форма", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);
	ФормаПланаОбмена.Открыть();
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДокументыПоТипу(ТипДокумента, СтруктураОтбора, МенеджерВТ)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);		
	Запрос.УстановитьПараметр("ТипДокумента", ТипДокумента);
	Запрос.УстановитьПараметр("Узел", ПланОбменаБухгалтерия);
	
	Если СтруктураОтбора.Свойство("ПараметрыЗапроса") Тогда 
		Для Каждого Стр Из СтруктураОтбора.ПараметрыЗапроса Цикл 
			Запрос.УстановитьПараметр(Стр.Ключ, Стр.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Отбор = Новый ТекстовыйДокумент;
	ЕстьКонтрагент = ТипДокумента <> "ЗКС_Общий";
	
	Если СтруктураОтбора.Свойство("Подразделение") Тогда 
		Отбор.ДобавитьСтроку("И " + СтруктураОтбора.Подразделение);
	КонецЕсли;
	
	Если СтруктураОтбора.Свойство("Организация") Тогда 
		Отбор.ДобавитьСтроку("И " + СтруктураОтбора.Организация);
	КонецЕсли;
	
	Если ЕстьКонтрагент И СтруктураОтбора.Свойство("Контрагент") Тогда 
		Отбор.ДобавитьСтроку("И " + СтруктураОтбора.Контрагент);
	КонецЕсли;
		
	ТипДок = ТипДокумента;
	ТекстЗапроса = "";
	
	Если ТипДокумента = "ПоступлениеТоваров" Тогда				
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)");
		
	ИначеЕсли ТипДокумента = "ПоступлениеТоваровТранзит" Тогда 
		
		ТипДок = "ПоступлениеТоваров";	
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		Отбор.ДобавитьСтроку("И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)");
		
	ИначеЕсли ТипДокумента = "ВозвратПоставщику" Тогда 
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)");
		
	ИначеЕсли ТипДокумента = "ВозвратПоставщикуТранзит" Тогда 
		
		ТипДок = "ВозвратПоставщику";	
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)");
		
	ИначеЕсли ТипДокумента = "РеализацияТоваров" Тогда 		
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		Отбор.ДобавитьСтроку("И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОбОказанииУслуг)");
		
	ИначеЕсли ТипДокумента = "ВозвратОтПокупателя" Тогда 		
		
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");
		
	ИначеЕсли ТипДокумента = "КорректировкаДолга" Тогда 
		
		ТипДок = "ТК_КорректировкаДолга";
		Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		
	ИначеЕсли ТипДокумента = "НалоговыеНакладные" Тогда 		
		
		ТипДок = "ТК_НалоговаяНакладная";
		//Отбор.ДобавитьСтроку("И Документ.РегламентированныйУчет");		
		
	ИначеЕсли ТипДокумента = "Приложение2кНН" Тогда 
		
		ТипДок = "ТК_Приложение2КНалоговойНакладной";
		
	ИначеЕсли ТипДокумента = "ЗКС_Общий" Тогда 
		
		//ТекстЗапроса = "ВЫБРАТЬ
		//               |	Документ.Ссылка КАК Ссылка,
		//               |	НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) КАК Дата,
		//               |	Документ.Организация КАК Организация,
		//               |	Документ.ПодразделениеКомпании КАК Подразделение
		//               |ПОМЕСТИТЬ втЗКС
		//               |ИЗ
		//               |	Документ.ТК_ЗакрытиеСменыЗКС КАК Документ
		//               |ГДЕ
		//               |	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		//               |	И Документ.Проведен
		//			   |	%3
		//               |;
		//               |
		//               |////////////////////////////////////////////////////////////////////////////////
		//               |ВЫБРАТЬ
		//               |	ВЫБОР
		//               |		КОГДА ТК_РегистрацияВыгрузкиЗКСИзменения.Организация ЕСТЬ NULL
		//               |			ТОГДА ИСТИНА
		//               |		ИНАЧЕ ЛОЖЬ
		//               |	КОНЕЦ КАК Флаг,
		//               |	ВЫБОР
		//               |		КОГДА ТК_ЗакрытиеСменыЗКСТовары.Возврат
		//               |				И ТК_ЗакрытиеСменыЗКСТовары.ДатаВозврата <> ДАТАВРЕМЯ(1, 1, 1)
		//               |			ТОГДА ТК_ЗакрытиеСменыЗКСТовары.ДатаВозврата
		//               |		ИНАЧЕ втЗКС.Дата
		//               |	КОНЕЦ КАК Дата,
		//               |	втЗКС.Организация КАК Организация,
		//               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего) КАК СуммаВсего,
		//               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаНДС) КАК СуммаНДС,
		//               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога
		//               |ИЗ
		//               |	втЗКС КАК втЗКС
		//               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК ТК_ЗакрытиеСменыЗКСТовары
		//               |		ПО втЗКС.Ссылка = ТК_ЗакрытиеСменыЗКСТовары.Ссылка
		//               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РегистрацияВыгрузкиЗКС.Изменения КАК ТК_РегистрацияВыгрузкиЗКСИзменения
		//               |		ПО втЗКС.Дата = ТК_РегистрацияВыгрузкиЗКСИзменения.Период
		//               |			И втЗКС.Организация = ТК_РегистрацияВыгрузкиЗКСИзменения.Организация
		//               |			И (ТК_РегистрацияВыгрузкиЗКСИзменения.Узел = &Узел)
		//               |
		//               |СГРУППИРОВАТЬ ПО
		//               |	втЗКС.Организация,
		//               |	ВЫБОР
		//               |		КОГДА ТК_ЗакрытиеСменыЗКСТовары.Возврат
		//               |				И ТК_ЗакрытиеСменыЗКСТовары.ДатаВозврата <> ДАТАВРЕМЯ(1, 1, 1)
		//               |			ТОГДА ТК_ЗакрытиеСменыЗКСТовары.ДатаВозврата
		//               |		ИНАЧЕ втЗКС.Дата
		//               |	КОНЕЦ,
		//               |	ВЫБОР
		//               |		КОГДА ТК_РегистрацияВыгрузкиЗКСИзменения.Организация ЕСТЬ NULL
		//               |			ТОГДА ИСТИНА
		//               |		ИНАЧЕ ЛОЖЬ
		//               |	КОНЕЦ
		//               |
		//               |ИМЕЮЩИЕ
		//               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего) > 0
		//               |
		//               |УПОРЯДОЧИТЬ ПО
		//               |	Дата,
		//               |	Организация";
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	Документ.Ссылка КАК Ссылка,
		               |	НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) КАК Дата,
		               |	Документ.Организация КАК Организация,
		               |	Документ.ПодразделениеКомпании КАК Подразделение
		               |ПОМЕСТИТЬ втЗКС
		               |ИЗ
		               |	Документ.ТК_ЗакрытиеСменыЗКС КАК Документ
		               |ГДЕ
		               |	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И Документ.Проведен
					   |	%3
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА ТК_РегистрацияВыгрузкиЗКСИзменения.Организация ЕСТЬ NULL
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК Флаг,
		               |	втЗКС.Дата КАК Дата,
		               |	втЗКС.Организация КАК Организация,
		               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего) КАК СуммаВсего,
		               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаНДС) КАК СуммаНДС,
		               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога
		               |ИЗ
		               |	втЗКС КАК втЗКС
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК ТК_ЗакрытиеСменыЗКСТовары
		               |		ПО втЗКС.Ссылка = ТК_ЗакрытиеСменыЗКСТовары.Ссылка
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РегистрацияВыгрузкиЗКС.Изменения КАК ТК_РегистрацияВыгрузкиЗКСИзменения
		               |		ПО втЗКС.Дата = ТК_РегистрацияВыгрузкиЗКСИзменения.Период
		               |			И втЗКС.Организация = ТК_РегистрацияВыгрузкиЗКСИзменения.Организация
		               |			И (ТК_РегистрацияВыгрузкиЗКСИзменения.Узел = &Узел)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втЗКС.Организация,
		               |	ВЫБОР
		               |		КОГДА ТК_РегистрацияВыгрузкиЗКСИзменения.Организация ЕСТЬ NULL
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ,
		               |	втЗКС.Дата
		               |
		               |ИМЕЮЩИЕ
		               |	СУММА(ТК_ЗакрытиеСменыЗКСТовары.СуммаВсего) > 0
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Дата,
		               |	Организация";

		
	ИначеЕсли ТипДокумента = "РКОБанк" Тогда
		
		ТипДок = "РасходныйКассовыйОрдер";
		Отбор.ДобавитьСтроку("И Документ.СтатьяДДС = &СтатьяДДС");
	  	Запрос.УстановитьПараметр("СтатьяДДС", Справочники.СтатьиДДС.НайтиПоКоду("DG000001"));
		
	ИначеЕсли ТипДокумента = "КонсПоступленияОрг" Тогда 
		
		ТипДок = "КонсолидированныеОстаткиОрганизации";
		
		Отбор.ДобавитьСтроку("И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.КонсолидированныеПоступленияОрганизации)");
		Отбор.ДобавитьСтроку("И Документ.ДоговорВзаиморасчетов.ВидДоговора <> ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.КомиссияПоставка)");
		Отбор.ДобавитьСтроку("И Документ.ДоговорВзаиморасчетов.ВидДоговора <> ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.БухгалтерскаяКомиссияПоставка)");
		
	ИначеЕсли ТипДокумента = "СчетНаОплату" Тогда
		ТипДок = "СчетНаОплату";
		Отбор.ДобавитьСтроку("И Документ.ДоговорВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.Маркетинг)");
	ИначеЕсли ТипДокумента = "РеализацияТоваровМаркетинг" Тогда
		ТипДок = "РеализацияТоваров";
		Отбор.ДобавитьСтроку("И Документ.ДоговорВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.Маркетинг)");
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА ДокументИзменения.Ссылка ЕСТЬ NULL
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК Флаг,
					   |	%2
		               |	Документ.Ссылка КАК Ссылка,
		               |	Документ.Дата КАК Дата,
		               |	Документ.Организация КАК Организация,
		               |	Документ.ПодразделениеКомпании КАК Подразделение,
		               |	Документ.СуммаДокумента КАК СуммаВсего
		               |ИЗ
		               |	Документ.%1 КАК Документ
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%1.Изменения КАК ДокументИзменения
		               |		ПО Документ.Ссылка = ДокументИзменения.Ссылка
		               |			И (ДокументИзменения.Узел = &Узел)
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазрешенныеПодразделения КАК РазрешенныеПодразделения
		               |		ПО Документ.ПодразделениеКомпании = РазрешенныеПодразделения.Подразделение
		               |ГДЕ
		               |	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И Документ.Проведен
					   |    %3
					   |
		               |УПОРЯДОЧИТЬ ПО
		               |	Дата";

		
		//ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		//               |	ВЫБОР
		//               |		КОГДА ДокументИзменения.Ссылка ЕСТЬ NULL
		//               |			ТОГДА ИСТИНА
		//               |		ИНАЧЕ ЛОЖЬ
		//               |	КОНЕЦ КАК Флаг,
		//			   |	%2
		//               |	Документ.Ссылка КАК Ссылка,
		//               |	Документ.Дата КАК Дата,
		//               |	Документ.Организация КАК Организация,
		//               |	Документ.ПодразделениеКомпании КАК Подразделение,
		//               |	Документ.СуммаДокумента КАК СуммаВсего
		//               |ИЗ
		//               |	Документ.%1 КАК Документ
		//               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%1.Изменения КАК ДокументИзменения
		//               |		ПО Документ.Ссылка = ДокументИзменения.Ссылка
		//               |			И (ДокументИзменения.Узел = &Узел)
		//               |ГДЕ
		//               |	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		//               |	И Документ.Проведен
		//			   |    %3
		//               |
		//               |УПОРЯДОЧИТЬ ПО
		//               |	Дата";
	КонецЕсли;
	
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстЗапроса, 
						ТипДок, 
						?(ЕстьКонтрагент, "Документ.Контрагент КАК Контрагент,", ""),
						Отбор.ПолучитьТекст());
						
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);		
		
		НоваяСтрока.ТипДокумента = ТипДокумента;
		НоваяСтрока.Выбор = Выборка.Флаг;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруОтборов()
	
	СтруктураОтборов = Новый Структура;
	ПараметрыЗапроса = Новый Структура;
	
	Для Каждого Отбор Из КомпоновщикНастроекОтборы.Настройки.Отбор.Элементы Цикл 
		ИмяОтбора = Строка(Отбор.ЛевоеЗначение);		
		Если Не Отбор.Использование ИЛИ ПустаяСтрока(ИмяОтбора) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ИмяОтбора = "Организация" ИЛИ ИмяОтбора = "Контрагент" Тогда 
			ИмяРеквизита = ИмяОтбора;	
		ИначеЕсли ИмяОтбора = "Подразделение" Тогда 			
			ИмяРеквизита = "ПодразделениеКомпании";
		Иначе
			Продолжить;
		КонецЕсли;
		
		ОтборИсключение = Ложь;
		Если Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда 
			текВидСравнения = "=";
		ИначеЕсли Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда 
			текВидСравнения = "=";
			ОтборИсключение = Истина;
		ИначеЕсли Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда 
			текВидСравнения = "В";
		ИначеЕсли Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке Тогда 
			текВидСравнения = "В";
			ОтборИсключение = Истина;
		ИначеЕсли Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии 
			ИЛИ Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда 
			
			текВидСравнения = "В ИЕРАРХИИ";
		ИначеЕсли Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии 
			ИЛИ Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда 
			
			текВидСравнения = "В ИЕРАРХИИ";
			ОтборИсключение = Истина;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Вид сравнения ""%1"" не поддерживается для поля ""%2""'; uk = 'Вид порівняння ""%1"" не підтримується для поля ""%2""'"),
					Строка(Отбор.ВидСравнения),
					ИмяОтбора));
			Продолжить;
		КонецЕсли;
		
		СтруктураОтборов.Вставить(ИмяОтбора, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												"%1Документ.%2 %3 (&%4)",
												?(ОтборИсключение, "НЕ ", ""),
												ИмяРеквизита,
												текВидСравнения,
												ИмяОтбора));
																								
		ПараметрыЗапроса.Вставить(ИмяОтбора, Отбор.ПравоеЗначение);																							
	КонецЦикла;
	
	Если ПараметрыЗапроса.Количество() > 0 Тогда 
		СтруктураОтборов.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	КонецЕсли;
	
	Возврат СтруктураОтборов;
КонецФункции

&НаСервере
Процедура ЗарегистрироватьДокументыПоТипу(ТипДокумента, тзДокументы)
		
	Для Каждого Стр Из тзДокументы Цикл 
		Если ТипДокумента = "ЗКС_Общий" Тогда 						
			НаборЗаписей = РегистрыСведений.ТК_РегистрацияВыгрузкиЗКС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Период.Установить(Стр.Дата);
			НаборЗаписей.Отбор.Организация.Установить(Стр.Организация);			
			ЗаписьНабора = НаборЗаписей.Добавить();			 
			
			ПланыОбмена.ЗарегистрироватьИзменения(ПланОбменаБухгалтерия, НаборЗаписей);		
		Иначе
			ПланыОбмена.ЗарегистрироватьИзменения(ПланОбменаБухгалтерия, Стр.Ссылка);		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеВидимостьюЛистов()
	
	Если Не Элементы.Страницы.Видимость Тогда 
		Возврат;
	КонецЕсли;
	
	МассивЛистов = Новый Массив;	
	МассивВидимыхЛистов = Новый Массив;
	
	Для Каждого Стр Из СписокДокументов Цикл 
		МассивЛистов.Добавить("Страница" + Стр.Значение);
		Если Стр.Пометка Тогда 
			МассивВидимыхЛистов.Добавить("Страница" + Стр.Значение);
		КонецЕсли;
	КонецЦикла;
		
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивЛистов, МассивВидимыхЛистов);
	
	Если МассивВидимыхЛистов.Количество() > 0 Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы[МассивВидимыхЛистов[0]];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьЭлементов()
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("Страницы");
	МассивЭлементов.Добавить("ГруппаПрогрессВыгрузки");
	МассивЭлементов.Добавить("ФормаВыгрузитьДокументы");
	
	МассивВидимыхЭлементов = Новый Массив;
	
	Если ПоказатьПрогрессВыгрузки Тогда 
		МассивВидимыхЭлементов.Добавить("ГруппаПрогрессВыгрузки");		
		Элементы.ГруппаПрогрессВыгрузки.Заголовок = НСтр("ru = 'Выгружено 0%'; uk = 'Вивантажено 0%'");
	Иначе
		МассивВидимыхЭлементов.Добавить("Страницы");
		МассивВидимыхЭлементов.Добавить("ФормаВыгрузитьДокументы");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивЭлементов, МассивВидимыхЭлементов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборСтрок()
	
	Элементы.ТаблицаПоступлениеТоваров.ОтборСтрок 		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ПоступлениеТоваров"));
	Элементы.ТаблицаПоступлениеТоваровТранзит.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ПоступлениеТоваровТранзит"));
	Элементы.ТаблицаВозвратПоставщику.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратПоставщику"));
	Элементы.ТаблицаВозвратПоставщикуТранзит.ОтборСтрок	 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратПоставщикуТранзит"));
	Элементы.ТаблицаРеализацияТоваров.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "РеализацияТоваров"));	
	Элементы.ТаблицаВозвратОтПокупателя.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ВозвратОтПокупателя"));	
	Элементы.ТаблицаКорректировкаДолга.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "КорректировкаДолга"));	
	Элементы.ТаблицаНалоговыеНакладные.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "НалоговыеНакладные"));	
	Элементы.ТаблицаПриложение2кНН.ОтборСтрок			 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "Приложение2кНН"));
	Элементы.ТаблицаРКО.ОтборСтрок						 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "РКОБанк"));
	Элементы.ТаблицаЗКС_Общий.ОтборСтрок				 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "ЗКС_Общий"));
	Элементы.ТаблицаКонсПоступленияОрг.ОтборСтрок		 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "КонсПоступленияОрг"));
	Элементы.ТаблицаСчетНаОплату.ОтборСтрок				 = Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "СчетНаОплату"));
	Элементы.ТаблицаРеализацияТоваровМаркетинг.ОтборСтрок= Новый ФиксированнаяСтруктура(Новый Структура("ТипДокумента", "РеализацияТоваров"));

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОтбораПоУмолчанию()

	СхемаКомпоновкиДанных = Обработки.ВыгрузкаДокументовВБухгалтерию.ПолучитьМакет("ПоляОтбора");
	КомпоновщикНастроекОтборы.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));
	КомпоновщикНастроекОтборы.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);		
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПланОбменаБухгалтерия()
	
	ПланОбмена = ПланыОбмена.ОбменБухгалтерия.НайтиПоКоду("BUH");
	
	Если ПланОбмена.Пустая() Тогда 
		новПланОбмена = ПланыОбмена.ОбменБухгалтерия.СоздатьУзел();
		новПланОбмена.код = "BUH";
		новПланОбмена.Наименование = "Обмен с базой Бухгалтерия";
		
		новПланОбмена.Записать();
		
		ПланОбмена = новПланОбмена.Ссылка;
	КонецЕсли;
	
	Возврат ПланОбмена;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИмяМетаданныхОбъекта(ТипДокумента) 
	
	Если ТипДокумента = "ПоступлениеТоваровТранзит" Тогда 
		
		ТипДок = "ПоступлениеТоваров";	
		
	ИначеЕсли ТипДокумента = "ВозвратПоставщикуТранзит" Тогда 
		
		ТипДок = "ВозвратПоставщику";	
				
	ИначеЕсли ТипДокумента = "КорректировкаДолга" Тогда 
		
		ТипДок = "ТК_КорректировкаДолга";
		
	ИначеЕсли ТипДокумента = "НалоговыеНакладные" Тогда 		
		
		ТипДок = "ТК_НалоговаяНакладная";
		
	ИначеЕсли ТипДокумента = "Приложение2кНН" Тогда 
		
		ТипДок = "ТК_Приложение2КНалоговойНакладной";
				
	ИначеЕсли ТипДокумента = "РКОБанк" Тогда
		
		ТипДок = "РасходныйКассовыйОрдер";
		
	ИначеЕсли ТипДокумента = "КонсПоступленияОрг" Тогда 
		
		ТипДок = "КонсолидированныеОстаткиОрганизации";
		
	ИначеЕсли ТипДокумента = "РеализацияТоваровМаркетинг" Тогда 
		
		ТипДок = "РеализацияТоваров";
		
	Иначе
		
		ТипДок = ТипДокумента;
		
	КонецЕсли;

	Возврат ТипДок;	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьФлажки(ТипДокумента, ЗначениеФлажка)
	
	мСтрок = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Не ЗначениеФлажка, ТипДокумента));
	
	Для Каждого Стр Из мСтрок Цикл 
		Стр.Выбор = ЗначениеФлажка;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьФлажки(ТипДокумента)
	
	Количество = ТаблицаДокументов.НайтиСтроки(Новый Структура("Выбор, ТипДокумента", Истина, ТипДокумента)).Количество();
	
	Стр = СписокДокументов.НайтиПоЗначению(ТипДокумента);
	Если Стр = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекСтраница = Элементы["Страница" + ТипДокумента];	
	ТекСтраница.Заголовок = Стр.Представление + ?(Количество > 0, " (" + Количество + ")", "");
	
КонецПроцедуры

//Отоброжение прогресса
&НаКлиенте
Процедура ОбновитьИнформациюПрогресса() Экспорт 
	
	Если ПоказатьПрогрессВыгрузки И Элементы.ГруппаПрогрессВыгрузки.Видимость Тогда 
		Результат = ОбновитьИнформациюПрогрессаНаСервере();
		
		ВыгрузкаЗавершена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ВыгрузкаЗавершена", Истина);
		Всего = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Всего", 0);
		
		Если ВыгрузкаЗавершена Тогда 			
			ОтключитьОбработчикОжидания("ОбновитьИнформациюПрогресса");
			
			ТаблицаДокументов.Очистить();
			
			Для Каждого Стр Из СписокДокументов Цикл 
				Если Стр.Пометка Тогда 
					Стр.Пометка = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			ПоказатьПрогрессВыгрузки = Ложь;						
			
			УправлениеВидимостьЭлементов();
			УправлениеВидимостьюЛистов();
			
			Если Всего > 0 Тогда 
				ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Выгружено %1 документов'; uk = 'Вивантажено %1 документів'"),
									Всего);
									
				ПоказатьОповещениеПользователя("Выгрузка в Бухгалтерию",, ТекстОповещения, БиблиотекаКартинок.Информация32);
			КонецЕсли;				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьИнформациюПрогрессаНаСервере()
	
	Результат = Новый Структура;
	
	ТаблицаВыгрузки = ТаблицаДокументов.Выгрузить(Новый Структура("Выбор", Истина), "Дата,Организация,Ссылка,ТипДокумента");
	
	СписокТиповДокументов = ТаблицаВыгрузки.Скопировать(, "ТипДокумента");
	СписокТиповДокументов.Свернуть("ТипДокумента");
	
	Если СписокТиповДокументов.Количество() = 0 Тогда 
		Результат.Вставить("ВыгрузкаЗавершена", Истина);
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаВыгрузки);
	Запрос.УстановитьПараметр("Узел", ПланОбменаБухгалтерия);
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТаблицаДокументов.Дата КАК Дата,
	               |	ТаблицаДокументов.Организация КАК Организация,
	               |	ТаблицаДокументов.Ссылка КАК Ссылка,
	               |	ТаблицаДокументов.ТипДокумента КАК ТипДокумента
	               |ПОМЕСТИТЬ втДокументы
	               |ИЗ
	               |	&ТаблицаДокументов КАК ТаблицаДокументов
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
				   |";
	
	ТекстИзменения = "";
	
	Ит = 0;
	Для Каждого Стр Из СписокТиповДокументов Цикл 
		Ит = Ит + 1;
		
		текТипДокумента = Стр.ТипДокумента;
		ИмяПараметра = "ТипДокумента" + Ит;
		
		Если текТипДокумента = "ЗКС_Общий" Тогда
			текЗапрос = "ВЫБРАТЬ
		               |	втДокументы.ТипДокумента КАК ТипДокумента,
		               |	КОЛИЧЕСТВО(втДокументы.Организация) КАК НаВыгрузку,
		               |	КОЛИЧЕСТВО(ТК_РегистрацияВыгрузкиЗКСИзменения.Организация) КАК Остаток
		               |ИЗ
		               |	втДокументы КАК втДокументы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РегистрацияВыгрузкиЗКС.Изменения КАК ТК_РегистрацияВыгрузкиЗКСИзменения
		               |		ПО втДокументы.Дата = ТК_РегистрацияВыгрузкиЗКСИзменения.Период
		               |			И втДокументы.Организация = ТК_РегистрацияВыгрузкиЗКСИзменения.Организация
		               |			И (ТК_РегистрацияВыгрузкиЗКСИзменения.Узел = &Узел)
					   |ГДЕ
		               |	втДокументы.ТипДокумента = &%2
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втДокументы.ТипДокумента";	
		Иначе
			текЗапрос = "ВЫБРАТЬ
		               |	втДокументы.ТипДокумента КАК ТипДокумента,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втДокументы.Ссылка) КАК НаВыгрузку,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументИзменения.Ссылка) КАК Остаток
		               |ИЗ
		               |	втДокументы КАК втДокументы
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%1.Изменения КАК ДокументИзменения
		               |		ПО втДокументы.Ссылка = ДокументИзменения.Ссылка
		               |			И ДокументИзменения.Узел = &Узел
		               |ГДЕ				
		               |   втДокументы.ТипДокумента = &%2
					   |
		               |СГРУППИРОВАТЬ ПО
		               |	втДокументы.ТипДокумента
					   |";
			
		КонецЕсли;
	
		Если Не ПустаяСтрока(ТекстИзменения) Тогда 
			ТекстИзменения = ТекстИзменения + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
		КонецЕсли;		
				
		
		ТекстИзменения = ТекстИзменения + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											текЗапрос,
											ПолучитьИмяМетаданныхОбъекта(текТипДокумента),
											ИмяПараметра);
											
		Запрос.УстановитьПараметр(ИмяПараметра, текТипДокумента);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса + ТекстИзменения;
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	ВсегоНаВыгрузку = тзРезультат.Итог("НаВыгрузку");
	ВсегоОстаток 	= тзРезультат.Итог("Остаток");
	
	Элементы.НадписьПрогрессВыгрузки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
													НСтр("ru = 'Выгружено %1 из %2 (%3%)'; uk = 'Вивантажено %1 из %2 (%3%)'"),
													ВсегоНаВыгрузку - ВсегоОстаток,
													ВсегоНаВыгрузку,
													Цел((ВсегоНаВыгрузку - ВсегоОстаток)/ВсегоНаВыгрузку*100));
													
	ВыгрузкаЗавершена = ВсегоОстаток = 0;													
	
	Результат.Вставить("ВыгрузкаЗавершена", ВсегоОстаток = 0);
	Результат.Вставить("Всего", ВсегоНаВыгрузку);
	
	Возврат Результат;
	
	//ТекстЗапроса = "ВЫБРАТЬ
	//               |	ТаблицаДокументов.Дата КАК Дата,
	//               |	ТаблицаДокументов.Организация КАК Организация,
	//               |	ТаблицаДокументов.Ссылка КАК Ссылка,
	//               |	ТаблицаДокументов.ТипДокумента КАК ТипДокумента
	//               |ПОМЕСТИТЬ втДокументы
	//               |ИЗ
	//               |	ТаблицаДокументов КАК ТаблицаДокументов
	//               |
	//               |ИНДЕКСИРОВАТЬ ПО
	//               |	Ссылка
	//               |;
	//               |
	//               |////////////////////////////////////////////////////////////////////////////////
	//               |ВЫБРАТЬ
	//               |	втДокументы.ТипДокумента КАК ТипДокумента,
	//               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втДокументы.Ссылка) КАК НаВыгрузку,
	//               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументИзменения.Ссылка) КАК Остаток
	//               |ИЗ
	//               |	втДокументы КАК втДокументы
	//               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров.Изменения КАК ДокументИзменения
	//               |		ПО втДокументы.Ссылка = ДокументИзменения.Ссылка
	//               |			И (ДокументИзменения.Узел = &Узел
	//               |				И втДокументы.ТипДокумента = &ТипДокумента)
	//               |
	//               |СГРУППИРОВАТЬ ПО
	//               |	втДокументы.ТипДокумента
	//               |
	//               |ОБЪЕДИНИТЬ ВСЕ
	//               |
	//               |ВЫБРАТЬ
	//               |	втДокументы.ТипДокумента,
	//               |	КОЛИЧЕСТВО(втДокументы.Организация),
	//               |	КОЛИЧЕСТВО(ТК_РегистрацияВыгрузкиЗКСИзменения.Организация)
	//               |ИЗ
	//               |	втДокументы КАК втДокументы
	//               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РегистрацияВыгрузкиЗКС.Изменения КАК ТК_РегистрацияВыгрузкиЗКСИзменения
	//               |		ПО втДокументы.Дата = ТК_РегистрацияВыгрузкиЗКСИзменения.Период
	//               |			И втДокументы.Организация = ТК_РегистрацияВыгрузкиЗКСИзменения.Организация
	//               |			И (ТК_РегистрацияВыгрузкиЗКСИзменения.Узел = &Узел)
	//               |			И (втДокументы.ТипДокумента = &ТипДокумента)
	//               |
	//               |СГРУППИРОВАТЬ ПО
	//               |	втДокументы.ТипДокумента";
	

	
КонецФункции

&НаСервере
Функция ПолучитьМенеджерВтПодразделения()
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПодразделенияКомпании.Ссылка КАК Подразделение
	               |ПОМЕСТИТЬ РазрешенныеПодразделения
	               |ИЗ
	               |	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании";
	
	Запрос.Выполнить();

	Возврат МенеджерВТ;		
КонецФункции


#КонецОбласти



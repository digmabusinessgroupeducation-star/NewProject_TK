
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Перем НалоговыеСтавки;
Перем ЗагруженныеОбъекты;	


#Область ПрограммныйИнтерфейс

Процедура ВыполнитьЗагрузку(Загруженные = Ложь, НаДату = Ложь, ЗагружатьЗКС = Истина, ЗагружатьПРРО = Истина) Экспорт
	
	//Загрузка ЗКС ТС Аттика
	Если ЗагружатьЗКС Тогда 
		ЗагрузитьДокументыЗКС(Загруженные, НаДату);
	КонецЕсли;	
	
	//Загрузка ЗКС из ПРРО
	Если ЗагружатьПРРО Тогда 
		ПодразделенияПРРО = ПолучитьМассивПодразделенияСПРРО();	
		
		Для Каждого Подразделение Из ПодразделенияПРРО Цикл 
			ЗагрузитьДокументыЗКС(Загруженные, НаДату, Подразделение);	
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

Процедура ВыполнитьВыгрузку(Параметры)Экспорт
	
	ЗаписыватьРезультаты = Параметры.Свойство("СтруктруаЗаписиРезультатаВыполнения");
	ВыгружатьОстатки = Неопределено;
    Параметры.Свойство("ВыгружатьОстатки",ВыгружатьОстатки);
	
	Если ВыгружатьОстатки = Истина Тогда
		ВыгрузитьОстатки();
	КонецЕсли;
	
	Если Параметры.Свойство("ВыгружатьПродажи") Тогда
		ВыгрузитьПродажи(Параметры.ВыгружатьПродажи.ДатаПродаж);
	КонецЕсли;
	

	
КонецПроцедуры

Функция ЗагрузитьЗКС(ДатаПродаж,Смена,СкладСсылка,SAREAID,WORKDAYID,SYSTEMID,ZREPNUM,ZREPFPSN) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОК",Ложь);
	Параметры.Вставить("Ошибки","");
	
	
	Если ЗагруженныеОбъекты = Неопределено Тогда
		ЗагруженныеОбъекты = Новый Структура();
	КонецЕсли;
	
	//Если НалоговыеСтавки = Неопределено Тогда
	//	НалоговыеСтавки = Новый Массив;
	//	Запрос = Новый Запрос;
	//	Запрос.Текст = 
	//	"ВЫБРАТЬ
	//	|	СтавкиНДС.Ссылка,
	//	|	СтавкиНДС.Порядок КАК Порядок
	//	|ИЗ
	//	|	Справочник.СтавкиНДС КАК СтавкиНДС
	//	|
	//	|УПОРЯДОЧИТЬ ПО
	//	|	Порядок";
	//	
	//	РезультатЗапроса = Запрос.Выполнить();
	//	
	//	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	//	
	//	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	//		НалоговыеСтавки.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	//	КонецЦикла;
	//КонецЕсли;
	
	//Vov41k 13.07.2022
	Если НалоговыеСтавки = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДС.Ссылка КАК СтавкаНДС,
		|	СтавкиНДС.Ставка КАК Ставка,
		|	СтавкиНДС.СоставнойНалог КАК СоставнойНалог,
		|	СтавкиНДС.Порядок КАК Порядок
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		НалоговыеСтавки = РезультатЗапроса.Выгрузить();	
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_ЗакрытиеСменыЗКС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТК_ЗакрытиеСменыЗКС КАК ТК_ЗакрытиеСменыЗКС
	|ГДЕ
	|	ТК_ЗакрытиеСменыЗКС.КассоваяСмена = &КассоваяСмена
	|	И ТК_ЗакрытиеСменыЗКС.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Запрос.УстановитьПараметр("КассоваяСмена", Смена);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДатаПродаж));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДатаПродаж));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ДокОбъект = Документы.ТК_ЗакрытиеСменыЗКС.СоздатьДокумент();
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		ДокОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ДокОбъект.ПометкаУдаления = Ложь;
		ДокОбъект.Товары.Очистить();
		ДокОбъект.ОплатаПлатежнымиКартами.Очистить();
		
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ДокОбъект,Смена,"Организация,ПодразделениеКомпании,ТорговаяПлощадка");
	ДокОбъект.ХозОперация = Справочники.ХозОперации.ЗакрытиеСменыОрганизации;
	ДокОбъект.Автор =  Пользователи.ТекущийПользователь();
	ДокОбъект.ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ДокОбъект.КурсДокумента       = 1;
	ДокОбъект.РегламентированныйУчет = Истина;
	ДокОбъект.Дата = КонецДня(ДатаПродаж);
	ДокОбъект.СкладКомпании  = СкладСсылка;
	ДокОбъект.ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладСсылка);
	ДокОбъект.КассоваяСмена = Смена;	
	ДокОбъект.СписыватьОстатки = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(ДокОбъект.Дата);
	
	//Товары зет отчета
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	V_Продажи.ARTID КАК ARTID,
	|	V_Продажи.CHARGUID КАК CHARGUID,
	|	V_Продажи.SALESREFUND КАК Возврат,
	|	V_Продажи.PACKCOUNT / 1000 КАК Коэффициент,
	|	V_Продажи.SALESPRICE / 100 КАК Цена,
	|	ВЫБОР
	|		КОГДА V_Продажи.SALESREFUND
	|			ТОГДА -V_Продажи.SALESCOUNT
	|		ИНАЧЕ V_Продажи.SALESCOUNT
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА V_Продажи.SALESREFUND
	|			ТОГДА -V_Продажи.salesdisc / 100
	|		ИНАЧЕ V_Продажи.salesdisc / 100
	|	КОНЕЦ КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА V_Продажи.SALESREFUND
	|			ТОГДА -V_Продажи.salessum / 100
	|		ИНАЧЕ V_Продажи.salessum / 100
	|	КОНЕЦ КАК СуммаВсего,
	|	V_Продажи.TAXGRP КАК TAXGRP,
	|	ВЫБОР
	|		КОГДА V_Продажи.SALESREFUND
	|			ТОГДА -V_Продажи.salestax / 100
	|		ИНАЧЕ V_Продажи.salestax / 100
	|	КОНЕЦ КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА V_Продажи.SALESREFUND
	|			ТОГДА -V_Продажи.salestaxcharge / 100
	|		ИНАЧЕ V_Продажи.salestaxcharge / 100
	|	КОНЕЦ КАК СуммаАкцизногоНалога,
	|	ВЫБОР
	|		КОГДА V_Продажи.isBank = 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОплатаБезнал,
	|	NULL КАК ДатаВозврата
	|ИЗ
	|	ВнешнийИсточникДанных.zFOC.Таблица.dbo_vSALESD КАК V_Продажи
	|ГДЕ
	|	V_Продажи.SAREAID = &SAREAID
	|	И V_Продажи.SYSTEMID = &SYSTEMID
	|	И V_Продажи.WORKDAYID = &WORKDAYID
	|	И V_Продажи.OPDATE = &WORKDAYEND1
	|	И V_Продажи.ZREPNUM = &ZREPNUM
	|	И НЕ V_Продажи.SALESREFUND
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	dbo_vREFUNDSD.ARTID,
	|	dbo_vREFUNDSD.CHARGUID,
	|	dbo_vREFUNDSD.SALESREFUND,
	|	dbo_vREFUNDSD.PACKCOUNT / 1000,
	|	dbo_vREFUNDSD.SALESPRICE / 100,
	|	-dbo_vREFUNDSD.SALESCOUNT,
	|	-dbo_vREFUNDSD.salesdisc / 100,
	|	-dbo_vREFUNDSD.salessum / 100,
	|	dbo_vREFUNDSD.TAXGRP,
	|	-dbo_vREFUNDSD.salestax / 100,
	|	-dbo_vREFUNDSD.salestaxcharge / 100,
	|	ВЫБОР
	|		КОГДА dbo_vREFUNDSD.isBank = 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	dbo_vREFUNDSD.refundsdate
	|ИЗ
	|	ВнешнийИсточникДанных.zFOC.Таблица.dbo_vREFUNDSD КАК dbo_vREFUNDSD
	|ГДЕ
	|	dbo_vREFUNDSD.SAREAID = &SAREAID
	|	И dbo_vREFUNDSD.SYSTEMID = &SYSTEMID
	|	И dbo_vREFUNDSD.WORKDAYID = &WORKDAYID
	|	И dbo_vREFUNDSD.OPDATE = &WORKDAYEND1
	|	И dbo_vREFUNDSD.ZREPNUM = &ZREPNUM";
	
	Запрос.УстановитьПараметр("SAREAID", SAREAID);
	Запрос.УстановитьПараметр("SYSTEMID", SYSTEMID);
	Запрос.УстановитьПараметр("WORKDAYID", WORKDAYID);
	Запрос.УстановитьПараметр("ZREPNUM", ZREPNUM);
	Запрос.УстановитьПараметр("WORKDAYEND1", НачалоДня(ДатаПродаж));
	
	
	
	РезультатЗапросаПоТоварам =  Запрос.Выполнить();
	
	
	тзПродажи = РезультатЗапросаПоТоварам.Выгрузить();
	тзПродажи.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	тзПродажи.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	тзПродажи.Колонки.Добавить("ЕдиницаИзмерения",Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	тзПродажи.Колонки.Добавить("КоличествоБазовое",Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15,3)));
	тзПродажи.Колонки.Добавить("СтавкаНДС",Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	тзПродажи.Колонки.Добавить("Ставка",Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(2,0)));
	//тзПродажи.Колонки.Добавить("СуммаНДС",Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15,2)));
	тзПродажи.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15,2)));
	
	
	ОбработатьТабличнуюЧасть("Товары",тзПродажи,ЗагруженныеОбъекты);
	тчТовары = ДокОбъект.Товары;
	
	тчТовары.Загрузить(тзПродажи);
	//тчТовары.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,Коэффициент,Цена,СтавкаНДС,Возврат","Количество,КоличествоБазовое,Сумма,СуммаНДС,СуммаВсего,СуммаСкидки,СуммаАкцизногоНалога");
	
	Для Каждого СтрокаТаблицы Из тчТовары Цикл 
		СтрокаТаблицы.ПроцентСкидки=?(СтрокаТаблицы.СуммаВсего=0,0,(СтрокаТаблицы.СуммаСкидки*100)/СтрокаТаблицы.СуммаВсего);
	КонецЦикла;
	//
	//Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЭквайринговыеТерминалы.Ссылка КАК Терминал
	|ИЗ
	|	РегистрСведений.ТК_СчетаЭкваринга.СрезПоследних(&НаДату, ТорговаяПлощадка = &Объект) КАК ТК_СчетаЭкварингаСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
	|		ПО ТК_СчетаЭкварингаСрезПоследних.Контрагент = ЭквайринговыеТерминалы.Эквайер
	|			И ТК_СчетаЭкварингаСрезПоследних.БанковскийСчет = ЭквайринговыеТерминалы.БанковскийСчет";
	
	Запрос.УстановитьПараметр("Объект", ДокОбъект.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("НаДату", ДокОбъект.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Терминал = ТК_СправочникиПовтИсп.ТерминалПриват();
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Терминал = Выборка.Терминал;
	КонецЕсли;
	
	Запрос.Текст =  "ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА V_ОплатыТерминал.refund 
	|				ТОГДА -V_ОплатыТерминал.salessum / 100
	|			ИНАЧЕ V_ОплатыТерминал.salessum / 100
	|		КОНЕЦ) КАК Сумма,
	|	V_ОплатыТерминал.Bank КАК Bank
	|ИЗ
	|	ВнешнийИсточникДанных.zFOC.Таблица.dbo_vBankSalesD КАК V_ОплатыТерминал
	|ГДЕ
	|	V_ОплатыТерминал.SAREAID = &SAREAID
	|	И V_ОплатыТерминал.SYSTEMID = &SYSTEMID
	|	И V_ОплатыТерминал.WORKDAYID = &WORKDAYID
	|	И V_ОплатыТерминал.OPDATE = &WORKDAYEND1
	|	И V_ОплатыТерминал.zrepnum = &ZREPNUM
	|
	|СГРУППИРОВАТЬ ПО
	|	V_ОплатыТерминал.Bank";
	
	РезультатЗапросаТерминал = Запрос.Выполнить();
	Выборка = РезультатЗапросаТерминал.Выбрать();
	тчПлатежныеКарты = ДокОбъект.ОплатаПлатежнымиКартами;
	
	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = тчПлатежныеКарты.Добавить();
		НоваяСтрока.КассаККМ = Смена.КассаККМ;
		НоваяСтрока.Сумма    = Выборка.Сумма;
		НоваяСтрока.ЭквайринговыйТерминал    =  Терминал;
	КонецЦикла;	
	
	
	Попытка 
		
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ВнешниеИсточникиДанных.zFOC.dbo_MarkSalesDownloaded(ДатаПродаж,ZREPFPSN,ZREPNUM);
		
		//	ВнешниеИсточникиДанных.zFOC.dbo_MarkSalesDownloaded("'"+Формат(ДатаПродаж,"ДФ=yyyyMMdd")+"'","'"+ZREPFPSN+"'",Формат(ZREPNUM,"ЧГ="));
		Параметры.Вставить("ОК",Истина);
	Исключение
		Параметры.Вставить("Ошибки",ОписаниеОшибки());
		
	КонецПопытки;
	
	
	Возврат Параметры;
	
	
	
КонецФункции


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ВыгрузитьОстатки()

	
	 	
	
	
	//[dbo].[RESTSDISTRIB](
	//[ORGGUID] [binary](16) NOT NULL, — ГУИД Организация
	//[ARTID] [int] NOT NULL,  — Артикул
	//[CHARGUID] [binary](16) NOT NULL, — ГУИД Характеристики
	//[PRICE] [int] NOT NULL, — Цена в коп
	//[QUANTITY] [bigint] NOT NULL, — Кол-во
	//CONSTRAINT [PK_RESTSDISTRIB] PRIMARY KEY CLUSTERED — пооля первичного ключа
	//(
	//[ORGGUID] ASC,
	//[ARTID] ASC,
	//[CHARGUID] ASC

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Артикул КАК Артикул,
		|	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ВложенныйЗапрос.КоличествоОстаток <= 0
		|			ТОГДА ВложенныйЗапрос.Цена
		|		ИНАЧЕ ВложенныйЗапрос.Цена / ВложенныйЗапрос.КоличествоОстаток
		|	КОНЕЦ КАК Цена,
		|	ВложенныйЗапрос.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.Организация КАК Организация,
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.Номенклатура.Артикул КАК Артикул,
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		СУММА(ОстаткиОрганизацииОстатки.СуммаОстаток) КАК Цена,
		|		СУММА(ОстаткиОрганизацииОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|	ИЗ
		|		РегистрНакопления.ОстаткиОрганизации.Остатки(, АналитикаУчета.Номенклатура В ИЕРАРХИИ (&Номенклатура)) КАК ОстаткиОрганизацииОстатки
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.ХарактеристикаНоменклатуры,
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.Организация,
		|		ОстаткиОрганизацииОстатки.АналитикаУчета.Номенклатура.Артикул) КАК ВложенныйЗапрос";
	Запрос.УстановитьПараметр("Номенклатура", Справочники.Номенклатура.НайтиПоКоду("D0009000"));
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("Организация") Цикл 
		
		ORGGUID =  ВыборкаДетальныеЗаписи.Организация.УникальныйИдентификатор();
		
		НаборЗаписи =	ВнешниеИсточникиДанных.zFOC_SyncAgent.Таблицы.dbo_RESTSDISTRIB.СоздатьНаборЗаписей();
		НаборЗаписи.Отбор.ORGGUID.Установить(ORGGUID);		
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			нЗапись = НаборЗаписи.Добавить();
			нЗапись.ORGGUID = ORGGUID;
			нЗапись.ARTID = ВыборкаДетальныеЗаписи.Артикул;
			нЗапись.CHARGUID = ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры.УникальныйИдентификатор();
			нЗапись.PRICE = ВыборкаДетальныеЗаписи.Цена*100;
			нЗапись.QUANTITY = ВыборкаДетальныеЗаписи.КоличествоОстаток;
			
			
			
		КонецЦикла;
		  НаборЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыгрузитьПродажи(ДатаВыгрузки)
  
  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Продажи.Регистратор КАК ДокументРегистратор,
		|	Продажи.НомерСтроки КАК НомерСтроки,
		|	Продажи.Период КАК Период,
		|	ВЫБОР
		|		КОГДА Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|			ТОГДА ВЫРАЗИТЬ(Продажи.Регистратор КАК Документ.РеализацияТоваров).Организация
		|	КОНЕЦ КАК Организация,
		|	Продажи.СкладКомпании КАК СкладКомпании,
		|	Продажи.Номенклатура.Артикул КАК НоменклатураАртикул,
		|	Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения КАК НоменклатураОсновнаяЕдиницаИзмерения,
		|	Продажи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Продажи.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА Продажи.Себестоимость = 0
		|			ТОГДА 0
		|		ИНАЧЕ Продажи.Себестоимость / Продажи.Количество
		|	КОНЕЦ КАК Себестоимость,
		|	Продажи.Сумма КАК Сумма
		|ИЗ
		|	РегистрНакопления.Продажи КАК Продажи
		|ГДЕ
		|	Продажи.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров
		|	И Продажи.Регистратор.РегламентированныйУчет = ЛОЖЬ
		|	И Продажи.ТорговаяПлощадка В ИЕРАРХИИ(&ТорговаяПлощадка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументРегистратор";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДатаВыгрузки));
	Запрос.УстановитьПараметр("НачалоПериода", ДатаВыгрузки);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000295"));

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	  //[DOCGUID] [binary](16) NOT NULL,  -- ГУИД документа реализации
  //[LINE] [int] NOT NULL,        -- Номер строки в документе
  //[SALESDATE] [date] NOT NULL,    -- Дата продажи по документу
  //[ORGGUID] [binary](16) NOT NULL,  -- ГУИД организации
  //[SAREAGUID] [binary](16) NOT NULL,  -- ГУИД склада
  //[ARTID] [int] NOT NULL,        -- Артикул
  //[PACKGUID] [binary](32) NOT NULL,  -- ГУИД упаковки+ГУИД характеристики
  //[SALESCOUNT] [bigint] NOT NULL,    -- кол-во товара
  //[SALESPRICE] [int] NOT NULL,    -- цена себестоимости в коп
  //[SALESSUM] [bigint] NOT NULL,    -- сумма продажи по докуемнту
  СуммаМакс = 999999;
  Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("ДокументРегистратор") Цикл

	  
	  	DOCGUID =ВыборкаДетальныеЗаписи.ДокументРегистратор.УникальныйИдентификатор();
		ORGGUID =  ВыборкаДетальныеЗаписи.Организация.УникальныйИдентификатор();
		SAREAGUID = ВыборкаДетальныеЗаписи.СкладКомпании.УникальныйИдентификатор();
		SALESDATE = ВыборкаДетальныеЗаписи.Период;
		НаборЗаписи =	ВнешниеИсточникиДанных.zFOC_SyncAgent.Таблицы.dbo_SALESDISTRIB.СоздатьНаборЗаписей();
		НаборЗаписи.Отбор.DOCGUID.Установить(DOCGUID);		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			PACKGUID = ВыборкаДетальныеЗаписи.НоменклатураОсновнаяЕдиницаИзмерения.УникальныйИдентификатор();
			CHARGUID = ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры.УникальныйИдентификатор();
			ARTID = Число(ВыборкаДетальныеЗаписи.НоменклатураАртикул);
			СтрокаЗаписи = НаборЗаписи.Добавить();
			СтрокаЗаписи.DOCGUID =  DOCGUID;
			СтрокаЗаписи.ORGGUID =  ORGGUID;
			СтрокаЗаписи.SAREAGUID =  SAREAGUID;
			СтрокаЗаписи.SALESDATE = SALESDATE;
			СтрокаЗаписи.LINE = ВыборкаДетальныеЗаписи.НомерСтроки;
			СтрокаЗаписи.ARTID = ARTID; 
			
			СтрокаЗаписи.PACKGUID = PACKGUID;			
			СтрокаЗаписи.CHARGUID = CHARGUID;
			Если ВыборкаДетальныеЗаписи.Себестоимость*ВыборкаДетальныеЗаписи.Количество >= СуммаМакс Тогда
				Количество =  ВыборкаДетальныеЗаписи.Количество/2;
				Себестоимость = ВыборкаДетальныеЗаписи.Себестоимость;
				Сумма = ВыборкаДетальныеЗаписи.Сумма /2;
				
				СтрокаЗаписи.SALESCOUNT = Количество;
				СтрокаЗаписи.SALESPRICE = Себестоимость*100;
				СтрокаЗаписи.SALESSUM = Сумма*100;
				
				СтрокаЗаписи2 = НаборЗаписи.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗаписи2,СтрокаЗаписи);
				СтрокаЗаписи2.LINE =ВыборкаДетальныеЗаписи.НомерСтроки+1000;
			Иначе
				СтрокаЗаписи.SALESCOUNT = ВыборкаДетальныеЗаписи.Количество;
				СтрокаЗаписи.SALESPRICE = ВыборкаДетальныеЗаписи.Себестоимость*100;
				СтрокаЗаписи.SALESSUM = ВыборкаДетальныеЗаписи.Сумма*100;
			КонецЕсли;
			
		КонецЦикла;
		
		НаборЗаписи.Записать();
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА

  
	
КонецПроцедуры


Процедура ЗагрузитьДокументыЗКС(Загруженные, НаДату, Подразделение = Неопределено)
	
	ЭтоЗагрузкаПРРО = Подразделение <> Неопределено;
	ЗавершитьСошибкой = Ложь;
	ОшибкиВыполнения = Новый ТекстовыйДокумент;	
	
	Если НаДату = Ложь Тогда
		НачПер = НачалоДня(ТекущаяДата())-60*60*24*60;
		КонПер = НачалоДня(ТекущаяДата())-60*60*24; 
	Иначе
		НачПер = НачалоДня(НаДату);
		КонПер = НачалоДня(НаДату);
	КонецЕсли;
	
	ДатаНачалаРаботы = '20230801'; //Vov41k
	Если НачПер < ДатаНачалаРаботы Тогда
		НачПер = ДатаНачалаРаботы;
	КонецЕсли;	
	Если КонПер  < ДатаНачалаРаботы Тогда
		КонПер = ДатаНачалаРаботы;
	КонецЕсли;
	
	СтруктруаЗаписиРезультатаВыполнения = РегистрыСведений.РезультатыВыполненияОбработок.СформироватьСтруктуруЗаписиРезультатов();
	СтруктруаЗаписиРезультатаВыполнения.ИдентификаторОбработки = "zFoc";
	СтруктруаЗаписиРезультатаВыполнения.КлючОбработки = ?(ЭтоЗагрузкаПРРО, "Загрузка зет Foc (ПРРО)", "Загрузка зет Foc");
	
	Если ЭтоЗагрузкаПРРО Тогда 
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Подразделение: "+Подразделение, СтруктруаЗаписиРезультатаВыполнения, СтатусСообщения.Информация);
	КонецЕсли;
	
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Пользователь: "+ИмяПользователя(), СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.Информация);
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(?(НаДату = Ложь,"За весь период 7 дней","Период загрузки "+Формат(НаДату,"ДФ=dd.MM.yyyy")), СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.Информация);
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(?(Загруженные,"Обработка загруженных","Обработка новых"), СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.Информация);
		
	
	НастройкиПодлючения = Неопределено;	
	
	Если ЭтоЗагрузкаПРРО Тогда 
		Узел = ПланыОбмена.ОбменФронт.ПолучитьУзелОбмена(Подразделение);
		Если Узел <> Неопределено Тогда 
			НастройкиПодлючения = ПланыОбмена.ОбменФронт.ЗаполнитьПараметрыПодключения(Узел);		
		Иначе 
			ЗавершитьСошибкой = Истина;
			ОшибкиВыполнения.ДобавитьСтроку("------------------");
			ОшибкиВыполнения.ДобавитьСтроку(НСтр("ru='Ошибка определения узла по подразделению: "+ Строка(Подразделение)+"';uk='Помилка визначення вузла по підрозділу: "+ Строка(Подразделение)+"'"));		
		КонецЕсли;				
	КонецЕсли;
	
	Если Не ЗавершитьСошибкой Тогда 
		ТекстСообщения = "";
		Успешно = ПодключениеКВнешнемуИсточнику(Ложь, НастройкиПодлючения, ТекстСообщения);			
		
		Если Не Успешно Тогда 
			ЗавершитьСошибкой = Истина;
			Если Не ПустаяСтрока(ТекстСообщения) Тогда 
				ОшибкиВыполнения.ДобавитьСтроку("------------------");
				ОшибкиВыполнения.ДобавитьСтроку(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Успешно = Ложь;
	КонецЕсли;
	
	Если Успешно Тогда 
		Попытка	
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("isDOWNLOADED",Загруженные);
			Запрос.УстановитьПараметр("ЗаПериод",НаДату);		
			Запрос.УстановитьПараметр("OPDATE",НачПер);
			Запрос.УстановитьПараметр("OPDATE2",КонПер);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	dbo_STAT_DATESALES.OPDATE КАК OPDATE,
			|	dbo_STAT_DATESALES.ZREPFPSN КАК ZREPFPSN,
			|	dbo_STAT_DATESALES.SAREAGUID КАК SAREAGUID,
			|	dbo_STAT_DATESALES.SAREAID КАК SAREAID,
			|	dbo_STAT_DATESALES.SYSTEMID КАК SYSTEMID,
			|	dbo_STAT_DATESALES.WORKDAYID КАК WORKDAYID,
			|	dbo_STAT_DATESALES.ZREPNUM КАК ZREPNUM,
			|	dbo_STAT_DATESALES.SALESSUM КАК SALESSUM,
			|	dbo_STAT_DATESALES.REFUNDSUM КАК REFUNDSUM,
			|	dbo_STAT_DATESALES.isDOWNLOADED КАК isDOWNLOADED,
			|	dbo_STAT_DATESALES.FPFN КАК FPFN
			|ИЗ
			|	ВнешнийИсточникДанных.zFOC.Таблица.dbo_STAT_DATESALES КАК dbo_STAT_DATESALES
			|ГДЕ
			|	dbo_STAT_DATESALES.isDOWNLOADED = &isDOWNLOADED
			|	И ВЫБОР
			|			КОГДА &ЗаПериод = ЛОЖЬ
			|				ТОГДА dbo_STAT_DATESALES.OPDATE МЕЖДУ &OPDATE И &OPDATE2
			|			ИНАЧЕ dbo_STAT_DATESALES.OPDATE = &OPDATE
			|		КОНЕЦ
			|
			|УПОРЯДОЧИТЬ ПО
			|	OPDATE";
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			//ВремяНачала = ТекущаяДатаСеанса();	
			ВсегоСтрок = ВыборкаДетальныеЗаписи.Количество();
			РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Документов на загрузку = "+Формат(ВсегоСтрок, "ЧН=0; ЧГ=0"), СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.Информация);
			
			Счетчик   = 1;
			ОбрДок    = 0;
			ДокОшибка = 0;
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ZREPNUM = СокрЛП(ВыборкаДетальныеЗаписи.ZREPNUM);
				
				СкладСсылка = Справочники.СкладыКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор(ВыборкаДетальныеЗаписи.SAREAGUID));
				//ОрганизацияОбъекта = Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(СкладСсылка.ТорговаяПлощадка,КонецДня(ВыборкаДетальныеЗаписи.OPDATE));
				
				//КассаККМ = Справочники.КассыККМ.ПолучитьКассуККМзвНомер(ВыборкаДетальныеЗаписи.ZREPFPSN,ОрганизацияОбъекта);
				КассаККМ = Справочники.КассыККМ.ВернутьКассуККМ_Фок(ВыборкаДетальныеЗаписи.ZREPFPSN,ВыборкаДетальныеЗаписи.FPFN, СкладСсылка.ТорговаяПлощадка);

				Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
					ОшибкиВыполнения.ДобавитьСтроку("------------------");
					АхтСтрока1 = "Загрузка по складу "+Строка(СкладСсылка)+Символы.ПС+"Ахтунг не найдена касса ККМ скпо номеру " + ВыборкаДетальныеЗаписи.ZREPFPSN +" и тоговая площадка """ + Строка(СкладСсылка.ТорговаяПлощадка) +"""";
					АхтСтрока2 = "SAREAID="+Формат(ВыборкаДетальныеЗаписи.SAREAID,"ЧЦ=15; ЧГ=")+";WORKDAYID="+Формат(ВыборкаДетальныеЗаписи.WORKDAYID,"ЧЦ=15; ЧГ=")+";SYSTEMID="+Формат(ВыборкаДетальныеЗаписи.SYSTEMID,"ЧЦ=15; ЧГ=")+";ZREPNUM="+ZREPNUM;
					ОшибкиВыполнения.ДобавитьСтроку(АхтСтрока1);
					ОшибкиВыполнения.ДобавитьСтроку(АхтСтрока2);
					ОбщегоНазначения.СообщитьПользователю(АхтСтрока1+Символы.ПС+АхтСтрока2);
					
					ДокОшибка = ДокОшибка +1;
					Счетчик = Счетчик + 1;
					ОбрДок = ОбрДок + 1;
					ЗавершитьСошибкой = Истина;
					Продолжить;
					
				КонецЕсли;
				
				ОрганизацияОбъекта = КассаККМ.Организация;
				
				//Сформируем номер смены для поиска /код кассы / номер зет отчета / номер рабочего дня
				//НомерЗетОтчетаДок = СтрЗаменить(ZREPNUM, ".", "D");
				НомерСмены = СокрЛП(КассаККМ.Код)+"-"+ ZREPNUM +"-"+ СокрЛП(Формат(ВыборкаДетальныеЗаписи.WORKDAYID,"ЧЦ=15; ЧГ="));
				
				Смена = Документы.ТК_КассоваяСменаЗКС.НайтиСменуПоНомеру(НомерСмены);
				
				Если Смена = Неопределено Тогда
					СменаОбъект = Документы.ТК_КассоваяСменаЗКС.СоздатьДокумент();
					ЗетОтчет =  ПолучитьСтруктуруЗетОтчета(ВыборкаДетальныеЗаписи.ZREPFPSN, ZREPNUM);
					Если ЗетОтчет = Неопределено Тогда
						
						ОшибкиВыполнения.ДобавитьСтроку("Нет данных Z - отчета,ZREPFPSN:"+Строка( ВыборкаДетальныеЗаписи.ZREPFPSN)+", ZREPNUM:"+ZREPNUM);
						ДокОшибка = ДокОшибка +1;
						Счетчик = Счетчик + 1;
						ОбрДок = ОбрДок + 1;
						ЗавершитьСошибкой = Истина;
						Продолжить;
						
					КонецЕсли;			
					
					ЗаполнитьЗначенияСвойств(СменаОбъект,ЗетОтчет);
					СменаОбъект.Номер = НомерСмены;
					СменаОбъект.Организация = ОрганизацияОбъекта;
					СменаОбъект.ПодразделениеКомпании = СкладСсылка.Подразделение;
					СменаОбъект.ТорговаяПлощадка = СкладСсылка.ТорговаяПлощадка;
					СменаОбъект.КассаККМ = КассаККМ;
					//СменаОбъект.НачалоКассовойСмены  
					//СменаОбъект.ОкончаниеКассовойСмены  
					
					СменаОбъект.ОбменДанными.Загрузка = Истина;
					СменаОбъект.Записать();
					
					Смена = СменаОбъект.Ссылка;
				Иначе
					СменаОбъект = Смена.ПолучитьОбъект();
					
					Если СменаОбъект.ПометкаУдаления Тогда 
						СменаОбъект.УстановитьПометкуУдаления(Ложь);
					КонецЕсли;
					
					ЗетОтчет =  ПолучитьСтруктуруЗетОтчета(ВыборкаДетальныеЗаписи.ZREPFPSN, ZREPNUM);
					ЗаполнитьЗначенияСвойств(СменаОбъект,ЗетОтчет);
					СменаОбъект.Номер = НомерСмены;
					СменаОбъект.Организация = ОрганизацияОбъекта;
					СменаОбъект.ПодразделениеКомпании = СкладСсылка.Подразделение;
					СменаОбъект.ТорговаяПлощадка = СкладСсылка.ТорговаяПлощадка;
					СменаОбъект.КассаККМ = КассаККМ;
					//СменаОбъект.НачалоКассовойСмены  
					//СменаОбъект.ОкончаниеКассовойСмены  
					
					СменаОбъект.ОбменДанными.Загрузка = Истина;
					СменаОбъект.Записать();
					
					Смена = СменаОбъект.Ссылка;
					
				КонецЕсли;
				Процент = Цел(Счетчик / ВсегоСтрок * 100);
				
				ДлительныеОперации.СообщитьПрогресс(Процент, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 из %2 Обработка документа по складу: %3'; uk = '%1 з %2 Обрабка документа по складу: %3'"), 
				ОбрДок, 
				ВсегоСтрок, 
				Строка(СкладСсылка)));
				
				
				Рез =  ЗагрузитьЗКС(ВыборкаДетальныеЗаписи.OPDATE,Смена,СкладСсылка,ВыборкаДетальныеЗаписи.SAREAID,ВыборкаДетальныеЗаписи.WORKDAYID,ВыборкаДетальныеЗаписи.SYSTEMID, ZREPNUM, ВыборкаДетальныеЗаписи.ZREPFPSN);
				
				стрМагазинЗагружен = Формат(ВыборкаДетальныеЗаписи.OPDATE,"ДФ=dd.MM.yyyy")+" по магазину "+Строка(Смена.ТорговаяПлощадка);
				Если НЕ Рез.ОК Тогда
					ЗавершитьСошибкой = Истина;
					ОшибкиВыполнения.ДобавитьСтроку("------------------");
					АхтСтрока1 = "Ахтунг "+стрМагазинЗагружен + Символы.ПС + Рез.Ошибки;
					АхтСтрока2 = "SAREAID="+Формат(ВыборкаДетальныеЗаписи.SAREAID,"ЧЦ=15; ЧГ=")+";WORKDAYID="+Формат(ВыборкаДетальныеЗаписи.WORKDAYID,"ЧЦ=15; ЧГ=")+";SYSTEMID="+Формат(ВыборкаДетальныеЗаписи.SYSTEMID,"ЧЦ=15; ЧГ=")+";ZREPNUM="+ZREPNUM;
					ОшибкиВыполнения.ДобавитьСтроку(АхтСтрока1);
					ОшибкиВыполнения.ДобавитьСтроку(АхтСтрока2);
					//РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(АхтСтрока1, СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.ОченьВажное);
					ДокОшибка = ДокОшибка +1;
					
					ОбщегоНазначения.СообщитьПользователю(АхтСтрока1+Символы.ПС+АхтСтрока2);				
				Иначе
					РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("ОК "+стрМагазинЗагружен, СтруктруаЗаписиРезультатаВыполнения,СтатусСообщения.Обычное);				
				КонецЕсли;
							
				Счетчик = Счетчик + 1;
				ОбрДок = ОбрДок + 1;
			КонецЦикла;
		Исключение
			ЗавершитьСошибкой = Истина;
			ОшибкиВыполнения.ДобавитьСтроку("------------------");
			ОшибкиВыполнения.ДобавитьСтроку("Ошибка загрузки: " + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ЗавершитьСошибкой Тогда
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Документов с шибками = " +Формат(ДокОшибка, "ЧН=0; ЧГ=0")+Символы.ПС+ОшибкиВыполнения.ПолучитьТекст(),СтруктруаЗаписиРезультатаВыполнения);
	КонецЕсли;	
	
	РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, НЕ ЗавершитьСошибкой);
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьМассивПодразделенияСПРРО()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Массив = Новый Массив;
	Массив.Добавить(Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("aca6e0b4-d29a-11e6-80d2-0050569272d9"))); //ТС Аттика
	Массив.Добавить(Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("b6315344-cb1a-11ee-a5ab-005056bf2a5e"))); //ТС Фабрика сыра
	
	//Массив.Добавить(Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("aca6e0b8-d29a-11e6-80d2-0050569272d9"))); //Фабрика Сыра Производство	
	//Массив.Добавить(Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("aca6e0ba-d29a-11e6-80d2-0050569272d9"))); //ТС Buffet
	
	Возврат Массив;
	
КонецФункции

//Формирует структуру зет отчета
//ZREPFPSN - зв номер фискалки
//ZREPNUM  - номер зет отчета
//
Функция ПолучитьСтруктуруЗетОтчета(ZREPFPSN,ZREPNUM)
	
	СтрутураЗет = Новый Структура("Дата,ДатаZотчета,НомерZотчета,НомерСменыККМ,СуммаДокумента,СуммаВозврат,КоличествоЧеков,СуммаЭкваер,СуммаСкидки");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	dbo_vZREPxml.WORKDAYID КАК НомерСменыККМ,
	|	dbo_vZREPxml.SAREAID КАК SAREAID,
	|	dbo_vZREPxml.SYSTEMID КАК SYSTEMID,
	|	dbo_vZREPxml.ZREPFPSN КАК ZREPFPSN,
	|	dbo_vZREPxml.ZREPNUM КАК НомерZотчета,
	|	dbo_vZREPxml.ZREPTIME КАК ДатаZотчета,
	|	dbo_vZREPxml.SALESCOUNT КАК КоличествоЧеков,
	|	dbo_vZREPxml.ZREPTIME КАК Дата,
	|	dbo_vZREPxml.REFSSUM / 100 КАК СуммаВозврат,
	|	dbo_vZREPxml.SALESSUM / 100 КАК СуммаДокумента,
	|	dbo_vZREPxml.SALESDISC / 100 КАК СуммаСкидки,
	|	dbo_vZREPxml.ZREPDATA КАК ZREPDATA
	|ИЗ
	|	ВнешнийИсточникДанных.zFOC.Таблица.dbo_vZREPxml КАК dbo_vZREPxml
	|ГДЕ
	|	dbo_vZREPxml.ZREPFPSN = &ZREPFPSN
	|	И dbo_vZREPxml.ZREPNUM = &ZREPNUM";
	
	Запрос.УстановитьПараметр("ZREPFPSN", ZREPFPSN);
	Запрос.УстановитьПараметр("ZREPNUM", ZREPNUM);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	ЗаполнитьЗначенияСвойств(СтрутураЗет,ВыборкаДетальныеЗаписи);
	Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ZREPDATA) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(ВыборкаДетальныеЗаписи.ZREPDATA);
	
	ДатаЗет = ФабрикаXDTO.ПрочитатьXML(Чтение);
	СуммаЭкваер = Число(ДатаЗет.SALES.CHARGES.VALUE[0])/100;
	СуммаВозврЭкваер =  Число(ДатаЗет.refunds.CHARGES.VALUE[0])/100;
	СтрутураЗет.СуммаЭкваер =  СуммаЭкваер-СуммаВозврЭкваер;
	
	
	
	Возврат СтрутураЗет;
	
КонецФункции

Процедура ОбработатьТабличнуюЧасть(ТаблицаДокумента, тзРезультатЗапроса, ЗагруженныеОбъекты)
	
	Если ТаблицаДокумента = "Товары" Тогда
		
		Для Каждого СтрокаТаблицы Из тзРезультатЗапроса Цикл 
			Артикул = Формат(СтрокаТаблицы.ARTID, "ЧЦ=6; ЧДЦ=; ЧГ=");
			КофЕдИзм   = Формат(СтрокаТаблицы.Коэффициент,"ЧЦ=15; ЧГ=");
			Характ  = СокрЛП(СтрокаТаблицы.CHARGUID);
			
			КлючТовара = СтрЗаменить("А"+Артикул+""+КофЕдИзм+""+Характ,"-","");
			ДанныеПоТовару =   Неопределено;
			
			ЗагруженныеОбъекты.Свойство(КлючТовара,ДанныеПоТовару);
			
			Если ДанныеПоТовару = Неопределено Тогда
				
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	спрНоменклатура.Ссылка КАК Номенклатура,
				|	спрЕдиницыИзмерения.Ссылка КАК ЕдИзмерения
				|ИЗ
				|	Справочник.Номенклатура КАК спрНоменклатура
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК спрЕдиницыИзмерения
				|		ПО спрНоменклатура.Ссылка = спрЕдиницыИзмерения.Владелец
				|			И (спрЕдиницыИзмерения.Коэффициент = &Коф)
				|ГДЕ
				|	спрНоменклатура.Артикул = &Артикул
				|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ";
				
				Запрос.УстановитьПараметр("Артикул", Артикул);
				Запрос.УстановитьПараметр("Коф", СтрокаТаблицы.Коэффициент);
				
				РезультатЗапроса = Запрос.Выполнить();
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				СоставнойНалог = Ложь;
				Если ВыборкаДетальныеЗаписи.Следующий() Тогда
					нНоменклатура      = ВыборкаДетальныеЗаписи.Номенклатура;
					//нСтавкаНДС         = НалоговыеСтавки[СтрокаТаблицы.TAXGRP-1];
					//нСтавка            = нСтавкаНДС.Ставка;
					нЕденицаИзмерения  = ВыборкаДетальныеЗаписи.ЕдИзмерения;
				Иначе
					нНоменклатура     = Справочники.Номенклатура.ПустаяСсылка();
					нЕденицаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
					
					//нСтавкаНДС        = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
					//нСтавка           = 20;
				КонецЕсли;
				//	нЕденицаИзмерения = Справочники.ЕдиницыИзмерения.ПолучитьСсылку(Новый УникальныйИдентификатор(ЕдИзм));
				Если Характ = "00000000-0000-0000-0000-000000000000" Тогда
					нХарактеристика   = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				Иначе
					нХарактеристика   = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(Характ));
				КонецЕсли;	
				СтрокаТаблицы.Номенклатура               = нНоменклатура;
				СтрокаТаблицы.ХарактеристикаНоменклатуры = нХарактеристика;
				СтрокаТаблицы.ЕдиницаИзмерения           = нЕденицаИзмерения;
				//СтрокаТаблицы.СтавкаНДС                  = нСтавкаНДС;
				//СтрокаТаблицы.Ставка                     = нСтавка;
				//СоставнойНалог 							 = СтрокаТаблицы.СтавкаНДС.СоставнойНалог;
				//ЗагруженныеОбъекты.Вставить(КлючТовара,Новый Структура("Номенклатура,ЕденицаИзмерения,Характеристика,СтавкаНДС,Ставка,СоставнойНалог",нНоменклатура,нЕденицаИзмерения,нХарактеристика,нСтавкаНДС,нСтавка,СоставнойНалог));
				
				ЗагруженныеОбъекты.Вставить(КлючТовара,Новый Структура("Номенклатура,ЕденицаИзмерения,Характеристика",нНоменклатура,нЕденицаИзмерения,нХарактеристика));
			Иначе
				СтрокаТаблицы.Номенклатура               = ДанныеПоТовару["Номенклатура"];
				СтрокаТаблицы.ХарактеристикаНоменклатуры = ДанныеПоТовару["Характеристика"];
				СтрокаТаблицы.ЕдиницаИзмерения           = ДанныеПоТовару["ЕденицаИзмерения"];
				//СтрокаТаблицы.СтавкаНДС                  = ДанныеПоТовару["СтавкаНДС"];
				//СтрокаТаблицы.Ставка                     = ДанныеПоТовару["Ставка"];
				//СоставнойНалог 							 = ДанныеПоТовару["СоставнойНалог"];
				
			КонецЕсли;
			
			
			//Vov41k 13.07.2022
			Если СтрокаТаблицы.TAXGRP <= НалоговыеСтавки.Количество() Тогда 
				стрСтавка		= НалоговыеСтавки[СтрокаТаблицы.TAXGRP-1];	
				нСтавкаНДС		= стрСтавка.СтавкаНДС;
				нСтавка			= стрСтавка.Ставка;
				СоставнойНалог	= стрСтавка.СоставнойНалог;
			Иначе
				нСтавкаНДС		= Справочники.СтавкиНДС.ПустаяСсылка();
				нСтавка			= 20;
				СоставнойНалог	= Ложь;
			КонецЕсли;
			
			СтрокаТаблицы.СтавкаНДС = нСтавкаНДС;
			СтрокаТаблицы.Ставка	= нСтавка;
			
			
			СтрокаТаблицы.КоличествоБазовое	   = СтрокаТаблицы.Количество * СтрокаТаблицы.Коэффициент ;
			СтрокаТаблицы.Цена     = СтрокаТаблицы.Цена /?(СтрокаТаблицы.Коэффициент = 0,1,СтрокаТаблицы.Коэффициент);
			СтрокаТаблицы.Сумма	   = СтрокаТаблицы.Цена * СтрокаТаблицы.КоличествоБазовое ;
			//	СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.СуммаВсего * СтрокаТаблицы.Ставка / (100 + СтрокаТаблицы.Ставка);
			//Если СоставнойНалог Тогда
			//	СуммаАкцизногоНалога = СтрокаТаблицы.СуммаВсего * 0.05 /1.05;
			//	СтрокаТаблицы.СуммаНДС=(СтрокаТаблицы.СуммаВсего-СуммаАкцизногоНалога)*СтрокаТаблицы.Ставка/(100+СтрокаТаблицы.Ставка);
			//	
			//	СтрокаТаблицы.СуммаАкцизногоНалога = СуммаАкцизногоНалога;
			//	
			//КонецЕсли;
			
			
		КонецЦикла;
	КонецЕсли;
	
	
КонецПроцедуры

Функция ПодключениеКВнешнемуИсточнику(Загрузка = Истина, ПараметрыПодключения, ТекстСообщения)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//Если ВнешниеИсточникиДанных.zFOC.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен Тогда
	//	ВнешниеИсточникиДанных.zFOC.РазорватьСоединение();
	//КонецЕсли;	
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
		СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
	Иначе
		ТекстСообщения = ТекстСообщения + НСтр("ru='Операционная система "+СистемнаяИнформация.ТипПлатформы +" не поддерживается.';uk='Операційна система "+ СистемнаяИнформация.ТипПлатформы +" не підтримується'")+Символы.ПС;		
		Возврат Ложь
	КонецЕсли;
	
	Если ПараметрыПодключения <> Неопределено Тогда 
		ПараметрыПодключенияВИ = ВнешниеИсточникиДанных.zFOC.ПолучитьОбщиеПараметрыСоединения();	
		ПараметрыПодключенияВИ.АутентификацияСтандартная = Истина;
		ПараметрыПодключенияВИ.ИмяПользователя = ПараметрыПодключения.ИмяПользователя; 
		ПараметрыПодключенияВИ.Пароль = ПараметрыПодключения.Пароль;
		ПараметрыПодключенияВИ.СтрокаСоединения = СтрокаПодключения + "Server="+СокрЛП(ПараметрыПодключения.АдресСервера) + ";Database="+?(Загрузка,СокрЛП(ПараметрыПодключения.БазаДанныхТранзит),СокрЛП(ПараметрыПодключения.БазаДанных)) + ";Uid="+СокрЛП(ПараметрыПодключения.ИмяПользователя) + ";Pwd="+СокрЛП(ПараметрыПодключения.Пароль) +";";		
		ПараметрыПодключенияВИ.СУБД = "MS SQL Server";
	Иначе
		ПараметрыПодключенияВИ = ВнешниеИсточникиДанных.zFOC.ПолучитьПараметрыСоединенияПользователя(ИмяПользователя());		
	КонецЕсли;
		
	ВнешниеИсточникиДанных.zFOC.УстановитьПараметрыСоединенияСеанса(ПараметрыПодключенияВИ);	
	
	Попытка
		ВнешниеИсточникиДанных.zFOC.УстановитьСоединение();
	Исключение
		ТекстСообщения = ТекстСообщения + ОписаниеОшибки();
	КонецПопытки;	
	
	Если ВнешниеИсточникиДанных.zFOC.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен Тогда
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;	
	
	
КонецФункции


#КонецОбласти



#КонецЕсли

















//Список статусов формы:
//	- "ОжиданиеСканирования",
//	- "НакладнаяНайдена",
//	- "ПроверкаНакладной",
//	- "ПечатьНакладной",
//	- "ВводТары"


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Кладовщик = Пользователи.ТекущийПользователь();	
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ЦветВажного",	ЦветаСтиля.ЦветВажного);
	ДопПараметры.Вставить("ЦветОшибка", 	ЦветаСтиля.ЦветОтрицательногоЧисла); //ЦветаСтиля.ПоясняющийОшибкуТекст);
	ДопПараметры.Вставить("ГруппаВозвратнаяТара", Справочники.Номенклатура.НайтиПоКоду("м0001326")); //Возвратная тара	
	
	ПланОбмена = ПланыОбмена.ОбменФронт.НайтиПоКоду("000000003"); //OS - fabrika
	
	Если Не ПланОбмена.Пустая() Тогда 
		ДопПараметры.Вставить("ПланОбмена", ПланОбмена);
	КонецЕсли;
	
	Тара_ДоступенВводТары = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");								
		//ОповещениеПриПодключении = НСтр("ru = 'Отсканируйте накладную'; uk = 'Отскануйте накладну'");
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	// Конец ПодключаемоеОборудование	
	
	текСтатус = "ОжиданиеСканирования";
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекКод = Параметр[0];
			Иначе
				ТекКод = Параметр[1][1];
			КонецЕсли;
			
			ОбработатьПолученныйШК(ТекКод);
			Возврат;
			
		ИначеЕсли ИмяСобытия = "ScanDataBase64" Тогда
			
			ШтрихкодBase64 = "";
			СимволыШтрихкодBase64 = Base64Значение(Параметр[0]); // Декодируем строку
			// Преобразуем в строку символов
			МассивСимволов = СтрРазделить(СимволыШтрихкодBase64, " ");
			Для Каждого СимволШтрихкода Из МассивСимволов Цикл
				Если СимволШтрихкода = "1D" Тогда // Код символа GS в HEX
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
				Иначе
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияКлиентСервер.ПреобразоватьHEXВСтроку(СимволШтрихкода);
				КонецЕсли;
			КонецЦикла;
			ОбработатьПолученныйШК(ШтрихкодBase64);
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	ДобавитьЛог(ЛогДействий, "Сканер", "Не известное событие <" + ИмяСобытия + ">, Источник <" + Источник + ">");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(, УникальныйИдентификатор, ПоддерживаемыеТипыВО);	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КэшТекущейСтроки = Новый Структура("КоличествоПроверено");
	
	ЗаполнитьЗначенияСвойств(КэшТекущейСтроки, ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПровереноПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.СуммаПроверено = ТекущаяСтрока.КоличествоПроверено * ТекущаяСтрока.Коэффициент * ТекущаяСтрока.Цена;
	ТекущаяСтрока.Вес = ТекущаяСтрока.КоличествоПроверено * ТекущаяСтрока.ВесовойКоэффициент;
	
	Если ТипЗнч(КэшТекущейСтроки) = Тип("Структура") Тогда 
		КоличествоДоИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КэшТекущейСтроки, "КоличествоПроверено", ТекущаяСтрока.КоличествоПроверено);
	Иначе
		КоличествоДоИзменения = ТекущаяСтрока.КоличествоПроверено;
	КонецЕсли;
	
	ДобавитьЛог(
		ЛогДействий, 
		"Изменение количества", 		
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Строка №%1 [%2]. Количество было ""%3"" стало ""%4""",
			ТекущаяСтрока.НомерСтроки,
			ТекущаяСтрока.Номенклатура,
			КоличествоДоИзменения,
			ТекущаяСтрока.КоличествоПроверено));
	
КонецПроцедуры


&НаКлиенте
Процедура ВозвратнаяТараПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	КэшТекущейСтроки = Новый Структура("Количество");
	
	ЗаполнитьЗначенияСвойств(КэшТекущейСтроки, ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена * ТекущаяСтрока.Количество;
	
	Если ТипЗнч(КэшТекущейСтроки) = Тип("Структура") Тогда 
		КоличествоДоИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КэшТекущейСтроки, "Количество", ТекущаяСтрока.Количество);
	Иначе
		КоличествоДоИзменения = ТекущаяСтрока.Количество;
	КонецЕсли;
	
	Если КоличествоДоИзменения <> ТекущаяСтрока.Количество Тогда 
		ДопПараметры.Вставить("ЕстьИзмененияТара", Истина);
	КонецЕсли;
	
	Тара_Сумма = ВозвратнаяТара.Итог("Сумма");
	
	ДобавитьЛог(
		ЛогДействий, 
		текСтатус, 		
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Возвратная тара ""[%2]"". Количество было ""%3"" стало ""%4""",
			"",
			ТекущаяСтрока.Номенклатура,
			КоличествоДоИзменения,
			ТекущаяСтрока.Количество));
	
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РучноеСканирование(Команда)
	
	Если Не ПустаяСтрока(ПолеСканировки) Тогда 		
		ДобавитьЛог(ЛогДействий, "Сканер", "Ручной ввод штрих-кода: " + ПолеСканировки);		
		
		ОбработатьПолученныйШК(ПолеСканировки);
		
		ПолеСканировки = "";
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Поле сканировки пустое'; uk = 'Поле сканування не заповнене'"), 5);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаНакладной(Команда)
	
	ЗаказПокупателя = Неопределено;
	ЗаказПокупателяРеквизиты = Неопределено;
	Отгрузка_Документ = Неопределено;
	Отгрузка_ТТН = Неопределено;
	Отгрузка_Выпуск = Неопределено;	
	
	дКонтрагент = Неопределено;
	дТочкаДоставки = Неопределено;	
	
	Объект.Тара.Очистить();
	
	ДобавитьЛог(ЛогДействий, "Отмена обработки накладной", "Нажатие клавиши ""Отмена"" на странице проверки товара", Строка(ЗаказПокупателя));	
	ЗаписатьЛог();		
	
	текСтатус = "ОжиданиеСканирования";		
	УстановитьСтраницуПоСтатусу();	
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНакладную(Команда)
	
	Если текСтатус <> "НакладнаяНайдена" Тогда		
		Возврат;	
	КонецЕсли;
	
	ДокументКорректен = Истина;	
	
	ДобавитьЛог(ЛогДействий, "Проверка", "Нажатие кнопки ""Проверить""", Строка(ЗаказПокупателя));
	
	РасхожденияКоличества.Очистить();
	
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.КоличествоПроверено <> Стр.Количество Тогда 
			ДокументКорректен = Ложь;
			
			НоваяСтрока = РасхожденияКоличества.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			
			НоваяСтрока.РазницаКоличество = Стр.КоличествоПроверено - Стр.Количество;
			НоваяСтрока.РазницаСумма = Стр.СуммаПроверено - Стр.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ЗаказПокупателяРеквизиты.Вставить("ДокументКорректен", ДокументКорректен);
	
	Если ДокументКорректен Тогда 
		Элементы.ДекорацияРезультатПроверки.Заголовок = НСтр("ru = 'Расхождения не выявлены'; uk = 'Розбіжності не виявлені'");
		Элементы.ДекорацияРезультатПроверки.ЦветТекста = ДопПараметры.ЦветВажного;
	Иначе
		Элементы.ДекорацияРезультатПроверки.Заголовок = НСтр("ru = 'Внимание!
                                                              |В документе есть расхождения'; uk = 'Увага!
                                                              |У документі є розбіжності'");
		Элементы.ДекорацияРезультатПроверки.ЦветТекста = ДопПараметры.ЦветОшибка;
	КонецЕсли;
	
	Элементы.РасхожденияКоличества.Видимость = Не ДокументКорректен;
	
	
	текСтатус = "ПроверкаНакладной";
	УстановитьСтраницуПоСтатусу();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПроверки(Команда)

	Если текСтатус <> "ПроверкаНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	РасхожденияКоличества.Очистить();
	
	ДобавитьЛог(ЛогДействий, "Отмена", "Нажатием кнопки ""Отмена"" на странице проверки накладной");
	
	текСтатус = "НакладнаяНайдена";
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасход(Команда)
	
	Если текСтатус <> "ПроверкаНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "Формирование расхода", "Нажатие кнопки ""Сформировать (F7)""", Строка(ЗаказПокупателя));		
	
	ОчиститьСообщения();
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПроверка Тогда 
		ДокументКорректен = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗаказПокупателяРеквизиты, "ДокументКорректен", Истина);
		
		Если ДокументКорректен Тогда 
			СформироватьРасходНаСервере();
			УстановитьСтраницуПоСтатусу();
		Иначе
			ОповещениеЗавершения = Новый ОписаниеОповещения("СформироватьРасходСРасхождениемПродолжение", ЭтаФорма);
			ПоказатьВопрос(ОповещениеЗавершения, НСтр("ru = 'Обнаружены расхождения в документе.
                                                       |Сформировать документ с расхождениями?'; uk = 'Знайдені розбіжності у документі.
                                                       |Сформувати документ з розбіжностями?'"), РежимДиалогаВопрос.ДаНет, 10, КодВозвратаДиалога.Нет);
		КонецЕсли;
	Иначе
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Ложное срабатывание кнопки ""Сформировать (F7)"". Текущая страница = " + Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасходСРасхождениемПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Подтверждение формирования расхода с расхождениями", Строка(ЗаказПокупателя));		
		
		СформироватьРасходНаСервере();
		УстановитьСтраницуПоСтатусу();
		
	Иначе
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Отказ от формирования расхода с расхождениями", Строка(ЗаказПокупателя));		
	КонецЕсли;		
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаПечать(Команда)		
	
	Если текСтатус <> "ПечатьНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ВыполнитьКомандуПечати(Ложь);
	
	Элементы.Декорация1.Заголовок = НСтр("ru = 'Просканируйте накладную!'; uk = 'Проскануйте накладну!'");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьНаПринтер(Команда)
	
	Если текСтатус <> "ПечатьНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ВыполнитьКомандуПечати(Истина);
	
	Элементы.Декорация1.Заголовок = НСтр("ru = 'Просканируйте накладную!'; uk = 'Проскануйте накладну!'");
	
КонецПроцедуры


&НаКлиенте
Процедура Тара_Ввод(Команда)
	
	Если текСтатус <> "НакладнаяНайдена" Тогда		
		Возврат;	
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "ВводТары", "Нажатие кнопки ""Ввод тары""", Строка(ЗаказПокупателя));	
	
	ДопПараметры.Вставить("ЕстьИзмененияТара", Ложь);
	
	ПодготовитьВводТарыНаСервере();
	
	текСтатус = "ВводТары";
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура Тара_Отмена(Команда)
	
	Если текСтатус <> "ВводТары" Тогда 
		Возврат;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "ВводТары", "Нажатием кнопки ""Отмена"" на странице ввода тары");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Тара_ОтменаПродолжение", ЭтаФорма);
	
	Если ДопПараметры.ЕстьИзмененияТара Тогда
		ПоказатьВопрос(
			ОписаниеОповещения, 
			НСтр("ru = 'Отменить внесенные изменения?'; uk = 'Відмінити зроблені зміни?'"), 
			РежимДиалогаВопрос.ДаНет,
			10,
			КодВозвратаДиалога.Нет,
			НСтр("ru = 'Отмена изменений'; uk = 'Відміна змін'"),
			КодВозвратаДиалога.Нет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Тара_ОтменаПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
		
	ВозвратнаяТара.Очистить();
	
	текСтатус = "НакладнаяНайдена";
	УстановитьСтраницуПоСтатусу();	
	
КонецПроцедуры

&НаКлиенте
Процедура Тара_Сохранить(Команда)
	
	Если текСтатус <> "ВводТары" Тогда 
		Возврат;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "ВводТары", "Нажатием кнопки ""Сохранить"" на странице ввода тары");
	
	Объект.Тара.Очистить();
	
	Для Каждого Стр Из ВозвратнаяТара Цикл 
		Если Стр.Количество > 0 Тогда
			НоваяСтрока = Объект.Тара.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЕсли;
	КонецЦикла;
	
	текСтатус = "НакладнаяНайдена";
	УстановитьСтраницуПоСтатусу();		
	
КонецПроцедуры



&НаКлиенте
Процедура ЗавершитьРаботуРабочегоМеста(Команда)		
	Если текСтатус <> "ОжиданиеСканирования" И текСтатус <> "ПечатьНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаписатьЛог();
	ЗавершитьРаботуСистемы(Истина, Ложь);
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСтраницуПоСтатусу()

	//Список статусов формы:
	//	- "ОжиданиеСканирования",
	//	- "НакладнаяНайдена",
	//	- "ПроверкаНакладной",
	//	- "ПечатьНакладной",
	//  - "ВводТары"

	МассивВсехЭлементов = Новый Массив;
	МассивВидимыхЭлементов = Новый Массив;
	
	ИмяТекущейСтраницы = "";
	
	ГруппаСтраницы = Элементы.ГруппаСтраницы;
	
	стрСписокЗаказов 		= Элементы.СтраницаСписокЗаказов;
	стрОжидание 			= Элементы.СтраницаОжидания;
	стрНакладнаяНайдена 	= Элементы.СтраницаНайденнаяНакладная;	
	стрПроверка				= Элементы.СтраницаПроверка;
	стрПечать				= Элементы.СтраницаПечать;
	стрВводТары				= Элементы.СтраницаВозвратнаяТара;

	Если текСтатус = "ОжиданиеСканирования" Тогда 
		Если ГруппаСтраницы.ТекущаяСтраница <> стрОжидание Тогда 
			ГруппаСтраницы.ТекущаяСтраница = стрОжидание;			
		КонецЕсли;
	ИначеЕсли текСтатус = "НакладнаяНайдена" Тогда 
		Если ГруппаСтраницы.ТекущаяСтраница <> стрНакладнаяНайдена Тогда 
			ГруппаСтраницы.ТекущаяСтраница = стрНакладнаяНайдена;
		КонецЕсли;
	ИначеЕсли текСтатус = "ВводТары" Тогда
		Если ГруппаСтраницы.ТекущаяСтраница <> стрВводТары Тогда 
			ГруппаСтраницы.ТекущаяСтраница = стрВводТары;
		КонецЕсли;		
	ИначеЕсли текСтатус = "ПроверкаНакладной" Тогда 
		Если ГруппаСтраницы.ТекущаяСтраница <> стрПроверка Тогда 
			ГруппаСтраницы.ТекущаяСтраница = стрПроверка;
		КонецЕсли;
	ИначеЕсли текСтатус = "ПечатьНакладной" Тогда 
		Если ГруппаСтраницы.ТекущаяСтраница <> стрПечать Тогда 
			ГруппаСтраницы.ТекущаяСтраница = стрПечать;
		КонецЕсли;
	КонецЕсли;
	
	ИмяТекущейСтраницы = ГруппаСтраницы.ТекущаяСтраница.Имя;

	Если ЗначениеЗаполнено(ИмяТекущейСтраницы) Тогда
		МассивВсехЭлементов.Добавить(стрСписокЗаказов.Имя);
		МассивВсехЭлементов.Добавить(стрОжидание.Имя);
		МассивВсехЭлементов.Добавить(стрНакладнаяНайдена.Имя);
		МассивВсехЭлементов.Добавить(стрПроверка.Имя);
		МассивВсехЭлементов.Добавить(стрПечать.Имя);
		МассивВсехЭлементов.Добавить(стрВводТары.Имя);
		
		МассивВидимыхЭлементов.Добавить(ИмяТекущейСтраницы);
	КонецЕсли;
	
	//Видимость элементов на странице
	Если текСтатус = "НакладнаяНайдена" Тогда 
		МассивВсехЭлементов.Добавить("Тара_Ввод");
		
		Если Тара_ДоступенВводТары Тогда 
			МассивВидимыхЭлементов.Добавить("Тара_Ввод");
		КонецЕсли;
	ИначеЕсли текСтатус = "ПроверкаНакладной" Тогда 
		МассивВсехЭлементов.Добавить("ГруппаВозвратнаяТара");
		
		Если Объект.Тара.Количество() > 0 Тогда 
			МассивВидимыхЭлементов.Добавить("ГруппаВозвратнаяТара");
		КонецЕсли;
	ИначеЕсли текСтатус = "ПечатьНакладной" Тогда 		
		МассивВсехЭлементов.Добавить("ПолеТТН");
		
		Если Не Отгрузка_ТТН.Пустая() Тогда 
			МассивВидимыхЭлементов.Добавить("ПолеТТН");
		КонецЕсли;		
	КонецЕсли;	
	
	Если МассивВсехЭлементов.Количество() > 0 Тогда  
		ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехЭлементов, МассивВидимыхЭлементов);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ОбработатьПоискНакладной(тИдДокумента)

	УстановитьПривилегированныйРежим(Истина);

	текДействие = "Сканирование документа";
	
	ЗаказПокупателя = Неопределено;
	ЗаказПокупателяРеквизиты = Неопределено;
	Отгрузка_Документ = Неопределено;
	Отгрузка_ТТН = Неопределено;
	Отгрузка_Выпуск = Неопределено;
	
	дРезерв = Ложь;
	Тара_ДоступенВводТары = Ложь;
	
	ДобавитьЛог(ЛогДействий, текДействие, "Сканирование на странице поиска накладной", тИдДокумента);
	
	Если Не СтрДлина(тИдДокумента) = 36 Тогда 	
		ДобавитьЛог(ЛогДействий, текДействие, "Не корректный ID накладной", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не корректний ID накладной <%1>'; uk = 'Некоректний ID накладної <%1>'"),
								СокрЛП(тИдДокумента));
		Возврат;
	КонецЕсли;
	
	Попытка
		ГУИДДокумента = Новый УникальныйИдентификатор(тИдДокумента);
	Исключение
		ДобавитьЛог(ЛогДействий, текДействие, "Не корректный ID накладной", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не корректний ID накладной <%1>'; uk = 'Некоректний ID накладної <%1>'"),
								СокрЛП(тИдДокумента));
		Возврат;		
	КонецПопытки;
	
	ЗаказСсылка  = Документы.ЗаказПокупателя.ПолучитьСсылку(ГУИДДокумента);
	РезервСсылка = Документы.РезервированиеЗаказовПокупателя.ПолучитьСсылку(ГУИДДокумента);
	
	ЕстьЗаказ 	= ОбщегоНазначения.СсылкаСуществует(ЗаказСсылка);
	ЕстьРезерв 	= ОбщегоНазначения.СсылкаСуществует(РезервСсылка);
	
	Если Не ЕстьЗаказ И Не ЕстьРезерв Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Документ не найден", тИдДокумента);			
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не найден документ с ID <%1>'; uk = 'Не знайдене документ з ID <%1>'"),
								СокрЛП(тИдДокумента));
				
		Возврат;
	КонецЕсли;
	
	тПараметры = Новый Структура();
	тПараметры.Вставить("текДействие", текДействие);
	тПараметры.Вставить("тИдДокумента", тИдДокумента);
	
	Если ЕстьЗаказ Тогда
		ОбработатьЗаказПокупателя(ЗаказСсылка, тПараметры);
	ИначеЕсли ЕстьРезерв Тогда
		дРезерв = Истина;
		ОбработатьРезерв(РезервСсылка, тПараметры)
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработатьЗаказПокупателя(ЗаказСсылка, тПараметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Ссылка КАК Ссылка,
	               |	ЗаказПокупателя.Дата КАК Дата,
	               |	ЗаказПокупателя.Номер КАК Номер,
	               |	ЗаказПокупателя.Проведен КАК Проведен,
	               |	ЗаказПокупателя.ПометкаУдаления КАК ПометкаУдаления,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ХозОперация КАК ХозОперация,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.СрокПоставки КАК СрокПоставки,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Коэффициент КАК Коэффициент,
	               |		ЕдиницаИзмерения.Вес КАК ВесовойКоэффициент,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		Количество КАК КоличествоПроверено,
	               |		Сумма КАК СуммаПроверено,
	               |		ЗаказПокупателя.Товары.Количество * ЗаказПокупателя.Товары.Коэффициент * ЕСТЬNULL(ЗаказПокупателя.Товары.ЕдиницаИзмерения.Вес, 0) КАК Вес
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказСсылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Не удалось получить реквизиты Документа заказ покупателя запросом", тПараметры.ИдДокумента);
		Возврат;		
	КонецЕсли;
		
	Если Реквизиты.ПометкаУдаления Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Документ заказ покупателя помечен на удаление", ЗаказСсылка);
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Заказ %1 помечен на удаление!'; uk = 'Замовлення %1 помічене на видалення!'"),
								Строка(ЗаказСсылка));
		Возврат;
	КонецЕсли;
	
	Если Не Реквизиты.Проведен Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Документ заказ покупателя не проведен", ЗаказСсылка);
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Заказ %1 не проведен!'; uk = 'Замовлення %1 не проведений!'"),
								Строка(ЗаказСсылка));
		Возврат;		
	КонецЕсли;
	
	
	// Заполняем реквизиты на форме
	ЗаказПокупателя = ЗаказСсылка;
	
	ЗаказПокупателяРеквизиты = Новый Структура;
	ЗаказПокупателяРеквизиты.Вставить("ХозОперация", 			Реквизиты.ХозОперация);
	ЗаказПокупателяРеквизиты.Вставить("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);	
	ЗаказПокупателяРеквизиты.Вставить("СрокПоставки", 			Реквизиты.СрокПоставки);
	
	Объект.Товары.Загрузить(Реквизиты.Товары.Выгрузить());	
	
	дКонтрагент = Реквизиты.Контрагент;
	дТочкаДоставки = Реквизиты.ТочкаДоставки;
	Элементы.ДекорацияОтгрузкаФ1.Видимость = Реквизиты.РегламентированныйУчет;
	
	текСтатус = "НакладнаяНайдена";
	
	ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Заказ найден", ЗаказСсылка);		
	
	// Находим отгрузку
	Если Реквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателя
		ИЛИ Реквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяФизОбмен Тогда 
		
		МассивДокументов = ПолучитьПодчиненныеДокументы(ЗаказПокупателя, "ВыпускПродукции", Новый Структура("ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСРеализацией), Истина);
		
		Если МассивДокументов.Количество() > 0 Тогда
			Отгрузка_Документ 	= МассивДокументов[0];
			Отгрузка_Выпуск 	= МассивДокументов[0];		
			
			ЗаполнитьТаблицуТара(Отгрузка_Документ);
		КонецЕсли;				
		
		Тара_ДоступенВводТары = Истина;
		
	ИначеЕсли Реквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство Тогда 
		
		МассивДокументов = ПолучитьПодчиненныеДокументы(ЗаказПокупателя, "ВыпускПродукции", Новый Структура("ХозОперация", Справочники.ХозОперации.ВыпускПродукции), Истина);
		
		Если МассивДокументов.Количество() > 0 Тогда
			Отгрузка_Выпуск = МассивДокументов[0];
		КонецЕсли;						
		
		Если Не Отгрузка_Выпуск.Пустая() Тогда 
			МассивДокументов = ПолучитьПодчиненныеДокументы(Отгрузка_Выпуск, "ПеремещениеТоваров", Новый Структура("ХозОперация", Справочники.ХозОперации.ПеремещениеТоваровВФилиал), Истина);
			
			Если МассивДокументов.Количество() > 0 Тогда
				Отгрузка_Документ = МассивДокументов[0];
			КонецЕсли;
		КонецЕсли;
		
		//Vov41k 25.11.2023
		Если ЗначениеЗаполнено(Отгрузка_Документ) Тогда 
			МассивПеремещений = ПолучитьПодчиненныеДокументы(Отгрузка_Документ, "ПеремещениеТоваров", Новый Структура("ХозОперация, Проведен", Справочники.ХозОперации.ПеремещениеТоваровИзФилиала, Истина), Истина);
			Если МассивПеремещений.Количество() > 0 Тогда 
				ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Заказ уже был отгружен и подтвержден торговой точкой", ЗаказСсылка);
				
				ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Заказ %1 уже отгружен и подтвержден тоговой точкой!'; uk = 'Замовлення %1 вже відвантажене і підтверджене торгівельною точкою!'"),
					Строка(ЗаказСсылка));
					
				Возврат;		
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Реквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутренний Тогда 
		
		МассивДокументов = ПолучитьПодчиненныеДокументы(ЗаказПокупателя, "ПеремещениеТоваров", Новый Структура("ХозОперация", Справочники.ХозОперации.ПеремещениеТоваровВФилиал), Истина);
		
		Если МассивДокументов.Количество() > 0 Тогда
			Отгрузка_Документ = МассивДокументов[0];
		КонецЕсли;
				
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отгрузка_Документ) Тогда 
		МассивДокументов = ПолучитьПодчиненныеДокументы(Отгрузка_Документ, "ТК_ТТН", Новый Структура("ХозОперация,ПометкаУдаления", Справочники.ХозОперации.ТТН, Ложь), Истина);
		
		Если МассивДокументов.Количество() > 0 Тогда
			Отгрузка_ТТН = МассивДокументов[0];
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры 

Процедура ОбработатьРезерв(РезервСсылка, тПараметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РезервированиеЗаказовПокупателя.Ссылка КАК Ссылка,
	               |	РезервированиеЗаказовПокупателя.ПометкаУдаления КАК ПометкаУдаления,
	               |	РезервированиеЗаказовПокупателя.Номер КАК Номер,
	               |	РезервированиеЗаказовПокупателя.Дата КАК Дата,
	               |	РезервированиеЗаказовПокупателя.Проведен КАК Проведен,
	               |	РезервированиеЗаказовПокупателя.Организация КАК Организация,
	               |	РезервированиеЗаказовПокупателя.ХозОперация КАК ХозОперация,
	               |	РезервированиеЗаказовПокупателя.Контрагент КАК Контрагент,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВложенныйЗапрос.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВложенныйЗапрос.СрокПоставки КАК СрокПоставки,
	               |	ВложенныйЗапрос.Заказ КАК Заказ,
	               |	РезервированиеЗаказовПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		РезервированиеЗаказовПокупателя.Товары.Количество * РезервированиеЗаказовПокупателя.Товары.Коэффициент * ЕСТЬNULL(РезервированиеЗаказовПокупателя.Товары.ЕдиницаИзмерения.Вес, 0) КАК Вес,
	               |		Количество КАК КоличествоПроверено,
	               |		1 КАК Цена
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			МАКСИМУМ(ВложенныйЗапрос.ДокументОснованиеРегламентированныйУчет) КАК РегламентированныйУчет,
	               |			ВложенныйЗапрос.Резерв КАК Резерв,
	               |			ВложенныйЗапрос.СрокПоставки КАК СрокПоставки,
	               |			ВложенныйЗапрос.Заказ КАК Заказ
	               |		ИЗ
	               |			(ВЫБРАТЬ
	               |				РезервированиеЗаказовПокупателя.ДокументОснование.РегламентированныйУчет КАК ДокументОснованиеРегламентированныйУчет,
	               |				РезервированиеЗаказовПокупателя.Ссылка КАК Резерв,
	               |				РезервированиеЗаказовПокупателя.ДокументОснование.СрокПоставки КАК СрокПоставки,
	               |				РезервированиеЗаказовПокупателя.ДокументОснование КАК Заказ
	               |			ИЗ
	               |				Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	               |			ГДЕ
	               |				РезервированиеЗаказовПокупателя.Ссылка = &Ссылка
	               |			
	               |			ОБЪЕДИНИТЬ ВСЕ
	               |			
	               |			ВЫБРАТЬ
	               |				РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя.РегламентированныйУчет,
	               |				РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка,
	               |				РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя.СрокПоставки,
	               |				РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя
	               |			ИЗ
	               |				Документ.РезервированиеЗаказовПокупателя.РаспределениеОтгрузки КАК РезервированиеЗаказовПокупателяРаспределениеОтгрузки
	               |			ГДЕ
	               |				РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ВложенныйЗапрос.Резерв,
	               |			ВложенныйЗапрос.СрокПоставки,
	               |			ВложенныйЗапрос.Заказ) КАК ВложенныйЗапрос
	               |		ПО РезервированиеЗаказовПокупателя.Ссылка = ВложенныйЗапрос.Резерв
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", РезервСсылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Не удалось получить реквизиты Документа резервирование заказов запросом", тПараметры.тИдДокумента);
		Возврат;		
	КонецЕсли;
		
	Если Реквизиты.ПометкаУдаления Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Документ резервирование заказов помечен на удаление", РезервСсылка);
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Резерв %1 помечен на удаление!'; uk = 'Резерв %1 помічене на видалення!'"),
								Строка(РезервСсылка));
		Возврат;
	КонецЕсли;
	
	Если Не Реквизиты.Проведен Тогда 
		ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Документ резервирование заказов не проведен", РезервСсылка);
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Резерв %1 не проведен!'; uk = 'Резерв %1 не проведений!'"),
								Строка(РезервСсылка));
		Возврат;		
	КонецЕсли;
	
	// Заполняем реквизиты на форме
	ЗаказПокупателя = Реквизиты.Заказ;
	
	ЗаказПокупателяРеквизиты = Новый Структура;
	ЗаказПокупателяРеквизиты.Вставить("ХозОперация", 			Реквизиты.ХозОперация);
	ЗаказПокупателяРеквизиты.Вставить("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);	
	ЗаказПокупателяРеквизиты.Вставить("СрокПоставки", 			Реквизиты.СрокПоставки);
	
	Объект.Товары.Загрузить(Реквизиты.Товары.Выгрузить());	
	
	дКонтрагент = Реквизиты.Контрагент;
	дТочкаДоставки = Реквизиты.ТочкаДоставки;	
	Элементы.ДекорацияОтгрузкаФ1.Видимость = Реквизиты.РегламентированныйУчет;
	
	текСтатус = "НакладнаяНайдена";
	
	ДобавитьЛог(ЛогДействий, тПараметры.текДействие, "Резерв найден", РезервСсылка);		
	
	// Находим отгрузку
	
	МассивДокументов = ПолучитьПодчиненныеДокументы(ЗаказПокупателя, "РеализацияТоваров", Новый Структура("ХозОперация", Справочники.ХозОперации.РеализацияТоваров), Истина);
	
	Если МассивДокументов.Количество() > 0 Тогда
		Отгрузка_Документ 	= МассивДокументов[0];
		ЗаполнитьТаблицуТара(Отгрузка_Документ);
	КонецЕсли;				
	
	Если ЗначениеЗаполнено(Отгрузка_Документ) Тогда 
		МассивДокументов = ПолучитьПодчиненныеДокументы(Отгрузка_Документ, "ТК_ТТН", Новый Структура("ХозОперация,ПометкаУдаления", Справочники.ХозОперации.ТТН, Ложь), Истина);
		
		Если МассивДокументов.Количество() > 0 Тогда
			Отгрузка_ТТН = МассивДокументов[0];
		КонецЕсли;		
	КонецЕсли;
	
	Тара_ДоступенВводТары = Истина;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьПолученныйШК(ТекКод)

	ДанныеСканирования = ПроверитьИД(СокрЛП(ТекКод));	
	
	Если текСтатус = "ОжиданиеСканирования" ИЛИ текСтатус = "ПечатьНакладной" Тогда 		
		
		ОбработатьПоискНакладной(ДанныеСканирования);			
		ПоказатьОшибкуСканировния();		
		
	КонецЕсли;
	
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьВводТарыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппаВозвратнаяТара", ДопПараметры.ГруппаВозвратнаяТара);
	Запрос.УстановитьПараметр("Дата", 	ТекущаяДата());
	Запрос.УстановитьПараметр("ТипЦен", Справочники.СкладыКомпании.ТипЦенРозничный(ЗаказПокупателя.СкладКомпании));
	Запрос.УстановитьПараметр("тзТара", Объект.Тара.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	тзТара.Номенклатура КАК Номенклатура,
	               |	тзТара.Количество КАК Количество
	               |ПОМЕСТИТЬ тчСохраненнаяТара
	               |ИЗ
	               |	&тзТара КАК тзТара
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ втТара
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппаВозвратнаяТара)
	               |	И НЕ Номенклатура.ЭтоГруппа
	               |	И НЕ Номенклатура.ПометкаУдаления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТара.Ссылка КАК Номенклатура,
	               |	ЕСТЬNULL(ВложенныйЗапрос.Количество, 0) КАК Количество,
	               |	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена,
	               |	ЕСТЬNULL(ВложенныйЗапрос.Количество, 0) * ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Сумма
	               |ИЗ
	               |	втТара КАК втТара
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(
	               |				&Дата,
	               |				Номенклатура В
	               |						(ВЫБРАТЬ
	               |							втТара.Ссылка КАК Ссылка
	               |						ИЗ
	               |							втТара КАК втТара)
	               |					И ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	               |		ПО втТара.Ссылка = ЦеныСрезПоследних.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			тчСохраненнаяТара.Номенклатура КАК Номенклатура,
	               |			СУММА(тчСохраненнаяТара.Количество) КАК Количество
	               |		ИЗ
	               |			тчСохраненнаяТара КАК тчСохраненнаяТара
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			тчСохраненнаяТара.Номенклатура) КАК ВложенныйЗапрос
	               |		ПО втТара.Ссылка = ВложенныйЗапрос.Номенклатура";
	
	ВозвратнаяТара.Загрузить(Запрос.Выполнить().Выгрузить());	
	Тара_Сумма = ВозвратнаяТара.Итог("Сумма");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТовары(Документ)
	
	текДействие = "Заполнение тч Товары";
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументОбъект.ВыпускПродукции") Тогда 
		ИмяТаблицы = "Продукция";
	Иначе
		ИмяТаблицы = "Товары";
	КонецЕсли;
	
	тзТовары = Документ[ИмяТаблицы].Выгрузить();
	
	ТаблицаТовары = тзТовары.Скопировать();
	ТаблицаТовары.Очистить();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	
	Если ТипДокумента = Тип("ДокументОбъект.ВыпускПродукции") Тогда 
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	КонецЕсли;
	
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.КоличествоПроверено = 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("Номенклатура", Стр.Номенклатура);
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Стр.ХарактеристикаНоменклатуры);
		
		нСтроки = тзТовары.НайтиСтроки(СтруктураОтбора);
		КолВоСтрок = нСтроки.Количество();
		
		Если КолВоСтрок > 0 Тогда
			РеквизитыСтроки = нСтроки[0];
			
			Если КолВоСтрок > 1 Тогда
				ДобавитьЛог(ЛогДействий, текДействие, "В документе расхода найдено " + КолВоСтрок + " строк с номенклатурой", Строка(СтруктураОтбора.Номенклатура));			
			КонецЕсли;
		Иначе
			ДобавитьЛог(ЛогДействий, текДействие, "ОШИБКА! Не найдена номенклатура в документе расход", Строка(СтруктураОтбора.Номенклатура));			
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, РеквизитыСтроки);
		
		НоваяСтрока.Количество = Стр.КоличествоПроверено;
	КонецЦикла;
			
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ТаблицаТовары, СтруктураДействий, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения()); 

	Документ[ИмяТаблицы].Загрузить(ТаблицаТовары);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТара(Документ)
	
	Объект.Тара.Очистить();	
	ТипДокумента = ТипЗнч(Документ);	
	
	Если Не (ТипДокумента = Тип("ДокументСсылка.ВыпускПродукции")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров")) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ПолноеИмяДокумента = Метаданные.НайтиПоТипу(ТипДокумента).ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.Текст = "ВЫБРАТЬ
	               |	тчТара.Номенклатура КАК Номенклатура,
	               |	тчТара.Количество КАК Количество,
	               |	тчТара.Цена КАК Цена,
	               |	тчТара.Сумма КАК Сумма
	               |ИЗ
	               |	"+ПолноеИмяДокумента+".Тара КАК тчТара
	               |ГДЕ
	               |	тчТара.Ссылка = &Ссылка
	               |	И тчТара.Количество > 0";
	
	Объект.Тара.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьДокументНаВыгрузку(Документ, текДействие)
	
	ПланОбмена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "ПланОбмена", Неопределено);
	
	Если ПланОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, Документ);	
		ДобавитьЛог(ЛогДействий, текДействие, "Документ зарегистрирован в Плане обмена """ + Строка(ПланОбмена) + """", Документ);	
	Исключение		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка регистрации в Плане обмена ""%1""
                              |Ошибки: %2'; uk = 'Помилка реєстрації в Плані обміну ""%1""
                              |Помилки: %2'"),
						Строка(ПланОбмена),
						ОписаниеОшибки());
		ДобавитьЛог(ЛогДействий, текДействие, ТекстОшибки, Документ);	
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТТН_ПеремещениеВФилиал(ТТН, ПеремещениеВФилиал)
	
	ТТН.Дата = ПеремещениеВФилиал.Дата;
	ТТН.Статус = Перечисления.СтатусыТТН.Черновик;
	ТТН.ХозОперация = Справочники.ХозОперации.ТТН;
	ТТН.ВидПеревозки = "автоперевезення";
	ТТН.Отправитель = Справочники.Организации.ПолучитьПредставлениеДляПечати(ПеремещениеВФилиал.Организация, Истина,,,,,ТТН.Дата, "uk");
	ТТН.ДолжностьОтветственногоОтправителя = "комірник";
	
	ТТН.ПунктОтправитель = ПеремещениеВФилиал.СкладКомпании.ТочкаДоставки;
	ТТН.ПунктПолучатель = ПеремещениеВФилиал.СкладКомпанииПолучатель.ТочкаДоставки;
	
	ЗаполнитьЗначенияСвойств(ТТН, ПеремещениеВФилиал, "Организация,ПодразделениеКомпании");
	
	ЗаказПокупателя = Неопределено;
	Если ПеремещениеВФилиал.РаспределениеОтгрузки.Количество() > 0 Тогда 
		ЗаказПокупателя = ПеремещениеВФилиал.РаспределениеОтгрузки[0].ЗаказПокупателя;
	ИначеЕсли ТипЗнч(ПеремещениеВФилиал.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаказПокупателя = ПеремещениеВФилиал.ДокументОснование;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Контрагент");
	СтруктураРеквизитов.Вставить("Перевозчик");
	СтруктураРеквизитов.Вставить("Автомобиль");
	СтруктураРеквизитов.Вставить("Водитель");
	СтруктураРеквизитов.Вставить("НомерПрав");
	СтруктураРеквизитов.Вставить("Заказчик", ТТН.Отправитель);
	СтруктураРеквизитов.Вставить("Прицеп");
	СтруктураРеквизитов.Вставить("Основание");
	СтруктураРеквизитов.Вставить("НомерПЛ");
	СтруктураРеквизитов.Вставить("Экспедитор");
		
	Если ЗначениеЗаполнено(ЗаказПокупателя) Тогда 		
	
		МассивИменСвойствДоговора = Новый Массив;
		МассивИменСвойствДоговора.Добавить("ТТН_Автомобиль");
		МассивИменСвойствДоговора.Добавить("ТТН_Заказчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Перевозчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Водитель");		
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ЗаказПокупателя);
		Запрос.УстановитьПараметр("МассивСвойств", МассивИменСвойствДоговора);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ЗаказПокупателя.Контрагент КАК Контрагент,
		               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		               |ИЗ
		               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		               |ГДЕ
		               |	ЗаказПокупателя.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ДополнительныеСведения.Свойство.Имя КАК СвойствоИмя,
		               |	ДополнительныеСведения.Значение КАК Значение
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Объект = ВЫРАЗИТЬ(&Ссылка КАК Документ.ЗаказПокупателя).ДоговорВзаиморасчетов
		               |	И ДополнительныеСведения.Свойство.Имя В(&МассивСвойств)";
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ВыборкаРеквизиты  = МассивРезультатов[0].Выбрать();
		ВыборкаПоДоговору = МассивРезультатов[1].Выбрать();
		
		Если ВыборкаРеквизиты.Следующий() Тогда 
			СтруктураРеквизитов.Контрагент = ВыборкаРеквизиты.Контрагент;		
			СтруктураРеквизитов.Заказчик = ВыборкаРеквизиты.Контрагент;
		КонецЕсли;
		
		Пока ВыборкаПоДоговору.Следующий() Цикл 
			
			Если ВыборкаПоДоговору.СвойствоИмя = "ТТН_Автомобиль" Тогда 
				
				СтруктураРеквизитов.Автомобиль = ВыборкаПоДоговору.Значение;
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Водитель" Тогда 
				
				Водитель = ВыборкаПоДоговору.Значение;				
				
				СтруктураРеквизитов.Водитель = Водитель;
				СтруктураРеквизитов.Экспедитор = Водитель;
				
				НомерПрав = "";
				Если ТипЗнч(Водитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
					НомерПрав = Справочники.ФизическиеЛица.ПолучитьНомерВодительскогоУдостоверения(Водитель, ТТН.Дата);
				КонецЕсли;
				
				СтруктураРеквизитов.НомерПрав = НомерПрав;
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Перевозчик" Тогда 				
				
				Перевозчик = ВыборкаПоДоговору.Значение;
				ТипРеквизитаПеревозчик = ТипЗнч(Перевозчик);
				
				СтруктураРеквизитов.Перевозчик = Перевозчик;
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Заказчик" Тогда 
				СтруктураРеквизитов.Заказчик = ВыборкаПоДоговору.Значение;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ТТН, СтруктураРеквизитов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ПеремещениеВФилиал.Ссылка);	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПеремещениеТоваровТовары.Ссылка КАК Документ,
	               |	СУММА(ЕСТЬNULL(ПеремещениеТоваровТовары.ЕдиницаИзмерения.Вес, 0) * ПеремещениеТоваровТовары.КоличествоБазовое) КАК Масса
	               |ИЗ
	               |	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	               |ГДЕ
	               |	ПеремещениеТоваровТовары.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПеремещениеТоваровТовары.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Масса = 0;
	Если Выборка.Следующий() Тогда 
		Масса = Выборка.Масса;
	КонецЕсли;
	
	ТТН.Источники.Очистить();
	
	НоваяСтрока = ТТН.Источники.Добавить();
	НоваяСтрока.Документ = ПеремещениеВФилиал.Ссылка;
	НоваяСтрока.Масса = Масса;
	
	
КонецПроцедуры

&НаСервере
Функция Отгрузка_ВыпускСРеализацией()
	
	ВсеОк = Истина;	
	текДействие = "Формирование выпуска с реализацией";
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документа выпуск продукции с реализацией");	

	НачатьТранзакцию();
	
	Попытка
		ДокументВыпуск = Неопределено;				
		ЭтоНовыйДокумент = Ложь;
		
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
			ДокументВыпуск = Отгрузка_Документ.ПолучитьОбъект();
		КонецЕсли;
		
		Если ДокументВыпуск = Неопределено Тогда 
			ДокументВыпуск = Документы.ВыпускПродукции.СоздатьДокумент();				
			ЭтоНовыйДокумент = Истина;
		КонецЕсли;	
		
		ДокументВыпуск.Заполнить(ЗаказПокупателя);				
		ДокументВыпуск.Дата = ТекущаяДатаСеанса();
		
		Если Не ЗаказПокупателяРеквизиты.ДокументКорректен Тогда
			ЗаполнитьТаблицуТовары(ДокументВыпуск);
			Документы.ВыпускПродукции.ПерезаполнитьИнгридиенты(ДокументВыпуск);
		КонецЕсли;		
		
		ДокументВыпуск.Тара.Загрузить(Объект.Тара.Выгрузить());
		
		ДокументВыпуск.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		ДокументВыпуск.Записать(РежимЗаписиДокумента.Проведение);
		
		ЗафиксироватьТранзакцию();			
		
		ДобавитьЛог(ЛогДействий, текДействие, "Записан документ", Строка(ДокументВыпуск.Ссылка));	
		
		Если ЭтоНовыйДокумент Тогда 
			Документы.ВыпускПродукции.ЗаполнитьРеквизитыТТНПоУмолчанию(ДокументВыпуск);
		КонецЕсли;
		
		Отгрузка_Документ = ДокументВыпуск.Ссылка;				
	Исключение
		
		ВсеОк = Ложь;		
		ОтменитьТранзакцию();	
		
		ТекстОшибки = ОписаниеОшибки();
		
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка записи документа: " + ТекстОшибки);	
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать выпуск для %1.
                      |Ошибки: %2'; uk = 'Не вдалося записати випуск для %1.
                      |Помилки: %2'"),
				ЗаказПокупателя,
				ТекстОшибки),
			ЗаказПокупателя);
		
	КонецПопытки;
	
	Возврат ВсеОк;
КонецФункции

&НаСервере
Функция Отгрузка_Реализация()	
	
	ВсеОк = Истина;	
	текДействие = "Формирование реализации";
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документа реализациея товаров");	

	НачатьТранзакцию();
	
	тчТовары = Новый ТаблицаЗначений;
	тчТовары.Колонки.Добавить("Номенклатура");
	тчТовары.Колонки.Добавить("ХарактеристикаНоменклатуры");
	тчТовары.Колонки.Добавить("ЕдиницаИзмерения"); 
	тчТовары.Колонки.Добавить("Количество");
	
	Для Каждого СтрТовар Из Объект.Товары Цикл
		Если стрТовар.КоличествоПроверено = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		новСтрока = тчТовары.Добавить();
		ЗаполнитьЗначенияСвойств(новСтрока, СтрТовар);
		новСтрока.Количество = стрТовар.КоличествоПроверено;							
	КонецЦикла;
	
	Если тчТовары.Количество() = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Нет заполненной номенклатуры для создания отгрузки по заказу", Строка(ЗаказПокупателя.Ссылка));
		ВсеОк = Ложь;
		Возврат ВсеОк;
	КонецЕсли;

	
	Попытка
		ДокРеализация = Документы.РеализацияТоваров.СоздатьДокумент(); //Всегда создается только новый документ?
		ДокРеализация.Дата = ТекущаяДата();
		ДокРеализация.Автор = Пользователи.ТекущийПользователь();
		ДокРеализация.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.РеализацияТоваров");
		ДокРеализация.ДокументОснование = ЗаказПокупателя.Ссылка;
		
		СписокСвойств = "РегламентированныйУчет, Организация, ПодразделениеКомпании, ТорговаяПлощадка, СкладКомпании, Контрагент, ДоговорВзаиморасчетов, ВалютаДокумента, КурсДокумента, ТочкаДоставки";
		ЗаполнитьЗначенияСвойств(ДокРеализация, ЗаказПокупателя, СписокСвойств);	
		
		Для Каждого стрТовар Из тчТовары Цикл 	
			новСтрока = ДокРеализация.Товары.Добавить();		
			ЗаполнитьЗначенияСвойств(новСтрока, стрТовар, "Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество");
		КонецЦикла;
		
		тзТовары = ДокРеализация.Товары.Выгрузить();
		тзТовары.Колонки.Добавить("ХарактеристикиИспользуются");
		тзТовары.Колонки.Добавить("ГруппаЦенообразования");
		тзТовары.Колонки.Добавить("ТипЦен");
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",ДокРеализация.Организация,ДокРеализация.ТорговаяПлощадка,"Реализация"));
		СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокРеализация))); //РеквизитыОснование.Ссылка)));
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСумму");

		///Цены из договора по группе ценообразования
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",ДокРеализация.РегламентированныйУчет);

		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(ДокРеализация));
		
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(тзТовары,СтруктураДействий,Неопределено);
	
		ДокРеализация.ТипЦен = тзТовары[0].ТипЦен;
		ДокРеализация.Товары.Загрузить(тзТовары);
		
		ОбработкаТабличнойЧастиСервер.СортироватьТЧ(ДокРеализация.Товары);
			
		//ДокРеализация.СуммаДокумента = ДокРеализация.Товары.Итог("СуммаВсего");		
		//ДокРеализация.УстановитьНовыйНомер();
		
		ДокРеализация.Тара.Загрузить(Объект.Тара.Выгрузить());
		
		ВсеОк = ДокРеализация.ПроверитьЗаполнение();
		Если ВсеОк Тогда 		
			Попытка
				ДокРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);		
				ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ расхода", Строка(ДокРеализация.Ссылка)); 
				
				ЗафиксироватьТранзакцию();			
		
				Отгрузка_Документ = ДокРеализация.Ссылка;
			Исключение
				ВсеОк = Ложь;
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
				ДобавитьЛог(ЛогДействий, текДействие, "Ошибка проведения документа ""Реализация товаров"". Ошибки: " + ОписаниеОшибки()); 
				
				ОтменитьТранзакцию();	
	
			КонецПопытки;
		Иначе			
			ДобавитьЛог(ЛогДействий, текДействие, "Документ реализация не может быть проведен. Не заполнены основные реквизиты");	
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Документ реализация не может быть проведен. Не заполнены основные реквизиты'; uk = 'Документ реалізації не може бути проведений. Не заповнені головні реквізити'"));
		КонецЕсли;
	Исключение
		ВсеОк = Ложь;		
		Ошибки = ОписаниеОшибки();
		
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка проведения реализации: " + Ошибки);
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Возникла ошибка при проведении документа Реализация товаров. Ошибка: %1'; uk = 'Виникла помилка при проведенні документу Реалізація товарів. Помилка: %1'"),
				Ошибки));
	КонецПопытки;
	
	Возврат ВсеОк;

КонецФункции

&НаСервере
Функция Отгрузка_ПеремещениеИВыпуск()
	
	ВсеОк = Истина;
	текДействие = "Формирование выпуска и перемещения";	
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документов");	
	
	ДокВыпуск = Неопределено;
	ДокПеремещениеВФилиал = Неопределено;
	ДокПеремещениеИзФилиала = Неопределено;
	ДокТТН = Неопределено;
	
	НачатьТранзакцию();
	
	Попытка
		
		Если Не Отгрузка_Выпуск.Пустая() Тогда 
			ДокВыпуск = Отгрузка_Выпуск.ПолучитьОбъект();
			
			Если ДокВыпуск.ПометкаУдаления Тогда 
				ДокВыпуск.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;								
		Иначе
			ДокВыпуск = Документы.ВыпускПродукции.СоздатьДокумент();			
		КонецЕсли;
				
		ДокВыпуск.Заполнить(ЗаказПокупателя);
		ДокВыпуск.Дата = ТекущаяДатаСеанса();
		
		Если Не ЗаказПокупателяРеквизиты.ДокументКорректен Тогда
			ЗаполнитьТаблицуТовары(ДокВыпуск);
			Документы.ВыпускПродукции.ПерезаполнитьИнгридиенты(ДокВыпуск);
		КонецЕсли;				
		
		ДокВыпуск.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		ДокВыпуск.Записать(РежимЗаписиДокумента.Проведение);
		
		Отгрузка_Выпуск = ДокВыпуск.Ссылка;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ", Строка(ДокВыпуск.Ссылка));	
		
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
			ДокПеремещениеВФилиал = Отгрузка_Документ.ПолучитьОбъект();
			
			Если ДокПеремещениеВФилиал.ПометкаУдаления Тогда 
				ДокПеремещениеВФилиал.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;			
		Иначе
			ДокПеремещениеВФилиал = Документы.ПеремещениеТоваров.СоздатьДокумент();			
		КонецЕсли;
				
		ДокПеремещениеВФилиал.Заполнить(ДокВыпуск.Ссылка);				
		ДокПеремещениеВФилиал.Дата = ДокВыпуск.Дата + 1;	
		
		ДокПеремещениеВФилиал.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		ДокПеремещениеВФилиал.Записать(РежимЗаписиДокумента.Проведение);
		
		Отгрузка_Документ = ДокПеремещениеВФилиал.Ссылка;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ Перемещение в филиал", Строка(ДокПеремещениеВФилиал.Ссылка));	
		
		МассивПеремещений = ПолучитьПодчиненныеДокументы(ДокПеремещениеВФилиал.Ссылка, "ПеремещениеТоваров", Новый Структура("ХозОперация", Справочники.ХозОперации.ПеремещениеТоваровИзФилиала), Истина);
		
		Если МассивПеремещений.Количество() > 0 Тогда
			ДокПеремещениеИзФилиала = МассивПеремещений[0].ПолучитьОбъект();			
			
			Если ДокПеремещениеИзФилиала.ПометкаУдаления Тогда 
				ДокПеремещениеИзФилиала.УстановитьПометкуУдаления(Ложь);	
			КонецЕсли;							
		Иначе
			ДокПеремещениеИзФилиала = Документы.ПеремещениеТоваров.СоздатьДокумент();						
		КонецЕсли;
				
		Если Не ДокПеремещениеИзФилиала.Проведен Тогда						
			ДокПеремещениеИзФилиала.Заполнить(ДокПеремещениеВФилиал.Ссылка);								
			ДокПеремещениеИзФилиала.Дата = ДокПеремещениеВФилиал.Дата + 1;			
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.Запись);
			
			ДобавитьЛог(ЛогДействий, текДействие, "Записан документ Перемещение из филиала", Строка(ДокПеремещениеИзФилиала.Ссылка));	
		ИначеЕсли ДокПеремещениеИзФилиала.Проведен И Не ДокПеремещениеИзФилиала.Дата > ДокПеремещениеВФилиал.Дата Тогда 
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ДокПеремещениеИзФилиала.Дата = ДокПеремещениеВФилиал.Дата + 1;
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.Проведение);
			
			ДобавитьЛог(ЛогДействий, текДействие, "Перепроведен документ Перемещение из филиала. Изменена дата", Строка(ДокПеремещениеИзФилиала.Ссылка));	
		Иначе
			ДобавитьЛог(ЛогДействий, текДействие, "Документ Перемещение из филиала не изменен, т.к. уже был проведен", Строка(ДокПеремещениеИзФилиала.Ссылка));	
		КонецЕсли;
			
		Если Не Отгрузка_ТТН.Пустая() Тогда 
			ДокТТН = Отгрузка_ТТН.ПолучитьОбъект();
			
			Если ДокТТН.ПометкаУдаления Тогда 
				ДокТТН.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;			
		Иначе
			ДокТТН = Документы.ТК_ТТН.СоздатьДокумент();
		КонецЕсли;
		
		ЗаполнитьТТН_ПеремещениеВФилиал(ДокТТН, ДокПеремещениеВФилиал);
		
		ДокТТН.Записать(РежимЗаписиДокумента.Запись);
		
		Отгрузка_ТТН = ДокТТН.Ссылка;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Записан документ ТТН", Строка(ДокТТН.Ссылка));	
		
		ЗафиксироватьТранзакцию();
		
		Если Не ДокПеремещениеИзФилиала.Проведен Тогда 
			ЗарегистрироватьДокументНаВыгрузку(ДокПеремещениеИзФилиала.Ссылка, текДействие);
		КонецЕсли;
	Исключение
		ВсеОк = Ложь;		
		ОтменитьТранзакцию();	
		
		ТекстОшибки = ОписаниеОшибки();	
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибки: " + ТекстОшибки);	
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось провести отгрузку для %1.
                      |Ошибки: %2'; uk = 'Не вдалося провести відвантаження для %1.
                      |Помилки: %2'"),
				ЗаказПокупателя,
				ТекстОшибки),
			ЗаказПокупателя);		
		
	КонецПопытки;
	
	Возврат ВсеОк;
	
КонецФункции

&НаСервере
Функция Отгрузка_Перемещение()
	
	ВсеОк = Истина;
	текДействие = "Формирование перемещения";
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документов");	
	
	ДокПеремещениеВФилиал = Неопределено;
	ДокПеремещениеИзФилиала = Неопределено;
	ДокТТН = Неопределено;
	
	НачатьТранзакцию();
	
	Попытка					
				
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
			ДокПеремещениеВФилиал = Отгрузка_Документ.ПолучитьОбъект();
			
			Если ДокПеремещениеВФилиал.ПометкаУдаления Тогда 
				ДокПеремещениеВФилиал.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;			
		Иначе
			ДокПеремещениеВФилиал = Документы.ПеремещениеТоваров.СоздатьДокумент();
		КонецЕсли;
		
		ДокПеремещениеВФилиал.Заполнить(ЗаказПокупателя);	
		
		Если Не ЗаказПокупателяРеквизиты.ДокументКорректен Тогда
			ЗаполнитьТаблицуТовары(ДокПеремещениеВФилиал);
		КонецЕсли;				
		
		ДокПеремещениеВФилиал.Дата = ТекущаяДатаСеанса(); //ДокВыпуск.Дата + 1; ИЛИ Заказ.СрокПоставки + (60*60*12);	
		ДокПеремещениеВФилиал.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		ДокПеремещениеВФилиал.Записать(РежимЗаписиДокумента.Проведение);
				
		Отгрузка_Документ = ДокПеремещениеВФилиал.Ссылка;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ Перемещение в филиал", Строка(ДокПеремещениеВФилиал.Ссылка));	
		
		МассивПеремещений = ПолучитьПодчиненныеДокументы(ДокПеремещениеВФилиал.Ссылка, "ПеремещениеТоваров", Новый Структура("ХозОперация", Справочники.ХозОперации.ПеремещениеТоваровИзФилиала), Истина);
		
		Если МассивПеремещений.Количество() > 0 Тогда
			ДокПеремещениеИзФилиала = МассивПеремещений[0].ПолучитьОбъект();			
			
			Если ДокПеремещениеИзФилиала.ПометкаУдаления Тогда 
				ДокПеремещениеИзФилиала.УстановитьПометкуУдаления(Ложь);	
			КонецЕсли;							
		Иначе
			ДокПеремещениеИзФилиала = Документы.ПеремещениеТоваров.СоздатьДокумент();
		КонецЕсли;
		
		Если Не ДокПеремещениеИзФилиала.Проведен Тогда 
			ДокПеремещениеИзФилиала.Заполнить(ДокПеремещениеВФилиал.Ссылка);
			ДокПеремещениеИзФилиала.Дата = ДокПеремещениеВФилиал.Дата + 1;
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.Запись);
		
			ДобавитьЛог(ЛогДействий, текДействие, "Записан документ Перемещение из филиала", Строка(ДокПеремещениеИзФилиала.Ссылка));	
			
		ИначеЕсли ДокПеремещениеИзФилиала.Проведен И Не ДокПеремещениеИзФилиала.Дата > ДокПеремещениеВФилиал.Дата Тогда 
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ДокПеремещениеИзФилиала.Дата = ДокПеремещениеВФилиал.Дата + 1;
			ДокПеремещениеИзФилиала.Записать(РежимЗаписиДокумента.Проведение);
			
			ДобавитьЛог(ЛогДействий, текДействие, "Перепроведен документ Перемещение из филиала. Изменена дата", Строка(ДокПеремещениеИзФилиала.Ссылка));	
		Иначе
			ДобавитьЛог(ЛогДействий, текДействие, "Документ Перемещение из филиала не изменен, т.к. уже был проведен", Строка(ДокПеремещениеИзФилиала.Ссылка));	
		КонецЕсли;
		
		Если Не Отгрузка_ТТН.Пустая() Тогда 
			ДокТТН = Отгрузка_ТТН.ПолучитьОбъект();
			
			Если ДокТТН.ПометкаУдаления Тогда 
				ДокТТН.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;			
		Иначе
			ДокТТН = Документы.ТК_ТТН.СоздатьДокумент();
		КонецЕсли;
		
		ЗаполнитьТТН_ПеремещениеВФилиал(ДокТТН, ДокПеремещениеВФилиал);
		
		ДокТТН.Записать(РежимЗаписиДокумента.Запись);
		
		Отгрузка_ТТН = ДокТТН.Ссылка;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Записан документ ТТН", Строка(ДокТТН.Ссылка));	
		
		ЗафиксироватьТранзакцию();

		Если Не ДокПеремещениеИзФилиала.Проведен Тогда 
			ЗарегистрироватьДокументНаВыгрузку(ДокПеремещениеИзФилиала.Ссылка, текДействие);		
		КонецЕсли;
	Исключение
		ВсеОк = Ложь;		
		ОтменитьТранзакцию();	
		
		ТекстОшибки = ОписаниеОшибки();	
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибки: " + ТекстОшибки);	
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось провести отгрузку для %1.
                      |Ошибки: %2'; uk = 'Не вдалося провести відвантаження для %1.
                      |Помилки: %2'"),
				ЗаказПокупателя,
				ТекстОшибки),
			ЗаказПокупателя);				
	КонецПопытки;
	
	Возврат ВсеОк;
	
КонецФункции

&НаСервере
Процедура СформироватьРасходНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);

	текДействие = "Формирование расхода";		
	
	Если Объект.Товары.Итог("КоличествоПроверено") = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "В документе нет провереных строк. Расход не сформирован");
		Возврат;
	КонецЕсли;		
	
	ВсеОк = Истина;
	
	Если ЗаказПокупателяРеквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателя
		ИЛИ ЗаказПокупателяРеквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяФизОбмен Тогда 
		
		ВсеОк = Отгрузка_ВыпускСРеализацией();
	ИначеЕсли ЗаказПокупателяРеквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство Тогда 
		ВсеОк = Отгрузка_ПеремещениеИВыпуск();
	ИначеЕсли ЗаказПокупателяРеквизиты.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутренний Тогда 
		ВсеОк = Отгрузка_Перемещение();
	ИначеЕсли дРезерв Тогда 
		ВсеОк = Отгрузка_Реализация();
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ВсеОк Тогда 
		текСтатус = "ПечатьНакладной";
		ЗаписатьЛог();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьКомандуПечати(НаПринтер = Ложь)
	
	МассивПечатныхФорм = Новый Массив;
	
	Если ЗаказПокупателяРеквизиты.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателя") 
		ИЛИ ЗаказПокупателяРеквизиты.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателяФизОбмен") Тогда 
		
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ВыпускПродукции") Тогда 		
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_Документ);
			СтруктураПФ.Вставить("СтрокаИменПФ", "РасходнаяНакладная,РасходнаяНакладная,РасходнаяНакладная,РасходнаяНакладная");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ВыпускПродукции");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);
			
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_Документ);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ДекларацияПроизводителя");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ВыпускПродукции");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);			
			
			Если Не Отгрузка_ТТН.Пустая() Тогда 
				СтруктураПФ = Новый Структура;
				СтруктураПФ.Вставить("Документ", Отгрузка_ТТН);
				СтруктураПФ.Вставить("СтрокаИменПФ", "ТТН_ФС,ТТН_ФС");
				СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ТК_ТТН");
				
				МассивПечатныхФорм.Добавить(СтруктураПФ);								
			Иначе
				СтруктураПФ = Новый Структура;
				СтруктураПФ.Вставить("Документ", Отгрузка_Документ);
				СтруктураПФ.Вставить("СтрокаИменПФ", "ТТН_ФС,ТТН_ФС");
				СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ВыпускПродукции");
				
				МассивПечатныхФорм.Добавить(СтруктураПФ);												
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ЗаказПокупателяРеквизиты.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателяВнутреннийПроизводство") Тогда 
		
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 		
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_Документ);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ПеремещениеТоваров,ПеремещениеТоваров,ПеремещениеТоваров,ПеремещениеТоваров");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ПеремещениеТоваров");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);	
		КонецЕсли;
		
		Если Не Отгрузка_Выпуск.Пустая() Тогда 
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_Выпуск);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ДекларацияПроизводителя");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ВыпускПродукции");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);			
		КонецЕсли;	
		
		Если Не Отгрузка_ТТН.Пустая() Тогда 
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_ТТН);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ТТН_ФС,ТТН_ФС");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ТК_ТТН");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);
		КонецЕсли;
		
	ИначеЕсли ЗаказПокупателяРеквизиты.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателяВнутренний") Тогда 
		
		Если ЗначениеЗаполнено(Отгрузка_Документ) И ТипЗнч(Отгрузка_Документ) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 		
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_Документ);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ПеремещениеТоваров,ПеремещениеТоваров,ПеремещениеТоваров,ПеремещениеТоваров");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ПеремещениеТоваров");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);	
		КонецЕсли;
		
		Если Не Отгрузка_ТТН.Пустая() Тогда 
			СтруктураПФ = Новый Структура;
			СтруктураПФ.Вставить("Документ", Отгрузка_ТТН);
			СтруктураПФ.Вставить("СтрокаИменПФ", "ТТН_ФС,ТТН_ФС");
			СтруктураПФ.Вставить("ИмяМенеджераПечати", "Документ.ТК_ТТН");
			
			МассивПечатныхФорм.Добавить(СтруктураПФ);
		КонецЕсли;		
		
	КонецЕсли;
	
	Для Каждого СтруктураПФ Из МассивПечатныхФорм Цикл 
		Если НаПринтер Тогда 
			ТК_УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(СтруктураПФ.ИмяМенеджераПечати, СтруктураПФ.СтрокаИменПФ, СтруктураПФ.Документ, Новый Структура());
		Иначе
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(СтруктураПФ.ИмяМенеджераПечати, СтруктураПФ.СтрокаИменПФ, СтруктураПФ.Документ, ЭтаФорма, Новый Структура());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьИД(Знач ИД)
	тНовИД = "";
	  
	Символица = "АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЬЮЯЫЭ";
	Наим=СокрЛП(ВРег(ИД));
	Для й=1 По СтрДлина(Наим) Цикл
		тСимвол = Сред(Наим,й,1);
		Если Найти(Символица,тСимвол)>0 Тогда
			
			Если тСимвол ="А" Тогда 
				тНовИД = тНовИД + "f";
			ИначеЕсли тСимвол="Б" Тогда 
				тНовИД=тНовИД+",";
			ИначеЕсли тСимвол="В" Тогда 
				тНовИД=тНовИД+"d";
			ИначеЕсли тСимвол="Г" Тогда 
				тНовИД=тНовИД+"u";
			ИначеЕсли тСимвол="Д" Тогда 
				тНовИД=тНовИД+"l";
			ИначеЕсли тСимвол="Е" Тогда 
				тНовИД=тНовИД+"t";
			ИначеЕсли тСимвол="Ё" Тогда 
				тНовИД=тНовИД+"`";
			ИначеЕсли тСимвол="Ж" Тогда 
				тНовИД=тНовИД+";";
			ИначеЕсли тСимвол="З" Тогда 
				тНовИД=тНовИД+"p";
			ИначеЕсли тСимвол="И" Тогда 
				тНовИД=тНовИД+"b";
			ИначеЕсли тСимвол="Й" Тогда 
				тНовИД=тНовИД+"q";
			ИначеЕсли тСимвол="К" Тогда 
				тНовИД=тНовИД+"r";
			ИначеЕсли тСимвол="Л" Тогда 
				тНовИД=тНовИД+"k";
			ИначеЕсли тСимвол="М" Тогда 
				тНовИД=тНовИД+"v";
			ИначеЕсли тСимвол="Н" Тогда 
				тНовИД=тНовИД+"y";
			ИначеЕсли тСимвол="О" Тогда 
				тНовИД=тНовИД+"j";
			ИначеЕсли тСимвол="П" Тогда 
				тНовИД=тНовИД+"g";
			ИначеЕсли тСимвол="Р" Тогда 
				тНовИД=тНовИД+"h";
			ИначеЕсли тСимвол="С" Тогда 
				тНовИД=тНовИД+"c";
			ИначеЕсли тСимвол="Т" Тогда 
				тНовИД=тНовИД+"n";
			ИначеЕсли тСимвол="У" Тогда 
				тНовИД=тНовИД+"e";
			ИначеЕсли тСимвол="Ф" Тогда 
				тНовИД=тНовИД+"a";
			ИначеЕсли тСимвол="Х" Тогда 
				тНовИД=тНовИД+"[";
			ИначеЕсли тСимвол="Ц" Тогда 
				тНовИД=тНовИД+"w";
			ИначеЕсли тСимвол="Ч" Тогда 
				тНовИД=тНовИД+"x";
			ИначеЕсли тСимвол="Ш" Тогда 
				тНовИД=тНовИД+"i";
			ИначеЕсли тСимвол="Щ" Тогда 
				тНовИД=тНовИД+"o";
			ИначеЕсли тСимвол="Ъ" Тогда 
				тНовИД=тНовИД+"]";
			ИначеЕсли тСимвол="Ь" Тогда 
				тНовИД=тНовИД+"m";
			ИначеЕсли тСимвол="Ю" Тогда 
				тНовИД=тНовИД+".";
			ИначеЕсли тСимвол="Я" Тогда 
				тНовИД=тНовИД+"z";
			ИначеЕсли тСимвол="Ы" Тогда 
				тНовИД=тНовИД+"s";
			ИначеЕсли тСимвол="Э" Тогда 
				тНовИД=тНовИД+"'";
			КонецЕсли;	
			
			
		Иначе
			тНовИД = тНовИД + тСимвол;
		КонецЕсли;
		
	КонецЦикла;
	
	НовИД = НРег(тНовИД);
	Возврат ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(НовИД);
КонецФункции

&НаСервере
Функция ПолучитьПодчиненныеДокументы(Документ, ТипПодчиненного, Отборы, ПолучитьМассив = Ложь) 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	
	ПолноеИмяДокумента = "Документ." + ТипПодчиненного;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СвязанныеДокументы.Ссылка КАК Ссылка,
	               |	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).ХозОперация КАК ХозОперация,
	               |	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).Проведен КАК Проведен,
	               |	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).ПометкаУдаления КАК ПометкаУдаления,
	               |	ВЫБОР
	               |		КОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).Проведен
	               |			ТОГДА 1
	               |		КОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).ПометкаУдаления
	               |			ТОГДА 3
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Порядок,
	               |	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1).Дата КАК Дата
	               |ИЗ
	               |	КритерийОтбора.СвязанныеДокументы(&Документ) КАК СвязанныеДокументы
	               |ГДЕ
	               |	СвязанныеДокументы.Ссылка ССЫЛКА %1";
	
	Если ТипЗнч(Отборы) = Тип("Структура") Тогда 
		Для Каждого Стр Из Отборы Цикл 
			ИмяПараметра = Стр.Ключ;
			Значение = Стр.Значение;
			
			ТекстЗапроса = ТекстЗапроса + Символы.ПС + "И ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК %1)."+ ИмяПараметра +" = &" + ИмяПараметра;
			
			Запрос.УстановитьПараметр(ИмяПараметра, Значение);
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + "
								               |УПОРЯДОЧИТЬ ПО
								               |	Порядок,
								               |	Дата УБЫВ";
	
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ПолноеИмяДокумента);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Если ПолучитьМассив Тогда 
		Возврат ТЗ.ВыгрузитьКолонку("Ссылка");
	Иначе
		Возврат ТЗ;
	КонецЕсли;
	
КонецФункции

#Область Логгирование

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьЛог(ЛогДействий, Действие = "", Информация = "", Данные = "")	

	ЗаписьЛогаСтрока = "";	
	
	СтруктураСтроки = Новый Структура("Действие, Инфо, Данные", Действие, Информация, Данные);
	Для Каждого Стр Из СтруктураСтроки Цикл 
		Если Не ПустаяСтрока(Стр.Значение) Тогда 
			текПараметр = """" + Стр.Ключ + """=>""" + Стр.Значение + """";
			ЗаписьЛогаСтрока = ?(Не ПустаяСтрока(ЗаписьЛогаСтрока), ЗаписьЛогаСтрока + ",", "") + текПараметр;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ЗаписьЛогаСтрока) Тогда 
		ЛогДействий.ДобавитьСтроку(Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd_HH:mm:ss") + "|" + ЗаписьЛогаСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЛог()
	
	УстановитьПривилегированныйРежим(Истина);

	Если ЛогДействий.КоличествоСтрок() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаЗаписиЛога = ТекущаяДата();
	
	НаборЗаписей = РегистрыСведений.ДействияОперацииКонтроля.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДатаРегистрации.Установить(ДатаЗаписиЛога);
	НаборЗаписей.Отбор.Пользователь.Установить(Кладовщик);
	НаборЗаписей.Отбор.ДокументПроверки.Установить(ЗаказПокупателя);
		
	НоваяЗапиь = НаборЗаписей.Добавить();
	НоваяЗапиь.ДатаРегистрации	= ДатаЗаписиЛога;
	НоваяЗапиь.Пользователь     = Кладовщик;
	НоваяЗапиь.ДокументПроверки = ЗаказПокупателя;
	НоваяЗапиь.Действия 		= ЛогДействий.ПолучитьТекст();	
	НаборЗаписей.Записать();
	
	ЛогДействий.Очистить();

КонецПроцедуры


#КонецОбласти


#Область ОбработкаОшибокСканирования

&НаКлиенте
Процедура ПоказатьОшибкуСканировния()		
	Если ПустаяСтрока(ОшибкаСканирования) Тогда 
		Возврат;
	КонецЕсли;	
			
	Элементы.ДекорацияНадписьОшибкаСканирования.Заголовок = ОшибкаСканирования;
	Элементы.ГруппаОшибкиСканирования.Видимость = Истина;
	
	текСтатус = "ОжиданиеСканирования";
	УстановитьСтраницуПоСтатусу();
	
	ПодключитьОбработчикОжидания("ОчиститьОшибкиСканирования", 5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОшибкиСканирования()	
	ГруппаОшибкиСканирования = Элементы.ГруппаОшибкиСканирования;
	
	Если ГруппаОшибкиСканирования.Видимость Тогда 
		ГруппаОшибкиСканирования.Видимость = Ложь;
	КонецЕсли;
	
	ОшибкаСканирования = "";	
КонецПроцедуры


#КонецОбласти



#КонецОбласти
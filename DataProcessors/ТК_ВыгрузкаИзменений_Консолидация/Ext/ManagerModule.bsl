Процедура УстановитьУзелОбменаУСтрокДерева(Дерево, УзелОбмена)
	
	Для Каждого Строка Из Дерево Цикл
		Если Строка.ЭтоГруппа Тогда
			УстановитьУзелОбменаУСтрокДерева(Строка.Строки, УзелОбмена);
		Иначе
			Строка.СсылкаНаУзелОбмена = УзелОбмена;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьПакетДанных(ПравилаОбмена, УзелОбмена, СтатусВыгрузки, Выгружено, Ошибки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	
	ЧтениеХМЛ = Новый ЧтениеXML;
	
	ЧтениеХМЛ.УстановитьСтроку(ПравилаОбмена.Получить());
	ЧтениеХМЛ.Прочитать();
	
	Обмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	
	ФайлПротокола = ПолучитьИмяВременногоФайла(".txt");
	Обмен.ИмяФайлаПротоколаОбмена = ФайлПротокола;
	
	Обмен.РежимОбмена = "Выгрузка";
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	Обмен.ИмяФайлаОбмена = ИмяВремФайла;
	Обмен.ИмяФайлаПравилОбмена ="ЧтениеXML";	
	Обмен.ЗагрузитьПравилаОбмена(ЧтениеХМЛ,"ЧтениеXML");
	
	//УстановитьУзелОбменаУСтрокДерева(Обмен.ТаблицаПравилВыгрузки.Строки,УзелОбмена);
	
	Обмен.ЗагружатьДанныеВРежимеОбмена = Истина;
	Обмен.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
	Обмен.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 0;
	
	Если УзелОбмена.Код = "CON" Тогда
		Обмен.УстановитьЗначениеПараметраВТаблице("Получатель", УзелОбмена);
	КонецЕсли;
	
	Обмен.ВыполнитьВыгрузку();
	
	/// читаем логи
	ЧтениеТекстаПротокола = Новый ЧтениеТекста;
	ЧтениеТекстаПротокола.Открыть(ФайлПротокола, КодировкаТекста.Системная);
	ТекстПротокола = ЧтениеТекстаПротокола.Прочитать();
	
	ВсеОк = Истина;
	Для ст = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл 
		тст  = СтрПолучитьСтроку(ТекстПротокола,ст);
		Если  СтрНайти(тст,"Выгружено объектов:") > 0 Тогда
			Попытка
				Выгружено = Число(СокрЛП(СтрЗаменить(тст,"Выгружено объектов:","")));
			Исключение
			КонецПопытки;
		ИначеЕсли СтрНайти(ВРег(тст),ВРег("Ошибка"))> 0 Тогда
			ВсеОк = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Ошибки = ТекстПротокола;
	СтатусВыгрузки = ВсеОк;	
	ЧтениеТекстаПротокола.Закрыть();
	УдалитьФайлы(ФайлПротокола);
	
	///Результат выгрузки данных
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ИмяВремФайла, КодировкаТекста.UTF8);
	Результат = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ИмяВремФайла);
	
	ХранилищеДанных = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));
	
	Возврат ХранилищеДанных;
	
КонецФункции

Процедура ВыгрузитьДанные() Экспорт 
	
	ИдентификаторОбработки = "ТК_ВыгрузкаИзменений_Консолидация";		
	КлючОбработки = "ВыгрузкаДокументы_В_Консолидация";
	
	СтруктураЗаписиЖурнала = РегистрыСведений.РезультатыВыполненияОбработок.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиЖурнала);
	
	Попытка
		//Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_consolidation/ws/cwd2.1cws?wsdl","ObmenSSL","bynthghbpf",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
		Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_consolidation/cwd2.1cws?wsdl",,,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	

		WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
		//WS.Пароль ="bynthghbpf";
		//WS.Пользователь="ObmenSSL";

	Исключение 
		ОшибкиИнициализации = ОписаниеОшибки();
		
		ОбщегоНазначения.СообщитьПользователю(ОшибкиИнициализации);
		
		КодСостояния = "ОбработкаОбмена.ИнициализацияОбработки - Не удалось подключится к базе ""Консолидация""";						
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание		"+ОшибкиИнициализации, СтруктураЗаписиЖурнала);												
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		
		Возврат;
	КонецПопытки;
	
	//ПравилаОбмена	= ПолучитьМакет("Правило").ПолучитьТекст();
	//ХранилищеДанных	= Новый ХранилищеЗначения(ПравилаОбмена, Новый СжатиеДанных(9));	
	ХранилищеДанных	= ОбменДаннымиПереопределяемый.ПравилаКонвертацииОбъектов("ОбменConsolidation");
	
	Если ХранилищеДанных = Неопределено Тогда
		КодСостояния = "РегистрСведений.ПравилаДляОбменаДанными - Не удалось найти ПравилаКонвертацииОбъектов для ПланыОбмена.ОбменConsolidation";						
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		Возврат;
	КонецЕсли;
	
	УзелОбмена		= ПланыОбмена.ОбменConsolidation.НайтиПоКоду("CON");
	СтатусВыгрузки	= Ложь;
	Ошибки			= "";
	Выгружено		= 0;
	
	Попытка
		ДанныеОбмена = СформироватьПакетДанных(ХранилищеДанных, УзелОбмена, СтатусВыгрузки, Выгружено, Ошибки);
	Исключение 
		ОшибкиИнициализации = ОписаниеОшибки();
		
		ОбщегоНазначения.СообщитьПользователю(ОшибкиИнициализации);
		
		КодСостояния = "ОбработкаОбмена.ФормированиеПакетаДанных - Не удалось выгрузить данные";						
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния	"+КодСостояния, СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание		"+ОшибкиИнициализации, СтруктураЗаписиЖурнала);												
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
		
		Возврат;
	КонецПопытки;
	
	Если СтатусВыгрузки Тогда
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(Ошибки, СтруктураЗаписиЖурнала);
	Иначе
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке(Ошибки, СтруктураЗаписиЖурнала);												
	КонецЕсли;
	
	Если Выгружено Тогда
		Ошибки = "";
		Попытка
			ОтветКонсолидация = WS.ЗагрузитьПоПравилам(ДанныеОбмена,Ошибки);
			Если ОтветКонсолидация Тогда
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, 1);
				РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Загружено объектов:  "+ОтветКонсолидация, СтруктураЗаписиЖурнала);
				РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиЖурнала);
			Иначе
				РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке(Ошибки, СтруктураЗаписиЖурнала);												
			КонецЕсли;
			РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, ОтветКонсолидация);
		Исключение 
			ОшибкиИнициализации = ОписаниеОшибки();
			
			ОбщегоНазначения.СообщитьПользователю(ОшибкиИнициализации);
			
			КодСостояния = "ОбработкаОбмена.ЗагрузкаДокументов - Не удалось загрузить данные";						
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния "+КодСостояния, СтруктураЗаписиЖурнала);
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание "+ОшибкиИнициализации, СтруктураЗаписиЖурнала);												
			РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Ложь);
			
			Возврат;
		КонецПопытки;
	Иначе
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, СтатусВыгрузки);
	КонецЕсли;
	
	
КонецПроцедуры

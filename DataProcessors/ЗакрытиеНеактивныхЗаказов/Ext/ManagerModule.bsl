
Процедура ЗакрытьНеактивныеЗаказы(НаДату, ДнейОтсрочки) Экспорт
	
	нпп = 0;
	Ок = Истина;
	РезВО = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтрЗР = РезВО.СформироватьСтруктуруЗаписиРезультатов();
	СтрЗР.ИдентификаторОбработки = "ЗакрытиеНеактивныхЗаказов";
	СтрЗР.КлючОбработки = "Закрытие неактивных заказов сети";
	
	Дата = НаДату - ДнейОтсрочки * 86400;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЗаказПоставщику
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.СрокПоставки < &Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыПокупателейОстатки.Заказ КАК Заказ
	|ПОМЕСТИТЬ втЗаказыПокупателей
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(
	|			,
	|			Заказ.ДокументОснование В
	|				(ВЫБРАТЬ
	|					втЗаказПоставщику.Ссылка КАК Ссылка
	|				ИЗ
	|					втЗаказПоставщику КАК втЗаказПоставщику)) КАК ЗаказыПокупателейОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыПокупателейОстатки.Заказ
	|ИЗ
	|	РегистрНакопления.ЗаказыПокупателей.Остатки(, ) КАК ЗаказыПокупателейОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя.РаспределениеЗаказовПоставщиков КАК ЗаказПокупателяРаспределениеЗаказовПоставщиков
	|		ПО ЗаказыПокупателейОстатки.Заказ = ЗаказПокупателяРаспределениеЗаказовПоставщиков.Ссылка
	|ГДЕ
	|	ЗаказПокупателяРаспределениеЗаказовПоставщиков.ЗаказПоставщику В
	|			(ВЫБРАТЬ
	|				втЗаказПоставщику.Ссылка КАК Ссылка
	|			ИЗ
	|				втЗаказПоставщику КАК втЗаказПоставщику)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втЗаказПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втЗаказыПокупателей.Заказ.Проведен КАК Проведен,
	|	РезервыТоваровОстатки.РезервОстаток ЕСТЬ NULL КАК НетОстаткаРезерва,
	|	ТК_ОчередьНаСборкуРезервов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборкиРезервов.ГотовКСборке) КАК ГотовКСборке,
	|	втЗаказыПокупателей.Заказ КАК Заказ,
	|	РезервыТоваров.Регистратор КАК Резерв,
	|	ТК_ОчередьНаСборкуРезервов.Статус КАК Статус
	|ИЗ
	|	втЗаказыПокупателей КАК втЗаказыПокупателей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваров.Остатки КАК РезервыТоваровОстатки
	|		ПО втЗаказыПокупателей.Заказ = РезервыТоваровОстатки.Заказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваров КАК РезервыТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
	|			ПО РезервыТоваров.Регистратор = ТК_ОчередьНаСборкуРезервов.Документ
	|		ПО втЗаказыПокупателей.Заказ = РезервыТоваров.Заказ
	|			И (РезервыТоваров.Регистратор ССЫЛКА Документ.РезервированиеЗаказовПокупателя)
	|ИТОГИ ПО
	|	Проведен,
	|	НетОстаткаРезерва,
	|	ГотовКСборке";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПроведен = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПроведен.Следующий() Цикл
		
		ВыборкаНетОстаткаРезерва = ВыборкаПроведен.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНетОстаткаРезерва.Следующий() Цикл
			
			ВыборкаГотовКСборке = ВыборкаНетОстаткаРезерва.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаГотовКСборке.Следующий() Цикл
				Проведен = ВыборкаГотовКСборке.Проведен;
				НетОстаткаРезерва = ВыборкаГотовКСборке.НетОстаткаРезерва;
				ГотовКСборке = ВыборкаГотовКСборке.ГотовКСборке;
				Если (Проведен И НЕ НетОстаткаРезерва И ГотовКСборке = Истина)
					ИЛИ (Проведен И НетОстаткаРезерва И ГотовКСборке = NULL) Тогда
					Выборка = ВыборкаГотовКСборке.Выбрать();
					Пока Выборка.Следующий() Цикл
						Попытка
							ДокКор = Документы.КорректировкаЗаказаПокупателя.СоздатьДокумент();
							ДокКор.ДополнительныеСвойства.Вставить("ЗаказПросрочен", Истина);
							ДокКор.Заполнить(Выборка.Заказ);
							ДокКор.Комментарий = "Не взятый в работу заказ "+ТекущаяДата();
							ДокКор.Записать(РежимЗаписиДокумента.Проведение);
							нпп = 1 + нпп;
						Исключение
							Ок = Ложь;
							ОписаниеОшибки = ОписаниеОшибки();
							ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
							РезВО.СообщитьОбОшибке("Ошибка " + Выборка.Заказ + Символы.ПС + ОписаниеОшибки,СтрЗР);
						КонецПопытки;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	РезВО.ДобавитьКомментарий("Скорректировано заказов " + нпп, СтрЗР);
	РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР,Ок);
	
КонецПроцедуры
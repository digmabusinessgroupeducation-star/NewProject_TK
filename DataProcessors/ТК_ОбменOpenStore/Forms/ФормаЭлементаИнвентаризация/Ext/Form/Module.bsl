
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ИД;
	
	//Connection = Обработки.ТК_ОбменOpenStore.ПолучитьСоединение(, Истина);
	//Если Connection = Неопределено Тогда
	//	ВызватьИсключение "Не удалось подключится к серверу OS!"; 
	//КонецЕсли;
	ТекстСообщения = "";
	Успешно = ОбменФронт.УстановитьПодключениеКВнешнемуИсточнику(Истина, ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Пользователи.ТекущийПользователь()), ТекстСообщения);
	
	Если Не Успешно Тогда
		ЗаписьЖурналаРегистрации("Загрузка.Инвентаризация",УровеньЖурналаРегистрации.Информация,,,"Соеденение не установлено "+ТекстСообщения);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;	
	
	
	Параметры.Свойство("НомерДокумента", Номер);
	Параметры.Свойство("ДатаДокумента", Дата);
	Параметры.Свойство("СкладДокумента", Склад);
	Параметры.Свойство("ИД", ИД);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Инвентаризация №%1 от %2'; uk = 'Інвентаризація №%1 від %2'"), Номер, Формат(Дата, "ДФ=dd.MM.yyyy"));	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	V_Инвентаризации.DOCID,
		|	V_Инвентаризации.CALCTIME,
		|	V_Инвентаризации.DOCDATE,
		|	V_Инвентаризации.DOCIMPORTDATE,
		|	V_Инвентаризации.DOCSTATUS,
		|	V_Инвентаризации.DOCSYNCTIME
		|ИЗ
		|	ВнешнийИсточникДанных.OpenStore.Таблица.V_Инвентаризации КАК V_Инвентаризации
		|ГДЕ
		|	V_Инвентаризации.DOCGUID = &DOCGUID
		|	И V_Инвентаризации.DOCTYPE = 1003";
	
	Запрос.УстановитьПараметр("DOCGUID", ИД);
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	
	Выборка = Рез.Выбрать();
	Выборка.Следующий();
	
	CALCTIME = Дата(Выборка.CALCTIME);	
	DOCDATE  = Дата(Формат(Выборка.DOCDATE,"ЧЦ=14; ЧГ="));
	DOCIMPORTDATE = Дата(Формат(Выборка.DOCIMPORTDATE,"ЧЦ=14; ЧГ="));
	DOCSYNCTIME = Дата(Формат(Выборка.DOCSYNCTIME,"ЧЦ=14; ЧГ="));
	DOCID = Выборка.DOCID;

	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	V_ИнвентаризацииДвижения.DOCITEMNUM КАК НомерСтроки,
		|	V_ИнвентаризацииДвижения.ARTID КАК Артикул,
		|	V_ИнвентаризацииДвижения.PACKQUANT КАК Коф,
		|	V_ИнвентаризацииДвижения.QUANTITY КАК Количество,
		|	V_ИнвентаризацииДвижения.DELTAQTY КАК Дельта,
		|	V_ИнвентаризацииДвижения.PACKGUID КАК ИдЕдИзм,
		|	V_ИнвентаризацииДвижения.CHARGUID КАК ИдХарактеристика
		|ИЗ
		|	ВнешнийИсточникДанных.OpenStore.Таблица.V_ИнвентаризацииДвижения КАК V_ИнвентаризацииДвижения
		|ГДЕ
		|	V_ИнвентаризацииДвижения.DOCID = &DOCID";
	
	Запрос.УстановитьПараметр("DOCID", DOCID);
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	тзтчОС = Рез.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тзтчОС.НомерСтроки КАК НомерСтроки,
		|	тзтчОС.Артикул КАК Артикул,
		|	тзтчОС.Коф КАК Коф,
		|	тзтчОС.Количество КАК Количество,
		|	тзтчОС.Дельта КАК Дельта,
		|	тзтчОС.ИдЕдИзм КАК ИдЕдИзм,
		|	тзтчОС.ИдХарактеристика КАК ИдХарактеристика
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&тзтчОС КАК тзтчОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.НомерСтроки КАК НомерСтроки,
		|	ВТ_Товары.Артикул КАК Артикул,
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	ВТ_Товары.Коф КАК Коэффициент,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Дельта КАК Дельта,
		|	ВТ_Товары.ИдЕдИзм КАК ИдЕдИзм,
		|	ВТ_Товары.ИдХарактеристика КАК ИдХарактеристика
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ВТ_Товары.Артикул = Номенклатура.Артикул
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("тзтчОС", тзтчОС);
	
	Выборка = Запрос.Выполнить().Выбрать();	
	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = ТоварыИнвентаризации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если ЗначениеЗаполнено(Выборка.ИдЕдИзм) Тогда 
			НоваяСтрока.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПолучитьСсылку(Новый УникальныйИдентификатор(Выборка.ИдЕдИзм));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ИдХарактеристика) Тогда 
			НоваяСтрока.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(Выборка.ИдХарактеристика));
		КонецЕсли;
	КонецЦикла;
	
	//ТоварыИнвентаризации.Загрузить(Рез.Выгрузить());
	
	ЗаголовокСтраницы = НСтр("ru = 'Товары'; uk = 'Товари'");
	КолисествоТоваров = ТоварыИнвентаризации.Количество();
	
	Если КолисествоТоваров > 0 Тогда 
		ЗаголовокСтраницы = ЗаголовокСтраницы + " (" + КолисествоТоваров + ")";
	КонецЕсли;

	Элементы.СтраницаТовары.Заголовок = ЗаголовокСтраницы;
	
КонецПроцедуры

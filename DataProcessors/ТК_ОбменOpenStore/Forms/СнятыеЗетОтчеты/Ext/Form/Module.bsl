

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПериодЗагрузки", Период);	
	Параметры.Свойство("Подразделение", Подразделение);
	
	ОтборПоСкладам = Параметры.Свойство("ОтборПоСкладам"); 

	Если ОтборПоСкладам Тогда 		
		СписокСкладов.ЗагрузитьЗначения(Параметры.ОтборПоСкладам);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЗаполнитьСнятыеЗетОтчеты();
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовФормы


#КонецОбласти



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)

	ЗаполнитьСнятыеЗетОтчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуОтчета(Команда)

	ТекДанные = Элементы.СнятыеЗетОтчеты.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("SAREAID,SYSTEMID,WORKDAYID");	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ТекДанные);
	ДополнительныеПараметры.Вставить("NEWTIME", ТекДанные.ДатаЗетОтчета);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВводаДаты", ЭтаФорма, ДополнительныеПараметры);
	
	ПоказатьВводДаты(ОписаниеОповещения, ДополнительныеПараметры.NEWTIME, НСтр("ru = 'Новая дата Z-отчета'; uk = 'Нова дата Z-звіту'"), ЧастиДаты.ДатаВремя);
	
КонецПроцедуры


#КонецОбласти



#Область Диалоги

&НаКлиенте
Процедура ПослеВводаДаты(Дата, ДопПараметры) Экспорт
	
	Если Дата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьДатуНаСервере(Дата, ДопПараметры);		
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьДатуНаСервере(НоваяДата, Параметры)
	
	Обработки.ТК_ОбменOpenStore.УстановитьНовуюДатуЗетОтчета(НоваяДата, Параметры);
	
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьСнятыеЗетОтчеты()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если Объект.ТаблицаДокументов.Количество() > 0 Тогда 
		
		Отказ = Истина;	
		
	КонецЕсли;                	
	
	Если Не Отказ Тогда 		
		ДлительнаяОперация = НачатьВыполнениеОперации();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Получаем список снятых Z-отчетов'; uk = 'Отримуємо список знятих Z-звітів'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
		ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
		ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ОбменOpenStore";
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.Интервал = 2;		
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект,"Загрузка закрытия смены");		
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;		
	
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеОперации()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НачалоПериода", Период.ДатаНачала);
	ПараметрыОперации.Вставить("КонецПериода", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Подразделение", Подразделение);	
	ПараметрыОперации.Вставить("ОтборПоСкладам", ОтборПоСкладам);
	
	Если ОтборПоСкладам И СписокСкладов.Количество() > 0 Тогда 
		ПараметрыОперации.Вставить("МассивСкладов", СписокСкладов.ВыгрузитьЗначения());	
	КонецЕсли;
	
	ТаблицаСнятыхОтчетов = СнятыеЗетОтчеты.Выгрузить();
	ТаблицаСнятыхОтчетов.Очистить();	
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);		
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ОбменOpenStore.ПолучитьТаблицуСнятыхЗетОтчетов", ПараметрыОперации, ТаблицаСнятыхОтчетов);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Операция, СопровождающийТекст) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		ЗаполнитьТаблицуСнятыхЗетОтчетов(Операция, СообщениеЗавершения);
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСнятыхЗетОтчетов(Операция, СообщениеЗавершения)
	
	Если Не Операция.Свойство("АдресРезультата") Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);	
		
	Если Результат.Свойство("СнятыеЗетОтчеты") Тогда 
		
		СнятыеЗетОтчеты.Загрузить(Результат.СнятыеЗетОтчеты);
		
		СообщениеЗавершения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СообщениеЗавершения", СообщениеЗавершения);	
				
	КонецЕсли;

	
КонецПроцедуры



#КонецОбласти



#Область ПрочиеПроцедурыИФункции


#КонецОбласти
 

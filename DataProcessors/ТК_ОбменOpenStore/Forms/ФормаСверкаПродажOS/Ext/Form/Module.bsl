
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Подразделение", Подразделение);
	Параметры.Свойство("ПериодЗагрузки", Период);		
	ОтборПоСкладам = Параметры.Свойство("ОтборПоСкладам"); 

	Если ОтборПоСкладам Тогда 		
		СписокСкладов.ЗагрузитьЗначения(Параметры.ОтборПоСкладам);
	КонецЕсли;

	СтатусСверки = 1;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтборТабличнойЧасти();
	ПолучитьДанныеСверки();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтатусСверкиПриИзменении(Элемент)
	УстановитьОтборТабличнойЧасти();
КонецПроцедуры

&НаКлиенте
Процедура ТипСверкиПриИзменении(Элемент)
	УстановитьОтборТабличнойЧасти();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДанныеСверки(Команда)
	ПолучитьДанныеСверки();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборТабличнойЧасти()
	
	СтруктураОтбора = Новый Структура;		
	Если ТипСверки <> 0 Тогда 
		СтруктураОтбора.Вставить("Тип", ?(ТипСверки = 1, "ТКС", "ЗКС"));
	КонецЕсли;
	
	Если СтатусСверки <> 0 Тогда 
		СтруктураОтбора.Вставить("ЕстьРасхождения", СтатусСверки = 1);
	КонецЕсли;
		
	Элементы.ТаблицаСверки.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеСверки()
	
	Отказ = Ложь;	
	ОчиститьСообщения();
	
	Если ТаблицаСверки.Количество() > 0 Тогда 
		ТаблицаСверки.Очистить();
	КонецЕсли;		
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ВидОтчета", 		"СверкаПродажOS");
	ПараметрыОперации.Вставить("НачалоПериода", 	Период.ДатаНачала);
	ПараметрыОперации.Вставить("КонецПериода", 		Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Подразделение", 	Подразделение);	
	ПараметрыОперации.Вставить("ОтборПоСкладам", 	ОтборПоСкладам);
	
	Если ОтборПоСкладам И СписокСкладов.Количество() > 0 Тогда 
		ПараметрыОперации.Вставить("МассивСкладов", СписокСкладов.ВыгрузитьЗначения());	
	КонецЕсли;	
	
	ДлительнаяОперация = НачатьВыполнениеОперации(ПараметрыОперации, ТаблицаСверки, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Получаем данные для сверки'; uk = 'Отримуємо дані для звірки'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ОбменOpenStore";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;		
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект,"Загрузка закрытия смены");		
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьВыполнениеОперации(Знач ПараметрыОперации, Знач ТаблицаСверки, Знач УникальныйИдентификатор)
	
	тзСверки = ДанныеФормыВЗначение(ТаблицаСверки, Тип("ТаблицаЗначений"));	
	ПараметрыОперации.Вставить("ТаблицаСверки", тзСверки);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);					
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ОбменOpenStore.СформироватьОтчет", ПараметрыОперации);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультат(Операция, СопровождающийТекст) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда				
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");				
		
		Если Операция.Свойство("АдресРезультата") Тогда 		
			ЗаполнитьДанныеСверки(Операция, СообщениеЗавершения);
		КонецЕсли;
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСверки(Операция, СообщенияЗавершения);
	
	Если Не Операция.Свойство("АдресРезультата") Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);	
	
	Если Результат.Свойство("ТаблицаСверки") Тогда 		
		ТаблицаСверки.Загрузить(Результат.ТаблицаСверки);									
	КонецЕсли;		
	
	СообщенияЗавершения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СообщениеЗавершения", СообщенияЗавершения);	
	
	//Статистика
	Элементы.НадписьСтатистика.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Всего сверено: %1 шт. (ЗКС - %2/ ТКС - %3)
                                                      |С расхождениями: %4 шт.'; uk = 'Всього звірено %1 шт. (ЗКС - %2/ТКС - %3)
                                                      |З розбіжностями: %4 шт.'"),
												ТаблицаСверки.Количество(),
												ТаблицаСверки.НайтиСтроки(Новый Структура("Тип", "ЗКС")).Количество(),
												ТаблицаСверки.НайтиСтроки(Новый Структура("Тип", "ТКС")).Количество(),
												ТаблицаСверки.НайтиСтроки(Новый Структура("ЕстьРасхождения", Истина)).Количество());
																								
КонецПроцедуры





#КонецОбласти


#Область ПрочиеПроцедурыИФункции



#КонецОбласти
 
 
 
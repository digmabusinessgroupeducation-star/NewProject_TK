

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ОтборПоСкладам, ПериодВыборки;
	
	Заголовок = НСтр("ru = 'Инвентаризация ОС'; uk = 'Інвентаризація ОС'");	
	
	ЭлементыОтбора = СписокИнвентаризаций.Отбор.Элементы;
	
	Попытка
	
	Если Параметры.Свойство("ОтборПоСкладам", ОтборПоСкладам) И ТипЗнч(ОтборПоСкладам) = Тип("Массив") Тогда 
		СписокСкладов = Новый СписокЗначений;
		Для Каждого Стр Из ОтборПоСкладам Цикл 
			СписокСкладов.Добавить(Стр);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокИнвентаризаций, 
			"СкладКомпании", 
			СписокСкладов, 
			ВидСравненияКомпоновкиДанных.ВСписке,
			, 
			Истина);

	КонецЕсли;		

	Если Параметры.Свойство("Период", ПериодВыборки) Тогда
		
		
		ЭлементОтбораДанных = Неопределено;
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			Если ЭлементОтбора.Представление = "ОтборПериод" Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
			
			Если ЭлементОтбораДанных = Неопределено Тогда
				ГруппаОтборПериод = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтборПериод.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли; 
				ГруппаОтборПериод.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
				ГруппаОтборПериод.Использование = Истина;
				ГруппаОтборПериод.Представление = "ОтборПериод";
			Иначе
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Истина;
			КонецЕсли;	
			
			ГруппаДатаСортировки = ГруппаОтборПериод.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаДатаСортировки.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ; 
			ГруппаДатаСортировки.Использование = Истина;
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаНачала) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаНачала;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаОкончания;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;
			
			Если ГруппаДатаСортировки.Элементы.Количество() = 0 Тогда 
				ГруппаОтборПериод.Элементы.Удалить(ГруппаДатаСортировки);
			КонецЕсли;
			
		Иначе
			
			Если ЭлементОтбораДанных <> Неопределено Тогда
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Исключение КонецПопытки;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокИнвентаризацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = ПолучитьПараметрыОткрытияФормы(ВыбраннаяСтрока);
			
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.ФормаЭлементаИнвентаризация", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервереБезКонтекста
Функция ПолучитьПараметрыОткрытияФормы(Ссылка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Ссылка.Дата);
	ПараметрыФормы.Вставить("НомерДокумента", Ссылка.Номер);
	ПараметрыФормы.Вставить("СкладДокумента", Ссылка.СкладКомпании);
	ПараметрыФормы.Вставить("ИД", Строка(Ссылка.УникальныйИдентификатор()));
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДатуИнвентаризации(Команда)	
	
	ТекСтрока = Элементы.СписокИнвентаризаций.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ИнвСсылка = ТекСтрока.Ссылка;
	ИнвДата = ТекСтрока.Дата;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Инвентаризация", ИнвСсылка);
	ДопПараметры.Вставить("Номер", ТекСтрока.Номер);
	ДопПараметры.Вставить("Дата", ИнвДата);
	
	Оповещение = Новый ОписаниеОповещения("УстановитьДатуПродолжение", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(Оповещение, 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Изменить дату <%1> инвентаризации?'; uk = 'Змінити дату <%1> інвентаризації?'"),
			Формат(ИнвДата, "ДЛФ=DT")), 
		РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВопроса = Неопределено ИЛИ РезультатВопроса <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("УстановитьДатуЗавершение", ЭтаФорма, ДополнительныеПараметры);
	ПоказатьВводДаты(Оповещение, ДополнительныеПараметры.Дата, НСтр("ru = 'Новая дата инвентаризации'; uk = 'Нова дата інвентаризації'"), ЧастиДаты.ДатаВремя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуЗавершение(Дата, ДополнительныеПараметры) Экспорт 
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("НоваяДата", Дата);
	
	Если УстановитьДатуИнвентаризацииНаСервере(ДополнительныеПараметры) Тогда 
		ТекстПредупреждения = НСтр("ru = 'Дата инвентаризации изменена'; uk = 'Дата інвентаризації змінена'");
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не удалось изменить дату инвентаризации'; uk = 'Не вдалося змінити дату інвентаризації'");
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УстановитьДатуИнвентаризацииНаСервере(ДопПараметры)
	
	Возврат Обработки.ТК_ОбменOpenStore.УстановитьНовуюДатуИнвентаризации(ДопПараметры.Номер, ДопПараметры.НоваяДата);
	
КонецФункции


#КонецОбласти


#Область ПрочиеПроцедурыИФункции


#КонецОбласти
 
 
 
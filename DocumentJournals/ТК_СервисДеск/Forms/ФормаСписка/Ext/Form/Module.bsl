



&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	нСтрСтатус = СписокСтатусов.Добавить();
	нСтрСтатус.Порядок = -1;
	нСтрСтатус.Статус = "Все";
	
	нСтрСтатус = СписокСтатусов.Добавить();
	нСтрСтатус.Порядок = -2;
	нСтрСтатус.Статус = "Все не закрытые";
	
	нСтрСтатус = СписокСтатусов.Добавить();
	нСтрСтатус.Порядок = 1;
	нСтрСтатус.Статус = "Зарегистрирован";

	нСтрСтатус = СписокСтатусов.Добавить();
	нСтрСтатус.Порядок = 2;
	нСтрСтатус.Статус = "Принят к выполнению";
	
	нСтрСтатус = СписокСтатусов.Добавить();
	нСтрСтатус.Порядок = 4;
	нСтрСтатус.Статус = "Завершен";


	ПолныйДоступ = Пользователи.ЭтоПолноправныйПользователь()ИЛИ РольДоступна("ТК_СервисДескПроведениеРедактирование"); 
	
	
	ЧтениеВсехЗаявок = ПолныйДоступ ИЛИ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(),"ПросмотрЗаявокИнициаторов",Ложь);
	
	Список.Параметры.УстановитьЗначениеПараметра("ЧтениеВсехЗаявок", ЧтениеВсехЗаявок);
	Список.Параметры.УстановитьЗначениеПараметра("Инициатор", Пользователи.ТекущийПользователь());
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,"СписокВыбратьИсполнителя","Видимость",ПолныйДоступ);
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Востанавливаем настройки быстрых фильтров
	Если СохраненныйСтатус > 0 Тогда 
		ЭтаФорма.Элементы.СписокСтатусов.ТекущаяСтрока = СохраненныйСтатус; 
	КонецЕсли;
	

КонецПроцедуры



&НаКлиенте
Процедура СписокСтатусовПриАктивизацииСтроки(Элемент)
	
	ИспользованиеФильтра = Ложь;
	ВидСравненияФильтра = ВидСравненияКомпоновкиДанных.Равно;
	ЗначениеФильтра = 4;
	
	ТекДанные = Элемент.ТекущиеДанные;
	СохраненныйСтатус = Элемент.ТекущаяСтрока;

	Если ТекДанные <> Неопределено Тогда
		Если ТекДанные.Статус = "Все не закрытые" Тогда
			ИспользованиеФильтра = Истина;
			ВидСравненияФильтра = ВидСравненияКомпоновкиДанных.НеРавно;
			
		ИначеЕсли ТекДанные.Статус <> "Все" Тогда
			ИспользованиеФильтра = Истина;
			ЗначениеФильтра = ТекДанные.Порядок;
		КонецЕсли;
	КонецЕсли;		

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СтатусПорядок", ЗначениеФильтра, ВидСравненияФильтра,, ИспользованиеФильтра);
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменениеЗавершенногоЗаданияЗапрещено(Знач ЗаданиеСсылка)
	
	Результат = Ложь;
	Если ЗаданиеСсылка.Статус = Перечисления.СтатусыПроцессов.Завершено Тогда
		Результат = Истина; 
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменениеИсполнителья(Знач ЗаданиеСсылка,Знач Исполнитель)
	
	ЗаданиеОбъект = ЗаданиеСсылка.ПолучитьОбъект();
	ЗаданиеОбъект.ТекущийИсполнитель = Исполнитель;
	ЗаданиеОбъект.Записать();
	
КонецПроцедуры


&НаКлиенте
Процедура ИзменитьИсполнителя(ТекущаяСтрока,ВыбранныйСотрудник)
	Если ТекущаяСтрока <> Неопределено Тогда
		
		
		Если ИзменениеЗавершенногоЗаданияЗапрещено(ТекущаяСтрока) Тогда
			
			ПоказатьПредупреждение(, Нстр("ru = 'Задание завершено. Изменение исполнителя в задании не возможно.'"));
			
			Возврат;
		КонецЕсли;
		ИзменениеИсполнителья(ТекущаяСтрока,ВыбранныйСотрудник);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьИсполнителя(Команда)
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыборСотрудника",ЭтаФорма);
	ПараметрыВыбора = Новый Структура("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора",ПараметрыВыбора,,,,,ОповещениеВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
			
КонецПроцедуры

&НаКлиенте
Процедура ВыборСотрудника(ВыбранныйСотрудник,ДопПараметры)Экспорт
	
	Если ВыбранныйСотрудник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	МассивСтрок = Элементы.Список.ВыделенныеСтроки;
	
	Для Каждого ТекущаяСтрока Из МассивСтрок Цикл
		  ИзменитьИсполнителя(ТекущаяСтрока,ВыбранныйСотрудник);
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПовторяющиесяЗадачи(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ПовторныеЗадачиСервиДеск.Форма",ПараметрыФормы); 

КонецПроцедуры

&НаКлиенте
Процедура РеестрЗадач(Команда)
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.РеестрЗадачиСервисДеск.Форма",ПараметрыФормы); 
КонецПроцедуры

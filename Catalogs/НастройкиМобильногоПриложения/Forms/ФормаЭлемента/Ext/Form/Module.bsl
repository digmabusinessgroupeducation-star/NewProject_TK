

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	
	
	ВыполнитьДействияПриСозданииНаСервере();	
	
КонецПроцедуры


&НаСервере
Процедура ВыполнитьДействияПриСозданииНаСервере()
	
	//
	//Если НЕ Объект.Ссылка.Пустая() Тогда
	//	
	//	УстановитьПривилегированныйРежим(Истина);
	//	ПарольИзХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "Пароль");
	//	УстановитьПривилегированныйРежим(Ложь);
	//	Пароль = ?(ЗначениеЗаполнено(ПарольИзХранилища), ЭтотОбъект.УникальныйИдентификатор, "");
	//	
	//КонецЕсли;
	
	
	ЗаполнитьГиперссылкиЦенСервер();
	
	ЗаполнитьТаблицуКаталоговСервер();
	
	УстановитьПараметрыТаблицыКаталоговСервер();
	//
	УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер();
	//
	
КонецПроцедуры


&НаСервере
Процедура УстановитьТипЗначенийСпискаГруппТаблицыКаталоговСервер()
	
	//Если Объект.ОтборГруппыКатегорииНоменклатуры = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры Тогда
		ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	//Иначе
	//	ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
	//КонецЕсли;	
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталогов Цикл
		СтрокаТаблицыКаталогов.Группы.ТипЗначения = ТипЗначений;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыТаблицыКаталоговСервер()
	
	ЗаголовокТаблицы = НСтр("ru='Таблица каталогов (соответствие групп номенклатуры каталогам на сайте)';uk='Таблиця каталогів (відповідність груп номенклатури каталогам на сайті)'");
	//Если Объект.ОтборГруппыКатегорииНоменклатуры = Перечисления.ВидыОтборовНоменклатуры.ГруппыНоменклатуры Тогда
		ЗаголовокКолонки = НСтр("ru='Группы номенклатуры';uk='Групи номенклатури'");
		ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	//Иначе
	//	ЗаголовокКолонки = НСтр("ru='Категории номенклатуры';uk='Категорії номенклатури'");
	//	ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	//КонецЕсли;
	
	Элементы.ГруппаТаблицаКаталогов.Заголовок = ЗаголовокТаблицы;
	Элементы.ТаблицаКаталоговГруппы.Заголовок = ЗаголовокКолонки;
	Элементы.ТаблицаКаталоговГруппы.ВыборГруппИЭлементов = ВыборГруппИЭлементов;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКаталоговСервер()
	
	НадписьВсеЭлементыСписка = "(" + НСтр("ru='Все';uk='Всі'") + ")";
	
	СохраненнаяТаблицаКаталогов = РеквизитФормыВЗначение("Объект").СохраненнаяТаблицаКаталогов.Получить();
	
	Если НЕ ТипЗнч(СохраненнаяТаблицаКаталогов) = Тип("ТаблицаЗначений") Тогда
		
		СоздатьКаталогПоУмолчаниюСервер();
		
	Иначе
		
		Для Каждого СтрокаСохраненнойТаблицыКаталогов Из СохраненнаяТаблицаКаталогов Цикл
			
			НоваяСтрока = ТаблицаКаталогов.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСохраненнойТаблицыКаталогов);
			
			ХранилищеНастроекКомпоновки = СтрокаСохраненнойТаблицыКаталогов.ХранилищеНастроекКомпоновки.Получить();
			НоваяСтрока.АдресНастроекКомпоновки = ПоместитьВоВременноеХранилище(ХранилищеНастроекКомпоновки, УникальныйИдентификатор);
			
		КонецЦикла;
		
		Если ТаблицаКаталогов.Количество() = 0 Тогда
			
			СоздатьКаталогПоУмолчаниюСервер();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГиперссылкиЦенСервер()
	
	СписокВидовЦенСтрока = "";
	Для каждого СтрокаВидовЦен Из Объект.ВидыЦен Цикл 
		
		НовыйЭлемент = СписокВидовЦен.Добавить();
		НовыйЭлемент.Значение = СтрокаВидовЦен.ВидЦены;
		
		СписокВидовЦенСтрока = СписокВидовЦенСтрока + ?(СписокВидовЦенСтрока = "","","; ") + СтрокаВидовЦен.ВидЦены.Наименование;
		
	КонецЦикла;
	Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = "Вид цен: "+СписокВидовЦенСтрока;
	
	
КонецПроцедуры


&НаСервере
Процедура СоздатьКаталогПоУмолчаниюСервер()
	
	НоваяСтрока = ТаблицаКаталогов.Добавить();
	НоваяСтрока.Каталог = НСтр("ru='Основной каталог товаров';uk='Основний каталог товарів'");
	НоваяСтрока.Группы.Добавить(НеОпределено, НадписьВсеЭлементыСписка);
	НоваяСтрока.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры 


&НаКлиенте
Процедура НастроитьОтбор(Команда)

	Если Элементы.ТаблицаКаталогов.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуОтбораКаталога(Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКомпоновки);
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораКаталога(АдресНастроекКомпоновки)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресНастроекКомпоновки", АдресНастроекКомпоновки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуОтбораКаталогаЗавершение", ЭтотОбъект);
	
	РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Справочник.НастройкиМобильногоПриложения.Форма.ФормаНастройкиОтбора", ПараметрыФормы, ЭтаФорма,,,,ОписаниеОповещения, РежимОткрытия);
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФормуОтбораКаталогаЗавершение(НастройкиКомпоновки, ДополнительныеПараметры) Экспорт

	Если НастройкиКомпоновки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	АдресНастроекКомпоновки = ПоместитьВоВременноеХранилище(НастройкиКомпоновки, УникальныйИдентификатор);
	
	Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекКомпоновки = АдресНастроекКомпоновки;

КонецПроцедуры // ОткрытьФормуОтбораКаталога()



&НаКлиенте
Процедура ТаблицаКаталоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	
	Если Элемент.ТекущийЭлемент.Имя = "ТаблицаКаталоговСтруктураКаталога" Тогда
		
		ТекДанные = Элемент.ТекущиеДанные;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Ключ", Объект.Ссылка);
		//СтруктураПараметров.Вставить("ОтборГруппыКатегорииНоменклатуры", Объект.ОтборГруппыКатегорииНоменклатуры);
		СтруктураПараметров.Вставить("ТаблицаКаталоговГруппы", ТекДанные.Группы);
		СтруктураПараметров.Вставить("ИдентификаторКаталога", ТекДанные.ИдентификаторКаталога);
		СтруктураПараметров.Вставить("АдресНастроекСтруктурыКаталога", ТекДанные.АдресНастроекСтруктурыКаталога);
		СтруктураПараметров.Вставить("РучнаяНастройкаКаталога", ТекДанные.РучнаяНастройкаКаталога);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаСтруктурыКаталогаЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("Справочник.НастройкиМобильногоПриложения.Форма.НастройкаСтруктурыКаталога", СтруктураПараметров, ЭтотОбъект,,,,ОписаниеОповещения);
		
	КонецЕсли;	

	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаСтруктурыКаталогаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.РучнаяНастройкаКаталога = Результат.ДетализацияОтбора;
	Элементы.ТаблицаКаталогов.ТекущиеДанные.АдресНастроекСтруктурыКаталога = Результат.АдресВХранилище;

КонецПроцедуры // ОткрытьФормуОтбораКаталога()


&НаКлиенте
Процедура ТаблицаКаталоговПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьКаталогЗавершение", ЭтотОбъект), 
	НСтр("ru='Настройки структуры каталога и идентификатор каталога будут удалены. Продолжить?';uk='Налаштування структури каталогу і ідентифікатор каталогу будуть видалені. Продовжити?'"),
	РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКаталоговПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ИдентификаторКаталога = "";
		
	КонецЕсли;

	Если (Элемент.ТекущиеДанные.Группы.Количество() = 1
		И НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Группы[0].Значение) 
		И Элемент.ТекущиеДанные.Группы[0].Представление = НадписьВсеЭлементыСписка)
		ИЛИ Элемент.ТекущиеДанные.Группы.Количество() = 0 Тогда
			
		НовыйСписокГрупп = Новый СписокЗначений;
		
		//Если Объект.ОтборГруппыКатегорииНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыОтборовНоменклатуры.ГруппыНоменклатуры") Тогда
			ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
			Элементы.ТаблицаКаталоговГруппы.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
		//Иначе
		//	ТипЗначений = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
		//	Элементы.ТаблицаКаталоговГруппыКатегории.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
		//КонецЕсли;
		
		НовыйСписокГрупп.ТипЗначения = ТипЗначений;
		НовыйСписокГрупп.Добавить(Неопределено, НадписьВсеЭлементыСписка);
		Элемент.ТекущиеДанные.Группы = НовыйСписокГрупп;
		
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ТаблицаКаталоговПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
		Отказ = НЕ ПроверитьУникальностьИдентификатора();

КонецПроцедуры

&НаКлиенте
Функция ПроверитьУникальностьИдентификатора()
	
	ИдентификаторКаталога = Элементы.ТаблицаКаталогов.ТекущиеДанные.ИдентификаторКаталога;
	Найдено = ТаблицаКаталогов.НайтиСтроки(Новый Структура("ИдентификаторКаталога", ИдентификаторКаталога));
	ИдентификаторыУникальны = Найдено.Количество() = 1;
	
	Если НЕ ИдентификаторыУникальны Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Идентификатор каталога должен быть уникальным!';uk='Ідентифікатор каталогу повинен бути унікальним!'"),
			Объект.Ссылка,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ТаблицаКаталогов", ТаблицаКаталогов.Индекс(Элементы.ТаблицаКаталогов.ТекущиеДанные) + 1, "ИдентификаторКаталога"));
	
	КонецЕсли;
	
	Возврат ИдентификаторыУникальны;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаКаталоговПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = НеОпределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьВыбранныеГруппыСерверБезКонтекста(Элемент.ТекущиеДанные.Группы, НадписьВсеЭлементыСписка, Элементы.ТаблицаКаталоговГруппы.ВыборГруппИЭлементов);
	
	Если ПустаяСтрока(Элемент.ТекущиеДанные.ИдентификаторКаталога) Тогда
		Элемент.ТекущиеДанные.ИдентификаторКаталога = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Если ПустаяСтрока(Элемент.ТекущиеДанные.Каталог) Тогда
		Элемент.ТекущиеДанные.Каталог = НСтр("ru='Каталог товаров';uk='Каталог товарів'") + " " + ВРег(СокрЛП(Лев(Элемент.ТекущиеДанные.ИдентификаторКаталога, 8)));
	КонецЕсли;
	
	Модифицированность = Истина;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьВыбранныеГруппыСерверБезКонтекста(СписокГрупп, НадписьВсеЭлементыСписка, ВыборГруппИЭлементов)
	
	ГруппыВыбраны = Ложь;
	
	Если ВыборГруппИЭлементов = ГруппыИЭлементы.Группы Тогда
		// Удаляем не группы номенклатуры.
		МассивУдалить = Новый Массив;
		Для Каждого ЭлементСЗ Из СписокГрупп Цикл
			
			ТекГруппа = ЭлементСЗ.Значение;
			Если ЗначениеЗаполнено(ТекГруппа) И НЕ ТекГруппа.ЭтоГруппа Тогда
				МассивУдалить.Добавить(ЭлементСЗ);
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекГруппа) Тогда
				ЭлементСЗ.Представление = Строка(ЭлементСЗ.Значение);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекГруппа) И НЕ ЗначениеЗаполнено(ЭлементСЗ.Представление) Тогда
				ЭлементСЗ.Представление =  НСтр("ru='<Нет группы>';uk='<Без групи>'");
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ЭлементМУ Из МассивУдалить Цикл
			СписокГрупп.Удалить(ЭлементМУ);
		КонецЦикла;
	КонецЕсли;
	
	// Удаляем дубли и подчиненные элементы.
	МассивУдалить = Новый Массив;
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если НЕ МассивУдалить.Найти(ЭлементСЗ) = НеОпределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекГруппа = ЭлементСЗ.Значение;
		
		Для Каждого ЭлементСЗВлож Из СписокГрупп Цикл

			Если НЕ МассивУдалить.Найти(ЭлементСЗВлож) = НеОпределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекГруппа)
				ИЛИ НЕ ЗначениеЗаполнено(ЭлементСЗВлож.Значение) Тогда
				//Пропускаем элемент "Все" и "Нет группы"
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЭлементСЗВлож = ЭлементСЗ
				И ЭлементСЗВлож.Значение = ТекГруппа Тогда
				
				МассивУдалить.Добавить(ЭлементСЗВлож);
				
			Иначе
				
				Если ЭлементСЗВлож.Значение.ПринадлежитЭлементу(ТекГруппа) Тогда
				
					МассивУдалить.Добавить(ЭлементСЗВлож);
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ЭлементМУ Из МассивУдалить Цикл
		
		СписокГрупп.Удалить(ЭлементМУ);
		
	КонецЦикла;
	
	Для Каждого ЭлементСЗ Из СписокГрупп Цикл
		
		Если ЗначениеЗаполнено(ЭлементСЗ.Значение) ИЛИ ЗначениеЗаполнено(ЭлементСЗ.Представление) Тогда
			
			ГруппыВыбраны = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ГруппыВыбраны Тогда
		
		СписокГрупп.Очистить();
		СписокГрупп.Добавить(Неопределено, НадписьВсеЭлементыСписка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКаталогЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Модифицированность = Истина;
		ТаблицаКаталогов.Удалить(Элементы.ТаблицаКаталогов.ТекущаяСтрока);		
    КонецЕсли;
КонецПроцедуры
&НаСервере
Процедура ТаблицаКаталоговСохранить(ТекущийОбъект)

	ТаблицаКаталоговТЗ = ДанныеФормыВЗначение(ТаблицаКаталогов, Тип("ТаблицаЗначений"));
	ТаблицаКаталоговТЗ.Колонки.Добавить("ХранилищеНастроекКомпоновки", Новый ОписаниеТипов("ХранилищеЗначения"));
	
	Для Каждого СтрокаТаблицыКаталогов Из ТаблицаКаталоговТЗ Цикл
		
		Если ЭтоАдресВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКомпоновки) Тогда
			НастройкиКомпоновки = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекКомпоновки);
			СтрокаТаблицыКаталогов.ХранилищеНастроекКомпоновки = Новый ХранилищеЗначения(НастройкиКомпоновки);
		КонецЕсли;
		
		ОбновитьНастройкиСтруктурыКаталога(СтрокаТаблицыКаталогов);
	КонецЦикла;
	
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекКомпоновки");
	ТаблицаКаталоговТЗ.Колонки.Удалить("АдресНастроекСтруктурыКаталога");
	
	ТекущийОбъект.СохраненнаяТаблицаКаталогов = Новый ХранилищеЗначения(ТаблицаКаталоговТЗ);
	
	ОчиститьУдаленныеНастройкиСтруктурыКаталога();

КонецПроцедуры
&НаСервере
Процедура ИерархияНоменклатурыСохранить(ТекущийОбъект)
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиСтруктурыКаталога(СтрокаТаблицыКаталогов)
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицыКаталогов.АдресНастроекСтруктурыКаталога) Тогда
		// не было изменений
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.МобильноеПриложениеСтруктураКаталога.СоздатьНаборЗаписей();
	НЗ.Отбор.НастройкаМобильногоПриложения.Установить(Объект.Ссылка);
	НЗ.Отбор.ИдентификаторКаталога.Установить(СтрокаТаблицыКаталогов.ИдентификаторКаталога);
	
	СохраненныеДанные = ПолучитьИзВременногоХранилища(СтрокаТаблицыКаталогов.АдресНастроекСтруктурыКаталога);
	СохраненныеДанные.ЗаполнитьЗначения(СтрокаТаблицыКаталогов.ИдентификаторКаталога, "ИдентификаторКаталога");
	СохраненныеДанные.Свернуть("НастройкаМобильногоПриложения, ИдентификаторКаталога, Номенклатура, ГруппаКаталога, РодительИдентификатор, 
		|Идентификатор, РодительГруппа, РодительКатегория, ЭтоГруппа");
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТВ.Номенклатура КАК Номенклатура,
		|	ТВ.ГруппаКаталога КАК ГруппаКаталога,
		|	ТВ.РодительИдентификатор КАК РодительИдентификатор,
		|	ТВ.Идентификатор КАК Идентификатор,
		|	ТВ.РодительГруппа КАК РодительГруппа,
		|	ТВ.РодительКатегория КАК РодительКатегория,
		|	ТВ.ЭтоГруппа КАК ЭтоГруппа
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&ТВ КАК ТВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.ГруппаКаталога КАК ГруппаКаталога,
		|	ВложенныйЗапрос.РодительИдентификатор КАК РодительИдентификатор,
		|	ВложенныйЗапрос.Идентификатор КАК Идентификатор,
		|	ВложенныйЗапрос.РодительГруппа КАК РодительГруппа,
		|	ВложенныйЗапрос.РодительКатегория КАК РодительКатегория,
		|	ВложенныйЗапрос.ЭтоГруппа КАК ЭтоГруппа,
		|	СУММА(ВложенныйЗапрос.ПолеЧТ) КАК ПолеЧТ
		|ИЗ
		|	(ВЫБРАТЬ
		|		МобильноеПриложениеСтруктураКаталога.Номенклатура КАК Номенклатура,
		|		МобильноеПриложениеСтруктураКаталога.ГруппаКаталога КАК ГруппаКаталога,
		|		МобильноеПриложениеСтруктураКаталога.РодительИдентификатор КАК РодительИдентификатор,
		|		МобильноеПриложениеСтруктураКаталога.Идентификатор КАК Идентификатор,
		|		МобильноеПриложениеСтруктураКаталога.РодительГруппа КАК РодительГруппа,
		|		МобильноеПриложениеСтруктураКаталога.РодительКатегория КАК РодительКатегория,
		|		МобильноеПриложениеСтруктураКаталога.ЭтоГруппа КАК ЭтоГруппа,
		|		1 КАК ПолеЧТ
		|	ИЗ
		|		РегистрСведений.МобильноеПриложениеСтруктураКаталога КАК МобильноеПриложениеСтруктураКаталога
		|	ГДЕ
		|		МобильноеПриложениеСтруктураКаталога.НастройкаМобильногоПриложения = &НастройкаМобильногоПриложения
		|		И МобильноеПриложениеСтруктураКаталога.ИдентификаторКаталога = &ИдентификаторКаталога
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВТ.Номенклатура,
		|		ВТ.ГруппаКаталога,
		|		ВТ.РодительИдентификатор,
		|		ВТ.Идентификатор,
		|		ВТ.РодительГруппа,
		|		ВТ.РодительКатегория,
		|		ВТ.ЭтоГруппа,
		|		-1
		|	ИЗ
		|		ВТ КАК ВТ) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.ГруппаКаталога,
		|	ВложенныйЗапрос.РодительГруппа,
		|	ВложенныйЗапрос.ЭтоГруппа,
		|	ВложенныйЗапрос.Идентификатор,
		|	ВложенныйЗапрос.РодительКатегория,
		|	ВложенныйЗапрос.РодительИдентификатор
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.ПолеЧТ) <> 0";
	
	Запрос.УстановитьПараметр("ИдентификаторКаталога", СтрокаТаблицыКаталогов.ИдентификаторКаталога);
	Запрос.УстановитьПараметр("НастройкаМобильногоПриложения", Объект.Ссылка);
	Запрос.УстановитьПараметр("ТВ", СохраненныеДанные);
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ  РезультатЗапроса.Пустой() Тогда
			МассивПланыОбмена = ПланыОбмена.ОбменМобильноеПриложение.ПолучитьИспользуемыеУзлыПланаОбмена(Объект.Ссылка); 

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Номенклатура.Пустая() Тогда
				Продолжить;
			КонецЕсли;
			
			ПланыОбмена.ЗарегистрироватьИзменения(МассивПланыОбмена,ВыборкаДетальныеЗаписи.Номенклатура);
		КонецЦикла;
		
	КонецЕсли;

	НЗ.Загрузить(СохраненныеДанные);
	НЗ.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);

	
КонецПроцедуры

&НаСервере
Процедура ОчиститьУдаленныеНастройкиСтруктурыКаталога()
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МобильноеПриложениеСтруктураКаталога.ИдентификаторКаталога КАК ИдентификаторКаталога
	|ИЗ
	|	РегистрСведений.МобильноеПриложениеСтруктураКаталога КАК МобильноеПриложениеСтруктураКаталога
	|ГДЕ
	|	МобильноеПриложениеСтруктураКаталога.НастройкаМобильногоПриложения = &НастройкаМобильногоПриложения";
	Запрос.УстановитьПараметр("НастройкаМобильногоПриложения", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеИдентификаторы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИдентификаторКаталога");
	Для каждого Идентификатор Из СохраненныеИдентификаторы Цикл
	
		Если ТаблицаКаталогов.НайтиСтроки(Новый Структура("ИдентификаторКаталога", Идентификатор)).Количество() = 0 Тогда
			
			НЗ = РегистрыСведений.МобильноеПриложениеСтруктураКаталога.СоздатьНаборЗаписей();
			НЗ.Отбор.НастройкаМобильногоПриложения.Установить(Объект.Ссылка);
			НЗ.Отбор.ИдентификаторКаталога.Установить(Идентификатор);
			НЗ.Записать(Истина);
		
		КонецЕсли; 
	
	КонецЦикла; 
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если ТаблицаКаталогов.Количество() = 0 Тогда
		
		Отказ = Истина;
		
		Сообщение = НСтр("ru='Таблица каталогов не заполнена!';uk='Таблиця каталогів не заповнена!'");
		Поле = "ТаблицаКаталогов";
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Сообщение, Объект.Ссылка, Поле);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	
	// Сохранение видов цен.
	ВидыЦен = ТекущийОбъект.ВидыЦен;
	ВидыЦен.Очистить();
	
	Для каждого ЭлементСЗ Из СписокВидовЦен Цикл 
		
		НоваяСтрока = ВидыЦен.Добавить();
		НоваяСтрока.ВидЦены = ЭлементСЗ.Значение;
		
	КонецЦикла;
	

	ТаблицаКаталоговСохранить(ТекущийОбъект);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьВидыЦенНоменклатуры(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокВидовЦен", СписокВидовЦен);
	ОткрытьФорму("Справочник.ТипыЦен.Форма.ФормаВыбораСписок",ПараметрыФормы,ЭтаФорма,,,, Новый ОписаниеОповещения("ВыбратьВидыЦенНоменклатурыЗавершение", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидыЦенНоменклатурыЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
    
    Если РезультатВыбора <> Неопределено И ТипЗнч(РезультатВыбора) = Тип("СписокЗначений") Тогда
        
        СписокВидовЦен = РезультатВыбора;
        
        СписокВидовЦенСтрока = "";
        Для каждого ЭлементСЗ Из СписокВидовЦен Цикл
            
            СписокВидовЦенСтрока = СписокВидовЦенСтрока + ?(СписокВидовЦенСтрока = "","","; ") + ЭлементСЗ.Представление;
            
		КонецЦикла;
		Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = НСтр("ru='Виды цен: ';uk='Види цін: '")+СписокВидовЦенСтрока;
		
		 //ЗарегистрироватьИзмененияЦенПоТипуЦен(Объект.Ссылка,СписокВидовЦен)

    ИначеЕсли РезультатВыбора <> Неопределено И ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.ВидыЦен") Тогда
        СписокВидовЦен.Очистить();
        СписокВидовЦен.Добавить(РезультатВыбора, РезультатВыбора);
		Элементы.ВыбратьВидыЦенНоменклатуры.Заголовок = НСтр("ru='Вид цен: ';uk='Вид цін: '")+РезультатВыбора;
	//	ЗарегистрироватьИзмененияЦенПоТипуЦен(Объект.Ссылка,СписокВидовЦен)

    КонецЕсли;

	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗарегистрироватьИзмененияЦенПоТипуЦен(Настройка,СписокВидовЦен)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменМобильноеПриложение.Ссылка КАК Ссылка,
		|	ОбменМобильноеПриложение.Код КАК Код
		|ИЗ
		|	ПланОбмена.ОбменМобильноеПриложение КАК ОбменМобильноеПриложение
		|ГДЕ
		|	ОбменМобильноеПриложение.ПрофильНастроек = &ПрофильНастроек";
	
	Запрос.УстановитьПараметр("ПрофильНастроек", Настройка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивЦен = СписокВидовЦен.ВыгрузитьЗначения();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбменМобильноеПриложение.ЗапуститьФоновоеЗаданиеРегистрациЦениПоУзлуОбмена(ВыборкаДетальныеЗаписи.Ссылка,МассивЦен,ВыборкаДетальныеЗаписи.Код);
	КонецЦикла;
	
КонецПроцедуры



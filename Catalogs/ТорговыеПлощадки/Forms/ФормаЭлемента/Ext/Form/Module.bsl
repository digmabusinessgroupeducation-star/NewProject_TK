
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	ДополнительныеПараметры = УправлениеКонтактнойИнформацией.ПараметрыКонтактнойИнформации();
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаКонтактнаяИнформация");
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(ЭтотОбъект, Объект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	//	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	//Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФормеПоИмениИзмерения(ЭтаФорма, "ПривязкаСчетчиков", Объект.Ссылка, "ТорговаяПлощадка");
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "ТК_УчетнаяПолитикаТорговыхПлощадок", Объект.Ссылка);
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьНаборЗаписейИстории(ЭтаФорма, "ТК_УчетнаяПолитикаТорговыхПлощадок", Объект.Ссылка);
	
	//Vov41k 06.08.2024
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "ТК_РежимыРаботыТорговыхПлощадок", Объект.Ссылка);
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьНаборЗаписейИстории(ЭтаФорма, "ТК_РежимыРаботыТорговыхПлощадок", Объект.Ссылка);
	
	ОбновитьПолеУчетнаяПолитикаТорговыхПлощадокПериодСтрокой(ЭтаФорма);
	ОбновитьПолеГрафикиРаботыТорговыхПлощадокПериодСтрокой(ЭтаФорма);
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(СокрЛП(Объект.Код) ,1,125);
		ШК_GlobalID  = ПоместитьВоВременноеХранилище(Новый Картинка(ДанныеQRКода), Новый УникальныйИдентификатор);
		
		ОформлениеТЧ_Потребители();
		ПолучениеДанные();
	КонецЕсли;
	
	КассыККМ.Параметры.УстановитьЗначениеПараметра("Объект", Объект.Ссылка);
	УправлениеЭлементамиФормы(Элементы);
	ЗаполнитьДополнительныеРеквизитыВФорме();
	Элементы.ДатаРегистрации.Видимость = ЗначениеЗаполнено(Объект.ДатаРегистрации);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ТК_УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Элементы.КассыККМ.Отображение = ОтображениеТаблицы.Список;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	
	Если ИмяСобытия = "ОтредактированаИстория" И Объект.Ссылка = Источник
		И Параметр.ИмяРегистра = "ТК_УчетнаяПолитикаТорговыхПлощадок" Тогда
		
		Если ТК_УчетнаяПолитикаТорговыхПлощадокНаборЗаписейПрочитан Тогда
			НастройкиОрганизацииВведены = Истина;
			РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
			ЭтаФорма,
			Объект.Ссылка,
			ИмяСобытия,
			Параметр,
			Источник);
			
			ОбновитьПолеУчетнаяПолитикаТорговыхПлощадокПериодСтрокой(ЭтаФорма);
			
		КонецЕсли;
		
	// Vov41k 06.08.2024
	ИначеЕсли ИмяСобытия = "ОтредактированаИстория" И Объект.Ссылка = Источник
		И Параметр.ИмяРегистра = "ТК_РежимыРаботыТорговыхПлощадок" Тогда
		
		Если ТК_РежимыРаботыТорговыхПлощадокНаборЗаписейПрочитан Тогда
			ТК_РежимРаботыВведен = Истина;
			РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
				ЭтаФорма,
				Объект.Ссылка,
				ИмяСобытия,
				Параметр,
				Источник);
			
			ОбновитьПолеГрафикиРаботыТорговыхПлощадокПериодСтрокой(ЭтаФорма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьДанныеПоОрганизации(ТекущийОбъект);
	ЗаписатьДанныеГрафикРаботы(ТекущийОбъект);
	
КонецПроцедуры



&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры



&НаКлиенте
Процедура ДатаОткрытиЗакрытияяПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ДатаОткрытия) И ЗначениеЗаполнено(Объект.ДатаЗакрытия)
		И Объект.ДатаОткрытия > Объект.ДатаЗакрытия Тогда
		ПоказатьПредупреждение(,"Дата закрытия не может быть меньше даты открытия!");
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ЗаписатьДанныеПоОрганизации(ТекущийОбъект)
	
	Если НЕ НастройкиОрганизацииВведены Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок) Тогда
		
		Если Не ЗначениеЗаполнено(ТК_УчетнаяПолитикаТорговыхПлощадок.ТорговаяПлощадка) Тогда
			ТК_УчетнаяПолитикаТорговыхПлощадок.ТорговаяПлощадка = ТекущийОбъект.Ссылка;
		КонецЕсли;
		
		РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма,
		"ТК_УчетнаяПолитикаТорговыхПлощадок",
		ТекущийОбъект.Ссылка);
		РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма,
		"ТК_УчетнаяПолитикаТорговыхПлощадок",
		ТекущийОбъект.Ссылка);
		Если Не ЗначениеЗаполнено(ТК_УчетнаяПолитикаТорговыхПлощадок.Период) Тогда
			ТК_УчетнаяПолитикаТорговыхПлощадок.Период = Дата(0001,1,1);
		КонецЕсли;
		ОбновитьПолеУчетнаяПолитикаТорговыхПлощадокПериодСтрокой(ЭтаФорма);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеГрафикРаботы(ТекущийОбъект)
	
	Если НЕ ТК_РежимРаботыВведен Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ТК_РежимыРаботыТорговыхПлощадок) Тогда
		
		Если Не ЗначениеЗаполнено(ТК_РежимыРаботыТорговыхПлощадок.ТорговаяПлощадка) Тогда
			ТК_РежимыРаботыТорговыхПлощадок.ТорговаяПлощадка = ТекущийОбъект.Ссылка;
		КонецЕсли;
		
		РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма,
		"ТК_РежимыРаботыТорговыхПлощадок",
		ТекущийОбъект.Ссылка);
		РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма,
		"ТК_РежимыРаботыТорговыхПлощадок",
		ТекущийОбъект.Ссылка);
		Если Не ЗначениеЗаполнено(ТК_РежимыРаботыТорговыхПлощадок.Период) Тогда
			ТК_РежимыРаботыТорговыхПлощадок.Период = Дата(0001,1,1);
		КонецЕсли;
		ОбновитьПолеГрафикиРаботыТорговыхПлощадокПериодСтрокой(ЭтаФорма);
		
	КонецЕсли;
	
	
КонецПроцедуры


&НаСервере
Функция ДатаСеанса()
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
КонецФункции


&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолеУчетнаяПолитикаТорговыхПлощадокПериодСтрокой(Форма)
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "ТК_УчетнаяПолитикаТорговыхПлощадок.Период");
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма,"ТК_УчетнаяПолитикаТорговыхПлощадокПериодСтрокой", ПолучитьПредставлениеДаты(Значение));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолеГрафикиРаботыТорговыхПлощадокПериодСтрокой(Форма)
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "ТК_РежимыРаботыТорговыхПлощадок.Период");
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма,"ТК_РежимыРаботыТорговыхПлощадокПериодСтрокой", ПолучитьПредставлениеДаты(Значение));
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеДаты(Знач ДатаНачала)
	
	Возврат Формат(ДатаНачала, "ДФ=dd.MM.yyyy");
	
КонецФункции

&НаКлиенте
Функция СписокМесяцевПоСтроке(Текст)
	
	СписокМесяцев  = Новый СписокЗначений;
	Месяцы         = Новый Соответствие;
	МесяцыВозврата = Новый Массив;
	
	Для Счетчик = 1 По 12 Цикл
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='ММММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='МММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
	КонецЦикла;
	
	Для Каждого ЭлементСписка Из СписокМесяцев Цикл
		Если ВРег(Текст) = ВРег(Лев(ЭлементСписка.Представление, СтрДлина(Текст))) Тогда
			Месяцы[ЭлементСписка.Значение] = 0;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из Месяцы Цикл
		МесяцыВозврата.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Возврат МесяцыВозврата;
	
КонецФункции

&НаКлиенте
Функция ДатаКакМесяцПодобратьДатуПоТексту(Текст, ДатаПоТексту = НеОпределено)
	
	СписокВозврата = Новый СписокЗначений;
	ТекущийГод = Год(ДатаСеанса());
	
	Если ПустаяСтрока(Текст) Тогда
		ДатаПоТексту = Дата(1, 1, 1);
		Возврат СписокВозврата;
	КонецЕсли;
	
	Если Найти(Текст, ".") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ".");
	ИначеЕсли Найти(Текст, ",") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ",");
	ИначеЕсли Найти(Текст, "-") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "-");
	ИначеЕсли Найти(Текст, "/") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "/");
	ИначеЕсли Найти(Текст, "\") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "\");
	Иначе
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ");
	КонецЕсли;
	
	Если Подстроки.Количество() = 1 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Текст) Тогда
			МесяцЧислом = Число(Текст);
			Если МесяцЧислом >= 1 и МесяцЧислом <=12 Тогда
				ДатаПоТексту = Дата(ТекущийГод, МесяцЧислом, 1);
				Если СтрДлина(Текст) = 1 Тогда
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='М/гг'"));
				Иначе
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММ/гг'"));
				КонецЕсли;
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
		Иначе
			СписокМесяцев = СписокМесяцевПоСтроке(Текст);
			Для Каждого Месяц Из СписокМесяцев Цикл
				ДатаПоТексту = Дата(ТекущийГод, Месяц, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Подстроки.Количество() = 2 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[1]) Тогда
			
			Если ПустаяСтрока(Подстроки[1]) Тогда
				ГодЧислом = 0;
				Подстроки[1] = "0";
				ТекстВозврата = Текст + "0";
			Иначе
				ГодЧислом = Число(Подстроки[1]);
				ТекстВозврата = "";
			КонецЕсли;
			
			Если ГодЧислом > 3000 Тогда
				Возврат СписокВозврата;
			КонецЕсли;
			
			Если СтрДлина(Подстроки[1]) <= 1 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 3) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 2 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 2) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 3 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 1) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 4 Тогда
				ГодЧислом = Число(Подстроки[1]);
			КонецЕсли;                    
			
		Иначе
			
			Возврат СписокВозврата;
			
		КонецЕсли;                
		Если ЗначениеЗаполнено(Подстроки[0]) И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[0]) Тогда
			
			МесяцЧислом = Число(Подстроки[0]);
			Если МесяцЧислом >= 1 и МесяцЧислом <= 12 Тогда
				ДатаПоТексту = Дата(ГодЧислом, МесяцЧислом, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
			
		Иначе
			
			СписокМесяцев = СписокМесяцевПоСтроке(Подстроки[0]);
			
			Если СписокМесяцев.Количество() = 1 Тогда
				ДатаПоТексту = Дата(ГодЧислом, СписокМесяцев[0], 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Для Каждого Месяц Из СписокМесяцев Цикл
					ДатаПоТексту = Дата(ГодЧислом, Месяц, 1);
					СписокВозврата.Добавить(Формат(Дата(ГодЧислом, Месяц, 1), "ДФ='ММММ гггг'"));
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат СписокВозврата;
	
КонецФункции


&НаКлиенте
Процедура ВводПериодаПриИзменении(РедактируемыйОбъект, ПутьРеквизита, ПутьРеквизитаПредставления, Модифицированность = Ложь)
	
	ЗначениеПредставления = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления);
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	
	ДатаКакМесяцПодобратьДатуПоТексту(ЗначениеПредставления, Значение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект,
	ПутьРеквизитаПредставления,
	ПолучитьПредставлениеДаты(Значение));
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	
	Модифицированность = Истина;
	
	// Vov41k 06.08.2024
	Если ПутьРеквизитаПредставления = "ТК_УчетнаяПолитикаТорговыхПлощадокПериодСтрокой" Тогда 
		ЗапроситьРежимИзмененияПараметровОрганизации(ТК_УчетнаяПолитикаТорговыхПлощадок.Период, Ложь);
	ИначеЕсли ПутьРеквизитаПредставления = "ТК_РежимыРаботыТорговыхПлощадокПериодСтрокой" Тогда 
		ЗапроситьРежимИзмененияРежимРаботы(ТК_РежимыРаботыТорговыхПлощадок.Период, Ложь);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ВводПериодаНачалоВыбора(Форма,
	РедактируемыйОбъект,
	ПутьРеквизита,
	ПутьРеквизитаПредставления,
	ИзменитьМодифицированность = Истина,
	ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("РедактируемыйОбъект", РедактируемыйОбъект);
	ДополнительныеПараметры.Вставить("ПутьРеквизита", ПутьРеквизита);
	ДополнительныеПараметры.Вставить("ПутьРеквизитаПредставления", ПутьРеквизитаПредставления);
	ДополнительныеПараметры.Вставить("ИзменитьМодифицированность", ИзменитьМодифицированность);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ПоясняющийТекст = НСтр("ru='Выберите дату начала действия';uk='Виберіть дату початку дії'") + " ";
	
	Оповещение = Новый ОписаниеОповещения("ВводПериодаНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
	"ОбщаяФорма.ВыборДаты",
	Новый Структура("ПоясняющийТекст, НачальноеЗначение", ПоясняющийТекст),
	,,,,
	Оповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОгнетушителиДатаТОПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Огнетушители.ТекущиеДанные;
	ТекущиеДанные.ДатаСледующегоТО = ДобавитьМесяц(ТекущиеДанные.ДатаТО,12);
КонецПроцедуры

&НаКлиенте
Процедура ВводПериодаНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	РедактируемыйОбъект = ДополнительныеПараметры.РедактируемыйОбъект;
	ПутьРеквизита = ДополнительныеПараметры.ПутьРеквизита;
	ПутьРеквизитаПредставления = ДополнительныеПараметры.ПутьРеквизитаПредставления;
	ИзменитьМодифицированность = ДополнительныеПараметры.ИзменитьМодифицированность;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Значение = ВыбранноеЗначение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	Представление = ПолучитьПредставлениеДаты(Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления, Представление);
	
	Если ИзменитьМодифицированность Тогда 
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	КонецЕсли;
	
	Если ПутьРеквизита = "ТК_УчетнаяПолитикаТорговыхПлощадок.Период" 
		ИЛИ ПутьРеквизита = "ТК_УчетнаяПолитикаТорговыхПлощадок.Организация" Тогда 
		ЗапроситьРежимИзмененияПараметровОрганизации(ТК_УчетнаяПолитикаТорговыхПлощадок.Период, Ложь);
	ИначеЕсли ПутьРеквизита = "ТК_РежимыРаботыТорговыхПлощадок.Период" Тогда 
		ЗапроситьРежимИзмененияРежимРаботы(ТК_РежимыРаботыТорговыхПлощадок.Период, Ложь);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияПараметровОрганизации(ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ЗапроситьРежимИзмененияУчетнаяПолитикаТорговыхПлощадокЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстКнопкиДа = НСтр("ru='Изменить Организацию';uk='Змінити Організацію'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru =  'При редактировании изменилась Организация.
	|Если просто исправлены прежние данные (они были ошибочны), нажмите ""Исправлена ошибка"".
	|Если Организация изменилась с %1, нажмите ""Изменить Организацию""'"), 
	Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"));
	
	ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "ТК_УчетнаяПолитикаТорговыхПлощадок", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияРегистра(Форма, ИмяРегистра, ТекстЗапроса, ТекстКнопкиДа, Отказ, ОповещениеЗавершения = Неопределено)
	// Требуется запрашивать пользователя об изменении только если еще не принято решение, что запись - новая
	Если Форма[ИмяРегистра + "НоваяЗапись"] = Истина Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// Требуется запрашивать пользователя об изменении только если задана дата записи
	Если Не ЗначениеЗаполнено(Форма[ИмяРегистра].Период) Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// Требуется запрашивать пользователя об изменении только если была считана прежняя запись
	Если Не ЗначениеЗаполнено(Форма[ИмяРегистра + "Прежняя"].Период) Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	//ИзменилсяПериод = (Форма[ИмяРегистра].Период <> Форма[ИмяРегистра + "Прежняя"].Период);
	ИзменилисьДанные = Ложь;
	Для Каждого Поле Из Форма[ИмяРегистра + "Прежняя"] Цикл
		//Если Поле.Ключ = "Период" Тогда
		//	Продолжить;
		//КонецЕсли;
		ИзменилисьДанные = Форма[ИмяРегистра][Поле.Ключ] <> Поле.Значение;
		Если ИзменилисьДанные Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Требуется запрашивать пользователя об изменении - изменили и текущие данные, и дату записи
	Если ИзменилисьДанные  Тогда
		
		Кнопки = Новый СписокЗначений();
		Кнопки.Добавить(КодВозвратаДиалога.Нет,  	НСтр("ru='Исправлена ошибка';uk='Виправлена помилка'"));
		Кнопки.Добавить(КодВозвратаДиалога.Да, 		ТекстКнопкиДа);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, 	НСтр("ru='Отмена';uk='Відмінити'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма",                Форма);
		ДополнительныеПараметры.Вставить("Отказ",                Отказ);
		ДополнительныеПараметры.Вставить("ИмяРегистра",          ИмяРегистра);
		ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
		
		ПоказатьВопрос(
		Новый ОписаниеОповещения("ЗапроситьРежимИзмененияРегистраОбработкаОтвета", ЭтотОбъект, ДополнительныеПараметры), 
		ТекстЗапроса, 
		Кнопки, ,
		КодВозвратаДиалога.Отмена);
		
		Возврат;
		
	Иначе
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияРегистраОбработкаОтвета(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Форма                = ДополнительныеПараметры.Форма;
	Отказ                = ДополнительныеПараметры.Отказ;
	ИмяРегистра          = ДополнительныеПараметры.ИмяРегистра;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;	
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Форма[ИмяРегистра + "НоваяЗапись"] = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияУчетнаяПолитикаТорговыхПлощадокЗавершение(Отказ, ДополнительныеПараметры) Экспорт 
	
	Если НЕ Отказ Тогда
		НастройкиОрганизацииВведены = Истина;
	КонецЕсли;
	
КонецПроцедуры



// Vov41k 06.08.2024
&НаКлиенте
Процедура ЗапроситьРежимИзмененияРежимРаботы(ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ЗапроситьРежимИзмененияРежимРаботыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстКнопкиДа = НСтр("ru = 'Изменить Режим работы'; uk = 'Змінити Режим роботи'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'При редактировании изменился Режим роботы.
                              |Если просто исправлены прежние данные (они были ошибочны), нажмите ""Исправлена ошибка"".
                              |Если Режим работы изменился с %1, нажмите ""Изменить Режим работы""'"), 
						Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"));
	
	ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "ТК_РежимыРаботыТорговыхПлощадок", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияРежимРаботыЗавершение(Отказ, ДополнительныеПараметры) Экспорт 
	
	Если НЕ Отказ Тогда
		ТК_РежимРаботыВведен = Истина;
	КонецЕсли;
	
КонецПроцедуры







&НаКлиенте
Процедура УчетнаяПолитикаТорговыхПлощадокПериодСтрокойПриИзменении(Элемент)
	
	ВводПериодаПриИзменении(
		ЭтаФорма,
		"ТК_УчетнаяПолитикаТорговыхПлощадок.Период",
		"ТК_УчетнаяПолитикаТорговыхПлощадокПериодСтрокой",
		Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ТК_РежимыРаботыТорговыхПлощадокПериодСтрокойПриИзменении(Элемент)
	
	ВводПериодаПриИзменении(
		ЭтаФорма,
		"ТК_РежимыРаботыТорговыхПлощадок.Период",
		"ТК_РежимыРаботыТорговыхПлощадокПериодСтрокой",
		Модифицированность);	
	
КонецПроцедуры


&НаКлиенте
Процедура УчетнаяПолитикаТорговыхПлощадокПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВводПериодаНачалоВыбора(
		ЭтаФорма,
		ЭтаФорма,
		"ТК_УчетнаяПолитикаТорговыхПлощадок.Период",
		"ТК_УчетнаяПолитикаТорговыхПлощадокПериодСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура ТК_РежимыРаботыТорговыхПлощадокПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВводПериодаНачалоВыбора(
		ЭтаФорма,
		ЭтаФорма,
		"ТК_РежимыРаботыТорговыхПлощадок.Период",
		"ТК_РежимыРаботыТорговыхПлощадокПериодСтрокой");	
	
КонецПроцедуры


&НаКлиенте
Процедура ИсторияИзменений(Команда)
	
	ТолькоПросмотрИстории = Ложь;
	
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию(
		"ТК_УчетнаяПолитикаТорговыхПлощадок",
		Объект.Ссылка,	
		ЭтаФорма,
		ТолькоПросмотрИстории);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимРаботыИсторияИзменений(Команда)
	
	ТолькоПросмотрИстории = Ложь;
	
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию(
		"ТК_РежимыРаботыТорговыхПлощадок",
		Объект.Ссылка,	
		ЭтаФорма,
		ТолькоПросмотрИстории);
	
КонецПроцедуры



&НаКлиенте
Процедура УчетнаяПолитикаОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = ТК_УчетнаяПолитикаТорговыхПлощадок.Организация Тогда 
		СтандартнаяОбработка = Ложь;
	Иначе
		НастройкиОрганизацииВведены = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТК_РежимыРаботыТорговыхПлощадокРежимРаботыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы Тогда 
		СтандартнаяОбработка = Ложь;
	Иначе 
		ТК_РежимРаботыВведен = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
КонецПроцедуры




// СтандартныеПодсистемы.КонтактнаяИнформация
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	УправлениеКонтактнойИнформациейКлиент.НачатьИзменение(ЭтотОбъект, Элемент);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыбор(ЭтотОбъект, Элемент, , СтандартнаяОбработка);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОчистку(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	УправлениеКонтактнойИнформациейКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда.Имя);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, Элемент.Имя, СтандартнаяОбработка);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачатьОбработкуНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьОбновлениеКонтактнойИнформации(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьКонтактнуюИнформацию(Результат);
КонецПроцедуры
&НаСервере
Процедура ОбновитьКонтактнуюИнформацию(Результат)
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Объект, Результат);
КонецПроцедуры
// Конец СтандартныеПодсистемы.КонтактнаяИнформация



#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры




// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОформлениеТЧ_Потребители();
	Для Каждого строка из Объект.Потребители Цикл
		Потребитель = строка.Потребитель;
		Если ТипЗнч(Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") Тогда
			строка.ИндексКартинки = 8;
			строка.Код = Потребитель.Код;
			строка.Контрагент = Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(Потребитель, ТекущаяДата()).Контрагент;
		ИначеЕсли ТипЗнч(Потребитель) = Тип("СправочникСсылка.ТочкиДоставки") Тогда
			строка.ИндексКартинки = 5;
			строка.Код = Потребитель.Global_ID;
			строка.Контрагент = Потребитель.Владелец;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПолучениеДанные();
	ШтатнаяЧисленность = ШтатнаяЧисленность(Объект.Код);
	РезультатПодключениеТочки = ПодключениеТочки(Объект.Ссылка);
	Если РезультатПодключениеТочки <> Неопределено Тогда
		ПодключениеТочки = РезультатПодключениеТочки.ПодключениеТочки;
		ПодключенЧерез = РезультатПодключениеТочки.ПодключенЧерез;
		КоммунальнаяУслуга = РезультатПодключениеТочки.КоммунальнаяУслуга;
	КонецЕсли;
	Элементы.ПодключенЧерез.Видимость = ЗначениеЗаполнено(ПодключенЧерез);
	КоличествоДверейХолодильников = КоличествоДверейХолодильников(Объект.Ссылка,"f2540698-b69f-11ee-bbdb-005056bc2b7a");
	КоличествоДверейМорозильников = КоличествоДверейХолодильников(Объект.Ссылка,"3aaa5dcd-b616-11ee-bbdb-005056bc2b7a");
	ЗаполнитьДанныеПоАренде();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШтатнаяЧисленность(КодОбъекта)
	Определение = Новый WSОпределения("https://orders-1c.digma.ua/zuUU/ws/cwd2.1cws?wsdl","ObmenSSL","bynthghbpf",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
	WS = Новый WSПрокси(Определение,"http://www.digma.ua","CommunicationWithDatabases","CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
	WS.Пользователь="ObmenSSL";
	WS.Пароль ="bynthghbpf";
	
	ШЧ = WS.GetStaffing(КодОбъекта);
	
	Возврат ШЧ;
КонецФункции

&НаСервереБезКонтекста
Функция ПодключениеТочки(Потребитель)
	ПоставщикЭЭ = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТорговыеПлощадкиПотребители.Ссылка КАК ПодключенЧерез,
	|	КоммунальныеУслугиПотребители.Ссылка КАК КоммунальнаяУслуга,
	|	КоммунальныеУслугиПотребители.Ссылка.Контрагент КАК ПодключениеТочки
	|ИЗ
	|	Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
	|		ПО ТорговыеПлощадкиПотребители.Ссылка = КоммунальныеУслугиПотребители.Потребитель
	|ГДЕ
	|	ТорговыеПлощадкиПотребители.Потребитель = &Потребитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	NULL,
	|	КоммунальныеУслугиПотребители.Ссылка,
	|	КоммунальныеУслугиПотребители.Ссылка.Контрагент
	|ИЗ
	|	Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
	|ГДЕ
	|	КоммунальныеУслугиПотребители.Потребитель = &Потребитель";
	
	Запрос.УстановитьПараметр("Потребитель", Потребитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = Новый Структура("ПодключениеТочки,ПодключенЧерез,КоммунальнаяУслуга");
		ЗаполнитьЗначенияСвойств(Результат,ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоДверейХолодильников(Площадка,ГуидТипаОборудования)
	Количество = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ТорговыеПлощадкиОборудованиеПоставщиков.КоличествоДверей) КАК КоличествоДверей
	|ИЗ
	|	Справочник.ТорговыеПлощадки.ОборудованиеПоставщиков КАК ТорговыеПлощадкиОборудованиеПоставщиков
	|ГДЕ
	|	ТорговыеПлощадкиОборудованиеПоставщиков.Оборудование В ИЕРАРХИИ(&Оборудование)
	|	И ТорговыеПлощадкиОборудованиеПоставщиков.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Площадка);
	Запрос.УстановитьПараметр("Оборудование", Справочники.ОборудованиеПоставщиков.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидТипаОборудования)));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Количество = ВыборкаДетальныеЗаписи.КоличествоДверей;
	КонецЕсли;
	
	Возврат Количество;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеПоАренде()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УсловияАрендыПоДоговорамСрезПоследних.Период КАК Период,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента КАК Договор,
		|	УсловияАрендыПоДоговорамСрезПоследних.СуммаФ1 КАК СуммаФ1,
		|	УсловияАрендыПоДоговорамСрезПоследних.СуммаФ2 КАК СуммаФ2,
		|	УсловияАрендыПоДоговорамСрезПоследних.СуммаФ1 + УсловияАрендыПоДоговорамСрезПоследних.СуммаФ2 КАК ОбщаяСумма,
		|	УсловияАрендыПоДоговорамСрезПоследних.ГарантийныйПлатеж КАК СуммаГарантийногоПлатеж,
		|	УсловияАрендыПоДоговорамСрезПоследних.ТорговаяПлощадка КАК Объект,
		|	УсловияАрендыПоДоговорамСрезПоследних.Согласовано КАК Согласовано,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.Организация КАК Организация,
		|	УсловияАрендыПоДоговорамСрезПоследних.ТорговаяПлощадка.Код КАК КодТТ,
		|	УсловияАрендыПоДоговорамСрезПоследних.ТорговаяПлощадка.ТерриторияПоКОАТУУ КАК Район,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.Контрагент КАК Контрагент,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.ДатаОкончанияДействия КАК СрокДействияДоговора,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.ПравоСобственности КАК ПравоСобственности,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.КонтактноеЛицоАренда КАК КонтактоеЛицо,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.КонтактАренда КАК НомерТелефона,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.АдресЭлектронныйАренда КАК ЭлектроныйАдрес,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.Контрагент.КодПоЕДРПОУ КАК ИНН,
		|	УсловияАрендыПоДоговорамСрезПоследних.ДоговорКонтрагента.ВариантПродления КАК ПролонгацияДоговора,
		|	""1 раз в год"" КАК ПереодичностьПересмотраАренднойПлаты,
		|	""письменое обращение"" КАК СпособПересмотраАренднойПлаты,
		|	""за 30 дней"" КАК ДоговорМожетБытьРасторгнут,
		|	""с 25 текущего месяца до 5 следующего месяца"" КАК ДатаОплатыЗаАренду,
		|	""с 25 текущего месяца до 15 следующего месяца"" КАК ДатаОплатыПоКомунальнымПлатежам
		|ИЗ
		|	РегистрСведений.УсловияАрендыПоДоговорам.СрезПоследних(&НаДату, ТорговаяПлощадка = &ТорговаяПлощадка) КАК УсловияАрендыПоДоговорамСрезПоследних";
	
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	Запрос.УстановитьПараметр("ТорговаяПлощадка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Аренда.Добавить(),ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Элементы)
	
	ГруппыДепартаментов = Новый Соответствие;
	ГруппыДепартаментов.Вставить("ГруппаДепартаментБезопасности"	,"ТК_ДепартаментБезопасности");
	ГруппыДепартаментов.Вставить("ГруппаДепартаментИТ"				,"ТК_ДепартаментИТ");
	ГруппыДепартаментов.Вставить("ГруппаДепартаментКоммерческий"	,"ТК_ДепартаментКоммерческий");
	ГруппыДепартаментов.Вставить("ГруппаДепартаментОперационный"	,"ТК_ДепартаментОперационный");
	ГруппыДепартаментов.Вставить("ГруппаДепартаментРазвития"		,"ТК_ДепартаментРазвития");
	ГруппыДепартаментов.Вставить("ГруппаДепартаментФинансов"		,"ТК_ДепартаментФинансов");
	ГруппыДепартаментов.Вставить("ГруппаОтделОТиПБ"					,"ТК_ОтделОТиПБ");
	ГруппыДепартаментов.Вставить("ГруппаОборудованиеПоставщиков"	,"ТК_ОборудованиеПоставщиков");
	ГруппыДепартаментов.Вставить("ГруппаКоммунальныеУслуги"			,"ТК_ДобавлениеИзменениеКоммунальныхУслуг");
	
	Индекс = 0;
	СписокРолиДоступны = "";
	Количество = ГруппыДепартаментов.Количество();
	Для Каждого Департаменто Из ГруппыДепартаментов Цикл
		Индекс = 1 + Индекс;
		СписокРолиДоступны = СписокРолиДоступны + Департаменто.Значение + ?(Индекс = Количество, "", ",");
	КонецЦикла;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь(Пользователи.ТекущийПользователь(),Истина);
	
	Департамент =  НЕ ЭтоПолноправныйПользователь И Пользователи.РолиДоступны(СписокРолиДоступны);
	
	Если Департамент Тогда
		
		МассивРеквизитов = Новый Массив;
		МассивРеквизитов.Добавить("ГруппаШапка");
		МассивРеквизитов.Добавить("ГруппаОсновное");
		
		Для Каждого Департаменто Из ГруппыДепартаментов Цикл
			Если Пользователи.РолиДоступны(Департаменто.Значение) Тогда 
				ТекГруппа = ?(Департаменто.Значение = "ТК_ОборудованиеПоставщиков" ИЛИ Департаменто.Значение = "ТК_ДобавлениеИзменениеКоммунальныхУслуг",Департаменто.Ключ,Неопределено);
			Иначе
				МассивРеквизитов.Добавить(Департаменто.Ключ);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекГруппа) Тогда
			ТекущаяСтраница = Элементы[ТекГруппа];
		Иначе
			ТекущаяСтраница = Элементы.ГруппаПаспорт;
		КонецЕсли;
		
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивРеквизитов, "ТолькоПросмотр", Истина);
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСтраницы", "ТекущаяСтраница", ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизитыВФорме()
	
	//ГруппаДепартаментБезопасности;
	//ГруппаДепартаментИТ;
	//ГруппаДепартаментКоммерческий;
	//ГруппаДепартаментОперационный;
	//ГруппаДепартаментРазвития;
	//ГруппаДепартаментФинансов;
	//ГруппаОтделОТиПБ
	
	WebЦветаАнтикБелый =  WebЦвета.АнтикБелый;
	Если РольДоступна("ТК_ДепартаментБезопасности") Тогда
		Элементы.ГруппаДепартаментБезопасности.Показать();
		Элементы.ГруппаДепартаментБезопасности.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ДепартаментИТ") Тогда
		Элементы.ГруппаДепартаментИТ.Показать();
		Элементы.ГруппаДепартаментИТ.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ДепартаментКоммерческий") Тогда
		Элементы.ГруппаДепартаментКоммерческий.Показать();
		Элементы.ГруппаДепартаментКоммерческий.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ДепартаментОперационный") Тогда
		Элементы.ГруппаДепартаментОперационный.Показать();
		Элементы.ГруппаДепартаментОперационный.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ДепартаментРазвития") Тогда
		Элементы.ГруппаДепартаментРазвития.Показать();
		Элементы.ГруппаДепартаментРазвития.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ДепартаментФинансов") Тогда
		Элементы.ГруппаДепартаментФинансов.Показать();
		Элементы.ГруппаДепартаментФинансов.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	Если РольДоступна("ТК_ОтделОТиПБ") Тогда
		Элементы.ГруппаОтделОТиПБ.Показать();
		Элементы.ГруппаОтделОТиПБ.ЦветФона = WebЦветаАнтикБелый;
	КонецЕсли;
	
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ОхранноеАгентство"						, "ГруппаДепартаментБезопасности", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПультовойНомер"							, "ГруппаДепартаментБезопасности", Доступность = Истина);
	
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "Провайдер"								, "ГруппаДепартаментИТ", Доступность = Истина);
	
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПартнерскаяПрограмма"					, "ГруппаДепартаментКоммерческий", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "КоличествоМорозильногоОборудования"		, "ГруппаДепартаментКоммерческий", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ДлинаПолочногоПространства"				, "ГруппаДепартаментКоммерческий", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЧасыПриемаТовара"						, "ГруппаДепартаментКоммерческий", Доступность = Истина);
	
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "НомерТелефонаОбъекта"					, "ГруппаДепартаментОперационный", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "КураторПлощадки"							, "ГруппаДепартаментОперационный", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ГрафикРаботы"							, "ГруппаДепартаментОперационный", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "РежимРаботы"								, "ГруппаДепартаментОперационный", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ВодаДляХозНужд"							, "ГруппаДепартаментОперационный", Доступность = Истина);
	
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЧасыПродажиАлкоголя"						, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПлощадьОбщая"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПлощадьТорговая"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПлощадьПодсобка"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПлощадьСанузел"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ВидПлощадки"								, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ОбъектВАренде"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЗемляПодОбъектомВАренде"					, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ДоговорАрендыЗемлиПодОбъектом"			, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЛимитМощностиЗима"						, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЛимитМощностиВеснаОсень"					, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ЛимитМощностиЛето"						, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "УрнаГородская"							, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "УборкаТерритории"						, "ГруппаДепартаментРазвития", Доступность = Истина);
	ТК_ДопРеквизитыСервер.ПереместитьДополнительныйРеквизит(ЭтаФорма, "ПроизводительКонструкции"				, "ГруппаДепартаментРазвития", Доступность = Истина);
	
	Поле = ТК_ДопРеквизитыСервер.ПолучитьПолеДополнительногоРеквизитаНаФорме(ЭтаФорма, "Геометка");
	Если Поле <> Неопределено Тогда
		ИмяГруппыРеквизита = СтрЗаменить(Поле.ИмяРеквизитаЗначение,"ДополнительныйРеквизитЗначение", "Группа");
		ГруппыРеквизита = ЭтаФорма.Элементы.Найти(ИмяГруппыРеквизита);
		Если ГруппыРеквизита <> Неопределено Тогда
			ЭтаФорма.Элементы.Переместить(ГруппыРеквизита, Элементы.ГруппаДепартаментОперационный);
		КонецЕсли;
		ПолеФормы = ЭтаФорма.Элементы[Поле.ИмяРеквизитаЗначение];
		ПолеФормы.УстановитьДействие("ОбработкаНавигационнойСсылки", "ДР_ГеометкаНажатие");
	КонецЕсли;
	
	Поле = ТК_ДопРеквизитыСервер.ПолучитьПолеДополнительногоРеквизитаНаФорме(ЭтаФорма, "ОбъектВАренде");
	Если Поле <> Неопределено Тогда
		ПолеФормы = ЭтаФорма.Элементы[Поле.ИмяРеквизитаЗначение];
		ПолеФормы.Заголовок = "Объект находится";
	КонецЕсли;
	Поле = ТК_ДопРеквизитыСервер.ПолучитьПолеДополнительногоРеквизитаНаФорме(ЭтаФорма, "ЗемляПодОбъектомВАренде");
	Если Поле <> Неопределено Тогда
		ПолеФормы = ЭтаФорма.Элементы[Поле.ИмяРеквизитаЗначение];
		ПолеФормы.Заголовок = "Земля под объектом находится";
	КонецЕсли;
	Поле = ТК_ДопРеквизитыСервер.ПолучитьПолеДополнительногоРеквизитаНаФорме(ЭтаФорма, "НомерТелефонаОбъекта");
	Если Поле <> Неопределено Тогда
		ПолеФормы = ЭтаФорма.Элементы[Поле.ИмяРеквизитаЗначение];
		ПолеФормы.Маска = "(999) 999-99-99";
	КонецЕсли;
	Элементы.ГруппаКонтактнаяИнформацияПоле58094408x6eaex11eaxa223x005056bf18571.Видимость = Ложь;
КонецПроцедуры

&НаСервере
Функция ЗначениеГеометкаНаСервере()
	ЗначениеГеометка = ТК_ДопРеквизитыСервер.ПолучитьЗначениеДополнительногоРеквизитаНаФорме(ЭтаФорма, "Геометка");
	Возврат ЗначениеГеометка;
КонецФункции

&НаКлиенте
Процедура ДР_ГеометкаНажатие(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ЗначениеГеометка = ЗначениеГеометкаНаСервере();
	НачальныйНомер = СтрНайти(ЗначениеГеометка,">");
	ЧислоСимволов = СтрДлина(ЗначениеГеометка) - НачальныйНомер - 4;
	ЗначениеГеометка = Сред(ЗначениеГеометка,1 + НачальныйНомер,ЧислоСимволов);
	НавигационнаяСсылкаФорматированнойСтроки = "https://www.google.com/search?q=" + ЗначениеГеометка;
	//ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://www.google.com/search?q=" + ЗначениеМестоположение);
КонецПроцедуры



#КонецОбласти


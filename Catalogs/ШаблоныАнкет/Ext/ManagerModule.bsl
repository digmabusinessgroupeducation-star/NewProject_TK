///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция АнкетаКТочкеДляСтартСтопа(КодТочки) Экспорт
	
	Ответ = Новый Структура;
	Ответ.Вставить("DescriptionErrors","");
	Ответ.Вставить("GuidShop");
	Ответ.Вставить("NameShop");
	Ответ.Вставить("GuidCheckList");
	Ответ.Вставить("CheckList");
	//УстановитьПривилегированныйРежим(Истина);
	
	ТорговаяПлощадка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(КодТочки);
	
	Если ТорговаяПлощадка = Неопределено Тогда
		Ответ.DescriptionErrors = "По коду "+СокрЛП(КодТочки)+" торговая точка не найдена!";
	Иначе
		//Анкета = Справочники.ШаблоныАнкет.ПолучитьСсылку(Новый УникальныйИдентификатор("58c90c44-6606-11ee-bbc8-005056bc658e"));	//	Чек-лист Кураторы (ТСД)
		Анкета = Справочники.ШаблоныАнкет.ПолучитьСсылку(Новый УникальныйИдентификатор("86e7be2c-3235-11ef-bbdd-005056bc2b7a"));	//	Чек-лист Кураторы (ТСД) 2024.07.01
		ВопросыАнкеты = ВопросыАнкеты(Анкета);
		Ответ.CheckList = ВопросыАнкеты;
		Ответ.GuidCheckList = Строка(Анкета.УникальныйИдентификатор());
		Ответ.GuidShop = Строка(ТорговаяПлощадка.УникальныйИдентификатор());
		Ответ.NameShop = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТорговаяПлощадка,"Наименование");
	КонецЕсли;
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	ЗаписатьJSON(ЗаписьJSON, Ответ); 
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив Из Строка
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Наименование");
	Результат.Добавить("Заголовок");
	Результат.Добавить("Вступление");
	Результат.Добавить("Заключение");
	Результат.Добавить("РедактированиеШаблонаЗавершено");
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

Функция ВопросыАнкеты(Анкета)
	
	МассивВопросов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВопросыШаблонаАнкеты.Родитель КАК Родитель,
	|	ВопросыШаблонаАнкеты.Ссылка КАК Ссылка,
	|	ВопросыШаблонаАнкеты.Формулировка КАК Вопрос,
	|	ВопросыШаблонаАнкеты.Обязательный КАК Обязательный,
	|	ВопросыШаблонаАнкеты.ЭлементарныйВопрос.ТипЗначения КАК ТипЗначения,
	|	ВопросыШаблонаАнкеты.ЭлементарныйВопрос.ТребуетсяКомментарий КАК ТребуетсяКомментарий
	|ИЗ
	|	Справочник.ВопросыШаблонаАнкеты КАК ВопросыШаблонаАнкеты
	|ГДЕ
	|	ВопросыШаблонаАнкеты.Владелец = &Владелец
	|	И ВопросыШаблонаАнкеты.Родитель <> ЗНАЧЕНИЕ(справочник.ВопросыШаблонаАнкеты.Пустаяссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВопросыШаблонаАнкеты.Родитель.Код,
	|	ВопросыШаблонаАнкеты.Код
	|ИТОГИ ПО
	|	Родитель";
	
	Запрос.УстановитьПараметр("Владелец", Анкета);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаРодитель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Ур1 = 0;
	Пока ВыборкаРодитель.Следующий() Цикл
		Ур1 = 1 + Ур1;
		СтруктураГруппы = новый Структура;
		СтруктураГруппы.Вставить("id",Строка(Ур1)+".");
		СтруктураГруппы.Вставить("Name",СокрЛП(ВыборкаРодитель.Родитель.Наименование));
		СтруктураГруппы.Вставить("Guid",Строка(ВыборкаРодитель.Родитель.УникальныйИдентификатор()));
		
		Выборка = ВыборкаРодитель.Выбрать();
		
		МассивУровня = Новый Массив;
		Ур2 = 0;
		Пока Выборка.Следующий() Цикл
			Ур2 = 1 + Ур2;
			СтруктураВопросов = новый Структура;
			СтруктураВопросов.Вставить("id",Строка(Ур1)+"."+Строка(Ур2)+".");
			СтруктураВопросов.Вставить("Name",СокрЛП(Выборка.Вопрос));
			СтруктураВопросов.Вставить("Required",Выборка.Обязательный);
			СтруктураВопросов.Вставить("TypeValue",Строка(Выборка.ТипЗначения));
			СтруктураВопросов.Вставить("CommentRequired",Выборка.ТребуетсяКомментарий);
			СтруктураВопросов.Вставить("Guid",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
			МассивУровня.Добавить(СтруктураВопросов);
		КонецЦикла;
		СтруктураГруппы.Вставить("items",МассивУровня);
		МассивВопросов.Добавить(СтруктураГруппы);
	КонецЦикла;
	
	
	Возврат МассивВопросов;
	
КонецФункции



#КонецОбласти

#КонецОбласти

#КонецЕсли

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс


// Возвращает список доступных видов договора для документа.
//
// Параметры
//	Документ  - любой документ, предусматривающий договор контрагента
//	ВидОперации  - вид операции документа.
//
// Возвращаемое значение:
//	<СписокЗначений>   - список видов договора, доступных для документа.
//
Функция ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации = Неопределено, ИмяТабличнойЧасти = "") Экспорт
	
	СписокВидовДоговора = Новый СписокЗначений;
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		
		Если ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваров")
			ИЛИ ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваровТранзиты")Тогда
			
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.Поставка"));
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.ПоставкаПереводДолга"));
			
		ИначеЕсли ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваровКомиссия") Тогда
			
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.КомиссияПоставка"));
			
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратПоставщику") ИЛИ ТипДокумента = Тип("ДокументСсылка.ТК_ЗаявкаНаВозвратПоставщику") Тогда 
		
		Если ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ВозвратТоваровПоставщику")
			ИЛИ ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты") 
			ИЛИ ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаявкаНаВозвратПоставщику") 
			ИЛИ ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаявкаНаВозвратПоставщикуТранзиты") Тогда 
			
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.Поставка"));
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.ПоставкаПереводДолга"));
			
		ИначеЕсли ВидОперации = ПредопределенноеЗначение("Справочник.ХозОперации.ВозвратТоваровПоставщикуКомиссия") Тогда 			
			
			СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.КомиссияПоставка"));
			
		КонецЕсли;
	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказПокупателя")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВозвратОтПокупателя")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
		
		СписокВидовДоговора.Добавить(ПредопределенноеЗначение("Справочник.ВидыДоговоров.Продажа"));
		
	КонецЕсли;
	
	
	Возврат СписокВидовДоговора;
	
КонецФункции // ПолучитьСписокВидовДоговораДляДокумента()

// Получает договор контрагента по умолчанию с учетом условий отбора. Возвращается основной договор или единственный или пустую ссылку.
//
// Параметры
//  Контрагент	–	<СправочникСсылка.Контрагенты> 
//							Контрагент, договор которого нужно получить
//  Организация	–	<СправочникСсылка.Организации> 
//							Организация, договор которой нужно получить
//  Подразделение –	<СправочникСсылка.ПодразделенияКомпании> 
//  СписокВидовДоговора	–	<Массив> или <СписокЗначений>, состоящий из значений типа <ПеречислениеСсылка.ВидыДоговоров> 
//							Нужные виды договора
//
// Возвращаемое значение:
//   <СправочникСсылка.ДоговорыКонтрагентов> – найденный договор или пустая ссылка
//
Функция ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговора = Неопределено, ВалютаРасчетов = Неопределено, ДатаДоговора,НомерДоговора= Неопределено,ТорговаяПлощадка = Неопределено,Менеджер=Неопределено) Экспорт
	
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И НЕ ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|	И ДоговорыКонтрагентов.Подразделение = &Подразделение
	|	И (ДоговорыКонтрагентов.ДатаНачалаДействия <= &ДатаДоговора
	|				И ДоговорыКонтрагентов.ДатаОкончанияДействия = &ПустаяДата
	|			ИЛИ &ДатаДоговора МЕЖДУ ДоговорыКонтрагентов.ДатаНачалаДействия И ДоговорыКонтрагентов.ДатаОкончанияДействия)"
	+?(СписокВидовДоговора <> Неопределено,"
	|	И ДоговорыКонтрагентов.ВидДоговора В (&СписокВидовДоговора)","")
	+?(НомерДоговора <> Неопределено,"
	|	И ДоговорыКонтрагентов.Номер = (&НомерДоговора)","")
	+?(ВалютаРасчетов <> Неопределено,"
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = (&ВалютаРасчетов)","")
	+?(Менеджер <> Неопределено,"
	|	И ДоговорыКонтрагентов.Менеджер = &Менеджер","")
	+?(ТорговаяПлощадка <> Неопределено,"
	|	И ДоговорыКонтрагентов.ТорговаяПлощадка = &ТорговаяПлощадка","")+"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорыКонтрагентов.ДатаНачалаДействия УБЫВ,
	|	ДоговорыКонтрагентов.ДатаОкончанияДействия";
	
	
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ДатаДоговора",ДатаДоговора);
	Запрос.УстановитьПараметр("ПустаяДата", Дата("00010101"));
	Запрос.УстановитьПараметр("СписокВидовДоговора", СписокВидовДоговора);
	Запрос.УстановитьПараметр("ВалютаРасчетов", ВалютаРасчетов);
	Запрос.УстановитьПараметр("НомерДоговора", НомерДоговора);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Выборка.Следующий();
	Возврат Выборка.Ссылка;

КонецФункции // ПолучитьДоговорКонтрагента()

Функция ПолучитьСогласованныйДоговорПоНоменклатуре(Организация,ПодразделениеКомпании,Контрагент,Номенклатура)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ЦеныКонтрагентовСрезПоследних.Регистратор.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ИЗ
		|	РегистрСведений.ЦеныКонтрагентов.СрезПоследних(
		|			,
		|			Контрагент = &Контрагент
		|				И Номенклатура = &Номенклатура
		|				И Регистратор.Организация = &Организация
		|				И Регистратор.ПодразделениеКомпании = &ПодразделениеКомпании) КАК ЦеныКонтрагентовСрезПоследних";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.ДоговорВзаиморасчетов;
	
КонецФункции


// Получает таблицу доступных договоров по переданным параметрам
//
// Параметры
//  Параметры	–	<Структура> 
//							Параметры отбора договоров
//
// Возвращаемое значение:
//   <ТаблицаЗначений> – Таблица найденных договоров
//
Функция ПолучитьДоступныеДоговоры(Параметры) Экспорт
	
	Запрос = Новый Запрос;
		
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Договоры.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК Договоры
				   |	%1
	               |ГДЕ
	               |	Договоры.Контрагент = &Контрагент
				   |	%2";
	
	ТекстСоединений = "";
	ТекстОтборов = "";
	            
	Запрос.УстановитьПараметр("Контрагент", Параметры.Контрагент);
	Если Параметры.Свойство("Организация") тогда
		ТекстОтборов = ТекстОтборов + "И (Договоры.Организация=ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) ИЛИ Договоры.Организация=&Организация)" + Символы.ПС;
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	КонецЕсли;
	
	Если Параметры.Свойство("Подразделение") тогда
		ТекстОтборов = ТекстОтборов + "И (Договоры.Подразделение = &Подразделение ИЛИ Договоры.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияКомпании.ПустаяСсылка))" + Символы.ПС;		
		Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	КонецЕсли;
	
	Если Параметры.Свойство("Дата") тогда
		
		Запрос.УстановитьПараметр("Дата", Параметры.Дата);
		
		Если Не ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Незакрытые", Ложь) Тогда 
			
			ТекстОтборов = ТекстОтборов + "И Договоры.ДатаНачалаДействия < &Дата
										|И (Договоры.ДатаОкончанияДействия > &Дата
										|		ИЛИ Договоры.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1))";
		Иначе
			
			ТекстСоединений = "		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(&Дата, Контрагент = &Контрагент) КАК РасчетыСПоставщикамиПоДокументамОстатки
								|		ПО (РасчетыСПоставщикамиПоДокументамОстатки.ДоговорВзаиморасчетов = Договоры.Ссылка)
								|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(&Дата, Контрагент = &Контрагент) КАК РасчетыСПокупателямиПоДокументамОстатки
								|		ПО (РасчетыСПокупателямиПоДокументамОстатки.ДоговорВзаиморасчетов = Договоры.Ссылка)";
			
			ТекстОтборов = ТекстОтборов + "И ЕСТЬNULL(РасчетыСПоставщикамиПоДокументамОстатки.СуммаОстаток, 0) <> 0 И ЕСТЬNULL(РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток, 0) <> 0" + Символы.ПС;
			
		КонецЕсли;		
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВидДоговора") тогда
		ТекстОтборов = ТекстОтборов + "И Договоры.ВидДоговора В (&ВидДоговора)" + Символы.ПС;
		Запрос.УстановитьПараметр("ВидДоговора", Параметры.ВидДоговора);
	КонецЕсли;
		
	Если Параметры.Свойство("Валюта") тогда
		ТекстОтборов = ТекстОтборов + "И Договоры.ВалютаВзаиморасчетов=&Валюта";
		Запрос.УстановитьПараметр("Валюта", Параметры.Валюта);
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ТекстСоединений, ТекстОтборов);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(Менеджер)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры


Функция СрокОплатыПоДоговору(Знач Период = Неопределено,ДоговорКонтрагента)Экспорт
	
	//УстановитьПривилегированныйРежим(Истина);
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура("Ф1,Ф2",0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СрокиОплатПоДоговорамСрезПоследних.Дней КАК Ф1,
		|	СрокиОплатПоДоговорамСрезПоследних.Дней2 КАК Ф2
		|ИЗ
		|	РегистрСведений.СрокиОплатПоДоговорам.СрезПоследних(&НаДату, ДоговорКонтрагента = &ДоговорКонтрагента) КАК СрокиОплатПоДоговорамСрезПоследних";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("НаДату", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;	
		
		
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	ЗаполнитьЗначенияСвойств(СтруктураВозврата,ВыборкаДетальныеЗаписи);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьДоговорСогласованнойЦены(Период,Подразделение,Контрагент,Договор=Неопределено,Номенклатура)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Согл.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ИЗ
		|	РегистрСведений.СогласованиеЦен.СрезПоследних(
		|			&Период,
		|			Контрагент = &Контрагент
		|				И ДоговорВзаиморасчетов.Подразделение = &Подразделение
		|				И Номенклатура = &Номенклатура) КАК Согл
		|ГДЕ
		|	Согл.СрокДействия >= &Период
		|	И Согл.Цена <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Согл.Период УБЫВ";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Договор;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 

	Возврат ВыборкаДетальныеЗаписи.ДоговорВзаиморасчетов;

	
КонецФункции



Функция ПолучитьДоговорПоКлючуФоновогоЗадания(КлючФоновогоЗадания) Экспорт

	ПозицияРазделителя = Найти(КлючФоновогоЗадания, "UID#");
	
	УникальныйИдентификаторСклада = Сред(КлючФоновогоЗадания, ПозицияРазделителя + 4);
	
	Возврат Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторСклада));

КонецФункции // ПолучитьСкладПоКлючуФоновогоЗадания()

Функция ПолучитьКлючФоновогоЗаданияПоДоговора(Договор) Экспорт

	Возврат СокрЛП(Договор.Наименование) + ", UID#" + Договор.УникальныйИдентификатор();	

КонецФункции // ПолучитьКлючФоновогоЗаданияПоСкладу()




#КонецОбласти






#КонецЕсли

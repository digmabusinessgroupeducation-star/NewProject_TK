
&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	ТекстЗапроса = "";
	
	Если ПравоДоступа("Чтение",Метаданные.РегистрыНакопления.РасчетыСПокупателями)Тогда	
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетыСПокупателямиОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РасчетыСПокупателямиОстатки.СуммаОстаток КАК Долг
		|ПОМЕСТИТЬ втДоговора
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями.Остатки(&Дата, ДоговорВзаиморасчетов В (&СписокДоговоров)) КАК РасчетыСПокупателямиОстатки";
	КонецЕсли;
	Если ПравоДоступа("Чтение",Метаданные.РегистрыНакопления.РасчетыСПоставщиками)Тогда
		

		Если Не ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса +"
			|
			|ОБЪЕДИНИТЬ ВСЕ
			| ";
			
		КонецЕсли;
		
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ "+?(ПустаяСтрока(ТекстЗапроса),"РАЗРЕШЕННЫЕ","")+"
		|	РасчетыСПоставщикамиОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РасчетыСПоставщикамиОстатки.СуммаОстаток   КАК Долг "+?(ПустаяСтрока(ТекстЗапроса),"ПОМЕСТИТЬ втДоговора","")+"
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками.Остатки(&Дата, ДоговорВзаиморасчетов В (&СписокДоговоров)) КАК РасчетыСПоставщикамиОстатки
		| ";
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		
		ТекстЗапроса = ТекстЗапроса+ "
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДоговора.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	СУММА(втДоговора.Долг) КАК Долг
		|ИЗ
		|	втДоговора КАК втДоговора
		|
		|СГРУППИРОВАТЬ ПО
		|	втДоговора.ДоговорВзаиморасчетов";
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст =  ТекстЗапроса;
		Запрос.УстановитьПараметр("Дата", ТекущаяДата());
		Запрос.УстановитьПараметр("СписокДоговоров", Строки.ПолучитьКлючи());
		
		Выборка = Запрос.Выполнить().Выбрать();	
		Пока Выборка.Следующий() Цикл 
			СтрокаСписка = Строки[Выборка.ДоговорВзаиморасчетов];	
			СтрокаСписка.Данные.Долг = Выборка.Долг;
			
			Если Выборка.Долг < 0 Тогда 
				СтрокаСписка.Оформление["Долг"].УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
		
	
КонецПроцедуры

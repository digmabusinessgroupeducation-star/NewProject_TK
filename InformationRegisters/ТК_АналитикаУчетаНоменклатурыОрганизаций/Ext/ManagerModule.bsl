#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Функция получает элемент справочника - ключ аналитики учета номенклатураы.
//
// Параметры:
//	ПараметрыАналитики - Выборка или Структура с полями "Организация, Номенклатура, Характеристика".
//
// Возвращаемое значение:
//	СправочникСсылка.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций - Созданный элемент справочника
//
Функция ЗначениеКлючаАналитики(ПараметрыАналитики) Экспорт

	МенеджерЗаписи = ПолучитьМенеджерЗаписи(ПараметрыАналитики);
	
	Если МенеджерЗаписи <> Неопределено Тогда
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран()
		 И Не ЗначениеЗаполнено(МенеджерЗаписи.КлючАналитики) Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;

		Если МенеджерЗаписи.Выбран() И ЗначениеЗаполнено(МенеджерЗаписи.КлючАналитики) Тогда
			Результат = МенеджерЗаписи.КлючАналитики;
		Иначе
			Результат = СоздатьКлючАналитики(ПараметрыАналитики);
		КонецЕсли;

		Возврат Результат;
	КонецЕсли;

КонецФункции
	
// Функция получает элемент справочника - ключ аналитики учета.
//
// Параметры:
//	ПараметрыАналитики - Выборка или Структура  с полями "Организация, Номенклатура, Характеристика".
//
// Возвращаемое значение:
//	СправочникСсылка.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций - Созданный элемент справочника
//
Функция СоздатьКлючАналитики(ПараметрыАналитики) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыАналитики.Номенклатура) Тогда
		МенеджерЗаписи = ПолучитьМенеджерЗаписи(ПараметрыАналитики);
		
		Если МенеджерЗаписи <> Неопределено Тогда

			// Создание нового ключа аналитики.
			СправочникОбъект = Справочники.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций.СоздатьЭлемент();
			СправочникОбъект.Наименование = ПолучитьПолноеНаименованиеКлючаАналитики(МенеджерЗаписи);
			ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыАналитики, "Организация, Номенклатура, ХарактеристикаНоменклатуры");
			СправочникОбъект.Записать();

			Результат = СправочникОбъект.Ссылка;

			МенеджерЗаписи.КлючАналитики = Результат;
			МенеджерЗаписи.Записать(Ложь);

			Возврат Результат;
			
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьВКоллекции(Коллекция, Организация) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(ТекстЗначенияКлючейАналитикиВКоллекции());
	Запрос.УстановитьПараметр("Коллекция", Коллекция);
	Запрос.УстановитьПараметр("Организация",Организация);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.АналитикаУчетаНоменклатурыОрганизаций) Тогда
			КлючАналитики = ЗначениеКлючаАналитики(Выборка)
		Иначе
			КлючАналитики = Выборка.АналитикаУчетаНоменклатурыОрганизаций;
		КонецЕсли;
		Коллекция[Выборка.Индекс].АналитикаУчетаНоменклатурыОрганизаций = КлючАналитики;
	КонецЦикла;
КонецПроцедуры





Функция ПолучитьПолноеНаименованиеКлючаАналитики(МенеджерЗаписи)

	Возврат СокрЛП(МенеджерЗаписи.Номенклатура) + "; " 
		+ ?(ЗначениеЗаполнено(МенеджерЗаписи.ХарактеристикаНоменклатуры), СокрЛП(МенеджерЗаписи.ХарактеристикаНоменклатуры) + "; ", "")
		+ СокрЛП(МенеджерЗаписи.Организация);

КонецФункции


Функция ПолучитьМенеджерЗаписи(ПараметрыАналитики)
	
	// В параметрах аналитики могут быть не все свойства
	СтруктураАналитики = Новый Структура("Организация, Номенклатура, ХарактеристикаНоменклатуры");
	ЗаполнитьЗначенияСвойств(СтруктураАналитики, ПараметрыАналитики);
	Если НЕ ЗначениеЗаполнено(СтруктураАналитики.Организация)
	 И НЕ ЗначениеЗаполнено(СтруктураАналитики.Номенклатура)
	 И НЕ ЗначениеЗаполнено(СтруктураАналитики.ХарактеристикаНоменклатуры)Тогда 
		Возврат Неопределено
	Иначе 
		МенеджерЗаписи = РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыАналитики, "Организация, Номенклатура, ХарактеристикаНоменклатуры");
		ПроверитьЗаполнениеПоляОрганизация(МенеджерЗаписи);
		Возврат МенеджерЗаписи;
	КонецЕсли;
	
КонецФункции

Процедура ПроверитьЗаполнениеПоляОрганизация(МенеджерЗаписиАналитикаУчетаНоменклатуры)

	Если МенеджерЗаписиАналитикаУчетаНоменклатуры.Организация = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Ошибочное значение парамера ""Организация""';uk='Помилкове значення параметру ""Организация""'");
	КонецЕсли;

КонецПроцедуры



Функция ТекстЗначенияКлючейАналитикиВКоллекции()
	ТекстЗапроса =     "ВЫБРАТЬ
	                   |	Коллекция.НомерСтроки - 1 КАК Индекс,
	                   |	&Организация КАК Организация,
	                   |	Коллекция.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций,
	                   |	Коллекция.Номенклатура КАК Номенклатура,
	                   |	Коллекция.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	                   |ПОМЕСТИТЬ Коллекция
	                   |ИЗ
	                   |	&Коллекция КАК Коллекция
	                   |;
	                   |
	                   |////////////////////////////////////////////////////////////////////////////////
	                   |ВЫБРАТЬ
	                   |	Коллекция.Индекс КАК Индекс,
	                   |	Коллекция.Организация КАК Организация,
	                   |	Коллекция.Номенклатура КАК Номенклатура,
	                   |	Коллекция.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                   |	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатурыОрганизаций
	                   |ИЗ
	                   |	Коллекция КАК Коллекция
	                   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций КАК Аналитика
	                   |		ПО (Аналитика.Номенклатура = Коллекция.Номенклатура)
	                   |			И (Аналитика.ХарактеристикаНоменклатуры = Коллекция.ХарактеристикаНоменклатуры)
	                   |			И (Аналитика.Организация = Коллекция.Организация)
	                   |ГДЕ
	                   |	(Аналитика.КлючАналитики ЕСТЬ NULL
	                   |			ИЛИ Аналитика.КлючАналитики <> Коллекция.АналитикаУчетаНоменклатурыОрганизаций
	                   |			ИЛИ Аналитика.КлючАналитики = ЗНАЧЕНИЕ(Справочник.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций.ПустаяСсылка))";
	
	
	
	Возврат ТекстЗапроса;
КонецФункции




	
	#КонецОбласти

	
	
	
	
	
#КонецЕсли
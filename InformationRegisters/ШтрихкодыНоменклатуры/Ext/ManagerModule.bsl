#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция вычисляет контрольный символ кода EAN
//
// Параметры:
//  Штрихкод     - штрихкод (без контрольной цифры)
//  Тип          - тип штрихкода: 13 - EAN13, 8 - EAN8
//
// Возвращаемое значение:
//  Контрольный символ штрихкода
//
Функция КонтрольныйСимволEAN(ШтрихКод, Тип) Экспорт
	
	Четн   = 0;
	Нечетн = 0;
	
	КоличествоИтераций = ?(Тип = 13, 6, 4);
	
	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) и (Индекс = КоличествоИтераций) Тогда
		Иначе
			Четн   = Четн   + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		Нечетн = Нечетн + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;
	
	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;
	
	КонтЦифра = 10 - (Четн + Нечетн) % 10;
	
	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));
	
КонецФункции


// Функция возращает массив со штрихкодами
//
// Параметры:
//  Номенклатура   - СправочникСсылка.Номенклатура
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения
//
// Возвращаемое значение:
//  Массив штрихкодов
//
Функция ШтрихкодыНоменклатуры(Номенклатура, Характеристика, Упаковка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
	|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика
	|	И ШтрихкодыНоменклатуры.Упаковка = &Упаковка
	|";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Упаковка", Упаковка);
	
	Штрихкоды = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Штрихкод");
	
	ВозвращаемоеЗначение = Новый Массив;
	Для Каждого ЭлементМассива Из Штрихкоды Цикл
		ВозвращаемоеЗначение.Добавить(ПодготовитьШтрихкод(ЭлементМассива));
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Шаблон весового штрихкода дополняется префиксом узла и контрольным числом.
//
// Параметры:
//  Штрихкод
//
// Возвращаемое значение:
//  Строка - Штрихкод
//
Функция ПодготовитьШтрихкод(Знач Штрихкод) Экспорт
	
	Если СтрДлина(Штрихкод) = 13 И Лев(Штрихкод, 2) = "2_" Тогда
		Диапазон = ЗначениеНастроекПовтИсп.ДиапазонВесовыхШтрихкодовПоУмолчанию();
		
		Если Диапазон = Неопределено Тогда
			ВызватьИсключение НСтр("ru='Не определены диапазоны весовых штрихкодов.';uk='Не визначені діапазони вагових штрихкодів.'");	
		КонецЕсли;
		
		Строка = Диапазон + Сред(Штрихкод, 3, 5) + "00000";
		Возврат Строка + КонтрольныйСимволEAN(Строка, 13) 	
	КонецЕсли;
	
	Возврат Штрихкод; 
	
КонецФункции


Функция НайтиТоварПоШтрихКоду(Знач Штрихкод) Экспорт
	
	Если ТипЗнч(Штрихкод)  = Тип("Число") Тогда
		Штрихкод = Формат(Штрихкод,"ЧЦ=15; ЧГ=");
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод";
	
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Номенклатура;
	
КонецФункции



// Функция возвращает массив настроек диапазонов штрихкодов по умолчанию
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - Настройки диапазонов штрихкодов
//
Функция НастройкиДиапазоновШтрихкодовПоУмолчанию() Экспорт
	
	НастройкиДиапазоновШтрихкодов = Новый Массив;

	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Ложь;
	Настройка.Диапазон = "20";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "21";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "22";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "23";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "24";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "25";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "26";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "27";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "28";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Настройка = Новый Структура("Весовой, Диапазон");
	Настройка.Весовой  = Истина;
	Настройка.Диапазон = "29";
	НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
	
	Возврат НастройкиДиапазоновШтрихкодов;
	
КонецФункции

// Функция возвращает массив настроек диапазонов штрихкодов
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - Настройки диапазонов штрихкодов
//
Функция НастройкиДиапазоновШтрихкодов() Экспорт
	
	НастройкиДиапазоновШтрихкодов = Новый Массив;
	
	Настройки = Константы.НастройкиПрефиксацииШтрихкодов.Получить().Получить();
	Если Настройки = Неопределено Тогда
		
		НастройкиДиапазоновШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.НастройкиДиапазоновШтрихкодовПоУмолчанию();
		
	Иначе
		
		Попытка
			Для Каждого СтрокаТЧ Из Настройки Цикл
				Настройка = Новый Структура("Весовой, Диапазон");
				Настройка.Весовой  = СтрокаТЧ.Весовой;
				Настройка.Диапазон = СтрокаТЧ.Диапазон;
				НастройкиДиапазоновШтрихкодов.Добавить(Настройка);
			КонецЦикла;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Ошибка формата настроек диапазонов штрихкодов. Загружены настройки по умолчанию.';uk='Помилка формату настройок діапазонів штрихкодів. Завантажені настройки по умовчанню.'"));
			НастройкиДиапазоновШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.НастройкиДиапазоновШтрихкодовПоУмолчанию();
		КонецПопытки
		
	КонецЕсли;
	
	Возврат НастройкиДиапазоновШтрихкодов;
	
КонецФункции


Процедура ЗаполнитьКонстантыПодсистемыШтрихкодирования() Экспорт

	НастройкиДиапазоновШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.НастройкиДиапазоновШтрихкодовПоУмолчанию();
	Константы.НастройкиПрефиксацииШтрихкодов.Установить(Новый ХранилищеЗначения(НастройкиДиапазоновШтрихкодов));
	
КонецПроцедуры

// Процедура выполняет преобразование штрихкода, полученного
// из весов с печатью этикеток в штрихкод, пригодный для поиска в БД.
//
// Параметры:
//  ТекШтрихкод               – Строка
//  ПрефиксыВесовыхШтрихкодов – Массив
//
Процедура ПреобразоватьВесовойШтрихкод(ТекШтрихкод, ПрефиксыВесовыхШтрихкодов) Экспорт
	
	Если СтрДлина(ТекШтрихкод.Штрихкод) = 13 // EAN13
		И Лев(ТекШтрихкод.Штрихкод, 1) = "2" // Внутренний штрихкод
		И ПрефиксыВесовыхШтрихкодов.Найти(Сред(ТекШтрихкод.Штрихкод, 2, 1)) <> Неопределено Тогда // Найден префикс весового товара
		
		// Штрихкод является весовым, выполняем преобразование.
		// Формат весового штрихкода: 2 + П + ЧЧЧЧЧ + BBBBB + K
		// Где,
		//  П     - Префикс весового товара
		//  ЧЧЧЧЧ - код весового товара
		//  BBBBB - Вес
		//  К     - Контрольное число
		
		Код = Сред(ТекШтрихкод.Штрихкод, 3, 5);
		Вес = Число(Сред(ТекШтрихкод.Штрихкод, 8, 2)) + Число(Сред(ТекШтрихкод.Штрихкод, 10, 3)) / 1000;
		
		ТекШтрихкод.Штрихкод   = ПолучитьШтрихкодВесовогоТовараПоКоду(Код,,Истина);
		ТекШтрихкод.Количество = ТекШтрихкод.Количество * Вес;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает формат весового штрихкода для запросов
//
// Параметры:
//  ПрефиксВесовогоШтрихкода – Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ФорматВесовогоШтрихкода(ПрефиксВесовогоШтрихкода, Загрузка = Ложь) Экспорт
	
	Если Загрузка Тогда
		Возврат "2______00000_";
	Иначе
		Возврат "2" + Строка(ПрефиксВесовогоШтрихкода) + Строка(ПрефиксВесовогоШтрихкода) + "____00000_";
	КонецЕсли;
	
КонецФункции

// Функция возвращает штрихкод весового товара,
// создаваемый из кода с префиксом весового товара и контрольным символом
//
// Параметры:
//  Код                   - Код
//  ПрефиксВесовогоТовара - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ПолучитьШтрихкодВесовогоТовараПоКоду(Код, ПрефиксУзлаШтрихкода = "_", Загрузка = Ложь) Экспорт

	Если Загрузка Тогда
		КодТовара = Формат(Код, "ЧЦ=5; ЧВН=; ЧГ=");
	Иначе
		КодТовара = Строка(ПрефиксУзлаШтрихкода) + Формат(Код, "ЧЦ=4; ЧВН=; ЧГ=");
	КонецЕсли;
	
	Штрихкод = "2_" + КодТовара + "00000";
	
	КонтрольныйСимволEAN = "_";
	Штрихкод = Штрихкод + КонтрольныйСимволEAN;

	Возврат Штрихкод;

КонецФункции


// Функция осуществляет формирование штрихкода EAN13 для
// штучного товара
//
// Параметры:
//  ПрефиксШтучногоТовара       – Строка
//  ПрефиксВнутреннегоШтрихкода – Строка
//  МаксимальныйКод             – Число
//
// Возвращаемое значение:
//  Строка
//
Функция СформироватьШтрихкодEAN13() Экспорт

	ПрефиксУзлаШтрихкода = "0";
	
	Код = Неопределено;
	Диапазон = Неопределено;
	Диапазоны = ПолучитьМаксимальныеЗначенияКодовШтучныхШтрихкодов();
	Для Каждого СтрокаТЧ Из Диапазоны Цикл
		Код = СтрокаТЧ.Код + 1;
		Диапазон = СтрокаТЧ.Диапазон;
		Прервать;
	КонецЦикла;
	
	Если Код = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияНетСвободныхКодовШтучныхШтрихкодов());
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьШтрихкодПоКоду(Код, Диапазон, ПрефиксУзлаШтрихкода);

КонецФункции

// Функция возвращает штрихкод, создаваемый из кода с префиксами и контрольным символом
//
// Параметры:
//  Код                  - Число
//  Диапазон             - Строка
//  ПрефиксУзлаШтрихкода - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ПолучитьШтрихкодПоКоду(Код, Диапазон, ПрефиксУзлаШтрихкода) Экспорт

	Штрихкод = Строка(Диапазон) + ПрефиксУзлаШтрихкода + Формат(Код, "ЧЦ=9; ЧВН=; ЧГ=");
	Штрихкод = Штрихкод + КонтрольныйСимволEAN(ШтрихКод, 13);

	Возврат Штрихкод;

КонецФункции

// Функция осуществляет формирование штрихкода EAN13 для
// весового товара
//
// Параметры:
//  ПрефиксВесовогоТовара       – Строка
//  МаксимальныйКод             – Число
//
// Возвращаемое значение:
//  Строка
//
Функция СформироватьШтрихкодВесовогоТовараEAN13() Экспорт

	ПрефиксУзлаШтрихкода = "0";

	Код = ПолучитьМаксимальноеЗначениеКодаВесовыхШтрихкодов() + 1;
	
	Если Код = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияНетСвободныхКодовВесовыхШтрихкодов());
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьШтрихкодВесовогоТовараПоКоду(Код, ПрефиксУзлаШтрихкода);

КонецФункции


// Функция возвращает максимальный код штучного товара
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Число
//
Функция МаксимальныйКодШтучногоТовара() Экспорт
	
	Возврат 999999999;
	
КонецФункции

// Функция возвращает максимальный код весового товара
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Число
//
Функция МаксимальныйКодВесовогоТовара() Экспорт
	
	Возврат 9999;
	
КонецФункции

// Функция возвращает массив максимальное значений кодов штучных штрихкодов
// по диапазонам
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Массив - Максимальные значения кодов по диапазонам
//
Функция ПолучитьМаксимальныеЗначенияКодовШтучныхШтрихкодов() Экспорт
	
	ДиапазоныШтучныхШтрихкодов = ЗначениеНастроекПовтИсп.ДиапазоныШтучныхШтрихкодов();
	
	Если ДиапазоныШтучныхШтрихкодов.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПрефиксУзла = "0";
	
	ТекстЗапроса = "";
	Запросы = Новый Массив;
	Для Каждого СтрокаТЧ Из ДиапазоныШтучныхШтрихкодов Цикл
		Запросы.Добавить(
		"ВЫБРАТЬ
		|	""" + СтрокаТЧ.Диапазон + """ КАК Диапазон,
		|	МАКСИМУМ(ПОДСТРОКА(ШтрихкодыНоменклатуры.Штрихкод, 4, 9)) КАК КодСтрока
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО """ + СтрокаТЧ.Диапазон + Строка(ПрефиксУзла) + "__________""
		|");
	КонецЦикла;
	ТекстЗапроса = СтрСоединить(Запросы, Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	ОписаниеТипаЧисла = ОбщегоНазначенияТК.ПолучитьОписаниеТиповЧисла(15, 0);
	МаксимальныйКодШтучногоТовара = МаксимальныйКодШтучногоТовара();
	
	МаксимальныеЗначения = Новый Массив;
	Для Каждого СтрокаТЧ Из РезультатЗапроса Цикл
		
		Код = ОписаниеТипаЧисла.ПривестиЗначение(СтрокаТЧ.КодСтрока);
		Если Не Код < МаксимальныйКодШтучногоТовара Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРезультата = Новый Структура;
		СтрокаРезультата.Вставить("Код", Код);
		СтрокаРезультата.Вставить("Диапазон", СтрокаТЧ.Диапазон);
		
		МаксимальныеЗначения.Добавить(СтрокаРезультата);
		
	КонецЦикла;
	
	Возврат МаксимальныеЗначения;

КонецФункции

// Функция возвращает максимальное значение кода весового товара
//  из зарегистрированных в системе штрихкодов
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Число
//
Функция ПолучитьМаксимальноеЗначениеКодаВесовыхШтрихкодов() Экспорт
	
	ПрефиксУзла = Константы.ПрефиксШтрихкодовУзлаРИБ.Получить();
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	МАКСИМУМ(ПОДСТРОКА(ШтрихкодыНоменклатуры.Штрихкод, 4, 4)) КАК КодСтрока
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО """ + "2_" + Строка(ПрефиксУзла) + "____00000_""
		|");
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	ОписаниеТипаЧисла = ОбщегоНазначенияТК.ПолучитьОписаниеТиповЧисла(15, 0);
	
	МаксимальныйКодВесовогоТовара = МаксимальныйКодВесовогоТовара();
	
	Для Каждого СтрокаТЧ Из РезультатЗапроса Цикл
		
		Код = ОписаниеТипаЧисла.ПривестиЗначение(СтрокаТЧ.КодСтрока);
		Если Не Код < МаксимальныйКодВесовогоТовара Тогда
			Продолжить;
		КонецЕсли;
		
		Возврат Код;
		
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции


// Возвращает текст сообщения о недостатке свободных кодов штучных штрихкодов
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияНетСвободныхКодовШтучныхШтрихкодов() Экспорт
	Возврат НСтр("ru='Нет свободных кодов в доступных диапазонах штучных штрихкодов.
                       |Штрихкод не сформирован.'
                       |;uk='Немає вільних кодів в доступних діапазонах штучних штрихкодів.
                       |Штрихкод не сформований.'")	
КонецФункции


// Возвращает текст сообщения о недостатке свободных кодов весовых штрихкодов
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияНетСвободныхКодовВесовыхШтрихкодов() Экспорт
	Возврат НСтр("ru='Нет свободных кодов весовых штрихкодов.
                       |Штрихкод не сформирован.'
                       |;uk='Немає вільних кодів вагових штрихкодів.
                       |Штрихкод не сформований.'")	
КонецФункции

Функция ПолучитьШтрихКодАртикула(Знач Код)Экспорт
	ДлиннаКода = СтрДлина(Код);
	Шк = СокрЛП(Код);
	Если ДлиннаКода = 2 Тогда
		Шк = "200000"+Шк+"00000";
	ИначеЕсли ДлиннаКода = 3 Тогда
		Шк  = "20000"+Шк+"00000";
	ИначеЕсли ДлиннаКода = 4 Тогда
		Шк  = "2000"+Шк+"00000";
	ИначеЕсли ДлиннаКода = 5 Тогда
		Шк  = "200"+Шк+"00000";
	ИначеЕсли ДлиннаКода = 6 Тогда
		Шк  = "20"+Шк+"00000";
	КонецЕсли;
	
	Возврат Шк;
КонецФункции


#КонецОбласти



#КонецЕсли




Функция ПолучитьСтавкиНДС_СпискаНоменклатуры(Организация,СписокНоменклатуры,ТорговаяПлощадка = Неопределено)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат	= Новый Соответствие;
	Если СписокНоменклатуры.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;

	Для Каждого Номенклатура Из СписокНоменклатуры Цикл
		Если Номенклатура = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураВозврата = Новый Структура();
		
		СтруктураВозврата.Вставить("Номенклатура",				 Неопределено);
		СтруктураВозврата.Вставить("Организация",				 Неопределено);
		СтруктураВозврата.Вставить("ТорговаяПлощадка",			 Неопределено);
		СтруктураВозврата.Вставить("СтавкаНДС",					 Номенклатура.СтавкаНДС);
		СтруктураВозврата.Вставить("СтавкаНДСВходящегоДокумента",Номенклатура.СтавкаНДС);

		Результат.Вставить(Номенклатура, СтруктураВозврата);
	КонецЦикла;

	МассивОрганизаций = Новый Массив();
	МассивОрганизаций.Добавить(Справочники.Организации.ПустаяСсылка());
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивОрганизаций.Добавить(Организация);
	КонецЕсли;
	МассивТорговаяПлощадка = Новый Массив();
	МассивТорговаяПлощадка.Добавить(Справочники.ТорговыеПлощадки.ПустаяСсылка());

	Если ЗначениеЗаполнено(ТорговаяПлощадка) Тогда
		МассивТорговаяПлощадка.Добавить(ТорговаяПлощадка);
	КонецЕсли;
		
	СоответствиеЭлементовИГрупп = ОбщегоНазначенияТКВызовСервера.ПолучитьСписокВышеСтоящихГруппЭлементов(СписокНоменклатуры);

	ТаблицаИерархии = Новый ТаблицаЗначений;
	ТаблицаИерархии.Колонки.Добавить("Элемент",		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаИерархии.Колонки.Добавить("Родитель",	Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаИерархии.Колонки.Добавить("Уровень",		ОбщегоНазначенияТК.ПолучитьОписаниеТиповЧисла(10, 0));
	
	Для каждого Номенклатура Из СписокНоменклатуры Цикл
		
		НоваяСтрока = ТаблицаИерархии.Добавить();
		НоваяСтрока.Элемент		= Номенклатура;
		НоваяСтрока.Родитель	= Номенклатура;
		
		СписокГрупп = СоответствиеЭлементовИГрупп.Получить(Номенклатура);
		Если СписокГрупп = Неопределено Тогда
			НоваяСтрока.Уровень = 1;
			Продолжить;
		КонецЕсли;
		
		КоличествоВышеСтоящихГрупп = СписокГрупп.Количество();
		
		НоваяСтрока.Уровень = КоличествоВышеСтоящихГрупп + 1;
		
		Для Индекс = 1 По КоличествоВышеСтоящихГрупп Цикл
			НоваяСтрока = ТаблицаИерархии.Добавить();
			НоваяСтрока.Элемент	= Номенклатура;
			НоваяСтрока.Родитель= СписокГрупп[Индекс - 1];
			НоваяСтрока.Уровень	= КоличествоВышеСтоящихГрупп - Индекс + 1;
		КонецЦикла;
		
	КонецЦикла;

 	НоваяСтрока = ТаблицаИерархии.Добавить();	// Корень
	
	МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИерархии, "Родитель", Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаИерархии",	ТаблицаИерархии);
	Запрос.УстановитьПараметр("МассивОрганизаций",	МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивНоменклатуры",	ОбщегоНазначенияТКВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивНоменклатуры));
	Запрос.УстановитьПараметр("МассивТорговаяПлощадка",	МассивТорговаяПлощадка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИерархии.Элемент КАК Элемент,
	|	ТаблицаИерархии.Родитель КАК Родитель,
	|	ТаблицаИерархии.Уровень КАК Уровень
	|ПОМЕСТИТЬ ВТТаблицаИерархии
	|ИЗ
	|	&ТаблицаИерархии КАК ТаблицаИерархии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИерархии.Элемент КАК Номенклатура,
	|	ТаблицаИерархии.Родитель КАК Родитель,
	|	ТаблицаИерархии.Уровень КАК Уровень,
	|	СтавкиНДС.Организация КАК Организация,
	|	СтавкиНДС.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	СтавкиНДС.Номенклатура КАК СтавкаНДСНоменклатура,
	|	СтавкиНДС.СтавкаНДС КАК СтавкаНДС,
	|	СтавкиНДС.СтавкаНДСВходящегоДокумента КАК СтавкаНДСВходящегоДокумента
	|ИЗ
	|	ВТТаблицаИерархии КАК ТаблицаИерархии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СтавкиНДС_Номенклатуры КАК СтавкиНДС
	|		ПО ТаблицаИерархии.Родитель = СтавкиНДС.Номенклатура
	|ГДЕ
	|	СтавкиНДС.Организация В(&МассивОрганизаций)
	|	И СтавкиНДС.Номенклатура В(&МассивНоменклатуры)
	|	И СтавкиНДС.ТорговаяПлощадка В(&МассивТорговаяПлощадка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Уровень УБЫВ,
	|	ТорговаяПлощадка УБЫВ,
	|	Организация УБЫВ";

	ТаблицаСтавкаНДС = Запрос.Выполнить().Выгрузить();
	ТаблицаСтавкаНДС.Индексы.Добавить("Номенклатура");
	
	ОтборНоменклатура = Новый Структура("Номенклатура");
	
	ПустаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();

	Для Каждого Номенклатура Из СписокНоменклатуры Цикл
		СтруктураВозврата = Результат.Получить(Номенклатура);

		 ОтборНоменклатура.Номенклатура = Номенклатура;
		 
		 НайденныеСтроки = ТаблицаСтавкаНДС.НайтиСтроки(ОтборНоменклатура);
		 Если НайденныеСтроки.Количество() > 0 Тогда
			 нСтока = НайденныеСтроки[0];
			 Если ЗначениеЗаполнено(нСтока.СтавкаНДСВходящегоДокумента)Тогда
				СтруктураВозврата.СтавкаНДСВходящегоДокумента = нСтока.СтавкаНДСВходящегоДокумента;  
			 КонецЕсли;
			 Если ЗначениеЗаполнено(нСтока.СтавкаНДС)Тогда
				 СтруктураВозврата.СтавкаНДС = нСтока.СтавкаНДС;  
			 КонецЕсли;
			 
			 ЗаполнитьЗначенияСвойств(СтруктураВозврата, НайденныеСтроки[0],,"СтавкаНДСВходящегоДокумента,СтавкаНДС");
			 СтруктураВозврата.Номенклатура = нСтока.СтавкаНДСНоменклатура;
		 Иначе
			 
			 ОтборНоменклатура.Номенклатура = ПустаяНоменклатура;
			 
			 НайденныеСтроки = ТаблицаСтавкаНДС.НайтиСтроки(ОтборНоменклатура);
			 Если НайденныеСтроки.Количество() > 0 Тогда
				 
				 нСтока = НайденныеСтроки[0];
				 Если ЗначениеЗаполнено(нСтока.СтавкаНДСВходящегоДокумента)Тогда
					 СтруктураВозврата.СтавкаНДСВходящегоДокумента = нСтока.СтавкаНДСВходящегоДокумента;  
				 КонецЕсли;
				 Если ЗначениеЗаполнено(нСтока.СтавкаНДС)Тогда
					 СтруктураВозврата.СтавкаНДС = нСтока.СтавкаНДС;  
				 КонецЕсли;
				 
				 ЗаполнитьЗначенияСвойств(СтруктураВозврата, нСтока,,"СтавкаНДСВходящегоДокумента,СтавкаНДС");

				 СтруктураВозврата.Номенклатура = нСтока.СтавкаНДСНоменклатура;
			 КонецЕсли;
			 
		 КонецЕсли;

	КонецЦикла;
	
	Возврат Результат;

КонецФункции

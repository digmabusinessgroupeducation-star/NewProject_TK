
Функция ПолучитьWSСсылкуБухгалтерия(Пользователь = "", Пароль = "") Экспорт 
	
	ВсеОк = Истина;	
	WS = Неопределено;
	Результат = Новый Структура;
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда 
		Пользователь = "ObmenSSL";
		Пароль = "bynthghbpf";	
	КонецЕсли;
	
	Попытка
		
		Определение = Новый WSОпределения("https://orders-1c.digma.ua/buh/ws/cwd2.1cws?wsdl", Пользователь, Пароль,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено),Ложь);
		//Определение = Новый WSОпределения("https://orders.digma.ua/Buh/cwd2.1cws?wsdl",,,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
		//WS = Новый WSПрокси(Определение,"http://www.digma.ua/xml-exchenge", "ОбменПоПравилам", "ОбменПоПравиламSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
		WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
		WS.Пользователь = Пользователь;
		WS.Пароль = Пароль;
		
	Исключение 
		ВсеОк = Ложь;
		Результат.Вставить("Сообщение", ОписаниеОшибки());		
	КонецПопытки;
	
	Результат.Вставить("Успешно", ВсеОк);
	Результат.Вставить("WS", WS);
	
	Возврат Результат;
КонецФункции


Функция ВернутьМассивДокументов(ДатаНачала, ДатаОкончания, ТипДокумента, ТолькоПроведенные, GuidOrganization, GuidPartner, GuidDepartment) Экспорт 
	
	Отказ = Ложь;
	Описание = "";
	
	Организация 	= ПолучитьСсылкуПоГУИД(GuidOrganization, "Организации", Описание, Отказ);
	Контрагент 		= ПолучитьСсылкуПоГУИД(GuidPartner, "Контрагенты", Описание, Отказ);
	Подразделение 	= ПолучитьСсылкуПоГУИД(GuidDepartment, "ПодразделенияКомпании", Описание, Отказ);
	
	Если Не Отказ Тогда 
		Попытка
			МассивДокументовТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnArrayDocumentsBUH");
			Результат = ФабрикаXDTO.Создать(МассивДокументовТип);	
		Исключение
			Описание = ОписаниеОшибки();
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Если ТипДокумента = "ПоступлениеТоваров" Тогда
			ВыборкаДок = ВыборкаПоступлениеТоваров(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Ложь);
		ИначеЕсли ТипДокумента = "ПоступлениеТоваровТранзит" Тогда 
			ВыборкаДок = ВыборкаПоступлениеТоваров(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Истина);
		ИначеЕсли ТипДокумента = "ВозвратПоставщику" Тогда 
			ВыборкаДок = ВыборкаВозвратТоваровПоставщику(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Ложь);
		ИначеЕсли ТипДокумента = "ВозвратПоставщикуТранзит" Тогда 	
			ВыборкаДок = ВыборкаВозвратТоваровПоставщику(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Истина);
		ИначеЕсли ТипДокумента = "РеализацияТоваров" Тогда 
			ВыборкаДок = ВыборкаРеализацияТоваров(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение);
		ИначеЕсли ТипДокумента = "ВозвратОтПокупателя" Тогда 
			ВыборкаДок = ВыборкаВозвратОтПокупателя(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение);
		ИначеЕсли ТипДокумента = "КорректировкаДолга" Тогда 
			ВыборкаДок = ВыборкаКорректировкаДолга(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение);	
		Иначе
			Отказ = Истина;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Не поддерживается загрузка документа с типом ""%1""",
			ТипДокумента);
		КонецЕсли;
	КонецЕсли;
	
	
	Если Не Отказ Тогда		
		Попытка
			ДокументТип = Результат.Свойства().Получить("DocumentsList").Тип;
			
			Пока ВыборкаДок.Следующий() Цикл 
				СтрокаТаблицы = ФабрикаXDTO.Создать(ДокументТип);
				СтрокаТаблицы.DateTime = ВыборкаДок.Дата;
				СтрокаТаблицы.NumberDoc = СокрЛП(ВыборкаДок.Номер);
				СтрокаТаблицы.GuidDoc = ЗначениеВСтрокуВнутр(ВыборкаДок.Ссылка.УникальныйИдентификатор());
				СтрокаТаблицы.OrganizationName = ВыборкаДок.Организация;
				СтрокаТаблицы.PartnerName = ВыборкаДок.Контрагент;
				СтрокаТаблицы.DepartmentName = ВыборкаДок.Подразделение;
				
				Результат.DocumentsList.Добавить(СтрокаТаблицы);
			КонецЦикла;
		Исключение
			Отказ = Истина;
			Описание = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;		
	
	Результат.Status = Не Отказ;
	Результат.Description = Описание;
	
	Возврат Результат;
	
КонецФункции


Функция ПолучитьСсылкуПоГУИД(СтрокаГУИД, ТипСправочника, Описание, Отказ)
	
	Ссылка = Неопределено;
	
	Если ЗначениеЗаполнено(СтрокаГУИД) Тогда 		
		
		Попытка
			ГУИД = Новый УникальныйИдентификатор(СтрокаГУИД);
			нСсылка = Справочники[ТипСправочника].ПолучитьСсылку(ГУИД);
			Если ОбщегоНазначения.СсылкаСуществует(нСсылка) Тогда 
				Ссылка = нСсылка;
			Иначе
				Отказ = Истина;
			КонецЕсли;
		Исключение
			Отказ = Истина;
		КонецПопытки;
		
		Если Ссылка = Неопределено Тогда 
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1Ошибка получения элемента справочника ""%2"" по идентификатору <%3>",
			?(Не ПустаяСтрока(Описание), Описание + Символы.ПС, ""),
			ТипСправочника,
			СтрокаГУИД);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

Функция ВыборкаПоступлениеТоваров(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Транзит)
	
	ЕстьОрганизация = ЗначениеЗаполнено(Организация);
	ЕстьКонтрагент = ЗначениеЗаполнено(Контрагент);
	ЕстьПодразделение = ЗначениеЗаполнено(Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеТоваров.Ссылка КАК Ссылка,
	|	ПоступлениеТоваров.Дата КАК Дата,
	|	ПоступлениеТоваров.Номер КАК Номер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПоступлениеТоваров.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПоступлениеТоваров.ПодразделениеКомпании) КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПоступлениеТоваров.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ ПоступлениеТоваров.ПометкаУдаления
	|	И ПоступлениеТоваров.РегламентированныйУчет				   
	|	%1
	|	%2
	|	%3
	|	%4
	|	%5				   
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	Если ЕстьОрганизация Тогда 
		Запрос.УстановитьПараметр("Организация", Организация);		
	КонецЕсли;
	Если ЕстьКонтрагент Тогда 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);		
	КонецЕсли;
	Если ЕстьПодразделение Тогда 
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);		
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	ТекстЗапроса,
	?(ТолькоПроведенные, "И ПоступлениеТоваров.Проведен", ""),
	?(Транзит, "И ПоступлениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)", "И ПоступлениеТоваров.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)"),
	?(ЕстьОрганизация, "И ПоступлениеТоваров.Организация = &Организация", ""),
	?(ЕстьКонтрагент, "И ПоступлениеТоваров.Контрагент = &Контрагент", ""),
	?(ЕстьПодразделение, "И ПоступлениеТоваров.ПодразделениеКомпании = &ПодразделениеКомпании", ""));
	
	Возврат Запрос.Выполнить().Выбрать();						
	
КонецФункции

Функция ВыборкаВозвратТоваровПоставщику(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение, Транзит)
	
	ЕстьОрганизация = ЗначениеЗаполнено(Организация);
	ЕстьКонтрагент = ЗначениеЗаполнено(Контрагент);
	ЕстьПодразделение = ЗначениеЗаполнено(Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Номер КАК Номер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.ПодразделениеКомпании) КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.ВозвратПоставщику КАК Документ
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.РегламентированныйУчет				   
	|	%1
	|	%2
	|	%3
	|	%4
	|	%5				   				   
	|				   
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	Если ЕстьОрганизация Тогда 
		Запрос.УстановитьПараметр("Организация", Организация);		
	КонецЕсли;
	Если ЕстьКонтрагент Тогда 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);		
	КонецЕсли;
	Если ЕстьПодразделение Тогда 
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);		
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	ТекстЗапроса,
	?(ТолькоПроведенные, "И Документ.Проведен", ""),
	?(Транзит, "И Документ.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)", "И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)"),
	?(ЕстьОрганизация, "И Документ.Организация = &Организация", ""),
	?(ЕстьКонтрагент, "И Документ.Контрагент = &Контрагент", ""),
	?(ЕстьПодразделение, "И Документ.ПодразделениеКомпании = &ПодразделениеКомпании", ""));
	
	Возврат Запрос.Выполнить().Выбрать();			
	
КонецФункции

Функция ВыборкаРеализацияТоваров(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение)
	
	ЕстьОрганизация = ЗначениеЗаполнено(Организация);
	ЕстьКонтрагент = ЗначениеЗаполнено(Контрагент);
	ЕстьПодразделение = ЗначениеЗаполнено(Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Номер КАК Номер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.ПодразделениеКомпании) КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.РеализацияТоваров КАК Документ
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.РегламентированныйУчет
	|	И Документ.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОбОказанииУслуг)
	|	%1
	|	%2
	|	%3
	|	%4			   				   				   
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	Если ЕстьОрганизация Тогда 
		Запрос.УстановитьПараметр("Организация", Организация);		
	КонецЕсли;
	Если ЕстьКонтрагент Тогда 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);		
	КонецЕсли;
	Если ЕстьПодразделение Тогда 
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);		
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	ТекстЗапроса,
	?(ТолькоПроведенные, "И Документ.Проведен", ""),
	?(ЕстьОрганизация, "И Документ.Организация = &Организация", ""),
	?(ЕстьКонтрагент, "И Документ.Контрагент = &Контрагент", ""),
	?(ЕстьПодразделение, "И Документ.ПодразделениеКомпании = &ПодразделениеКомпании", ""));
	
	Возврат Запрос.Выполнить().Выбрать();			
	
КонецФункции

Функция ВыборкаВозвратОтПокупателя(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение)
	
	ЕстьОрганизация = ЗначениеЗаполнено(Организация);
	ЕстьКонтрагент = ЗначениеЗаполнено(Контрагент);
	ЕстьПодразделение = ЗначениеЗаполнено(Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Номер КАК Номер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.ПодразделениеКомпании) КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.ВозвратОтПокупателя КАК Документ
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.РегламентированныйУчет
	|	%1
	|	%2
	|	%3
	|	%4			   				   				   
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	Если ЕстьОрганизация Тогда 
		Запрос.УстановитьПараметр("Организация", Организация);		
	КонецЕсли;
	Если ЕстьКонтрагент Тогда 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);		
	КонецЕсли;
	Если ЕстьПодразделение Тогда 
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);		
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	ТекстЗапроса,
	?(ТолькоПроведенные, "И Документ.Проведен", ""),
	?(ЕстьОрганизация, "И Документ.Организация = &Организация", ""),
	?(ЕстьКонтрагент, "И Документ.Контрагент = &Контрагент", ""),
	?(ЕстьПодразделение, "И Документ.ПодразделениеКомпании = &ПодразделениеКомпании", ""));
	
	Возврат Запрос.Выполнить().Выбрать();			
	
КонецФункции

Функция ВыборкаКорректировкаДолга(ДатаНачала, ДатаОкончания, ТолькоПроведенные, Организация, Контрагент, Подразделение)
	
	ЕстьОрганизация = ЗначениеЗаполнено(Организация);
	ЕстьКонтрагент = ЗначениеЗаполнено(Контрагент);
	ЕстьПодразделение = ЗначениеЗаполнено(Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Номер КАК Номер,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.ПодразделениеКомпании) КАК Подразделение,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.ТК_КорректировкаДолга КАК Документ
	|ГДЕ
	|	Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.РегламентированныйУчет
	|	%1
	|	%2
	|	%3
	|	%4			   				   				   				   				   
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	
	Если ЕстьОрганизация Тогда 
		Запрос.УстановитьПараметр("Организация", Организация);		
	КонецЕсли;
	Если ЕстьКонтрагент Тогда 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);		
	КонецЕсли;
	Если ЕстьПодразделение Тогда 
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);		
	КонецЕсли;
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	ТекстЗапроса,
	?(ТолькоПроведенные, "И Документ.Проведен", ""),
	?(ЕстьОрганизация, "И Документ.Организация = &Организация", ""),
	?(ЕстьКонтрагент, "И Документ.Контрагент = &Контрагент", ""),
	?(ЕстьПодразделение, "И Документ.ПодразделениеКомпании = &ПодразделениеКомпании", ""));
	
	Возврат Запрос.Выполнить().Выбрать();			
	
КонецФункции

//Гамазин
Функция ПолучитьМассивОКПОВнутреннихКонтрагентов() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отказ = Ложь;
	
	Попытка
		МассивОКПОТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnInnerContragents");
		МассивОКПО = ФабрикаXDTO.Создать(МассивОКПОТип);	
		
		Запр = Новый Запрос;
		Запр.Текст = "ВЫБРАТЬ
		|	Выразить(Контрагенты.КодПоЕДРПОУ как Строка(10)) как ОКПО
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ПометкаУдаления = ЛОЖЬ
		|	И Контрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.Внутренний)";
		
		
		Выборка = Запр.выполнить().Выбрать();
		
		Пока Выборка.Следующий() цикл
			Если СокрЛП(Выборка.ОКПО) <> "" тогда
				МассивОКПО.OKPO.Добавить(СокрЛП(Выборка.ОКПО));
			КонецЕсли;	
		КонецЦикла;	
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	Если не Отказ тогда
		Возврат МассивОКПО;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	
КонецФункции	
//окончание Гамазин

Функция НачисленияПоКоммунальнымУслугам(Месяц, Отчет = Ложь)	Экспорт
	
	КоммунальнаяУслуга = Новый СписокЗначений;
	КоммунальнаяУслуга.Добавить(Справочники.КоммунальныеУслуги.НайтиПоКоду("000000012"));		//	ХарьковЭнергоСбыт (банк)
	КоммунальнаяУслуга.Добавить(Справочники.КоммунальныеУслуги.НайтиПоКоду("000000019"));		//	ХарьковЭнергоСбыт Безучетка
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияКоммунальныхУслуг.Регистратор КАК Регистратор,
	|	НачисленияКоммунальныхУслуг.КоммунальнаяУслуга.Договор КАК Договор,
	|	ПРЕДСТАВЛЕНИЕ(НачисленияКоммунальныхУслуг.КоммунальнаяУслуга.ВидКоммунальнойУслуги) КАК ВидКоммунальнойУслуги,
	|	НачисленияКоммунальныхУслуг.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	НачисленияКоммунальныхУслуг.ПотреблениеИтого КАК ПотреблениеИтого,
	|	НачисленияКоммунальныхУслуг.Начислено КАК Начислено,
	|	НачисленияКоммунальныхУслуг.СуммаКОплате КАК СуммаКОплате,
	|	ВложенныйЗапросПотребители.Потребитель ЕСТЬ НЕ NULL  КАК ЕстьПотребитель,
	|	ВЫРАЗИТЬ(ВложенныйЗапросПотребители.Потребитель КАК Справочник.ТорговыеПлощадки) КАК Потребитель
	|ПОМЕСТИТЬ ВТ_Начисления
	|ИЗ
	|	РегистрНакопления.НачисленияКоммунальныхУслуг КАК НачисленияКоммунальныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТорговыеПлощадкиПотребители.Ссылка КАК Донар,
	|			ТорговыеПлощадкиПотребители.Ссылка КАК Потребитель
	|		ИЗ
	|			Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
	|		ГДЕ
	|			ТорговыеПлощадкиПотребители.Потребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ТорговыеПлощадкиПотребители.Ссылка,
	|			ТорговыеПлощадкиПотребители.Потребитель
	|		ИЗ
	|			Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
	|		ГДЕ
	|			ТорговыеПлощадкиПотребители.Потребитель ССЫЛКА Справочник.ТорговыеПлощадки) КАК ВложенныйЗапросПотребители
	|		ПО НачисленияКоммунальныхУслуг.ТорговаяПлощадка = ВложенныйЗапросПотребители.Донар
	|ГДЕ
	|	НачисленияКоммунальныхУслуг.ОтчетныйМесяц = &ОтчетныйМесяц
	|	И НачисленияКоммунальныхУслуг.КоммунальнаяУслуга В ИЕРАРХИИ(&КоммунальнаяУслуга)
	|	И НачисленияКоммунальныхУслуг.Начислено > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Начисления.Регистратор КАК Регистратор,
	|	""00000000-0000-0000-0000-000000000000"" КАК ГуидДок,
	|	ВТ_Начисления.Договор КАК Договор,
	|	ВТ_Начисления.ВидКоммунальнойУслуги КАК ВидКоммунальнойУслуги,
	|	ВТ_Начисления.Договор.Код КАК КодДоговор,
	|	ВТ_Начисления.Договор.Подразделение.Код КАК КодПодразделение,
	|	ВТ_Начисления.Договор.Организация.Код КАК КодОрганизация,
	|	ВТ_Начисления.Договор.Контрагент.Код КАК КодКонтрагент,
	|	ВТ_Начисления.ЕстьПотребитель КАК ЕстьПотребитель,
	|	ВТ_Начисления.ТорговаяПлощадка КАК ТорговаяПлощадкаСсылка,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_Начисления.ТорговаяПлощадка) КАК ТорговаяПлощадка,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_Начисления.Потребитель) КАК Потребитель,
	|	ВТ_Начисления.Потребитель КАК ПотребительСсылка,
	|	ВЫБОР
	|		КОГДА ВТ_Начисления.ЕстьПотребитель
	|			ТОГДА ВТ_Начисления.Потребитель.Код
	|		ИНАЧЕ ВТ_Начисления.ТорговаяПлощадка.Код
	|	КОНЕЦ КАК КодТорговаяПлощадка,
	|	ВТ_Начисления.ПотреблениеИтого КАК ПотреблениеИтого,
	|	ВТ_Начисления.Начислено КАК Начислено,
	|	ВТ_Начисления.СуммаКОплате КАК СуммаКОплате,
	|	ЕСТЬNULL(ЗатратыОбъектов.ПотреблениеФакт, 0) КАК ПотреблениеФакт
	|ИЗ
	|	ВТ_Начисления КАК ВТ_Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗатратыОбъектов КАК ЗатратыОбъектов
	|		ПО ВТ_Начисления.Потребитель = ЗатратыОбъектов.ТорговаяПлощадка
	|			И ВТ_Начисления.Регистратор = ЗатратыОбъектов.Регистратор
	|ИТОГИ
	|	МАКСИМУМ(ЕстьПотребитель),
	|	СУММА(ПотреблениеФакт)
	|ПО
	|	Регистратор,
	|	Договор,
	|	ТорговаяПлощадкаСсылка";
	
	Запрос.УстановитьПараметр("КоммунальнаяУслуга", КоммунальнаяУслуга);
	Запрос.УстановитьПараметр("ОтчетныйМесяц", Месяц);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Отчет Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ДЗ = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого стрДок Из ДЗ.Строки Цикл
		стрДок.ГуидДок = Строка(стрДок.Регистратор.УникальныйИдентификатор());
	КонецЦикла;
	
	ДЗ.Колонки.Удалить("Регистратор");
	ДЗ.Колонки.Удалить("ТорговаяПлощадкаСсылка");
	ДЗ.Колонки.Удалить("ПотребительСсылка");
	
	СтрокаXML = ОбщегоНазначения.ЗначениеВСтрокуXML(ДЗ);	
	
	Возврат СтрокаXML;
	
КонецФункции


Функция ВернутьИтоги_ПериодОбщИНВ(НачалоПериода,КонецПериода,ИД_ТоговаяПлощадка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИтогИН =  ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnTotalIN_v2"));
	ИтогИН.Status = Истина;
	ИтогИН.Description = "";
	ИтогИН.BeginDate = НачалоПериода;
	ИтогИН.EndDate	 = КонецПериода;
	
	//НачалоОтсчета = НачалоДня(Дата(2024,1,1));
	
	НачалоОтсчета = НачалоДня(Дата(2023,12,1));
	
	Попытка 		
		
		нСсылка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(ИД_ТоговаяПлощадка);			
		Если нСсылка = Неопределено Тогда
			ИтогИН.Status = Ложь;
			ИтогИН.Description = "Не найден склад : "+ИД_ТоговаяПлощадка;
			
			Возврат ИтогИН;
		КонецЕсли;		
				
		СвУдержано 		= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "СтатусУдержанияЗУП");
		СвОбработано 	= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ОбработаноЗУП");
		
		СписокСвойств = УправлениеСвойствами.ПолучитьСписокСвойств(Документы.Инвентаризация.ПустаяСсылка(), Ложь, Истина);
		
		Если СписокСвойств.Найти(СвУдержано) = Неопределено Тогда 
			ИтогИН.Status = Ложь;
			ИтогИН.Description = "Не найдено свойство ""СтатусУдержанияЗУП""";
			Возврат ИтогИН;			
		КонецЕсли;

		Если СписокСвойств.Найти(СвОбработано) = Неопределено Тогда 
			ИтогИН.Status = Ложь;
			ИтогИН.Description = "Не найдено свойство ""ОбработаноЗУП""";
			Возврат ИтогИН;			
		КонецЕсли;
		
		НеУдерживать = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию("Не удерживать",,, СвУдержано);
			
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Инвентаризация.Ссылка КАК Ссылка,
		               |	Инвентаризация.Дата КАК Дата,
		               |	Инвентаризация.Номер КАК Номер,
		               |	Инвентаризация.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	Инвентаризация.СкладКомпании КАК Склад,
		               |	Инвентаризация.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦенРозничный,
		               |	Инвентаризация.ТипЦенИтогов КАК ТипЦенИтогов,
		               |	Инвентаризация.РевизионнаяИнвентаризация КАК Ревизия,
		               |	ЕСТЬNULL(ДопСв_Удержано.Значение, ЗНАЧЕНИЕ(Справочник.ЗначенияСвойствОбъектов.ПустаяСсылка)) КАК Удержано,
		               |	ВЫБОР
		               |		КОГДА ДопСв_Обработано.Значение ЕСТЬ NULL
		               |			ТОГДА ЛОЖЬ
		               |		ИНАЧЕ ВЫРАЗИТЬ(ДопСв_Обработано.Значение КАК БУЛЕВО)
		               |	КОНЕЦ КАК Обработано
		               |ПОМЕСТИТЬ втИнвентаризации
		               |ИЗ
		               |	Документ.Инвентаризация КАК Инвентаризация
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДопСв_Удержано
		               |		ПО Инвентаризация.Ссылка = ДопСв_Удержано.Объект
		               |			И (ДопСв_Удержано.Свойство = &СвУдержано)
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДопСв_Обработано
		               |		ПО Инвентаризация.Ссылка = ДопСв_Обработано.Объект
		               |			И (ДопСв_Обработано.Свойство = &СвОбработано)
		               |ГДЕ
		               |	Инвентаризация.Проведен
		               |	И Инвентаризация.Дата МЕЖДУ &НачалоОтсчета И &КонецПериода
		               |	И Инвентаризация.СкладКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втИнвентаризации.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	МАКСИМУМ(втИнвентаризации.Дата) КАК Дата
		               |ПОМЕСТИТЬ втМаксДата
		               |ИЗ
		               |	втИнвентаризации КАК втИнвентаризации
		               |ГДЕ
		               |	втИнвентаризации.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И втИнвентаризации.Ревизия
		               |	И НЕ втИнвентаризации.Обработано
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втИнвентаризации.ТорговаяПлощадка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втМаксДата.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	ВЫБОР
		               |		КОГДА НЕ НачалоПериодаИНВ.Дата ЕСТЬ NULL
		               |			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(НачалоПериодаИНВ.Дата, ДЕНЬ), ДЕНЬ, 1)
		               |		ИНАЧЕ &НачалоОтсчета
		               |	КОНЕЦ КАК НачалоПериода,
		               |	втМаксДата.Дата КАК КонецПериода
		               |ИЗ
		               |	втМаксДата КАК втМаксДата
		               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			втИнвентаризации.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |			МАКСИМУМ(втИнвентаризации.Дата) КАК Дата
		               |		ИЗ
		               |			втИнвентаризации КАК втИнвентаризации
		               |		ГДЕ
		               |			втИнвентаризации.Ревизия
		               |			И втИнвентаризации.Обработано
		               |		
		               |		СГРУППИРОВАТЬ ПО
		               |			втИнвентаризации.ТорговаяПлощадка) КАК НачалоПериодаИНВ
		               |		ПО втМаксДата.ТорговаяПлощадка = НачалоПериодаИНВ.ТорговаяПлощадка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втИнвентаризации.Ссылка КАК Ссылка,
		               |	втИнвентаризации.Дата КАК Дата,
		               |	втИнвентаризации.Номер КАК Номер,
		               |	втИнвентаризации.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	втИнвентаризации.Склад КАК Склад,
		               |	втИнвентаризации.ТипЦенРозничный КАК ТипЦенРозничный,
		               |	втИнвентаризации.ТипЦенИтогов КАК ТипЦенИтогов,
		               |	втИнвентаризации.Ревизия КАК Ревизия,
		               |	втИнвентаризации.Удержано = &ЗнНеУдерживать КАК НеУдерживать
		               |ИЗ
		               |	втМаксДата КАК втМаксДата
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИнвентаризации КАК втИнвентаризации
		               |		ПО втМаксДата.ТорговаяПлощадка = втИнвентаризации.ТорговаяПлощадка
		               |			И втМаксДата.Дата >= втИнвентаризации.Дата
		               |			И (НЕ втИнвентаризации.Обработано)";
		
		Запрос.УстановитьПараметр("НачалоОтсчета", 	НачалоДня(НачалоОтсчета));
		Запрос.УстановитьПараметр("НачалоПериода", 	НачалоДня(НачалоПериода));
		Запрос.УстановитьПараметр("КонецПериода", 	КонецДня(КонецПериода));
		Запрос.УстановитьПараметр("СвОбработано",	СвОбработано);
		Запрос.УстановитьПараметр("СвУдержано",		СвУдержано);
		Запрос.УстановитьПараметр("ЗнНеУдерживать", НеУдерживать);
		Запрос.УстановитьПараметр("ТорговаяПлощадка", нСсылка);
		
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		ГраницаМассива = МассивРезультатов.ВГраница();
		
		ВыборкаПериод = МассивРезультатов[ГраницаМассива-1].Выбрать();
		Рез  		  = МассивРезультатов[ГраницаМассива];
		
		Если Рез.Пустой() Тогда
			ИтогИН.Description = "Нет инвентаризаций по складу: "+Строка(нСсылка);
			Возврат ИтогИН;
		КонецЕсли;
		
		
		Если ВыборкаПериод.Следующий() Тогда 
			ИтогИН.BeginDate 	= ВыборкаПериод.НачалоПериода;
			ИтогИН.EndDate		= ВыборкаПериод.КонецПериода;
		Иначе			
			ИтогИН.BeginDate 	= НачалоПериода;
			ИтогИН.EndDate		= КонецПериода;
		КонецЕсли;
		
		
		
		ТаблицаТип = ИтогИН.Свойства().Получить("AmountTotal_List").Тип;
		ИтогТип = ТаблицаТип.Свойства.Получить("AmountTotal").Тип;
		
		ТаблицаРезультат = ФабрикаXDTO.Создать(ТаблицаТип);
		
		Выборка = Рез.Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(Выборка.ТипЦенИтогов) Тогда
				ТипЦенИН = Выборка.ТипЦенИтогов;
			Иначе
				ТипЦенИН = Выборка.ТипЦенРозничный;
			КонецЕсли;
			
			
			СуммаПоИнвентаризации = Документы.Инвентаризация.ПолучитьИтогПоИнвентаризации(Выборка.Ссылка,ТипЦенИН);		
					
			СтрокаИтога =   ФабрикаXDTO.Создать(ИтогТип);
			СтрокаИтога.GuidDoc 	= Строка(Выборка.Ссылка.УникальныйИдентификатор());
			СтрокаИтога.Date 		= Выборка.Дата;
			СтрокаИтога.NumberDoc 	= Выборка.Номер;
			СтрокаИтога.Revision	= Выборка.Ревизия;
			СтрокаИтога.Total 		= СуммаПоИнвентаризации;
			СтрокаИтога.NoPenalty	= Выборка.НеУдерживать;
			
			ТаблицаРезультат.AmountTotal.Добавить(СтрокаИтога);			
			
		КонецЦикла;
	Исключение
		ИтогИН.Status = Ложь;
		ИтогИН.Description = ОписаниеОшибки();
	КонецПопытки;
	
	ИтогИН.AmountTotal_List = ТаблицаРезультат;
	
	Возврат ИтогИН;
	
	
КонецФункции

Функция ВернутьИтогиИН(НачалоПериода,КонецПериода,ИД_ТоговаяПлощадка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ИтогИН =  ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnTotalIN"));
	ИтогИН.Status = Истина;
	ИтогИН.Description = "";
	//ИтогИН.AmountTotal = 0;
	
	Попытка 
		Запрос=новый Запрос;
		Запрос.Текст="
		|ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Объект КАК ТТ
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Объект ССЫЛКА Справочник.ТорговыеПлощадки
		|	И ЗначенияСвойствОбъектов.Свойство = &Свойство
		|	И ЗначенияСвойствОбъектов.Значение = &Значение
		|";
		запрос.УстановитьПараметр("Значение",ИД_ТоговаяПлощадка);
		
		нСсылка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(ИД_ТоговаяПлощадка);
		
		
		Если нСсылка = Неопределено Тогда
			ИтогИН.Status = Ложь;
			ИтогИН.Description = "Не найден склад : "+ИД_ТоговаяПлощадка;
			
			Возврат ИтогИН;
		КонецЕсли;
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Инвентаризация.Ссылка КАК Ссылка,
		               |	Инвентаризация.Дата КАК Дата,
		               |	Инвентаризация.Номер КАК Номер,
		               |	Инвентаризация.СкладКомпании КАК Склад,
		               |	Инвентаризация.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦенРозничный,
		               |	Инвентаризация.ТипЦенИтогов КАК ТипЦенИтогов
		               |ИЗ
		               |	Документ.Инвентаризация КАК Инвентаризация
		               |ГДЕ
		               |	Инвентаризация.Проведен
		               |	И Инвентаризация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		               |	И Инвентаризация.СкладКомпании.ТорговаяПлощадка = &ТорговаяПлощадка";
		
		Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
		Запрос.УстановитьПараметр("ТорговаяПлощадка",нСсылка);
		
		Рез = Запрос.Выполнить();
		Если Рез.Пустой() Тогда
			ИтогИН.Description = "Нет инвентаризаций по складу: "+Строка(нСсылка);
			Возврат ИтогИН;
		КонецЕсли;
		
		Выборка = Рез.Выбрать();
		
		
		ТаблицаТип = ИтогИН.Свойства().Получить("AmountTotal_List").Тип;
		ИтогТип = ТаблицаТип.Свойства.Получить("AmountTotal").Тип;
		
		ТаблицаРезультат = ФабрикаXDTO.Создать(ТаблицаТип);
		
		
		Пока Выборка.Следующий() Цикл 
			
			Если ЗначениеЗаполнено(Выборка.ТипЦенИтогов) Тогда
				ТипЦенИН = Выборка.ТипЦенИтогов;
			Иначе
				ТипЦенИН = Выборка.ТипЦенРозничный;
			КонецЕсли;
			
			
			СуммаПоИнвентаризации = Документы.Инвентаризация.ПолучитьИтогПоИнвентаризации(Выборка.Ссылка,ТипЦенИН);		
					
			СтрокаИтога =   ФабрикаXDTO.Создать(ИтогТип);
			СтрокаИтога.GuidDoc 	= Строка(Выборка.Ссылка.УникальныйИдентификатор());
			СтрокаИтога.Date 		= Выборка.Дата;
			СтрокаИтога.NumberDoc 	= СокрЛП(Выборка.Номер);
			СтрокаИтога.Total 		= СуммаПоИнвентаризации;
			
			ТаблицаРезультат.AmountTotal.Добавить(СтрокаИтога);			
			
		КонецЦикла;
	Исключение
		ИтогИН.Status = Ложь;
		ИтогИН.Description = ОписаниеОшибки();
	КонецПопытки;
	
	ИтогИН.AmountTotal_List = ТаблицаРезультат;
	
	Возврат ИтогИН;
	
	
КонецФункции

Функция ВернутьСуммыИнкассации(ДатаНач,ДатаКон)Экспорт
	УстановитьПривилегированныйРежим(Истина);

	РезИтог =  ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnTabInc"));
	РезИтог.Status = Истина;
	РезИтог.Description = "";
	
	
	СтрокаБазаДанных = "Integration1C";
	СтрокаСервер = "OS_ATTIKA_ROOT";
	СтрокаПользователь = "mp";
	СтрокаПароль = "mp";
	СтрокаПодключенияКБазе = "Provider=SQLNCLI11;Server="+СокрЛП(СтрокаСервер) + ";Database="+СокрЛП(СтрокаБазаДанных) + ";Uid="+СокрЛП(СтрокаПользователь) + ";Pwd="+СокрЛП(СтрокаПароль) +";";
	
	
	STARTTIME = Формат(ДатаНач,"ДФ='yyyyMMdd 00:00:00'");
	ENDTIME   = Формат(ДатаКон,"ДФ='yyyyMMdd 23:59:59'");
	
	ТекстЗапроса =	"EXECUTE  [dbo].[GET_CASHIO]  @STARTTIME = '"+STARTTIME+"',  @ENDTIME = '"+ENDTIME+"'";
	
	Попытка 
		
		
		Connection = Новый COMObject("ADODB.Connection"); 	
		Connection.ConnectionTimeOut = 2500;//1200;
		Connection.CommandTimeout = 180;
		
		Connection.CursorLocation = 1; 
		Connection.Mode = 1; 
		Connection.Open(СтрокаПодключенияКБазе);  
		
		RS = Новый COMОбъект("ADODB.Recordset"); 
		RS.ActiveConnection = Connection;
		RS.Open(ТекстЗапроса);
		
		Если НЕ RS.EOF() тогда
			RS.MoveFirst();
		КонецЕсли;
		
		
		КешТоговыхПлощадок = Новый Соответствие;
		
		
		СтрокаТаблицаТип = РезИтог.Свойства().Получить("List").Тип;
		
		Пока RS.EOF()=0 Цикл
			
			СтрокаТаблица = ФабрикаXDTO.Создать(СтрокаТаблицаТип);
			
			ИДСклад =  СокрЛП(RS.Fields("SAREAGUID").Value);
			
			КешСклад = КешТоговыхПлощадок.Получить(ИДСклад);
			Если КешСклад = Неопределено Тогда
				
				СкладСсылка = Справочники.СкладыКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор(ИДСклад));
				КешСклад = Новый Структура("ТорговаяПлощадка,Номер",Строка(СкладСсылка.ТорговаяПлощадка),Формат(СкладСсылка.ТорговаяПлощадка.Global_ID,"ЧГ="));
				КешТоговыхПлощадок.Вставить(ИДСклад,КешСклад);
			КонецЕсли;
			
			
			//СтрокаТаблица
			СтрокаТаблица.Дата = RS.Fields("CASHIOTIME").Value;
			СтрокаТаблица.Кассир = Строка(RS.Fields("CASHIERNAME").Value);
			СтрокаТаблица.ИНН = Строка(RS.Fields("CASHIERKEY").Value);
			СтрокаТаблица.ТорговаяПлощадка = КешСклад.ТорговаяПлощадка;
			СтрокаТаблица.Номер = КешСклад.Номер;
			СтрокаТаблица.Сумма = Формат(RS.Fields("CASHIOSUM").Value,"ЧЦ=15; ЧДЦ=2; ЧГ=");
			
			
			РезИтог.List.Добавить(СтрокаТаблица);
			
			
			RS.MoveNext();
		КонецЦикла;
		
		
		
	Исключение
		РезИтог.Status = Ложь;
		РезИтог.Description = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат РезИтог;
	
КонецФункции

Функция ВернутьПродажиПоСкладу(Дата,Дата2 = Неопределено,ИД_ТоговаяПлощадка,ИД_Исключения="") Экспорт
	УстановитьПривилегированныйРежим(Истина);

	РезИтог =  ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnSales"));
	РезИтог.Status = Истина;
	РезИтог.Description = "";
	РезИтог.Amount = 0;
	РезИтог.quantity = 0;	
	
	Попытка 
		
		МассивИсключений = Новый Массив;
		//Если ЗначениеЗаполнено(ИД_Исключения) Тогда
		//	
		//	СпрФильт = Справочники.Фильтры.НайтиПоКоду(ИД_Исключения);	
		//	Если НЕ СпрФильт.Пустая()Тогда
		//		МассивИсключений = СпрФильт.Товары.ВыгрузитьКолонку("Товар");
		//	КонецЕсли;
		//	
		//КонецЕсли;
		
		Если ЗначениеЗаполнено(ИД_Исключения) Тогда
			ПапкаИсключеннойНоменклатуры = Справочники.Номенклатура.НайтиПоКоду(ИД_Исключения);
			Если ЗначениеЗаполнено(ПапкаИсключеннойНоменклатуры) тогда
				МассивИсключений.Добавить(ПапкаИсключеннойНоменклатуры);
			КонецЕсли;	
		КонецЕсли;	
	
		ТорговаяПлощадкаСсылка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(ИД_ТоговаяПлощадка);
		
		
		Если ТорговаяПлощадкаСсылка = Неопределено Тогда
			РезИтог.Status = Ложь;
			РезИтог.Description = "Не найден склад : "+ИД_ТоговаяПлощадка;
			
			Возврат РезИтог;
		КонецЕсли;
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажиОбороты.СуммаОборот КАК СуммаОборот,
		|	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			ТорговаяПлощадка = &ТорговаяПлощадка
		|				И ХозОперация в (&СписокОпераций)
		|			"+?(МассивИсключений.Количество()=0,""," И НЕ Номенклатура В ИЕРАРХИИ (&МассивИсключений)")+") КАК ПродажиОбороты";
		
		Запрос.УстановитьПараметр("НачалоПериода",НачалоДня(Дата));
		Запрос.УстановитьПараметр("КонецПериода", ?(Дата2 = Неопределено,КонецДня(Дата),КонецДня(Дата2)));
		Запрос.УстановитьПараметр("МассивИсключений",МассивИсключений);
		Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадкаСсылка);
		
		СписокОпераций = Новый СписокЗначений;
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСмены);
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСменыВозврат);
		Запрос.УстановитьПараметр("СписокОпераций", СписокОпераций); 
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
			РезИтог.Amount = ВыборкаДетальныеЗаписи.СуммаОборот;
			РезИтог.quantity = ВыборкаДетальныеЗаписи.КоличествоОборот;
		КонецЕсли;
		
	Исключение
		РезИтог.Status = Ложь;
		РезИтог.Description = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат РезИтог;
	
	
КонецФункции

Функция ВозвратОстатковТоваровНаСкладе(Date, GuidWarehouse)  Экспорт 
	УстановитьПривилегированныйРежим(Истина);

	РезТип = ФабрикаXDTO.Тип("http://www.digma.ua", "RestGoods");
	РезСтруктура = ФабрикаXDTO.Создать(РезТип);
	НоменклатураТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ArrayRestGoods");
	НоменклатураСтруктура = ФабрикаXDTO.Создать(НоменклатураТип);
	Склад = Справочники.СкладыКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор(GuidWarehouse));
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры.Наименование КАК ХарактеристикаНоменклатурыНаименование
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&ДатаЗапроса, СкладКомпании = &Склад) КАК ОстаткиТоваровКомпанииОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураНаименование,
	|	ХарактеристикаНоменклатурыНаименование";
	Запрос.УстановитьПараметр("ДатаЗапроса", Date);
	Запрос.УстановитьПараметр("Склад", Склад);
	РезЗапроса = Запрос.Выполнить();
	Выборка = РезЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЭлементНоменклатуры = ФабрикаXDTO.Создать(НоменклатураТип);
		ЭлементНоменклатуры.GuidProduct = Строка(Выборка.Номенклатура.УникальныйИдентификатор());
		ЭлементНоменклатуры.GuidSpecification = Строка(Выборка.ХарактеристикаНоменклатуры.УникальныйИдентификатор());
		ЭлементНоменклатуры.Quantity = Выборка.КоличествоОстаток;
		РезСтруктура.ArrayRestGoods.Добавить(ЭлементНоменклатуры);
	КонецЦикла; 
	Возврат РезСтруктура;
	
КонецФункции // ВозвратОстатковТоваровНаСкладе(GuidWarehouse)

Функция УстановитьСтатусЗУП_Инв(ХранилищеМассива, Статус) Экспорт 
	
	//Описание статусов инвентаризаций:
	//  0 - Не обработано,
	//  1 - Удержано,
	//  2 - Не удерживать
	
	Отказ = Ложь;
	Ошибки = Новый ТекстовыйДокумент;
	Обработано = 0;           
	СОшибками = 0;
	ЕстьТранзакция = Ложь;
	
	ОбработаноЗУП = Статус <> 0;
	
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnSales"));
	Результат.Status = Истина;
	Результат.Description = "";
	Результат.Amount = 0;
	Результат.quantity = 0;	
	
	Свойство = Неопределено;
	СвОбработано = Неопределено;
	
	нСвойство 		= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "СтатусУдержанияЗУП");	
	нСвОбработано 	= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ОбработаноЗУП");	
	
	СписокСвойств = УправлениеСвойствами.ПолучитьСписокСвойств(Документы.Инвентаризация.ПустаяСсылка(), Ложь, Истина);
	
	Если СписокСвойств.Найти(нСвойство) <> Неопределено Тогда 
		Свойство = нСвойство;
	Иначе
		Отказ = Истина;
		Ошибки.ДобавитьСтроку("Не удалось найти свойство");
	КонецЕсли;
	
	Если СписокСвойств.Найти(нСвОбработано) <> Неопределено Тогда 
		СвОбработано = нСвОбработано;
	Иначе
		Отказ = Истина;
		Ошибки.ДобавитьСтроку("Не удалось найти свойство ""ОбработаноЗУП""");
	КонецЕсли;	
	
	
	Попытка
		//МассивИД = ХранилищеМассива.Получить();
		
		тзДанныеИнв = ХранилищеМассива.Получить();
		
		Значение = Неопределено;
		Если Статус = 0 Тогда 
			Значение = Справочники.ЗначенияСвойствОбъектов.ПустаяСсылка();
		ИначеЕсли Статус = 1 Тогда 
			Значение = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию("Удержано",,, Свойство);
		ИначеЕсли Статус = 2 Тогда 
			Значение = Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию("Не удерживать",,, Свойство);
		Иначе
			Отказ = Истина;
			Ошибки.ДобавитьСтроку("Не установлено значения для параметра Статус=" + Статус);
		КонецЕсли;
		
		Если Не Отказ И Значение = Неопределено Тогда 
			Отказ = Истина;
			Ошибки.ДобавитьСтроку("Не найден элемент справочника ""ЗначенияСвойствОбъектов""");
		КонецЕсли;
		
		ТаблицаСвойств = Новый ТаблицаЗначений;
		ТаблицаСвойств.Колонки.Добавить("Свойство");
		ТаблицаСвойств.Колонки.Добавить("Значение");
		
		СтрокаУдержано 	 = Новый Структура("Свойство,Значение", Свойство, Значение);
		СтрокаОбработано = Новый Структура("Свойство,Значение", СвОбработано, ОбработаноЗУП);
				
		Если Не Отказ Тогда 
			НачатьТранзакцию();
			ЕстьТранзакция = Истина;
		КонецЕсли;
		
		Для Каждого Стр Из тзДанныеИнв Цикл 
			текИД = Стр.ИнвентаризацияИД;
			
			Если Отказ Тогда 
				Прервать;
			КонецЕсли;
			
			Если ПустаяСтрока(текИД) Тогда 
				Продолжить;
			КонецЕсли;
			
			ГУИД = Новый УникальныйИдентификатор(текИД);
			Док = Документы.Инвентаризация.ПолучитьСсылку(ГУИД);
			
			Если Не ОбщегоНазначения.СсылкаСуществует(Док) Тогда 
				СОшибками = СОшибками + 1;
				
				Отказ = Истина;
				Ошибки.ДобавитьСтроку(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не найдена инвентаризация с идентификатором <%1>'; uk = 'Не знайдена інвентаризація із ідентифікатором <%1>'"),
						текИД));										
				Продолжить;
			КонецЕсли;
			
			ТаблицаСвойств.Очистить();
			НоваяСтрока = ТаблицаСвойств.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОбработано);
			
			Если Стр.РаспределятьИтог Тогда 
				НоваяСтрока = ТаблицаСвойств.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаУдержано);
			КонецЕсли;
			
			УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Док, ТаблицаСвойств);
			
			Обработано = Обработано + 1;
		КонецЦикла;
		
		Если ЕстьТранзакция Тогда 
			Если Не Отказ Тогда 
				ЗафиксироватьТранзакцию();			
			Иначе
				ОтменитьТранзакцию();
			КонецЕсли;
			ЕстьТранзакция = Ложь;
		КонецЕсли;
	Исключение
		Отказ = Истина;
		Ошибки.ДобавитьСтроку(ОписаниеОшибки());
		
		Если ЕстьТранзакция Тогда 
			ОтменитьТранзакцию();
		КонецЕсли;
	КонецПопытки;
	
	Результат.Amount = Обработано;
	Результат.quantity = СОшибками;	
	
	Если Отказ Тогда 
		Результат.Status = Ложь;
	КонецЕсли;
	
	Если Ошибки.КоличествоСтрок() > 0 Тогда 
		Результат.Description = Ошибки.ПолучитьТекст();	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьГрафикиРаботыТТ(ХранилищеМассива) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеОк = Истина;
	Ошибки = Новый ТекстовыйДокумент;
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "GetSchedule"));
	
	Попытка
		СтрокаТаблицыТип = Результат.Свойства().Получить("Schedule").Тип;
		
		МассивКодов = ХранилищеМассива.Получить();
		
		тзКодыТТ = Новый ТаблицаЗначений;
		тзКодыТТ.Колонки.Добавить("Код", ОбщегоНазначения.ОписаниеТипаСтрока(9));
		
		Для Каждого КодТТ Из МассивКодов Цикл 
			Если ЗначениеЗаполнено(КодТТ) Тогда 
				НоваяСтрока = тзКодыТТ.Добавить();
				НоваяСтрока.Код = КодТТ;
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КодыТТ", тзКодыТТ);
		Запрос.Текст = "ВЫБРАТЬ
		               |	КодыТТ.Код КАК Код
		               |ПОМЕСТИТЬ втКодыТТ
		               |ИЗ
		               |	&КодыТТ КАК КодыТТ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втКодыТТ.Код КАК Код,
		               |	ТорговыеПлощадки.Ссылка КАК Объект,
		               |	ВЫБОР
		               |		КОГДА НЕ РежимРаботы.Ссылка ЕСТЬ NULL
		               |			ТОГДА РАЗНОСТЬДАТ(РежимРаботы.Будни.Начало, РежимРаботы.Будни.Окончание, ЧАС)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК ГрафикБудни,
		               |	ВЫБОР
		               |		КОГДА НЕ РежимРаботы.Ссылка ЕСТЬ NULL
		               |			ТОГДА РАЗНОСТЬДАТ(РежимРаботы.Выходные.Начало, РежимРаботы.Выходные.Окончание, ЧАС)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК ГрафикВыходные
		               |ИЗ
		               |	втКодыТТ КАК втКодыТТ
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТП_ДопРеквизиты
		               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РежимРаботы КАК РежимРаботы
		               |				ПО ТП_ДопРеквизиты.Значение = РежимРаботы.Ссылка
		               |			ПО ТорговыеПлощадки.Ссылка = ТП_ДопРеквизиты.Ссылка
		               |				И (ТП_ДопРеквизиты.Свойство.Имя = ""РежимРаботы"")
		               |		ПО втКодыТТ.Код = ТорговыеПлощадки.Код";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			Если Не ЗначениеЗаполнено(Выборка.Объект) Тогда 
				
				Ошибки.ДобавитьСтроку(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не найден Объект с кодом ""%1""'"),
						Выборка.Код));					

			ИначеЕсли Выборка.ГрафикБудни = 0 И Выборка.ГрафикВыходные = 0 Тогда 
				
				Ошибки.ДобавитьСтроку(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для объекта [%1] %2 не установлен график работы'"),
						Выборка.Код,
						Выборка.Объект));					
						
			Иначе
						
				СтрокаГрафика = ФабрикаXDTO.Создать(СтрокаТаблицыТип);							
				СтрокаГрафика.Code 			= Выборка.Код;
				СтрокаГрафика.Hours 		= Выборка.ГрафикБудни;
				СтрокаГрафика.HoursWeekend 	= Выборка.ГрафикВыходные;
				
				Результат.Schedule.Добавить(СтрокаГрафика);				
			КонецЕсли;
		КонецЦикла;
		
	Исключение
		ВсеОк = Ложь;
		Ошибки.ДобавитьСтроку(ОписаниеОшибки());		
	КонецПопытки;
	

	Результат.Status = ВсеОк;
	
	Если Ошибки.КоличествоСтрок() > 0 Тогда 
		Результат.Description = Ошибки.ПолучитьТекст();
	Иначе
		Результат.Description = "";
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции


//гамазин
Функция ПолучитьДетальныеГрафикиРаботыТТ(ХранилищеМассива) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеОк = Истина;

	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "GetDetailedSchedule"));
	
	Результат.Description = "";
	
	Попытка
		СтрокаТаблицыТип = Результат.Свойства().Получить("Schedule").Тип;
		
		МассивКодов = ХранилищеМассива.Получить();
		
		тзКодыТТ = Новый ТаблицаЗначений;
		тзКодыТТ.Колонки.Добавить("Код", ОбщегоНазначения.ОписаниеТипаСтрока(9));
		
		Для Каждого КодТТ Из МассивКодов Цикл 
			Если ЗначениеЗаполнено(КодТТ) Тогда 
				НоваяСтрока = тзКодыТТ.Добавить();
				НоваяСтрока.Код = КодТТ;
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КодыТТ", тзКодыТТ);
		Запрос.Текст = "ВЫБРАТЬ
		               |	КодыТТ.Код КАК Код
		               |ПОМЕСТИТЬ втКодыТТ
		               |ИЗ
		               |	&КодыТТ КАК КодыТТ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втКодыТТ.Код КАК Код,
		               |	ТорговыеПлощадки.Ссылка КАК Объект,
		               |		IsNULL(РежимРаботы.Будни.Начало, ДатаВремя(1,1,1)) как БудниНачало,
					   |		IsNULL(РежимРаботы.Будни.Окончание, ДатаВремя(1,1,1)) как БудниОкончание,
					   |		IsNULL(РежимРаботы.Выходные.Начало, ДатаВремя(1,1,1)) как ВыходныеНачало,
					   |		IsNULL(РежимРаботы.Выходные.Окончание, ДатаВремя(1,1,1)) как ВыходныеОкончание
		               |ИЗ
		               |	втКодыТТ КАК втКодыТТ
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТП_ДопРеквизиты
		               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РежимРаботы КАК РежимРаботы
		               |				ПО ТП_ДопРеквизиты.Значение = РежимРаботы.Ссылка
		               |			ПО ТорговыеПлощадки.Ссылка = ТП_ДопРеквизиты.Ссылка
		               |				И (ТП_ДопРеквизиты.Свойство.Имя = ""РежимРаботы"")
		               |		ПО втКодыТТ.Код = ТорговыеПлощадки.Код";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			Если ЗначениеЗаполнено(Выборка.Объект) Тогда 
				
				СтрокаГрафика = ФабрикаXDTO.Создать(СтрокаТаблицыТип);							
				СтрокаГрафика.Code 			= Выборка.Код;
				СтрокаГрафика.Start 		= Выборка.БудниНачало;
				СтрокаГрафика.Stop 			= Выборка.БудниОкончание;
				СтрокаГрафика.StartWeekend 	= Выборка.ВыходныеНачало;
				СтрокаГрафика.StopWeekend 	= Выборка.ВыходныеОкончание;
				
				Результат.Schedule.Добавить(СтрокаГрафика);				
			КонецЕсли;
		КонецЦикла;
		
	Исключение
		ВсеОк = Ложь;
		Результат.Description = ОписаниеОшибки();
	КонецПопытки;
	
	Результат.Status = ВсеОк;
			
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМассивКодовОбъектовКуратора(GuidFL) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Попытка
		Отказ = Ложь;
		
		ГУИД = Новый УникальныйИдентификатор(GuidFL);
		ФЛСсылка = Справочники.ФизическиеЛица.ПолучитьСсылку(ГУИД);
		Если ОбщегоНазначения.СсылкаСуществует(ФЛСсылка) Тогда 
			СсылкаФЛ = ФЛСсылка;
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		Если не Отказ тогда
			МассивКодовТип = ФабрикаXDTO.Тип("http://www.digma.ua", "GetKuratorObjects");
			МассивКодов = ФабрикаXDTO.Создать(МассивКодовТип);	

			Запр = Новый Запрос;
			Запр.Текст = "
			|ВЫБРАТЬ
			|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.Код КАК КодОбъекта
			|ИЗ
			|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
			|		ПО (ТорговыеПлощадкиДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка)
			|			И (ДополнительныеРеквизитыИСведения.Имя = ""КураторПлощадки"")
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
			|		ПО ТорговыеПлощадкиДополнительныеРеквизиты.Значение = ФизическиеЛица.Ссылка			
			|ГДЕ
			|	ФизическиеЛица.Ссылка = &ФЛ";

			Запр.УстановитьПараметр("ФЛ", СсылкаФЛ); 
			Выборка = Запр.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() цикл
				МассивКодов.Code.Добавить(СокрЛП(Выборка.КодОбъекта));
			КонецЦикла;
		КонецЕсли;	
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	Если не Отказ тогда
		Возврат МассивКодов;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТаблицуКураторовИОбъектов() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Попытка
		ТаблицаКураторовОбъектовТип = ФабрикаXDTO.Тип("http://www.digma.ua", "KuratorObjectsList");
		ТаблицаКураторовОбъектов = ФабрикаXDTO.Создать(ТаблицаКураторовОбъектовТип);
		
		СтрокаТаблицыТип = ТаблицаКураторовОбъектов.Свойства().Получить("CorrArray").Тип;
		
		ТаблицаКураторовОбъектов.Status = Истина;
		
		Запр = Новый Запрос;
		Запр.Текст = "ВЫБРАТЬ
		             |	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.Код КАК КодОбъекта,
		             |	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК ФизЛицо
		             |ИЗ
		             |	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		             |		ПО ТорговыеПлощадкиДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
		             |			И (ДополнительныеРеквизитыИСведения.Имя = ""КураторПлощадки"")
		             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		             |		ПО ТорговыеПлощадкиДополнительныеРеквизиты.Значение = ФизическиеЛица.Ссылка";
		
		Выборка = Запр.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 тогда
			Пока Выборка.Следующий() Цикл
				СтрокаТаблицы = ФабрикаXDTO.Создать(СтрокаТаблицыТип);
				СтрокаТаблицы.ObjCode = Выборка.КодОбъекта; 
				СтрокаТаблицы.KuratorGUID = Строка(Выборка.ФизЛицо.УникальныйИдентификатор());
				ТаблицаКураторовОбъектов.CorrArray.Добавить(СтрокаТаблицы);
			КонецЦикла;
		Иначе
			ТаблицаКураторовОбъектов.Status = Ложь;
		КонецЕсли;	
	Исключение
		ТаблицаКураторовОбъектов.Status = Ложь;
	КонецПопытки;	
	
	Возврат ТаблицаКураторовОбъектов;
	
КонецФункции	

Функция ПолучитьДеревоНоменклатуры() экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "GetGoodsTree"));
	Результат.Description = "";	
	
	Попытка
		Запр = Новый Запрос;
		
		Запр.Текст = "ВЫБРАТЬ
		             |	Номенклатура.Код КАК Код,
		             |	Номенклатура.Наименование КАК Наименование
		             |ИЗ
		             |	Справочник.Номенклатура КАК Номенклатура
		             |ГДЕ
		             |	Номенклатура.Ссылка В ИЕРАРХИИ(&МассивНоменклатуры)
		             |
		             |УПОРЯДОЧИТЬ ПО
		             |	Номенклатура.Наименование ИЕРАРХИЯ";
		
		МассивНоменклатуры = Новый Массив;
		МассивНоменклатуры.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0009000")); //табачные изделия
		МассивНоменклатуры.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0010000")); //продукты питания
		Запр.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
		
		ДЗ = Запр.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Хранилище = Новый ХранилищеЗначения(ДЗ);
		Результат.ValStorage = Хранилище;
		
		Результат.Status = Истина;	
	Исключение
		Результат.Description = ОписаниеОшибки();		
		Результат.Status = Ложь;
	КонецПопытки;
		
	Возврат Результат;
	
КонецФункции	

Функция ПолучитьПродажиПоПодразделениямСФильтром(ДатаНачала, ДатаОкончания, ТаблицаПараметров) экспорт
	Перем тзРез;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnSalesGoodsFilter"));
	Результат.Description = "";	
	
	Попытка
		тзПодразделенияНоменклатура = ТаблицаПараметров.Получить(); //valueStorage хранилище значений
		тзПодразделенияНоменклатура.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура")); 
		тзПодразделенияНоменклатура.Колонки.Добавить("ТорговаяПлощадка", Новый ОписаниеТипов("СправочникСсылка.ТорговыеПлощадки"));
		
		Для каждого СтрокаТЗ из тзПодразделенияНоменклатура цикл
			СтрокаТЗ.ТорговаяПлощадка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(СокрЛП(СтрокаТЗ.КодПодразделения));
			СтрокаТЗ.Номенклатура = Справочники.Номенклатура.НайтиПоКоду(СокрЛП(СтрокаТЗ.КодТовара));
		КонецЦикла;
		
		тзРез = Новый ТаблицаЗначений;
		тзРез.Колонки.Добавить("КодПодразделения");
		тзРез.Колонки.Добавить("Количество");
		тзРез.Колонки.Добавить("Сумма");
		
		Запр = Новый Запрос;
		Запр.Текст = 
			"ВЫБРАТЬ
			|	ПродажиОбороты.ТорговаяПлощадка.Код КАК КодПодразделения,
			|	ПродажиОбороты.СуммаОборот КАК СуммаОборот,
			|	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот
			|ИЗ
			|	РегистрНакопления.Продажи.Обороты(
			|			&НачалоПериода,
			|			&КонецПериода,
			|			,
			|			ТорговаяПлощадка = &ТорговаяПлощадка
			|				И ХозОперация в (&СписокОпераций)
			|				И Номенклатура В ИЕРАРХИИ (&МассивНоменклатуры)) КАК ПродажиОбороты";
		
		Запр.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаНачала));
		Запр.УстановитьПараметр("КонецПериода", КонецДня(ДатаОкончания));
		
		СписокОпераций = Новый СписокЗначений;
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСмены);
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСменыВозврат);
		Запр.УстановитьПараметр("СписокОпераций", СписокОпераций); 

		
		ТекПодразделение = Неопределено;
		МассивНоменклатуры = Новый Массив;
		МаксИндекс = тзПодразделенияНоменклатура.Количество() - 1; 
		
		Для каждого СтрокаТЗ из тзПодразделенияНоменклатура цикл 
			ТекИндекс = тзПодразделенияНоменклатура.Индекс(СтрокаТЗ);
			
			Если ТекПодразделение <> СтрокаТЗ.ТорговаяПлощадка
				или ТекИндекс = МаксИндекс тогда
					Если ТекИндекс = МаксИндекс тогда //забрать последнюю запись
						ТекПодразделение = СтрокаТЗ.ТорговаяПлощадка; //если в таблице 1 запись
						МассивНоменклатуры.Добавить(СтрокаТЗ.Номенклатура);
					КонецЕсли;
				Если ЗначениеЗаполнено(ТекПодразделение)
					и МассивНоменклатуры.Количество() > 0 тогда
					Запр.УстановитьПараметр("ТорговаяПлощадка", ТекПодразделение);
					Запр.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
					
					Выборка = Запр.Выполнить().Выбрать();
					Если Выборка.Следующий() тогда
						НС = тзРез.Добавить();	
						НС.КодПодразделения = Выборка.КодПодразделения;
						НС.Количество 		= Выборка.КоличествоОборот;
						НС.Сумма 			= Выборка.СуммаОборот;
					КонецЕсли;	
				КонецЕсли;
				
				ТекПодразделение = СтрокаТЗ.ТорговаяПлощадка;
				МассивНоменклатуры.Очистить();
				МассивНоменклатуры.Добавить(СтрокаТЗ.Номенклатура);
			Иначе
				МассивНоменклатуры.Добавить(СтрокаТЗ.Номенклатура);
			КонецЕсли;
			

		КонецЦикла;

			
		Результат.Status = Истина;	
	Исключение
		Результат.Description = ОписаниеОшибки();		
		Результат.Status = Ложь;
	КонецПопытки;
	
	Хранилище = Новый ХранилищеЗначения(тзРез);
	Результат.ResTable = Хранилище;
	
	
	Возврат Результат;
	
КонецФункции	

Функция ПолучитьДанныеПоСканированиюТСД(ДатаНачала, ДатаОкончания, ВидСканирования, Параметры) экспорт
	Перем тзРез;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnScanKPI"));
	Результат.Description = "";	
	
	Попытка
		ВходящийМассивПодразделений = Параметры.Получить(); //valueStorage хранилище значений
		МассивПодразделений = Новый Массив;
		
		Для каждого Эл из ВходящийМассивПодразделений цикл
			ТоргПлощадка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(СокрЛП(Эл));
			Если ТоргПлощадка <> Неопределено тогда
				МассивПодразделений.Добавить(ТоргПлощадка);
			КонецЕсли;	
		КонецЦикла;
		
		тзРез = Новый ТаблицаЗначений;
		тзРез.Колонки.Добавить("КодПодразделения");
		тзРез.Колонки.Добавить("ПроцентВыполнения");
		
		Запр = Новый Запрос;
		Если ВидСканирования = "ТНП" тогда
			Запр.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ТК_ТНП_ИтогиСканирования.Задание КАК Задача,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Дата КАК Дата,
			             |	ТК_ТНП_ИтогиСканирования.Задание.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Склад КАК Склад,
			             |	ТК_ТНП_ИтогиСканирования.Номенклатура КАК Номенклатура,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Выполнена КАК Выполнена,
			             |	ТК_ТНП_ИтогиСканирования.Задание.СрокИсполнения КАК СрокИсполнения,
			             |	ТК_ТНП_ИтогиСканирования.Задание.ДатаИсполнения КАК ДатаИсполнения,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Основание КАК Основание,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Предмет КАК Предмет,
			             |	ТК_ТНП_ИтогиСканирования.Задание.Исполнитель КАК Исполнитель,
			             |	1 КАК План,
			             |	ТК_ТНП_ИтогиСканирования.ОтсканированоТовар КАК ОтсканированоТовар,
			             |	ТК_ТНП_ИтогиСканирования.ОтсканированоЦенник КАК ОтсканированоЦенник
			             |ПОМЕСТИТЬ Вт_Задачи
			             |ИЗ
			             |	РегистрСведений.ТК_ТНП_ИтогиСканирования КАК ТК_ТНП_ИтогиСканирования
			             |ГДЕ
			             |	ТК_ТНП_ИтогиСканирования.Задание.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ИнвентаризацияТовары.Ссылка КАК ДокументИН,
			             |	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	СУММА(ИнвентаризацияТовары.Количество) КАК Количество,
			             |	СУММА(1) КАК КоличествоСКЮ,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ КАК ТипЦен
			             |ПОМЕСТИТЬ Вт_ИН
			             |ИЗ
			             |	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
			             |ГДЕ
			             |	ИнвентаризацияТовары.Ссылка.Проведен
			             |	И ИнвентаризацияТовары.Ссылка.ЗадачаТСД В
			             |			(ВЫБРАТЬ
			             |				Вт_Задачи.Задача КАК Задача
			             |			ИЗ
			             |				Вт_Задачи КАК Вт_Задачи)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ИнвентаризацияТовары.Номенклатура,
			             |	ИнвентаризацияТовары.Ссылка,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ПеремещениеТоваровТовары.Ссылка КАК ДокументПТ,
			             |	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
			             |	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	СУММА(ПеремещениеТоваровТовары.КоличествоБазовое) КАК КоличествоПТ,
			             |	СУММА(1) КАК КоличествоСКЮПТ,
			             |	ВЫБОР
			             |		КОГДА ПеремещениеТоваровТовары.Ссылка.ТипЦен <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ПеремещениеТоваровТовары.Ссылка.ТипЦен
			             |		ИНАЧЕ ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ КАК ТипЦен
			             |ПОМЕСТИТЬ Вт_Перемещения
			             |ИЗ
			             |	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
			             |ГДЕ
			             |	ПеремещениеТоваровТовары.Ссылка В
			             |			(ВЫБРАТЬ
			             |				Вт_Задачи.Предмет КАК Предмет
			             |			ИЗ
			             |				Вт_Задачи КАК Вт_Задачи)
			             |	И ПеремещениеТоваровТовары.Ссылка.Проведен
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры,
			             |	ПеремещениеТоваровТовары.Номенклатура,
			             |	ПеремещениеТоваровТовары.Ссылка,
			             |	ВЫБОР
			             |		КОГДА ПеремещениеТоваровТовары.Ссылка.ТипЦен <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ПеремещениеТоваровТовары.Ссылка.ТипЦен
			             |		ИНАЧЕ ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ
			             |	Вт_ИН.ДокументИН КАК Документ,
			             |	Вт_ИН.ТипЦен КАК ТипЦен,
			             |	Вт_ИН.Номенклатура КАК Номенклатура,
			             |	Вт_ИН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	Вт_ИН.Количество КАК КоличествоИН,
			             |	Вт_ИН.КоличествоСКЮ КАК КоличествоСКЮИН,
			             |	0 КАК КоличествоПТ,
			             |	0 КАК КоличествоСКЮПТ,
			             |	Вт_Задачи.Задача КАК Задача
			             |ПОМЕСТИТЬ Вт_Документы
			             |ИЗ
			             |	Вт_Задачи КАК Вт_Задачи
			             |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_ИН КАК Вт_ИН
			             |		ПО Вт_Задачи.Задача = Вт_ИН.ДокументИН.ЗадачаТСД
			             |
			             |ОБЪЕДИНИТЬ ВСЕ
			             |
			             |ВЫБРАТЬ
			             |	Вт_Перемещения.ДокументПТ,
			             |	Вт_Перемещения.ТипЦен,
			             |	Вт_Перемещения.Номенклатура,
			             |	Вт_Перемещения.ХарактеристикаНоменклатуры,
			             |	0,
			             |	0,
			             |	Вт_Перемещения.КоличествоПТ,
			             |	Вт_Перемещения.КоличествоСКЮПТ,
			             |	Вт_Задачи.Задача
			             |ИЗ
			             |	Вт_Задачи КАК Вт_Задачи
			             |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Перемещения КАК Вт_Перемещения
			             |		ПО Вт_Задачи.Предмет = Вт_Перемещения.ДокументПТ
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	Вт_Документы.Документ КАК Документ,
			             |	Вт_Документы.ТипЦен КАК ТипЦен,
			             |	Вт_Документы.Номенклатура КАК Номенклатура,
			             |	Вт_Документы.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	СУММА(Вт_Документы.КоличествоИН) КАК КоличествоИН,
			             |	СУММА(Вт_Документы.КоличествоСКЮИН) КАК КоличествоСКЮИН,
			             |	СУММА(Вт_Документы.КоличествоПТ) КАК КоличествоПТ,
			             |	СУММА(Вт_Документы.КоличествоСКЮПТ) КАК КоличествоСКЮПТ,
			             |	Вт_Документы.Задача КАК Задача
			             |ПОМЕСТИТЬ Вт_ДокументыИтог
			             |ИЗ
			             |	Вт_Документы КАК Вт_Документы
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	Вт_Документы.Документ,
			             |	Вт_Документы.ТипЦен,
			             |	Вт_Документы.Номенклатура,
			             |	Вт_Документы.ХарактеристикаНоменклатуры,
			             |	Вт_Документы.Задача
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ
			             |	Вт_Задачи.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	Вт_Задачи.ТорговаяПлощадка.Код КАК Код,
			             |	СУММА(ВЫБОР
			             |			КОГДА Вт_ДокументыИтог.КоличествоСКЮПТ > 0
			             |					ИЛИ Вт_ДокументыИтог.КоличествоСКЮИН > 0
			             |					ИЛИ Вт_Задачи.ОтсканированоТовар = 1
			             |					ИЛИ Вт_Задачи.ОтсканированоЦенник = 1
			             |				ТОГДА 1
			             |			ИНАЧЕ 0
			             |		КОНЕЦ) / СУММА(ЕСТЬNULL(Вт_Задачи.План, 0.000001)) * 100 КАК ПроцентВыполнения
			             |ИЗ
			             |	Вт_Задачи КАК Вт_Задачи
			             |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_ДокументыИтог КАК Вт_ДокументыИтог
			             |		ПО Вт_Задачи.Задача = Вт_ДокументыИтог.Задача
			             |			И Вт_Задачи.Номенклатура = Вт_ДокументыИтог.Номенклатура
			             |ГДЕ
			             |	Вт_Задачи.ТорговаяПлощадка В(&Подразделения)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	Вт_Задачи.ТорговаяПлощадка,
			             |	Вт_Задачи.ТорговаяПлощадка.Код";
		ИначеЕсли ВидСканирования = "ТБД" тогда
			Запр.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ВложенныйЗапрос.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	СУММА(1) КАК План,
			             |	СУММА(ВЫБОР
			             |			КОГДА ЕСТЬNULL(ТК_ТБД_ИтогиСканирования.ОтсканированоТовар, 0) <> 0
			             |					И ЕСТЬNULL(ТК_ТБД_ИтогиСканирования.ОтсканированоЦенник, 0) <> 0
			             |				ТОГДА 1
			             |			ИНАЧЕ 0
			             |		КОНЕЦ) КАК Факт,
			             |	СУММА(ВЫБОР
			             |			КОГДА ТК_ТБД_ИтогиСканирования.ЦенаПроверена = 0
			             |				ТОГДА 1
			             |			ИНАЧЕ 0
			             |		КОНЕЦ) КАК НекорректныеЦенники,
			             |	ВложенныйЗапрос.Ссылка КАК Задание,
			             |	ТК_ТБД_ИтогиСканирования.Номенклатура КАК Номенклатура,
			             |	ТК_ТБД_ИтогиСканирования.Исполнитель КАК Исполнитель,
			             |	ВложенныйЗапрос.Ссылка.Выполнена КАК Выполнена
			             |ПОМЕСТИТЬ Вт_Задания
			             |ИЗ
			             |	(ВЫБРАТЬ
			             |		ТК_ЗадачаТСД.Ссылка КАК Ссылка
			             |	ИЗ
			             |		Задача.ТК_ЗадачаТСД КАК ТК_ЗадачаТСД
			             |	ГДЕ
			             |		ТК_ЗадачаТСД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			             |		И ТК_ЗадачаТСД.ВидЗадачи = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыЗадачТСД.ТБД)) КАК ВложенныйЗапрос
			             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ТБД_ИтогиСканирования КАК ТК_ТБД_ИтогиСканирования
			             |		ПО ВложенныйЗапрос.Ссылка = ТК_ТБД_ИтогиСканирования.Задание
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ТК_ТБД_ИтогиСканирования.Номенклатура,
			             |	ТК_ТБД_ИтогиСканирования.Исполнитель,
			             |	ВложенныйЗапрос.Ссылка.Выполнена,
			             |	ВложенныйЗапрос.Ссылка,
			             |	ВложенныйЗапрос.Ссылка.ТорговаяПлощадка
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ИнвентаризацияТовары.Ссылка КАК ДокументИН,
			             |	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	СУММА(ИнвентаризацияТовары.Количество) КАК Количество,
			             |	СУММА(1) КАК КоличествоСКЮ,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ КАК ТипЦен
			             |ПОМЕСТИТЬ Вт_ИН
			             |ИЗ
			             |	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
			             |ГДЕ
			             |	ИнвентаризацияТовары.Ссылка.Проведен
			             |	И ИнвентаризацияТовары.Ссылка.Ссылка В
			             |			(ВЫБРАТЬ
			             |				Вт_Задания.Задание.Предмет КАК ИН
			             |			ИЗ
			             |				Вт_Задания КАК Вт_Задания)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ИнвентаризацияТовары.Номенклатура,
			             |	ИнвентаризацияТовары.Ссылка,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
						 |	Вт_Задания.ТорговаяПлощадка.Код КАК Код,
						 |	Вт_Задания.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	СУММА(Вт_Задания.Факт) / СУММА(IsNULL(Вт_Задания.План, 0.000001)) * 100 КАК ПроцентВыполнения
			             |ИЗ
			             |	Вт_Задания КАК Вт_Задания
			             |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_ИН КАК Вт_ИН
			             |		ПО Вт_Задания.Задание.Предмет = Вт_ИН.ДокументИН
			             |			И Вт_Задания.Номенклатура = Вт_ИН.Номенклатура
			             |ГДЕ
			             |	Вт_Задания.ТорговаяПлощадка В(&Подразделения)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	Вт_Задания.ТорговаяПлощадка";
			
		Иначе //верификация ценников
			Запр.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ТК_ВерификацияЦенников.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	СУММА(1) КАК План,
			             |	СУММА(ВЫБОР
			             |			КОГДА ТК_ВерификацияЦенников.Отсканировано <> 0
			             |					И ТК_ВерификацияЦенников.НеКорректный = 0
			             |				ТОГДА 1
			             |			ИНАЧЕ 0
			             |		КОНЕЦ) КАК Факт,
			             |	СУММА(ТК_ВерификацияЦенников.НетЦенников) КАК НекорректныеЦенники,
			             |	ТК_ВерификацияЦенников.Задача КАК Задание,
			             |	ТК_ВерификацияЦенников.Номенклатура КАК Номенклатура,
			             |	ТК_ВерификацияЦенников.Задача.Исполнитель КАК Исполнитель,
			             |	ТК_ВерификацияЦенников.Задача.Дата КАК Дата,
			             |	ТК_ВерификацияЦенников.Задача.ГруппаНоменклатуры КАК ГруппаНоменклатуры,
			             |	ТК_ВерификацияЦенников.Задача.Склад КАК Склад
			             |ПОМЕСТИТЬ Вт_Задания
			             |ИЗ
			             |	РегистрСведений.ТК_ВерификацияЦенников КАК ТК_ВерификацияЦенников
			             |ГДЕ
			             |	ТК_ВерификацияЦенников.Период МЕЖДУ &НачалоПериода И &КонецПериода
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ТК_ВерификацияЦенников.Номенклатура,
			             |	ТК_ВерификацияЦенников.Задача.Исполнитель,
			             |	ТК_ВерификацияЦенников.ТорговаяПлощадка,
			             |	ТК_ВерификацияЦенников.Задача,
			             |	ТК_ВерификацияЦенников.Задача.Дата,
			             |	ТК_ВерификацияЦенников.Задача.ГруппаНоменклатуры,
			             |	ТК_ВерификацияЦенников.Задача.Склад
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ РАЗРЕШЕННЫЕ
			             |	ИнвентаризацияТовары.Ссылка КАК ДокументИН,
			             |	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
			             |	СУММА(ИнвентаризацияТовары.Количество) КАК Количество,
			             |	СУММА(1) КАК КоличествоСКЮ,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ КАК ТипЦен
			             |ПОМЕСТИТЬ Вт_ИН
			             |ИЗ
			             |	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
			             |ГДЕ
			             |	ИнвентаризацияТовары.Ссылка.Проведен
			             |	И (ИнвентаризацияТовары.Ссылка.Ссылка, ИнвентаризацияТовары.Номенклатура) В
			             |			(ВЫБРАТЬ
			             |				Вт_Задания.Задание.Предмет КАК ИН,
			             |				Вт_Задания.Номенклатура КАК Номенклатура
			             |			ИЗ
			             |				Вт_Задания КАК Вт_Задания)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	ИнвентаризацияТовары.Номенклатура,
			             |	ИнвентаризацияТовары.Ссылка,
			             |	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
			             |	ВЫБОР
			             |		КОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
			             |			ТОГДА ИнвентаризацияТовары.Ссылка.ТипЦенИтогов
			             |		ИНАЧЕ ИнвентаризацияТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
			             |	КОНЕЦ
			             |;
			             |
			             |////////////////////////////////////////////////////////////////////////////////
			             |ВЫБРАТЬ
						 |	Вт_Задания.ТорговаяПлощадка.Код КАК Код,
						 |	Вт_Задания.ТорговаяПлощадка КАК ТорговаяПлощадка,
			             |	(СУММА(Вт_Задания.Факт) + СУММА(Вт_Задания.НекорректныеЦенники)) / СУММА(ЕСТЬNULL(Вт_Задания.План, 0.00001)) * 100 КАК ПроцентВыполнения
			             |ИЗ
			             |	Вт_Задания КАК Вт_Задания
			             |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_ИН КАК Вт_ИН
			             |		ПО Вт_Задания.Задание.Предмет = Вт_ИН.ДокументИН
			             |			И Вт_Задания.Номенклатура = Вт_ИН.Номенклатура
			             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиВозвратаТовара КАК СрокиВозвратаТовара
			             |		ПО Вт_Задания.ТорговаяПлощадка.ПодразделениеКомпании = СрокиВозвратаТовара.Подразделение
			             |			И Вт_Задания.Номенклатура = СрокиВозвратаТовара.Номенклатура
			             |ГДЕ
			             |	Вт_Задания.ТорговаяПлощадка В(&Подразделения)
			             |
			             |СГРУППИРОВАТЬ ПО
			             |	Вт_Задания.ТорговаяПлощадка";
		КонецЕсли;	
		
		Запр.УстановитьПараметр("НачалоПериода",НачалоДня(ДатаНачала));
		Запр.УстановитьПараметр("КонецПериода", КонецДня(ДатаОкончания));
		Запр.УстановитьПараметр("Подразделения", МассивПодразделений);
		
		Выборка = Запр.Выполнить().Выбрать();
		Пока Выборка.Следующий() цикл
			НС = тзРез.Добавить();
			НС.КодПодразделения = Выборка.Код;
			НС.ПроцентВыполнения = Выборка.ПроцентВыполнения;
		КонецЦикла;	
		
		Результат.Status = Истина;	
	Исключение
		Результат.Description = ОписаниеОшибки();		
		Результат.Status = Ложь;
	КонецПопытки;
	
	Хранилище = Новый ХранилищеЗначения(тзРез);
	Результат.ResTable = Хранилище;
	
	
	Возврат Результат;
	
КонецФункции	

//конец гамазин




Функция ПолучитьЕжедневныйОтчетФабрикаСыра(Дата = Неопределено) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = КонецДня(ТекущаяДата() - 60*60*24);
	КонецЕсли;	
	
	МассивИсключений = Новый Массив;
	МассивИсключений.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоКоду("000000050")); //Тара и этикетка
	МассивИсключений.Добавить(Справочники.НоменклатурныеГруппы.НайтиПоКоду("000000054")); //Сахар
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("aca6e0b8-d29a-11e6-80d2-0050569272d9")));
	Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ) КАК Период,
	               |	""ПоДням"" КАК ТипПериода,
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии КАК ГруппаНоменклатуры,
	               |	СУММА(ВЫБОР
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент ЕСТЬ NULL
	               |				ТОГДА 0
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент <= 1
	               |				ТОГДА Продажи.Количество * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |			ИНАЧЕ Продажи.Количество / Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |		КОНЕЦ) КАК Вес,
	               |	СУММА(Продажи.Сумма) КАК Сумма,
	               |	СУММА(Продажи.Сумма - Продажи.СуммаНДС - (Продажи.Себестоимость - Продажи.СуммаНДСВходящий)) КАК Прибыль,
	               |	СУММА(Продажи.СуммаНДС) КАК СуммаНДС,
	               |	NULL КАК Маржа
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрНакопления.Продажи КАК Продажи
	               |ГДЕ
	               |	Продажи.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	               |	И Продажи.ПодразделениеКомпании = &ПодразделениеКомпании
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии,
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, ДЕНЬ)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, МЕСЯЦ),
	               |	""ПредМесяц"",
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии,
	               |	СУММА(ВЫБОР
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент ЕСТЬ NULL
	               |				ТОГДА 0
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент <= 1
	               |				ТОГДА Продажи.Количество * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |			ИНАЧЕ Продажи.Количество / Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |		КОНЕЦ),
	               |	СУММА(Продажи.Сумма),
	               |	СУММА(Продажи.Сумма - Продажи.СуммаНДС - (Продажи.Себестоимость - Продажи.СуммаНДСВходящий)),
	               |	СУММА(Продажи.СуммаНДС),
	               |	NULL
	               |ИЗ
	               |	РегистрНакопления.Продажи КАК Продажи
	               |ГДЕ
	               |	Продажи.Период МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&Дата, МЕСЯЦ, -1), МЕСЯЦ) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Дата, МЕСЯЦ, -1), МЕСЯЦ)
	               |	И Продажи.ПодразделениеКомпании = &ПодразделениеКомпании
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии,
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, МЕСЯЦ)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, МЕСЯЦ),
	               |	""ПредГод"",
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии,
	               |	СУММА(ВЫБОР
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент ЕСТЬ NULL
	               |				ТОГДА 0
	               |			КОГДА Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент <= 1
	               |				ТОГДА Продажи.Количество * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |			ИНАЧЕ Продажи.Количество / Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент * Продажи.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес
	               |		КОНЕЦ),
	               |	СУММА(Продажи.Сумма),
	               |	СУММА(Продажи.Сумма - Продажи.СуммаНДС - (Продажи.Себестоимость - Продажи.СуммаНДСВходящий)),
	               |	СУММА(Продажи.СуммаНДС),
	               |	NULL
	               |ИЗ
	               |	РегистрНакопления.Продажи КАК Продажи
	               |ГДЕ
	               |	Продажи.Период МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&Дата, ГОД, -1), МЕСЯЦ) И КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Дата, ГОД, -1), МЕСЯЦ)
	               |	И Продажи.ПодразделениеКомпании = &ПодразделениеКомпании
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Продажи.Номенклатура.ГруппаДляБухгалтерии,
	               |	НАЧАЛОПЕРИОДА(Продажи.Период, МЕСЯЦ)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ТК_Планирование.Период, МЕСЯЦ),
	               |	""Бюджет"",
	               |	ТК_Планирование.НоменклатураПланирования,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТК_Планирование.Показатель = ЗНАЧЕНИЕ(Перечисление.ТК_ПоказательПланирования.Продажи_Вес)
	               |				ТОГДА ТК_Планирование.Значение
	               |			ИНАЧЕ 0
	               |		КОНЕЦ),
	               |	СУММА(ВЫБОР
	               |			КОГДА ТК_Планирование.Показатель = ЗНАЧЕНИЕ(Перечисление.ТК_ПоказательПланирования.Продажи_Сумма)
	               |				ТОГДА ТК_Планирование.Значение
	               |			ИНАЧЕ 0
	               |		КОНЕЦ),
	               |	СУММА(ВЫБОР
	               |			КОГДА ТК_Планирование.Показатель = ЗНАЧЕНИЕ(Перечисление.ТК_ПоказательПланирования.Маржа_Прибыль)
	               |				ТОГДА ТК_Планирование.Значение
	               |			ИНАЧЕ 0
	               |		КОНЕЦ),
	               |	NULL,
	               |	СУММА(ВЫБОР
	               |			КОГДА ТК_Планирование.Показатель = ЗНАЧЕНИЕ(Перечисление.ТК_ПоказательПланирования.Маржа_Процент)
	               |				ТОГДА ТК_Планирование.Значение
	               |			ИНАЧЕ 0
	               |		КОНЕЦ)
	               |ИЗ
	               |	РегистрСведений.ТК_Планирование КАК ТК_Планирование
	               |ГДЕ
	               |	ТК_Планирование.Регистратор.ПодразделениеКомпании = &ПодразделениеКомпании
	               |	И ТК_Планирование.Период = НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ)
	               |	И ТК_Планирование.НоменклатураПланирования ССЫЛКА Справочник.НоменклатурныеГруппы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НАЧАЛОПЕРИОДА(ТК_Планирование.Период, МЕСЯЦ),
	               |	ТК_Планирование.НоменклатураПланирования
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПРЕДСТАВЛЕНИЕ(ВТ.ГруппаНоменклатуры) КАК ГруппаНоменклатуры,
	               |	ВТ.Период КАК Период,
	               |	ВТ.ТипПериода КАК ТипПериода,
	               |	ЕСТЬNULL(ВТ.Вес, 0) КАК Вес,
	               |	ЕСТЬNULL(ВТ.Сумма, 0) КАК Сумма,
	               |	ЕСТЬNULL(ВТ.Прибыль, 0) КАК Прибыль,
	               |	ЕСТЬNULL(ВТ.СуммаНДС, 0) КАК СуммаНДС,
	               |	ЕСТЬNULL(ВТ.Маржа, 0) КАК Маржа
	               |ИЗ
	               |	ВТ КАК ВТ
	               |ГДЕ
	               |	НЕ ВТ.ГруппаНоменклатуры В (&МассивИсключений)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивОтвета = Новый Массив;
	Пока Выборка.Следующий() Цикл 
		СтрокаОтвета = Новый Структура("Период,ГруппаНоменклатуры,ТипПериода,Вес,Сумма,СуммаНДС,Прибыль,Маржа");
		ЗаполнитьЗначенияСвойств(СтрокаОтвета, Выборка);
		
		МассивОтвета.Добавить(СтрокаОтвета);
	КонецЦикла;
	
	Возврат МассивОтвета;
	
КонецФункции

Функция ПолучитьДанныеОтчетаPowerBi(ИмяОтчета, ДопПараметры, ВсеОк = Истина, Описание = "") Экспорт 
	
	Параметры = Новый Структура;
	Параметры.Вставить("Действие", ИмяОтчета);
	
	Если ЗначениеЗаполнено(ДопПараметры) Тогда 
		Параметры.Вставить("ДопПараметры", ДопПараметры);
	КонецЕсли;
		
	КодСостояния = 200;
	ТелоОтчета = "";
	
	Результат = ТК_ОбменДаннымиСервер.ПолучитьРезультатДополнительнойОбработки("ДанныеДляВнешнихЗапросов", "ПолучитьДанные", Параметры, ВсеОк, Описание);
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		ДанныеОтчета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ДанныеОтчета", Неопределено);
		
		Если ВсеОк И ЗначениеЗаполнено(ДанныеОтчета) Тогда 
			ЗаписьJSON = Новый ЗаписьJSON;
			ЗаписьJSON.УстановитьСтроку();
			ЗаписатьJSON(ЗаписьJSON, ДанныеОтчета);
			ТелоОтчета = ЗаписьJSON.Закрыть();	
		Иначе
			Если Не ПустаяСтрока(Описание) Тогда 
				ТелоОтчета = Описание;
			КонецЕсли;
		КонецЕсли;
		
		Если Результат.Свойство("КодСостояния") Тогда 
			КодСостояния = Результат.КодСостояния;
		Иначе
			Если Не ВсеОк Тогда 
				КодСостояния = 500;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Не ВсеОк Тогда 
			КодСостояния = 500;	
		КонецЕсли;				
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ТелоОтчета) Тогда 
		ТелоОтчета = НСтр("ru = 'Данные не получены'; uk = 'Дані не отримані'");
	КонецЕсли;
	
	Возврат Новый Структура("КодСостояния, ТелоОтчета", КодСостояния, ТелоОтчета); 
	
КонецФункции
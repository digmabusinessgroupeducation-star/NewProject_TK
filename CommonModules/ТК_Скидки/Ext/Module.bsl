

//Регистр Скидки
Процедура ПодготовитьСкидки(Запрос, ДополнительныеСвойства) Экспорт
	
	
	
	ТаблицаДвижения = Новый ТаблицаЗначений;
	ТаблицаДвижения.Колонки.Добавить("Период");
	ТаблицаДвижения.Колонки.Добавить("ТорговаяПлощадка", Новый ОписаниеТипов("СправочникСсылка.ТорговыеПлощадки"));
	ТаблицаДвижения.Колонки.Добавить("Объект",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДвижения.Колонки.Добавить("НачалоДействия");
	ТаблицаДвижения.Колонки.Добавить("КонецДействия");
	ТаблицаДвижения.Колонки.Добавить("ДниНедели");
	ТаблицаДвижения.Колонки.Добавить("ЭтоПодарок");
	ТаблицаДвижения.Колонки.Добавить("ДисконтнаяКарта",Новый ОписаниеТипов("СправочникСсылка.Карточки"));
	ТаблицаДвижения.Колонки.Добавить("СуммаНакопления",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("СуммаСтроки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("СуммаЧека",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("Правило",Новый ОписаниеТипов("ПеречислениеСсылка.ПравилаРасчетаСкидкиПоКоличеству"));
	ТаблицаДвижения.Колонки.Добавить("Скидка",Новый ОписаниеТипов("СправочникСсылка.ТК_ТипыСкидок"));
	ТаблицаДвижения.Колонки.Добавить("Порядок");
	ТаблицаДвижения.Колонки.Добавить("КоэффициентНабора",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Любой)));
	
	ТаблицаДвижения.Колонки.Добавить("ЗначениеСкидки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("Действует");
	ТаблицаДвижения.Колонки.Добавить("СпособВычисления",Новый ОписаниеТипов("ПеречислениеСсылка.СкидкиСпособВычисления"));
	ТаблицаДвижения.Колонки.Добавить("ФлагВытеснения");
	
	ТаблицаДвижения.Колонки.Добавить("МаксимальнаяСуммаПодарка",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	ТаблицаДвижения.Колонки.Добавить("ПланАкционныхДневныхПродаж",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	
	ТаблицаДвижения.Колонки.Добавить("Наименование");
	ТаблицаДвижения.Колонки.Добавить("КлассификаторПродаж",Новый ОписаниеТипов("СправочникСсылка.КлассификаторПродаж"));

	
	Запрос.Текст ="ВЫБРАТЬ
	              |	ВТ_Скидки.ФлагВытеснения КАК ФлагВытеснения,
	              |	ВТ_Скидки.ЗначениеСкидки КАК ЗначениеСкидки,
	              |	ВТ_Скидки.СпособВычисления КАК СпособВычисления,
	              |	ВТ_Скидки.Номенклатура КАК Объект,
	              |	ВТ_Скидки.Количество КАК Количество,
	              |	ВТ_Скидки.КоэффициентНабора КАК КоэффициентНабора,
	              |	ВТ_Скидки.Правило КАК Правило,
	              |	ВТ_Скидки.СуммаЧека КАК СуммаЧека,
	              |	ВТ_Скидки.СуммаСтроки КАК СуммаСтроки,
	              |	ВТ_Скидки.ДисконтнаяКарта КАК ДисконтнаяКарта,
	              |	ВТ_Скидки.СуммаНакопления КАК СуммаНакопления,
	              |	ВТ_Скидки.НачалоДействия КАК НачалоДействия,
	              |	ВТ_Скидки.КонецДействия КАК КонецДействия,
	              |	ВТ_Скидки.ДниНедели КАК ДниНедели,
	              |	ВТ_Скидки.МаксимальнаяСуммаПодарка КАК МаксимальнаяСуммаПодарка,
	              |	ВТ_ТорговыеПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка,
	              |	ВТ_Скидки.НомерДокумента КАК НомерДокумента,
				  |	ВТ_Скидки.КлассификаторПродаж КАК КлассификаторПродаж,
	              |	ВТ_Скидки.Скидка КАК Скидка
	              |ИЗ
	              |	ВТ_Скидки КАК ВТ_Скидки,
	              |	ВТ_ТорговыеПлощадки КАК ВТ_ТорговыеПлощадки";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ДатаДокумента  = ДополнительныеСвойства.ДляПроведения.Дата;
	ХозОперация    = ДополнительныеСвойства.ДляПроведения.ХозОперация;
	СкидкаДействует = Ложь;
	
	Если ХозОперация = Справочники.ХозОперации.УстановкаСкидки Тогда
		СкидкаДействует = Истина;
	КонецЕсли;
	
		
	Пока Выборка.Следующий() Цикл 
		 НоваяЗапись = ТаблицаДвижения.Добавить();
		 ЗаполнитьЗначенияСвойств(НоваяЗапись,Выборка);
		 НоваяЗапись.Действует = СкидкаДействует;
		 ВремяОкончания = Формат(Окр((Дата("21200101") - Выборка.КонецДействия) / 86400, 0), "ЧГ=0");
		 Пока СтрДлина(ВремяОкончания) < 5 Цикл
			 ВремяОкончания = "0" + ВремяОкончания;	
		 КонецЦикла;
		НоваяЗапись.Порядок				= Формат(Выборка.НачалоДействия, "ДФ=ггггММдд") + ВремяОкончания + Формат(ДатаДокумента, "ДФ=ггггММддЧЧммсс") + СокрЛП(Выборка.НомерДокумента);

	КонецЦикла;
	
		
		
	ТаблицаДвижения.ЗаполнитьЗначения(ДатаДокумента,"Период");	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТК_Скидки",ТаблицаДвижения);	
	
КонецПроцедуры



// Процедура формирует движения по РС "Скидки"
// Параметры: 
// 		ДополнительныеСвойства - Структура
//   	Движения - Коллекция движений
//   	Отказ - Булево
Процедура ОтразитьСкидки(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТК_Скидки;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияТК_Скидки = Движения.ТК_Скидки;
	ДвиженияТК_Скидки.Записывать = Истина;
	//
	ДвиженияТК_Скидки.Загрузить(Таблица);
	
КонецПроцедуры // ОтразитьЦеныНоменклатуры()

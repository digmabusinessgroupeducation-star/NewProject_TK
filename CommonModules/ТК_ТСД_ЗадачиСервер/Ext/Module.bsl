


Функция ПолучитьДатуПрошлогоПериода(ВидПлана,НачалоПериода)
	ДататПрошлогоПериода = НачалоПериода;
	
	Если ВидПлана = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.Еженедельно Тогда
		ДататПрошлогоПериода =  НачалоПериода-60*60*24*7;
	ИначеЕсли  ВидПлана = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.РазВДвеНедели Тогда
		ДататПрошлогоПериода =  НачалоПериода-60*60*24*14;
	ИначеЕсли  ВидПлана = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.РазВТриНедели Тогда
		ДататПрошлогоПериода =  НачалоПериода-60*60*24*21;
	ИначеЕсли  ВидПлана = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.РазВЧетыреНедели Тогда
		ДататПрошлогоПериода =  НачалоПериода-60*60*24*28;
	ИначеЕсли  ВидПлана = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.НаДатуМесяца Тогда
	    ДататПрошлогоПериода =  ДобавитьМесяц(НачалоПериода,-1);
	КонецЕсли;
	
	Возврат ДататПрошлогоПериода;	
	
КонецФункции






Процедура ОбработатьГрафикиЗаДату(НаДату)Экспорт
	
	НачалоПериода =НачалоДня(НаДату); //НачалоДня(ТекущаяДата());
	ДниНедели = "%"+ДеньНедели(НаДату)+"%"; // "%"+ДеньНедели(НачалоПериода)+"%";
	
	
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДниНедели", ДниНедели);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаЧисло", День(НачалоПериода));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Грф.Ссылка КАК План,
	|	Грф.ДниНедели КАК ДниНедели,
	|	Грф.Периодичность КАК Периодичность,
	|	1 КАК Порядок,
	|	Грф.ВидГрафика КАК ВидГрафика,
	|	Грф.НоменклатураЭлементы КАК НоменклатураЭлементы
	|ИЗ
	|	Справочник.ТК_ГрафикиТСД КАК Грф
	|ГДЕ
	|	Грф.Активный
	|	И Грф.Периодичность = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыПериодичностиГрафикаТСД.НаДату)
	|	И Грф.Дата = &НачалоПериода
	|	И Грф.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Грф.Ссылка,
	|	Грф.ДниНедели,
	|	Грф.Периодичность,
	|	2,
	|	Грф.ВидГрафика,
	|	Грф.НоменклатураЭлементы
	|ИЗ
	|	Справочник.ТК_ГрафикиТСД КАК Грф
	|ГДЕ
	|	Грф.Активный
	|	И НЕ Грф.Периодичность = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыПериодичностиГрафикаТСД.НаДату)
	|	И НЕ Грф.Периодичность = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыПериодичностиГрафикаТСД.НаДатуМесяца)
	|	И Грф.ДниНедели ПОДОБНО &ДниНедели
	|	И Грф.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Грф.Ссылка,
	|	Грф.ДниНедели,
	|	Грф.Периодичность,
	|	3,
	|	Грф.ВидГрафика,
	|	Грф.НоменклатураЭлементы
	|ИЗ
	|	Справочник.ТК_ГрафикиТСД КАК Грф
	|ГДЕ
	|	Грф.Активный
	|	И Грф.Периодичность = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыПериодичностиГрафикаТСД.НаДатуМесяца)
	|	И Грф.ДатаЧисло = &ДатаЧисло
	|	И Грф.ПометкаУдаления = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		//Если ВыборкаДетальныеЗаписи.Порядок = 2 Тогда
			
			ДатаПрошлогоЗадания = ПолучитьДатуПрошлогоПериода(ВыборкаДетальныеЗаписи.Периодичность,НачалоПериода);
			
			ОбработатьПланГрафик(ВыборкаДетальныеЗаписи.План,ДатаПрошлогоЗадания,ВыборкаДетальныеЗаписи.ВидГрафика,ВыборкаДетальныеЗаписи.Порядок,ВыборкаДетальныеЗаписи.НоменклатураЭлементы);
		//Иначе
		//	ОбработатьПланГрафик(ВыборкаДетальныеЗаписи.План,НачалоПериода,ВыборкаДетальныеЗаписи.ВидГрафика);

		//КонецЕсли;
		
	КонецЦикла;
	
	
	
КонецПроцедуры

Процедура ОбработатьГрафикиИнвентаризаций(НаДату)Экспорт 
	
	НачалоПериода =НачалоДня(НаДату); //НачалоДня(ТекущаяДата());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_ГрафикиИнвентаризаций.Регистратор КАК Регистратор,
		|	ТК_ГрафикиИнвентаризаций.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ТК_ГрафикиИнвентаризаций.ХранилищеОтборов КАК ХранилищеОтборов
		|ИЗ
		|	РегистрСведений.ТК_ГрафикиИнвентаризаций КАК ТК_ГрафикиИнвентаризаций
		|ГДЕ
		|	ТК_ГрафикиИнвентаризаций.ДатаИнвентаризации = &ДатаИнвентаризации";
	
	Запрос.УстановитьПараметр("ДатаИнвентаризации", НачалоПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выб = РезультатЗапроса.Выбрать();
	
	Пока Выб.Следующий() Цикл
		
		ДатаЗадания = НайтиДатуПоследнегоЗадания(
			Выб.ТорговаяПлощадка,
			Справочники.Номенклатура.ПустаяСсылка(),
			Перечисления.ТК_ВидыЗадачТСД.ГрафикИнвентаризаций,
			Выб.Регистратор);
			Если ДатаЗадания < НачалоПериода Тогда
				
				НачатьТранзакцию();
				Попытка
					
					Настройки = Выб.ХранилищеОтборов.Получить();
					
					Для Каждого СтрЗадача Из Настройки Цикл 
						СформироватьЗадание(
							Выб.Регистратор,
							Выб.ТорговаяПлощадка,
							Справочники.Номенклатура.ПустаяСсылка(),
							Перечисления.ТК_ВидыЗадачТСД.ГрафикИнвентаризаций,
							СтрЗадача.ТипСклада,
							СтрЗадача.Описание,
							СтрЗадача.ХранилищеЗначения);
					
					КонецЦикла;
				 
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					Ошибка = ОписаниеОшибки();
					ВызватьИсключение (Ошибка);
				КонецПопытки;
				
				
			КонецЕсли;
	  
	КонецЦикла;
	

КонецПроцедуры

Процедура ЗакрытьЗадачиТСД()Экспорт
	
	ДатаИсполнения = ТекущаяДатаСеанса() - 60*60*24*2;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_ЗадачаТСД.Ссылка КАК Ссылка
	|ИЗ
	|	Задача.ТК_ЗадачаТСД КАК ТК_ЗадачаТСД
	|ГДЕ
	|	ТК_ЗадачаТСД.Выполнена = ЛОЖЬ
	|	И ТК_ЗадачаТСД.СрокИсполнения <= &ДатаИсполнения";
	
	Запрос.УстановитьПараметр("ДатаИсполнения", ДатаИсполнения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗадачаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ЗадачаОбъект.Выполнена = Истина;
		ЗадачаОбъект.Описание = ЗадачаОбъект.Описание + Символы.ПС + "Закрыта по сроку давности "+Формат(ТекущаяДатаСеанса(),"ДФ=yyyy-MM-dd");
		ЗадачаОбъект.Записать();
	КонецЦикла;
	
	
	
КонецПроцедуры



Функция ОпределитьНеобходимостьФормироватьЗадание(ЗаданиеНаДату,ДатаПрошлогоЗадания,ДатаПоследнегоЗадания)
	флФормировать = Ложь;
	Если ЗаданиеНаДату = 1 И ДатаПрошлогоЗадания=ДатаПоследнегоЗадания Тогда
		Возврат флФормировать;
	КонецЕсли;
	
	Если ДатаПрошлогоЗадания>=ДатаПоследнегоЗадания  Тогда
		флФормировать = Истина;
		
	КонецЕсли;  
	Возврат флФормировать;
	
КонецФункции


Процедура ОбработатьПланГрафик(ПланГрафик,ДатаПрошлогоЗадания,ТипЗадания,ЗаданиеНаДату,НоменклатураЭлементы)

	пг_ТорговыеОбъекты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПланГрафик,"ТорговыеОбъекты").Выгрузить();

	Если ТипЗадания = Перечисления.ТК_ВидыЗадачТСД.ТБД Тогда
		МассивТоваров = Справочники.ТК_ГрафикиТСД.ПолучитьМассивТоваров(ПланГрафик);	
	Иначе
		МассивТоваров = Новый Массив;
	КонецЕсли;
	
	
	Для Каждого строкаТП Из пг_ТорговыеОбъекты Цикл 
		
		СсылкаТорговаяПлощадка = строкаТП.ТорговаяПлощадка;
		Если СсылкаТорговаяПлощадка.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		МассивТП = Новый Массив;
		
		Если строкаТП.ВключаяНижестоящие Тогда
			Справочники.ТорговыеПлощадки.ПолучитьМассивИерархииТорговыхПлощадок(МассивТП,СсылкаТорговаяПлощадка);
	         
		Иначе
			МассивТП.Добавить(СсылкаТорговаяПлощадка);
		КонецЕсли;
		
		
		Если ТипЗадания = Перечисления.ТК_ВидыЗадачТСД.ТБД Тогда
			
			НоменклатураПустаяСсылка = Справочники.Номенклатура.ПустаяСсылка();

			
			Для Каждого ТорговаяПлощадкаСсылка Из МассивТП Цикл 
				ДатаПоследнегоЗадания = НайтиДатуПоследнегоЗадания(ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания,ПланГрафик);
				
				Если ОпределитьНеобходимостьФормироватьЗадание(ЗаданиеНаДату,ДатаПрошлогоЗадания,ДатаПоследнегоЗадания) Тогда
					
					Если НЕ РегистрыСведений.ТК_ТБД_Периоды.ПериодТБД(ТорговаяПлощадкаСсылка) = НачалоДня(ТекущаяДатаСеанса()) Тогда
						СсылкаСклад	           = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ТорговаяПлощадкаСсылка);
						
						ПараметрыЗадания = Новый Структура;
						ПараметрыЗадания.Вставить("Склад",СсылкаСклад);
						ПараметрыЗадания.Вставить("ТорговаяПлощадка",ТорговаяПлощадкаСсылка);

						ФоновыеЗаданияТК.ЗапуститьРасчетТБД(ПараметрыЗадания);
					КонецЕсли;
					Если РегистрыСведений.ТК_ТБД.НеобходимоФормироватьЗадание(ТорговаяПлощадкаСсылка,МассивТоваров) Тогда
						СформироватьЗадание(ПланГрафик,ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания) 
					КонецЕсли;
				КонецЕсли;  
			КонецЦикла;
		ИначеЕсли ТипЗадания = Перечисления.ТК_ВидыЗадачТСД.ПоказанияЭлектроСчетчиков Тогда
			НоменклатураПустаяСсылка = Справочники.Номенклатура.ПустаяСсылка();

			Для Каждого ТорговаяПлощадкаСсылка Из МассивТП Цикл 
				ДатаПоследнегоЗадания = НайтиДатуПоследнегоЗадания(ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания,ПланГрафик);
				Если ОпределитьНеобходимостьФормироватьЗадание(ЗаданиеНаДату,ДатаПрошлогоЗадания,ДатаПоследнегоЗадания) Тогда
					СформироватьЗадание(ПланГрафик,ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания) 
				КонецЕсли; 
			КонецЦикла;

		ИначеЕсли НоменклатураЭлементы Тогда
			НоменклатураПустаяСсылка = Справочники.Номенклатура.ПустаяСсылка();

			Для Каждого ТорговаяПлощадкаСсылка Из МассивТП Цикл 
				ДатаПоследнегоЗадания = НайтиДатуПоследнегоЗадания(ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания);
				
				Если ОпределитьНеобходимостьФормироватьЗадание(ЗаданиеНаДату,ДатаПрошлогоЗадания,ДатаПоследнегоЗадания) Тогда
					
					СформироватьЗадание(ПланГрафик,ТорговаяПлощадкаСсылка,НоменклатураПустаяСсылка,ТипЗадания) 
					
				КонецЕсли;  
			КонецЦикла;

		Иначе
			
			Для Каждого СтрокаНоменклатура  Из ПланГрафик.ТоварныеГруппы Цикл 
				
				НоменклатураСсылка = СтрокаНоменклатура.Номенклатура;
				Если НоменклатураСсылка.Пустая() Тогда
					Продолжить;
				КонецЕсли;
				
				Для Каждого ТорговаяПлощадкаСсылка Из МассивТП Цикл 
					
					ДатаПоследнегоЗадания = НайтиДатуПоследнегоЗадания(ТорговаяПлощадкаСсылка,СтрокаНоменклатура.Номенклатура,ТипЗадания);
					
					Если ЗаданиеНаДату = 1 И ДатаПрошлогоЗадания=ДатаПоследнегоЗадания Тогда
						Продолжить;
					КонецЕсли;
					
					Если  ДатаПрошлогоЗадания>=ДатаПоследнегоЗадания  Тогда
						
						СформироватьЗадание(ПланГрафик,ТорговаяПлощадкаСсылка,СтрокаНоменклатура.Номенклатура,ТипЗадания) 
						
					КонецЕсли;  
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры
  


Процедура СформироватьЗадание(График,ТорговаяПлощадка,ГруппаНоменклатуры,ВидЗадачи,ВидСклада = Неопределено,Наименование ="",Отборы = Неопределено)
	
	Если ВидСклада = Справочники.ВидыСкладов.ТНП Тогда
		СкладСсылка = Справочники.СкладыКомпании.ТНПСкладТорговойПлощадки(ТорговаяПлощадка);
	Иначе
		СкладСсылка = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ТорговаяПлощадка);
	КонецЕсли;
	Если СкладСсылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	
	СтрокаГруппаНоменклатуры = Строка(ГруппаНоменклатуры);
	
	Если  ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ВерификацияЦенников Тогда
		
		ТекстОписание = "Верифікація за групою: "+ СтрокаГруппаНоменклатуры;
		ТекстНаименование = "Верифікація: "+ СтрокаГруппаНоменклатуры;
	ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.Инвентаризация Тогда
		Если ГруппаНоменклатуры = Справочники.Номенклатура.ПустаяСсылка() Тогда
			ТекстОписание = "Інвентаризація "+Строка(График);	
			ТекстНаименование = "Інвентаризація "+Строка(График); 
		Иначе
			ТекстОписание = "Інвентаризація  за групою: "+ СтрокаГруппаНоменклатуры;
			ТекстНаименование = "Інвентаризація: "+ СтрокаГруппаНоменклатуры;
		КонецЕсли;
	ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ТБД Тогда
		ТекстОписание = "ТБД  "+Строка(График);	
		ТекстНаименование = "ТБД "+Строка(График); 
	ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ГрафикТНП Тогда
		ТекстОписание = "ТНП  за групою: "+ СтрокаГруппаНоменклатуры;	
		ТекстНаименование = "ТНП "+СтрокаГруппаНоменклатуры; 
	ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ГрафикИнвентаризаций Тогда
		ТекстНаименование = "ІН. : "+ Наименование;
		ТекстОписание     = "ІН. : "+ Наименование;
	ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ПоказанияЭлектроСчетчиков Тогда
		ТекстНаименование = "Сбор показаний электроэнергии";
		ТекстОписание = "Сбор показаний электроэнергии";

	Иначе
		Возврат
	КонецЕсли;

	
	
	ЗадачаОбъект = Задачи.ТК_ЗадачаТСД.СоздатьЗадачу();
	
	ЗадачаОбъект.Дата = ТекущаяДатаСеанса();
//	ЗадачаОбъект.Инициатор   = Пользователи.ТекущийПользователь();
	ЗадачаОбъект.ТорговаяПлощадка = ТорговаяПлощадка;
	ЗадачаОбъект.Склад            = СкладСсылка;
	ЗадачаОбъект.ГруппаНоменклатуры = ГруппаНоменклатуры;
	ЗадачаОбъект.Основание = График;
	ЗадачаОбъект.ВидЗадачи = ВидЗадачи;
	ЗадачаОбъект.Исполнитель = Пользователи.ТекущийПользователь();
	ЗадачаОбъект.СрокИсполнения  = ТекущаяДатаСеанса();
//	ЗадачаОбъект.СрокОповещения  = КонецДня(ТекущаяДатаСеанса());
	
	
	
	ЗадачаОбъект.Описание =  ТекстОписание;
	ЗадачаОбъект.Наименование = ТекстНаименование;
	
	Если ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ГрафикИнвентаризаций Тогда
		ЗадачаОбъект.ХранилищеОтборов = Отборы;
	КонецЕсли;
	
	
	
	текстОшибки = "";
	ВсеОк = Истина;
	Попытка 
		
		НачатьТранзакцию();
		
		ЗадачаОбъект.Записать();
		
	Исключение
		текстОшибки = ОписаниеОшибки();
		ВсеОк = Ложь;
	КонецПопытки;
	
	Если ВсеОк Тогда
		ЗафиксироватьТранзакцию();
		Если ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ВерификацияЦенников Тогда
			ЗаписатьДвиженияВФ(ЗадачаОбъект);
		ИначеЕсли ВидЗадачи = Перечисления.ТК_ВидыЗадачТСД.ГрафикТНП Тогда  
			ЗаписатьДвиженияТНП(ЗадачаОбъект.Ссылка);
		КонецЕсли; 
	Иначе
		ОтменитьТранзакцию();
		ВызватьИсключение(текстОшибки);
	КонецЕсли;
	
	
		
	
КонецПроцедуры

Процедура ЗаписатьДвиженияВФ(ЗадачаОбъект)
	
	ПериодВФ = НачалоДня(ТекущаяДата());
	
	Если ЗначениеЗаполнено(ЗадачаОбъект.ГруппаНоменклатуры) Тогда
		Группа = Истина;
		МассивНоменклатуры = Новый Массив;
	Иначе
		Группа = Ложь;
		МассивНоменклатуры = ЗадачаОбъект.Основание.ТоварныеЭлементы.Выгрузить(,"Номенклатура");
	КонецЕсли; 
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			,
		|			СкладКомпании = &СкладКомпании
		|				И Номенклатура В "+?(Группа,"ИЕРАРХИИ (&ГруппаНоменклатуры))"," (&МассивНоменклатуры))")+" КАК ОстаткиТоваровКомпанииОстатки";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ЗадачаОбъект.ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);
	Запрос.УстановитьПараметр("СкладКомпании", ЗадачаОбъект.Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	НаборЗаписей = РегистрыСведений.ТК_ВерификацияЦенников.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ПериодВФ);
	НаборЗаписей.Отбор.ТорговаяПлощадка.Установить(ЗадачаОбъект.ТорговаяПлощадка);

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нЗапись = НаборЗаписей.Добавить();
		нЗапись.Период = ПериодВФ;
		нЗапись.ТорговаяПлощадка = ЗадачаОбъект.ТорговаяПлощадка;
		нЗапись.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		нЗапись.Задача = ЗадачаОбъект.Ссылка;
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры 

Процедура ЗаписатьДвиженияТНП(ЗадачаОбъект)
	
	Если ЗначениеЗаполнено(ЗадачаОбъект.ГруппаНоменклатуры) Тогда
		Группа = Истина;
		МассивНоменклатуры = Новый Массив;
	Иначе
		Группа = Ложь;
		МассивНоменклатуры = ЗадачаОбъект.Основание.ТоварныеЭлементы.Выгрузить(,"Номенклатура");
	КонецЕсли; 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			,
		|			СкладКомпании = &СкладКомпании
		|				И Номенклатура В "+?(Группа,"ИЕРАРХИИ (&ГруппаНоменклатуры))"," (&МассивНоменклатуры))")+" КАК ОстаткиТоваровКомпанииОстатки";
	
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ЗадачаОбъект.ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("СкладКомпании", ЗадачаОбъект.Склад);
	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатуры);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	НаборЗаписей = РегистрыСведений.ТК_ТНП_ИтогиСканирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТорговаяПлощадка.Установить(ЗадачаОбъект.ТорговаяПлощадка);
	НаборЗаписей.Отбор.Задание.Установить(ЗадачаОбъект);

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нЗапись = НаборЗаписей.Добавить();
		нЗапись.ТорговаяПлощадка = ЗадачаОбъект.ТорговаяПлощадка;
		нЗапись.Задание 		 = ЗадачаОбъект;
		нЗапись.Номенклатура 	 = ВыборкаДетальныеЗаписи.Номенклатура;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	

КонецПроцедуры

Функция НайтиДатуПоследнегоЗадания(ТогрПлощадка,ГруппаНоменклатуры,ВидЗадачи,Основание=Неопределено)
	
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗадачиПользователяЗадачиПоИсполнителю.Дата КАК Дата
		|ИЗ
		|	Задача.ТК_ЗадачаТСД.ЗадачиПоИсполнителю(
		|			&ТорговаяПлощадка, ГруппаНоменклатуры = &ГруппаНоменклатуры И ВидЗадачи = &ВидЗадачи И ПометкаУдаления = ЛОЖЬ И Основание ССЫЛКА Справочник.ТК_ГрафикиТСД ) КАК ЗадачиПользователяЗадачиПоИсполнителю
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ";
	
	Если НЕ Основание = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"Основание ССЫЛКА Справочник.ТК_ГрафикиТСД","Основание =&Основание");
	КонецЕсли;
	
	
	
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТогрПлощадка);
	Запрос.УстановитьПараметр("ВидЗадачи", ВидЗадачи);
	Запрос.УстановитьПараметр("Основание", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат '00010101';
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий(); 	
	
	Возврат НачалоДня(ВыборкаДетальныеЗаписи.Дата);
	
КонецФункции


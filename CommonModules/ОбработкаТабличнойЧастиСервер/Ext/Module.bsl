
#Область ПрограммныйИнтерфейс

// Обработка строки табличной части - вызывается из клиентского общего модуля при необходимости выполнения на сервере.
//
// СтруктураДействий - Структура. Возможно передавать следующие поля:
//  "ПроверитьХарактеристикуПоВладельцу", Характеристика.
//  "ПроверитьЗаполнитьУпаковкуПоВладельцу"      , Упаковка.
//  "ПересчитатьКоличествоЕдиниц".
//  "ПересчитатьКоличествоЕдиницСуффикс". Суффикс
//  "ПересчитатьКоличествоУпаковок".,
//  "ПересчитатьКоличествоУпаковокСуффикс". Суффикс,
//  "ПересчитатьКоличествоУпаковокСуффиксИзОтклонения", Суффикс
//  "ПересчитатьВесОбъем",
//  "ЗаполнитьЦенуПродажи"              , СтруктураПараметровДействия.
//  "ЗаполнитьПомещение"                , СтруктураПараметровДействия.
//  "ЗаполнитьПродавца"                , СтруктураПараметровДействия.
//  "ПересчитатьСумму".
//  "ПересчитатьСуммуСУчетомРучнойСкидки"     , СтруктураПараметровДействия.
//  "ПересчитатьСуммуСУчетомАвтоматическойСкидки"     , СтруктураПараметровДействия.
//  "ПересчитатьЦенуПоСуммеВСтрокеТЧ",
//  "ПересчитатьЦенуЗаУпаковку".
//  "ПересчитатьСуммуНДС".
//  "ЗаполнитьСтавкуНДС".
//  "ОбработатьШтрихкоды".
//  "ЗаполнитьНоменклатуруПоНоменклатуреПоставщика".
//  "ЗаполнитьУсловияПродажВСтрокеТЧКлиент"
//  "ЗаполнитьУсловияЗакупокВСтрокеТЧКлиент"
//  "ЗаполнитьФлагИзлишекПорча"
//  "ЗаполнитьФлагРасхождение"
//  "ПересчитатьКоличествоУпаковокОтклонение"
//  "ОчиститьСуммуВзаиморасчетов"
//	"ЗаполнитьФлагиНедоборНеотгружаемые"
//  "ПересчитатьКоличествоНеОтгружать",
//	"ЗаполнитьПризнакАдресногоХранения", СтруктураПараметровАдресногоХранения
//
Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
	
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	// + Рецептура
	ЗаполнитьРецептуруПоНоменклатуре(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	// - Рецептура
	
	ПроверитьКорректностьЗаполнитьХарактеристикиИУпаковки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ЗаполнитьСтавкуНДСПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);
	ЗаполнитьТипЦенПоДоговору(ТекущаяСтрока, СтруктураДействий);
	
	//Заполнение договора
	  ЗаполнитьДоговорСогласованныхЦен(ТекущаяСтрока, СтруктураДействий);
	//

	ЗаполнитьЦенуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);
	ЗаполнитьРекомендованныеНаценкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);
		
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоЕдиницВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ЗаполнитьОстаткиТоваровВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуИзСуммыБезНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьЦенуСНДСВстрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьЦенуБезНалога(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуСУчетомРучнойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуБезНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуСНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВсегоОкругление(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	//Закрытие смены
	ОбработкаТабличнойЧастиКлиентСервер.ЗаполнитьПризнакВозваратаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДСиАкцизногоНалогаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	//+ Документ.ИзменениеЦен тч Товары
	ОбработкаТабличнойЧастиКлиентСервер.ЗаполнитьПроцентНаценкиИзРекомендованныхВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьНовуюЦенуИПроцентНаценки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ПроверитьНовуюЦенуПоИндикативу(ТекущаяСтрока, СтруктураДействий);
	//- Документ.ИзменениеЦен тч Товары
	
	// Документ.НазначениеСкидок 
	ПроверитьСкидкуПоИндикативу(ТекущаяСтрока, СтруктураДействий);
	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьРазностьВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

Процедура ОбработатьТЧ(ТЧ, СтруктураДействий, КэшированныеЗначения) Экспорт	
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
	
	Для Каждого СтрТабл из ТЧ Цикл
		ОбработатьСтрокуТЧ(СтрТабл, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры

Процедура СортироватьТЧ(ТЧ,ТипСортировки="")Экспорт
	
	тзТоварыИсходные = ТЧ.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", тзТоварыИсходные);
	Если ТипСортировки ="Инвентаризация" Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ *
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ &Товары КАК Товары
		|;
		|ВЫБРАТЬ
		|	Товары.*,
		|	Товары.Номенклатура.Родитель.Родитель.Родитель.Наименование КАК Родитель3,
		|	Товары.Номенклатура.Родитель.Родитель.Наименование КАК Родитель2,
		|	Товары.Номенклатура.Родитель.Наименование КАК Родитель1,
		|	Товары.Номенклатура.ТоварнаяМарка.Родитель.Наименование КАК Марка,
		|	Товары.Номенклатура.Наименование КАК НаименованиеТовара
		|ИЗ ТаблицаТовары КАК Товары";
		
		Запрос.Текст = ТекстЗапроса;
		Результат = Запрос.Выполнить();
		тзТовары = Результат.Выгрузить();
		тзТовары.Сортировать("Родитель3, Родитель2, Родитель1, Марка, НаименованиеТовара");
		
		ТЧ.Загрузить(тзТовары);
	Иначе
		ТекстЗапроса = "ВЫБРАТЬ *
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Номенклатура.ТоварнаяМарка КАК ТоварнаяМарка
		|ИЗ
		|	ТаблицаТовары КАК Товары
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура
		|УПОРЯДОЧИТЬ ПО
		|ТоварнаяМарка,
		|Номенклатура
		|ИТОГИ ПО
		|	ТоварнаяМарка ИЕРАРХИЯ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		
		Запрос.Текст = ТекстЗапроса;
	
		
		деревоГруппы = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		тзИерархия = Новый ТаблицаЗначений;
		
		ОбщегоНазначенияТК.ВыгрузитьДеревоВТЗ(тзИерархия, деревоГруппы, деревоГруппы.Колонки);
		тзТоварыИсходные.Колонки.Добавить("Порядок");
		
		Для каждого стзТовары Из тзТоварыИсходные Цикл
			стзТовары.Порядок = тзИерархия.Индекс(тзИерархия.Найти(стзТовары.Номенклатура));
		КонецЦикла;	
		
		тзТоварыИсходные.Сортировать("Порядок");
		ТЧ.Загрузить(тзТоварыИсходные);
		
	КонецЕсли;

	
КонецПроцедуры

Процедура ЗаполнитьОстаткиТабличнойЧасти(КоллекцияДанных,СтрокиЗаполнения = Неопределено,СтруктураПараметров)Экспорт
	Параметры = Неопределено;
	
	Если Не СтруктураПараметров.Свойство("ЗаполнитьОстатокТовара", Параметры)		Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОстатокСУчетомРезерва = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОстатокСУчетомРезерва", Ложь);
	БезУчетаХарактеристик = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "БезУчетаХарактеристик", Ложь);		
	
	Если СтрокиЗаполнения = Неопределено Тогда
		ПараметрКоллекция = КоллекцияДанных.Выгрузить(СтрокиЗаполнения ,"НомерСтроки,Номенклатура,ХарактеристикаНоменклатуры,Коэффициент");
	ИначеЕсли СтрокиЗаполнения.Количество() > 0 Тогда
		ПараметрКоллекция = КоллекцияДанных.Выгрузить(СтрокиЗаполнения ,"НомерСтроки,Номенклатура,ХарактеристикаНоменклатуры,Коэффициент");
	Иначе
		Возврат;
	КонецЕсли;
		
	Если ТипЗнч(СтруктураПараметров) <> Тип("Структура") Тогда 
		ВызватьИсключение НСтр("ru = 'Не заполнена структура парметров для получения остатков'; uk = 'Не заповнена структура параметрів для отримання залишків'");
	КонецЕсли;
	
	ТекущиеПараметры = Новый Структура;
	ТекущиеПараметры.Вставить("Дата", КонецДня(ТекущаяДата()));
	ТекущиеПараметры.Вставить("СкладКомпании", ПредопределенноеЗначение("Справочник.СкладыКомпании.ПустаяСсылка"));
	ТекущиеПараметры.Вставить("ИмяПоляОстатка", "Остаток");
	
	ЗаполнитьЗначенияСвойств(ТекущиеПараметры, Параметры);
			
	ШаблонЗапроса = "ВЫБРАТЬ
	                |	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки,
	                |	Таблица.Номенклатура КАК Номенклатура,
	                |	Таблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                |	Таблица.Коэффициент КАК Коэффициент
	                |ПОМЕСТИТЬ втТаблицаНоменклатуры
	                |ИЗ
	                |	&КоллекцияДанных КАК Таблица";
	
	ШаблонЗапроса = ШаблонЗапроса + Символы.ПС + ";" + Символы.ПС;
	
	Если ОстатокСУчетомРезерва Тогда 
		ШаблонЗапроса = ШаблонЗапроса + 
					"ВЫБРАТЬ
	                |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	                |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	                |ПОМЕСТИТЬ втОстаткиИРезервы
	                |ИЗ
	                |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	                |			&Дата,
	                |			СкладКомпании = &СкладКомпании
	                |				И Номенклатура В
	                |					(ВЫБРАТЬ
	                |						втТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	                |					ИЗ
	                |						втТаблицаНоменклатуры КАК втТаблицаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	РезервыТоваровОстатки.Номенклатура,
	                |	РезервыТоваровОстатки.Характеристика,
	                |	РезервыТоваровОстатки.РезервОстаток * -1
	                |ИЗ
	                |	РегистрНакопления.РезервыТоваров.Остатки(
	                |			&Дата,
	                |			Склад = &СкладКомпании
	                |				И Номенклатура В
	                |					(ВЫБРАТЬ
	                |						втТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	                |					ИЗ
	                |						втТаблицаНоменклатуры КАК втТаблицаНоменклатуры)) КАК РезервыТоваровОстатки";
	Иначе
		ШаблонЗапроса = ШаблонЗапроса + 
					"ВЫБРАТЬ
	                |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	                |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	                |ПОМЕСТИТЬ втОстаткиИРезервы
	                |ИЗ
	                |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	                |			&Дата,
	                |			СкладКомпании = &СкладКомпании
	                |				И Номенклатура В
	                |					(ВЫБРАТЬ
	                |						втТаблицаНоменклатуры.Номенклатура КАК Номенклатура
	                |					ИЗ
	                |						втТаблицаНоменклатуры КАК втТаблицаНоменклатуры)) КАК ОстаткиТоваровКомпанииОстатки";
		
	КонецЕсли;
	
	ШаблонЗапроса = ШаблонЗапроса + Символы.ПС + ";" + Символы.ПС;
	
	Если БезУчетаХарактеристик Тогда 
		ШаблонЗапроса = ШаблонЗапроса + 
					"ВЫБРАТЬ
					|	втТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
					|	втТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
					|	втТаблицаНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	втТаблицаНоменклатуры.Коэффициент КАК Коэффициент,
					|	ВЫБОР
					|		КОГДА ВложенныйЗапрос.Остаток ЕСТЬ NULL
					|			ТОГДА 0
					|		КОГДА ВложенныйЗапрос.Остаток < 0
					|			ТОГДА 0
					|		ИНАЧЕ ВложенныйЗапрос.Остаток
					|	КОНЕЦ КАК Остаток
					|ИЗ
					|	втТаблицаНоменклатуры КАК втТаблицаНоменклатуры
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			втОстаткиИРезервы.Номенклатура КАК Номенклатура,
					|			СУММА(втОстаткиИРезервы.Остаток) КАК Остаток
					|		ИЗ
					|			втОстаткиИРезервы КАК втОстаткиИРезервы
					|		
					|		СГРУППИРОВАТЬ ПО
					|			втОстаткиИРезервы.Номенклатура) КАК ВложенныйЗапрос
					|		ПО втТаблицаНоменклатуры.Номенклатура = ВложенныйЗапрос.Номенклатура
					|
					|УПОРЯДОЧИТЬ ПО
					|	НомерСтроки";
	Иначе
		ШаблонЗапроса = ШаблонЗапроса + 		
					"ВЫБРАТЬ
					|	втТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
					|	втТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
					|	втТаблицаНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	втТаблицаНоменклатуры.Коэффициент КАК Коэффициент,
					|	ВЫБОР
					|		КОГДА ВложенныйЗапрос.Остаток ЕСТЬ NULL
					|			ТОГДА 0
					|		КОГДА ВложенныйЗапрос.Остаток < 0
					|			ТОГДА 0
					|		ИНАЧЕ ВложенныйЗапрос.Остаток
					|	КОНЕЦ КАК Остаток
					|ИЗ
					|	втТаблицаНоменклатуры КАК втТаблицаНоменклатуры
					|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
					|			втОстаткиИРезервы.Номенклатура КАК Номенклатура,
					|			втОстаткиИРезервы.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|			СУММА(втОстаткиИРезервы.Остаток) КАК Остаток
					|		ИЗ
					|			втОстаткиИРезервы КАК втОстаткиИРезервы
					|		
					|		СГРУППИРОВАТЬ ПО
					|			втОстаткиИРезервы.Номенклатура,
					|			втОстаткиИРезервы.ХарактеристикаНоменклатуры) КАК ВложенныйЗапрос
					|		ПО втТаблицаНоменклатуры.Номенклатура = ВложенныйЗапрос.Номенклатура
					|			И втТаблицаНоменклатуры.ХарактеристикаНоменклатуры = ВложенныйЗапрос.ХарактеристикаНоменклатуры
					|
					|УПОРЯДОЧИТЬ ПО
					|	НомерСтроки";
					
	КонецЕсли;
	
	Запрос = Новый Запрос(ШаблонЗапроса);
	Запрос.УстановитьПараметр("КоллекцияДанных", ПараметрКоллекция);
	Запрос.УстановитьПараметр("Дата", ТекущиеПараметры.Дата);
	Запрос.УстановитьПараметр("СкладКомпании", ТекущиеПараметры.СкладКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		ЗаполнитьЗначенияСвойств(КоллекцияДанных[Выборка.НомерСтроки - 1], Выборка, ТекущиеПараметры.ИмяПоляОстатка);
	КонецЦикла;		
	
	
	//Если СтрокиЗаполнения <> Неопределено Тогда
	//	
	//	ТЗРезультат = Запрос.Выполнить().Выгрузить();
	//	Для Каждого Стр Из ТЗРезультат Цикл
	//		ЗаполнитьЗначенияСвойств(КоллекцияДанных[Стр.Номерстроки - 1], Стр, ТекущиеПараметры.ИмяПоляОстатка);
	//	КонецЦикла;
	//	
	//Иначе
	//	
	//	Выборка = Запрос.Выполнить().Выбрать();
	//	Для Н=0 По КоллекцияДанных.Количество()-1 Цикл
	//		Выборка.Следующий(); 
	//		ТекСтрока = 
	//		ЗаполнитьЗначенияСвойств(КоллекцияДанных[Н], Выборка, ТекущиеПараметры.ИмяПоляОстатка);
	//	КонецЦикла;
	//	
	//КонецЕсли;
	
	
Конецпроцедуры

#КонецОбласти



#Область ПроцедурыПересчетаИЗаполненияСервер


Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	Перем СтруктураПараметровДействия;
	СтруктураДействийЗаполнения = Новый Структура;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАртикул", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакАртикул", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьЗначениеМРЦ", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьЗначениеМРЦ", СтруктураПараметровДействия);
	КонецЕсли;

	
	Если СтруктураДействий.Свойство("ЗаполнитьКодУКТВЭД", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьКодУКТВЭД", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьВидНоменклатуры", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьВидНоменклатуры", СтруктураПараметровДействия);
	КонецЕсли;


	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия)
	  	И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
	 	
	  	СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПодакцизныйТовар", СтруктураПараметровДействия);
	КонецЕсли;
	
		
	Если СтруктураДействий.Свойство("ЗаполнитьГруппуЦенообразования",СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьГруппуЦенообразования",СтруктураПараметровДействия );
	КонецЕсли;

	Если СтруктураДействийЗаполнения.Количество() <> 0 Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтруктуре(ТекущаяСтрока, СтруктураДействийЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// + Рецептура

Процедура ЗаполнитьРецептуруПоНоменклатуре(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	Перем Подразделение, ДопПараметры;
	
	Если Не СтруктураДействий.Свойство("ПроверитьЗаполнитьРецептуру", ДопПараметры) Тогда 
		Возврат;
	КонецЕсли;
		
	Подразделение 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "ПодразделениеКомпании", Справочники.ПодразделенияКомпании.ПустаяСсылка());
	ИмяПоля 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "ИмяПоля", "Рецептура");
	Рецептура 		= ТекущаяСтрока[ИмяПоля];
	ЗаполнитьРецептуру = Не ЗначениеЗаполнено(Рецептура);
	
	Если Не ЗаполнитьРецептуру Тогда 				
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Рецептура.Проведен КАК Проведен,
		               |	Рецептура.ПодразделениеКомпании КАК ПодразделениеКомпании,
		               |	Рецептура.Продукция КАК Продукция,
		               |	Рецептура.Актуальная КАК Актуальная,
		               |	Рецептура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		               |ИЗ
		               |	Документ.Рецептура КАК Рецептура
		               |ГДЕ
		               |	Рецептура.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Рецептура);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() 
			И Выборка.Проведен 
			И Выборка.Актуальная 
			И Выборка.ПодразделениеКомпании = Подразделение
			И Выборка.Продукция = ТекущаяСтрока.Номенклатура Тогда 
				
			ТекущаяСтрока.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
			
		Иначе 
			ЗаполнитьРецептуру = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаполнитьРецептуру Тогда 
		Структура = Документы.Рецептура.ПолучитьАктуальнуюРецептуру(ТекущаяСтрока.Номенклатура, Подразделение, Ложь);
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Структура, "ЕдиницаИзмерения, Коэффициент");
		
		ТекущаяСтрока[ИмяПоля] = Структура.Рецептура;
	КонецЕсли;
	
КонецПроцедуры
	
// - Рецептура
	
Процедура ПроверитьКорректностьЗаполнитьХарактеристикиИУпаковки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	Перем Характеристика;
	Перем ЕдиницаИзмерений;
	СтруктураПараметровДействия = Неопределено;
	
	ПроверитьХарактеристикуПоВладельцу 	          = СтруктураДействий.Свойство("ПроверитьХарактеристикуПоВладельцу", Характеристика);
	ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу = СтруктураДействий.Свойство("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ЕдиницаИзмерений);
	
	Если ПроверитьХарактеристикуПоВладельцу ИЛИ  ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу Тогда
		РезультатПроверки = Справочники.Номенклатура.ХарактеристикаИУпаковкаПринадлежатВладельцу(ТекущаяСтрока.Номенклатура, Характеристика, ЕдиницаИзмерений);
		Если ПроверитьХарактеристикуПоВладельцу Тогда
			ТекущаяСтрока.ХарактеристикаНоменклатуры = РезультатПроверки.ХарактеристикаНоменклатуры;											
			ИспользованиеХарактеристик = Новый Структура("ХарактеристикиИспользуются,ЗначениеМРЦ", РезультатПроверки.ХарактеристикиИспользуются,РезультатПроверки.ЗначениеМРЦ);
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ИспользованиеХарактеристик);
		КонецЕсли;
		
		Если ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу Тогда
			ТекущаяСтрока.ЕдиницаИзмерения = РезультатПроверки.ЕдиницаИзмерения;
			ТекущаяСтрока.Коэффициент      = РезультатПроверки.Коэффициент;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// + Изменение цен

Процедура ПроверитьНовуюЦенуПоИндикативу(ТекущаяСтрока, СтруктураДействий)
	
	Если Не СтруктураДействий.Свойство("ПроверитьНовуюЦенуПоИндикативу") Тогда 
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура("Номенклатура,НоваяЦена,ГруппаЦенообразования", Неопределено, 0, Неопределено);
	ЗаполнитьЗначенияСвойств(Структура, ТекущаяСтрока);
	
	Если Структура.ГруппаЦенообразования <> Неопределено
		И (Структура.ГруппаЦенообразования = ПредопределенноеЗначение("Справочник.ГруппыЦенообразования.Спиртное")
		ИЛИ Структура.ГруппаЦенообразования = ПредопределенноеЗначение("Справочник.ГруппыЦенообразования.СпиртноеИндикатив")) Тогда 
		
		ЦенаИндикатива = ЦенообразованиеСервер.ПолучитьМинимальнуюРозничнуюЦену(Структура.Номенклатура);
		Если ЗначениеЗаполнено(ЦенаИндикатива) И ЦенаИндикатива > Структура.НоваяЦена Тогда 
			ТекущаяСтрока.НоваяЦена = ЦенаИндикатива;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// - Изменение цен

//НазначениеСкидок

Процедура ПроверитьСкидкуПоИндикативу(ТекущаяСтрока, СтруктураДействий)
	
	Если Не СтруктураДействий.Свойство("ПроверитьСкидкуПоИндикативу") Тогда 
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура("Номенклатура,ЗначениеСкидки,ГруппаЦенообразования,СпособВычисления", Неопределено, 0, Неопределено,Неопределено);
	ЗаполнитьЗначенияСвойств(Структура, ТекущаяСтрока);
	
	ЗначениеСкидки = Структура.ЗначениеСкидки;		
	Если Структура.СпособВычисления <> ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
		АктуальнаяЦена = ПолучитьЦенуТорговихПлощадок(Структура.Номенклатура, СтруктураДействий.ТорговыеПлощадки);
		
		Если АктуальнаяЦена = 0 Тогда
			ТекущаяСтрока.ЗначениеСкидки = АктуальнаяЦена;
			ОбщегоНазначения.СообщитьПользователю("Нет цены на товар "+Структура.Номенклатура+ ", размер скидки изменен на "+ТекущаяСтрока.ЗначениеСкидки);
			Возврат;	
		КонецЕсли; 
		
		Если Структура.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная Тогда
			НоваяЦена = АктуальнаяЦена-ЗначениеСкидки;
		ИначеЕсли Структура.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная Тогда  
			НоваяЦена = АктуальнаяЦена*(100-ЗначениеСкидки)/100;
		КонецЕсли; 
	Иначе
		НоваяЦена = ЗначениеСкидки;
	КонецЕсли; 
	
	Если Структура.ГруппаЦенообразования <> Неопределено
		И (Структура.ГруппаЦенообразования = ПредопределенноеЗначение("Справочник.ГруппыЦенообразования.Спиртное")
		ИЛИ Структура.ГруппаЦенообразования = ПредопределенноеЗначение("Справочник.ГруппыЦенообразования.СпиртноеИндикатив")) Тогда 
		
		ЦенаИндикатива = ЦенообразованиеСервер.ПолучитьМинимальнуюРозничнуюЦену(Структура.Номенклатура);
		Если ЗначениеЗаполнено(ЦенаИндикатива) И ЦенаИндикатива > НоваяЦена Тогда 
			Если Структура.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная Тогда
				ТекущаяСтрока.ЗначениеСкидки = АктуальнаяЦена - ЦенаИндикатива;
				ОбщегоНазначения.СообщитьПользователю("Скидка на товар "+Структура.Номенклатура+ " установлена меньше индикатива, допустимый размер скидки изменен на "+ТекущаяСтрока.ЗначениеСкидки);
			ИначеЕсли Структура.СпособВычисления = Перечисления.СкидкиСпособВычисления.Относительная Тогда  
				ТекущаяСтрока.ЗначениеСкидки = 100-Окр(ЦенаИндикатива/АктуальнаяЦена*100,2);
				ОбщегоНазначения.СообщитьПользователю("Скидка на товар "+Структура.Номенклатура+ " установлена меньше индикатива, допустимый размер скидки изменен на "+ТекущаяСтрока.ЗначениеСкидки);
			ИначеЕсли Структура.СпособВычисления = Перечисления.СкидкиСпособВычисления.ФиксированнаяЦена Тогда  	
				ТекущаяСтрока.ЗначениеСкидки = ЦенаИндикатива;
				ОбщегоНазначения.СообщитьПользователю("Скидка на товар "+Структура.Номенклатура+ " установлена меньше индикатива, цена изменена на "+ЦенаИндикатива);
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
		
	
КонецПроцедуры

//-НазначениеСкидок

#КонецОбласти


Функция ПолучитьЦенуТорговихПлощадок(Номенклатура,ТорговыеПлощадки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкладыКомпанииДоступныеЦены.НомерСтроки КАК НомерСтроки,
	|	СкладыКомпанииДоступныеЦены.ТипЦен КАК ТипЦен
	|ПОМЕСТИТЬ ТипыЦен
	|ИЗ
	|	Справочник.СкладыКомпании.ДоступныеЦены КАК СкладыКомпанииДоступныеЦены
	|ГДЕ
	|	СкладыКомпанииДоступныеЦены.Ссылка.ТорговаяПлощадка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1000,
	|	СкладыКомпании.ТипЦенРозничнойТорговли
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.ТорговаяПлощадка В(&Ссылка)
	|	И НЕ СкладыКомпании.ТипЦенРозничнойТорговли = ЗНАЧЕНИЕ(справочник.ТипыЦен.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СкладыКомпании.ТипЦенРозничнойТорговли
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СкладыКомпании.ДоступныеЦены.ТипЦен) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|ПОМЕСТИТЬ Цены
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&НаДату,
	|			Номенклатура = &Номенклатура
	|				И ТипЦен В
	|					(ВЫБРАТЬ
	|						ТипыЦен.ТипЦен КАК ТипЦен
	|					ИЗ
	|						ТипыЦен КАК ТипыЦен)) КАК ЦеныСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТипыЦен.НомерСтроки КАК НомерСтроки,
	|	ТипыЦен.ТипЦен КАК ТипЦен,
	|	Цены.Цена КАК Цена
	|ИЗ
	|	ТипыЦен КАК ТипыЦен
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Цены КАК Цены
	|		ПО ТипыЦен.ТипЦен = Цены.ТипЦен
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Цена";
	
	Запрос.УстановитьПараметр("НаДату", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Ссылка", ТорговыеПлощадки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Цена = 0;
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Цена = ВыборкаДетальныеЗаписи.Цена;
	КонецЕсли;
	
	Возврат Цена;
	
КонецФункции

Процедура ЗаполнитьСтавкуНДСПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	
	Перем Параметры;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДСПоНоменклатуре",Параметры) Тогда
		Если ТипЗнч(Параметры) = Тип("Структура") Тогда
			ВходящаяСтавка   	  = Ложь;
			ПроизводительИмпортер = Ложь;
			Организация      = Неопределено;
			ТорговаяПлощадка = Неопределено;
			
			Параметры.Свойство("Организация",Организация);
			Параметры.Свойство("ТорговаяПлощадка",ТорговаяПлощадка);
			
			Если Параметры.Свойство("ТипСтавкиНДС") И Параметры.ТипСтавкиНДС="Закупка" Тогда
				ВходящаяСтавка = Истина;
			КонецЕсли;
			
			Если ВходящаяСтавка И Параметры.Свойство("Контрагент")Тогда
				ПроизводительИмпортер = Параметры.Контрагент.ПроизводительИмпортерТабак;			
			КонецЕсли;
			
			ТекущаяСтрока.СтавкаНДС = Справочники.Номенклатура.ПолучитьСтавкуНДС(ТекущаяСтрока.Номенклатура,Организация,ТорговаяПлощадка,ПроизводительИмпортер);
	
		Иначе
			ТекущаяСтрока.СтавкаНДС = Справочники.Номенклатура.ЗначенияРеквизитовНоменклатуры(ТекущаяСтрока.Номенклатура).СтавкаНДС;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТипЦенПоДоговору(ТекущаяСтрока, СтруктураДействий)
	Перем Параметры;
	
	Если СтруктураДействий.Свойство("ЦеныИзДоговора",Параметры) Тогда				
		
		ТекущаяСтрока.ТипЦен = ЦенообразованиеСервер.ПолучитьТипЦен(Параметры.ДоговорВзаиморасчетов,ТекущаяСтрока.ГруппаЦенообразования,Параметры.ВидУчета,Параметры.Дата);	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДоговорСогласованныхЦен(ТекущаяСтрока, СтруктураДействий)
	Перем Параметры;
	
	Если СтруктураДействий.Свойство("ЗаполнитьДоговор",Параметры) Тогда				
		
		ТекущаяСтрока.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорСогласованнойЦены(Параметры.Дата,Параметры.Подразделение,Параметры.Контрагент,Параметры.ДоговорКонтрагента,ТекущаяСтрока.Номенклатура);	
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьЦенуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	
	Перем СтруктураПолейДляЗаполненияЦен;		
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен) Тогда				
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураПолейДляЗаполненияЦен) <> Тип("Структура") Тогда
		ВызватьИсключение НСтр("ru='Не заполнена структура полей для заполнения цен'; uk='Не заповнена структура полів для заповнення цін'");
	КонецЕсли;	
	
	// Набойченко !!Задача № 20347 
	Если СтруктураДействий.Свойство("ЦеныИзДоговора") И НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТипЦен) Тогда
		ТекущаяСтрока.Цена = 0;
		Возврат;
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	СтруктураРезультата = Новый Структура;
	
	ЦенаБезХарактеристики = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДействий, "ЦеныБезХарактеристики", Ложь);	
	
	Ит = 0;	
	Для Каждого ПолеЗаполнения Из СтруктураПолейДляЗаполненияЦен Цикл
		Ит = Ит + 1;
		ИмяПоля 			= ПолеЗаполнения.Ключ;
		ПараметрыЗаполнения = ПолеЗаполнения.Значение;
		ИмяРегистра 		= Неопределено;
		
		СтруктураРезультата.Вставить(ИмяПоля, 0);
		
		Если НЕ ПараметрыЗаполнения.Свойство("ИмяРегистра", ИмяРегистра) ИЛИ НЕ ЗначениеЗаполнено(ИмяРегистра) Тогда
			ТипЦен = Неопределено;
			Если НЕ ПараметрыЗаполнения.Свойство("ТипЦен", ТипЦен) ИЛИ ТипЗнч(ТипЦен) <> Тип("СправочникСсылка.ТипыЦен") Тогда
				Возврат;			
			КонецЕсли;
			ИмяРегистра = ОпределитьИмяРегистраЦенПоТипуЦен(ТипЦен);
			
		КонецЕсли;
		
		СтруктураПараметровОтбора = Неопределено;
		Если ИмяРегистра = "СогласованиеЦен" Тогда
			СтрокаПараметровОтбора = "Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, Контрагент, ДоговорВзаиморасчетов";
			//СтруктураПараметровОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, Контрагент, ДоговорВзаиморасчетов");
		ИначеЕсли ИмяРегистра = "ЦеныКонтрагентов" Тогда
			СтрокаПараметровОтбора = "Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, ТипЦен, Контрагент";
			//СтруктураПараметровОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, ТипЦен, Контрагент");
		ИначеЕсли ИмяРегистра = "Цены" Тогда
			СтрокаПараметровОтбора  = "Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, ТипЦен";
			//СтруктураПараметровОтбора = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, Дата, ГраницаИсклюая, ТипЦен");
		КонецЕсли;		
		
		Если ЦенаБезХарактеристики Тогда
			СтрокаПараметровОтбора = СтрЗаменить(СтрокаПараметровОтбора,"ХарактеристикаНоменклатуры,","УпорядочитьРезультат,");
		КонецЕсли;
		
		СтруктураПараметровОтбора = Новый Структура(СтрокаПараметровОтбора);		
		
		ЗаполнитьЗначенияСвойств(СтруктураПараметровОтбора, ТекущаяСтрока);
		ЗаполнитьЗначенияСвойств(СтруктураПараметровОтбора, ПараметрыЗаполнения);			
		
		
		Если  ИмяРегистра = "Цены" И СтруктураДействий.Свойство("ЦеныИзДоговора")Тогда
			Если ЗначениеЗаполнено(ТекущаяСтрока.ТипЦен) Тогда
				СтруктураПараметровОтбора.ТипЦен = ТекущаяСтрока.ТипЦен
			Иначе
				
				Продолжить; // Набойченко !!Задача № 20347 
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИмяРегистра = "СогласованиеЦен" Тогда	//	т.к. СрокДействия это начало дня	//	Тихий
			СтруктураПараметровОтбора.Дата = НачалоДня(СтруктураПараметровОтбора.Дата);
		КонецЕсли;

		
		ЦенообразованиеСервер.ЗаполнитьТекстЗапросаПолученияЦен(Запрос, ИмяРегистра, ИмяПоля, СтруктураПараметровОтбора, Ит);		
	КонецЦикла;	
	
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		УстановитьПривилегированныйРежим(Истина);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтруктураРезультата.Вставить(Выборка.ИмяПоля, Выборка.Цена);
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);	
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураРезультата);
КонецПроцедуры

Процедура ЗаполнитьОстаткиТоваровВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	Перем Параметры;
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьОстаток", Параметры) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры) <> Тип("Структура") Тогда 
		ВызватьИсключение НСтр("ru = 'Не заполнена структура парметров для получения остатков'; uk = 'Не заповнена структура параметрів для отримання залишків'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОстатокСУчетомРезерва = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОстатокСУчетомРезерва", Ложь);
	БезУчетаХарактеристик = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "БезУчетаХарактеристик", Ложь);
	
	ТекущиеПараметры = Новый Структура;
	ТекущиеПараметры.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	ТекущиеПараметры.Вставить("ХарактеристикаНоменклатуры", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));	
	ТекущиеПараметры.Вставить("Коэффициент", 1);
	ТекущиеПараметры.Вставить("Дата", '00010101000000');
	ТекущиеПараметры.Вставить("СкладКомпании", ПредопределенноеЗначение("Справочник.СкладыКомпании.ПустаяСсылка"));
	ТекущиеПараметры.Вставить("ИмяПоляОстатка", "Остаток");
	
	ЗаполнитьЗначенияСвойств(ТекущиеПараметры, ТекущаяСтрока);
	ЗаполнитьЗначенияСвойств(ТекущиеПараметры, Параметры);

	Если БезУчетаХарактеристик И ТекущиеПараметры.ХарактеристикаНоменклатуры <> ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка") Тогда 
		ТекущиеПараметры.Вставить("ХарактеристикаНоменклатуры", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));		
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеПараметры.Дата) <> Тип("МоментВремени") И Параметры.Свойство("Ссылка") Тогда 
		ТекущиеПараметры.Дата = Новый МоментВремени(ТекущиеПараметры.Дата, Параметры.Ссылка);
	КонецЕсли;
	
	Если ОстатокСУчетомРезерва И Не БезУчетаХарактеристик И ЗначениеЗаполнено(ТекущиеПараметры.ХарактеристикаНоменклатуры) Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ЕСТЬNULL(РезервыТоваровОстатки.РезервОстаток, 0), 0)) КАК Количество
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		               |			&Дата,
		               |			Номенклатура = &Номенклатура
		               |				И ХарактеристикаНоменклатуры = &Характеристика
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваров.Остатки(
		               |				&Дата,
		               |				Номенклатура = &Номенклатура
		               |					И Характеристика = &Характеристика
		               |					И Склад = &СкладКомпании) КАК РезервыТоваровОстатки
		               |		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура = РезервыТоваровОстатки.Номенклатура
		               |			И ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры = РезервыТоваровОстатки.Характеристика";
	
	
	ИначеЕсли ОстатокСУчетомРезерва Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	СУММА(ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток - ЕСТЬNULL(РезервыТоваровОстатки.РезервОстаток, 0), 0)) КАК Количество
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		               |			&Дата,
		               |			Номенклатура = &Номенклатура
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РезервыТоваров.Остатки(
		               |				&Дата,
		               |				Номенклатура = &Номенклатура
		               |					И Склад = &СкладКомпании) КАК РезервыТоваровОстатки
		               |		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура = РезервыТоваровОстатки.Номенклатура";
						
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Количество
		               |ИЗ
		               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		               |			&Дата,
		               |			Номенклатура = &Номенклатура
					   |				%1
		               |				И СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки";
		
		Если Не БезУчетаХарактеристик И ЗначениеЗаполнено(ТекущиеПараметры.ХарактеристикаНоменклатуры) Тогда
			ЗаменаХарактеристика = "И ХарактеристикаНоменклатуры = &Характеристика";
		Иначе
			ЗаменаХарактеристика = "";
		КонецЕсли;
		
		ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ТекстЗапроса,
							ЗаменаХарактеристика);
	
	КонецЕсли;
	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Дата", ТекущиеПараметры.Дата);
	Запрос.УстановитьПараметр("Номенклатура", ТекущиеПараметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", ТекущиеПараметры.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("СкладКомпании", ТекущиеПараметры.СкладКомпании);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() И ЗначениеЗаполнено(Результат.Количество) Тогда 
		ТекущаяСтрока[ТекущиеПараметры.ИмяПоляОстатка] = Результат.Количество;
	Иначе
		ТекущаяСтрока[ТекущиеПараметры.ИмяПоляОстатка] = 0;
	КонецЕсли;
	
КонецПроцедуры

Функция ОпределитьИмяРегистраЦенПоТипуЦен(ТипЦен)
	ИмяРегистра = "";
	
	Если ТипЦен.ТипЦенЗакупки = Истина Тогда
		ИмяРегистра = "ЦеныКонтрагентов";
	Иначе
		ИмяРегистра = "Цены";	
	КонецЕсли;
	
	Возврат ИмяРегистра;
КонецФункции

Процедура ЗаполнитьРекомендованныеНаценкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий)
	СтруктураТиповЦен = Неопределено;
	ТипЦен = Неопределено;	
	
	Если НЕ СтруктураДействий.Свойство("ЗаполнитьРекомендованныеНаценки", СтруктураТиповЦен) Тогда				
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(СтруктураТиповЦен) Тогда
		Возврат;
	Иначе
		Если НЕ СтруктураТиповЦен.Свойство("ТипЦен", ТипЦен) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураРекомендованныхНаценок = ЦенообразованиеСервер.ПолучитьСтруктуруРекомендованныхНаценок(ТекущаяСтрока.Номенклатура, ТипЦен);
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураРекомендованныхНаценок);
	
КонецПроцедуры

Функция ДанныеОбЕдиницеИзмерения(Номенклатура, ЕдиницаИзмерения, КэшированныеЗначения) Экспорт 
	
	ДанныеОбЕдиницеИзмерения = Справочники.ЕдиницыИзмерения.КоэффициентВесОбъем(ЕдиницаИзмерения, Номенклатура);
	
	
	КлючКоэффициента = ОбработкаТабличнойЧастиКлиентСервер.КлючКэшаУпаковки(Номенклатура, ЕдиницаИзмерения);; 
	
	КэшированныеЗначения.КоэффициентыУпаковок.Вставить(КлючКоэффициента,	Новый Структура("Коэффициент, Вес,НужноОкруглятьКоличество",
	ДанныеОбЕдиницеИзмерения.Коэффициент,
	ДанныеОбЕдиницеИзмерения.Вес,
	ДанныеОбЕдиницеИзмерения.НужноОкруглятьКоличество));
	
	
	Возврат ДанныеОбЕдиницеИзмерения;
	
КонецФункции

#Область ПроцедурыИФункцииЗаполненияСлужебныхРеквизитовПоНоменклатуре

// Возвращает шаблон поля выборки соответствуюий для указанного ключа действия
//
// Параметры:
// 		КлючДействия - Строка - Строка имени ключа действия
//
// Возвращаемое значение:
// 		Строка - Строка шаблоно поля запроса
//
Функция ПолучитьШаблонПоляВыборкиПоКлючуДействия(КлючДействия)
	
	//Если КлючДействия = "ЗаполнитьПризнакТипНоменклатуры" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ТипНоменклатуры КАК %Значение%";
	//КонецЕсли;
	//
	//
	//
	//Если КлючДействия = "ЗаполнитьПризнакЕдиницаИзмерения" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмерения КАК %Значение%";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьПризнакТипИзмеряемойВеличины" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмерения.ТипИзмеряемойВеличины КАК %Значение%";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьВесУпаковки" Тогда
	//	ТекстЗапроса = ",
	//	|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	//	|		ТОГДА &ТекстЗапросаВесУпаковки
	//	|	ИНАЧЕ &ТекстЗапросаВесНоменклатуры
	//	|	КОНЕЦ КАК ВесУпаковки";
	//	
	//	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	//		"&ТекстЗапросаВесУпаковки",
	//		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("втТаблицаНоменклатуры.Упаковка","втТаблицаНоменклатуры.Номенклатура", Ложь));
	//	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	//		"&ТекстЗапросаВесНоменклатуры",
	//		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("втТаблицаНоменклатуры.Номенклатура.ЕдиницаИзмерения","втТаблицаНоменклатуры.Номенклатура", Ложь));
	//	Возврат ТекстЗапроса
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьОбъемУпаковки" Тогда
	//	ТекстЗапроса = ",
	//	|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	//	|		ТОГДА &ТекстЗапросаОбъемУпаковки
	//	|	ИНАЧЕ &ТекстЗапросаОбъемНоменклатуры
	//	|	КОНЕЦ КАК ОбъемУпаковки";
	//	
	//	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	//		"&ТекстЗапросаОбъемУпаковки",
	//		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("втТаблицаНоменклатуры.Упаковка","втТаблицаНоменклатуры.Номенклатура", Ложь));
	//	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	//		"&ТекстЗапросаОбъемНоменклатуры",
	//		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("втТаблицаНоменклатуры.Номенклатура.ЕдиницаИзмерения","втТаблицаНоменклатуры.Номенклатура", Ложь));
	//	Возврат ТекстЗапроса
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьЕдиницуИзмеренияВеса" Тогда
	//	Возврат ",
	//	|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
	//	|		ТОГДА втТаблицаНоменклатуры.Упаковка.ВесЕдиницаИзмерения
	//	|	ИНАЧЕ втТаблицаНоменклатуры.Номенклатура.ВесЕдиницаИзмерения
	//	|	КОНЕЦ КАК ЕдиницаИзмеренияВеса";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьЕдиницуИзмеренияОбъема" Тогда
	//	Возврат ",
	//	|	ВЫБОР КОГДА втТаблицаНоменклатуры.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
	//	|		ТОГДА втТаблицаНоменклатуры.Упаковка.ОбъемЕдиницаИзмерения
	//	|	ИНАЧЕ втТаблицаНоменклатуры.Номенклатура.ОбъемЕдиницаИзмерения
	//	|	КОНЕЦ КАК ЕдиницаИзмеренияОбъема";
	//КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакАртикул" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.Артикул КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьЗначениеМРЦ" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ЗначениеМРЦ КАК %Значение%";
	КонецЕсли;

	//
	Если КлючДействия = "ЗаполнитьГруппуЦенообразования" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ГруппаЦенообразования КАК %Значение%";
	КонецЕсли;
	//
	Если КлючДействия = "ЗаполнитьПризнакХарактеристикиИспользуются" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ИспользованиеХарактеристик КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьКодУКТВЭД" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.КодУКТВЭД КАК %Значение%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьВидНоменклатуры" Тогда
		Возврат ",
		|	втТаблицаНоменклатуры.%Ключ%.ВидНоменклатуры КАК %Значение%";
	КонецЕсли;


	
	//Если КлючДействия = "ЗаполнитьПризнакВедетсяУчетПоГТД" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ВестиУчетПоГТД КАК %Значение%";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьПризнакЭтоУслуга" Тогда
	//	Возврат ",
	//	|	ВЫБОР КОГДА втТаблицаНоменклатуры.%Ключ%.ТипНоменклатуры НЕ В
	//	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	//	|		ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ
	//	|	КОНЕЦ КАК %Значение%";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьПризнакПодакцизныйТовар" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ПодакцизныйТовар КАК %Значение%";
	//КонецЕсли;
	//
	//Если КлючДействия = "ЗаполнитьПризнакСерииИспользуются" Тогда
	//	Возврат ",
	//	|	втТаблицаНоменклатуры.%Ключ%.ВидНоменклатуры.ИспользоватьСерии КАК %Значение%";
	//КонецЕсли;
	//
	//Если ТипЗнч(КлючДействия) = Тип("Строка") Тогда
	//	ТекстЗапроса =
	//		",
	//		|	втТаблицаНоменклатуры.%Ключ%.%ИмяПоля% КАК %Значение%";
	//	Возврат СтрЗаменить(ТекстЗапроса, "%ИмяПоля%", КлючДействия);
	//КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает текст запроса выборки по служебным реквизитам номенклатуры
//
// Парметры:
// 		СтруктураРеквизитов - Структура - Структура с именами служебыных реквизитов в качестве полей
// 		СтруктураДопДанных - Структура - Структура с дополнительными данными
//
// Возвращаемое значение:
// 		Строка - Строка с текстом запроса
//
Функция ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных) Экспорт
	
	ШаблонЗапроса = "";
	
	// Формирование шаблона запроса временной таблицы по номенклатуре
	ШаблонЗапроса = "
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки%ТекстВЫБРАТЬ%
	|ПОМЕСТИТЬ втТаблицаНоменклатуры
	|ИЗ
	|	&КоллекцияДанных КАК Таблица;";
	
	ШаблонВЫБРАТЬ = ",
	|	ВЫРАЗИТЬ(Таблица.%ИмяПоля% КАК Справочник.Номенклатура) КАК %ИмяПоля%";
	
	ТекстВЫБРАТЬ = "";
	Для Каждого Поле Из СтруктураДопДанных.СтруктураИсточников Цикл
		Если Поле.Ключ = "Упаковка" Тогда
			ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + ",
			|	ВЫРАЗИТЬ(Таблица.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка";
		ИначеЕсли Поле.Ключ = "ХарактеристикаНоменклатуры" Тогда
	       ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + ",
			|	ВЫРАЗИТЬ(Таблица.ХарактеристикаНоменклатуры КАК Справочник.ХарактеристикиНоменклатуры) КАК ХарактеристикаНоменклатуры";
		Иначе
			ТекстВЫБРАТЬ = ТекстВЫБРАТЬ + СтрЗаменить(ШаблонВЫБРАТЬ, "%ИмяПоля%", Поле.Ключ);
		КонецЕсли;
	КонецЦикла;
	ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "%ТекстВЫБРАТЬ%", ТекстВЫБРАТЬ);
	
	// Шаблон запроса основной выборки
	ШаблонЗапроса = ШаблонЗапроса + "
	|///////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки%ТекстВЫБРАТЬ%
	|ИЗ
	|	втТаблицаНоменклатуры КАК втТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	// Формирование полей запроса основной выборки
	ТекстВЫБРАТЬ = "";
	Для Каждого Действие Из СтруктураДействий Цикл
		ШаблонВЫБРАТЬ = ПолучитьШаблонПоляВыборкиПоКлючуДействия(Действие.Ключ);
		Если ШаблонВЫБРАТЬ <> Неопределено Тогда
			Для Каждого Поле Из Действие.Значение Цикл
				Если ЗначениеЗаполнено(Поле.Значение) Тогда
					ТекстВыбрать = ТекстВЫБРАТЬ + СтрЗаменить(СтрЗаменить(ШаблонВЫБРАТЬ, "%Значение%", Поле.Значение), "%Ключ%", Поле.Ключ);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрЗаменить(ШаблонЗапроса, "%ТекстВЫБРАТЬ%", ТекстВЫБРАТЬ);
	
КонецФункции // ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ

// Возвращает структуру дополнительной информации получения служебных реквизитов
//
// Параметры:
// 		СтруктураДействий - Структура - Структура с действиями по получения служебных реквизитов
//
// Возвращаемое значение:
// 		Структура
//
Функция ПолучитьСтруктуруДополнительнойИнформации(СтруктураДействий) Экспорт
	
	СтруктураИсточников = Новый Структура;
	СтрокаРеквизитовЗаполнения = "";
	СтрокаРеквизитовВыгрузки = "";
	
	Для Каждого Действие Из СтруктураДействий Цикл
		Если ЗначениеЗаполнено(Действие.Значение) Тогда
			Для Каждого Поле Из Действие.Значение Цикл
				Если Не СтруктураИсточников.Свойство(Поле.Ключ) Тогда
					СтруктураИсточников.Вставить(Поле.Ключ);
					СтрокаРеквизитовВыгрузки = СтрокаРеквизитовВыгрузки + ", " + Поле.Ключ;
				КонецЕсли;
				Если ЗначениеЗаполнено(Поле.Значение) Тогда
					СтрокаРеквизитовЗаполнения = СтрокаРеквизитовЗаполнения + ", " + Поле.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура(
		"СтруктураИсточников, РеквизитыЗаполнения, РеквизитыВыгрузки",
		СтруктураИсточников,
		Сред(СтрокаРеквизитовЗаполнения, 2), // Отрезать первый символ строки, т.к. это запятая
		СтрокаРеквизитовВыгрузки);
	
КонецФункции // ПолучитьСтруктуруДополнительнойИнформации()


#КонецОбласти

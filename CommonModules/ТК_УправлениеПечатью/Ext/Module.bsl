
#Область ПрограммныйИнтерфейс

Процедура УстановитьУсловиеВидимостиКомандыПечати(КомандаПечати) Экспорт 
	
	МассивУсловий = ЗначениеНастроекПовтИсп.ПолучитьУсловияВидимостиКомандПечати(КомандаПечати.Идентификатор, КомандаПечати.МенеджерПечати);
	
	Если МассивУсловий.Количество() > 0 Тогда 
		КомандаПечати.УсловияВидимости = МассивУсловий;
	КонецЕсли;
	
КонецПроцедуры



#Область РасчетМест

Функция ПолучитьСтруктуруМест(
			Номенклатура,
			КоличествоБазовое,
			ОсновнаяЕдиница = Неопределено,
			ЕдиницаЯщиков = Неопределено,
			ЕдиницаУпаковки = Неопределено,
			КоэффициентОсновнаяЕдиница = Неопределено,
			КоэффициентЕдиницаЯщиков = Неопределено,
			КоэффициентЕдиницаУпаковки = Неопределено) Экспорт 
			
	Если ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") ИЛИ КоличествоБазовое = 0 Тогда 			
		Возврат Новый Структура;
	КонецЕсли;
	
	СтруктураПолейЗапроса = Новый Структура;	
	
	Если ОсновнаяЕдиница = Неопределено ИЛИ КоэффициентОсновнаяЕдиница = Неопределено Тогда 
		СтруктураПолейЗапроса.Вставить("ОсновнаяЕдиница", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
		СтруктураПолейЗапроса.Вставить("КоэффициентОсновнаяЕдиница", 0);
	КонецЕсли;
		
	Если ЕдиницаЯщиков = Неопределено ИЛИ КоэффициентЕдиницаЯщиков = Неопределено Тогда 
		СтруктураПолейЗапроса.Вставить("ЕдиницаЯщиков", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
		СтруктураПолейЗапроса.Вставить("КоэффициентЕдиницаЯщиков", 0);
	КонецЕсли;
	
	Если ЕдиницаУпаковки = Неопределено ИЛИ КоэффициентЕдиницаУпаковки = Неопределено Тогда 
		СтруктураПолейЗапроса.Вставить("ЕдиницаУпаковки", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
		СтруктураПолейЗапроса.Вставить("КоэффициентЕдиницаУпаковки", 0);		
	КонецЕсли;
	
	Если СтруктураПолейЗапроса.Количество() > 0 Тогда 
		
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		                      |	Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		                      |	Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК КоэффициентОсновнаяЕдиница,
		                      |	Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаЯщиков,
		                      |	Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент КАК КоэффициентЕдиницаЯщиков,
		                      |	Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаУпаковки,
		                      |	Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КоэффициентЕдиницаУпаковки
		                      |ИЗ
		                      |	Справочник.Номенклатура КАК Номенклатура
		                      |ГДЕ
		                      |	Номенклатура.Ссылка = &Номенклатура");
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ЗаполнитьЗначенияСвойств(СтруктураПолейЗапроса, Выборка);
		КонецЕсли;		
		
		ОсновнаяЕдиница 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "ОсновнаяЕдиница", ОсновнаяЕдиница);
		КоэффициентОсновнаяЕдиница	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "КоэффициентОсновнаяЕдиница", КоэффициентОсновнаяЕдиница);
		ЕдиницаЯщиков				= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "ЕдиницаЯщиков", ЕдиницаЯщиков);
		КоэффициентЕдиницаЯщиков	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "КоэффициентЕдиницаЯщиков", КоэффициентЕдиницаЯщиков);
		ЕдиницаУпаковки				= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "ЕдиницаУпаковки", ЕдиницаУпаковки);
		КоэффициентЕдиницаУпаковки	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПолейЗапроса, "КоэффициентЕдиницаУпаковки", КоэффициентЕдиницаУпаковки);
		
	КонецЕсли;
	
	СтруктураМест = Новый Структура("Номенклатура,КоличествоБазовое",
								Номенклатура,
								КоличествоБазовое);
								
	Если ЗначениеЗаполнено(ОсновнаяЕдиница) Тогда 
		СтруктураМест.Вставить("ОсновнаяЕдиница", ОсновнаяЕдиница);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КоэффициентОсновнаяЕдиница) Тогда 
		СтруктураМест.Вставить("КоэффициентОсновнаяЕдиница", КоэффициентОсновнаяЕдиница);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕдиницаЯщиков) Тогда 
		СтруктураМест.Вставить("ЕдиницаЯщиков", ЕдиницаЯщиков);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КоэффициентЕдиницаЯщиков) Тогда 
		СтруктураМест.Вставить("КоэффициентЕдиницаЯщиков", КоэффициентЕдиницаЯщиков);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕдиницаУпаковки) Тогда 
		СтруктураМест.Вставить("ЕдиницаУпаковки", ЕдиницаУпаковки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КоэффициентЕдиницаУпаковки) Тогда 
		СтруктураМест.Вставить("КоэффициентЕдиницаУпаковки", КоэффициентЕдиницаУпаковки);
	КонецЕсли;
								
	Возврат СтруктураМест;									
			
КонецФункции
			
Функция ПолучитьСтруктуруИтоговМест() Экспорт 
	
	Возврат Новый Структура("КвоЕдЯщиковВсего, КвоЕдУпаковкиВсего, КвоОснЕдВсего", 0, 0, 0);
	
КонецФункции

Функция ПредставлениеКоличестваМест(СтруктураМест, СтруктураИтоговМест = Неопределено) Экспорт 
	
	ПредставлениеМест = "";
	
	Если ТипЗнч(СтруктураМест) <> Тип("Структура") Тогда 
		Возврат ПредставлениеМест;
	КонецЕсли;	
	
	Номенклатура 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	КвоБазЕд 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "КоличествоБазовое", 0);
	ОснЕд 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "ОсновнаяЕдиница", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
	ЕдЯщиков 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "ЕдиницаЯщиков", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
	ЕдУпаковки 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "ЕдиницаУпаковки", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка"));
	КоэффОснЕд 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "КоэффициентОсновнаяЕдиница", 0);
	КоэффЯщиков 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "КоэффициентЕдиницаЯщиков", 0);
	КоэффУпаковки 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураМест, "КоэффициентЕдиницаУпаковки", 0);
	
	Если КоэффЯщиков<>0 И ЕдЯщиков<>ЕдУпаковки И ЕдЯщиков<>ОснЕд тогда
		КвоЯщиков = Цел(КвоБазЕд / КоэффЯщиков);
		Если КвоЯщиков<>0 тогда
			ПредставлениеМест = ПредставлениеМест + КвоЯщиков+" "+ЕдЯщиков;
			КвоБазЕд = КвоБазЕд - КвоЯщиков*КоэффЯщиков;
			Если СтруктураИтоговМест <> Неопределено тогда
				СтруктураИтоговМест.КвоЕдЯщиковВсего = СтруктураИтоговМест.КвоЕдЯщиковВсего + КвоЯщиков;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если КоэффУпаковки<>0 И ЕдУпаковки<>ОснЕд тогда
		КвоУпаковки = Цел(КвоБазЕд / КоэффУпаковки);
		Если КвоУпаковки<>0 тогда
			ПредставлениеМест = ПредставлениеМест + ?(СтрДлина(ПредставлениеМест)>0, " ","") + КвоУпаковки+" "+ЕдУпаковки;
			КвоБазЕд = КвоБазЕд - КвоУпаковки*КоэффУпаковки;
			Если СтруктураИтоговМест <> Неопределено тогда
				СтруктураИтоговМест.КвоЕдУпаковкиВсего = СтруктураИтоговМест.КвоЕдУпаковкиВсего + КвоУпаковки;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если КоэффОснЕд<>0 тогда
		КвоОснЕд = Цел(КвоБазЕд / КоэффОснЕд);
		Если КвоОснЕд<>0 тогда
			ПредставлениеМест = ПредставлениеМест + ?(СтрДлина(ПредставлениеМест)>0, " ","") + КвоОснЕд+" "+ОснЕд;
			КвоБазЕд = КвоБазЕд - КвоОснЕд*КоэффОснЕд;
			Если СтруктураИтоговМест <> Неопределено тогда
				СтруктураИтоговМест.КвоОснЕдВсего = СтруктураИтоговМест.КвоОснЕдВсего + КвоОснЕд;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если КвоБазЕд<>0 Тогда 
		//ОМ.Информировать("ВНИМАНИЕ! Не удалось корректно рассчитать места товара '"+стчТовары.Номенклатура+"'!");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'ВНИМАНИЕ! Не удалось корректно рассчитать места товара %1!'; uk = 'УВАГА! Не вдалося коректно розрахувати кількість місць товару %1!'"),
				Номенклатура));		
	КонецЕсли;
	
	Возврат ПредставлениеМест;	
	
КонецФункции

Функция ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЛокализации = "") Экспорт 
	
	Представление = "";
	
	Если ТипЗнч(СтруктураИтоговМест) <> Тип("Структура") Тогда 
		Возврат Представление;
	КонецЕсли;	
	
	КвоЕдЯщиковВсего 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураИтоговМест, "КвоЕдЯщиковВсего", 0);
	КвоЕдУпаковкиВсего 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураИтоговМест, "КвоЕдУпаковкиВсего", 0);
	КвоОснЕдВсего		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураИтоговМест, "КвоОснЕдВсего", 0);

	Если ПустаяСтрока(КодЛокализации) Тогда 
		КодЛокализации = КодЛокализацииИнформационнойБазы();
	КонецЕсли;
	
	КодЛокализации = Лев(КодЛокализации, 2);	
	
	Если КвоЕдЯщиковВсего <> 0 Тогда 
		Представление = Формат(КвоЕдЯщиковВсего, "ЧДЦ=0") + " " + "ящ."; //НСтр("ru = 'ящ'; uk = 'ящ'", КодЛокализации);
	КонецЕсли;
	
	Если КвоЕдУпаковкиВсего <> 0 Тогда 
		Представление = Представление + ?(СтрДлина(Представление)>0, " ", "") + Формат(КвоЕдУпаковкиВсего, "ЧДЦ=0") + " " + "уп."; //НСтр("ru = 'уп'; uk = 'пак'", КодЛокализации);
	КонецЕсли;
	
	Если КвоОснЕдВсего <> 0 Тогда 
		Представление = Представление + ?(СтрДлина(Представление)>0, " ", "") + Формат(КвоОснЕдВсего, "ЧДЦ=0") + " " + "шт."; //НСтр("ru = 'шт'; uk = 'шт'", КодЛокализации);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#КонецОбласти




#Область ВспомогательныеПроцедурыПечати

Функция ВывестиОбластьСПроверкойВывода(ТабличныйДокумент, ВыводимыеОбласти, НомерСтрокиНачалоСтраницы, Проверка = Ложь) Экспорт 
	
	МассивОбластей = Новый Массив;	
	ВывестиМассивОбластей = ТипЗнч(ВыводимыеОбласти) = Тип("Массив");
	
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	
	ТекущаяОбласть = ТабличныйДокумент.ПолучитьОбласть(НомерСтрокиНачалоСтраницы,, ВысотаТаблицы,);
	МассивОбластей.Добавить(ТекущаяОбласть);
	  
	Если ВывестиМассивОбластей Тогда 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОбластей, ВыводимыеОбласти);
	Иначе
		МассивОбластей.Добавить(ВыводимыеОбласти);
	КонецЕсли;
	
	ПомещаетсяНаЛист = ТабличныйДокумент.ПроверитьВывод(МассивОбластей);
	
	Если Не Проверка Тогда 
		Если Не ПомещаетсяНаЛист Тогда 
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			НомерСтрокиНачалоСтраницы = ВысотаТаблицы + 1;
		КонецЕсли;
		
		Если ВывестиМассивОбластей Тогда 
			Для Каждого тОбласть Из ВыводимыеОбласти Цикл 
				ТабличныйДокумент.Вывести(тОбласть);
			КонецЦикла;
		Иначе
			ТабличныйДокумент.Вывести(ВыводимыеОбласти);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПомещаетсяНаЛист;
	
КонецФункции

#КонецОбласти



#КонецОбласти


#Область СлужебныеПроцедурыИФункции


#КонецОбласти
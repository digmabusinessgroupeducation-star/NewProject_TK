

#Область ПодготовкаИЗзаписьДвиженийДокумента


// Процедура инициализирует общие структуры, используемые при проведении документов.
// Вызывается из модуля документов при проведении.
//
Процедура ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства, РежимПроведения = Неопределено) Экспорт

	// В структуре "ДополнительныеСвойства" создаются свойства с ключами "ТаблицыДляДвижений", "ДляПроведения".

	// "ТаблицыДляДвижений" - структура, которая будет содержать таблицы значений с данными для выполнения движений.
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);

	// "ДляПроведения" - структура, содержащая свойства и реквизиты документа, необходимые для проведения.
	ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
	
	// Структура, содержащая ключ с именем "МенеджерВременныхТаблиц", в значении которого хранится менеджер временных таблиц.
	// Содержит для каждой временной таблицы ключ (имя временной таблицы) и значение (признак наличия записей во временной таблице).
	ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
	ДополнительныеСвойства.ДляПроведения.Вставить("РежимПроведения",           РежимПроведения);
	ДополнительныеСвойства.ДляПроведения.Вставить("МетаданныеДокумента",       ДокументСсылка.Метаданные());
	ДополнительныеСвойства.ДляПроведения.Вставить("Ссылка",                    ДокументСсылка);
	ДополнительныеСвойства.ДляПроведения.Вставить("Момент",                    Новый МоментВремени(ДокументСсылка.Дата,ДокументСсылка));
	ДополнительныеСвойства.ДляПроведения.Вставить("Дата",                      ДокументСсылка.Дата);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",               ДокументСсылка.ХозОперация);


КонецПроцедуры

Процедура ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства) Экспорт

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();

КонецПроцедуры


// Функция формирует массив имен регистров, по которым документ имеет движения.
// Вызывается при подготовке записей к регистрации движений.
//
Функция ПолучитьМассивИспользуемыхРегистров(Регистратор, Движения, МассивИсключаемыхРегистров = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Результат = Новый Массив;
	МаксимумТаблицВЗапросе = 256;

	СчетчикТаблиц   = 0;
	СчетчикДвижений = 0;

	ВсегоДвижений = Движения.Количество();
	ТекстЗапроса  = "";
	Для Каждого Движение Из Движения Цикл

		СчетчикДвижений = СчетчикДвижений + 1;

		ПропуститьРегистр = МассивИсключаемыхРегистров <> Неопределено
							И МассивИсключаемыхРегистров.Найти(Движение.Имя) <> Неопределено;

		Если Не ПропуститьРегистр Тогда

			Если СчетчикТаблиц > 0 Тогда

				ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";

			КонецЕсли;

			СчетчикТаблиц = СчетчикТаблиц + 1;

			ТекстЗапроса = ТекстЗапроса + 
			"
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|""" + Движение.Имя + """ КАК ИмяРегистра
			|
			|ИЗ " + Движение.ПолноеИмя() + "
			|
			|ГДЕ Регистратор = &Регистратор
			|";

		КонецЕсли;

		Если СчетчикТаблиц = МаксимумТаблицВЗапросе Или СчетчикДвижений = ВсегоДвижений Тогда

			Запрос.Текст  = ТекстЗапроса;
			ТекстЗапроса  = "";
			СчетчикТаблиц = 0;

			Если Результат.Количество() = 0 Тогда

				Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");

			Иначе

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.ИмяРегистра);
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции



// Определяет необходимость подготовить таблицу для формирования движений
//
// Параметры:
//  ИмяРегистра	- Строка - имя регистра. Например "ТоварыНаСкладах"
//  Регистры	- Строка, Структура, Неопределено - список регистров, разделенных запятой, или структура, в ключах которой - имена регистров
//													Если неопределено - то всегда возвращается ИСТИНА
// 
// Возвращаемое значение:
//   - Булево - Истина, если требуется инициализировать указанную таблицу
//
Функция ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Экспорт

	Если ЗначениеЗаполнено(Регистры) Тогда
		
		Если ТипЗнч(Регистры) = Тип("Строка") Тогда
			МассивРегистров = Новый Структура(Регистры);
		Иначе
			МассивРегистров = Регистры;
		КонецЕсли;
		
		Если НЕ МассивРегистров.Свойство(ИмяРегистра) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат Истина;

КонецФункции

// Процедура выполняет пордготовку наборов записей документа к записи движений.
// 1. Очищает наборы записей от "старых записей" (ситуация возможна только в толстом клиенте)
// 2. Взводит флаг записи у наборов, по которым документ имеет движения
// Вызывается из модуля документов при проведении.
//
Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект, ЭтоНовый = Ложь) Экспорт
	Перем ЭтоНовыйДокумент, МетаданныеДвижения;
	
	Для Каждого НаборЗаписей Из Объект.Движения Цикл

		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;

	КонецЦикла;
	
	Если НЕ Объект.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовыйДокумент) Тогда
		ЭтоНовыйДокумент = ЭтоНовый;
	КонецЕсли;
	
	Если НЕ ЭтоНовыйДокумент Тогда

		// Регистры, движения по которым формируются не из модуля менеджера документа.
		ИсключаемыеРегистры = Новый Массив;
			
		Если Объект.ДополнительныеСвойства.Свойство("ДляПроведения")
		 И Объект.ДополнительныеСвойства.ДляПроведения.Свойство("МетаданныеДокумента") Тогда
			МетаданныеДвижения = Объект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Движения;
		Иначе
			МетаданныеДвижения = Объект.Метаданные().Движения;
		КонецЕсли;
		
		МассивИменРегистров = ПолучитьМассивИспользуемыхРегистров(
			Объект.Ссылка,
			МетаданныеДвижения,
			ИсключаемыеРегистры);

		Для Каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Записывать = Истина;
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

// Процедура компонует текст запроса, выполняет запрос и выгружает результаты запроса в таблицы
//
// Параметры:
//	Запрос				- Запрос - запрос, параметры которого предварительно установлены.
//	ТекстыЗапроса		- Список значений - в списке перечислены тексты запросов и их имена.
//	Таблицы				- Структура - структура в которую будут помещены полученные таблицы для движений.
//	ДобавитьРазделитель	- Булево - Истина, если нужно добавить разделитель ";" между запросами.
//	ДобавлятьСловоТаблица	- Булево - Истина, если к имени таблицы движений нужно вначало добавить слово "Таблица"
//
Процедура ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, Таблицы, ДобавитьРазделитель = Ложь, ДобавлятьСловоТаблица = Истина, ТолькоОтмеченные=Ложь) Экспорт
	
	ТаблицыЗапроса = ОбщегоНазначенияТК.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, ДобавитьРазделитель);
	
	// Помещение результатов запроса в таблицы
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл

		ИмяТаблицы = ТекстЗапроса.Представление;

		Если Не ПустаяСтрока(ИмяТаблицы) И (Не ТолькоОтмеченные Или ТекстЗапроса.Пометка) Тогда

			Если ДобавлятьСловоТаблица Тогда
				// Таблицы для проведения должны начинаться с "Таблица"
				Если НЕ СтрНачинаетсяС(ИмяТаблицы, "Таблица") Тогда
					ИмяТаблицы = "Таблица" + ИмяТаблицы;
				КонецЕсли;
			КонецЕсли;
			
			Таблицы.Вставить(ИмяТаблицы, ТаблицыЗапроса[ТекстЗапроса.Представление]);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Проверяет наличие текста запроса для формирования указанной таблицы
//
// Параметры:
//  ИмяТаблицы		- Строка - имя таблицы
//	ТекстыЗапроса 	- Список значений - список значений, значениями которого являются блоки запроса,
//	                                  синонимами - имена таблиц в которые необходимо поместить
//	                                  результат выполнения каждого отдельного блока запроса.
// 
// Возвращаемое значение:
//   - Булево - Истина, если текст запроса есть.
//
Функция ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Экспорт

	Для каждого ТекстЗапроса Из ТекстыЗапроса Цикл
		Если НРег(ТекстЗапроса.Представление) = НРег(ИмяТаблицы) Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Ложь;

КонецФункции

// Процедура записывает движения документа. Дополнительно происходит копирование параметров
// в модули наборов записей для выполнения регистрации изменений в движениях.
// Процедура вызывается из модуля документов при проведении.
//
Процедура ЗаписатьНаборыЗаписей(Объект) Экспорт
	
	Перем РегистрыДляКонтроля, РассчитыватьИзменения, ПараметрыКонтроля;

	//// Заполним общие дополнительные свойства всех движений
	
	Для Каждого Движение Из Объект.Движения Цикл
		
		Движение.ДополнительныеСвойства.Вставить("ЭтоНовый", Объект.ДополнительныеСвойства.ЭтоНовый);
		Движение.ДополнительныеСвойства.Вставить("РежимЗаписи", Объект.ДополнительныеСвойства.РежимЗаписи);
		Движение.ДополнительныеСвойства.Вставить("ДатаРегистратора", Объект.Дата);
		Движение.ДополнительныеСвойства.Вставить("ДляПроведения",Объект.ДополнительныеСвойства.ДляПроведения); 
			
	КонецЦикла;
	
	// Регистры, для которых будут рассчитаны таблицы изменений движений.
	Если Объект.ДополнительныеСвойства.ДляПроведения.Свойство("РегистрыДляКонтроля", РегистрыДляКонтроля) Тогда
		
		// Установка флага регистрации изменений в наборе записей.
		Если НЕ Объект.ДополнительныеСвойства.Свойство("РассчитыватьИзменения", РассчитыватьИзменения) Тогда
			РассчитыватьИзменения = Истина;
		КонецЕсли;
		
		Для Каждого НаборЗаписей Из РегистрыДляКонтроля Цикл
			Если НаборЗаписей.Записывать Тогда
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", РассчитыватьИзменения);
							
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	

	//КонецЕсли;
	////
	////Для отработки случаев, когда документ перестает делать движения по регистру (при изменении вида операции). Очистим оффлайновые регистры.
	//Если Объект.ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение И Не Объект.ДополнительныеСвойства.ЭтоНовый Тогда
	//			
	//КонецЕсли;
	
	
	Объект.Движения.Записать();
	
КонецПроцедуры

// Процедура очищает и записывает пустые движения по указанным регистрам
//
// Параметры:
// 		Движения - КоллекцияДвижений - Коллекция наборов записей регистров документа
// 		Регистры - Строка - Строка с именами регистров для очистки, перечисленными через запятую
//
Процедура ОчиститьЗаписатьДвижения(Движения, Регистры) Экспорт
	
	СтруктураРегистров = Новый Структура(Регистры);
	Для Каждого КлючИЗначение Из СтруктураРегистров Цикл
		Движения[КлючИЗначение.Ключ].Очистить();
		Движения[КлючИЗначение.Ключ].Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт
	
	Если Объект.ДополнительныеСвойства.ДляПроведения.РегистрыДляКонтроля.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДанныеТаблиц    = Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	ПакетЗапросов   = Новый Запрос;
	МассивКонтролей = Новый Массив;
	ТекстЗапроса    = "";
	
	
	// Контроль отрицательных остатков по товарам
	Если ЕстьИзмененияВТаблице(ДанныеТаблиц,"ДвиженияОстаткиТоваровКомпанииИзменение") Тогда
		
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаКонтрольОстатковТоваровКомпании();
		МассивКонтролей.Добавить(Врег("ОстаткиВременнаяТаблица1"));
		МассивКонтролей.Добавить(Врег("ОстаткиТоваровКомпании"));
		ПакетЗапросов.УстановитьПараметр("ДатаКонтроля",Новый Граница(Объект.Дата,ВидГраницы.Включая));

	КонецЕсли;

	
	
		
	Если МассивКонтролей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПакетЗапросов.Текст = ТекстЗапроса;
	ПакетЗапросов.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	МассивРезультатов = ПакетЗапросов.ВыполнитьПакет();

	
	Итератор = -1;
	Для Каждого Результат Из МассивРезультатов Цикл
		
		Итератор = Итератор + 1;
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяКонтроля = МассивКонтролей[Итератор];
		Если ИмяКонтроля = Врег("ОстаткиВременнаяТаблица1") Тогда
		
		ИначеЕсли ИмяКонтроля = Врег("ОстаткиТоваровКомпании") Тогда
			
			СообщитьОбОшибкахПроведенияПоРегистрамОстаткиТоваровКомпании(Объект, Отказ, Результат);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры




#КонецОбласти

#Область ПроцедурыКонтроляДвиженийДокументовПоРегистрам

Процедура УстановитьРежимПроведения(ДокументОбъект, РежимЗаписи, РежимПроведения) Экспорт

	Если ДокументОбъект.Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		РежимПроведения = РежимПроведенияДокумента.Неоперативный;
	КонецЕсли;

КонецПроцедуры

// Функция вызывается из модулей наборов записей для проверки необходимости
// контроля изменений движений в регистре.
//
Функция РассчитыватьИзменения(ДополнительныеСвойстваНабораЗаписей) Экспорт
	Перем РассчитыватьИзменения;

	Возврат ДополнительныеСвойстваНабораЗаписей.Свойство("РассчитыватьИзменения", РассчитыватьИзменения)
		И РассчитыватьИзменения;

КонецФункции


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


// Функция проверяет наличие изменений в таблице регистра.
//
Функция ЕстьИзмененияВТаблице(СтруктураДанных, Ключ)
	Перем ЕстьИзменения;

	Возврат СтруктураДанных.Свойство(Ключ, ЕстьИзменения) И ЕстьИзменения;

КонецФункции


Функция ТекстЗапросаКонтрольОстатковТоваровКомпании()

	
	
	
	ТекстТаблицыТоваров =
	"ВЫБРАТЬ
	|	Т.СкладКомпании КАК СкладКомпании,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ ВтТоварыДляКонтроляОстатков
	|ИЗ
	|	ДвиженияОстаткиТоваровКомпанииИзменение КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СкладКомпании,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры
	|;
	|//////////////////////////////
	|";
	
	
	
	ТекстЗапроса = ТекстТаблицыТоваров	+ "ВЫБРАТЬ
	|	Набор.СкладКомпании КАК СкладКомпании,
	|	Набор.Номенклатура КАК Номенклатура,
	|	Набор.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(Набор.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.СкладКомпании КАК СкладКомпании,
	|		Т.Номенклатура КАК Номенклатура,
	|		Т.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		ВтТоварыДляКонтроляОстатков КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|					&ДатаКонтроля,
	|					(СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры) В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							Т.СкладКомпании КАК СкладКомпании,
	|							Т.Номенклатура КАК Номенклатура,
	|							Т.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|						ИЗ
	|							ВтТоварыДляКонтроляОстатков КАК Т)) КАК ОстаткиТоваровКомпанииОстатки
	|			ПО Т.СкладКомпании = ОстаткиТоваровКомпанииОстатки.СкладКомпании
	|				И Т.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
	|				И Т.ХарактеристикаНоменклатуры = ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры) КАК Набор
	|
	|СГРУППИРОВАТЬ ПО
	|	Набор.Номенклатура,
	|	Набор.СкладКомпании,
	|	Набор.ХарактеристикаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(Набор.КоличествоОстаток) < 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	СкладКомпании";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ИспользуютсяОстаткиОрганизаций(Знач Период)Экспорт
	
	ПроводитьПоОстаткамОрганизации = Ложь;
	ДатаУчетаОстатковОрганизации = ПолучитьФункциональнуюОпцию("ТК_ДатаУчетаОстатковОрганизаций");
	Если Период >= ДатаУчетаОстатковОрганизации И Не ДатаУчетаОстатковОрганизации ='00010101' Тогда
		ПроводитьПоОстаткамОрганизации = Истина;
	КонецЕсли;
	
	Возврат ПроводитьПоОстаткамОрганизации;
КонецФункции


#КонецОбласти


#Область ПроцедурыВыдачиСообщенийОбОшибкахПроведения


Процедура СообщитьОбОшибкахПроведенияПоРегистрамОстаткиТоваровКомпании(Объект, Отказ, РезультатЗапроса)

	ШаблонСообщенияВНаличииМеньшеНоля = НСтр("ru='Номенклатура %НоменклатураХарактеристика% 
                            |Недостаточно товара в наличии на складе %Склад% на %Количество% ед.'
                            |;uk='Номенклатура %НоменклатураХарактеристика% 
                            |Недостатньо товару в наявності на складі %Склад% на %Количество% од.'");

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл


		Если Выборка.КоличествоОстаток < 0 Тогда

			ШаблонСообщения = ШаблонСообщенияВНаличииМеньшеНоля;
			Количество      = - Выборка.КоличествоОстаток;


		Иначе
			
			ВызватьИсключение НСтр("ru='Неизвестная ошибка контроля остатков.';uk='Невідома помилка контролю залишків.'");

		КонецЕсли;

		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%НоменклатураХарактеристика%",
						НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.ХарактеристикаНоменклатуры));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Количество%", Строка(Количество));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Склад%",      Строка(Выборка.СкладКомпании));


		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, ,, Отказ);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти


Процедура ОтразитьПланАктивности(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПланАктивности;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ПланАктивности.Записывать = Истина;
	Движения.ПланАктивности.Загрузить(Таблица);

КонецПроцедуры

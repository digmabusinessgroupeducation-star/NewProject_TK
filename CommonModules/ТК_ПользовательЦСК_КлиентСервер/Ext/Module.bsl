


Функция Инициализация_EUTaxService() Экспорт 
	
	
	euKeyTypeAccountant = 1;
	euKeyTypeDirector   = 2;
	euKeyTypeDigitalStamp = 3;
	
	EUTaxService_Объект = Новый COMОбъект("EUTaxServiceFile.Library.1");
	EUTaxService_Объект.Initialize("UA1");
	EUTaxService_Объект.SetUIMode(Ложь);
	
	EUTaxService_Объект.ResetPrivateKey(euKeyTypeAccountant);
	EUTaxService_Объект.ResetPrivateKey(euKeyTypeDirector);
	EUTaxService_Объект.ResetPrivateKey(euKeyTypeDigitalStamp);
	
	Возврат EUTaxService_Объект;
	
КонецФункции


Функция EUTaxService_ВвестиКлючи(EUTaxService_Объект, ТипКлюча, ФайлКлюча, ПарольКлюча,ТекстСообщения="") Экспорт
	
	Попытка
		Поток = Новый COMОбъект("ADODB.Stream");
		Поток.Type = 1;
		Поток.Mode = 3;
		Поток.Open();
		Поток.LoadFromFile(ФайлКлюча);
		ДвоичныйМассив = Поток.Read(-1);
		
		Результат = EUTaxService_Объект.SetPrivateKey(ТипКлюча, ДвоичныйМассив, ПарольКлюча, Ложь);
		
	Исключение
		Результат = Ложь;
		ТекстСообщения = "Ошибка: " + ОписаниеОшибки() + Символы.ПС + EUTaxService_Объект.GetLastErrorDescription();
	КонецПопытки;
	
	Если Результат Тогда
		Если ТипКлюча = 1 Тогда
			ТекстСообщения = "Ключ бухгалтера считан успешно!";
		ИначеЕсли ТипКлюча = 2 Тогда	
			ТекстСообщения = "Ключ директора считан успешно!";
		ИначеЕсли ТипКлюча = 3 Или ТипКлюча = 4 Тогда	
			ТекстСообщения  = "Ключ печати считан успешно!";
		КонецЕсли;
	Иначе
		Если ТипКлюча = 1 Тогда
			ТекстСообщения = "Ошибка считывания персонального ключа бухгалтера: " + EUTaxService_Объект.GetLastErrorDescription();
		ИначеЕсли ТипКлюча = 3 Или ТипКлюча = 4 Тогда
			ТекстСообщения = "Ошибка считывания персонального ключа печати предприятия: " + EUTaxService_Объект.GetLastErrorDescription();
		ИначеЕсли ТипКлюча = 2 Тогда
			ТекстСообщения = "Ошибка считывания персонального ключа директора: " + EUTaxService_Объект.GetLastErrorDescription();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция ПолучитьИнформациюОКлюче(EUTaxService_Объект,ТипКлюча,ФайлКлюча,ПарольКлюча,Отказ=Ложь)Экспорт
	
	СтруктураРезультата = Новый Структура("ТипКлюча, ФайлКлюча, ПарольКлюча,Сообщение");
	Сообщение = "";
	ПолученКлюч = EUTaxService_ВвестиКлючи(EUTaxService_Объект,ТипКлюча, ФайлКлюча, ПарольКлюча,Сообщение);
	Если НЕ ПолученКлюч Тогда
		Отказ = Истина;
	КонецЕсли;
	
	СтруктураРезультата.ТипКлюча    = ТипКлюча;	
	СтруктураРезультата.ФайлКлюча   = ФайлКлюча;	
	СтруктураРезультата.ПарольКлюча = ПарольКлюча;
	СтруктураРезультата.Сообщение = Сообщение;

	
	Возврат СтруктураРезультата;

	
КонецФункции

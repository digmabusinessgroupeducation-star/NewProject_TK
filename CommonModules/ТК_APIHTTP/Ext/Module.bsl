

Функция ТаблицаЛицензийСервера(ИмяСервера,ТекстОшибки)
	
	тзЛиц = Новый ТаблицаЗначений;
	тзЛиц.Колонки.Добавить("Лицензия");
	тзЛиц.Колонки.Добавить("Количество");
	УстановитьПривилегированныйРежим(Истина);	
	Попытка
		Соединение = Новый COMОбъект("V83.COMConnector");	
		Агент = Соединение.ConnectAgent(ИмяСервера);
		Кластер = Агент.GetClusters().GetValue(0);
		Агент.Authenticate(Кластер,"","" );
		
		МассивСессий = Агент.GetSessions(Кластер).Выгрузить();	
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		Возврат тзЛиц;
	КонецПопытки;
	
	Для Каждого Сессия Из МассивСессий Цикл		
		Если Сессия.License <> Неопределено Тогда
			License =  Сессия.License.ShortPresentation;
			СтрЛиц = тзЛиц.Найти(License,"Лицензия");
			Если СтрЛиц = Неопределено Тогда
				нстрЛиц = тзЛиц.Добавить();
				нстрЛиц.Лицензия = License;
				нстрЛиц.Количество = 1;
			Иначе
				СтрЛиц.Количество = СтрЛиц.Количество +1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	         
	Возврат тзЛиц;
	
КонецФункции

Функция ПолучитьИмяСервера()
	
	Поиск1 = Найти(СтрокаСоединенияИнформационнойБазы(), "Srvr=");
	ПодстрокаПоиска = Сред(СтрокаСоединенияИнформационнойБазы(), Поиск1 + 6);
	ИмяСервера = Лев(ПодстрокаПоиска, Найти(ПодстрокаПоиска, """") - 1);
	Разделитель =  СтрНайти(ИмяСервера,":");
	Если Разделитель <> 0 Тогда
		тИмяСервера = СтрЗаменить(ИмяСервера,Сред(ИмяСервера, Разделитель),"");
		Попытка
			Порт =  Сред(ИмяСервера, Разделитель+1);
			ПортЧисло = Число(Порт);
			ИмяСервера = тИмяСервера+":"+Формат(ПортЧисло-1,"ЧЦ=10; ЧГ=");
		Исключение
			ИмяСервера = тИмяСервера;
		КонецПопытки;
	КонецЕсли;

	Возврат ИмяСервера;
	
КонецФункции



Функция ИспользуемыеЛицензии()Экспорт
	
	
	Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_consolidation/cwd2.1cws?wsdl",,,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
	WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
	Ошибки = "";
	
	ЛицензииSQLCORP = WS.License();
	КоличествоКорп = 0;
	ТабдокКорп = Новый ТекстовыйДокумент;
	Если ЛицензииSQLCORP.Status Тогда
		Если ТипЗнч(ЛицензииSQLCORP.ListLicense) = Тип("СписокXDTO") Тогда
			ТабдокКорп.ДобавитьСтроку("<p>");
			Для Каждого СтрЛицензияКорп Из  ЛицензииSQLCORP.ListLicense Цикл 
				КоличествоКорп = КоличествоКорп + Число(СтрЛицензияКорп.LicenseKol);
				ТабдокКорп.ДобавитьСтроку("Вид лицензии """+СтрЛицензияКорп.LicenseName + """ используется ("+СтрЛицензияКорп.LicenseKol+")шт.<br>");
			КонецЦикла;
			ТабдокКорп.ДобавитьСтроку("</p>");
		КонецЕсли;
	КонецЕсли;
	
		
		
	ИмяСервера      = ПолучитьИмяСервера();
	ОписаниеОшибки  = "";
	ТаблицаЛицензий = ТаблицаЛицензийСервера(ИмяСервера,ОписаниеОшибки);
	КоличествоЛицензийТК  = ТаблицаЛицензий.Итог("Количество");

	ТабДок = Новый ТекстовыйДокумент;
	ТабДок.ДобавитьСтроку("<html>");
	ТабДок.ДобавитьСтроку("<body>");
	ТабДок.ДобавитьСтроку("<b>Используемые лицензии</b><br>");
	
	ТабДок.ДобавитьСтроку("<b>Лицензии ТК - "+ Формат(КоличествоЛицензийТК,"ЧЦ=15; ЧГ=")+"</b><br>");

	ТабДок.ДобавитьСтроку("<p>");
	Для Каждого ТекущаяСтрока Из ТаблицаЛицензий Цикл 
		ТабДок.ДобавитьСтроку("Вид лицензии """+ТекущаяСтрока.Лицензия + """ используется ("+ Формат(ТекущаяСтрока.Количество, "ЧЦ=10; ЧГ=")+")шт.<br>");
	КонецЦикла;
	ТабДок.ДобавитьСтроку("</p>");
	
	ТабДок.ДобавитьСтроку("<b>Лицензии CORP - "+ Формат(КоличествоКорп, "ЧЦ=10; ЧГ=")+"</b><br>");

	ТабДок.ДобавитьСтроку(ТабдокКорп.ПолучитьТекст());
	
	ТабДок.ДобавитьСтроку("<b>ВСЕГО - "+ Формат(КоличествоКорп+КоличествоЛицензийТК, "ЧЦ=10; ЧГ=")+"</b><br>");

	
	ТабДок.ДобавитьСтроку("</body>");
	ТабДок.ДобавитьСтроку("</html>");

	Возврат ТабДок.ПолучитьТекст();
	
КонецФункции

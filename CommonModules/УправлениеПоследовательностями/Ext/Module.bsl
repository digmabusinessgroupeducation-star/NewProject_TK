

Процедура ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ) Экспорт
	
	Если Отказ ИЛИ ДополнительныеСвойства.РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Последовательность Из ПринадлежностьПоследовательностям Цикл 
		Если Последовательность.Количество() > 0 Тогда
			Последовательность.Очистить();
		КонецЕсли;
	КонецЦикла;
	
	ИзменениеПоследовательностиПартииТоваров = Истина;
	Если ДополнительныеСвойства.Свойство("ДвиженияПартииТоваровИзменение") Тогда
		 ИзменениеПоследовательностиПартииТоваров = ДополнительныеСвойства.ДвиженияПартииТоваровИзменение;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляПоследовательности.Свойство("ПартииТоваров") И ИзменениеПоследовательностиПартииТоваров Тогда
		Таблица =  ДополнительныеСвойства.ТаблицыДляПоследовательности.ПартииТоваров;
		ПринадлежностьПоследовательностям.ПартииТоваров.Загрузить(Таблица);
		
		Для Каждого ЗаписьПоследовательности Из Таблица Цикл 
			 СтрурктураОтбораСклад = Новый Структура("СкладКомпании,ПроведенВХронологическойПоследовательности",ЗаписьПоследовательности.СкладКомпании,Ложь);
			 ПроверитьЗаписатьПоследовательностьДокумента(ЗаписьПоследовательности.Период, ДополнительныеСвойства.ДляПроведения.Момент, СтрурктураОтбораСклад);
		КонецЦикла;
		
		Если ДополнительныеСвойства.Свойство("СкладКомпанииИзменен") И ДополнительныеСвойства.СкладКомпанииИзменен Тогда
			 СтрурктураОтбораСклад = Новый Структура("СкладКомпании,ПроведенВХронологическойПоследовательности",ДополнительныеСвойства.СтарыйСкладДокумента,Ложь);
			 ПроверитьЗаписатьПоследовательностьДокумента(ДополнительныеСвойства.ДляПроведения.Дата ,Новый МоментВремени(ДополнительныеСвойства.ДляПроведения.Дата),СтрурктураОтбораСклад);
		КонецЕсли;
		
		//Проверка документа перемещения склад получатель
		Если ДополнительныеСвойства.Свойство("СкладКомпанииПолучательИзменен") И ДополнительныеСвойства.СкладКомпанииПолучательИзменен Тогда
			 СтрурктураОтбораСклад = Новый Структура("СкладКомпании,ПроведенВХронологическойПоследовательности",ДополнительныеСвойства.СкладКомпанииПолучательИзменен,Ложь);
			 ПроверитьЗаписатьПоследовательностьДокумента(ДополнительныеСвойства.ДляПроведения.Дата ,Новый МоментВремени(ДополнительныеСвойства.ДляПроведения.Дата),СтрурктураОтбораСклад);
		КонецЕсли;
		                           		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляПоследовательности.Свойство("Взаиморасчеты") Тогда
		Таблица =  ДополнительныеСвойства.ТаблицыДляПоследовательности.Взаиморасчеты;
		ПринадлежностьПоследовательностям.Взаиморасчеты.Загрузить(Таблица);
		
		Для Каждого ЗаписьПоследовательности Из Таблица Цикл 

			СтрурктураОтбора = Новый Структура("ДоговорКонтрагента,ПроведенВХронологическойПоследовательности",ЗаписьПоследовательности.ДоговорКонтрагента,Ложь);
			ПроверитьЗаписатьПоследовательностьВзаиморасчетовДокумента(ЗаписьПоследовательности.Период, ДополнительныеСвойства.ДляПроведения.Момент, СтрурктураОтбора);
			
		КонецЦикла;
			
		Если ДополнительныеСвойства.Свойство("ДоговорВзаиморасчетовИзменен") И ДополнительныеСвойства.ДоговорВзаиморасчетовИзменен Тогда
			 СтрурктураОтбора = Новый Структура("ДоговорКонтрагента,ПроведенВХронологическойПоследовательности",ДополнительныеСвойства.СтарыйДоговорДокумента,Ложь);
			 ПроверитьЗаписатьПоследовательностьВзаиморасчетовДокумента(ДополнительныеСвойства.ДляПроведения.Дата, Новый МоментВремени(ДополнительныеСвойства.ДляПроведения.Дата),СтрурктураОтбора);
		КонецЕсли;
		
		Если ДополнительныеСвойства.Свойство("ТаблицаДоговоровПроверкиПоследовательности") Тогда
			Для Каждого ЗаписьДоговорПоследовательности Из ДополнительныеСвойства.ТаблицаДоговоровПроверкиПоследовательности Цикл
				СтрокаТаблицы = Таблица.Найти(ЗаписьДоговорПоследовательности.ДоговорКонтрагента,"ДоговорКонтрагента");
				Если СтрокаТаблицы = Неопределено Тогда
					СтрурктураОтбора = Новый Структура("ДоговорКонтрагента,ПроведенВХронологическойПоследовательности",ЗаписьДоговорПоследовательности.ДоговорКонтрагента,Ложь);
					ПроверитьЗаписатьПоследовательностьВзаиморасчетовДокумента(ДополнительныеСвойства.ДляПроведения.Дата, Новый МоментВремени(ДополнительныеСвойства.ДляПроведения.Дата),СтрурктураОтбора);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;

	Если ДополнительныеСвойства.ТаблицыДляПоследовательности.Свойство("ПартииОрганизаций") И ДополнительныеСвойства.ДляПроведения.Свойство("РегистрацияПоследовательности")
		И ДополнительныеСвойства.ДляПроведения.РегистрацияПоследовательности Тогда
		Таблица =  ДополнительныеСвойства.ТаблицыДляПоследовательности.ПартииОрганизаций;
		ПринадлежностьПоследовательностям.ПартииОрганизаций.Загрузить(Таблица);
		
		Для Каждого ЗаписьПоследовательности Из Таблица Цикл 
			
			СтрурктураОтбора = Новый Структура("Организация,ПроведенВХронологическойПоследовательности",ЗаписьПоследовательности.Организация,Ложь);
			ПроверитьЗаписатьПоследовательностьПартииОрганизаций(ЗаписьПоследовательности.Период, ДополнительныеСвойства.ДляПроведения.Момент, СтрурктураОтбора);
			
		КонецЦикла;
		
		Если ДополнительныеСвойства.Свойство("ОрганизацияИзменена") И ДополнительныеСвойства.ОрганизацияИзменена Тогда
			СтрурктураОтбора = Новый Структура("Организация,ПроведенВХронологическойПоследовательности",ДополнительныеСвойства.СтараяОрганизацияДокумента,Ложь);
			ПроверитьЗаписатьПоследовательностьПартииОрганизаций(ДополнительныеСвойства.ДляПроведения.Дата, Новый МоментВремени(ДополнительныеСвойства.ДляПроведения.Дата),СтрурктураОтбора);
		КонецЕсли;
		
		
	КонецЕсли;

	
КонецПроцедуры



Процедура ЗарегистрироватьВХронологическойПоследовательности(Последовательность,ДокументСсылка,Склад,МассивСкладовПолучателей)Экспорт
	
	НаборЗаписей = Последовательности[Последовательность].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	НеобходимоЗаписать = Ложь;
	
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
		Если ЗаписьНабора.СкладКомпании = Склад И Не ЗаписьНабора.ПроведенВХронологическойПоследовательности Тогда
			ЗаписьНабора.ПроведенВХронологическойПоследовательности = Истина;
			НеобходимоЗаписать = Истина;
		Иначе
			Для каждого СкладПолучателя Из МассивСкладовПолучателей Цикл
				Если ЗаписьНабора.СкладКомпании = СкладПолучателя И НЕ ЗаписьНабора.ПроведенВХронологическойПоследовательности = Ложь Тогда
					ЗаписьНабора.ПроведенВХронологическойПоследовательности = Ложь;
					НеобходимоЗаписать = Истина;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЦикла;
	
	Если НеобходимоЗаписать Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьВХронологическойПоследовательностиВзаиморасчеты(ДокументСсылка,ДоговорКонтрагента)Экспорт
	
	НаборЗаписей = Последовательности.Взаиморасчеты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	НеобходимоЗаписать = Ложь;
	
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
		Если  Не ЗаписьНабора.ПроведенВХронологическойПоследовательности Тогда
			ЗаписьНабора.ПроведенВХронологическойПоследовательности = Истина;
			НеобходимоЗаписать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НеобходимоЗаписать Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьВХронологическойПоследовательностиПартииОрганизации(ДокументСсылка,Организация)Экспорт
	
	НаборЗаписей = Последовательности.ПартииОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
	НаборЗаписей.Прочитать();
	
	НеобходимоЗаписать = Ложь;
	
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
		Если  Не ЗаписьНабора.ПроведенВХронологическойПоследовательности Тогда
			ЗаписьНабора.ПроведенВХронологическойПоследовательности = Истина;
			НеобходимоЗаписать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если НеобходимоЗаписать Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры






Процедура ПроверитьУстановитьПоследовательностьВходящегоДокумента(Последовательность,ДокументСсылка,ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПеремещениеТоваров.МоментВремени КАК МоментВремени,
		|	ПеремещениеТоваров.Ссылка КАК Ссылка,
		|	ПеремещениеТоваров.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
		|	ПеремещениеТоваров.ДокументОтгрузки.Дата КАК ДокументОтгрузкиДата,
		|	ПеремещениеТоваров.Дата КАК Дата
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
		|ГДЕ
		|	ПеремещениеТоваров.ДокументОтгрузки = &ДокументОтгрузки
		|	И ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)";
	
	Запрос.УстановитьПараметр("ДокументОтгрузки", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		//Такие документы не регистрируем 
		Если ВыборкаДетальныеЗаписи.ДокументОтгрузкиДата > ВыборкаДетальныеЗаписи.Дата Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписейПоследовательности = Последовательности.ПартииТоваров.СоздатьНаборЗаписей();
		НаборЗаписейПоследовательности.Отбор.Регистратор.Установить(ВыборкаДетальныеЗаписи.Ссылка);
		НаборЗаписейПоследовательности.Прочитать();
        НеобходимоЗаписать = Ложь;
		Для Каждого ЗаписьНабора Из  НаборЗаписейПоследовательности Цикл 
			Если ЗаписьНабора.ПроведенВХронологическойПоследовательности Тогда
				ЗаписьНабора.ПроведенВХронологическойПоследовательности = Ложь;
				НеобходимоЗаписать = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если НеобходимоЗаписать Тогда
			НаборЗаписейПоследовательности.Записать();
		КонецЕсли;
		
		СтрурктураОтбораСклад = Новый Структура("СкладКомпании,ПроведенВХронологическойПоследовательности",ВыборкаДетальныеЗаписи.СкладКомпанииПолучатель,Ложь);
		ПроверитьЗаписатьПоследовательностьДокумента(ВыборкаДетальныеЗаписи.Дата,ВыборкаДетальныеЗаписи.МоментВремени,СтрурктураОтбораСклад);
		
		
		
	КонецЦикла;
	
	
КонецПроцедуры


Процедура ПроверитьЗаписатьПоследовательностьДокумента(ДатаДокумента,НаМоментВремени,СтрурктураОтбораСклад);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПартииТоваровГраницы.Период КАК Период
	|ИЗ
	|	Последовательность.ПартииТоваров.Границы КАК ПартииТоваровГраницы
	|ГДЕ
	|	ПартииТоваровГраницы.СкладКомпании = &СкладКомпании";
	
	Запрос.УстановитьПараметр("СкладКомпании", СтрурктураОтбораСклад.СкладКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи.Следующий();
	ПериодПоследовательности = ВыборкаДетальныеЗаписи.Период;
	
	Если ПериодПоследовательности>ДатаДокумента Тогда
		Последовательности.ПартииТоваров.УстановитьГраницу(НаМоментВремени,СтрурктураОтбораСклад);
 	КонецЕсли;
	

	
КонецПроцедуры

Процедура ПроверитьЗаписатьПоследовательностьВзаиморасчетовДокумента(ДатаДокумента,НаМоментВремени,СтрурктураОтбора);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВзаиморасчетыГраницы.Период КАК Период
	|ИЗ
	|	Последовательность.Взаиморасчеты.Границы КАК ВзаиморасчетыГраницы
	|ГДЕ
	|	ВзаиморасчетыГраницы.ДоговорКонтрагента = &ДоговорКонтрагента";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", СтрурктураОтбора.ДоговорКонтрагента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи.Следующий();
	ПериодПоследовательности = ВыборкаДетальныеЗаписи.Период;
	
	Если ПериодПоследовательности>ДатаДокумента Тогда
		Последовательности.Взаиморасчеты.УстановитьГраницу(НаМоментВремени,СтрурктураОтбора);
 	КонецЕсли;
	

	
КонецПроцедуры

Процедура ПроверитьЗаписатьПоследовательностьПартииОрганизаций(ДатаДокумента,НаМоментВремени,СтрурктураОтбора);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПартииОрганизаций.Период КАК Период
	|ИЗ
	|	Последовательность.ПартииОрганизаций.Границы КАК ПартииОрганизаций
	|ГДЕ
	|	ПартииОрганизаций.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", СтрурктураОтбора.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи.Следующий();
	ПериодПоследовательности = ВыборкаДетальныеЗаписи.Период;
	
	Если ПериодПоследовательности>ДатаДокумента Тогда
		Последовательности.ПартииОрганизаций.УстановитьГраницу(НаМоментВремени,СтрурктураОтбора);
 	КонецЕсли;
	

	
КонецПроцедуры


Процедура ОтменаПроведенияРегистрацияВПоследовательностиПартииТоваров(Источник, Отказ) Экспорт
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипИсточника = ТипЗнч(Источник);
	
	МоментВремени =  Новый МоментВремени(Источник.Дата,Источник.Ссылка);
	
	
	Если Источник.ПринадлежностьПоследовательностям.Найти("ПартииТоваров")<> Неопределено Тогда
	
		СтрурктураОтбораСклад = Новый Структура("СкладКомпании,ПроведенВХронологическойПоследовательности",Источник.СкладКомпании,Ложь);
		
		Если Последовательности.ПартииТоваров.Проверить(МоментВремени,СтрурктураОтбораСклад)Тогда
			Последовательности.ПартииТоваров.УстановитьГраницу(МоментВремени,СтрурктураОтбораСклад)
		КонецЕсли;
		
		Если ТипИсточника = Тип("ДокументОбъект.ПеремещениеТоваров") И  Источник.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда 
			СтрурктураОтбораСклад.Вставить("СкладКомпании",Источник.СкладКомпанииПолучатель);
			
			Если Последовательности.ПартииТоваров.Проверить(МоментВремени,СтрурктураОтбораСклад)Тогда
				Последовательности.ПартииТоваров.УстановитьГраницу(МоментВремени,СтрурктураОтбораСклад)
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Источник.ПринадлежностьПоследовательностям.Найти("Взаиморасчеты")<> Неопределено Тогда
	
		СтрурктураОтбора = Новый Структура("ДоговорКонтрагента,ПроведенВХронологическойПоследовательности",Источник.ДоговорВзаиморасчетов,Ложь);
		
		Если Последовательности.Взаиморасчеты.Проверить(МоментВремени,СтрурктураОтбора)Тогда
			Последовательности.Взаиморасчеты.УстановитьГраницу(МоментВремени,СтрурктураОтбора)
		КонецЕсли;
		
	КонецЕсли;
	
	
	
	
	Если Источник.ПринадлежностьПоследовательностям.Найти("ПартииОрганизаций")<> Неопределено И ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Источник.Дата)Тогда
		СтрурктураОтбораОрганизация = Новый Структура("Организация,ПроведенВХронологическойПоследовательности",,Ложь);
		
		Если ТипИсточника = Тип("ДокументОбъект.ПоступлениеТоваров") И Источник.ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровТранзиты И ЗначениеЗаполнено(Источник.ДоговорВзаиморасчетов) Тогда
			СтрурктураОтбораОрганизация.Вставить("Организация",Источник.ДоговорВзаиморасчетов.Организация);
		ИначеЕсли  ТипИсточника = Тип("ДокументОбъект.ВозвратПоставщику") И Источник.ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуТранзиты И ЗначениеЗаполнено(Источник.ДоговорВзаиморасчетов) Тогда
			СтрурктураОтбораОрганизация.Вставить("Организация",Источник.ДоговорВзаиморасчетов.Организация);
		Иначе
			СтрурктураОтбораОрганизация.Вставить("Организация",Источник.Организация);
		КонецЕсли;
		
		Если Последовательности.ПартииОрганизаций.Проверить(МоментВремени,СтрурктураОтбораОрганизация)Тогда
			Последовательности.ПартииОрганизаций.УстановитьГраницу(МоментВремени,СтрурктураОтбораОрганизация)
		КонецЕсли;
	КонецЕсли;
	
	
	
КонецПроцедуры

Функция ИмяПоследовательности(ВидПоследовательности)Экспорт
	СтруктураПоследовательности = Новый Структура("стрГраница,стрОтборГраницы");
	Если ВидПоследовательности = Перечисления.ВидыПоследовательности.ПартииТоваров Тогда
		СтруктураПоследовательности.стрГраница ="ПартииТоваров";
		СтруктураПоследовательности.стрОтборГраницы ="СкладКомпании";
	ИначеЕсли ВидПоследовательности = Перечисления.ВидыПоследовательности.ПартииОрганизаций Тогда
		СтруктураПоследовательности.стрГраница = "ПартииОрганизаций";
		СтруктураПоследовательности.стрОтборГраницы ="Организация";
	ИначеЕсли ВидПоследовательности = Перечисления.ВидыПоследовательности.Взаиморасчеты Тогда
		СтруктураПоследовательности.стрГраница ="Взаиморасчеты";
		СтруктураПоследовательности.стрОтборГраницы ="ДоговорКонтрагента";
	КонецЕсли;
	
	Возврат СтруктураПоследовательности;
	
КонецФункции


Функция ПолучитьГраницуПоследовательности(ВидПоследовательности,ПарамОтбор)Экспорт
	
	СтруктураПоследовательности = ИмяПоследовательности(ВидПоследовательности);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПД.МоментВремени КАК МоментВремени
	|ИЗ
	|	Последовательность."+СтруктураПоследовательности.стрГраница+" КАК ПД
	|ГДЕ
	|	НЕ ПД.ПроведенВХронологическойПоследовательности
	|	И ПД."+СтруктураПоследовательности.стрОтборГраницы+" = &Отбор
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени";
	Запрос.УстановитьПараметр("Отбор",ПарамОтбор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат  Выборка.МоментВремени;
	Иначе
		Возврат Новый МоментВремени('00010101');
	КонецЕсли;
	
КонецФункции


Процедура ЗаполнитьИзмерениеПроведенВХронологическойПоследовательности(ВидПоследовательности,Дата,ПарамОтбор)Экспорт
	
	СтруктураПоследовательности = ИмяПоследовательности(ВидПоследовательности);
	
	Если ЗначениеЗаполнено(ПарамОтбор) Тогда
		УсловиеОтбора = " И ПД."+СтруктураПоследовательности.стрОтборГраницы+" = &ПарамОтбор";
	Иначе
		УсловиеОтбора = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПД.Регистратор КАК Регистратор
		|ИЗ
		|	Последовательность."+СтруктураПоследовательности.стрГраница+" КАК ПД
		|ГДЕ
		|	ПД.Период < &Дата
		|	И НЕ ПД.ПроведенВХронологическойПоследовательности"+ УсловиеОтбора;
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ПарамОтбор", ПарамОтбор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		НаборЗаписей = Последовательности[СтруктураПоследовательности.стрГраница].СоздатьНаборЗаписей();

		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл

			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			Для Каждого ТекущаяЗапись Из НаборЗаписей Цикл
				ТекущаяЗапись.ПроведенВХронологическойПоследовательности = Истина;
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПарамОтбор) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст ="ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПД."+СтруктураПоследовательности.стрОтборГраницы+" КАК  Реквизит
		|ИЗ
		|	Последовательность."+СтруктураПоследовательности.стрГраница+" КАК ПД
		|ГДЕ
		|	ПД.Период < &Дата";
		Запрос.УстановитьПараметр("Дата", Дата);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			МоментВремениДокумента = Новый МоментВремени(Дата);
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("ПроведенВХронологическойПоследовательности",Ложь);

			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				СтруктураОтбора.Вставить(СтруктураПоследовательности.стрОтборГраницы,Выборка.Реквизит);
				Последовательности[СтруктураПоследовательности.стрГраница].УстановитьГраницу(МоментВремениДокумента,СтруктураОтбора);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;

	
КонецПроцедуры

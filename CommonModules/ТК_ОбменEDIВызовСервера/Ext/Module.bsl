
Функция ЕстьПравоЧтенияEDI(ВыводитьСообщение = Ложь) Экспорт
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЕстьПраво = Ложь;
	
	Возврат ЕстьПраво;
		
КонецФункции

Функция ЕстьПравоОбработкиEDI(ВыводитьСообщение = Ложь) Экспорт
	
	ЕстьПраво = ТК_ОбменEDI.ЕстьПравоОбработкиEDI(ВыводитьСообщение);
		
	Возврат ЕстьПраво;
	
КонецФункции

Функция ЕстьПравоВыполненияОбмена(ВыводитьСообщение = Ложь) Экспорт
	
	ЕстьПраво = ТК_ОбменEDI.ЕстьПравоВыполненияОбмена(ВыводитьСообщение);
		
	Возврат ЕстьПраво;
	
КонецФункции


Функция НастройкаEDIСуществует(СсылкаНаВладельца, ПараметрыДокумента = Неопределено)Экспорт
	
	Результат =  ТК_ОбменEDI. НастройкаEDIСуществует(СсылкаНаВладельца, ПараметрыДокумента);
	
	Возврат Результат;
	
КонецФункции


Функция СостояниеВерсииEDI(СсылкаНаВладельца)
	
	ВозвращаемоеЗначение = Перечисления.СтатусыДокументовEDI.ПустаяСсылка();
	СтруктураВерсии = ТК_ОбменEDI.ДанныеСостоянияДокументаEDI(СсылкаНаВладельца);
	
	Если СтруктураВерсии.Свойство("СостояниеВерсии") Тогда
		
		Комментарий = Неопределено;
		ДобавитьПричинуЗакрытия = (СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.ЗакрытПринудительно);
		
		Если СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.ОбменЗавершен Тогда
			СостояниеВерсии = НСтр("ru='EDI завершен'");
		ИначеЕсли СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.ОбменЗавершенСИсправлением Тогда
			СостояниеВерсии = НСтр("ru='EDI завершен с исправлением'");
		ИначеЕсли СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.Аннулирован Тогда
			СостояниеВерсии = НСтр("ru='EDI аннулирован'");
		ИначеЕсли СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.ЗакрытПринудительно Тогда
			СостояниеВерсии = НСтр("ru='EDI закрыт принудительно'");
		ИначеЕсли СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.Отклонен Тогда
			СостояниеВерсии = НСтр("ru='EDI закрыт с отклонением'");
		ИначеЕсли СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.НеСформирован 
			Или СтруктураВерсии.СостояниеВерсии = Перечисления.СтатусыДокументовEDI.НеПолучен Тогда
			СостояниеВерсии = НСтр("ru='EDI не начат'");
		Иначе
			СостояниеВерсии = Строка(СтруктураВерсии.СостояниеВерсии);
		КонецЕсли;
		
		Если ДобавитьПричинуЗакрытия Тогда
			СтруктураВерсии.Свойство("КомментарийРС", Комментарий);
			Причина = СтрЗаменить(НСтр("ru = ', причина: %1'"), "%1", ?(ЗначениеЗаполнено(Комментарий), Комментарий, НСтр("ru ='не указана'")));
			ВозвращаемоеЗначение = СостояниеВерсии + Причина;
		Иначе
			ВозвращаемоеЗначение = СостояниеВерсии;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции



Функция ТекстСостоянияEDI(СсылкаНаВладельца, Гиперссылка) Экспорт
	
	Результат = "";
	Если ЗначениеЗаполнено(СсылкаНаВладельца) Тогда
		
		ТекущееСостояниеEDI = СостояниеВерсииEDI(СсылкаНаВладельца);
		Результат = Строка(ТекущееСостояниеEDI);
		
		ПараметрыEDI = Неопределено;
		Если НастройкаEDIСуществует(СсылкаНаВладельца, ПараметрыEDI) Тогда
			Если Не ЕстьПравоЧтенияEDI() Тогда
				ШаблонСостояния = НСтр("ru = '%1 «Недостаточно прав для чтения EDI»'");
				Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСостояния, Результат);
				Гиперссылка = Ложь;
			Иначе
				Гиперссылка = Истина;
				Если Не ЗначениеЗаполнено(Результат) Тогда
					Гиперссылка = Ложь;
					Результат = НСтр("ru = 'EDI не начат'");
				КонецЕсли;
				
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Результат) Тогда
				ШаблонСостоянияEDI = НСтр("ru = '%1 (настройка EDI не подключена)'");
				Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСостоянияEDI, Результат);
				Гиперссылка = Истина;
			Иначе
				Результат = НСтр("ru = 'Контрагент не подключен к EDI'");
				Гиперссылка = Ложь;
			КонецЕсли;
					
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции



Процедура РазместитьНаФормеКомандыEDI(Форма, МестоРазмещенияКомандПоУмолчанию, МодульПодсистемы, НаправлениеEDI = Неопределено, ТолькоВМенюЕще = Ложь) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	КомандыEDI = ТК_ОбменEDI_ПовтИсп.КомандыEDIФормы(ИмяФормы, МодульПодсистемы, НаправлениеEDI, ТолькоВМенюЕще).Скопировать();
	
	Если МестоРазмещенияКомандПоУмолчанию <> Неопределено Тогда
		Для Каждого КомандаEDI Из КомандыEDI Цикл
			Если ПустаяСтрока(КомандаEDI.МестоРазмещения) Тогда
				КомандаEDI.МестоРазмещения = МестоРазмещенияКомандПоУмолчанию.Имя;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	КомандыEDI.Колонки.Добавить("ИмяКомандыНаФорме", Новый ОписаниеТипов("Строка"));
	
	ТаблицаКоманд = КомандыEDI.Скопировать(,"МестоРазмещения");
	ТаблицаКоманд.Свернуть("МестоРазмещения");
	МестаРазмещения = ТаблицаКоманд.ВыгрузитьКолонку("МестоРазмещения");
	
	Если МестоРазмещенияКомандПоУмолчанию = Неопределено Тогда
		МестоРазмещенияКоманд = Форма.КоманднаяПанель;
		ПодменюEDI = Форма.Элементы.Добавить(МестоРазмещенияКоманд.Имя + "КомандыEDI", Тип("ГруппаФормы"), МестоРазмещенияКоманд);
		ПодменюEDI.Вид = ВидГруппыФормы.Подменю;
		ПодменюEDI.Заголовок = НСтр("ru = 'EDI'");
		МестоРазмещенияКомандПоУмолчанию = ПодменюEDI;
	КонецЕсли;
	
	Если МодульПодсистемы = "ТК_ОбменEDI" Тогда
		КартинкаОповещений = БиблиотекаКартинок["ЭмблемаСервисаEDI"];
		//МодульОбменEDI = ОбщегоНазначения.ОбщийМодуль("ТК_ОбменEDIВызовСервера");
		//Если МодульОбменEDI.ЕстьСобытияЭДО() Тогда
		//	КартинкаОповещений = БиблиотекаКартинок["ВосклицательныйЗнакКрасный"];
		//КонецЕсли;
		МестоРазмещенияКомандПоУмолчанию.Картинка = КартинкаОповещений;
	КонецЕсли;
	
	Для Каждого МестоРазмещения Из МестаРазмещения Цикл
		НайденныеКоманды = КомандыEDI.НайтиСтроки(Новый Структура("МестоРазмещения,СкрытаФункциональнымиОпциями,Отключена", МестоРазмещения, Ложь, Ложь));
		ЭлементФормыДляРазмещения = Форма.Элементы.Найти(МестоРазмещения);
		Если ЭлементФормыДляРазмещения = Неопределено Тогда
			ЭлементФормыДляРазмещения = МестоРазмещенияКомандПоУмолчанию;
		КонецЕсли;
		
		Если НайденныеКоманды.Количество() > 0 Тогда
			ДобавитьКомандыEDI(Форма, НайденныеКоманды, ЭлементФормыДляРазмещения);
		КонецЕсли;
	КонецЦикла;
	
	АдресКомандEDIВоВременномХранилище = "АдресКомандEDIВоВременномХранилище";
	КомандаФормы = Форма.Команды.Найти(АдресКомандEDIВоВременномХранилище);
	Если КомандаФормы = Неопределено Тогда
		КомандаФормы = Форма.Команды.Добавить(АдресКомандEDIВоВременномХранилище);
		КомандаФормы.Действие = ПоместитьВоВременноеХранилище(КомандыEDI, Форма.УникальныйИдентификатор);
	Иначе
		ОбщийСписокКомандEDIФормы = ПолучитьИзВременногоХранилища(КомандаФормы.Действие);
		Для Каждого КомандаEDI Из КомандыEDI Цикл
			ЗаполнитьЗначенияСвойств(ОбщийСписокКомандEDIФормы.Добавить(), КомандаEDI);
		КонецЦикла;
		КомандаФормы.Действие = ПоместитьВоВременноеХранилище(ОбщийСписокКомандEDIФормы, Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры


Процедура ДобавитьКомандыEDI(Форма, КомандыEDI, Знач МестоРазмещенияКоманд = Неопределено)
	
	МестоРазмещения = МестоРазмещенияКоманд;
	Для Каждого ОписаниеКомандыEDI Из КомандыEDI Цикл
		
		НомерКоманды = ОписаниеКомандыEDI.Владелец().Индекс(ОписаниеКомандыEDI);
		ИмяКоманды = МестоРазмещенияКоманд.Имя + ОписаниеКомандыEDI.Идентификатор + НомерКоманды;
		
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ВыполнитьКомандуEDI";
		КомандаФормы.Заголовок = ОписаниеКомандыEDI.Представление;
		КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
		
		Если ЗначениеЗаполнено(ОписаниеКомандыEDI.Отображение) Тогда 
			КомандаФормы.Отображение = ОписаниеКомандыEDI.Отображение;
		Иначе 
			КомандаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеКомандыEDI.Картинка) Тогда
			КомандаФормы.Картинка = ОписаниеКомандыEDI.Картинка;
		КонецЕсли;
		
		ОписаниеКомандыEDI.ИмяКомандыНаФорме = ИмяКоманды;
		
		// Для платформенной команды ввода на основании не добавляем префикс, чтобы команды не отделялись чертой.
		Если ОписаниеКомандыEDI.МестоРазмещения = "ФормаСоздатьНаОсновании" Тогда
			МестоРазмещенияИмя = ОписаниеКомандыEDI.МестоРазмещения;
		Иначе
			МестоРазмещенияИмя = МестоРазмещенияКоманд.Имя + ОписаниеКомандыEDI.МестоРазмещения;
		КонецЕсли;
	
		Если Форма.Элементы.Найти(МестоРазмещенияИмя) = Неопределено Тогда
			МестоРазмещения = Форма.Элементы.Добавить(МестоРазмещенияИмя, Тип("ГруппаФормы"), МестоРазмещения);
			МестоРазмещения.Вид = ВидГруппыФормы.ГруппаКнопок;
			МестоРазмещения.Заголовок = СтрЗаменить(ОписаниеКомандыEDI.МестоРазмещения, "КомандыEDI", "");
		КонецЕсли; 
		
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), МестоРазмещения);
		НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
		НовыйЭлемент.Видимость = Не ОписаниеКомандыEDI.Недоступна;
		НовыйЭлемент.ТолькоВоВсехДействиях = ОписаниеКомандыEDI.ТолькоВоВсехДействиях;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОписаниеКомандыEDI(ИмяКоманды, АдресКомандВоВременномХранилище) Экспорт
	
	КомандыEDI = ПолучитьИзВременногоХранилища(АдресКомандВоВременномХранилище);
	Для Каждого КомандаEDI Из КомандыEDI.НайтиСтроки(Новый Структура("ИмяКомандыНаФорме", ИмяКоманды)) Цикл
		Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КомандаEDI);
	КонецЦикла;
	
КонецФункции



Функция ВыполнитьДействияПоЭД(Знач МассивСсылокНаОбъект,
							  Знач МассивОтпечатковСертификатов,
							  Знач Действия,
							  ДопПараметры,
							  Знач ЭД,
							  Знач СоотвСертификатовИПаролей) Экспорт
	
	СтруктураВозврата = ТК_ОбменEDI.ВыполнитьДействияПоЭД(
		МассивСсылокНаОбъект, МассивОтпечатковСертификатов, Действия, ДопПараметры, ЭД, СоотвСертификатовИПаролей);
	
	Возврат СтруктураВозврата;
	
КонецФункции

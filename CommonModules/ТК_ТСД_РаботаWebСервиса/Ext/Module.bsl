

Функция ВернутьТекстЗапросаПоНоменклатуре_ДляТСД() 
	
	//		              |	{Условие}

	ТекстЗапроса ="ВЫБРАТЬ
	              |	спрСкладыКомпании.Ссылка КАК Склад
	              |ПОМЕСТИТЬ ВТ_Склады
	              |ИЗ
	              |	Справочник.СкладыКомпании КАК спрСкладыКомпании
	              |ГДЕ
	              |	спрСкладыКомпании.Ссылка = &Склад
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура КАК Номенклатура,
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток,
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоОборот КАК КоличествоОборот,
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход,
	              |	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоРасход КАК КоличествоРасход
	              |ПОМЕСТИТЬ ВТ_Движ
	              |ИЗ
	              |	РегистрНакопления.ОстаткиТоваровКомпании.ОстаткиИОбороты(
	              |			&НачПериода,
	              |			&КонПериода,
	              |			,
	              |			,
	              |			СкладКомпании В
	              |				(ВЫБРАТЬ
	              |					ВТ_Склады.Склад
	              |				ИЗ
	              |					ВТ_Склады КАК ВТ_Склады)) КАК ОстаткиТоваровКомпанииОстаткиИОбороты
	              |
	              |ИНДЕКСИРОВАТЬ ПО
	              |	Номенклатура
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВТ_Движ.Номенклатура КАК Номенклатура,
	              |	ШтрихКоды.Штрихкод КАК Штрихкод,
	              |	ВТ_Движ.Номенклатура.Артикул КАК Артикул,
	              |	ВТ_Движ.Номенклатура.Наименование КАК Наименование,
	              |	1 КАК Коэффициент
	              |ИЗ
	              |	ВТ_Движ КАК ВТ_Движ
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихКоды
	              |		ПО ВТ_Движ.Номенклатура = ШтрихКоды.Номенклатура
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Наименование";
		
		
		Возврат ТекстЗапроса;
	
КонецФункции	

Функция ВернутьТЗ_Контрагенты_ДляТСД_Инвентаризация()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Владелец,
	|	Контрагенты.Код КАК ВладелецКод,
	|	Контрагенты.Наименование КАК ВладелецНаименование,
	|	Контрагенты.КодПоЕДРПОУ КАК ВладелецКодПоОКПО
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ПометкаУдаления = ЛОЖЬ
	|	И Контрагенты.КодПоЕДРПОУ ПОДОБНО ""tsd%""";
	
	тзРез = Запрос.Выполнить().Выгрузить();
	Возврат тзРез;
КонецФункции // ()

Функция Преобразовать_XDTO_ВТаблицуЗначений(ПространствоИмен, ИмяПакета_XDTO, Список_XDTO)
	РезТип = ФабрикаXDTO.Тип(ПространствоИмен, ИмяПакета_XDTO);
	тз = Новый ТаблицаЗначений;
	Для каждого Свойство Из РезТип.Свойства Цикл
		тз.Колонки.Добавить(Свойство.Имя);
	КонецЦикла;
	// Создание описателя типов для таблицы значений
	Массив = Новый Массив;
	Массив.Добавить(Тип("Булево"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, , );
	тз.Колонки.Добавить("Обработан", ОписаниеТипов);
	тз.Колонки.Добавить("ЭтоИнвентаризация", ОписаниеТипов);
	Для каждого ЭлСписка Из Список_XDTO Цикл
		стз = тз.Добавить();
		Для каждого Свойство Из РезТип.Свойства Цикл
			Если Свойство.Имя = "DocNumber" И Лев(ЭлСписка[Свойство.Имя], 3) = "inv" Тогда
				стз.ЭтоИнвентаризация = Истина;
			КонецЕсли; 
			стз[Свойство.Имя] = ЭлСписка[Свойство.Имя];
		КонецЦикла; 
	КонецЦикла; 
	
	Возврат тз;
	
КонецФункции // ПреобразоватьВТаблицуЗначений()

Функция ВернутьТаблицуТоваровПоАртикулу(СписокТоваровXDTO)
	Артикулы = Новый Массив;
	Для каждого СтрокаТовар Из СписокТоваровXDTO Цикл
		Артикулы.Добавить(СтрокаТовар.NomenclatureCode);
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Артикул,
	|	Номенклатура.Ссылка
	|ПОМЕСТИТЬ Таб
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Артикул В(&Артикулы)
	|	И НЕ Номенклатура.ЭтоГруппа
	|	И НЕ Номенклатура.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Артикул,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Таб.Ссылка) КАК КоличествоДублей
	|ПОМЕСТИТЬ Дубли
	|ИЗ
	|	Таб КАК Таб
	|
	|СГРУППИРОВАТЬ ПО
	|	Таб.Артикул
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Артикул,
	|	Таб.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(Дубли.КоличествоДублей, 0) КАК КоличествоДублей,
	|	Таб.Ссылка.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения
	|ИЗ
	|	Таб КАК Таб
	|		ЛЕВОЕ СОЕДИНЕНИЕ Дубли КАК Дубли
	|		ПО Таб.Артикул = Дубли.Артикул";
	Запрос.УстановитьПараметр("Артикулы", Артикулы);
	Результат = Запрос.Выполнить();
	
	тзРез = Результат.Выгрузить();
	Возврат тзРез;
	
КонецФункции // ВернутьТаблицуТоваровПоАртикулу()

Функция ЗаписатьВРегистрДанныхТСД(НомерДокумента,СкладКомпании,МассивТоваров,тзТовары,стрСообщение)
	
	ДокументОбработан=Ложь;
	
	Попытка
		НаборЗаписей = РегистрыСведений.ТК_ДанныеИнвентаризацийТСД.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СкладКомпании.Установить(СкладКомпании);
		НаборЗаписей.Отбор.НомерДокумента.Установить(НомерДокумента);
		
		ДатаЗагрузки = ТекущаяДата();
		
		тзДанные = НаборЗаписей.ВыгрузитьКолонки();
		
		
		
		Для каждого СтрокаТовар Из МассивТоваров Цикл
			
			стзНоменклатура = тзТовары.Найти(СтрокаТовар.NomenclatureCode, "Артикул");
			Если стзНоменклатура = Неопределено Тогда
				стрСообщение = стрСообщение+СтрокаТовар.NomenclatureCode+";";
			Иначе
				
				НоваяЗапись = тзДанные.Добавить();
				НоваяЗапись.СкладКомпании  = СкладКомпании;
				НоваяЗапись.НомерДокумента = НомерДокумента;
				НоваяЗапись.Номенклатура   = стзНоменклатура.Номенклатура;
				НоваяЗапись.Количество     = СтрокаТовар.Qty;
				НоваяЗапись.ДатаЗагрузки   = ДатаЗагрузки;
			КонецЕсли;
		КонецЦикла;
		
		тзДанные.Свернуть("СкладКомпании,НомерДокумента,Номенклатура,ДатаЗагрузки","Количество");
		НаборЗаписей.Загрузить(тзДанные);
		
		Попытка
			НаборЗаписей.Записать();
			ДокументОбработан = Истина;	
		Исключение
			стрСообщение = стрСообщение + "Ошибка записи данных "+ОписаниеОшибки();
			
		КонецПопытки;
	Исключение
		стрСообщение = стрСообщение + "Ошибка записи данных "+ОписаниеОшибки();
		
	КонецПопытки;
	
	
	Возврат ДокументОбработан;
	
	
КонецФункции




Функция ВозвратНоменклатурыДляТСД_Инвентаризация(ПрефиксСклада) Экспорт
	УстановитьПривилегированныйРежим(Истина);

	РезТип = ФабрикаXDTO.Тип("http://www.digma.ua", "NomenclatureForTSD_List");
	РезСтруктура = ФабрикаXDTO.Создать(РезТип);
	ЭлементТип = ФабрикаXDTO.Тип("http://www.digma.ua", "NomenclatureForTSD");
	
	Запрос = Новый Запрос;
	ТекстЗапроса =  ВернутьТекстЗапросаПоНоменклатуре_ДляТСД();
	Если СтрНайти(ПрефиксСклада,"-") > 0 Тогда
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПрефиксСклада,"-");
		СкладКомпании         = Справочники.СкладыКомпании.НайтиПоКоду(СокрЛП(МассивПодстрок[1]));
	Иначе
		СкладКомпании         = Справочники.СкладыКомпании.НайтиПоКоду(СокрЛП(ПрефиксСклада));
	КонецЕсли;
	
	

	
	Запрос.УстановитьПараметр("Склад", СкладКомпании);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачПериода", ДобавитьМесяц(ТекущаяДата(),-1));
	Запрос.УстановитьПараметр("КонПериода", ТекущаяДата());
	тзРез = Запрос.Выполнить().Выгрузить();
	Для каждого Выборка Из тзРез Цикл
		Элемент = ФабрикаXDTO.Создать(ЭлементТип);
		Элемент.Code = Строка(Выборка.Артикул);
		Элемент.Barcode = Строка(Выборка.Штрихкод);
		Элемент.Name = Выборка.Наименование;
		Элемент.Inpack = Выборка.Коэффициент;
		РезСтруктура.NomenclatureList.Добавить(Элемент);
	КонецЦикла; 
	Возврат РезСтруктура;
	
КонецФункции // ВозвратНоменклатурыДляТСД()

Функция ВозвратКонтрагентовДляТСД_Инвентаризация() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	РезТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ClientForTSD_List");
	РезСтруктура = ФабрикаXDTO.Создать(РезТип);
	ЭлементТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ClientForTSD");
	
	тзРез = ВернутьТЗ_Контрагенты_ДляТСД_Инвентаризация();
	Для каждого Выборка Из тзРез Цикл
		Элемент = ФабрикаXDTO.Создать(ЭлементТип);
		Элемент.Code = ?(ЗначениеЗаполнено(Выборка.ВладелецКодПоОКПО), Выборка.ВладелецКодПоОКПО, Выборка.ВладелецКод);
		Элемент.Name = Выборка.ВладелецНаименование;
		РезСтруктура.ClientList.Добавить(Элемент);
	КонецЦикла; 
	Возврат РезСтруктура;
КонецФункции // ВозвратНоменклатурыДляТСД()



Функция СоздатьДокументыИзТСД(DocForTSD_List, ПрефиксСклада) Экспорт
	УстановитьПривилегированныйРежим(Истина);

	РезТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnDownloadStatusFor1C_List");
	РезСтруктура = ФабрикаXDTO.Создать(РезТип);
	ЭлементТип = ФабрикаXDTO.Тип("http://www.digma.ua", "ReturnDownloadStatusFor1C");
	
	
	Если СтрНайти(ПрефиксСклада,"-") > 0 Тогда
		МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПрефиксСклада,"-");
		СкладКомпании         = Справочники.СкладыКомпании.НайтиПоКоду(СокрЛП(МассивПодстрок[1]));
	Иначе
		СкладКомпании         = Справочники.СкладыКомпании.НайтиПоКоду(СокрЛП(ПрефиксСклада));
	КонецЕсли;
	
	
	СписокДокументовXDTO = DocForTSD_List.ПолучитьСписок("DocHeadForTSDList");
	СписокТоваровXDTO 	 = DocForTSD_List.ПолучитьСписок("DocTableForTSDList");
	
	тзXDTO = Преобразовать_XDTO_ВТаблицуЗначений("http://www.digma.ua", "DocTableForTSD", СписокТоваровXDTO);
	тзТоваровПоАртикулу = ВернутьТаблицуТоваровПоАртикулу(СписокТоваровXDTO);
	
	
	
	
	Для каждого СтрокаДокумент Из СписокДокументовXDTO Цикл
		МассивТоваров = тзXDTO.НайтиСтроки(Новый Структура("DocNumber", СтрокаДокумент.DocNumber));
		стрСообщение = "";

		
		ВсеОК = ЗаписатьВРегистрДанныхТСД(СтрокаДокумент.ClientCode,СкладКомпании,МассивТоваров,тзТоваровПоАртикулу,стрСообщение);
		Если ВсеОК Тогда
			Элемент = ФабрикаXDTO.Создать(ЭлементТип);
			Элемент.DocNumber = СтрокаДокумент.DocNumber;
			Элемент.StatusDownload = Истина;
			Элемент.Description = "Данные по товарам записаны успешно";
			РезСтруктура.ReturnDownloadStatusFor1CList.Добавить(Элемент);
		Иначе
			Элемент = ФабрикаXDTO.Создать(ЭлементТип);
			Элемент.DocNumber = СтрокаДокумент.DocNumber;
			Элемент.StatusDownload = Ложь;
			Элемент.Description = "Загрузка не выполнена. "+?(стрСообщение = "", "",стрСообщение);
			РезСтруктура.ReturnDownloadStatusFor1CList.Добавить(Элемент);
		КонецЕсли;
		
	КонецЦикла;
	
	
	
	Возврат РезСтруктура;
КонецФункции // СоздатьДокументыИзТСД()

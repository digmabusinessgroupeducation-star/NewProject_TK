
#Область ПодпискиНаСобытия


Процедура ТК_ПриЗаписиСправочникСкубисПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли ТипЗнч(Источник) <> Тип("СправочникОбъект.Номенклатура") И Источник.ЭтоГруппа Тогда
		Возврат;
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.ТипыЦен") И Источник.ТипЦенЗакупки Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьВРегистре(Источник);
	
КонецПроцедуры

Процедура ТК_ПриЗаписиДокументыСкубисПриЗаписи(Источник, Отказ) Экспорт
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли (ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваров") ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров"))
		И НЕ НаОснованииЗаказаПокупателя(Источник.Ссылка) Тогда
		Возврат;
	//ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваров")
	//	И (Источник.СкладКомпании.Код = "AC000021" ИЛИ Источник.СкладКомпании.Код = "AC000027") Тогда	//	Дистрибьюция самовывоз
	//	Возврат;
	КонецЕсли;
	
	ЗарегистрироватьВРегистре(Источник);
КонецПроцедуры

Процедура ТК_ПриЗаписиРегистрСведенийСкубисПриЗаписи(Источник, Отказ, Замещение) Экспорт
	Если ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.РаботникиОрганизаций") И Источник.Количество() > 0 Тогда
		УстановитьПривилегированныйРежим(Истина);
		РегистрыСведений.ТК_СправочникиСкубис.ЗаписатьОбъектСкубис(Источник[0].Сотрудник);
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьВРегистре(Источник)
	УстановитьПривилегированныйРежим(Истина);
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ЕдиницыИзмерения") Тогда
		СсылкаРегистрации = Источник.Владелец;
	Иначе
		СсылкаРегистрации = Источник.Ссылка;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоДокумент(СсылкаРегистрации.Метаданные()) Тогда
		РегистрыСведений.ТК_ДокументыСкубис.ЗаписатьОбъектСкубис(СсылкаРегистрации);
	Иначе
		РегистрыСведений.ТК_СправочникиСкубис.ЗаписатьОбъектСкубис(СсылкаРегистрации);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


Функция НаОснованииЗаказаПокупателя(Ссылка)
	
	ДокументОснование = Ссылка.ДокументОснование;
	РаспределениеОтгрузки = Ссылка.РаспределениеОтгрузки;
	
	Если ЗначениеЗаполнено(Ссылка.ДокументОснование) И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Возврат Истина;
	ИначеЕсли ЗначениеЗаполнено(Ссылка.РаспределениеОтгрузки) И ТипЗнч(РаспределениеОтгрузки[0].ЗаказПокупателя) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ВыгрузитьОбъекты(РезультатЗапроса)
	
	ТЗ = РезультатЗапроса.Выгрузить();
	ТЗ.Свернуть("ССылка");
	Массив = ТЗ.ВыгрузитьКолонку("ССылка");
	
	Возврат Массив;
	
КонецФункции

Функция СоздатьHTTPСоединениеСкубис()
	
	//СерверПриемник = "87.244.179.144"; //имя сервера без протокола (http) и порта
	//Порт = 4446; 
	СерверПриемник = "185.230.90.172"; //имя сервера без протокола (http) и порта
	Порт = 82; 
	ЗащищенноеСоединение = Ложь;
	Таймаут = 120;
	
	SSL =  ?(ЗащищенноеСоединение, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено);
	
	СоединениеНТТР = Новый HTTPСоединение(СерверПриемник, Порт, , , , Таймаут, SSL);
	
	Возврат СоединениеНТТР;
	
КонецФункции

Функция СтрокаJSON(МассивДанных)
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	ЗаписатьJSON(ЗаписьJSON, МассивДанных); 
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
КонецФункции

Процедура ПроверкаНаЗаполнено(Структура,Ключ,Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Структура.Вставить(Ключ,Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьАдрес(Код,знач Адрес)
	
	КодЧисло = Число(Код);
	Если Строка(КодЧисло) = лев(Адрес,СтрДлина(Строка(КодЧисло))) Тогда 
		Адрес = СокрЛП(Прав(Адрес,СтрДлина(Адрес)-СтрДлина(Строка(КодЧисло))));
		ПервыйСимвол = Лев(Адрес,1);
		Если ПервыйСимвол = "." ИЛИ ПервыйСимвол = "," Тогда 
			Адрес = СокрЛП(Прав(Адрес,СтрДлина(Адрес)-1));
		КонецЕсли;
		Если Строка(КодЧисло) = лев(Адрес,СтрДлина(Строка(КодЧисло))) Тогда 
			Адрес = СокрЛП(Прав(Адрес,СтрДлина(Адрес)-СтрДлина(Строка(КодЧисло))));	
		КонецЕсли;
	КонецЕсли;
	
	ПервыйСимвол = Лев(Адрес,1);
	Если ПервыйСимвол = "." ИЛИ ПервыйСимвол = "," Тогда 
		Адрес = СокрЛП(Прав(Адрес,СтрДлина(Адрес)-1));
	КонецЕсли;
	
	Возврат Адрес;
	
КонецФункции

Процедура ОтправитьПисьмо(Данные)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Получатели = Новый СписокЗначений;
	Получатели.Добавить(Справочники.Пользователи.НайтиПоНаименованию("Лохвицкий Сергей Викторович"));
	Получатели.Добавить(Справочники.Пользователи.НайтиПоНаименованию("ЛохвицкийСВ"));
	
	Тело = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = '%1%2При отправке графиков найдены проблемные площадки!%3%4 '; uk = '%1%2Під час відправлення графіків знайдено проблемні майданчики! %3%4'"),
	ТекущаяДата(),
	Символы.ПС,
	Символы.ПС,
	Данные,);
	
	СтрокаАдрес = "";
	
	ВидКИ = УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("EmailПользователя");
	
	Для Каждого Получатель Из Получатели Цикл
		ЭлАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Получатель.Значение,ВидКИ);
		
		Если ПустаяСтрока(ЭлАдрес) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаАдрес = СтрокаАдрес + ЭлАдрес+";";	
	КонецЦикла;
	
	Если ПустаяСтрока(СтрокаАдрес) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Нет пользователей для отправки уведомлений'; uk = 'Немає користувачів для відправки повідомлень'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Кому",СтрокаАдрес);
	ПараметрыПисьма.Вставить("Тема","Графики. Проблемные площадки");
	ПараметрыПисьма.Вставить("Тело",Тело);
	
	Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
	РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);
	
КонецПроцедуры

Функция НайтиТорговуюПлощадкуПоТочкеДоставки(ТочкаДоставки)
	
	ТорговаяПлощадка = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.ТочкаДоставки = &ТочкаДоставки
	|	И СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка)
	|	И (СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
	|			ИЛИ СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон))";
	
	Запрос.УстановитьПараметр("ТочкаДоставки", ТочкаДоставки);
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		//Если Выборка.Количество() = 1 Тогда
		ТорговаяПлощадка = Выборка.ТорговаяПлощадка;
		//ИначеЕсли Выборка.Количество() = 1 Тогда
		//	ТорговаяПлощадка = Выборка.ТорговаяПлощадка;
		//КонецЕсли;
	КонецЦикла;
	
	Возврат ТорговаяПлощадка;
	
КонецФункции

#КонецОбласти


#Область ПолученияДанных


Функция ПолучитьОстатки()
	
	НаДату = ТекущаяДата();
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.День КАК День,
	|	ВложенныйЗапрос.ТорговаяТочка КАК ТорговаяТочка,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СРЕДНЕЕ(ТК_РасcчитанныеПотребностиТовара.СреднедневныеПродажи) КАК СреднедневныеПродажи
	|ПОМЕСТИТЬ втСреднедневныеПродажи
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ТК_РасcчитанныеПотребностиТовара.День) КАК День,
	|		ТК_РасcчитанныеПотребностиТовара.ТорговаяТочка КАК ТорговаяТочка,
	|		ТК_РасcчитанныеПотребностиТовара.Номенклатура КАК Номенклатура
	|	ИЗ
	|		РегистрСведений.ТК_РасcчитанныеПотребностиТовара КАК ТК_РасcчитанныеПотребностиТовара
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТК_РасcчитанныеПотребностиТовара.ТорговаяТочка,
	|		ТК_РасcчитанныеПотребностиТовара.Номенклатура) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РасcчитанныеПотребностиТовара КАК ТК_РасcчитанныеПотребностиТовара
	|		ПО ВложенныйЗапрос.Номенклатура = ТК_РасcчитанныеПотребностиТовара.Номенклатура
	|			И ВложенныйЗапрос.ТорговаяТочка = ТК_РасcчитанныеПотребностиТовара.ТорговаяТочка
	|			И ВложенныйЗапрос.День = ТК_РасcчитанныеПотребностиТовара.День
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.День,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.ТорговаяТочка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	втСреднедневныеПродажи.СреднедневныеПродажи КАК СреднедневныеПродажи
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&НаДату, ) КАК ОстаткиТоваровКомпанииОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСреднедневныеПродажи КАК втСреднедневныеПродажи
	|		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура = втСреднедневныеПродажи.Номенклатура
	|			И ОстаткиТоваровКомпанииОстатки.СкладКомпании = втСреднедневныеПродажи.ТорговаяТочка";
	
	Запрос.УстановитьПараметр("НаДату",НаДату);
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("date",НаДату);
		СтруктураСтроки.Вставить("warehouse",Строка(Выборка.СкладКомпании.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("product",Строка(Выборка.Номенклатура.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"MRC",?(ЗначениеЗаполнено(Выборка.Характеристика),Строка(Выборка.Характеристика.УникальныйИдентификатор()),""));
		СтруктураСтроки.Вставить("quantity",Выборка.КоличествоОстаток);
		ПроверкаНаЗаполнено(СтруктураСтроки,"averageSales",Выборка.СреднедневныеПродажи);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

Функция ПолучитьФизическиеЛица()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Код КАК Код,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ФизическиеЛица";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("name",СокрЛП(Выборка.Наименование));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьАвтотранспорт()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.ГосНомер КАК ГосНомер,
	|	ТК_СправочникиСкубис.Объект.Грузоподъемность КАК Грузоподъемность,
	|	ТК_СправочникиСкубис.Объект.Вместимость КАК Вместимость,
	|	ТК_СправочникиСкубис.Объект.ВидТоплива КАК ВидТоплива,
	|	ТК_СправочникиСкубис.Объект.РасходТоплива КАК РасходТоплива,
	|	НЕ ТК_СправочникиСкубис.Объект В ИЕРАРХИИ (&Родитель) КАК Актуальный,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Автотранспорт";
	
	Запрос.УстановитьПараметр("Родитель",Справочники.Автотранспорт.НайтиПоКоду(1));
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"carNumber",СокрЛП(Выборка.ГосНомер));
		ПроверкаНаЗаполнено(СтруктураСтроки,"weight",Выборка.Грузоподъемность/1000);
		ПроверкаНаЗаполнено(СтруктураСтроки,"volume",Выборка.Вместимость);
		ПроверкаНаЗаполнено(СтруктураСтроки,"fuel",СокрЛП(Выборка.ВидТоплива));
		ПроверкаНаЗаполнено(СтруктураСтроки,"expengas",Выборка.РасходТоплива);
		СтруктураСтроки.Вставить("valid",Выборка.Актуальный);
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьКонтрагенты()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.НаименованиеПолное КАК НаименованиеПолное,
	|	ТК_СправочникиСкубис.Объект.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Контрагенты";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"fullname",СокрЛП(Выборка.НаименованиеПолное));
		ПроверкаНаЗаполнено(СтруктураСтроки,"code",СокрЛП(Выборка.КодПоЕДРПОУ));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьТочкиДоставки()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Владелец КАК Владелец,
	|	ТК_СправочникиСкубис.Объект.Код КАК Код,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.Действует КАК Действует,
	|	ТК_СправочникиСкубис.Объект.Владелец.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.Внутренний) КАК Внутренний,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТочкиДоставки";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("FullName",СокрЛП(Выборка.Код));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"address",ПолучитьАдрес(Выборка.Код,Выборка.Наименование));
		//СтруктураСтроки.Вставить("gps_x","");
		//СтруктураСтроки.Вставить("gps_y","");
		//СтруктураСтроки.Вставить("phone","");
		СтруктураСтроки.Вставить("counterparty",Строка(Выборка.Владелец.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("internal",Выборка.Внутренний);
		СтруктураСтроки.Вставить("valid",Выборка.Действует);
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		//СтруктураСтроки.Вставить("shopTypeID","");
		//СтруктураСтроки.Вставить("postponement","");
		//СтруктураСтроки.Вставить("segmentID","");
		//Массив = Новый Массив;
		//СтруктураСтроки.Вставить("types",Массив);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьПодразделения()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ПодразделенияКомпании";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьТипыЦен()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТипыЦен";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьВидыСкладов()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТК_СправочникиСкубис.Объект = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
	|			ТОГДА ""Павильон""
	|		КОГДА ТК_СправочникиСкубис.Объект = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон)
	|			ТОГДА ""Киоск""
	|		ИНАЧЕ ТК_СправочникиСкубис.Объект.Наименование
	|	КОНЕЦ КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ВидыСкладов";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьСкладыКомпании()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Код КАК Код,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.Подразделение КАК Подразделение,
	|	ТК_СправочникиСкубис.Объект.ТочкаДоставки.Наименование КАК ТочкаДоставкиНаименование,
	|	ТК_СправочникиСкубис.Объект.ТипЦенРозничнойТорговли КАК ТипЦен,
	|	ТК_СправочникиСкубис.Объект.ВидСклада КАК ВидСклада,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.СкладыКомпании";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("FullName",СокрЛП(Выборка.Код));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"address",СокрЛП(Выборка.ТочкаДоставкиНаименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"branch",?(ЗначениеЗаполнено(Выборка.Подразделение),Строка(Выборка.Подразделение.УникальныйИдентификатор()),""));
		ПроверкаНаЗаполнено(СтруктураСтроки,"shopType",?(ЗначениеЗаполнено(Выборка.ВидСклада),Строка(Выборка.ВидСклада.УникальныйИдентификатор()),""));
		ПроверкаНаЗаполнено(СтруктураСтроки,"PriceTypes",?(ЗначениеЗаполнено(Выборка.ТипЦен),Строка(Выборка.ТипЦен.УникальныйИдентификатор()),""));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьТоварныеМарки()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТоварныеМарки";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		//СтруктураСтроки.Вставить("gruz_type_id","");
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьГруппыТовара()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Родитель КАК Родитель,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект.ЭтоГруппа
	|	И ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"sub_unistring",?(ЗначениеЗаполнено(Выборка.Родитель),Строка(Выборка.Родитель.УникальныйИдентификатор()),""));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьНоменклатура()
	
	МассивДанныхТовар = Новый Массив;
	МассивДанныхШтрихкод = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Родитель КАК Родитель,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.Артикул КАК Артикул,
	|	ТК_СправочникиСкубис.Объект.Наименование2 КАК Наименование2,
	|	ТК_СправочникиСкубис.Объект.ОсновнаяЕдиницаИзмерения.Вес КАК Вес,
	|	ТК_СправочникиСкубис.Объект.ТоварнаяМарка КАК ТоварнаяМарка,
	|	ТК_СправочникиСкубис.Объект.Ширина * ТК_СправочникиСкубис.Объект.Ширина * ТК_СправочникиСкубис.Объект.Глубина / 1000000 КАК Объем,
	|	ВЫБОР
	|		КОГДА ТК_СправочникиСкубис.Объект.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТараТовар,
	|	ВЫБОР
	|		КОГДА ТК_СправочникиСкубис.Объект.ТипНоменклатуры.Весовой
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВесовойШтучный,
	|	ТК_СправочникиСкубис.Объект.СтавкаНДС.Ставка КАК СтавкаНДССтавка,
	|	ЕдиницыИзмерения.Ссылка КАК СсылкаЕдиница,
	|	ЕдиницыИзмерения.Наименование КАК НаименованиеЕдиница,
	|	ЕдиницыИзмерения.Коэффициент КАК КоэффициентЕдиница,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО ТК_СправочникиСкубис.Объект = ЕдиницыИзмерения.Владелец
	|ГДЕ
	|	НЕ ТК_СправочникиСкубис.Объект.ЭтоГруппа
	|	И ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(Статус)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТК_СправочникиСкубис.Объект = ШтрихкодыНоменклатуры.Номенклатура
	|ГДЕ
	|	НЕ ТК_СправочникиСкубис.Объект.ЭтоГруппа
	|	И ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	/////	Товар	/////
	ВыборкаТовар = Пакет[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТовар.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(ВыборкаТовар.Ссылка.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(ВыборкаТовар.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"catalog_id",?(ЗначениеЗаполнено(ВыборкаТовар.Родитель),Строка(ВыборкаТовар.Родитель.УникальныйИдентификатор()),""));
		ПроверкаНаЗаполнено(СтруктураСтроки,"vendor",СокрЛП(ВыборкаТовар.Артикул));
		ПроверкаНаЗаполнено(СтруктураСтроки,"product_brand_id",?(ЗначениеЗаполнено(ВыборкаТовар.ТоварнаяМарка),Строка(ВыборкаТовар.ТоварнаяМарка.УникальныйИдентификатор()),""));
		ПроверкаНаЗаполнено(СтруктураСтроки,"description",СокрЛП(ВыборкаТовар.Наименование2));
		ПроверкаНаЗаполнено(СтруктураСтроки,"weight",?(ЗначениеЗаполнено(ВыборкаТовар.Вес),Формат(ВыборкаТовар.Вес,"ЧЦ=12; ЧДЦ=6; ЧРД=.; ЧГ="),0));
		ПроверкаНаЗаполнено(СтруктураСтроки,"volume",?(ЗначениеЗаполнено(ВыборкаТовар.Объем),Формат(ВыборкаТовар.Объем,"ЧЦ=12; ЧДЦ=6; ЧРД=.; ЧГ="),0));
		СтруктураСтроки.Вставить("container",ВыборкаТовар.ТараТовар);
		СтруктураСтроки.Вставить("measuredValueType",ВыборкаТовар.ВесовойШтучный);
		
		МассивЕдиницИзмерений = Новый Массив;
		
		ВыборкаЕдиницы = ВыборкаТовар.Выбрать();
		Пока ВыборкаЕдиницы.Следующий() Цикл 
			СтруктураСтрокиЕдиницы = Новый Структура;
			ПроверкаНаЗаполнено(СтруктураСтрокиЕдиницы,"packageName",СокрЛП(ВыборкаЕдиницы.НаименованиеЕдиница));
			СтруктураСтрокиЕдиницы.Вставить("amount",Формат(ВыборкаЕдиницы.КоэффициентЕдиница,"ЧЦ=12; ЧРД=.; ЧГ="));
			СтруктураСтрокиЕдиницы.Вставить("forReport",1);
			МассивЕдиницИзмерений.Добавить(СтруктураСтрокиЕдиницы);
		КонецЦикла;
		
		ПроверкаНаЗаполнено(СтруктураСтроки,"package",МассивЕдиницИзмерений);
		//СтруктураСтроки.Вставить("package",МассивЕдиницИзмерений);
		СтруктураСтроки.Вставить("vatRate",ВыборкаТовар.СтавкаНДССтавка);
		СтруктураСтроки.Вставить("changed",ВыборкаТовар.Статус);
		МассивДанныхТовар.Добавить(СтруктураСтроки);
	КонецЦикла;     
	
	/////	Штрихкод	/////
	Выборка = Пакет[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("code",СокрЛП(Выборка.Штрихкод));
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Номенклатура.УникальныйИдентификатор()));
		МассивДанныхШтрихкод.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Пакет[0]);
	
	Возврат Новый Структура("Product,Barcode",Новый Структура("Данные,Объекты",МассивДанныхТовар,МассивОбъектов),МассивДанныхШтрихкод);
	
КонецФункции

Функция ПолучитьХарактеристики()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_СправочникиСкубис.Объект КАК Ссылка,
	|	ТК_СправочникиСкубис.Объект.Владелец КАК Владелец,
	|	ТК_СправочникиСкубис.Объект.Наименование КАК Наименование,
	|	ТК_СправочникиСкубис.Объект.ЗначениеМРЦ КАК ЗначениеМРЦ,
	|	ТК_СправочникиСкубис.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|ГДЕ
	|	ТК_СправочникиСкубис.Статус <> 3
	|	И ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ХарактеристикиНоменклатуры";
	
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("product_id",Строка(Выборка.Владелец.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"name",СокрЛП(Выборка.Наименование));
		ПроверкаНаЗаполнено(СтруктураСтроки,"mrc",Выборка.ЗначениеМРЦ);
		СтруктураСтроки.Вставить("changed",Выборка.Статус);
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции


Функция ПолучитьЗаказы()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка,
	|	ЗаказПокупателя.Проведен КАК Проведен,
	|	ЗаказПокупателя.Дата КАК Дата,
	|	ЗаказПокупателя.Номер КАК Номер,
	|	ЗаказПокупателя.Контрагент КАК Контрагент,
	|	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	|	ЗаказПокупателя.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	|	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	|	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|	ЗаказПокупателя.СрокПоставки КАК СрокПоставки,
	|	ЗаказПокупателя.Комментарий КАК Комментарий,
	|	ТК_ДокументыСкубис.Статус КАК Статус,
	|	ЗаказПокупателя.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		ЗаказПокупателя.Товары.Номенклатура.Ширина * ЗаказПокупателя.Товары.Номенклатура.Высота * ЗаказПокупателя.Товары.Номенклатура.Глубина / 1000000 КАК Объем,
	|		ЗаказПокупателя.Товары.ЕдиницаИзмерения.Вес * ЗаказПокупателя.Товары.Количество КАК Масса
	|	) КАК Товары
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|		ПО ТК_ДокументыСкубис.Объект = ЗаказПокупателя.Ссылка
	|			И (ТК_ДокументыСкубис.Статус <> 3)";
	
	Рез = Запрос.Выполнить();
	
	ВыборкаДок = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	МассивДанных = Новый Массив;
	
	Пока ВыборкаДок.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(ВыборкаДок.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("date",ВыборкаДок.Дата);
		СтруктураСтроки.Вставить("number",СокрЛП(ВыборкаДок.Номер));
		СтруктураСтроки.Вставить("orderType",0);
		СтруктураСтроки.Вставить("branch",Строка(ВыборкаДок.ПодразделениеКомпании.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("counterparty",Строка(ВыборкаДок.Контрагент.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("warehouse",Строка(ВыборкаДок.СкладКомпании.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("waypoints",Строка(ВыборкаДок.ТочкаДоставки.УникальныйИдентификатор()));
		ПроверкаНаЗаполнено(СтруктураСтроки,"comment",СокрЛП(ВыборкаДок.Комментарий));
		СтруктураСтроки.Вставить("posted",ВыборкаДок.Проведен);
		СтруктураСтроки.Вставить("changed",ВыборкаДок.Статус);
		
		МассивТовары = Новый Массив;
		
		Масса = 0;
		Объем = 0;
		
		ВыборкаТовар = ВыборкаДок.Товары.Выбрать();
		Пока ВыборкаТовар.Следующий() Цикл 
			СтруктураСтрокиТовары = Новый Структура;
			СтруктураСтрокиТовары.Вставить("product",Строка(ВыборкаТовар.Номенклатура.УникальныйИдентификатор()));
			ПроверкаНаЗаполнено(СтруктураСтрокиТовары,"MRC",?(ЗначениеЗаполнено(ВыборкаТовар.Характеристика),Строка(ВыборкаТовар.Характеристика.УникальныйИдентификатор()),""));
			СтруктураСтрокиТовары.Вставить("quantity",ВыборкаТовар.КоличествоБазовое);
			МассивТовары.Добавить(СтруктураСтрокиТовары);
			Масса = Масса + ВыборкаТовар.Масса;	
			Объем = Объем + ВыборкаТовар.Объем;	
		КонецЦикла;
		
		ПроверкаНаЗаполнено(СтруктураСтроки,"weight",Масса);
		ПроверкаНаЗаполнено(СтруктураСтроки,"volume",Объем);
		СтруктураСтроки.Вставить("goods",МассивТовары);
		
		МассивДанных.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьПереоценки()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзменениеЦен.Ссылка КАК Ссылка,
	|	ИзменениеЦен.Проведен КАК Проведен,
	|	ИзменениеЦен.ТипЦен КАК ТипЦен,
	|	ИзменениеЦен.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	ТК_ДокументыСкубис.Статус КАК Статус,
	|	ИзменениеЦен.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		НоваяЦена КАК НоваяЦена
	|	) КАК Товары
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеЦен КАК ИзменениеЦен
	|		ПО ТК_ДокументыСкубис.Объект = ИзменениеЦен.Ссылка
	|			И (ТК_ДокументыСкубис.Статус <> 3)";
	
	Рез = Запрос.Выполнить();
	
	ВыборкаДок = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДок.Следующий() Цикл 
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(ВыборкаДок.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("date",ВыборкаДок.ДатаНачалаДействия);
		СтруктураСтроки.Вставить("PriceTypes",Строка(ВыборкаДок.ТипЦен.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("posted",ВыборкаДок.Проведен);
		СтруктураСтроки.Вставить("changed",ВыборкаДок.Статус);
		
		МассивТовары = Новый Массив;
		
		ВыборкаТовар = ВыборкаДок.Товары.Выбрать();
		Пока ВыборкаТовар.Следующий() Цикл 
			СтруктураСтрокиТовары = Новый Структура;
			СтруктураСтрокиТовары.Вставить("product",Строка(ВыборкаТовар.Номенклатура.УникальныйИдентификатор()));
			ПроверкаНаЗаполнено(СтруктураСтрокиТовары,"MRC",?(ЗначениеЗаполнено(ВыборкаТовар.Характеристика),Строка(ВыборкаТовар.Характеристика.УникальныйИдентификатор()),""));
			СтруктураСтрокиТовары.Вставить("priсe",ВыборкаТовар.НоваяЦена);
			МассивТовары.Добавить(СтруктураСтрокиТовары);
		КонецЦикла;
		
		СтруктураСтроки.Вставить("goods",МассивТовары);
		
		МассивДанных.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьОтгрузки()
	
	МассивДанных = Новый Массив;
	
	Запрос = новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка КАК Ссылка,
	|	ПеремещениеТоваров.Номер КАК Номер,
	|	ПеремещениеТоваров.Дата КАК Дата,
	|	ПеремещениеТоваров.Проведен КАК Проведен,
	|	ПеремещениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ЕСТЬNULL(ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки КАК ТочкаДоставки,
	|	ПеремещениеТоваров.СуммаДокумента КАК СуммаДокумента,
	|	ПеремещениеТоваров.Комментарий КАК Комментарий,
	|	ПеремещениеТоваров.ДокументОснование КАК Заказ,
	|	ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).СрокПоставки КАК СрокПоставки,
	|	ИСТИНА КАК НашаДоставка,
	|	ТК_ДокументыСкубис.Статус КАК Статус,
	|	ПеремещениеТоваров.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК Характеристика,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		ПеремещениеТоваров.Товары.Номенклатура.Ширина * ПеремещениеТоваров.Товары.Номенклатура.Высота * ПеремещениеТоваров.Товары.Номенклатура.Глубина / 1000000 КАК Объем,
	|		ЕСТЬNULL(ПеремещениеТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес, 0) * ПеремещениеТоваров.Товары.КоличествоБазовое КАК Масса,
	|		Цена КАК Цена
	|	) КАК Товары
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|		ПО ТК_ДокументыСкубис.Объект = ПеремещениеТоваров.Ссылка
	|			И (ТК_ДокументыСкубис.Статус <> 3)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка,
	|	РеализацияТоваров.Номер,
	|	РеализацияТоваров.Дата,
	|	РеализацияТоваров.Проведен,
	|	РеализацияТоваров.ПодразделениеКомпании,
	|	РеализацияТоваров.СкладКомпании,
	|	РеализацияТоваров.Контрагент,
	|	РеализацияТоваров.ТочкаДоставки,
	|	РеализацияТоваров.СуммаДокумента,
	|	РеализацияТоваров.Комментарий,
	|	РеализацияТоваров.ДокументОснование,
	|	ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).СрокПоставки,
	|	НЕ(РеализацияТоваров.СкладКомпании.Код = ""AC000021""
	|			ИЛИ РеализацияТоваров.СкладКомпании.Код = ""AC000027""),
	|	ТК_ДокументыСкубис.Статус,
	|	РеализацияТоваров.Товары.(
	|		НомерСтроки,
	|		Номенклатура,
	|		ХарактеристикаНоменклатуры,
	|		КоличествоБазовое,
	|		РеализацияТоваров.Товары.Номенклатура.Ширина * РеализацияТоваров.Товары.Номенклатура.Высота * РеализацияТоваров.Товары.Номенклатура.Глубина / 1000000,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес, 0) * РеализацияТоваров.Товары.КоличествоБазовое,
	|		Цена
	|	)
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваров КАК РеализацияТоваров
	|		ПО ТК_ДокументыСкубис.Объект = РеализацияТоваров.Ссылка
	|			И (ТК_ДокументыСкубис.Статус <> 3)";
	
	Рез = Запрос.Выполнить();
	
	ВыборкаДок = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	МассивДанных = Новый Массив;
	
	Пока ВыборкаДок.Следующий() Цикл 
		СрокПоставки = ВыборкаДок.СрокПоставки;
		СрокПоставки = ?(ДеньНедели(СрокПоставки)=6,СрокПоставки+2*24*60*60,СрокПоставки+1*24*60*60);
		СтруктураСтроки = новый Структура;
		СтруктураСтроки.Вставить("unistring",Строка(ВыборкаДок.Ссылка.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("order",Строка(ВыборкаДок.Заказ.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("date",Строка(ВыборкаДок.Дата));
		СтруктураСтроки.Вставить("delivery_date",Строка(СрокПоставки));
		СтруктураСтроки.Вставить("number",СокрЛП(ВыборкаДок.Номер));
		СтруктураСтроки.Вставить("orderType",0);
		СтруктураСтроки.Вставить("branch",Строка(ВыборкаДок.ПодразделениеКомпании.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("counterparty",Строка(ВыборкаДок.Контрагент.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("warehouse",Строка(ВыборкаДок.СкладКомпании.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("waypoints",Строка(ВыборкаДок.ТочкаДоставки.УникальныйИдентификатор()));
		СтруктураСтроки.Вставить("totalAmount",ВыборкаДок.СуммаДокумента);
		ПроверкаНаЗаполнено(СтруктураСтроки,"comment",СокрЛП(ВыборкаДок.Комментарий));
		СтруктураСтроки.Вставить("our_delivery",ВыборкаДок.НашаДоставка);
		СтруктураСтроки.Вставить("posted",ВыборкаДок.Проведен);
		СтруктураСтроки.Вставить("typeDoc",ВыборкаДок.Ссылка.Метаданные().Имя);
		СтруктураСтроки.Вставить("changed",ВыборкаДок.Статус);
		
		МассивТовары = Новый Массив;
		
		Масса = 0;
		Объем = 0;
		
		ВыборкаТовар = ВыборкаДок.Товары.Выбрать();
		Пока ВыборкаТовар.Следующий() Цикл 
			СтруктураСтрокиТовары = Новый Структура;
			СтруктураСтрокиТовары.Вставить("line",ВыборкаТовар.НомерСтроки);
			СтруктураСтрокиТовары.Вставить("product",Строка(ВыборкаТовар.Номенклатура.УникальныйИдентификатор()));
			СтруктураСтрокиТовары.Вставить("productName",СокрЛП(ВыборкаТовар.Номенклатура));
			ПроверкаНаЗаполнено(СтруктураСтрокиТовары,"MRC",?(ЗначениеЗаполнено(ВыборкаТовар.Характеристика),Строка(ВыборкаТовар.Характеристика.УникальныйИдентификатор()),""));
			СтруктураСтрокиТовары.Вставить("quantity",ВыборкаТовар.КоличествоБазовое);
			СтруктураСтрокиТовары.Вставить("price",ВыборкаТовар.Цена);
			МассивТовары.Добавить(СтруктураСтрокиТовары);
			Масса = Масса + ВыборкаТовар.Масса;	
			Объем = Объем + ВыборкаТовар.Объем;	
		КонецЦикла;
		
		ПроверкаНаЗаполнено(СтруктураСтроки,"weight",Масса);
		ПроверкаНаЗаполнено(СтруктураСтроки,"volume",Объем);
		СтруктураСтроки.Вставить("goods",МассивТовары);
		
		МассивДанных.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	МассивОбъектов = ВыгрузитьОбъекты(Рез);
	
	Возврат Новый Структура("Данные,Объекты",МассивДанных,МассивОбъектов);
	
КонецФункции

Функция ПолучитьОперПродажи(НачалоПериода,ОкончаниеПериода)
	
	КешТовара = Новый Соответствие;
	КешСкладов = Новый Соответствие;
	
	ДатаНачала	= СтроковыеФункцииКлиентСервер.СтрокаВДату(НачалоПериода,ЧастиДаты.ДатаВремя);
	ДатаКонца	= СтроковыеФункцииКлиентСервер.СтрокаВДату(ОкончаниеПериода,ЧастиДаты.ДатаВремя);
	
	МассивДанных = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажиХарьков.Склад КАК СкладКод,
	|	ПродажиХарьков.СкладИмя КАК СкладИмя,
	|	ПродажиХарьков.Артикул КАК Артикул,
	|	ПродажиХарьков.Наименование КАК Наименование,
	|	ПродажиХарьков.ХарактИД КАК ХарактИД,
	|	СУММА(ПродажиХарьков.Кол) КАК Колво,
	|	ПродажиХарьков.Дата КАК Дата,
	|	ПродажиХарьков.Время КАК Время,
	|	ПродажиХарьков.ДатаВремя КАК ДатаВремя
	|ИЗ
	|	ВнешнийИсточникДанных.ОпПродажи.Таблица.dbo_vRCPTS КАК ПродажиХарьков
	|ГДЕ
	|	ПродажиХарьков.ДатаВремя МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиХарьков.Склад,
	|	ПродажиХарьков.СкладИмя,
	|	ПродажиХарьков.Артикул,
	|	ПродажиХарьков.Наименование,
	|	ПродажиХарьков.ХарактИД,
	|	ПродажиХарьков.Дата,
	|	ПродажиХарьков.Время,
	|	ПродажиХарьков.ДатаВремя
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВремя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиКиев.Склад КАК СкладКод,
	|	ПродажиКиев.СкладИмя КАК СкладИмя,
	|	ПродажиКиев.Артикул КАК Артикул,
	|	ПродажиКиев.Наименование КАК Наименование,
	|	ПродажиКиев.ХарактИД КАК ХарактИД,
	|	СУММА(ПродажиКиев.Кол) КАК Колво,
	|	ПродажиКиев.Дата КАК Дата,
	|	ПродажиКиев.Время КАК Время,
	|	ПродажиКиев.ДатаВремя КАК ДатаВремя
	|ИЗ
	|	ВнешнийИсточникДанных.ProdaziKiev.Таблица.dbo_vRCPTS КАК ПродажиКиев
	|ГДЕ
	|	ПродажиКиев.ДатаВремя МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиКиев.Склад,
	|	ПродажиКиев.СкладИмя,
	|	ПродажиКиев.Артикул,
	|	ПродажиКиев.Наименование,
	|	ПродажиКиев.ХарактИД,
	|	ПродажиКиев.Дата,
	|	ПродажиКиев.Время,
	|	ПродажиКиев.ДатаВремя
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВремя";
	
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ДатаКонца);
	
	//Тз = Рез.Выгрузить()
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого Рез Из Результат Цикл
		
		Выборка = Рез.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			СкладИД = КешСкладов.Получить(Выборка.СкладКод);
			Если СкладИД = Неопределено Тогда
				Склад = Справочники.СкладыКомпании.НайтиПоКоду(Выборка.СкладКод);
				СкладИД = Строка(Склад.УникальныйИдентификатор());
				КешСкладов.Вставить(Выборка.СкладКод,СкладИД);
			КонецЕсли;
			
			ТоварИД = КешТовара.Получить(Выборка.Артикул);
			Если ТоварИД = Неопределено Тогда
				Артикул = Формат(Выборка.Артикул, "ЧЦ=15; ЧДЦ=0; ЧРД=; ЧГ=0");
				Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
				ТоварИД = Строка(Номенклатура.УникальныйИдентификатор());
				КешТовара.Вставить(Выборка.Артикул,ТоварИД);
			КонецЕсли;
			
			СтруктураСтроки = новый Структура;
			СтруктураСтроки.Вставить("date1",Строка(НачалоПериода));
			СтруктураСтроки.Вставить("date2",Строка(ОкончаниеПериода));
			СтруктураСтроки.Вставить("date",Выборка.ДатаВремя);
			//СтруктураСтроки.Вставить("time",Выборка.Время);
			СтруктураСтроки.Вставить("warehouse",СкладИД);
			СтруктураСтроки.Вставить("product",ТоварИД);
			ПроверкаНаЗаполнено(СтруктураСтроки,"MRC",?(СокрЛП(Выборка.ХарактИД) = "00000000-0000-0000-0000-000000000000","",СокрЛП(Выборка.ХарактИД)));
			СтруктураСтроки.Вставить("quantity",Выборка.Колво);
			МассивДанных.Добавить(СтруктураСтроки);
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

Функция ПолучитьГрафики()
	
	МассивДанных = Новый Массив;
	Проблемные = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_ГрафикиЗаказовСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_ГрафикиЗаказовСрезПоследних.Договор КАК Договор,
	|	ТК_ГрафикиЗаказовСрезПоследних.ДниНедели КАК ДниНедели
	|ПОМЕСТИТЬ Графики
	|ИЗ
	|	РегистрСведений.ТК_ГрафикиЗаказов.СрезПоследних(, Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(перечисление.ВидыКонтрагентов.Внутренний)) КАК ТК_ГрафикиЗаказовСрезПоследних
	|ГДЕ
	|	ТК_ГрафикиЗаказовСрезПоследних.ПериодичностьЗаказа <> ЗНАЧЕНИЕ(перечисление.ВидыПериодичностиЗаказов.Неактуальный)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	СкладыКомпании.ТочкаДоставки КАК ТочкаДоставки
	|ПОМЕСТИТЬ ТочкиДоставки
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Графики.ТорговаяПлощадка КАК ТорговаяПлощадка
	|	ИЗ
	|		Графики КАК Графики) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
	|		ПО ВложенныйЗапрос.ТорговаяПлощадка = СкладыКомпании.ТорговаяПлощадка
	|			И (СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Графики.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТочкиДоставки.ТочкаДоставки КАК ТочкаДоставки,
	|	Графики.Договор.Контрагент КАК Контрагент,
	|	Графики.Договор.Код КАК ДоговорКод,
	|	Графики.Договор КАК Договор,
	|	Графики.ДниНедели КАК ДниНедели,
	|	ТочкиДоставки.ТочкаДоставки ЕСТЬ NULL
	|		ИЛИ ТочкиДоставки.ТочкаДоставки = ЗНАЧЕНИЕ(Справочник.ТочкиДоставки.ПустаяСсылка) КАК Проблема
	|ИЗ
	|	Графики КАК Графики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТочкиДоставки КАК ТочкиДоставки
	|		ПО Графики.ТорговаяПлощадка = ТочкиДоставки.ТорговаяПлощадка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТорговаяПлощадка,
	|	Контрагент
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	
	//Тз = Рез.Выгрузить()
	Рез = Запрос.Выполнить();
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Проблема Тогда
			Если Проблемные.Найти(Выборка.ТорговаяПлощадка) = Неопределено Тогда
				Проблемные.Добавить(Выборка.ТорговаяПлощадка);
			КонецЕсли;
		Иначе
			СтруктураСтроки = новый Структура;
			СтруктураСтроки.Вставить("waypoints",Строка(Выборка.ТочкаДоставки.УникальныйИдентификатор()));
			//СтруктураСтроки.Вставить("counterparty",Строка(Выборка.Контрагент.УникальныйИдентификатор()));
			СтруктураСтроки.Вставить("contract",Выборка.ДоговорКод);
			СтруктураСтроки.Вставить("days",Выборка.ДниНедели);
			МассивДанных.Добавить(СтруктураСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Если Проблемные.Количество() > 0 Тогда
		Проблема = "";
		Для Каждого ТП Из Проблемные Цикл
			Проблема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1%2	(%3)	%4%5",
			Проблема,
			ТП.Код+Лев("          ",10-СтрДлина(ТП.Код)),
			ТП.Global_ID+Лев("          ",10-СтрДлина(ТП.Global_ID)),
			ТП.Наименование,
			Символы.ПС);
		КонецЦикла;
		ОтправитьПисьмо(Проблема);
	КонецЕсли;
	
	Возврат МассивДанных;
	
КонецФункции


#КонецОбласти


#Область ПрограммныйИнтерфейс


Функция ОтправитьДанныеНаHTTP(Команда,Данные,СтруктураЗаписиРезультата) Экспорт
	
	ОК = Истина;
	
	//КодыОтвета = Новый Соответствие;
	//КодыОтвета.Вставить( 0,"без ошибок");
	//КодыОтвета.Вставить( 1,"не заполнено обязательное поле");
	//КодыОтвета.Вставить( 2,"не удалось найти объект по id");
	//КодыОтвета.Вставить( 5,"неверная структура входящих данных");
	//КодыОтвета.Вставить(-1,"неопознанная ошибка");
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+Данные.Количество()+" строк данных для выгрузки.", СтруктураЗаписиРезультата);
	
	Результат = Новый Структура();
	
	Если Данные.Количество() > 0 Тогда
		
		СоединениеНТТР = СоздатьHTTPСоединениеСкубис();
		АдресСтраницыНаСервере = "/"+Команда;
		СтрокаJSON = СтрокаJSON(Данные);
		
		//	Создаем запрос данных методом POST
		ЗапросHTTP = Новый HTTPЗапрос(АдресСтраницыНаСервере);
		ЗапросHTTP.Заголовки.Вставить("Content-type", "application/json");
		//Устанавливает строку, из которого будет прочитано тело POST-запроса.
		ЗапросHTTP.УстановитьТелоИзСтроки(СтрокаJSON, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
		
		Попытка
			//	Ответ от сервера получим в возвращаемом значении типа HTTPОтвет
			ОтветHTTP = СоединениеНТТР.ОтправитьДляОбработки(ЗапросHTTP);
			ОтветJSON = ОтветHTTP.ПолучитьТелоКакСтроку();
			СоединениеНТТР = Неопределено;
			Если ОтветHTTP.КодСостояния = 200 Тогда
				РезультатВыполненияОбработки.ДобавитьКомментарий(ОтветJSON, СтруктураЗаписиРезультата);
			Иначе
				ОК = Ложь;
				РезультатВыполненияОбработки.СообщитьОбОшибке(ОтветJSON, СтруктураЗаписиРезультата);
			КонецЕсли;			
		Исключение
			ОК = Ложь;
			Сервер = СоединениеНТТР.Сервер+":"+СоединениеНТТР.Порт+ЗапросHTTP.АдресРесурса;
			ТекстСообщения = "Сервер "+Сервер+Символы.ПС+"Неудачная попытка выполнить ЗапросHTTP: " + ОписаниеОшибки();
			РезультатВыполненияОбработки.СообщитьОбОшибке(ТекстСообщения, СтруктураЗаписиРезультата);
		КонецПопытки;
		
	КонецЕсли;
	
	Результат.Вставить("ВсеОК",ОК);
	Результат.Вставить("ОтветHTTP",ОтветHTTP);
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьСтатус_Выгружен(СписокОбъектов,ИмяРегистра)
	
	МенеджерЗаписи = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
	Для Каждого Объект Из СписокОбъектов Цикл
		МенеджерЗаписи.Объект = Объект;
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Статус = 3;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецФункции

Процедура ПроверитьДанныеДокументов()
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис Проверка перед Отправкой";
	СтруктураЗаписиРезультата.КлючОбработки = "Проверка Данных Документов";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.ПодразделениеКомпании КАК Объект
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.ПодразделениеКомпании = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.ТипЦен
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.ТипЦен = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ИзменениеЦен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.СкладКомпании
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.СкладКомпании = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И НЕ ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ИзменениеЦен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.СкладКомпанииПолучатель
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.СкладКомпанииПолучатель = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ПеремещениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.Контрагент
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.Контрагент = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И (ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ЗаказПокупателя
	|			ИЛИ ТК_ДокументыСкубис.Объект ССЫЛКА Документ.РеализацияТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.ТочкаДоставки
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.ТочкаДоставки = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И (ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ЗаказПокупателя
	|			ИЛИ ТК_ДокументыСкубис.Объект ССЫЛКА Документ.РеализацияТоваров)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ДокументыСкубис.Объект.СкладКомпанииПолучатель.ТочкаДоставки
	|ИЗ
	|	РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО ТК_ДокументыСкубис.Объект.СкладКомпанииПолучатель.ТочкаДоставки = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ПеремещениеТоваров";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Объект) Тогда
			ЗарегистрироватьВРегистре(Выборка.Объект);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+Выборка.Количество()+ " элементов.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
	
КонецПроцедуры

Процедура ПроверитьТовар()
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис Проверка перед Отправкой";
	СтруктураЗаписиРезультата.КлючОбработки = "Проверка Товара";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ втТовар
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|ГДЕ
	|	ЗаказПокупателяТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ТК_ДокументыСкубис.Объект КАК Объект
	|			ИЗ
	|				РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|			ГДЕ
	|				ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ЗаказПокупателя)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзменениеЦенТовары.Номенклатура,
	|	ИзменениеЦенТовары.ХарактеристикаНоменклатуры
	|ИЗ
	|	Документ.ИзменениеЦен.Товары КАК ИзменениеЦенТовары
	|ГДЕ
	|	ИзменениеЦенТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ТК_ДокументыСкубис.Объект КАК Объект
	|			ИЗ
	|				РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|			ГДЕ
	|				ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ИзменениеЦен)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПеремещениеТоваровТовары.Номенклатура,
	|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ТК_ДокументыСкубис.Объект КАК Объект
	|			ИЗ
	|				РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|			ГДЕ
	|				ТК_ДокументыСкубис.Объект ССЫЛКА Документ.ПеремещениеТоваров)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровТовары.Номенклатура,
	|	РеализацияТоваровТовары.ХарактеристикаНоменклатуры
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	|ГДЕ
	|	РеализацияТоваровТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ТК_ДокументыСкубис.Объект КАК Объект
	|			ИЗ
	|				РегистрСведений.ТК_ДокументыСкубис КАК ТК_ДокументыСкубис
	|			ГДЕ
	|				ТК_ДокументыСкубис.Объект ССЫЛКА Документ.РеализацияТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втТовар.ХарактеристикаНоменклатуры КАК Объект
	|ИЗ
	|	втТовар КАК втТовар
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО втТовар.ХарактеристикаНоменклатуры = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|	И втТовар.ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втТовар.Номенклатура КАК Объект
	|ИЗ
	|	втТовар КАК втТовар
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО втТовар.Номенклатура = ТК_СправочникиСкубис.Объект
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаМРЦ = РезультатЗапроса[1].Выбрать();
	
	Пока ВыборкаМРЦ.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаМРЦ.Объект) Тогда
			ЗарегистрироватьВРегистре(ВыборкаМРЦ.Объект);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+ВыборкаМРЦ.Количество()+ " МРЦ.", СтруктураЗаписиРезультата);
	
	ВыборкаТовар = РезультатЗапроса[2].Выбрать();
	
	Пока ВыборкаТовар.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаТовар.Объект) Тогда
			ЗарегистрироватьВРегистре(ВыборкаТовар.Объект);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель КАК Объект
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель.Родитель.Родитель
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).Родитель.Родитель.Родитель.Родитель.Родитель.Родитель.Родитель = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Номенклатура)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Объект КАК Объект
	|ИЗ
	|	ВТ КАК ВТ
	|ГДЕ
	|	НЕ ВТ.Объект ЕСТЬ NULL
	|	И НЕ ВТ.Объект = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Объект) Тогда
			ЗарегистрироватьВРегистре(Выборка.Объект);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+Выборка.Количество()+ " групп товара.", СтруктураЗаписиРезультата);
	
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
	
КонецПроцедуры

Процедура ПроверитьДанныеСправочников()
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис Проверка перед Отправкой";
	СтруктураЗаписиРезультата.КлючОбработки = "Проверка Данных Справочников";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТочкаДоставки.Владелец КАК Объект
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТочкаДоставки.Владелец = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Контрагенты)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.ТочкиДоставки).Владелец
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.ТочкиДоставки).Владелец = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.Контрагенты)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).Подразделение
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).Подразделение = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ПодразделенияКомпании)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТипЦенРозничнойТорговли
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТипЦенРозничнойТорговли = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТипыЦен)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).ТоварнаяМарка
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.Номенклатура).ТоварнаяМарка = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТоварныеМарки)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТочкаДоставки
	|ИЗ
	|	РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубисЧто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_СправочникиСкубис КАК ТК_СправочникиСкубис
	|		ПО (ВЫРАЗИТЬ(ТК_СправочникиСкубисЧто.Объект КАК Справочник.СкладыКомпании).ТочкаДоставки = ТК_СправочникиСкубис.Объект)
	|			И (ТК_СправочникиСкубис.Объект ССЫЛКА Справочник.ТочкиДоставки)
	|ГДЕ
	|	ТК_СправочникиСкубис.Объект ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Объект КАК Объект
	|ИЗ
	|	ВТ КАК ВТ
	|ГДЕ
	|	НЕ ВТ.Объект ЕСТЬ NULL
	|	И ВТ.Объект ССЫЛКА Справочник.ТипыЦен
	|	И ВТ.Объект ССЫЛКА Справочник.ТоварныеМарки
	|	И ВТ.Объект ССЫЛКА Справочник.ТочкиДоставки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Объект) Тогда
			ЗарегистрироватьВРегистре(Выборка.Объект);
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+Выборка.Количество()+ " элементов.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
	
КонецПроцедуры


Процедура ОтправитьОстатки() Экспорт
	//возврат;
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Отправка";
	СтруктураЗаписиРезультата.КлючОбработки = "Остатки - Stock";
	РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
	
	МассивДанных = ПолучитьОстатки();
	
	Результат = ОтправитьДанныеНаHTTP("stock",МассивДанных,СтруктураЗаписиРезультата);
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Результат.ВсеОК);
	
КонецПроцедуры

Процедура ОтправитьДанные() Экспорт
	
	ПроверитьДанныеДокументов();
	ПроверитьТовар();
	ПроверитьДанныеСправочников();
	
	Данные = Новый СписокЗначений;
	Данные.Добавить("employee"		,"ФизическиеЛица");
	Данные.Добавить("transports"	,"Автотранспорт");
	Данные.Добавить("counterparty"	,"Контрагенты");
	Данные.Добавить("waypoints"		,"ТочкиДоставки");
	Данные.Добавить("branch"		,"Подразделения");
	Данные.Добавить("pricetypes"	,"ТипыЦен");
	Данные.Добавить("shoptype"		,"ВидыСкладов");
	Данные.Добавить("warehouse"		,"СкладыКомпании");
	Данные.Добавить("product_brand"	,"ТоварныеМарки");
	Данные.Добавить("catalog"		,"ГруппыТовара");
	Данные.Добавить("product"		,"Номенклатура");
	Данные.Добавить("product_MRC"	,"Характеристики");
	
	Данные.Добавить("orders"		,"Заказы"		,Истина);
	Данные.Добавить("prices"		,"Переоценки"	,Истина);
	Данные.Добавить("shipments"		,"Отгрузки"		,Истина);
	
	Для Каждого Команда Из Данные Цикл
		
		РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
		СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
		СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Отправка";
		СтруктураЗаписиРезультата.КлючОбработки = Команда.Представление+" - "+Команда.Значение;
		РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
		
		ИмяРегистра = ?(Команда.Пометка,"ТК_ДокументыСкубис","ТК_СправочникиСкубис");
		Номенклатура = Ложь;
		КомандаЗначение = Команда.Значение;
		Если КомандаЗначение = "employee" Тогда
			СтруктураДанных = ПолучитьФизическиеЛица();
		ИначеЕсли КомандаЗначение = "transports" Тогда
			СтруктураДанных = ПолучитьАвтотранспорт();
		ИначеЕсли КомандаЗначение = "counterparty" Тогда
			СтруктураДанных = ПолучитьКонтрагенты();
		ИначеЕсли КомандаЗначение = "waypoints" Тогда
			СтруктураДанных = ПолучитьТочкиДоставки();
		ИначеЕсли КомандаЗначение = "branch" Тогда
			СтруктураДанных = ПолучитьПодразделения();
		ИначеЕсли КомандаЗначение = "priceTypes" Тогда
			СтруктураДанных = ПолучитьТипыЦен();
		ИначеЕсли КомандаЗначение = "shoptype" Тогда
			СтруктураДанных = ПолучитьВидыСкладов();
		ИначеЕсли КомандаЗначение = "warehouse" Тогда
			СтруктураДанных = ПолучитьСкладыКомпании();
		ИначеЕсли КомандаЗначение = "product_brand" Тогда
			СтруктураДанных = ПолучитьТоварныеМарки();
		ИначеЕсли КомандаЗначение = "catalog" Тогда
			СтруктураДанных = ПолучитьГруппыТовара();
		ИначеЕсли КомандаЗначение = "product" Тогда
			Номенклатура = Истина;
			СтруктураДанных = ПолучитьНоменклатура();
		ИначеЕсли КомандаЗначение = "product_MRC" Тогда
			СтруктураДанных = ПолучитьХарактеристики();
		ИначеЕсли КомандаЗначение = "orders" Тогда
			СтруктураДанных = ПолучитьЗаказы();
		ИначеЕсли КомандаЗначение = "prices" Тогда
			СтруктураДанных = ПолучитьПереоценки();
		ИначеЕсли КомандаЗначение = "shipments" Тогда
			СтруктураДанных = ПолучитьОтгрузки();
		КонецЕсли;
		
		Если Номенклатура Тогда
			Результат = ОтправитьДанныеНаHTTP("product",СтруктураДанных.Product.Данные,СтруктураЗаписиРезультата);	
			Результат = ОтправитьДанныеНаHTTP("barcode",СтруктураДанных.Barcode,СтруктураЗаписиРезультата);	
		Иначе
			Результат = ОтправитьДанныеНаHTTP(Команда.Значение,СтруктураДанных.Данные,СтруктураЗаписиРезультата);	
		КонецЕсли;
		
		ОК = Результат.ВсеОК;
		
		Если ОК Тогда
			Если Номенклатура Тогда
				УстановитьСтатус_Выгружен(СтруктураДанных.Product.Объекты,ИмяРегистра);
			Иначе
				УстановитьСтатус_Выгружен(СтруктураДанных.Объекты,ИмяРегистра);
			КонецЕсли;
		КонецЕсли;
		
		РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
		РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,ОК);
		
	КонецЦикла;
	
	//Сообщить("Ответ: " + Ответ.ПолучитьТелоКакСтроку("UTF-8"));
КонецПроцедуры

Функция ОтправитьОперПродажи(Дата1,Дата2) Экспорт
	
	МассивДанных = Новый Массив;
	СтрокаJSON = "";
	ТекстСообщения = "";
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Отдача";
	СтруктураЗаписиРезультата.КлючОбработки = "ОперПродажи - OnepSales";
	РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
	
	Попытка
		МассивДанных = ПолучитьОперПродажи(Дата1,Дата2);
		РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+МассивДанных.Количество()+ " строк данных.", СтруктураЗаписиРезультата);
		СтрокаJSON = СтрокаJSON(МассивДанных);
	Исключение
		ТекстСообщения = ОписаниеОшибки();
		РезультатВыполненияОбработки.СообщитьОбОшибке(ТекстСообщения, СтруктураЗаписиРезультата);
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("СтрокДанных",МассивДанных.Количество());
	Результат.Вставить("СтрокаJSON",СтрокаJSON);
	Результат.Вставить("Ошибка",ТекстСообщения);
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ОтправитьГрафики() Экспорт
	
	МассивДанных = Новый Массив;
	СтрокаJSON = "";
	ТекстСообщения = "";
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Отдача";
	СтруктураЗаписиРезультата.КлючОбработки = "Графики - Charts OUT";
	РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
	
	Попытка
		МассивДанных = ПолучитьГрафики();
		РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+МассивДанных.Количество()+ " строк данных.", СтруктураЗаписиРезультата);
		СтрокаJSON = СтрокаJSON(МассивДанных);
	Исключение
		ТекстСообщения = ОписаниеОшибки();
		РезультатВыполненияОбработки.СообщитьОбОшибке(ТекстСообщения, СтруктураЗаписиРезультата);
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("СтрокДанных",МассивДанных.Количество());
	Результат.Вставить("СтрокаJSON",СтрокаJSON);
	Результат.Вставить("Ошибка",ТекстСообщения);
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПринятьГрафики(МассивДанных) Экспорт
	
	ДаТаОбмена = ТекущаяДата();
	СтрокаJSON = "";
	ТекстСообщения = "";
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Получение";
	СтруктураЗаписиРезультата.КлючОбработки = "Графики - Charts IN";
	РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+МассивДанных.Количество()+ " строк данных.", СтруктураЗаписиРезультата);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого СтрокаМассива Из МассивДанных Цикл
			
			ТочкаДоставкиГуид	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "waypoints"		, "");
			ДоговорКод			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "contract"		, "");
			ДниНедели			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "days"			, "");
			
			//Если НЕ ЗначениеЗаполнено(ТочкаДоставкиГуид)
			//	ИЛИ НЕ ЗначениеЗаполнено(ДоговорКод)
			//	ИЛИ НЕ ЗначениеЗаполнено(ДниНедели) Тогда
			//	ВызватьИсключение("Переданы не все обязательные поля!");	
			//КонецЕсли;
			
			ПериодичностьЗаказа	= ?(СтрДлина(ДниНедели)>1,Перечисления.ВидыПериодичностиЗаказов.Ежедневно,Перечисления.ВидыПериодичностиЗаказов.Еженедельно);
			
			ТочкаДоставки	= Справочники.ТочкиДоставки.ПолучитьСсылку(Новый УникальныйИдентификатор(ТочкаДоставкиГуид));
			Договор			= Справочники.ДоговорыКонтрагентов.НайтиПоКоду(ДоговорКод);
			ТорговаяПлощадка= НайтиТорговуюПлощадкуПоТочкеДоставки(ТочкаДоставки);
			
			Если НЕ ЗначениеЗаполнено(Договор)
				ИЛИ НЕ ЗначениеЗаполнено(ТорговаяПлощадка) Тогда
				ВызватьИсключение("Найдены не все обязательные поля!");	
			КонецЕсли;
			
			ДатаДок = ТекущаяДата();
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТК_ГрафикиЗаказов.Регистратор КАК Регистратор
			|ИЗ
			|	РегистрСведений.ТК_ГрафикиЗаказов КАК ТК_ГрафикиЗаказов
			|ГДЕ
			|	ТК_ГрафикиЗаказов.Период = &Период
			|	И ТК_ГрафикиЗаказов.Договор = &Договор
			|	И ТК_ГрафикиЗаказов.Контрагент = &Контрагент
			|	И ТК_ГрафикиЗаказов.ТорговаяПлощадка = &ТорговаяПлощадка";
			
			Запрос.УстановитьПараметр("Период", НачалоДня(ДатаДок));
			Запрос.УстановитьПараметр("Договор", Договор);
			Запрос.УстановитьПараметр("Контрагент", Договор.Контрагент);
			Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
			
			Рез = Запрос.Выполнить();
			
			Если НЕ Рез.Пустой() Тогда
				Выборка = Рез.Выбрать();
				Выборка.Следующий();
				График = Выборка.Регистратор.ПолучитьОбъект();
				График.УстановитьПометкуУдаления(Истина);
				График.Комментарий = "Удален  СКУБИС "+ДаТаОбмена+Символы.ПС+Выборка.Регистратор.Комментарий;
				График.Записать();
			КонецЕсли;
			
			ГрафикОбъект = Документы.ТК_ГрафикЗаказов.СоздатьДокумент();
			ГрафикОбъект.Дата					= ДатаДок;
			ГрафикОбъект.Автор					= Пользователи.ТекущийПользователь();
			ГрафикОбъект.ХозОперация			= Справочники.ХозОперации.УстановкаГрафикаЗаказа;
			ГрафикОбъект.ПодразделениеКомпании	= ТорговаяПлощадка.ПодразделениеКомпании;
			ГрафикОбъект.ТорговаяПлощадка		= ТорговаяПлощадка;
			ГрафикОбъект.Договор				= Договор;
			ГрафикОбъект.Контрагент				= Договор.Контрагент;
			ГрафикОбъект.ПериодичностьЗаказа	= ПериодичностьЗаказа;
			ГрафикОбъект.ДниНедели				= ДниНедели;
			ГрафикОбъект.СрокПоставки			= 1;
			ГрафикОбъект.РегламентЗаказа		= НЕ Договор.Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Внутренний;
			ГрафикОбъект.Комментарий			= "Записан СКУБИС "+ДаТаОбмена;
			Если ГрафикОбъект.ПроверитьЗаполнение() Тогда
				ГрафикОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ВызватьИсключение("Заполнены не все обязательные поля!");	
			КонецЕсли;
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
		СтрокаJSON = "ОК. Загружено "+МассивДанных.Количество()+" графика.";
	Исключение
		ОтменитьТранзакцию();
		ОписаниеОшибки = ОписаниеОшибки();
		
		Проблема1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1	%2%3	%4%5	%6",
		"waypoints        - ",
		ТочкаДоставкиГуид+Символы.ПС,
		"contract         - ",
		ДоговорКод+Символы.ПС,
		"days             - ",
		ДниНедели+Символы.ПС);
		
		Проблема2 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1	%2%3	%4%5	%6",
		"ТочкаДоставки    - ",
		СокрЛП(ТочкаДоставки)+Символы.ПС,
		"Договор          - ",
		СокрЛП(Договор)+Символы.ПС,
		"ТорговаяПлощадка - ",
		СокрЛП(ТорговаяПлощадка)+Символы.ПС);
		
		ТекстСообщения = Проблема1+Проблема2+ОписаниеОшибки;
		РезультатВыполненияОбработки.СообщитьОбОшибке(ТекстСообщения, СтруктураЗаписиРезультата);
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("СтрокДанных",МассивДанных.Количество());
	Результат.Вставить("СтрокаJSON",СтрокаJSON);
	Результат.Вставить("Ошибка",ТекстСообщения);
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,ТекстСообщения = "");
	
	Возврат Результат;
	
КонецФункции

Функция ПринятьМаршруты(МассивДанных) Экспорт
	
	ДаТаОбмена = ТекущаяДата();
	СтрокаJSON = "";
	ТекстСообщения = "";
	
	РезультатВыполненияОбработки = РегистрыСведений.РезультатыВыполненияОбработок;	
	СтруктураЗаписиРезультата = РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиРезультата.ИдентификаторОбработки = "Скубис_Получение";
	СтруктураЗаписиРезультата.КлючОбработки = "Маршруты - Routes";
	РезультатВыполненияОбработки.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ДобавитьКомментарий("Найдено "+МассивДанных.Количество()+ " строк данных.", СтруктураЗаписиРезультата);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого СтрокаМассива Из МассивДанных Цикл
			
			Номер		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "id"		, "");
			Дата		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "date"		, "");
			Водитель	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "driver"	, "");
			Автомобиль	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "transport"	, "");
			Накладные	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтрокаМассива, "sections"	, "");
			
			//Если НЕ ЗначениеЗаполнено(Номер)
			//	ИЛИ НЕ ЗначениеЗаполнено(Дата)
			//	ИЛИ НЕ ЗначениеЗаполнено(Водитель)
			//	ИЛИ НЕ ЗначениеЗаполнено(Автомобиль)
			//	ИЛИ НЕ ЗначениеЗаполнено(Накладные) Тогда
			//	ВызватьИсключение("Переданы не все обязательные поля!");	
			//КонецЕсли;
			
			Автомобиль	= Справочники.Автотранспорт.ПолучитьСсылку(Новый УникальныйИдентификатор(Автомобиль));
			Водитель	= Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(Водитель));
			Перевозчик	= Справочники.Контрагенты.НайтиПоКоду("43648497");	//	Торонто Компани ООО
			
			//Если НЕ ЗначениеЗаполнено(Автомобиль)
			//	ИЛИ НЕ ЗначениеЗаполнено(Водитель)
			//	ИЛИ НЕ ЗначениеЗаполнено(Перевозчик) Тогда
			//	//ВызватьИсключение("Получены не все обязательные поля!");	
			//КонецЕсли;
			
			ДатаДок = ТекущаяДата();
			
			ДокТТН = Документы.ТК_ТТН.СоздатьДокумент();
			ДокТТН.Дата				= ДатаДок;
			ДокТТН.Автор			= Пользователи.ТекущийПользователь();
			ДокТТН.ХозОперация		= Справочники.ХозОперации.ТТН;
			ДокТТН.ПунктПолучатель	= "Согласно накладной";
			ДокТТН.Автомобиль		= Автомобиль;	
			ДокТТН.Водитель			= Водитель;	
			ДокТТН.Перевозчик		= Перевозчик;	
			ДокТТН.Статус			= Перечисления.СтатусыТТН.Черновик;	
			ДокТТН.Комментарий		= "Записан СКУБИС (№"+Номер+") 	"+ДаТаОбмена;
			
			//МаршрутРазвозки = Справочники.Маршруты.НайтиПоКоду(Маршрут);
			//
			//Если ЗначениеЗаполнено(МаршрутРазвозки) Тогда
			//	ДокТТН.Экспедитор = МаршрутРазвозки.Экспедитор;	
			//КонецЕсли;
			
			Для Каждого Накладная Из Накладные Цикл
				
				ДокГУИД	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Накладная, "TaskId"	, "");
				ДокТип	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Накладная, "typeDoc"	, "");
				Масса	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Накладная, "weight"	, 0);
				
				//Если НЕ ЗначениеЗаполнено(ДокГУИД)
				//	ИЛИ НЕ ЗначениеЗаполнено(ДокТип) Тогда
				//	ВызватьИсключение("Переданы не все обязательные поля!");	
				//КонецЕсли;
				
				ДокРасход	= Документы[ДокТип].ПолучитьСсылку(Новый УникальныйИдентификатор(ДокГУИД));
				
				//Если СтрНайти(Строка(ДокРасход),"<Объект не найден>") > 0 Тогда
				//	ВызватьИсключение("Не найден Документ "+ДокТип);	
				//КонецЕсли;
				
				строка = ДокТТН.Источники.Добавить();
				строка.Документ = ДокРасход;
				Строка.Масса = Масса;
			КонецЦикла;
			
			Организация = ДокРасход.Организация;
			ДокТТН.Организация	= Организация;	
			ДокТТН.Заказчик		= Организация.Контрагент;
			ДокТТН.Отправитель	= Организация.Контрагент.НаименованиеПолное;
			
			ТорговаяПлощадка = ДокРасход.СкладКомпании.ТорговаяПлощадка;
			ДокТТН.ПунктОтправитель = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТорговаяПлощадка, УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("ФактическийАдресОбъекта"),ТекущаяДата());
			ДокТТН.ПодразделениеКомпании = ДокРасход.ПодразделениеКомпании;	
			
			Если ДокТТН.ПроверитьЗаполнение() Тогда
				ДокТТН.Записать();//РежимЗаписиДокумента.Проведение);
			Иначе
				ВызватьИсключение("Заполнены не все обязательные поля!");	
			КонецЕсли;
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
		СтрокаJSON = "ОК. Загружено "+МассивДанных.Количество()+" маршрута.";
	Исключение
		ОтменитьТранзакцию();
		ОписаниеОшибки = ОписаниеОшибки();
		
		//Проблема1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1	%2%3	%4%5	%6%7	%8",
		//"waypoints		",
		//"ТочкаДоставкиГуид+Символы.ПС",
		//"counterparty	",
		//"КонтрагентГуид+Символы.ПС",
		//"contract		",
		//"ДоговорКод+Символы.ПС",
		//"days			",
		//"ДниНедели+Символы.ПС");
		//
		//Проблема2 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1	%2%3	%4%5	%6%7	%8",
		//"ТочкаДоставки		",
		//"СокрЛП(ТочкаДоставки)+Символы.ПС",
		//"Контрагент			",
		//"СокрЛП(Контрагент)+Символы.ПС",
		//"Договор			",
		//"СокрЛП(Договор)+Символы.ПС",
		//"ТорговаяПлощадка	",
		//"СокрЛП(ТорговаяПлощадка)+Символы.ПС");
		//
		//ТекстСообщения = Проблема1+Проблема2+ОписаниеОшибки;
		ТекстСообщения = ОписаниеОшибки;
		РезультатВыполненияОбработки.СообщитьОбОшибке(ТекстСообщения, СтруктураЗаписиРезультата);
	КонецПопытки;
	
	Результат = Новый Структура;
	Результат.Вставить("СтрокДанных",МассивДанных.Количество());
	Результат.Вставить("СтрокаJSON",СтрокаJSON);
	Результат.Вставить("Ошибка",ТекстСообщения);
	
	РезультатВыполненияОбработки.ДобавитьКомментарий("Окончание обработки.", СтруктураЗаписиРезультата);
	РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,ТекстСообщения = "");
	
	Возврат Результат;
	
КонецФункции




#КонецОбласти




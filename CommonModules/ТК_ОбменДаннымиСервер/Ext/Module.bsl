
Функция СтруктураОтвета()

	СтруктураОтвета =  Новый Структура("Success,Description,Загружено",Истина,"",0);
	Возврат СтруктураОтвета;

КонецФункции

Функция ПроверитьБлокировкуИБ()

	Возврат ОбновлениеИнформационнойБазыСлужебный.ИнформационнаяБазаЗаблокированаДляОбновления();
	
КонецФункции

Функция ВыполнитьЗагрузкуЧерезУниверсальныйОбменXML(КлючОбработки,ИдентификаторОбработки,СообщениеОбмена)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтветСтруктура = СтруктураОтвета();

	СтруктруаЗаписиРезультатаВыполнения = РегистрыСведений.ТК_РезультатыВыполненияОбмена.СформироватьСтруктуруЗаписиРезультатов();
	СтруктруаЗаписиРезультатаВыполнения.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктруаЗаписиРезультатаВыполнения.КлючОбработки = КлючОбработки;
	
	РегистрыСведений.ТК_РезультатыВыполненияОбмена.ДобавитьКомментарий(ИмяКомпьютера(), СтруктруаЗаписиРезультатаВыполнения, Неопределено);
	РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, Ложь);

	ИБЗаблокированаДляОбновления =  ПроверитьБлокировкуИБ();
	
	Если ЗначениеЗаполнено(ИБЗаблокированаДляОбновления) Тогда
		
		
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.СообщитьОбОшибке(ИБЗаблокированаДляОбновления, СтруктруаЗаписиРезультатаВыполнения);
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, Ложь);
		
		ОтветСтруктура.Success = Ложь;
		ОтветСтруктура.Description = ИБЗаблокированаДляОбновления;
		
		Возврат  ОтветСтруктура;
		
	КонецЕсли;

	
	
	
	Попытка   
		
		Каталог = КаталогВременныхФайлов();

		
		ФайлПротоколаОбмена = Каталог+"\"+Формат(ТекущаяДатаСеанса(),"ДФ=yyyyMMddччммсс")+"_"+КлючОбработки+"_.txt";
		
		//ПолучитьИмяВременногоФайла("txt");
		
		ОбработкаОбмена = Обработки.УниверсальныйОбменДаннымиXML.Создать();
		ОбработкаОбмена.РежимОбмена = "Загрузка";
		ОбработкаОбмена.НеВыводитьНикакихИнформационныхСообщенийПользователю = Истина;
		ОбработкаОбмена.ЗагружатьДанныеВРежимеОбмена = Истина;
		ОбработкаОбмена.ЗаписыватьВИнформационнуюБазуТолькоИзмененныеОбъекты = Истина;
		ОбработкаОбмена.ОптимизированнаяЗаписьОбъектов = Истина;
		ОбработкаОбмена.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
		ОбработкаОбмена.ИмяФайлаПротоколаОбмена = ФайлПротоколаОбмена;
		
		РаботаВозможна = ОбработкаОбмена.ВыполнитьДействияПередЧтениемДанных(СообщениеОбмена.Получить());
		
		Если НЕ РаботаВозможна Тогда
			КодСостояния = "ОбработкаОбмена.ВыполнитьДействияПередЧтениемДанных - загрузка не возможна";
			
			РегистрыСведений.ТК_РезультатыВыполненияОбмена.СообщитьОбОшибке("КодСостояния "+КодСостояния, СтруктруаЗаписиРезультатаВыполнения);
			РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, Ложь);
			
			ОтветСтруктура.Success = Ложь;
			ОтветСтруктура.Description = КодСостояния;

			Возврат  ОтветСтруктура;
			
		КонецЕсли;
		
		Ошибки = "";
		ОбработкаОбмена.ПроизвестиЧтениеДанных(Ошибки);
		
		Если Не ПустаяСтрока(Ошибки) Тогда
			КодСостояния = 	"ОбработкаОбмена.ПроизвестиЧтениеДанных - загрузка не возможна";
			
			РегистрыСведений.ТК_РезультатыВыполненияОбмена.СообщитьОбОшибке("КодСостояния "+КодСостояния, СтруктруаЗаписиРезультатаВыполнения);
			РегистрыСведений.ТК_РезультатыВыполненияОбмена.СообщитьОбОшибке("Описание "+Ошибки, СтруктруаЗаписиРезультатаВыполнения);
			РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, Ложь);
			
			ОтветСтруктура.Success = Ложь;
			ОтветСтруктура.Description = КодСостояния;
			
			Возврат ОтветСтруктура;

		КонецЕсли;
		
		ОбработкаОбмена.ВыполнитьДействияПослеЗавершенияЧтенияДанных(); 
		
		ЧтениеТекста = Новый ЧтениеТекста(ФайлПротоколаОбмена);
		ТекстПротокола = ЧтениеТекста.Прочитать();
		ЧтениеТекста.Закрыть();
		УдалитьФайлы(ФайлПротоколаОбмена);
		
		
		//Проверим протокол обмена
		ВсеОк = Истина;
		 Загружено = 0;
		Для ст = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл 
			тст  = СтрПолучитьСтроку(ТекстПротокола,ст);
			Если  СтрНайти(тст,"Загружено объектов:") > 0 Тогда
				Попытка
				Загружено = Число(СокрЛП(СтрЗаменить(тст,"Загружено объектов:","")));
			Исключение
				КонецПопытки;
			ИначеЕсли СтрНайти(тст,"Ошибка")> 0 Тогда
				ВсеОк = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ОтветСтруктура.Загружено = Загружено;

		ОтветСтруктура.Success = ВсеОк;
		ОтветСтруктура.Description = ТекстПротокола;

		
		
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.ДобавитьКомментарий(ТекстПротокола, СтруктруаЗаписиРезультатаВыполнения, Неопределено);
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, ВсеОк);
		
	Исключение
		КодСостояния =  "Описание ошибки "+ОписаниеОшибки();
		
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.СообщитьОбОшибке(КодСостояния, СтруктруаЗаписиРезультатаВыполнения);
		РегистрыСведений.ТК_РезультатыВыполненияОбмена.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктруаЗаписиРезультатаВыполнения, Ложь);
		
		ОтветСтруктура.Success = Ложь;
		ОтветСтруктура.Description = КодСостояния;
	
		
	КонецПопытки;
	
	
	Возврат ОтветСтруктура;
	
КонецФункции

Функция СформироватьПакетДанных(ПравилаОбмена,ЗначенияПараметровXDTO, ПродолжитьОбмен = Ложь, СтатусВыгрузки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	

		
	ЧтениеХМЛ = Новый ЧтениеXML;
	
	ЧтениеХМЛ.УстановитьСтроку(ПравилаОбмена.Получить());
	ЧтениеХМЛ.Прочитать();
	
	Обмен = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	
	ФайлПротокола = ПолучитьИмяВременногоФайла(".txt");
	Обмен.ИмяФайлаПротоколаОбмена = ФайлПротокола;

	Обмен.РежимОбмена = "Выгрузка";
	ИмяВремФайла = ПолучитьИмяВременногоФайла("xml");
	Обмен.ИмяФайлаОбмена = ИмяВремФайла;
	Обмен.ИмяФайлаПравилОбмена ="ЧтениеXML";	
	Обмен.ЗагрузитьПравилаОбмена(ЧтениеХМЛ,"ЧтениеXML");
	

		// Параметры
	ЗначенияПараметров = СериализаторXDTO.ПрочитатьXDTO(ЗначенияПараметровXDTO);
	Если ЗначениеЗаполнено(ЗначенияПараметров) Тогда
		Для каждого КлючИЗнач Из ЗначенияПараметров Цикл
			Обмен.УстановитьЗначениеПараметраВТаблице(КлючИЗнач.Ключ, КлючИЗнач.Значение);
		КонецЦикла; 
	КонецЕсли; 

	Обмен.ЗагружатьДанныеВРежимеОбмена = Истина;
	Обмен.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
	
	
	Обмен.ВыполнитьВыгрузку();
	
	
	/// читаем логи
	ЧтениеТекстаПротокола = Новый ЧтениеТекста;
	ЧтениеТекстаПротокола.Открыть(ФайлПротокола, КодировкаТекста.Системная);
	
	ТекстПротокола = ЧтениеТекстаПротокола.Прочитать();

	ВсеОк = Истина;
	Выгружено = 0;
	Для ст = 1 По СтрЧислоСтрок(ТекстПротокола) Цикл 
		тст  = СтрПолучитьСтроку(ТекстПротокола,ст);
		Если  СтрНайти(тст,"Выгружено объектов:") > 0 Тогда
			Попытка
				Выгружено = Число(СокрЛП(СтрЗаменить(тст,"Выгружено объектов:","")));
			Исключение
			КонецПопытки;
		ИначеЕсли СтрНайти(ВРег(тст),ВРег("Ошибка"))> 0 Тогда
			ВсеОк = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	СтатусВыгрузки = ВсеОк;	
	ЧтениеТекстаПротокола.Закрыть();
	УдалитьФайлы(ФайлПротокола);

	
	
	///Результат выгрузки данных
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ИмяВремФайла, КодировкаТекста.UTF8);
	Результат = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ИмяВремФайла);
	
	ХранилищеДанных = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));

	
	//Заполняем параметр ПродолжитьОбмен
	ТипОбмена = "";
	КодУзла = "";
	ЗначенияПараметров.Свойство("ТипОбмена", ТипОбмена);
	ЗначенияПараметров.Свойство("КодУзла", КодУзла);
	
	ОбъектВыгрузки ="";
	Если ТипОбмена = "ОбменТК_Center" Тогда
		 УзелОбмена = ПланыОбмена.ОбменТК_Center.НайтиПоКоду(КодУзла);
		 ОбъектВыгрузки = "Документ.";
	КонецЕсли;
	
	
	ШаблонЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	СписокОбъектов.Ссылка
	|ИЗ
	|	%ТипОбъекта%.Изменения КАК СписокОбъектов
	|ГДЕ
	|	СписокОбъектов.Узел = &Узел";
	
	текЗапрос = СтрЗаменить(ШаблонЗапроса, "%ТипОбъекта%", ОбъектВыгрузки+ЗначенияПараметров.ДокументВыгрузки);
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = текЗапрос;
	Запрос.УстановитьПараметр("Узел", УзелОбмена);
	ПродолжитьОбмен = Не Запрос.Выполнить().Пустой();	

	
	Возврат ХранилищеДанных;
	
	
КонецФункции



Функция СформироватьПакетДанныхДопОбработки(ИмяОбработки, ИмяФункции, Параметры) Экспорт 
	
	ТипПараметров = ТипЗнч(Параметры);
	Если ТипПараметров = Тип("ОбъектXDTO") Тогда 	
		ПараметрыОбработки = СериализаторXDTO.ПрочитатьXDTO(Параметры);
	ИначеЕсли ТипПараметров <> Тип("Структура") Тогда 
		ПараметрыОбработки = Новый Структура;
	КонецЕсли;
	
	Результат = Новый Структура;
	ВсеОк = Истина;
	Описание = "";
	
	РезультатОбработки = ПолучитьРезультатДополнительнойОбработки(ИмяОбработки, ИмяФункции, ПараметрыОбработки, ВсеОк, Описание);
	
	Если ВсеОк И ТипЗнч(РезультатОбработки) = Тип("Структура") Тогда 		
		Результат.Вставить("Result", РезультатОбработки);
	КонецЕсли;
	
	Результат.Вставить("Succes", ВсеОк);
	
	Если ЗначениеЗаполнено(Описание) Тогда 
		Результат.Вставить("Description", Описание);
	КонецЕсли;
	
	ХранилищеДанных = Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9));
	
	Возврат ХранилищеДанных;	
	
КонецФункции

Функция ПолучитьРезультатДополнительнойОбработки(ИмяОбработки, ИмяФункции, ПараметрыОбработки, ВсеОк = Истина, Описание = "") Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбработкаСсылка = Неопределено;
	РезультатОбработки = Неопределено;
	
	Если Не ЗначениеЗаполнено(ИмяОбработки) Тогда 
		ВсеОк = Ложь;
		Описание = "Не передано имя обработки!";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяОбработки) Тогда 
		ВсеОк = Ложь;
		Описание = "Не передано имя функции!";
	КонецЕсли;
	
	Если ВсеОк Тогда 
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИмяОбъекта", ИмяОбработки);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДополнительныеОтчетыИОбработки.Ссылка КАК Ссылка,
		               |	ДополнительныеОтчетыИОбработки.ПометкаУдаления КАК ПометкаУдаления,
		               |	ДополнительныеОтчетыИОбработки.БезопасныйРежим КАК БезопасныйРежим,
		               |	ДополнительныеОтчетыИОбработки.Публикация КАК Публикация
		               |ИЗ
		               |	Справочник.ДополнительныеОтчетыИОбработки КАК ДополнительныеОтчетыИОбработки
		               |ГДЕ
		               |	ДополнительныеОтчетыИОбработки.ИмяОбъекта = &ИмяОбъекта
		               |	И НЕ ДополнительныеОтчетыИОбработки.ЭтоГруппа";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда 
			Если Выборка.ПометкаУдаления Тогда 
				ВсеОк = Ложь;
				Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обработка ""%1"" помечена на удаление'; uk = 'Обробака ""%1"" помічена на видалення'"),
							ИмяОбработки);
 
			Иначе
				ОбработкаСсылка = Выборка.Ссылка;
			КонецЕсли;
		Иначе
			ВсеОк = Ложь;
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
							НСтр("ru = 'Не найдена обработка ""%1""'; uk = 'Не знайдена обрбаотка ""%1""'"),
							ИмяОбработки);
							
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВсеОк И ОбработкаСсылка <> Неопределено Тогда 
		Попытка
			ОбработкаОбъект = ДополнительныеОтчетыИОбработки.ОбъектВнешнейОбработки(ОбработкаСсылка);		
			РезультатОбработки = Неопределено;
			
			Выполнить("РезультатОбработки = ОбработкаОбъект." + ИмяФункции + "(ПараметрыОбработки)");
		Исключение
			ВсеОк = Ложь;
			Описание = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	
	Если ВсеОк И ТипЗнч(РезультатОбработки) = Тип("Структура") Тогда 		
		ВсеОк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатОбработки, "Успешно", Ложь);
		Описание = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатОбработки, "Описание", Неопределено);
	КонецЕсли;
	
	
	Возврат РезультатОбработки;
	
КонецФункции

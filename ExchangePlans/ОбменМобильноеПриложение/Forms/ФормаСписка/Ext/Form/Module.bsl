
&НаКлиенте
Процедура Подключить(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню", ЭтотОбъект);
	СписокПользователей = ПолучитьСписокПользователей();
	
	Если СписокПользователей.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нет доступных пользователей для подключения");
		Возврат;
	КонецЕсли;
	
	ПоказатьВыборИзМеню(Оповещение, СписокПользователей);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
КонецПроцедуры
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "НастройкаМобильногоПриложенияГотово" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокПользователей()
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокПользователей = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Ссылка,
		|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Служебный = ЛОЖЬ
		|	И Пользователи.Недействителен = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если Пользователи.ЭтоПолноправныйПользователь(ВыборкаДетальныеЗаписи.Ссылка) Тогда Продолжить; КонецЕсли;
		
		СтруктураВозврата = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВыборкаДетальныеЗаписи.ИдентификаторПользователяИБ);
		Если СтруктураВозврата <> Неопределено И СтруктураВозврата.АутентификацияСтандартная И Пользователи.РолиДоступны("ТК_ОбменМобильноеПриложение",ВыборкаДетальныеЗаписи.Ссылка) И НЕ ПланыОбмена.ОбменМобильноеПриложение.УзелОбменаПользователяСуществует(ВыборкаДетальныеЗаписи.Ссылка) Тогда
			Логин = СтруктураВозврата.Имя;
			СписокПользователей.Добавить(ВыборкаДетальныеЗаписи.Ссылка, Логин,,БиблиотекаКартинок.Пользователь);
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СписокПользователей;
	
КонецФункции

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено И ЗначениеЗаполнено(ВыбранныйЭлемент.Значение) Тогда
		ОткрытьФорму("ПланОбмена.ОбменМобильноеПриложение.ФормаОбъекта", Новый Структура("Пользователь", ВыбранныйЭлемент.Значение));
	КонецЕсли;

КонецПроцедуры




&НаСервереБезКонтекста
Процедура ЗарегистрироватьНаУзлуОбменаНаСервере(массивСсылок)
	
	Для Каждого тСсылка Из массивСсылок Цикл 
		ОбменМобильноеПриложение.ЗапуститьФоновоеЗаданиеРегистрацииПоУзлуОбмена(тСсылка,тСсылка.Код);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьНаУзлуОбмена(Команда)
	
	массивСсылок = Элементы.Список.ВыделенныеСтроки;
	ЗарегистрироватьНаУзлуОбменаНаСервере(массивСсылок);
	
КонецПроцедуры

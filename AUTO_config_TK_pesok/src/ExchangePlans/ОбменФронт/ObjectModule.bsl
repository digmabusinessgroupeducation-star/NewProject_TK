#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Функция Узел_OS()Экспорт
//	Узел = ОбменФронтПовтИсп.НайтиПоКоду("000000001");
//	Возврат Узел; 
//КонецФункции



// Возвращает регламентное, соответствующее объекту
//
// Параметры:
// нет
// 
// Возвращаемое значение:
// РегламентноеЗадание
//
Функция РегламентноеЗадание() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторРегламентногоЗадания);
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Задание;
	
КонецФункции

// Удаляет регламентное задание, соответствующее объекту
//
// Параметры:
// нет
// 
// Возвращаемое значение:
// нет
//
Процедура УдалитьРегламентноеЗадание() Экспорт
	
	Задание = РегламентноеЗадание();
	Если НЕ Задание = НеОпределено Тогда
		УстановитьПривилегированныйРежим(Истина);
		Задание.Удалить();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	Код = "";
	//Подразделение = Неопределено;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		СформироватьНаименование();                
	КонецЕсли;
	
	Если ВыгружатьОстатки И ВыгружатьТекущиеОстатки Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("Установлено два варианта выгрузки остатков, можно использоват только один",ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции



// Формирует уникальное наименование объекта
//
// Параметры:
// нет
// 
// Возвращаемое значение:
// нет
//
Процедура СформироватьНаименование()
	
		
	Префикс = НСтр("ru='Выгрузка';uk='Вивантаження'");
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(ОбменФронт.Наименование) КАК Наименование
	               |ИЗ
	               |	ПланОбмена.ОбменФронт КАК ОбменФронт
	               |ГДЕ
	               |	ОбменФронт.Наименование ПОДОБНО &Шаблон
	               |
	               |ИМЕЮЩИЕ
	               |	НЕ МАКСИМУМ(ОбменФронт.Наименование) ЕСТЬ NULL ";

	
	Запрос.УстановитьПараметр("Шаблон", Префикс + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Наименование = Префикс;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Суффикс = Прав(СокрЛП(Выборка.Наименование), 4);
	
	Попытка
		СуффиксЧислом = Число(Суффикс);
	Исключение
		Наименование = Префикс + " 0001";
		Возврат;
	КонецПопытки;
		
	Наименование = Префикс + " " + Формат(СуффиксЧислом + 1, "ЧЦ=4; ЧВН=; ЧГ=");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

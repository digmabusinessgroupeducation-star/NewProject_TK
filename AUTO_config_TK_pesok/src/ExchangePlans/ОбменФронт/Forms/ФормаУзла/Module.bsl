
#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УзелОбменаЭтаИБ = ПроверитьУзелОбменаЭтаИБСервер();
	
	Если УзелОбменаЭтаИБ Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если УзелОбменаЭтаИБ Тогда
		
		ОчиститьСообщения();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru='Узел соответствует этой информационной базе и не может использоваться в обмене. 
		|Используйте другой узел обмена или создайте новый.'
		|;uk='Вузол відповідає цій інформаційній базі і не може використовуватися у обміні. 
		|Використовуйте інший вузол обміну або створіть новий.'"));
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	СформироватьЗаголовокПараметрыВыгрузки();
	
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Задание = ТекущийОбъект.РегламентноеЗадание();
	
	Если НЕ Задание = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Задание.Расписание;
	КонецЕсли;
	
	ПриСозданииЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания
		И (РасписаниеРегламентногоЗадания = Неопределено
		ИЛИ (ОбщегоНазначения.РазделениеВключено()
		И Не РасписаниеРегламентногоЗадания.ПериодПовтораВТечениеДня > 0)) Тогда
		
		ТекущийОбъект.ИспользоватьРегламентныеЗадания = Ложь;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТекущийОбъект.ИспользоватьРегламентныеЗадания Тогда
		
		Задание = ТекущийОбъект.РегламентноеЗадание();
		Если Задание = НеОпределено Тогда
			Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание("ОбменФронт");
			Задание.Использование = Истина;
			Задание.Ключ = Строка(Новый УникальныйИдентификатор);
			Задание.Наименование = ТекущийОбъект.Наименование;
			ТекущийОбъект.ИдентификаторРегламентногоЗадания = Задание.УникальныйИдентификатор;
		КонецЕсли;
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(ТекущийОбъект.Код);
		Задание.Параметры = ПараметрыЗадания;
		Задание.Расписание = РасписаниеРегламентногоЗадания;
		
		Задание.Записать();
		
	Иначе
		
		ТекущийОбъект.УдалитьРегламентноеЗадание();
		ТекущийОбъект.ИдентификаторРегламентногоЗадания = НеОпределено;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти





#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПроверитьПодключение(НастройкиПодключения, ТекстПредупреждения)
	
	ОбменФронт.ВыполнитьТестовоеПодключение(НастройкиПодключения, ТекстПредупреждения);
	
КонецПроцедуры


&НаКлиенте
Процедура СформироватьЗаголовокПараметрыВыгрузки()
	
	ШаблонЗаголовка = "";
	Если Объект.РазмерПорции > 0 Тогда
		ШаблонЗаголовка = НСтр("ru='Количество товаров в порции %1;';uk='Кількість товарів в порції %1;'");
	КонецЕсли;
	
	Если Объект.КоличествоПовторений > 0 Тогда
		ШаблонЗаголовка = ШаблонЗаголовка + НСтр("ru='Количество попыток обмена %2.';uk='Кількість спроб обміну %2.'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШаблонЗаголовка) Тогда
		ЗаголовокКнопки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, Объект.РазмерПорции,
		Объект.КоличествоПовторений);
	Иначе
		ЗаголовокКнопки = НСтр("ru='Параметры порционной выгрузки данных.';uk='Параметри порційного вивантаження даних.'");
	КонецЕсли;
	
	Элементы.РасширеннаяНастройка.Заголовок = ЗаголовокКнопки;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьУзелОбменаЭтаИБСервер()
	
	ЭтотУзел = ПланыОбмена.ОбменФронт.ЭтотУзел();
	Возврат Объект.Ссылка = ЭтотУзел;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьНастройкуРасписанияОбмена()
	
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьРасписаниеОбмена", ЭтотОбъект);
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРасписаниеОбмена(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("РасписаниеРегламентногоЗадания") Тогда
		РасписаниеРегламентногоЗадания = Результат;
		
		УстановитьНадписьРасписанияОбмена();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьРасписанияОбмена()
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если Не ПараметрыРаботыКлиента.РазделениеВключено Тогда
		
		Если РасписаниеРегламентногоЗадания = Неопределено Тогда
			ТекстЗаголовка = НСтр("ru='Настроить расписание обмена';uk='Настроїти розклад обміну'");
		Иначе
			ТекстЗаголовка = РасписаниеРегламентногоЗадания;
		КонецЕсли;
		
		Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
		
	Иначе
		
		Если РасписаниеРегламентногоЗадания = Неопределено Тогда
			
			ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
			
		Иначе
			
			ЗначениеПериода = РасписаниеРегламентногоЗадания.ПериодПовтораВТечениеДня;
			Если ЗначениеПериода = 0 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 300 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 5 минут';uk='Один раз у 5 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 900 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 15 минут';uk='Один раз в 15 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 1800 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 3600 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в час';uk='Один раз в годину'");
				
			ИначеЕсли ЗначениеПериода <= 10800 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 3 часа';uk='Один раз в 3 години'");
				
			ИначеЕсли ЗначениеПериода <= 21600 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 6 часов';uk='Один раз на 6 годин'");
				
			ИначеЕсли ЗначениеПериода <= 43200 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 12 часов';uk='Один раз на 12 годин'");
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Заполняет значения расписания регламентного задания.
//
Процедура УстановитьРасписаниеРегламентногоЗадания()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	ПериодПовтораВТечениеДня = ПолучитьПериодПовтораВТечениеДня();
	
	Если ПериодПовтораВТечениеДня > 0 Тогда
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.Месяцы					= Месяцы;
		Расписание.ДниНедели				= ДниНедели;
		Расписание.ПериодПовтораВТечениеДня = ПериодПовтораВТечениеДня; // секунды
		Расписание.ПериодПовтораДней		= 1; // каждый день
		
		РасписаниеРегламентногоЗадания = Расписание;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Функция возвращает ПериодПовтораВТечениеДня в секундах
//
Функция ПолучитьПериодПовтораВТечениеДня()
	
	ЗначенияВыбора = СоответствиеЗначенийВыбораККоличествуСекунд();
	
	ПериодПовтораВТечениеДня = ЗначенияВыбора.Получить(ИнтервалОбмена);
	Возврат ?(ПериодПовтораВТечениеДня = Неопределено, 1800, ПериодПовтораВТечениеДня);
	
КонецФункции //ПолучитьПериодПовтораВТечениеДня()
&НаКлиенте
// Функция возвращает соответствие надписей выбора к количеству секунд
// 
Функция СоответствиеЗначенийВыбораККоличествуСекунд()
	
	СоответствиеНадписей = Новый Соответствие;
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 5 минут';uk='Один раз у 5 хвилин'"), 300);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 15 минут';uk='Один раз в 15 хвилин'"), 900);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'"), 1800);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в час';uk='Один раз в годину'"), 3600);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 3 часа';uk='Один раз в 3 години'"), 10800);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 6 часов';uk='Один раз на 6 годин'"), 21600);
	СоответствиеНадписей.Вставить(НСтр("ru='Один раз в 12 часов';uk='Один раз на 12 годин'"), 43200);
	
	Возврат СоответствиеНадписей;
	
КонецФункции //СоответствиеЗначенийВыбораККоличествуСекунд()


&НаСервере
Процедура УстановитьНадписьРасписанияОбменаСервер()
	
	Если Не ОбщегоНазначения.РазделениеВключено() Тогда
		
		Если РасписаниеРегламентногоЗадания = Неопределено Тогда
			ТекстЗаголовка = НСтр("ru='Настроить расписание обмена';uk='Настроїти розклад обміну'");
		Иначе
			ТекстЗаголовка = РасписаниеРегламентногоЗадания;
		КонецЕсли;
		
		Элементы.НастроитьРасписаниеОбмена.Заголовок = ТекстЗаголовка;
		
	Иначе
		
		Если РасписаниеРегламентногоЗадания = Неопределено Тогда
			
			ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
			
		Иначе
			
			ЗначениеПериода = РасписаниеРегламентногоЗадания.ПериодПовтораВТечениеДня;
			Если ЗначениеПериода = 0 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 300 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 5 минут';uk='Один раз у 5 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 900 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 15 минут';uk='Один раз в 15 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 1800 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 30 минут';uk='Один раз у 30 хвилин'");
				
			ИначеЕсли ЗначениеПериода <= 3600 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в час';uk='Один раз в годину'");
				
			ИначеЕсли ЗначениеПериода <= 10800 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 3 часа';uk='Один раз в 3 години'");
				
			ИначеЕсли ЗначениеПериода <= 21600 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 6 часов';uk='Один раз на 6 годин'");
				
			ИначеЕсли ЗначениеПериода <= 43200 Тогда
				
				ИнтервалОбмена = НСтр("ru='Один раз в 12 часов';uk='Один раз на 12 годин'");
				
			КонецЕсли;
			
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииЧтенииНаСервере(ЭтотОбъект, Объект)
	
	УстановитьНадписьРасписанияОбменаСервер();
	НастроитьЭлементыАвтообмен();
	
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяНастройкаЗавершить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РазмерПорции = Результат.РазмерПорции;
	Объект.КоличествоПовторений = Результат.КоличествоПовторений;
	Модифицированность = Истина;
	
	СформироватьЗаголовокПараметрыВыгрузки();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыАвтообмен()
	
	Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
		
КонецПроцедуры


#КонецОбласти




&НаКлиенте
Процедура ИспользоватьРегламентныеЗаданияПриИзменении(Элемент)
	Элементы.НастроитьРасписаниеОбмена.Доступность = Объект.ИспользоватьРегламентныеЗадания;
	Если Объект.ИспользоватьРегламентныеЗадания Тогда
		ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
		Если Не ПараметрыРаботыКлиента.РазделениеВключено Тогда
			ВыполнитьНастройкуРасписанияОбмена();
		Иначе
			УстановитьРасписаниеРегламентногоЗадания();
		КонецЕсли;
		
		УстановитьНадписьРасписанияОбмена();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеОбмена(Команда)
	ВыполнитьНастройкуРасписанияОбмена();
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяНастройка(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РазмерПорции", Объект.РазмерПорции);
	ПараметрыФормы.Вставить("КоличествоПовторений", Объект.КоличествоПовторений);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	ОбработчикОповещения = Новый ОписаниеОповещения("РасширеннаяНастройкаЗавершить", ЭтотОбъект);
	
	ОткрытьФорму("ПланОбмена.ОбменФронт.Форма.ФормаРасширенныеНастройки", ПараметрыФормы, ЭтотОбъект
	,
	,
	,
	,
	ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСоединение(Команда)
		
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("ИмяПользователя", Объект.ИмяПользователя);
	НастройкиПодключения.Вставить("Пароль", Объект.Пароль);
	НастройкиПодключения.Вставить("АдресСервера", Объект.АдресСервера);
	НастройкиПодключения.Вставить("БазаДанных", Объект.БазаДанных);
	
	ТекстСообщения = "";
	
	ПроверитьПодключение(НастройкиПодключения, ТекстСообщения);
	
	ПоказатьПредупреждение(, ТекстСообщения);

КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьОстаткиПриИзменении(Элемент)
	
	Если Объект.ВыгружатьТекущиеОстатки Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Установлен флаг Выгружать текущие остатки",,"Объект.ВыгружатьТекущиеОстатки");
		Объект.ВыгружатьОстатки = Ложь;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьТекущиеОстаткиПриИзменении(Элемент)
	Если Объект.ВыгружатьОстатки Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Установлен флаг Выгружать остатки",,"Объект.ВыгружатьОстатки");
		Объект.ВыгружатьТекущиеОстатки = Ложь;
	КонецЕсли;
	
КонецПроцедуры





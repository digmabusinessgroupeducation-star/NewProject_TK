#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Получает массив узлов обмена, используемых в настройках обмена.
//
Функция ПолучитьИспользуемыеУзлыПланаОбмена(ПрофильНастроек = Неопределено,Подразделение = Неопределено) Экспорт
	
	
	ТекстЗапроса = 	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МобильноеПриложение.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.ОбменМобильноеПриложение КАК МобильноеПриложение
	|ГДЕ
	|	НЕ МобильноеПриложение.ПометкаУдаления
	|	И МобильноеПриложение.Ссылка <> &ЭтотУзел
	|	И НЕ МобильноеПриложение.ДоступЗапрещен
	|	И 1 = 1
	|	И 2 = 2";
	
	СтрокаЗамены = ?(ПрофильНастроек = Неопределено,"","И МобильноеПриложение.ПрофильНастроек = &ПрофильНастроек");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И 1 = 1",СтрокаЗамены);
	
	СтрокаЗамены = ?(Подразделение = Неопределено,"","И МобильноеПриложение.ПодразделениеКомпании = &ПодразделениеКомпании");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И 2 = 2",СтрокаЗамены);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПрофильНастроек",ПрофильНастроек);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Подразделение);
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменМобильноеПриложение.ЭтотУзел());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции


// Получает массив узлов обмена, по типу цен.
//
Функция ПолучитьИспользуемыеУзлыПланаОбменаПоТипуЦен(ТипЦены) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МобильноеПриложение.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ОбменМобильноеПриложение КАК МобильноеПриложение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиМобильногоПриложения КАК НастройкиМобильногоПриложения
		|		ПО МобильноеПриложение.ПрофильНастроек = НастройкиМобильногоПриложения.Ссылка
		|ГДЕ
		|	НЕ МобильноеПриложение.ПометкаУдаления
		|	И МобильноеПриложение.Ссылка <> &ЭтотУзел
		|	И НастройкиМобильногоПриложения.ВидыЦен.ВидЦены = &ТипЦены");
		
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменМобильноеПриложение.ЭтотУзел());
	Запрос.УстановитьПараметр("ТипЦены", ТипЦены);

	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Получает массив узлов обмена, по контрагенту.
//
Функция ПолучитьИспользуемыеУзлыПланаОбменаПоКонтрагенту(Контрагент) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МобильноеПриложение.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ОбменМобильноеПриложение КАК МобильноеПриложение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиМобильногоПриложения.Контрагенты КАК НастройкиМобильногоПриложенияКонтрагенты
		|		ПО МобильноеПриложение.ПрофильНастроек = НастройкиМобильногоПриложенияКонтрагенты.Ссылка
		|ГДЕ
		|	НЕ МобильноеПриложение.ПометкаУдаления
		|	И МобильноеПриложение.Ссылка <> &ЭтотУзел
		|	И НастройкиМобильногоПриложенияКонтрагенты.Контрагент = &Контрагент");
		
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменМобильноеПриложение.ЭтотУзел());
	Запрос.УстановитьПараметр("Контрагент", Контрагент);

	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции


// Позволяет переопределить настройки плана обмена, заданные по умолчанию.
// Значения настроек по умолчанию см. в ОбменДаннымиСервер.НастройкиПланаОбменаПоУмолчанию
// 
// Параметры:
//	Настройки - Структура - Содержит настройки по умолчанию
//
// Пример:
//	Настройки.ПредупреждатьОНесоответствииВерсийПравилОбмена = Ложь;
Процедура ОпределитьНастройки(Настройки) Экспорт
	
	
	
КонецПроцедуры // ОпределитьНастройки()

Функция НастройкиУзла(УзелОбмена)Экспорт
	УстановитьПривилегированныйРежим(Истина);
	СтруктураНастроек =Новый Структура("ПодразделениеКомпании,ПрофильНастроек,СкладАгента,СкладЗаказа,СкладВозвратов");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменМобильноеПриложение.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	ОбменМобильноеПриложение.ПрофильНастроек КАК ПрофильНастроек,
		|	ОбменМобильноеПриложение.СкладАгента КАК СкладАгента,
		|	ОбменМобильноеПриложение.ПрофильНастроек.СкладЗаказа КАК СкладЗаказа,
		|	ОбменМобильноеПриложение.ПрофильНастроек.СкладВозвратов КАК СкладВозвратов
		|ИЗ
		|	ПланОбмена.ОбменМобильноеПриложение КАК ОбменМобильноеПриложение
		|ГДЕ
		|	ОбменМобильноеПриложение.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", УзелОбмена);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураНастроек,ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат СтруктураНастроек;
КонецФункции


Функция ПолучитьУзелОбменаМобильногоУстройства(ИдентификаторМобильногоУстройства)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменМобильноеПриложение.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.ОбменМобильноеПриложение КАК ОбменМобильноеПриложение
	|ГДЕ
	|	ОбменМобильноеПриложение.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ИдентификаторМобильногоУстройства);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ПланыОбмена.ОбменМобильноеПриложение.ПустаяСсылка()
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
КонецФункции

Функция УзелОбменаПользователяСуществует(Пользователь)Экспорт
	Рез = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменМобильноеПриложение.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ОбменМобильноеПриложение КАК ОбменМобильноеПриложение
		|ГДЕ
		|	ОбменМобильноеПриложение.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Рез = Ложь;
	КонецЕсли;
	Возврат Рез;
	
КонецФункции



#КонецЕсли

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	
	Если ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		ДанныеБлокировки  = ДанныеБлокировки();
		
		Блокировка = Новый БлокировкаДанных();
		
		Если ДанныеБлокировки.ТребуетсяУправляемаяБлокировка Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваровКомпании");
			ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ДанныеБлокировки.ТаблицаБлокируемыхИзмерений;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СкладКомпании", "СкладКомпании");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ХарактеристикаНоменклатуры", "ХарактеристикаНоменклатуры");
			
		Иначе
			БлокироватьДляИзменения = Истина;
		КонецЕсли;
		
		Блокировка.Заблокировать();
		СформироватьТаблицуИсходныхДвижений();
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ, Замещение)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		СформироватьТаблицуИзмененийДвижений();
	КонецЕсли;
	
КонецПроцедуры




Процедура СформироватьТаблицуИсходныхДвижений()

	// Текущее состояние набора помещается во временную таблицу "ДвиженияПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	//Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	Таблица.СкладКомпании КАК СкладКомпании,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -Таблица.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПередЗаписьюРасход,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -Таблица.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоПередЗаписьюПриход
	|ПОМЕСТИТЬ ОстаткиТоваровПередЗаписью
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();

КонецПроцедуры

Процедура СформироватьТаблицуИзмененийДвижений()

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	//Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	//и помещается во временную таблицу.
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.СкладКомпании КАК СкладКомпании,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ВложенныйЗапрос.КоличествоРасход) КАК КоличествоРасход,
	               |	СУММА(ВложенныйЗапрос.КоличествоПриход) КАК КоличествоПриход
	               |ПОМЕСТИТЬ ДвиженияОстаткиТоваровКомпанииИзменение
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ОстаткиТоваровПередЗаписью.СкладКомпании КАК СкладКомпании,
	               |		ОстаткиТоваровПередЗаписью.Номенклатура КАК Номенклатура,
	               |		ОстаткиТоваровПередЗаписью.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ОстаткиТоваровПередЗаписью.КоличествоПередЗаписьюРасход КАК КоличествоРасход,
	               |		ОстаткиТоваровПередЗаписью.КоличествоПередЗаписьюПриход КАК КоличествоПриход
	               |	ИЗ
	               |		ОстаткиТоваровПередЗаписью КАК ОстаткиТоваровПередЗаписью
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		Таблица.СкладКомпании,
	               |		Таблица.Номенклатура,
	               |		Таблица.ХарактеристикаНоменклатуры,
	               |		ВЫБОР
	               |			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |				ТОГДА Таблица.Количество
	               |			ИНАЧЕ 0
	               |		КОНЕЦ,
	               |		ВЫБОР
	               |			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |				ТОГДА Таблица.Количество
	               |			ИНАЧЕ 0
	               |		КОНЕЦ
	               |	ИЗ
	               |		РегистрНакопления.ОстаткиТоваровКомпании КАК Таблица
	               |	ГДЕ
	               |		Таблица.Регистратор = &Регистратор) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.СкладКомпании,
	               |	ВложенныйЗапрос.Номенклатура,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры
	               |
	               |ИМЕЮЩИЕ
	               |	(СУММА(ВложенныйЗапрос.КоличествоРасход) > 0
	               |		ИЛИ СУММА(ВложенныйЗапрос.КоличествоПриход) > 0)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ОстаткиТоваровПередЗаписью";
	
	Результат = Запрос.ВыполнитьПакет();

	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();
	СтруктураВременныеТаблицы.Вставить("ДвиженияОстаткиТоваровКомпанииИзменение", Выборка.Количество > 0);

КонецПроцедуры

Функция ДанныеБлокировки()

	ЕстьСтрокиБезКонтроля = Ложь;

	Набор = Новый ТаблицаЗначений();
	Набор.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Набор.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Набор.Колонки.Добавить("СкладКомпании",          Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
	НоваяСтрока = Неопределено;

	Для Каждого Запись Из ЭтотОбъект Цикл

		Если Запись.Количество <> 0 Или Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Количество > 0 Тогда

			Если НоваяСтрока = Неопределено Или
					НоваяСтрока.Номенклатура <> Запись.Номенклатура Или
					НоваяСтрока.ХарактеристикаНоменклатуры <> Запись.ХарактеристикаНоменклатуры Или
					НоваяСтрока.СкладКомпании <> Запись.СкладКомпании Тогда

				НоваяСтрока = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);

			КонецЕсли;

		Иначе
			ЕстьСтрокиБезКонтроля = Истина;
		КонецЕсли;

	КонецЦикла;

	
	ТребуетсяУправляемаяБлокировка = ЕстьСтрокиБезКонтроля;

	Возврат Новый Структура("ТребуетсяУправляемаяБлокировка, ТаблицаБлокируемыхИзмерений",
		ТребуетсяУправляемаяБлокировка, Набор);

КонецФункции


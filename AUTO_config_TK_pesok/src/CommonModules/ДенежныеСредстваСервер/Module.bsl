
#Область ПроцедурыФормированияДвиженийПоДенежнымСредствам

// Процедура формирования движений по регистру "Денежные средства (наличные)".
//
// Параметры:
//	ДокументОбъект - Текущий документ
//	Отказ - Булево - Признак отказа от проведения документа
//
Процедура ОтразитьДенежныеСредстваВКассахККМ(ДополнительныеСвойства, Движения, Отказ) Экспорт

	ТаблицаДенежныеСредства = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДенежныеСредстваВКассахККМ;
	
	Если Отказ ИЛИ ТаблицаДенежныеСредства.Количество() = 0 Тогда
	
		Возврат;
		
	КонецЕсли;
	
	ДвиженияДенежныеСредства = Движения.ДенежныеСредстваВКассахККМ;
	ДвиженияДенежныеСредства.Записывать = Истина;
	ДвиженияДенежныеСредства.Загрузить(ТаблицаДенежныеСредства);
	
КонецПроцедуры // ОтразитьДенежныеСредстваВКассахККМ()

// Процедура формирования движений по регистру "Денежные средства компании".
//
// Параметры:
//	ДокументОбъект - Текущий документ
//	Отказ - Булево - Признак отказа от проведения документа
//
Процедура ОтразитьДенежныеСредстваКомпании(ДополнительныеСвойства, Движения, Отказ) Экспорт

	ТаблицаДенежныеСредства = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДенежныеСредстваКомпании;
	
	Если Отказ ИЛИ ТаблицаДенежныеСредства.Количество() = 0 Тогда
	
		Возврат;
		
	КонецЕсли;
	
	ДвиженияДенежныеСредства = Движения.ДенежныеСредстваКомпании;
	ДвиженияДенежныеСредства.Записывать = Истина;
	ДвиженияДенежныеСредства.Загрузить(ТаблицаДенежныеСредства);
	
КонецПроцедуры // ОтразитьДенежныеСредстваКомпании()

#КонецОбласти


#Область УправлениеИнтерфейснымиЭлементами

// Процедура устанавливает видимость элементов формы по массиву имен.
//
// Параметры:
//	Элементы - ВсеЭлементыФормы - Коллекция элементов формы
//	МассивВсехРеквизитов - Массив - Массив имен реквизитов, для которых устанавливается видимость
//	МассивВидимыхРеквизитов - Массив - Массив имен реквизитов, которые должны быть видны на форме.
//
Процедура УстановитьВидимостьЭлементовПоМассиву(Элементы, Знач МассивВсехРеквизитов, Знач МассивВидимыхРеквизитов) Экспорт
	
	Если ТипЗнч(МассивВидимыхРеквизитов) <> Тип("Массив") Тогда
		МассивВидимыхРеквизитов = Новый Массив;
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивВсехРеквизитов Цикл
		
		ИмяРеквизита = СтрЗаменить(ЭлементМассива, ".", "");
		
		// Если реквизита нет в массиве видимых реквизитов, снимаем видимость элемента формы.
		ВидимостьРеквизита = (МассивВидимыхРеквизитов.Найти(ЭлементМассива) <> Неопределено);
		Если Элементы.Найти(ИмяРеквизита) <> Неопределено
		 И Элементы[ИмяРеквизита].Видимость <> ВидимостьРеквизита Тогда
			Элементы[ИмяРеквизита].Видимость = ВидимостьРеквизита;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // УстановитьВидимостьЭлементовПоМассиву()

// Процедура устанавливает видимость элементов формы по массиву имен.
//
// Параметры:
//	Элементы - ВсеЭлементыФормы - Коллекция элементов формы
//	МассивВсехРеквизитов - Массив - Массив имен реквизитов, для которых устанавливается видимость
//	МассивВидимыхРеквизитов - Массив - Массив имен реквизитов, которые должны быть видны на форме
//	ИмяТЧ - Строка - Имя табличной части, текущая строка которой выводится в виде отдельных реквизитов
//	ИмяЗаменыТЧ - Строка - Наименование подстроки замены наименования реквизитов табличной части
//
Процедура УстановитьВидимостьЭлементовПоМассивуРасширенный(Элементы, Знач МассивВсехРеквизитов, Знач МассивВидимыхРеквизитов,
	Знач ИмяТЧ, Знач ИмяЗаменыТЧ) Экспорт
	
	Если ТипЗнч(МассивВидимыхРеквизитов) <> Тип("Массив") Тогда
		МассивВидимыхРеквизитов = Новый Массив;
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивВсехРеквизитов Цикл
		
		ИмяРеквизита = СтрЗаменить(СтрЗаменить(ЭлементМассива, ИмяТЧ, ИмяЗаменыТЧ), ".", "");
		
		ВидимостьРеквизита = (МассивВидимыхРеквизитов.Найти(ЭлементМассива) <> Неопределено);
		Если Элементы.Найти(ИмяРеквизита) <> Неопределено
			И Элементы[ИмяРеквизита].Видимость <> ВидимостьРеквизита Тогда
			Элементы[ИмяРеквизита].Видимость = ВидимостьРеквизита;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция - возвращает текстовую строку остатка на структурной единице
//
// Параметры:
//  Дата				 - Дата - дата получения остатка по структурной единице
//  СтруктурнаяЕдиница	 - КассаКомпании, БанковскийСчетОрганизации - счет, или касса, по которому получаем остаток
//  Валюта				 - Валюта - валюта остатка
//
Функция ПолучитьТекстОстаткаНаСтруктурнойЕдинице(Дата, СтруктурнаяЕдиница, Валюта) Экспорт 
	Если ЗначениеЗаполнено(Дата) И ЗначениеЗаполнено(СтруктурнаяЕдиница) И ЗначениеЗаполнено(Валюта) Тогда 
		Остаток = ПолучитьОстатокПоСтруктурнойЕдинице(Дата, СтруктурнаяЕдиница, Валюта);
	Иначе 
		Остаток = 0;
	КонецЕсли;
	
	Возврат НСтр("ru = 'Остаток на счету: '; uk = 'Залишок на рахунку: '") + Формат(Остаток, "ЧДЦ=2; ЧН=") + " " + Строка(Валюта);
КонецФункции

// Функция - возвращает остаток на структурной единице
//
// Параметры:
//  Дата				 - Дата - дата получения остатка по структурной единице
//  СтруктурнаяЕдиница	 - КассаКомпании, БанковскийСчетОрганизации - счет, или касса, по которому получаем остаток
//  Валюта				 - Валюта - валюта остатка
//
Функция ПолучитьОстатокПоСтруктурнойЕдинице(Дата, СтруктурнаяЕдиница, Валюта) Экспорт 
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ДенежныеСредстваКомпанииОстатки.СуммаОстаток КАК Сумма
	                      |ИЗ
	                      |	РегистрНакопления.ДенежныеСредстваКомпании.Остатки(
	                      |			&Дата,
	                      |			СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	                      |				И Валюта = &Валюта) КАК ДенежныеСредстваКомпанииОстатки");
	
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Сумма;
	Иначе 
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецОбласти




Процедура УправлениеЭлементамиЮридическихРеквизитов(Форма, Знач ЮрФизЛицо, Знач ПлательщикНДС, Знач Ссылка = Неопределено) Экспорт
	
		
	Элементы = Форма.Элементы;
	
		
	МожетБытьПлательщикомНДС = ЮрФизЛицо <> ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо") И ЮрФизЛицо <> ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент");
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПлательщикНДС",      "Доступность", МожетБытьПлательщикомНДС);
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИННПлательщикаНДС",  "Доступность", МожетБытьПлательщикомНДС);
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НомерСвидетельства", "Доступность", МожетБытьПлательщикомНДС);
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПлательщикНДС", "Доступность", МожетБытьПлательщикомНДС);

	
	
КонецПроцедуры


//По переданному значению перечисления ЮрФизЛицо определяет, является ли оно признаком ЮрЛица
//
// Параметры
//  ЮрФизЛицо  - Перечисления.ЮрФизЛицо 
// Возвращаемое значение:
//  Булево     - Истина, если юридическое лицо, Ложь если нет.
//
Функция ЭтоЮрЛицо(ЮрФизЛицо) Экспорт
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		Возврат Истина;
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		Возврат Ложь;
	ИначеЕсли ТипЗнч(ЮрФизЛицо) = Тип("ПеречислениеСсылка.ЮридическоеФизическоеЛицо") Тогда
	 	Возврат ЮрФизЛицо <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции



// Устанавливаем условное оформление для характеристик номенклатуры
//
// Параметры:
// 		Форма - Форма - Содержит данную форму 
// 		ИмяПоляВводаХарактеристики - Строка - Наименование элемента формы, содержащего характеристики номенклатуры,
//											   если оно отличается от "ТоварыХарактеристикаНоменклатуры"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "характеристики используются",
//									если он отличается от "Объект.Товары.ХарактеристикиИспользуются"
// 
Процедура УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма,
	                                                            ИмяПоляВводаХарактеристики = "ТоварыХарактеристикаНоменклатуры",
																ПутьКПолюОтбора = "Объект.Товары.ХарактеристикиИспользуются") Экспорт
	
												
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаХарактеристики].Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<характеристики не используются>';uk='<характеристики не використовуються>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

// Заполняет служебные реквизиты по номенклатуре в коллекции
//
// Параметры:
// 		КоллекцияДанных - ДанныеФормыКоллекция, ТаблицаЗначний - Таблица, в которой необходимо заполнить реквизиты
// 		Реквизиты - Строка - Строка с перечислением через запятую имен реквизитов для заполнения
// 		СтрокиЗаполнения - Массив ДанныеФормыЭлементКоллекции - строки, для которых требуется заполнение
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(КоллекцияДанных, СтруктураДействий, СтрокиЗаполнения = Неопределено) Экспорт
	
	СтруктураДопДанных = ОбработкаТабличнойЧастиСервер.ПолучитьСтруктуруДополнительнойИнформации(СтруктураДействий);
	
	Если СтрокиЗаполнения = Неопределено Тогда
		ПараметрКоллекция = КоллекцияДанных.Выгрузить( ,"НомерСтроки" + СтруктураДопДанных.РеквизитыВыгрузки);
	ИначеЕсли СтрокиЗаполнения.Количество() > 0 Тогда
		ПараметрКоллекция = КоллекцияДанных.Выгрузить(СтрокиЗаполнения ,"НомерСтроки" + СтруктураДопДанных.РеквизитыВыгрузки);
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(ОбработкаТабличнойЧастиСервер.ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных));
	Запрос.УстановитьПараметр("КоллекцияДанных", ПараметрКоллекция);
	
	Если СтрокиЗаполнения <> Неопределено Тогда
		
		ТЗРезультат = Запрос.Выполнить().Выгрузить();
		Для Каждого Стр Из ТЗРезультат Цикл
			ЗаполнитьЗначенияСвойств(КоллекцияДанных[Стр.Номерстроки - 1], Стр, СтруктураДопДанных.РеквизитыЗаполнения);
		КонецЦикла;
		
	Иначе
		
		Выборка = Запрос.Выполнить().Выбрать();
		Для Н=0 По КоллекцияДанных.Количество()-1 Цикл
			Выборка.Следующий(); // Количество строк в выборке по запросу всегда равно количеству строк в коллекции
			ЗаполнитьЗначенияСвойств(КоллекцияДанных[Н], Выборка, СтруктураДопДанных.РеквизитыЗаполнения);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСлужебныеРеквизитыВКоллекции()

// Получает служебные реквизиты по номенклатуре в структуре
//
// Параметры:
// 		СтруктураДанных - Структура, СтрокаТаблицыЗначений - Структура данных, в которой необходимо заполнить поля
// 		Реквизиты - Строка - Строка с перечислением через запятую имен реквизитов для заполнения
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтруктуре(СтруктураДанных, СтруктураДействий) Экспорт
	
	СтруктураДопДанных = ОбработкаТабличнойЧастиСервер.ПолучитьСтруктуруДополнительнойИнформации(СтруктураДействий);
	
	ТаблицаВыгрузки = Новый ТаблицаЗначений;
	ТаблицаВыгрузки.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	Для Каждого Источник Из СтруктураДопДанных.СтруктураИсточников Цикл
		Если Источник.Ключ = "Упаковка" Тогда
			ТаблицаВыгрузки.Колонки.Добавить(Источник.Ключ, Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
		ИначеЕсли Источник.Ключ = "ХарактеристикаНоменклатуры" Тогда	
			ТаблицаВыгрузки.Колонки.Добавить(Источник.Ключ, Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		Иначе
			ТаблицаВыгрузки.Колонки.Добавить(Источник.Ключ, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(ТаблицаВыгрузки.Добавить(), СтруктураДанных);
	
	Запрос = Новый Запрос(ОбработкаТабличнойЧастиСервер.ПолучитьТекстЗапросаПоСлужебнымРеквизитамТЧ(СтруктураДействий, СтруктураДопДанных));
	Запрос.УстановитьПараметр("КоллекцияДанных", ТаблицаВыгрузки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка, СтруктураДопДанных.РеквизитыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСлужебныеРеквизитыВСтруктуре()


#Область ОбменЗУП

// Получает список точек кураторов из базы "work_UU_Zarplata_Attika" и обновляет их в базе
Процедура ОбновитьСписокКураторовТорговыхПлощадок() Экспорт 
	
	ИдентификаторОбработки = "ЗагрузкаКураторов";
	регРезультатВыполнения = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписи = регРезультатВыполнения.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписи.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписи.КлючОбработки = "Загрузка кураторов из ""work_UU_Zarplata_Attika""";
		
	ВсеОк = Истина;
	РеквизитКуратор = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "КураторПлощадки");
	
	Если РеквизитКуратор.Пустая() Тогда 
		ВсеОк = Ложь;
		регРезультатВыполнения.СообщитьОбОшибке("Не найден реквизит ""Куратор""", СтруктураЗаписи, СтатусСообщения.ОченьВажное);
	КонецЕсли;
	
	Если ВсеОк Тогда 
		Попытка
			Пользователь = "obmenTK_ws";
			Пароль = "<fhf<fy";	
			Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_UU_Zarplata_Attika/ws/cwd2.1cws?wsdl",Пользователь,Пароль,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
			WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			WS.Пользователь = Пользователь;
			WS.Пароль = Пароль;
			
			Ответ = WS.GetCuratorList();			
			
			ВсеОк = Ответ.Status;		
			регРезультатВыполнения.ДобавитьКомментарий(Ответ.Description, СтруктураЗаписи);			
		Исключение
			ВсеОк = Ложь;
			регРезультатВыполнения.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписи, СтатусСообщения.Важное);
		КонецПопытки;	
	КонецЕсли;

	Если ВсеОк Тогда 		
		ТаблицаКураторов = Новый ТаблицаЗначений;
		ТаблицаКураторов.Колонки.Добавить("ИД", ОбщегоНазначения.ОписаниеТипаЧисло(10,, ДопустимыйЗнак.Неотрицательный));
		ТаблицаКураторов.Колонки.Добавить("КодПлощадки", ОбщегоНазначения.ОписаниеТипаСтрока(20));
		ТаблицаКураторов.Колонки.Добавить("НаименованиеПлощадки", ОбщегоНазначения.ОписаниеТипаСтрока(150));
		ТаблицаКураторов.Колонки.Добавить("КодКуратора", ОбщегоНазначения.ОписаниеТипаСтрока(20));
		ТаблицаКураторов.Колонки.Добавить("ИмяКуратора", ОбщегоНазначения.ОписаниеТипаСтрока(150));
		
		Для Каждого Стр Из Ответ.List Цикл
			НоваяСтрока = ТаблицаКураторов.Добавить();
			НоваяСтрока.ИД 			= Стр.LineNumber;
			НоваяСтрока.КодПлощадки = Стр.CodeTradePoint;
			НоваяСтрока.НаименованиеПлощадки = Стр.NameTradePoint;
			НоваяСтрока.КодКуратора = Стр.CodeCurator;
			НоваяСтрока.ИмяКуратора = Стр.NameCurator;
		КонецЦикла;	
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("тчКураторы", ТаблицаКураторов);
		Запрос.УстановитьПараметр("СвойствоКуратор", РеквизитКуратор);
		Запрос.Текст = "ВЫБРАТЬ
		               |	тчКураторы.ИД КАК ИД,
		               |	тчКураторы.КодПлощадки КАК КодПлощадки,
		               |	тчКураторы.НаименованиеПлощадки КАК НаименованиеПлощадки,
		               |	тчКураторы.КодКуратора КАК КодКуратора,
		               |	тчКураторы.ИмяКуратора КАК ИмяКуратора
		               |ПОМЕСТИТЬ втТаблица
		               |ИЗ
		               |	&тчКураторы КАК тчКураторы
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	КодКуратора,
		               |	КодПлощадки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТаблица.ИД КАК ИД,
		               |	втТаблица.КодПлощадки КАК КодПлощадки,
		               |	ТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
		               |	ЕСТЬNULL(ТорговыеПлощадкиДополнительныеРеквизиты.Значение, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ТекущийКуратор
		               |ПОМЕСТИТЬ втПлощадки
		               |ИЗ
		               |	втТаблица КАК втТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		               |			ПО ТорговыеПлощадки.Ссылка = ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка
		               |				И (ТорговыеПлощадкиДополнительныеРеквизиты.Свойство = &СвойствоКуратор)
		               |		ПО втТаблица.КодПлощадки = ТорговыеПлощадки.Код
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТаблица.КодКуратора КАК КодКуратора,
		               |	втТаблица.ИмяКуратора КАК ИмяКуратора
		               |ПОМЕСТИТЬ втСписокКураторов
		               |ИЗ
		               |	втТаблица КАК втТаблица
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втТаблица.КодКуратора,
		               |	втТаблица.ИмяКуратора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втСписокКураторов.КодКуратора КАК КодКуратора,
		               |	втСписокКураторов.ИмяКуратора КАК ИмяКуратора,
		               |	МАКСИМУМ(ФизическиеЛица.Ссылка) КАК Куратор
		               |ПОМЕСТИТЬ втКураторыКод
		               |ИЗ
		               |	втСписокКураторов КАК втСписокКураторов
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		               |		ПО втСписокКураторов.КодКуратора = ФизическиеЛица.Код
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втСписокКураторов.КодКуратора,
		               |	втСписокКураторов.ИмяКуратора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВложенныйЗапрос.КодКуратора КАК КодКуратора,
		               |	ВложенныйЗапрос.ИмяКуратора КАК ИмяКуратора,
		               |	МАКСИМУМ(ФизическиеЛица.Ссылка) КАК Куратор
		               |ПОМЕСТИТЬ втКураторыКодДРФО
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		втСписокКураторов.КодКуратора КАК КодКуратора,
		               |		втСписокКураторов.ИмяКуратора КАК ИмяКуратора
		               |	ИЗ
		               |		втСписокКураторов КАК втСписокКураторов
		               |	ГДЕ
		               |		НЕ втСписокКураторов.КодКуратора В
		               |					(ВЫБРАТЬ
		               |						втКураторыКод.КодКуратора КАК КодКуратора
		               |					ИЗ
		               |						втКураторыКод КАК втКураторыКод)) КАК ВложенныйЗапрос
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		               |		ПО ВложенныйЗапрос.КодКуратора = ФизическиеЛица.КодПоДРФО
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВложенныйЗапрос.КодКуратора,
		               |	ВложенныйЗапрос.ИмяКуратора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втКураторыКод.КодКуратора КАК КодКуратора,
		               |	втКураторыКод.ИмяКуратора КАК ИмяКуратора,
		               |	втКураторыКод.Куратор КАК Куратор
		               |ПОМЕСТИТЬ втКураторы
		               |ИЗ
		               |	втКураторыКод КАК втКураторыКод
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	втКураторыКодДРФО.КодКуратора,
		               |	втКураторыКодДРФО.ИмяКуратора,
		               |	втКураторыКодДРФО.Куратор
		               |ИЗ
		               |	втКураторыКодДРФО КАК втКураторыКодДРФО
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ втСписокКураторов
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ втКураторыКод
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |УНИЧТОЖИТЬ втКураторыКодДРФО
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТаблица.ИД КАК ИД,
		               |	втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	втКураторы.Куратор КАК Куратор,
		               |	втПлощадки.ТекущийКуратор КАК ТекущийКуратор,
		               |	втТаблица.КодПлощадки КАК КодПлощадки,
		               |	втТаблица.НаименованиеПлощадки КАК НаименованиеПлощадки,
		               |	втТаблица.КодКуратора КАК КодКуратора,
		               |	втТаблица.ИмяКуратора КАК ИмяКуратора,
		               |	ВЫБОР
		               |		КОГДА НЕ втКураторы.Куратор ЕСТЬ NULL
		               |				И НЕ втПлощадки.ТорговаяПлощадка ЕСТЬ NULL
		               |				И втКураторы.Куратор <> (ВЫРАЗИТЬ(втПлощадки.ТекущийКуратор КАК Справочник.ФизическиеЛица))
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК Изменения
		               |ПОМЕСТИТЬ ОсновнаяТаблица
		               |ИЗ
		               |	втТаблица КАК втТаблица
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втПлощадки КАК втПлощадки
		               |		ПО втТаблица.ИД = втПлощадки.ИД
		               |			И втТаблица.КодПлощадки = втПлощадки.КодПлощадки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втКураторы КАК втКураторы
		               |		ПО втТаблица.КодКуратора = втКураторы.КодКуратора
		               |			И втТаблица.ИмяКуратора = втКураторы.ИмяКуратора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	""ТорговаяПлощадка"" КАК Тип,
		               |	ОсновнаяТаблица.КодПлощадки КАК Код,
		               |	ОсновнаяТаблица.НаименованиеПлощадки КАК Имя
		               |ИЗ
		               |	ОсновнаяТаблица КАК ОсновнаяТаблица
		               |ГДЕ
		               |	ОсновнаяТаблица.ТорговаяПлощадка ЕСТЬ NULL
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ОсновнаяТаблица.НаименованиеПлощадки,
		               |	ОсновнаяТаблица.КодПлощадки
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	""ФизическоеЛицо"",
		               |	ОсновнаяТаблица.КодКуратора,
		               |	ОсновнаяТаблица.ИмяКуратора
		               |ИЗ
		               |	ОсновнаяТаблица КАК ОсновнаяТаблица
		               |ГДЕ
		               |	ОсновнаяТаблица.Куратор ЕСТЬ NULL
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ОсновнаяТаблица.ИмяКуратора,
		               |	ОсновнаяТаблица.КодКуратора
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОсновнаяТаблица.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	ОсновнаяТаблица.Куратор КАК Куратор
		               |ИЗ
		               |	ОсновнаяТаблица КАК ОсновнаяТаблица
		               |ГДЕ
		               |	НЕ ОсновнаяТаблица.ТорговаяПлощадка ЕСТЬ NULL
		               |	И НЕ ОсновнаяТаблица.Куратор ЕСТЬ NULL
		               |	И ОсновнаяТаблица.Изменения";
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		КолвоТабл = МассивРезультатов.ВГраница();
		
		ВыборкаОшибки = МассивРезультатов[КолвоТабл - 1].Выбрать();
		ВыборкаИзменения = МассивРезультатов[КолвоТабл].Выбрать();

		//Если ВыборкаОшибки.Количество() > 0 Тогда 
		//	регРезультатВыполнения.ДобавитьКомментарий("Есть ошибки при сопоставлении объектов баз: " + ВыборкаОшибки.Количество(), СтруктураЗаписи);			
		//КонецЕсли;
		
		Пока ВыборкаОшибки.Следующий() Цикл 
			Сообщение = "";
			Если ВыборкаОшибки.Тип = "ТорговаяПлощадка" Тогда 
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не найдена торговая площадка ""(%1) ""%2""'; uk = 'Не знайдений торговий майданчик ""(%1) ""%2""'"),
								ВыборкаОшибки.Код,
								ВыборкаОшибки.Имя);
			ИначеЕсли ВыборкаОшибки.Тип = "ФизическоеЛицо" Тогда 
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не найден физ. лицо ""(%1) ""%2""'; uk = 'Не знайдена фіз. особа ""(%1) ""%2""'"),
								ВыборкаОшибки.Код,
								ВыборкаОшибки.Имя);								
			КонецЕсли;
			
			Если Не ПустаяСтрока(Сообщение) Тогда 
				регРезультатВыполнения.СообщитьОбОшибке(Сообщение, СтруктураЗаписи);
			КонецЕсли;
		КонецЦикла;
		
		Успешно = 0;
		СОшибками = 0;
		
		ТаблРеквизиты = Новый ТаблицаЗначений;
		ТаблРеквизиты.Колонки.Добавить("Свойство");
		ТаблРеквизиты.Колонки.Добавить("Значение");
		
		Пока ВыборкаИзменения.Следующий() Цикл 		
			ТаблРеквизиты.Очистить();
			НовыйРеквизит = ТаблРеквизиты.Добавить();
			НовыйРеквизит.Свойство = РеквизитКуратор;
			НовыйРеквизит.Значение = ВыборкаИзменения.Куратор;
			
			Попытка
				УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ВыборкаИзменения.ТорговаяПлощадка, ТаблРеквизиты);
				Успешно = Успешно + 1;
			Исключение
				регРезультатВыполнения.СообщитьОбОшибке(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось установить куратора ""%""1 на Торговой площадке ""%2""
                              |Ошибка: %3'"),
						ВыборкаИзменения.Куратор,
						ВыборкаИзменения.ТорговаяПлощадка,
						ОписаниеОшибки()),
					СтруктураЗаписи);
						
				СОшибками = СОшибками + 1;	
			КонецПопытки;
		КонецЦикла;
		
		СообщениеЗавершения = "";
		Если Успешно > 0 Тогда
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Записано изменений - %1'; uk = 'Записано змін - %1'"),
					Успешно),
				СтруктураЗаписи);
		КонецЕсли;
				
		Если СОшибками > 0 Тогда 
			ВсеОк = Ложь;
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибки записи - %'; uk = 'Помилки записів - %1'"),
					СОшибками),
				СтруктураЗаписи);			
		КонецЕсли;
		
		Если Успешно = 0 И СОшибками = 0 Тогда 
			регРезультатВыполнения.ДобавитьКомментарий(НСтр("ru = 'Изменения не обнаружены'; uk = 'Зміни не знайдені'"), СтруктураЗаписи);
		КонецЕсли;		
	КонецЕсли;
	
	регРезультатВыполнения.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписи, ВсеОк);
	
КонецПроцедуры

// Обновляет дополнительное свойство "График" из базы "work_UU_Zarplata_Attika" и обновляет их в базе
Процедура ОбновитьГрафикиРаботыТорговыхПлощадок() Экспорт 
	
	ИдентификаторОбработки = "ЗагрузкаГрафиковРаботыТорговыхПлощадок";
	регРезультатВыполнения = РегистрыСведений.РезультатыВыполненияОбработок;
	
	СтруктураЗаписи = регРезультатВыполнения.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписи.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписи.КлючОбработки = "Загрузка графиков Торговых Площадок из ""work_UU_Zarplata_Attika""";
		
	ВсеОк = Истина;
	РеквизитГрафик = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ГрафикРаботы");
	
	Если РеквизитГрафик.Пустая() Тогда 
		ВсеОк = Ложь;
		регРезультатВыполнения.СообщитьОбОшибке("Не найден реквизит ""График Работы""", СтруктураЗаписи, СтатусСообщения.ОченьВажное);
	КонецЕсли;
	
	Если ВсеОк Тогда 
		Попытка
			Пользователь = "obmenTK_ws";
			Пароль = "<fhf<fy";	
			Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_UU_Zarplata_Attika/ws/cwd2.1cws?wsdl",Пользователь,Пароль,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
			WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			WS.Пользователь = Пользователь;
			WS.Пароль = Пароль;
			
			Ответ = WS.GetTradePointScheduleList();			
			
			ВсеОк = Ответ.Status;		
			регРезультатВыполнения.ДобавитьКомментарий(Ответ.Description, СтруктураЗаписи);			
		Исключение
			ВсеОк = Ложь;
			регРезультатВыполнения.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписи, СтатусСообщения.Важное);
		КонецПопытки;	
	КонецЕсли;

	Если ВсеОк Тогда 		
		ТаблицаГрафиков = Новый ТаблицаЗначений;
		ТаблицаГрафиков.Колонки.Добавить("ИД", ОбщегоНазначения.ОписаниеТипаЧисло(10,, ДопустимыйЗнак.Неотрицательный));
		ТаблицаГрафиков.Колонки.Добавить("КодПлощадки", ОбщегоНазначения.ОписаниеТипаСтрока(20));
		ТаблицаГрафиков.Колонки.Добавить("НаименованиеПлощадки", ОбщегоНазначения.ОписаниеТипаСтрока(150));
		ТаблицаГрафиков.Колонки.Добавить("КоличествоЧасов", ОбщегоНазначения.ОписаниеТипаЧисло(2,0, ДопустимыйЗнак.Неотрицательный));
		
		Для Каждого Стр Из Ответ.List Цикл
			НоваяСтрока = ТаблицаГрафиков.Добавить();
			НоваяСтрока.ИД 			= Стр.LineNumber;
			НоваяСтрока.КодПлощадки = Стр.CodeTradePoint;
			НоваяСтрока.НаименованиеПлощадки = Стр.NameTradePoint;
			НоваяСтрока.КоличествоЧасов = Стр.Hours;
		КонецЦикла;	
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("тчГрафики", ТаблицаГрафиков);
		Запрос.УстановитьПараметр("Свойство", РеквизитГрафик);
		Запрос.Текст = "ВЫБРАТЬ
		               |	тчГрафики.ИД КАК ИД,
		               |	тчГрафики.КодПлощадки КАК КодПлощадки,
		               |	тчГрафики.НаименованиеПлощадки КАК НаименованиеПлощадки,
		               |	тчГрафики.КоличествоЧасов КАК КоличествоЧасов
		               |ПОМЕСТИТЬ втТаблица
		               |ИЗ
		               |	&тчГрафики КАК тчГрафики
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	КодПлощадки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТаблица.ИД КАК ИД,
		               |	втТаблица.КодПлощадки КАК КодПлощадки,
		               |	ТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
		               |	ЕСТЬNULL(ТорговыеПлощадкиДополнительныеРеквизиты.Значение, НЕОПРЕДЕЛЕНО) КАК График
		               |ПОМЕСТИТЬ втПлощадки
		               |ИЗ
		               |	втТаблица КАК втТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		               |			ПО ТорговыеПлощадки.Ссылка = ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка
		               |				И (ТорговыеПлощадкиДополнительныеРеквизиты.Свойство = &Свойство)
		               |		ПО втТаблица.КодПлощадки = ТорговыеПлощадки.Код
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗначенияСвойствОбъектов.Вес КАК Часы,
		               |	ЗначенияСвойствОбъектов.Ссылка КАК Значение
		               |ПОМЕСТИТЬ втСписокЗначенийСвойств
		               |ИЗ
		               |	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		               |ГДЕ
		               |	ЗначенияСвойствОбъектов.Владелец = &Свойство
		               |	И НЕ ЗначенияСвойствОбъектов.ЭтоГруппа
		               |	И НЕ ЗначенияСвойствОбъектов.ПометкаУдаления
		               |	И ЗначенияСвойствОбъектов.Вес > 0
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втПлощадки.ИД КАК ИД,
		               |	втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	втПлощадки.График КАК ТекГрафик,
		               |	ЕСТЬNULL(ТекСписокСвойств.Часы, 0) КАК ТекКоличествоЧасов,
		               |	ЕСТЬNULL(НовСписокСвойств.Значение, НЕОПРЕДЕЛЕНО) КАК НовГрафик,
		               |	втТаблица.КоличествоЧасов КАК НовКоличествоЧасов
		               |ПОМЕСТИТЬ втСписокГрафиков
		               |ИЗ
		               |	втПлощадки КАК втПлощадки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втТаблица КАК втТаблица
		               |			ЛЕВОЕ СОЕДИНЕНИЕ втСписокЗначенийСвойств КАК НовСписокСвойств
		               |			ПО втТаблица.КоличествоЧасов = НовСписокСвойств.Часы
		               |		ПО втПлощадки.ИД = втТаблица.ИД
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втСписокЗначенийСвойств КАК ТекСписокСвойств
		               |		ПО втПлощадки.График = ТекСписокСвойств.Значение
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТаблица.КодПлощадки КАК Код,
		               |	втТаблица.НаименованиеПлощадки КАК Имя
		               |ИЗ
		               |	втТаблица КАК втТаблица
		               |		ЛЕВОЕ СОЕДИНЕНИЕ втПлощадки КАК втПлощадки
		               |		ПО втТаблица.ИД = втПлощадки.ИД
		               |ГДЕ
		               |	втПлощадки.ТорговаяПлощадка ЕСТЬ NULL
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	МАКСИМУМ(втСписокГрафиков.НовГрафик) КАК График,
		               |	втСписокГрафиков.НовКоличествоЧасов КАК КоличествоЧасов
		               |ИЗ
		               |	втСписокГрафиков КАК втСписокГрафиков
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	втСписокГрафиков.НовКоличествоЧасов
		               |
		               |ИМЕЮЩИЕ
		               |	МАКСИМУМ(втСписокГрафиков.НовГрафик) = НЕОПРЕДЕЛЕНО
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втСписокГрафиков.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |	втСписокГрафиков.НовГрафик КАК График
		               |ИЗ
		               |	втСписокГрафиков КАК втСписокГрафиков
		               |ГДЕ
		               |	втСписокГрафиков.ТекГрафик <> втСписокГрафиков.НовГрафик
		               |	И втСписокГрафиков.ТекКоличествоЧасов <> втСписокГрафиков.НовКоличествоЧасов";
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		КолвоТабл = МассивРезультатов.ВГраница();
		                          
		ВыборкаОшибкиПлощадки = МассивРезультатов[КолвоТабл - 2].Выбрать();
		ВыборкаОшибкиГрафики = МассивРезультатов[КолвоТабл - 1].Выбрать();
		ВыборкаИзменения = МассивРезультатов[КолвоТабл].Выбрать();

		Пока ВыборкаОшибкиПлощадки.Следующий() Цикл 
			регРезультатВыполнения.СообщитьОбОшибке(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не найдена торговая площадка ""(%1) ""%2""'; uk = 'Не знайдений торговий майданчик ""(%1) ""%2""'"),
					ВыборкаОшибкиПлощадки.Код,
					ВыборкаОшибкиПлощадки.Имя),
				СтруктураЗаписи);
		КонецЦикла;
		     
		Пока ВыборкаОшибкиГрафики.Следующий() Цикл 
			регРезультатВыполнения.СообщитьОбОшибке(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не найден график ""%1 - часов""'; uk = 'Не знайдений графік ""%1-годин""'"),
					ВыборкаОшибкиГрафики.КоличествоЧасов),
				СтруктураЗаписи);			
		КонецЦикла;
		     		
		Успешно = 0;
		СОшибками = 0;
		
		ТаблРеквизиты = Новый ТаблицаЗначений;
		ТаблРеквизиты.Колонки.Добавить("Свойство");
		ТаблРеквизиты.Колонки.Добавить("Значение");
		
		Пока ВыборкаИзменения.Следующий() Цикл 		
			ТаблРеквизиты.Очистить();
			НовыйРеквизит = ТаблРеквизиты.Добавить();
			НовыйРеквизит.Свойство = РеквизитГрафик;
			НовыйРеквизит.Значение = ВыборкаИзменения.График;
			
			Попытка
				УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ВыборкаИзменения.ТорговаяПлощадка, ТаблРеквизиты);
				Успешно = Успешно + 1;
			Исключение
				регРезультатВыполнения.СообщитьОбОшибке(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось установить график ""%""1 на Торговой площадке ""%2""
                              |Ошибка: %3'"),
						ВыборкаИзменения.График,
						ВыборкаИзменения.ТорговаяПлощадка,                                 
						ОписаниеОшибки()),
					СтруктураЗаписи);
						
				СОшибками = СОшибками + 1;	
			КонецПопытки;
		КонецЦикла;
		
		СообщениеЗавершения = "";
		Если Успешно > 0 Тогда
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Записано изменений - %1'; uk = 'Записано змін - %1'"),
					Успешно),
				СтруктураЗаписи);
		КонецЕсли;
				
		Если СОшибками > 0 Тогда 
			ВсеОк = Ложь;
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибки записи - %'; uk = 'Помилки записів - %1'"),
					СОшибками),
				СтруктураЗаписи);			
		КонецЕсли;
		
		Если Успешно = 0 И СОшибками = 0 Тогда 
			регРезультатВыполнения.ДобавитьКомментарий(НСтр("ru = 'Изменения не обнаружены'; uk = 'Зміни не знайдені'"), СтруктураЗаписи);
		КонецЕсли;		
	КонецЕсли;
	
	регРезультатВыполнения.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписи, ВсеОк);	
	
КонецПроцедуры


#КонецОбласти
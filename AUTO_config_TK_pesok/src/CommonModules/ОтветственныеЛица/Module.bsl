

// Функция подбирает вероятную должность ответственного лица из справочника "Должности".
// 
Функция ПодобратьВероятнуюДолжностьОтветственногоЛица(ОтветственноеЛицо) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ОтветственноеЛицо) Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Если ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель Тогда
		НаименованиеДолжности1	= НСтр("ru='Генеральный директор';uk='Генеральний директор'");
		НаименованиеДолжности2	= НСтр("ru='%[Дд]иректор%';uk='%[Дд]иректор%'");
	ИначеЕсли ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда
		НаименованиеДолжности1	= НСтр("ru='Главный бухгалтер';uk='Головний бухгалтер'");
		НаименованиеДолжности2	= НСтр("ru='%[Бб]ухгалтер%';uk='%[Бб]ухгалтер%'");
	ИначеЕсли ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Кассир Тогда
		НаименованиеДолжности1	= НСтр("ru='Кассир';uk='Касир'");
		НаименованиеДолжности2	= НСтр("ru='%[Кк]ассир%';uk='%[Кк]ассир%'");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос	= Новый Запрос;
	
	Запрос.УстановитьПараметр("НаименованиеДолжности1", НаименованиеДолжности1);
	Запрос.УстановитьПараметр("НаименованиеДолжности2", НаименованиеДолжности2);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	Должности.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Должности КАК Должности
	|ГДЕ
	|	Должности.Наименование ПОДОБНО &НаименованиеДолжности1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	Должности.Ссылка
	|ИЗ
	|	Справочник.Должности КАК Должности
	|ГДЕ
	|	Должности.Наименование ПОДОБНО &НаименованиеДолжности2
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка	= РезультатЗапроса.Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Выборка.Ссылка;
	
КонецФункции

Функция ПодобратьОтветственноеЛицоОрганизации(Должность, Организация, Дата = Неопределено) Экспорт
	
	НаДату = ?(Дата = Неопределено, ТекущаяДата(), Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК Должность
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&НаДату,
		|			ОтветственноеЛицо = &ОтветственноеЛицо
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК ОтветственныеЛицаОрганизацийСрезПоследних";
	
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Должность);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат "";
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.ФизическоеЛицо;
	КонецЕсли;
	
КонецФункции

Функция ПодобратьОтветственноеЛицоСклада(Склад, Организация, Дата = Неопределено, Должность = Неопределено) Экспорт
	
	НаДату = ?(Дата = Неопределено, ТекущаяДата(), Дата);
	Должность = ?(Должность = Неопределено,Справочники.Должности.ПустаяСсылка(), Должность); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныеЛицаСкладСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаСклад.СрезПоследних(
		|			&НаДату,
		|			Склад = &Склад
		|				И Организация = &Организация) КАК ОтветственныеЛицаСкладСрезПоследних
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Должность = ЗНАЧЕНИЕ(Справочник.Должности.Пустаяссылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ОтветственныеЛицаСкладСрезПоследних.Должность = &Должность
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Должность", Должность);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат "";
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.ФизическоеЛицо;
	КонецЕсли;
	
КонецФункции
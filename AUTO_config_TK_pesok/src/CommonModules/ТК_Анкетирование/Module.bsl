
Процедура ОбработчикПередЗаписьюАнкеты(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Источник.ПроцентВыполнения = ПолучитьПроцентВыполненияАнкеты(Источник);
	
КонецПроцедуры

Функция ПолучитьПроцентВыполненияАнкеты(Анкета) Экспорт 
	
	тчСостав = Анкета.Состав.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Состав", тчСостав);
	Запрос.Текст = "ВЫБРАТЬ
	               |	АнкетаСостав.Вопрос КАК Вопрос,
	               |	АнкетаСостав.ЭлементарныйВопрос КАК ЭлементарныйВопрос,
	               |	АнкетаСостав.НомерЯчейки КАК НомерЯчейки,
	               |	АнкетаСостав.Ответ КАК Ответ,
	               |	АнкетаСостав.ОткрытыйОтвет КАК ОткрытыйОтвет
	               |ПОМЕСТИТЬ втСоставАнкеты
	               |ИЗ
	               |	&Состав КАК АнкетаСостав
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.МаксимальноеЗначение * ВложенныйЗапрос.ПроцентГруппы) КАК ПроцентВыполнения
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втСоставАнкеты.ЭлементарныйВопрос.Родитель КАК Группа,
	               |		втСоставАнкеты.ЭлементарныйВопрос.Родитель.Коэффициент КАК ПроцентГруппы,
	               |		СУММА(ВЫБОР
	               |				КОГДА НЕ ТК_ПараметрыВопросовАнкет.Коэффициент ЕСТЬ NULL
	               |					ТОГДА ТК_ПараметрыВопросовАнкет.Коэффициент * (ВЫРАЗИТЬ(втСоставАнкеты.Ответ КАК ЧИСЛО(1, 0)))
	               |				ИНАЧЕ ВЫРАЗИТЬ(втСоставАнкеты.Ответ КАК ЧИСЛО(1, 0))
	               |			КОНЕЦ) КАК Сумма,
	               |		СУММА(ВЫБОР
	               |				КОГДА НЕ ТК_ПараметрыВопросовАнкет.Коэффициент ЕСТЬ NULL
	               |					ТОГДА ТК_ПараметрыВопросовАнкет.Коэффициент * втСоставАнкеты.ЭлементарныйВопрос.МаксимальноеЗначение
	               |				ИНАЧЕ втСоставАнкеты.ЭлементарныйВопрос.МаксимальноеЗначение
	               |			КОНЕЦ) КАК МаксимальноеЗначение
	               |	ИЗ
	               |		втСоставАнкеты КАК втСоставАнкеты
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ПараметрыВопросовАнкет КАК ТК_ПараметрыВопросовАнкет
	               |			ПО втСоставАнкеты.ЭлементарныйВопрос = ТК_ПараметрыВопросовАнкет.ЭлементарныйВопрос
	               |	ГДЕ
	               |		втСоставАнкеты.ЭлементарныйВопрос.ТипОтвета = ЗНАЧЕНИЕ(Перечисление.ТипыОтветовНаВопрос.Число)
	               |		И втСоставАнкеты.НомерЯчейки = 0
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втСоставАнкеты.ЭлементарныйВопрос.Родитель,
	               |		втСоставАнкеты.ЭлементарныйВопрос.Родитель.Коэффициент) КАК ВложенныйЗапрос
	               |ГДЕ
	               |	ВложенныйЗапрос.МаксимальноеЗначение > 0";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.ПроцентВыполнения;
	КонецЕсли;
	
КонецФункции


#Область ПрограммныйИнтерфейс

Процедура ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	КонецЕсли;
		
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НеобходимВызовСервера(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Тогда

		ТекущаяСтрокаСтруктура = ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока,СтруктураДействий);
		
		ОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(ТекущаяСтрокаСтруктура, СтруктураДействий, КэшированныеЗначения);

		ЗаполнитьЗначенияСвойств(ТекущаяСтрока,ТекущаяСтрокаСтруктура);

		Возврат;
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	ПроверитьПересчитатьКоличество(ТекущаяСтрока,СтруктураДействий);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоЕдиницВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуИзСуммыБезНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьЦенуСНДСВстрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьЦенуБезНалога(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуСУчетомРучнойСкидкиВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуСНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуБезНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВсегоОкругление(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуВсегоНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	//Закрытие смены
	ОбработкаТабличнойЧастиКлиентСервер.ЗаполнитьПризнакВозваратаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьСуммуНДСиАкцизногоНалогаВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	
	//+ Документ.ИзменениеЦен тч Товары
	ОбработкаТабличнойЧастиКлиентСервер.ЗаполнитьПроцентНаценкиИзРекомендованныхВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьНовуюЦенуИПроцентНаценки(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	//- Документ.ИзменениеЦен тч Товары
	
	ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьРазностьВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
КонецПроцедуры

#КонецОбласти



#Область ПрочиеПроцедуры

Функция НеобходимВызовСервера(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("НеобходимоОбработатьВсюТЧ")
		И СтруктураДействий.НеобходимоОбработатьВсюТЧ Тогда
		ТекстИсключения = НСтр("ru='Для обработки всей ТЧ, необходимо сделать контекстный вызов серверной фукнции ""ОбработкаТабличнойЧастиСервер.ОбработатьТЧ""';uk='Для обробки всієї ТЧ, необхідно зробити контекстний виклик серверної фукнції ""ОбработкаТабличнойЧастиСервер.ОбработатьТЧ""'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
		
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАртикул")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПроверитьХарактеристикуПоВладельцу")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКодУКТВЭД")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу")
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДС", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЕдиницаИзмерения = Неопределено;
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиниц", ЕдиницаИзмерения) ИЛИ  СтруктураДействий.Свойство("ЗаполнитьКоэффициентЕдиницыИзмерения", ЕдиницаИзмерения) Тогда
		ПараметрыПересчета = ОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ЕдиницаИзмерения);
		КлючКоэффициента   = ОбработкаТабличнойЧастиКлиентСервер.КлючКэшаУпаковки(ПараметрыПересчета.Номенклатура, ПараметрыПересчета.ЕдиницаИзмерения); 
		
		Если ЗначениеЗаполнено(ПараметрыПересчета.ЕдиницаИзмерения)
			И ЗначениеЗаполнено(ПараметрыПересчета.Номенклатура)
			И КэшированныеЗначения.КоэффициентыУпаковок[КлючКоэффициента] = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
		
	Если СтруктураДействий.Свойство("ЗаполнитьЦены") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьРекомендованныеНаценки") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьОстаток") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьЗаполнитьРецептуру") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьНовуюЦенуПоИндикативу") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьСкидкуПоИндикативу") И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ПолучитьТекущуюСтрокуСтруктурой(ТекущаяСтрока, СтруктураДействий)
	Перем СтруктураПараметровДействия;
	
	СтруктураПолейТЧ = Новый Структура;
	
	Если СтруктураДействий.Свойство("ПроверитьХарактеристикуПоВладельцу") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ХарактеристикаНоменклатуры");
		СтруктураПолейТЧ.Вставить("ХарактеристикиИспользуются");
		СтруктураПолейТЧ.Вставить("ЗначениеМРЦ");

	КонецЕсли;	
	
	Если СтруктураДействий.Свойство("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		СтруктураПолейТЧ.Вставить("Коэффициент");
		СтруктураПолейТЧ.Вставить("Количество");
		СтруктураПолейТЧ.Вставить("КоличествоБазовое");
		
	КонецЕсли;
	
	//Если СтруктураДействий.Свойство("ЗаполнитьОстатокТовара") Тогда
	//	СтруктураПолейТЧ.Вставить("Номенклатура");
	//	СтруктураПолейТЧ.Вставить("ХарактеристикаНоменклатуры");
	//	СтруктураПолейТЧ.Вставить("Остаток");
	//КонецЕсли;
	//
	
	Если СтруктураДействий.Свойство("ЗаполнитьГруппуЦенообразования") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ГруппаЦенообразования");		
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоэффициентЕдиницыИзмерения") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("Коэффициент");	
		СтруктураПолейТЧ.Вставить("ЗначениеМРЦ");

	КонецЕсли;

	
	Если СтруктураДействий.Свойство("ЗаполнитьСтавкуНДСПоНоменклатуре") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("СтавкаНДС");
	КонецЕсли;
	
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиниц")Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ХарактеристикаНоменклатуры");
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		СтруктураПолейТЧ.Вставить("КоличествоБазовое", 0);
		СтруктураПолейТЧ.Вставить("Количество", 0);
		СтруктураПолейТЧ.Вставить("Коэффициент", 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму") Тогда
		Если ЗначениеЗаполнено(СтруктураДействий.ПересчитатьСумму) Тогда 
			СтруктураПолейТЧ.Вставить(СтруктураДействий.ПересчитатьСумму, 0);
		КонецЕсли;
		
		СтруктураПолейТЧ.Вставить("КоличествоБазовое",0);
		СтруктураПолейТЧ.Вставить("Сумма",0);
		СтруктураПолейТЧ.Вставить("Цена",0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуВсего") Тогда
		Если ЗначениеЗаполнено(СтруктураДействий.ПересчитатьСуммуВсего) Тогда 
			СтруктураПолейТЧ.Вставить(СтруктураДействий.ПересчитатьСуммуВсего, 0);
		КонецЕсли;

		СтруктураПолейТЧ.Вставить("КоличествоБазовое",0);
		СтруктураПолейТЧ.Вставить("СуммаВсего",0);
		СтруктураПолейТЧ.Вставить("Цена",0);
	КонецЕсли;	
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенуИзСуммы") Тогда 
		Если ЗначениеЗаполнено(СтруктураДействий.ПересчитатьЦенуИзСуммы) Тогда 
			СтруктураПолейТЧ.Вставить(СтруктураДействий.ПересчитатьЦенуИзСуммы, 0);
		КонецЕсли;
		
		СтруктураПолейТЧ.Вставить("КоличествоБазовое",0);
		СтруктураПолейТЧ.Вставить("Сумма",0);
		СтруктураПолейТЧ.Вставить("Цена",0);
	
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС") Тогда
		СтруктураПолейТЧ.Вставить("Сумма",0);
		СтруктураПолейТЧ.Вставить("СуммаВсего"	, 0);
		СтруктураПолейТЧ.Вставить("СтавкаНДС"	, 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" 	, 0);
		СтруктураПолейТЧ.Вставить("ЗначениеМРЦ");

	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДСиАкцизногоНалога") Тогда
		СтруктураПолейТЧ.Вставить("Сумма"    , 0);
		СтруктураПолейТЧ.Вставить("СуммаВсего"	, 0);
		СтруктураПолейТЧ.Вставить("СтавкаНДС"	, 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС" 	, 0);
		СтруктураПолейТЧ.Вставить("СуммаАкцизногоНалога" , 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуБезНДС") Тогда
		СтруктураПолейТЧ.Вставить("СуммаБезНалогов", 0);
		СтруктураПолейТЧ.Вставить("СуммаНДС", 		 0);
		СтруктураПолейТЧ.Вставить("Сумма", 			 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуВсегоОкругление") Тогда
		СтруктураПолейТЧ.Вставить("ЦенаБезНалогов", 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦенаБезНалога") Тогда   
		СтруктураПолейТЧ.Вставить("Цена", 0);
		СтруктураПолейТЧ.Вставить("ЦенаБезНалогов", 0);
		СтруктураПолейТЧ.Вставить("СтавкаНДС");
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПересчитатьСуммуСУчетомРучнойСкидки") Тогда
		СтруктураПолейТЧ.Вставить("КоличествоБазовое"	, 0);
		СтруктураПолейТЧ.Вставить("Цена"				, 0);
		СтруктураПолейТЧ.Вставить("ПроцентСкидки"		, 0);
		СтруктураПолейТЧ.Вставить("СуммаСкидки"			, 0);
		СтруктураПолейТЧ.Вставить("СуммаВсего" 			, 0);
		СтруктураПолейТЧ.Вставить("СуммаПФ"				, 0);
	КонецЕсли;
		
	Если СтруктураДействий.Свойство("ЗаполнитьЦены", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ХарактеристикаНоменклатуры");	
		СтруктураПолейТЧ.Вставить("ГруппаЦенообразования");
		Если ТипЗнч(СтруктураПараметровДействия) = Тип("Структура") Тогда
			Для Каждого Поле Из СтруктураПараметровДействия Цикл
				СтруктураПолейТЧ.Вставить(Поле.Ключ, 0);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАртикул", СтруктураПараметровДействия) 
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		Для Каждого Поле Из СтруктураПараметровДействия Цикл
			СтруктураПолейТЧ.Вставить(Поле.Ключ);
			СтруктураПолейТЧ.Вставить(Поле.Значение);
		КонецЦикла;
	КонецЕсли;
	
	
	Если СтруктураДействий.Свойство("ЗаполнитьКодУКТВЭД", СтруктураПараметровДействия) 
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		Для Каждого Поле Из СтруктураПараметровДействия Цикл
			СтруктураПолейТЧ.Вставить(Поле.Ключ);
			СтруктураПолейТЧ.Вставить(Поле.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьВидНоменклатуры", СтруктураПараметровДействия) 
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		Для Каждого Поле Из СтруктураПараметровДействия Цикл
			СтруктураПолейТЧ.Вставить(Поле.Ключ);
			СтруктураПолейТЧ.Вставить(Поле.Значение);
		КонецЦикла;
	КонецЕсли;
	

	


	
	Если СтруктураДействий.Свойство("ЗаполнитьРекомендованныеНаценки") ИЛИ СтруктураДействий.Свойство("ЗаполнитьПроцентНаценкиИзРекомендованных") Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ПроцентНаценки",										0);
		СтруктураПолейТЧ.Вставить("РекомендуемыйПроцентНаценкиНоменклатура", 			0);
		СтруктураПолейТЧ.Вставить("РекомендуемыйПроцентНаценкиРодитель", 				0);
		СтруктураПолейТЧ.Вставить("РекомендуемыйПроцентНаценкиТип", 					0);
		СтруктураПолейТЧ.Вставить("РекомендуемыйПроцентНаценкиГруппаЦенообразования",	0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьНовуюЦенуПоПроцентуНаценки") Тогда
		СтруктураПолейТЧ.Вставить("ЦенаБазовая");
		СтруктураПолейТЧ.Вставить("ПроцентНаценки");
		СтруктураПолейТЧ.Вставить("НоваяЦена", 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьПроцентНаценкиИзНовойЦены") ИЛИ СтруктураДействий.Свойство("ПересчитатьНовуюЦенуСУчетомОкругления") Тогда
		СтруктураПолейТЧ.Вставить("ЦенаБазовая");
		СтруктураПолейТЧ.Вставить("ПроцентНаценки");
		СтруктураПолейТЧ.Вставить("НоваяЦена");	
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьНовуюЦенуПоИндикативу") Тогда 
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("НоваяЦена" , 0);	
		СтруктураПолейТЧ.Вставить("ЗначениеСкидки" , 0);	
		СтруктураПолейТЧ.Вставить("ГруппаЦенообразования");
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьСкидкуПоИндикативу") Тогда 
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("СпособВычисления");	
		СтруктураПолейТЧ.Вставить("ЗначениеСкидки" , 0);	
		СтруктураПолейТЧ.Вставить("ГруппаЦенообразования");
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПересчитатьРазность", СтруктураПараметровДействия) Тогда
		Если ТипЗнч(СтруктураПараметровДействия) = Тип("Массив") Тогда
			Для Каждого Поля Из СтруктураПараметровДействия Цикл
				Если ТипЗнч(Поля) = Тип("Структура") Тогда
					Для Каждого Поле Из Поля Цикл
						СтруктураПолейТЧ.Вставить(Поле.Значение, 0);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакВозврата") Тогда
		СтруктураПолейТЧ.Вставить("Количество", 0);
		СтруктураПолейТЧ.Вставить("Возврат", Ложь);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьОстаток", СтруктураПараметровДействия)Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");	
		СтруктураПолейТЧ.Вставить("ХарактеристикаНоменклатуры");
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		СтруктураПолейТЧ.Вставить("Коэффициент", 0);
		
		ИмяПоляОстатка = Неопределено;
		Если Не СтруктураПараметровДействия.Свойство("ИмяПоляОстатка", ИмяПоляОстатка) Тогда
			ИмяПоляОстатка = "Остаток";
			СтруктураПараметровДействия.Вставить("ИмяПоляОстатка", ИмяПоляОстатка);
		КонецЕсли;
		
		СтруктураПолейТЧ.Вставить(ИмяПоляОстатка, 0);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПроверитьЗаполнитьРецептуру", СтруктураПараметровДействия) Тогда 
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ЕдиницаИзмерения");
		
		ИмяПоля = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПараметровДействия, "ИмяПоля", "Рецептура");
		
		СтруктураПолейТЧ.Вставить(ИмяПоля);
	КонецЕсли;
	
	
	Если СтруктураДействий.Свойство("ЦеныИзДоговора") Тогда 
		СтруктураПолейТЧ.Вставить("ТипЦен");
		СтруктураПолейТЧ.Вставить("ГруппаЦенообразования");
	КонецЕсли;
	
	
	ЗаполнитьЗначенияСвойств(СтруктураПолейТЧ, ТекущаяСтрока);
	
	Возврат СтруктураПолейТЧ;
	
КонецФункции

#КонецОбласти


#Область ПроцедурыПересчетаИЗаполнения


Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения)
	
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакХарактеристикиИспользуются") Тогда
		ТекущаяСтрока.ХарактеристикиИспользуются = Ложь;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакЭтоУслуга") Тогда
		ТекущаяСтрока.ЭтоУслуга = Ложь;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар") Тогда
	  	ТекущаяСтрока.ПодакцизныйТовар = Ложь;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакАртикул") Тогда
		ТекущаяСтрока.Артикул = "";
	КонецЕсли;
	

	
КонецПроцедуры




Процедура ПроверитьПересчитатьКоличество(ТекущаяСтрока,СтруктураДействий)
	
	Если СтруктураДействий.Свойство("ПересчитатьКоличество") Тогда
	    ТекущаяСтрока.КоличествоБазовое = ?(ТекущаяСтрока.Количество = 0, 1, ТекущаяСтрока.Количество) * ТекущаяСтрока.Коэффициент;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьСуммуНДСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) 
	
	
	Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС") Тогда
	
		ТекПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(ТекущаяСтрока.СтавкаНДС);
		ТекущаяСтрока.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(ТекущаяСтрока.Сумма, ТекПроцентНДС);
		
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти


#Область ПрограммныйИнтерфейс

Функция ВыполнитьТестовоеПодключение(НастройкиПодключения, ТекстСообщения) Экспорт
	
	
	Соединение = СоединениеССервером(НастройкиПодключения,ТекстСообщения);
	
	Если Соединение = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru='Ошибка при установке соединения.';uk='Помилка при встановленні з''єднання.'") + Символы.ПС + ТекстСообщения;
		Возврат Ложь;
	Иначе
		ТекстСообщения = НСтр("ru='Соединение успешно установлено.';uk='З''єднання успішно встановлено.'")+ Символы.ПС + ТекстСообщения;
		Возврат Истина;
	КонецЕсли;
	
	
КонецФункции

Функция УзелОбмена(Подразделение)Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменФронт.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.ОбменФронт КАК ОбменФронт
	|ГДЕ
	|	ОбменФронт.ПометкаУдаления = ЛОЖЬ
	|	И НЕ ОбменФронт.ЭтотУзел
	|	И ОбменФронт.ПодразделениеКомпании = &ПодразделениеКомпании";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат  ВыборкаДетальныеЗаписи.Ссылка;
	
КонецФункции

Функция СоединениеССервером(ПараметрыПодключения, ОписаниеОшибки)Экспорт 
	
	СтрокаПодключения = "";	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		СтрокаПодключения = "Provider=SQLNCLI11;";
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
		СтрокаПодключения = "Provider=SQLNCLI11;";
	Иначе
		ОписаниеОшибки = НСтр("ru='Операционная система "+СистемнаяИнформация.ТипПлатформы +" не поддерживается.';uk='Операційна система "+ СистемнаяИнформация.ТипПлатформы +" не підтримується'");
		
		Возврат Неопределено
	КонецЕсли;
	
	СтрокаПодключения = СтрокаПодключения + "Server=" + СокрЛП(ПараметрыПодключения.АдресСервера)+";"
	+ "Database="	+ СокрЛП(ПараметрыПодключения.БазаДанных) + ";"
	+ "Uid=" + СокрЛП(ПараметрыПодключения.ИмяПользователя) + ";" 	
	+ "Pwd=" + СокрЛП(ПараметрыПодключения.Пароль) + ";";
	
	Попытка
		Соединение	= Новый COMОбъект("ADODB.Connection");
		Соединение.ConnectionString = СтрокаПодключения;
		Соединение.ConnectionTimeOut = 120;//1200;
		Соединение.CommandTimeout = 300;
		Соединение.CursorLocation = 1; 
		Соединение.Mode = 1; 
		
		Соединение.Open(); 
	Исключение
		тОшибка = НСтр("ru='Не удалось установить соединение с сервером ';uk='Не вдалося встановити з''єднання з сервером'");
		ОписаниеОшибки = тОшибка + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Соединение;
	
	
КонецФункции

Функция ПодключениеКВнешнемуИсточнику(Загрузка = Истина, ПараметрыПодключения,ТекстСообщения)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	//
	//Если ВнешниеИсточникиДанных.OpenStore.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен Тогда
	//	Возврат Истина;
	//КонецЕсли;	
	
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
		СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
	Иначе
		ТекстСообщения = ТекстСообщения + НСтр("ru='Операционная система "+СистемнаяИнформация.ТипПлатформы +" не поддерживается.';uk='Операційна система "+ СистемнаяИнформация.ТипПлатформы +" не підтримується'")+Символы.ПС;
		
		Возврат Ложь
	КонецЕсли;
	
	
	
	ПараметрыПодключенияВИ = ВнешниеИсточникиДанных.OpenStore.ПолучитьОбщиеПараметрыСоединения();
	//ПараметрыПодключенияВИ = ВнешниеИсточникиДанных.OpenStore.ПолучитьПараметрыСоединенияСеанса();
	ПараметрыПодключенияВИ.АутентификацияСтандартная = Истина;
	ПараметрыПодключенияВИ.ИмяПользователя = ПараметрыПодключения.ИмяПользователя; 
	ПараметрыПодключенияВИ.Пароль = ПараметрыПодключения.Пароль;
	ПараметрыПодключенияВИ.СтрокаСоединения = СтрокаПодключения + "Server="+СокрЛП(ПараметрыПодключения.АдресСервера) + ";Database="+?(Загрузка,СокрЛП(ПараметрыПодключения.БазаДанныхТранзит),СокрЛП(ПараметрыПодключения.БазаДанных)) + ";Uid="+СокрЛП(ПараметрыПодключения.ИмяПользователя) + ";Pwd="+СокрЛП(ПараметрыПодключения.Пароль) +";";
	
	ПараметрыПодключенияВИ.СУБД ="MS SQL Server";
	
	ВнешниеИсточникиДанных.OpenStore.УстановитьОбщиеПараметрыСоединения(ПараметрыПодключенияВИ);
	//ВнешниеИсточникиДанных.OpenStore.УстановитьПараметрыСоединенияПользователя(ИмяПользователя(),ПараметрыПодключения);
	//ВнешниеИсточникиДанных.OpenStore.УстановитьПараметрыСоединенияСеанса(ПараметрыПодключенияВИ);
	
	Попытка
		ВнешниеИсточникиДанных.OpenStore.УстановитьСоединение();
	Исключение
		ТекстСообщения = ТекстСообщения + ОписаниеОшибки();
	КонецПопытки;
	
	
	
	
	Если ВнешниеИсточникиДанных.OpenStore.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен Тогда
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;	
	
	
КонецФункции

Функция ПолучитьСоеединение(Подразделение,ОписаниеОшибки="")
	
	ПараметрыПодключения = ОбменФронтПовтИсп.ПолучитьНастройкиПодключения(Подразделение);
	Если ПараметрыПодключения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Соединение = СоединениеССервером(ПараметрыПодключения,ОписаниеОшибки);
	Возврат Соединение;
	
КонецФункции



Процедура Начать_Транзакцию(Connection)Экспорт 
	Connection.BeginTrans();
КонецПроцедуры

Процедура Зафиксировать_Транзакцию(Connection)Экспорт 
	Connection.CommitTrans();
КонецПроцедуры

Процедура Откатить_Транзакцию(Connection)Экспорт
	Connection.RollbackTrans();
КонецПроцедуры


Функция УстановитьПодключениеКВнешнемуИсточнику(Загрузка,Подразделение,ТекстСообщения)Экспорт
	
	Узел = ПланыОбмена.ОбменФронт.ПолучитьУзелОбмена(Подразделение);
	Если Узел = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru='Ошибка определения узла по подразделению: "+ Строка(Подразделение)+"';uk='Помилка визначення вузла по підрозділу: "+ Строка(Подразделение)+"'")
		+ Символы.ПС + ТекстСообщения;
		Возврат Ложь;
	КонецЕсли;
	
	НастройкиПодлючения = ПланыОбмена.ОбменФронт.ЗаполнитьПараметрыПодключения(Узел);
	
	Возврат ПодключениеКВнешнемуИсточнику(Загрузка,НастройкиПодлючения,ТекстСообщения);
	
КонецФункции


Функция ПолучитьКоличествоПродажПоОтбору(Подразделение,Артикул,ИД_Склад,ИД_МРЦ)Экспорт
	
	Соединение = ПолучитьСоеединение(Подразделение);
	
	Если Соединение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Продажи = 0;
	
	ТекстЗапроса = "EXECUTE [Integration1C].[dbo].[GET_OPERATIONAL_SALESCOUNT]  
	| @artid  = "+Формат(Число(Артикул),"ЧЦ=6; ЧГ=")+",
	| @WHGUID = "+"'"+ИД_Склад +"',
	| @charguid = "+"'"+ИД_МРЦ +"'";
	
	RS = Новый COMОбъект("ADODB.Recordset");
	RS.ActiveConnection = Соединение;
	
	Попытка
		                                
		RS.Open(ТекстЗапроса);
		Продажи = RS.Fields("SALESCOUNT").Value;
	Исключение
		тОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	
	Возврат Продажи;
	
КонецФункции




#КонецОбласти

Функция ТекстНеверныйАргументМетода(Аргумент, Метод) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Неверный аргумент '%1' метода %2()", Аргумент, Метод);
	
КонецФункции



#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьЗапрос(Connection, Отказ, Query,ТекстСообщений = Неопределено) 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	//ЗаписьЖурналаРегистрации("Запрос.OS",УровеньЖурналаРегистрации.Информация,,,Query);
	
	
	ЗапросАДО = Новый COMОбъект("ADODB.Command");
	ЗапросАДО.CommandText = Query;
	ЗапросАДО.ActiveConnection = Connection;	
	
	
	Попытка
		ЗапросАДО.Execute(Query);
		//objRecordset = Connection.Execute(Query);
		
	Исключение
		тОшибка = "Невозможно выполнить SQL запрос " + ОписаниеОшибки();
		//Ом.Информировать(тОшибка, СтатусСообщения.Важное);
		//Ом.Информировать(Query);
		Отказ = Истина;
		
		
		
		
		Если ТекстСообщений <> Неопределено Тогда
			ТекстСообщений.ДобавитьСтроку(тОшибка);
			ТекстСообщений.ДобавитьСтроку("-------------ЗАПРОС------------");
			Если СтрЧислоСтрок(Query)>5 Тогда
				ТекстСообщений.ДобавитьСтроку("Первые 5 строк запроса");
				Для Счетчик = 1 По 5 Цикл
					ТекСтрока = СтрПолучитьСтроку(Query, Счетчик);
					ТекстСообщений.ДобавитьСтроку(ТекСтрока);
				КонецЦикла;
			Иначе
				ТекстСообщений.ДобавитьСтроку(Query);	
			КонецЕсли;
			
			ТекстСообщений.ДобавитьСтроку("-------------------------------");
		КонецЕсли;
		
	КонецПопытки;
	
	
КонецПроцедуры

Процедура ВыполнитьТекстЗапросаОтключения(Connection, Отказ, ТекстЗапросаОтключения,ТекстСообщений = Неопределено)
	
	
	СтрокЗапроса = ТекстЗапросаОтключения.КоличествоСтрок();
	
	Для НомерСтроки = 1 По СтрокЗапроса Цикл 
		
		ТекстЗапроса = ТекстЗапросаОтключения.ПолучитьСтроку(НомерСтроки);
		ВыполнитьЗапрос(Connection,Отказ,ТекстЗапроса,ТекстСообщений);
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


Функция ПолучитьВнутрСсылку(лСсылка)
	Если лСсылка = Неопределено ИЛИ лСсылка ="NULL" Тогда
		Возврат "NULL";
	КонецЕсли;
	Зн = ЗначениеВСтрокуВнутр(лСсылка);
	Зн = "0x" + Лев(Прав(Зн, 33), 32);
	Возврат Зн;
КонецФункции

Функция ЭкранироватьСимволы(ИсходнаяСтрока)
	Результат = ИсходнаяСтрока;
	Результат = СтрЗаменить(СокрЛП(Результат),"""","");
	Результат = СтрЗаменить(СокрЛП(Результат),"(","");
	Результат = СтрЗаменить(СокрЛП(Результат),")","");
	Результат = СтрЗаменить(СокрЛП(Результат),"'","");
	
	
	Возврат Результат;
КонецФункции

Функция ФорматироватьСтроку(ИсходнаяСтрока)
	
	Результат = СтрЗаменить(ИсходнаяСтрока, Символы.НПП, "");
	Результат = СокрЛП(Результат); 
	
	Возврат Результат;
	
КонецФункции

Функция ОбработатьШтрихКод(тШтрих)
	
	Результат = тШтрих;
	Результат = СтрЗаменить(СокрЛП(Результат),".","");
	Результат = СтрЗаменить(Результат,";","");
	Результат = СтрЗаменить(Результат,",","");
	
	Возврат Результат;
КонецФункции

Функция ДобавитьИформацию(ТаблицаИнформации,ДействиеПриОбмене,Описание)
	
	СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
	СтрокаТаблицыИнформации.ДействиеПриОбмене = ДействиеПриОбмене;
	СтрокаТаблицыИнформации.Описание = Описание;
	СтрокаТаблицыИнформации.ДатаНачала = ТекущаяДата();
	
	Возврат СтрокаТаблицыИнформации;
	
КонецФункции


Процедура ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена)
	
	СтрокаТаблицыИнформации.ДатаОкончания = ТекущаяДата();
	Если Отказ Тогда
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = РезультатОбмена.ТекстСообщений.ПолучитьТекст();
		
	Иначе
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Выполнено;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


Процедура ВыполнитьОбменСфронтом(ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации)Экспорт
	
	ОписаниеОшибки = "";
	Подключение = СоединениеССервером(ПараметрыОбмена.НастройкиПодключения,ОписаниеОшибки);
	
	Если Подключение = Неопределено Тогда
		СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	РезультатОбмена.Вставить("ТекстСообщений", Новый ТекстовыйДокумент);
	
	
	
	Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Ставки НДС';uk='Запуск вивантаження довідника ставки ПДВ'");
	СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
	
	Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.СтавкиНДС,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
	
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	Если НЕ Отказ Тогда
		
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Валюты';uk='Запуск вивантаження довідника Валюти'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.Валюты,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Классификатор единиц измерения';uk='Запуск вивантаження довідника Класифікатор одиниць виміру'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.КлассификаторЕдиницИзмерения,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Маски штрих кодов';uk='Запуск вивантаження довідника Маски штрих кодів'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ТК_МаскиШтрихКодовOS,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Организации';uk='Запуск вивантаження довідника Організації'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.Организации,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Типы цен';uk='Запуск вивантаження довідника Типи цін'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ТипыЦен,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Торговые площадки адреса';uk='Запуск вивантаження довідника Торгові майданчики адреси'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ТорговыеПлощадки,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Склады компании';uk='Запуск вивантаження довідника Склади компанії'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.СкладыКомпании,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки изменения Организации торговых площадок';uk='Запуск розвантаження зміни Організації торгових майданчиків'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Номенклатура';uk='Запуск вивантаження довідника Номенклатура'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.Номенклатура,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Единицы измерения';uk='Запуск вивантаження довідника Одиниці виміру'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ЕдиницыИзмерения,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Характеристики номенклатуры';uk='Запуск вивантаження довідника Характеристики номенклатури'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ХарактеристикиНоменклатуры,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Отделы';uk='Запуск вивантаження довідника Відділи'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.Отделы,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки регистра сведений Номенклатура отделов';uk='Запуск розвантаження регістра відомостей Номенклатура відділів'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.НоменклатураОтделов,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Штрихкоды номенклатуры';uk='Запуск вивантаження Штрихкоди номенклатури'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Цены изменения';uk='Запуск вивантаження змін цін'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ЦеныДляРегистрации,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Акции изменения';uk='Запуск вивантаження змін акції'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Документы.ТК_НазначениеСкидок,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки итоги инвентаризаций';uk='Запуск вивантаження підсумки інвентаризацій'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Документы.Инвентаризация,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Профиль кассира';uk='Запуск вивантаження профіль касира'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ПрофилиКассиров,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Кассира';uk='Запуск вивантаження Касира'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.ФизическиеЛица,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки Статьи возврата';uk='Запуск вивантаження Статті повернення'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.Справочники.СтатьиВозврата,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	
	
	
	//Если Отказ Тогда
	//	
	//	СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
	//	СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
	//	СтрокаТаблицыИнформации.Описание = РезультатОбмена.ТекстСообщений.ПолучитьТекст();
	//	
	//КонецЕсли;
	
	РезультатОбмена.Вставить("Успешно",Не Отказ);
	
	
КонецПроцедуры

Процедура ВыполнитьОбменСфронтомОстатки(ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации)Экспорт
	ОписаниеОшибки = "";
	Подключение = СоединениеССервером(ПараметрыОбмена.НастройкиПодключения,ОписаниеОшибки);
	
	Если Подключение = Неопределено Тогда
		СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	РезультатОбмена.Вставить("ТекстСообщений", Новый ТекстовыйДокумент);
	
	Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки остатки товаров';uk='Запуск вивантаження залишки товарів'");
	СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
	
	Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ТК_НоменклатураДляИзмененияOS,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	
	
	
	Если НЕ Отказ Тогда
		
		РазмерПорции = ПараметрыОбмена.РазмерПорции;
		ПараметрыОбмена.РазмерПорции = 1;
		
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки остатки по складу';uk='Запуск вивантаження залишки по складу'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		ТаблицаВыгрузкиОстатковПоСкладам = РегистрыСведений.ТК_ДатыОстатковOS.СоздатьНаборЗаписей().Выгрузить();
		
		РезультатОбмена.Вставить("РезультатВыгрузкиОстатков",ТаблицаВыгрузкиОстатковПоСкладам);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ТК_ДатыОстатковOS,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		ПараметрыОбмена.РазмерПорции = РазмерПорции;
		
	КонецЕсли;
	
	//Если НЕ Отказ И ПараметрыОбмена.ВыгружатьТекущиеОстатки Тогда
	Если НЕ Отказ Тогда
		
		Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки текущих остатков товаров';uk='Запуск вивантаження поточних залишки товарів'");
		СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
		
		Отказ = ЗапуститьВыгрузку(Подключение,Метаданные.РегистрыСведений.ТК_OS_РегистрацияНаВыгрузкуОстатков,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
		
		ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
		
	КонецЕсли;
	
	
	
	
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	РезультатОбмена.Вставить("Успешно",Не Отказ);
	
КонецПроцедуры

Процедура ВыполнитьОбменСфронтомОстаткиПоСкладам(ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации,МассивСкладов)Экспорт
	ОписаниеОшибки = "";
	Подключение = СоединениеССервером(ПараметрыОбмена.НастройкиПодключения,ОписаниеОшибки);
	
	Если Подключение = Неопределено Тогда
		СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	РезультатОбмена.Вставить("ТекстСообщений", Новый ТекстовыйДокумент);
	
	Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки остатки товаров по складу';uk='Запуск вивантаження залишки товарів по складу'");
	СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
	
	Отказ = ЗапуститьВыгрузкуОстатковПоСкладам(Подключение,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации,МассивСкладов);
	
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	РезультатОбмена.Вставить("Успешно",Не Отказ);
	
КонецПроцедуры

Процедура ВыполнитьОбменСфронтомКонтрагенты(ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации)Экспорт
	
	ОписаниеОшибки = "";
	Подключение = СоединениеССервером(ПараметрыОбмена.НастройкиПодключения,ОписаниеОшибки);
	
	Если Подключение = Неопределено Тогда
		СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	РезультатОбмена.Вставить("ТекстСообщений", Новый ТекстовыйДокумент);
	
	Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки справочника Контрагенты';uk='Запуск вивантаження довідника Контрагенти'");
	СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
	
	Отказ = ЗапуститьВыгрузкуКонтрагентов(Подключение,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
	
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	РезультатОбмена.Вставить("Успешно",Не Отказ);
	
	
КонецПроцедуры

Процедура ВыгрузитьЦеныПоТипуЦен(ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации,ТипЦен)Экспорт
	ОписаниеОшибки = "";
	Подключение = СоединениеССервером(ПараметрыОбмена.НастройкиПодключения,ОписаниеОшибки);
	
	Если Подключение = Неопределено Тогда
		СтрокаТаблицыИнформации = ТаблицаИнформации.Добавить();
		СтрокаТаблицыИнформации.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Ошибка;
		СтрокаТаблицыИнформации.Описание = ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбмена.Вставить("ВыгрузкаПоТипуЦен",ТипЦен);
	
	
	РезультатОбмена.Вставить("ТекстСообщений", Новый ТекстовыйДокумент);
	
	Описание = Строка(ТекущаяДата()) + " " + НСтр("ru='Запуск выгрузки цен по типу "+Строка(ТипЦен)+" ';uk='Запуск вивантаження цін за типом "+Строка(ТипЦен)+" '");
	СтрокаТаблицыИнформации = ДобавитьИформацию(ТаблицаИнформации,Перечисления.ДействияПриОбмене.ВыгрузкаДанных,Описание);
	
	Отказ = ЗапуститьВыгрузкуЦен(Подключение,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации);
	
	ЗафиксироватьИформацию(СтрокаТаблицыИнформации,Отказ,РезультатОбмена);
	
	РезультатОбмена.Вставить("Успешно",Не Отказ);
	
КонецПроцедуры




Функция СформироватьПакетВыгрузки(ОбъектМетаданных,МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	Если ОбъектМетаданных = Метаданные.Справочники.СтавкиНДС Тогда
		Возврат СформироватьПакетВыгрузкиСтавокНДС(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.Валюты Тогда
		Возврат СформироватьПакетВыгрузкиВалюты(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.Номенклатура Тогда
		Возврат СформироватьПакетВыгрузкиТоваров(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.КлассификаторЕдиницИзмерения Тогда
		Возврат СформироватьПакетВыгрузкиКлассификаторовЕдиницИзмерения(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ТК_МаскиШтрихКодовOS Тогда
		Возврат СформироватьПакетВыгрузкиМаскиШтрихКодов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.Организации Тогда
		Возврат СформироватьПакетВыгрузкиОрганизации(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ТипыЦен Тогда
		Возврат СформироватьПакетВыгрузкиТипыЦен(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.КлассификаторПродаж Тогда
		Возврат СформироватьПакетВыгрузкиКлассификаторПродаж(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ТорговыеПлощадки Тогда
		Возврат СформироватьПакетТорговыеПлощадкиАдреса(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.СкладыКомпании Тогда
		Возврат СформироватьПакетСкладыКомпании(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ЕдиницыИзмерения Тогда
		Возврат СформироватьПакетВыгрузкиУпаковокНоменклатуры(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ХарактеристикиНоменклатуры Тогда
		Возврат СформироватьПакетВыгрузкиХарактеристик(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.Отделы Тогда   //Справочник Отделы
		Возврат СформироватьПакетВыгрузкиОтделов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.НоменклатураОтделов Тогда // рег. сведений НоменклатураОтделов
		Возврат СформироватьПакетВыгрузкиНоменклатураОтделов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ЦеныДляРегистрации Тогда 
		Возврат СформироватьПакетВыгрузкиЦен(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры Тогда
		Возврат СформироватьПакетВыгрузкиШтрихКодов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ТК_НоменклатураДляИзмененияOS Тогда //Остатки изменения
		Возврат СформироватьПакетВыгрузкиОстатков(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ТК_OS_РегистрацияНаВыгрузкуОстатков Тогда //Остатки изменения текущие
		Возврат СформироватьПакетВыгрузкиТекущихОстатков(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ТК_ДатыОстатковOS Тогда    //Остатки
		Возврат СформироватьПакетВыгрузкиОстатковПоСкладам(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Документы.ТК_НазначениеСкидок Тогда
		Возврат СформироватьПакетВыгрузкиДокументыНазначениеСкидок(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Документы.Инвентаризация Тогда
		Возврат СформироватьПакетВыгрузкиДокументыИнвентаризацияИтоги(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ПрофилиКассиров Тогда
		Возврат СформироватьПакетВыгрузкиПрофильКассира(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.ФизическиеЛица Тогда
		Возврат СформироватьПакетВыгрузкиКассиры(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники.СтатьиВозврата Тогда  // Статьи возврата
		Возврат СформироватьПакетВыгрузкиСтатьиВозврата(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	ИначеЕсли ОбъектМетаданных = Метаданные.РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок Тогда  // Организации торговых площадок
		Возврат СформироватьПакетОрганизацияСкладыКомпанииИзменение(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
		
	КонецЕсли;
	
КонецФункции


/////////////////////////////////////////// Обмен по заданию
Функция ЗапуститьВыгрузку(Подключение,ОбъектМетаданных,ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации,ДатаОтбора = Неопределено)
	
	РазмерПорции    = ПараметрыОбмена.РазмерПорции;
	УзелПланаОбмена = ПараметрыОбмена.УзелОбмена;
	
	
	Отказ = Ложь;
	
	ЗапуститьФоновыеЗаданияРегистрацииИзмененийВсехСообщений(УзелПланаОбмена, ОбъектМетаданных,ПараметрыОбмена.КодУзлаОбмена);
	
	
	Пока Истина Цикл 
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		РезультатЗапроса = РезультатЗапросаВыборкиИзменений(УзелПланаОбмена,ОбъектМетаданных,,РазмерПорции,МенеджерВременныхТаблиц,ДатаОтбора);
		
		ФильтрВыборки = СформироватьФильтрВыборкиИзРезультатаЗапроса(ОбъектМетаданных, РезультатЗапроса);
		
		КоличествоИзменений = ФильтрВыборки.Количество();
		
		Если КоличествоИзменений = 0 Тогда
			Прервать;
		КонецЕсли;
		
		
		НомерСообщения = УстановитьНомерСообщения(УзелПланаОбмена, ФильтрВыборки);
		
		Если НомерСообщения = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Начать_Транзакцию(Подключение);
		
		Попытка
			ТекстЗапроса = СформироватьПакетВыгрузки(ОбъектМетаданных,МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
			Если ТекстЗапроса.КоличествоСтрок() > 0 Тогда
				
				
				Если РезультатОбмена.Свойство("ТекстЗапросаОтключения") Тогда
					ВыполнитьТекстЗапросаОтключения(Подключение,Отказ,РезультатОбмена.ТекстЗапросаОтключения,РезультатОбмена.ТекстСообщений);
					//	ВыполнитьЗапрос(Подключение,Отказ,РезультатОбмена.ТекстЗапросаОтключения.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
					РезультатОбмена.Удалить("ТекстЗапросаОтключения");
					Если Отказ Тогда
						ВызватьИсключение "Ошибка выполнения запроса";
					КонецЕсли;
				КонецЕсли;
				
				ВыполнитьЗапрос(Подключение,Отказ,ТекстЗапроса.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
				Если Отказ Тогда
					
					ВызватьИсключение "Ошибка выполнения запроса";
				КонецЕсли;
				
			КонецЕсли;
			
			ЗапуститьФоновоеЗаданиеУдаленияРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения,ПараметрыОбмена.КодУзлаОбмена);
		Исключение
			Откатить_Транзакцию(Подключение);
			
			
			ЗапуститьФоновоеЗаданиеРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения,ПараметрыОбмена.КодУзлаОбмена);
			РезультатОбмена.ТекстСообщений.ДобавитьСтроку(ОписаниеОшибки());
			
			Отказ = Истина;
			Прервать;
			
		КонецПопытки;
		
		Зафиксировать_Транзакцию(Подключение);
		
		Если КоличествоИзменений < РазмерПорции Тогда
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат	Отказ;
	
КонецФункции

Функция ЗапуститьВыгрузкуОстатковПоСкладам(Подключение, ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации,МассивСкладов)
	
	Отказ = Ложь;
	
	
	Запрос = Новый Запрос;
	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.СкладКомпании КАК СкладКомпании,
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	|ПОМЕСТИТЬ УчетХарактеристик
	|ИЗ
	|	РегистрСведений.ТК_УчетХарактеристикНаСкладах.СрезПоследних(
	|			&НаДату,
	|			СкладКомпании В (&МассивСкладов)
	|				И СкладКомпании.Подразделение В (&ПодразделениеКомпании)) КАК ТК_УчетХарактеристикНаСкладахСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТК_ДатыОстатковOS.СкладКомпании КАК СкладКомпании,
	|	ЕСТЬNULL(УчетХарактеристик.НеВестиУчетПоХарактеристикам, ЛОЖЬ) КАК ХарактеристикиОтключены,
	|	ТК_ДатыОстатковOS.Дата КАК ДатаОстатков
	|ИЗ
	|	РегистрСведений.ТК_ДатыОстатковOS КАК ТК_ДатыОстатковOS
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетХарактеристик КАК УчетХарактеристик
	|		ПО ТК_ДатыОстатковOS.СкладКомпании = УчетХарактеристик.СкладКомпании
	|ГДЕ
	|	ТК_ДатыОстатковOS.СкладКомпании В(&МассивСкладов)
	|	И ТК_ДатыОстатковOS.СкладКомпании.Подразделение В(&ПодразделениеКомпании)";
	
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	Запрос.УстановитьПараметр("МассивСкладов", МассивСкладов);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ДатаОстатков  = ВыборкаДетальныеЗаписи.ДатаОстатков;
		СкладКомпании = ВыборкаДетальныеЗаписи.СкладКомпании;
		
		ЗапросОстатки = Новый Запрос;
		ЗапросОстатки.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток * 1000 КАК КоличествоОстаток,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоОборот КАК КоличествоОборот,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоРасход КАК КоличествоРасход,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.ОстаткиИОбороты(&НачалоПериода, , , , СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстаткиИОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_Остатки.ОсновнаяЕдиница КАК ОсновнаяЕдиница,
		|	ВТ_Остатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВТ_Остатки.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
		|	ВТ_Остатки.КоличествоОборот КАК КоличествоОборот,
		|	ВТ_Остатки.КоличествоПриход КАК КоличествоПриход,
		|	ВТ_Остатки.КоличествоРасход КАК КоличествоРасход,
		|	ВТ_Остатки.ИспользоватьХарактеристику КАК ИспользоватьХарактеристику
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|ГДЕ
		|	НЕ ВТ_Остатки.ОсновнаяЕдиница = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|	И ВТ_Остатки.Номенклатура.НеАктивный = ЛОЖЬ
		|	И НЕ ВТ_Остатки.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)";
		
		ЗапросОстатки.УстановитьПараметр("НачалоПериода", ДатаОстатков-60*60*24*7);
		//ЗапросОстатки.УстановитьПараметр("НеАктивныйТовар",ОбменФронтПовтИсп.МассивНеАтивногоТовара());
	//	ЗапросОстатки.УстановитьПараметр("ДатаОстатков", Новый Граница(ДатаОстатков,ВидГраницы.Включая));
		ЗапросОстатки.УстановитьПараметр("СкладКомпании", СкладКомпании);
		
		РезультатЗапросаОстатки = ЗапросОстатки.Выполнить();
		Если РезультатЗапросаОстатки.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстЗапроса = Новый ТекстовыйДокумент;
		
		SliceType 	  = ?(ВыборкаДетальныеЗаписи.ХарактеристикиОтключены,"0","1");
		WarehouseGUID = ПолучитьВнутрСсылку(СкладКомпании);
		
		стрЗапроса = "EXECUTE [dbo].[Prepare_RestSlice] "
		+ SliceType
		+","+WarehouseGUID+";";
		ТекстЗапроса.ДобавитьСтроку(стрЗапроса);
		
		ВыборкаДетальныеЗаписиОстатки = РезультатЗапросаОстатки.Выбрать();
		
		ТекстЗапросаОстаткиПоСкладам(СкладКомпании,ВыборкаДетальныеЗаписи.ХарактеристикиОтключены,ДатаОстатков,ВыборкаДетальныеЗаписиОстатки,ТекстЗапроса,РезультатОбмена);
		
		стрЗапроса = "EXECUTE [dbo].[Send_RestSlice] "
		+ SliceType
		+","+WarehouseGUID+";";
		ТекстЗапроса.ДобавитьСтроку(стрЗапроса);
		
		
		Начать_Транзакцию(Подключение);
		
		ВыполнитьЗапрос(Подключение,Отказ,ТекстЗапроса.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
		
		Если Отказ тогда
			Откатить_Транзакцию(Подключение);
			Прервать;
		Иначе
			Зафиксировать_Транзакцию(Подключение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат	Отказ;
	
КонецФункции
/////////////////////////////////////////// Обмен по заданию


Функция ЗапуститьВыгрузкуКонтрагентов(Подключение, ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации)
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительныеСведения.Объект КАК Объект,
	|	Контрагенты.Ссылка КАК Ссылка,
	|	Контрагенты.Наименование КАК Наименование,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА Контрагенты.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА Контрагенты.ПлательщикНДС
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПлательщикНДС,
	|	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДополнительныеСведения.Объект = Контрагенты.Ссылка
	|ГДЕ
	|	ДополнительныеСведения.Свойство = &Свойство
	|	И ДополнительныеСведения.Значение = ИСТИНА";
	
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","OS_АктивныеКонтрагенты"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Отказ;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	ТекстЗапросаКонтрагенты(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	
	Начать_Транзакцию(Подключение);
	
	//
	//тСтрокаЗапроса = "declare @ver int = (select recordnum from Integration1C.dbo.DATAPUMP where dirname='COMPANY')+1
	//| update Integration1C.dbo.COMPANY set DELFLAG=1,UPDATENUM=@ver 
	//| update Integration1C.dbo.DATAPUMP set RECORDNUM=@ver where dirname='COMPANY'";
	//
	//ВыполнитьЗапрос(Подключение,Отказ,тСтрокаЗапроса,РезультатОбмена.ТекстСообщений);
	
	ВыполнитьЗапрос(Подключение,Отказ,ТекстЗапроса.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
	
	
	Если Отказ тогда
		Откатить_Транзакцию(Подключение);
	Иначе
		Зафиксировать_Транзакцию(Подключение);
	КонецЕсли;
	
	
	Возврат	Отказ;
	
КонецФункции

Функция ЗапуститьВыгрузкуЦен(Подключение, ПараметрыОбмена,РезультатОбмена,ТаблицаИнформации)
	
	Отказ = Ложь;
	
	
	ТекстЗапроса = СформироватьПакетВыгрузкиЦен(Новый МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
	
	Если ТекстЗапроса.КоличествоСтрок() = 0 Тогда
		Возврат Отказ;
	КонецЕсли;
	
	
	
	
	Начать_Транзакцию(Подключение);
	
	ВыполнитьЗапрос(Подключение,Отказ,ТекстЗапроса.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
	
	Если Отказ тогда
		Откатить_Транзакцию(Подключение);
		
	Иначе
		Зафиксировать_Транзакцию(Подключение);
	КонецЕсли;
	
	
	Возврат	Отказ;
	
КонецФункции



Функция ВыгрузитьТовары(Подключение,ПараметрыОбмена, РезультатОбмена,ТаблицаИнформации)
	
	РазмерПорции    = ПараметрыОбмена.РазмерПорции;
	УзелПланаОбмена = ПараметрыОбмена.УзелОбмена;
	
	ОбъектМетаданных = Метаданные.Справочники.Номенклатура;
	
	Отказ = Ложь;
	
	ЗапуститьФоновыеЗаданияРегистрацииИзмененийВсехСообщений(УзелПланаОбмена, ОбъектМетаданных,ПараметрыОбмена.КодУзлаОбмена);
	
	
	Пока Истина Цикл 
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		РезультатЗапроса = РезультатЗапросаВыборкиИзменений(УзелПланаОбмена,ОбъектМетаданных,,РазмерПорции,МенеджерВременныхТаблиц);
		
		ФильтрВыборки = СформироватьФильтрВыборкиИзРезультатаЗапроса(ОбъектМетаданных, РезультатЗапроса);
		
		КоличествоИзменений = ФильтрВыборки.Количество();
		
		Если КоличествоИзменений = 0 Тогда
			Прервать;
		КонецЕсли;
		
		
		НомерСообщения = УстановитьНомерСообщения(УзелПланаОбмена, ФильтрВыборки);
		
		Если НомерСообщения = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Попытка
			ТекстЗапроса = СформироватьПакетВыгрузкиТоваров(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена);
			Если ТекстЗапроса.КоличествоСтрок() > 0 Тогда
				
				ВыполнитьЗапрос(Подключение,Отказ,ТекстЗапроса.ПолучитьТекст(),РезультатОбмена.ТекстСообщений);
				Если Отказ Тогда
					
					ВызватьИсключение "Ошибка выполнения запроса";
				КонецЕсли;
				
			КонецЕсли;
			
			ЗапуститьФоновоеЗаданиеУдаленияРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения,ПараметрыОбмена.КодУзлаОбмена);
		Исключение
			
			ЗапуститьФоновоеЗаданиеРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения,ПараметрыОбмена.КодУзлаОбмена);
			
			Прервать;
			
		КонецПопытки;
		
		Если КоличествоИзменений < РазмерПорции Тогда
			Прервать;
		КонецЕсли;	
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат	Отказ;
	
КонецФункции



/////////////////////////////////////////

Функция СформироватьПакетВыгрузкиСтавокНДС(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтавкиНДС.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтавкиНДС.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	СтавкиНДС.Наименование КАК Наименование,
	|	СтавкиНДС.Ставка * 100 КАК Ставка,
	|	СтавкиНДС.Порядок КАК Порядок,
	|	СтавкиНДС.СоставнойНалог КАК СоставнойНалог
	|ИЗ
	|	Справочник.СтавкиНДС КАК СтавкиНДС
	|ГДЕ
	|	СтавкиНДС.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаПоСтавкамНДС(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиВалюты(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Валюты.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Валюты.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	Валюты.Код КАК Код,
	|	Валюты.Наименование КАК Наименование,
	|	Валюты.НаименованиеПолное КАК НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) = 0
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) = 0
	|			ТОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
	|		ИНАЧЕ ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) / ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0)
	|	КОНЕЦ КАК Курс
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						ТаблицаИзменений.Ссылка КАК Ссылка
	|					ИЗ
	|						ТаблицаИзменений КАК ТаблицаИзменений)) КАК КурсыВалютСрезПоследних
	|		ПО Валюты.Ссылка = КурсыВалютСрезПоследних.Валюта
	|ГДЕ
	|	Валюты.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаВалюты(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиКлассификаторовЕдиницИзмерения(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спрКлассификаторЕдиницИзмерения.Ссылка КАК Ссылка,
	|	спрКлассификаторЕдиницИзмерения.Наименование КАК Наименование,
	|	спрКлассификаторЕдиницИзмерения.НаименованиеПолное КАК НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА спрКлассификаторЕдиницИзмерения.Ссылка.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.КлассификаторЕдиницИзмерения КАК спрКлассификаторЕдиницИзмерения
	|ГДЕ
	|	спрКлассификаторЕдиницИзмерения.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаКлассификаторЕдиницИзмерения(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиМаскиШтрихКодов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =   "ВЫБРАТЬ
	|	ТК_МаскиШтрихКодовOS.Ссылка КАК Ссылка,
	|	ТК_МаскиШтрихКодовOS.Наименование КАК Наименование,
	|	ТК_МаскиШтрихКодовOS.Маска КАК Маска,
	|	ВЫБОР
	|		КОГДА ТК_МаскиШтрихКодовOS.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТК_МаскиШтрихКодовOS КАК ТК_МаскиШтрихКодовOS
	|		ПО ТаблицаИзменений.Ссылка = ТК_МаскиШтрихКодовOS.Ссылка";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаМаскиШтрихКодов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиОрганизации(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спрОрганизации.Ссылка КАК Ссылка,
	|	спрОрганизации.Наименование КАК Наименование,
	|	"""" КАК ЮрАдрес,
	|	"""" КАК Телефон,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизацийСрезПоследних.НомерСвидетельстваПлательщикаНДС, """") КАК НомерСвидетельства,
	|	спрОрганизации.КодПоЕДРПОУ КАК ОКПО,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС, """") КАК ИНН,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС, ЛОЖЬ) КАК ПлательшикНДС,
	|	ВЫБОР
	|		КОГДА спрОрганизации.Ссылка.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	спрОрганизации.Контрагент КАК Контрагент
	|ИЗ
	|	Справочник.Организации КАК спрОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних КАК УчетнаяПолитикаОрганизацийСрезПоследних
	|		ПО спрОрганизации.Ссылка = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
	|ГДЕ
	|	спрОрганизации.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаОрганизации(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиТипыЦен(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спрТипыЦен.Ссылка КАК Ссылка,
	|	спрТипыЦен.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА спрТипыЦен.Ссылка.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.ТипыЦен КАК спрТипыЦен
	|ГДЕ
	|	спрТипыЦен.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаТипыЦен(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиКлассификаторПродаж(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторПродаж.Ссылка КАК Ссылка,
	|	КлассификаторПродаж.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА КлассификаторПродаж.Ссылка.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.КлассификаторПродаж КАК КлассификаторПродаж
	|ГДЕ
	|	КлассификаторПродаж.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаКлассификаторПродаж(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции



Функция СформироватьПакетТорговыеПлощадкиАдреса(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТорговыеПлощадки.Ссылка КАК Ссылка,
	|	ТорговыеПлощадки.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ТорговыеПлощадки.Ссылка.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ЕСТЬNULL(ТорговыеПлощадкиКонтактнаяИнформация.Значение, """") КАК АдресЗначение
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.КонтактнаяИнформация КАК ТорговыеПлощадкиКонтактнаяИнформация
	|		ПО ТорговыеПлощадки.Ссылка = ТорговыеПлощадкиКонтактнаяИнформация.Ссылка
	|			И (ТорговыеПлощадкиКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (ТорговыеПлощадкиКонтактнаяИнформация.Вид = &Вид)
	|ГДЕ
	|	ТорговыеПлощадки.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И ТорговыеПлощадки.ПодразделениеКомпании В(&ПодразделениеКомпании)";
	
	Запрос.УстановитьПараметр("Вид",УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("ФактическийАдресОбъекта"));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаТорговыеПлощадкиАдреса(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетСкладыКомпании(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаДату",ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВидСклада",ОбменФронтПовтИсп.ВыдыСкладовДляВыгрузки());
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкладыКомпании.Ссылка КАК Ссылка,
	|	СкладыКомпании.Код КАК СкладКод,
	|	СкладыКомпании.Наименование КАК Наименование,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СкладыКомпании.ТочкаДоставки) КАК Адрес,
	|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация,
	|	СкладыКомпании.ТипЦенРозничнойТорговли КАК ТипЦенРозница,
	|	ВЫБОР
	|		КОГДА СкладыКомпании.Ссылка.ПометкаУдаления
	|				ИЛИ СкладыКомпании.НеАктивен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	СкладыКомпании.Розничный КАК Розничный,
	|	СкладыКомпании.ДоступныеЦены.(
	|		НомерСтроки КАК Порядок,
	|		ТипЦен КАК ТипЦенТорговли
	|	) КАК ДоступныеЦены
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(
	|				&НаДату,
	|				ТорговаяПлощадка В
	|					(ВЫБРАТЬ
	|						ТаблицаИзменений.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка
	|					ИЗ
	|						ТаблицаИзменений КАК ТаблицаИзменений)) КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних
	|		ПО СкладыКомпании.ТорговаяПлощадка = ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.ТорговаяПлощадка
	|ГДЕ
	|	СкладыКомпании.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И СкладыКомпании.ЭтоГруппа = ЛОЖЬ
	|	И НЕ СкладыКомпании.Ссылка.ТорговаяПлощадка = ЗНАЧЕНИЕ(Справочник.ТорговыеПлощадки.ПустаяСсылка)
	|	И СкладыКомпании.ВидСклада В(&ВидСклада)
	|	И СкладыКомпании.Подразделение В(&ПодразделениеКомпании)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаСкладыКомпании(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетОрганизацияСкладыКомпанииИзменение(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаДату",ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ВидСклада",ОбменФронтПовтИсп.ВыдыСкладовДляВыгрузки());
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкладыКомпании.Ссылка КАК Ссылка,
	|	СкладыКомпании.Код КАК СкладКод,
	|	СкладыКомпании.Наименование КАК Наименование,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СкладыКомпании.ТочкаДоставки) КАК Адрес,
	|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация,
	|	СкладыКомпании.ТипЦенРозничнойТорговли КАК ТипЦенРозница,
	|	ВЫБОР
	|		КОГДА СкладыКомпании.Ссылка.ПометкаУдаления
	|				ИЛИ СкладыКомпании.НеАктивен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	СкладыКомпании.Розничный КАК Розничный,
	|	СкладыКомпании.ДоступныеЦены.(
	|		НомерСтроки КАК Порядок,
	|		ТипЦен КАК ТипЦенТорговли
	|	) КАК ДоступныеЦены
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(
	|				&НаДату,
	|				ТорговаяПлощадка В
	|					(ВЫБРАТЬ
	|						ТаблицаИзменений.ТорговаяПлощадка КАК ТорговаяПлощадка
	|					ИЗ
	|						ТаблицаИзменений КАК ТаблицаИзменений)) КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних
	|		ПО СкладыКомпании.ТорговаяПлощадка = ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.ТорговаяПлощадка
	|ГДЕ
	|	СкладыКомпании.ТорговаяПлощадка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.ТорговаяПлощадка КАК ТорговаяПлощадка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И СкладыКомпании.ЭтоГруппа = ЛОЖЬ
	|	И СкладыКомпании.ВидСклада В(&ВидСклада)
	|	И СкладыКомпании.Подразделение В(&ПодразделениеКомпании)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаСкладыКомпании(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция СформироватьПакетВыгрузкиТоваров(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	//Группы товаров	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =   "ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Ссылка,
	|	спрНоменклатура.Артикул КАК Артикул,
	|	спрНоменклатура.Родитель КАК Родитель,
	|	спрНоменклатура.Родитель.Артикул КАК АртикулРодитель,
	|	спрНоменклатура.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА спрНоменклатура.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО ТаблицаИзменений.Ссылка = спрНоменклатура.Ссылка
	|ГДЕ
	|	спрНоменклатура.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка ИЕРАРХИЯ УБЫВ";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаПоГруппамТовара(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Бутыли = ТК_СправочникиПовтИсп.ЗначенияСвойства("ЕмкостьБутыля").ВыгрузитьКолонку("Объект");
	//Бутыли = Новый СписокЗначений;
	//Бутыли.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0783314"));		//	Бутыль 7л
	//Бутыли.Добавить(Справочники.Номенклатура.НайтиПоКоду("249990"));		//	Бутыль 18,9л Полифлекс
	
	ТК_СправочникиПовтИсп.ДобавитьВМассивТовар("2ad5afbb-ea3d-11de-9813-0050569742cd","ВидНоменклатуры",Перечисления.ВидыНоменклатуры.Тара,Бутыли);
	
	Запрос.Текст =  "ВЫБРАТЬ
	|	СпрНоменклатура.Ссылка КАК Ссылка,
	|	СпрНоменклатура.Артикул КАК Артикул,
	|	СпрНоменклатура.Родитель.Артикул КАК АртикулГруппы,
	|	СпрНоменклатура.Наименование КАК Наименование,
	|	СпрНоменклатура.Наименование2 КАК Наименование2,
	|	СпрНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ПометкаУдаления
	|				ИЛИ СпрНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	|					И НЕ СпрНоменклатура.Ссылка В (&Бутыли)
	|				ИЛИ СпрНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	СпрНоменклатура.СтавкаНДС.Порядок КАК СтавкаНДСПорядок,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.СтранаПроисхождения = &Страна
	|			ТОГДА """"
	|		ИНАЧЕ ""X""
	|	КОНЕЦ КАК Импорт,
	|	СпрНоменклатура.КодУКТВЭД КАК КодУКТВЭД
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаИзменений.Ссылка = СпрНоменклатура.Ссылка
	|ГДЕ
	|	СпрНоменклатура.ЭтоГруппа = ЛОЖЬ
	|	И НЕ СпрНоменклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|	И СпрНоменклатура.НеАктивный = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Страна",ОбменФронтПовтИсп.СтранаУкраина());
	Запрос.УстановитьПараметр("Бутыли", Бутыли);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаПоТоварам(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиУпаковокНоменклатуры(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	//Запрос.УстановитьПараметр("НеАктивныйТовар",ОбменФронтПовтИсп.МассивНеАтивногоТовара());
	Запрос.Текст = "ВЫБРАТЬ
	|	спрЕдиницыИзмерения.Ссылка КАК GUID,
	|	спрЕдиницыИзмерения.Владелец КАК ARTID,
	|	спрЕдиницыИзмерения.ЕдиницаПоКлассификатору КАК UNITID,
	|	спрЕдиницыИзмерения.Наименование КАК PACKNAME,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.ЕдиницаПоКлассификатору.ORDERUNIT = ""GRM""
	|			ТОГДА спрЕдиницыИзмерения.Коэффициент
	|		ИНАЧЕ спрЕдиницыИзмерения.Коэффициент * 1000
	|	КОНЕЦ КАК PACKQUANT,
	|	спрЕдиницыИзмерения.Владелец.СрокХраненияТовара КАК PACKSHELFLIFE,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.Владелец.ТипНоменклатуры.Весовой
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК PACKDTYPE,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.Ссылка = спрЕдиницыИзмерения.Ссылка.Владелец.ОсновнаяЕдиницаИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ISDEFAULT,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.Ссылка = спрЕдиницыИзмерения.Ссылка.Владелец.БазоваяЕдиницаИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ISBASE,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.Владелец.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Продукция)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ISCOMPOSCARDTARGET,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.ПометкаУдаления
	|				ИЛИ спрЕдиницыИзмерения.Коэффициент < 0
	|				ИЛИ спрЕдиницыИзмерения.НеВыгружатьВОборудование
	|				ИЛИ спрЕдиницыИзмерения.ЕдиницаПоКлассификатору.ORDERUNIT = ""GRM""
	|				ИЛИ спрЕдиницыИзмерения.Владелец.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК DELFLAG,
	|	ВЫБОР
	|		КОГДА спрЕдиницыИзмерения.Вес > 100
	|			ТОГДА 0
	|		ИНАЧЕ спрЕдиницыИзмерения.Вес * 1000
	|	КОНЕЦ КАК PACKWEIGHT,
	|	ВЫБОР
	|		КОГДА НЕ НоменклатураПоставщиков.МаскаШтрихКода ЕСТЬ NULL
	|			ТОГДА НоменклатураПоставщиков.МаскаШтрихКода
	|		ИНАЧЕ ВЫБОР
	|				КОГДА спрЕдиницыИзмерения.Владелец.ТипНоменклатуры.Весовой
	|					ТОГДА ЗНАЧЕНИЕ(Справочник.ТК_МаскиШтрихКодовOS.ОсновнаяМаска)
	|				ИНАЧЕ ""NULL""
	|			КОНЕЦ
	|	КОНЕЦ КАК BARCID,
	|	спрЕдиницыИзмерения.Владелец.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	ВЫБОР
	|		КОГДА НоменклатураПоставщиков.МаскаШтрихКода ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВыгружатьШтрихкодПоставщика,
	|	НоменклатураПоставщиков.Артикул КАК ШтрихКодПоставщика
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК спрЕдиницыИзмерения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|		ПО спрЕдиницыИзмерения.Владелец = НоменклатураПоставщиков.Номенклатура
	|			И спрЕдиницыИзмерения.Ссылка = НоменклатураПоставщиков.ЕдиницаИзмерения
	|			И (НЕ НоменклатураПоставщиков.МаскаШтрихКода = ЗНАЧЕНИЕ(Справочник.ТК_МаскиШтрихКодовOS.ПустаяСсылка))
	|ГДЕ
	|	спрЕдиницыИзмерения.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И НЕ спрЕдиницыИзмерения.Владелец.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|	И спрЕдиницыИзмерения.Владелец.НеАктивный = ЛОЖЬ";
	
	
	
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаУпаковокНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	
	
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК СсылкаХарактеристика,
	|	ХарактеристикиНоменклатуры.Владелец.Ссылка КАК СсылкаНоменклатура,
	|	ЕдиницыИзмерения.Ссылка КАК СсылкаЕдиницаИзмерений,
	|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору КАК СсылкаЕдиницаПоКлассификатору,
	|	ХарактеристикиНоменклатуры.Наименование КАК НаименованиеХарактеристики,
	|	ЕдиницыИзмерения.Коэффициент * 1000 КАК PACKQUANT,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.ПометкаУдаления
	|				ИЛИ ЕдиницыИзмерения.Коэффициент < 0
	|				ИЛИ ЕдиницыИзмерения.НеВыгружатьВОборудование
	|				ИЛИ ХарактеристикиНоменклатуры.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК DELFLAG,
	|	ЕдиницыИзмерения.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.Вес > 100
	|			ТОГДА 0
	|		ИНАЧЕ ЕдиницыИзмерения.Вес * 1000
	|	КОНЕЦ КАК PACKWEIGHT,
	|	ХарактеристикиНоменклатуры.Владелец.СрокХраненияТовара КАК PACKSHELFLIFE,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.Ссылка = ХарактеристикиНоменклатуры.Владелец.ОсновнаяЕдиницаИзмерения
	|				ИЛИ ЕдиницыИзмерения.ПометкаУдаления
	|				ИЛИ ЕдиницыИзмерения.Владелец.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ISDEFAULT,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ХарактеристикиНоменклатуры.НеАктивная КАК НеАктивная,
	|	ХарактеристикиНоменклатуры.Владелец.ПодакцизныйТовар КАК ПодакцизныйТовар
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|			ПО ЕдиницыИзмерения.Владелец = ШтрихкодыНоменклатуры.Номенклатура
	|				И ЕдиницыИзмерения.Ссылка = ШтрихкодыНоменклатуры.Упаковка
	|		ПО ХарактеристикиНоменклатуры.Владелец = ЕдиницыИзмерения.Владелец
	|ГДЕ
	|	ЕдиницыИзмерения.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	// |	И НЕ ХарактеристикиНоменклатуры.Владелец В ИЕРАРХИИ (&НеАктивныйТовар)
	|	И ХарактеристикиНоменклатуры.Владелец.НеАктивный = ЛОЖЬ";
	
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаХарактеристикНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиХарактеристик(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК СсылкаХарактеристика,
	|	ХарактеристикиНоменклатуры.Владелец.Ссылка КАК СсылкаНоменклатура,
	|	ЕдиницыИзмерения.Ссылка КАК СсылкаЕдиницаИзмерений,
	|	ЕдиницыИзмерения.ЕдиницаПоКлассификатору КАК СсылкаЕдиницаПоКлассификатору,
	|	ХарактеристикиНоменклатуры.Наименование КАК НаименованиеХарактеристики,
	|	ЕдиницыИзмерения.Коэффициент * 1000 КАК PACKQUANT,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.ПометкаУдаления
	|				ИЛИ ЕдиницыИзмерения.Коэффициент < 0
	|				ИЛИ ЕдиницыИзмерения.НеВыгружатьВОборудование
	|				ИЛИ ХарактеристикиНоменклатуры.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК DELFLAG,
	|	ЕдиницыИзмерения.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.Вес > 100
	|			ТОГДА 0
	|		ИНАЧЕ ЕдиницыИзмерения.Вес * 1000
	|	КОНЕЦ КАК PACKWEIGHT,
	|	ХарактеристикиНоменклатуры.Владелец.СрокХраненияТовара КАК PACKSHELFLIFE,
	|	ВЫБОР
	|		КОГДА ЕдиницыИзмерения.Ссылка = ХарактеристикиНоменклатуры.Владелец.ОсновнаяЕдиницаИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ISDEFAULT,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ХарактеристикиНоменклатуры.НеАктивная КАК НеАктивная,
	|	ХарактеристикиНоменклатуры.Владелец.ПодакцизныйТовар КАК ПодакцизныйТовар
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|			ПО ЕдиницыИзмерения.Владелец = ШтрихкодыНоменклатуры.Номенклатура
	|				И ЕдиницыИзмерения.Ссылка = ШтрихкодыНоменклатуры.Упаковка
	|		ПО ХарактеристикиНоменклатуры.Владелец = ЕдиницыИзмерения.Владелец
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И ХарактеристикиНоменклатуры.Владелец.НеАктивный = ЛОЖЬ";
	
	
	
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаХарактеристикНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиОтделов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст ="ВЫБРАТЬ
	|	Отделы.Ссылка КАК Ссылка,
	|	Отделы.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА Отделы.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.Отделы КАК Отделы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИзменений КАК ТаблицаИзменений
	|		ПО Отделы.Ссылка = ТаблицаИзменений.Ссылка";
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаОтделов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиНоменклатураОтделов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	
	Запрос.Текст ="ВЫБРАТЬ
	|	ТаблицаИзменений.Склад КАК Склад,
	|	ТаблицаИзменений.Отдел КАК Отдел,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА НоменклатураОтделов.Склад ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураОтделов КАК НоменклатураОтделов
	|		ПО ТаблицаИзменений.Склад = НоменклатураОтделов.Склад
	|			И ТаблицаИзменений.Отдел = НоменклатураОтделов.Отдел
	|			И ТаблицаИзменений.Номенклатура = НоменклатураОтделов.Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК спрСкладыКомпании
	|		ПО ТаблицаИзменений.Склад = спрСкладыКомпании.Ссылка
	|ГДЕ
	|	спрСкладыКомпании.Подразделение В(&ПодразделениеКомпании)";
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаНоменклатураОтделов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция СформироватьПакетВыгрузкиШтрихКодов(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаИзменений.ШтрихКод КАК ШтрихКодИзменения,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК ШтрихКод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ ВТ_ШтрихКода
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаИзменений.ШтрихКод = ШтрихкодыНоменклатуры.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ШтрихКода.ШтрихКодИзменения КАК ШтрихКодИзменения,
	|	NULL КАК ШтрихКод,
	|	NULL КАК Номенклатура,
	|	NULL КАК Характеристика,
	|	NULL КАК Упаковка,
	|	NULL КАК НеАктивная
	|ИЗ
	|	ВТ_ШтрихКода КАК ВТ_ШтрихКода
	|ГДЕ
	|	ВТ_ШтрихКода.ШтрихКод ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ШтрихКода.ШтрихКодИзменения,
	|	ВТ_ШтрихКода.ШтрихКод,
	|	ВТ_ШтрихКода.Номенклатура,
	|	ВТ_ШтрихКода.Характеристика,
	|	ВТ_ШтрихКода.Упаковка,
	|	ЛОЖЬ
	|ИЗ
	|	ВТ_ШтрихКода КАК ВТ_ШтрихКода
	|ГДЕ
	|	НЕ ВТ_ШтрихКода.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|	И ВТ_ШтрихКода.Номенклатура.НеАктивный = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ШтрихКода.ШтрихКодИзменения,
	|	ВТ_ШтрихКода.ШтрихКод,
	|	ВТ_ШтрихКода.Номенклатура,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	ВТ_ШтрихКода.Упаковка,
	|	ХарактеристикиНоменклатуры.НеАктивная
	|ИЗ
	|	ВТ_ШтрихКода КАК ВТ_ШтрихКода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ВТ_ШтрихКода.Номенклатура = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	НЕ ВТ_ШтрихКода.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|	И ВТ_ШтрихКода.Номенклатура.НеАктивный = ЛОЖЬ";
	
	
	
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаШтрихКодаНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиЦен(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	ЦеныНадату = ТекущаяДатаСеанса();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЦеныНадату",ЦеныНадату);
	//Запрос.УстановитьПараметр("НеАктивныйТовар",ОбменФронтПовтИсп.МассивНеАтивногоТовара());
	
	ТипЦен = Неопределено;
	ПараметрыОбмена.Свойство("ВыгрузкаПоТипуЦен",ТипЦен);
	Запрос.УстановитьПараметр("ТипЦен",ТипЦен);
	
	
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросТекст = "ВЫБРАТЬ
	              |	ЦеныСрезПоследних.ТипЦен.ТипЦенЗаБлок КАК ТипЦенЗаБлок,
	              |	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	              |	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	              |	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	              |	ЦеныСрезПоследних.Цена КАК Цена,
	              |	ЦеныСрезПоследних.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	              |	ЦеныСрезПоследних.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	              |	ЦеныСрезПоследних.Номенклатура.ЕдиницаИзмеренияУпаковки.ЕдиницаПоКлассификатору КАК КлассификаторУпаковки,
	              |	ЦеныСрезПоследних.Номенклатура.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК КлассификаторЕдИзм
	              |ПОМЕСТИТЬ ВТ_Цены
	              |ИЗ
	              |	РегистрСведений.Цены.СрезПоследних(
	              |			&ЦеныНадату,
	              |			ТипЦен = &ТипЦен
	              |				И Номенклатура.НеАктивный = ЛОЖЬ
	              |				И НЕ Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	              |				И ЕСТЬNULL(Характеристика.НеАктивная, ЛОЖЬ) = ЛОЖЬ) КАК ЦеныСрезПоследних
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВТ_Цены.ТипЦен КАК ТипЦен,
	              |	ВТ_Цены.ТипЦен.НеВестиУчетПоХарактеристикам КАК ОтклХарактеристика,
	              |	ВТ_Цены.Номенклатура КАК Номенклатура,
	              |	ВЫБОР
	              |		КОГДА ВТ_Цены.Номенклатура.ИспользованиеХарактеристик
	              |			ТОГДА ВТ_Цены.Характеристика
	              |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	              |	КОНЕЦ КАК Характеристика,
	              |	спрЕдиницыИзмерения.Ссылка КАК ЕденицаИзмерения,
	              |	ВЫБОР
	              |		КОГДА ВТ_Цены.ТипЦенЗаБлок
	              |			ТОГДА ВТ_Цены.Цена * 100
	              |		ИНАЧЕ ВТ_Цены.Цена * 100 * спрЕдиницыИзмерения.Коэффициент
	              |	КОНЕЦ КАК Цена,
	              |	ВТ_Цены.Цена КАК тЦена,
	              |	ВЫБОР
	              |		КОГДА ВТ_Цены.Номенклатура.ГруппаЦенообразования = ЗНАЧЕНИЕ(Справочник.ГруппыЦенообразования.Сигареты)
	              |				ИЛИ ВТ_Цены.Номенклатура.ГруппаЦенообразования = ЗНАЧЕНИЕ(Справочник.ГруппыЦенообразования.Сигары)
	              |			ТОГДА 1
	              |		КОГДА ВТ_Цены.Номенклатура.ГруппаЦенообразования = ЗНАЧЕНИЕ(Справочник.ГруппыЦенообразования.Спиртное)
	              |				ИЛИ ВТ_Цены.Номенклатура.ГруппаЦенообразования = ЗНАЧЕНИЕ(Справочник.ГруппыЦенообразования.СпиртноеИндикатив)
	              |			ТОГДА 2
	              |		ИНАЧЕ 0
	              |	КОНЕЦ КАК ТипМинЦен,
	              |	спрЕдиницыИзмерения.Коэффициент КАК КоэффициентТовара,
	              |	спрЕдиницыИзмерения.НеВыгружатьВОборудование КАК НеВыгружать,
	              |	ЕСТЬNULL(тИндикатив.УчитыватьКрепость, ЛОЖЬ) КАК УчитыватьКрепость,
	              |	ЕСТЬNULL(тИндикатив.ЦенаРозничная, 0) КАК Индикатив,
	              |	ВТ_Цены.Номенклатура.Крепость КАК Крепость
	              |ИЗ
	              |	ВТ_Цены КАК ВТ_Цены
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК спрЕдиницыИзмерения
	              |		ПО ВТ_Цены.Номенклатура = спрЕдиницыИзмерения.Владелец
	              |			И (ВЫБОР
	              |				КОГДА ВТ_Цены.ТипЦенЗаБлок
	              |					ТОГДА ВТ_Цены.КлассификаторУпаковки = спрЕдиницыИзмерения.ЕдиницаПоКлассификатору
	              |				ИНАЧЕ ИСТИНА
	              |			КОНЕЦ)
	              |			И (спрЕдиницыИзмерения.НеВыгружатьВОборудование = ЛОЖЬ)
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_МинимальныеРозничныеЦены.СрезПоследних(&ЦеныНадату, ) КАК тИндикатив
	              |		ПО ВТ_Цены.Номенклатура.ТипИндикатива = тИндикатив.ТипИндикатива";
	
	
	
	
	
	Если ТипЦен = Неопределено Тогда
		Условие = "
		|			(ТипЦен, Номенклатура, Характеристика) В
		|					(ВЫБРАТЬ
		|						ТаблицаИзменений.ТипЦен КАК ТипЦен,
		|						ТаблицаИзменений.Номенклатура КАК Номенклатура,
		|						ТаблицаИзменений.Характеристика КАК Характеристика
		|					ИЗ
		|						ТаблицаИзменений КАК ТаблицаИзменений)";
		
		ЗапросТекст = СтрЗаменить(ЗапросТекст,"ТипЦен = &ТипЦен",Условие);
		
	КонецЕсли;
	
	
	Запрос.Текст = ЗапросТекст;
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	//СписокНоменклатуры = новый СписокЗначений;
	//СписокНоменклатуры.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0009000"));
	//СписокНоменклатуры.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0100000"));
	//Запрос.УстановитьПараметр("Номенклатура", СписокНоменклатуры);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаЦеныНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена,ЦеныНадату);
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиОстатков(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	Запрос.Текст = "ВЫБРАТЬ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.СкладКомпании КАК СкладКомпании,
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	|ПОМЕСТИТЬ УчетХарактеристик
	|ИЗ
	|	РегистрСведений.ТК_УчетХарактеристикНаСкладах.СрезПоследних(&НаДату, ) КАК ТК_УчетХарактеристикНаСкладахСрезПоследних
	|ГДЕ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.СкладКомпании КАК СкладКомпании,
	|	ЕСТЬNULL(УчетХарактеристик.НеВестиУчетПоХарактеристикам, ЛОЖЬ) КАК ХарактеристикиОтключены,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТК_ДатыОстатковOS.Дата КАК ДатаОстатков
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ДатыОстатковOS КАК ТК_ДатыОстатковOS
	|			ЛЕВОЕ СОЕДИНЕНИЕ УчетХарактеристик КАК УчетХарактеристик
	|			ПО ТК_ДатыОстатковOS.СкладКомпании = УчетХарактеристик.СкладКомпании
	|		ПО ТаблицаИзменений.СкладКомпании = ТК_ДатыОстатковOS.СкладКомпании
	|ГДЕ
	|	ТаблицаИзменений.Номенклатура.НеАктивный = ЛОЖЬ
	|	И ТК_ДатыОстатковOS.СкладКомпании.Подразделение В(&ПодразделениеКомпании)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкладКомпании";
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	
	Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("СкладКомпании") Цикл
		
		СкладКомпании = ВыборкаДетальныеЗаписи.СкладКомпании;
		
		ДатаОстатков  = ВыборкаДетальныеЗаписи.ДатаОстатков;
		
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			нСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(нСтрока,ВыборкаДетальныеЗаписи,"Номенклатура,ХарактеристикаНоменклатуры");
		КонецЦикла;
		
		ЗапросОстатки = Новый Запрос;
		ЗапросОстатки.Текст = 
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток * 1000 КАК КоличествоОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			,
		|			СкладКомпании = &СкладКомпании
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		|	ВТ_Товары.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику,
		|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		|		ПО ВТ_Товары.Номенклатура = Остатки.Номенклатура
		|			И ВТ_Товары.ХарактеристикаНоменклатуры = Остатки.ХарактеристикаНоменклатуры
		|ГДЕ
		|	НЕ ВТ_Товары.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)";
		
		//
		//		|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		//|	ВТ_Товары.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику,
		
		
		//ЗапросОстатки.УстановитьПараметр("ДатаОстатков",  Новый Граница(ДатаОстатков,ВидГраницы.Включая));
		//ЗапросОстатки.УстановитьПараметр("НеАктивныйТовар", ОбменФронтПовтИсп.МассивНеАтивногоТовара());
		ЗапросОстатки.УстановитьПараметр("СкладКомпании", СкладКомпании);
		ЗапросОстатки.УстановитьПараметр("Товары", ТаблицаТоваров);
		
		РезультатЗапросаОстатки = ЗапросОстатки.Выполнить();
		Если РезультатЗапросаОстатки.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		
		ВыборкаДетальныеЗаписиОстатки = РезультатЗапросаОстатки.Выбрать();
		
		ТекстЗапросаОстаткиПоСкладам(СкладКомпании,ВыборкаДетальныеЗаписи.ХарактеристикиОтключены,ДатаОстатков,ВыборкаДетальныеЗаписиОстатки,ТекстЗапроса,РезультатОбмена);
		
		
	КонецЦикла;
	
	
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиТекущихОстатков(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДата()));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.СкладКомпании КАК СкладКомпании,
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	|ПОМЕСТИТЬ УчетХарактеристик
	|ИЗ
	|	РегистрСведений.ТК_УчетХарактеристикНаСкладах.СрезПоследних(&НаДату, ) КАК ТК_УчетХарактеристикНаСкладахСрезПоследних
	|ГДЕ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.СкладКомпании КАК СкладКомпании,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ТК_ДатыОстатковOS.Дата, &НачалоДня) КАК ДатаОстатков,
	|	ЕСТЬNULL(УчетХарактеристик.НеВестиУчетПоХарактеристикам, ЛОЖЬ) КАК ХарактеристикиОтключены
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ДатыОстатковOS КАК ТК_ДатыОстатковOS
	|		ПО ТаблицаИзменений.СкладКомпании = ТК_ДатыОстатковOS.СкладКомпании
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетХарактеристик КАК УчетХарактеристик
	|		ПО ТаблицаИзменений.СкладКомпании = УчетХарактеристик.СкладКомпании
	|ГДЕ
	|	ТаблицаИзменений.Номенклатура.НеАктивный = ЛОЖЬ
	|	И ТаблицаИзменений.СкладКомпании.Подразделение В(&ПодразделениеКомпании)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкладКомпании";
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	
	Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("СкладКомпании") Цикл
		
		СкладКомпании = ВыборкаДетальныеЗаписи.СкладКомпании;
		
		ДатаОстатков  = ВыборкаДетальныеЗаписи.ДатаОстатков;
		
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			нСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(нСтрока,ВыборкаДетальныеЗаписи,"Номенклатура,ХарактеристикаНоменклатуры");
		КонецЦикла;
		
		ЗапросОстатки = Новый Запрос;
		ЗапросОстатки.Текст = "ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток * 1000 КАК КоличествоОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			,
		|			СкладКомпании = &СкладКомпании
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		|	ВТ_Товары.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику,
		|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		|		ПО ВТ_Товары.Номенклатура = Остатки.Номенклатура
		|			И ВТ_Товары.ХарактеристикаНоменклатуры = Остатки.ХарактеристикаНоменклатуры
		|ГДЕ
		|	НЕ ВТ_Товары.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)";
		
		
		//|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		//|	ВТ_Товары.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику,
		
		
		ЗапросОстатки.УстановитьПараметр("СкладКомпании", СкладКомпании);
		ЗапросОстатки.УстановитьПараметр("Товары", ТаблицаТоваров);
		
		РезультатЗапросаОстатки = ЗапросОстатки.Выполнить();
		Если РезультатЗапросаОстатки.Пустой() Тогда
			Продолжить;
		КонецЕсли;         		
		
		ВыборкаДетальныеЗаписиОстатки = РезультатЗапросаОстатки.Выбрать();
		
		ТекстЗапросаОстаткиПоСкладам(СкладКомпании,ВыборкаДетальныеЗаписи.ХарактеристикиОтключены,ДатаОстатков,ВыборкаДетальныеЗаписиОстатки,ТекстЗапроса,РезультатОбмена);
		
		
	КонецЦикла;
	
	
	
	Возврат ТекстЗапроса;
	
	
КонецФункции


Функция СформироватьПакетВыгрузкиОстатковПоСкладам(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.СкладКомпании КАК СкладКомпании,
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	|ПОМЕСТИТЬ УчетХарактеристик
	|ИЗ
	|	РегистрСведений.ТК_УчетХарактеристикНаСкладах.СрезПоследних(, ) КАК ТК_УчетХарактеристикНаСкладахСрезПоследних
	|ГДЕ
	|	ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.СкладКомпании КАК СкладКомпании,
	|	ЕСТЬNULL(УчетХарактеристик.НеВестиУчетПоХарактеристикам, ЛОЖЬ) КАК ХарактеристикиОтключены,
	|	ТК_ДатыОстатковOS.Дата КАК ДатаОстатков
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ДатыОстатковOS КАК ТК_ДатыОстатковOS
	|			ЛЕВОЕ СОЕДИНЕНИЕ УчетХарактеристик КАК УчетХарактеристик
	|			ПО ТК_ДатыОстатковOS.СкладКомпании = УчетХарактеристик.СкладКомпании
	|		ПО ТаблицаИзменений.СкладКомпании = ТК_ДатыОстатковOS.СкладКомпании
	|ГДЕ
	|	ТК_ДатыОстатковOS.СкладКомпании.Подразделение В(&ПодразделениеКомпании)";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		СкладКомпании 			= ВыборкаДетальныеЗаписи.СкладКомпании;
		ДатаОстатков 			= ВыборкаДетальныеЗаписи.ДатаОстатков;
		ХарактеристикиОтключены = ВыборкаДетальныеЗаписи.ХарактеристикиОтключены;
		
		ДанныеВыгрузки = РезультатОбмена.РезультатВыгрузкиОстатков.Добавить();
		ДанныеВыгрузки.СкладКомпании       =  СкладКомпании;
		ДанныеВыгрузки.ДатаПолнойВыгрузки  = ТекущаяДата();
		
		ЗапросОстатки = Новый Запрос;
		ЗапросОстатки.УстановитьПараметр("НачалоПериода", ДатаОстатков-60*60*24*7);
		//ЗапросОстатки.УстановитьПараметр("ДатаОстатков", Новый Граница(ДатаОстатков,ВидГраницы.Включая));
		ЗапросОстатки.УстановитьПараметр("СкладКомпании", СкладКомпании);
		//	ЗапросОстатки.УстановитьПараметр("НеАктивныйТовар",ОбменФронтПовтИсп.МассивНеАтивногоТовара());
		
		ЗапросОстатки.Текст = "ВЫБРАТЬ
		                      |	Остатки.Номенклатура КАК Номенклатура,
		                      |	Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		                      |	Остатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
		                      |	Остатки.Номенклатура.ИспользованиеХарактеристик КАК ИспользоватьХарактеристику,
		                      |	Остатки.КоличествоКонечныйОстаток * 1000 КАК КоличествоОстаток,
		                      |	Остатки.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
		                      |	Остатки.КоличествоОборот КАК КоличествоОборот,
		                      |	Остатки.КоличествоПриход КАК КоличествоПриход,
		                      |	Остатки.КоличествоРасход КАК КоличествоРасход
		                      |ПОМЕСТИТЬ ВТ_Остатки
		                      |ИЗ
		                      |	РегистрНакопления.ОстаткиТоваровКомпании.ОстаткиИОбороты(&НачалоПериода, , Период, , СкладКомпании = &СкладКомпании) КАК Остатки
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	ВТ_Остатки.Номенклатура КАК Номенклатура,
		                      |	ВТ_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		                      |	ВТ_Остатки.ОсновнаяЕдиница КАК ОсновнаяЕдиница,
		                      |	ВТ_Остатки.ИспользоватьХарактеристику КАК ИспользоватьХарактеристику,
		                      |	ВТ_Остатки.КоличествоОстаток КАК КоличествоОстаток,
		                      |	ВТ_Остатки.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
		                      |	ВТ_Остатки.КоличествоОборот КАК КоличествоОборот,
		                      |	ВТ_Остатки.КоличествоПриход КАК КоличествоПриход,
		                      |	ВТ_Остатки.КоличествоРасход КАК КоличествоРасход
		                      |ИЗ
		                      |	ВТ_Остатки КАК ВТ_Остатки
		                      |ГДЕ
		                      |	НЕ ВТ_Остатки.ОсновнаяЕдиница = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		                      |	И ВТ_Остатки.Номенклатура.НеАктивный = ЛОЖЬ
		                      |	И НЕ ВТ_Остатки.Номенклатура.КодУКТВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторУКТВЭД.ПустаяСсылка)";
		
		Рез = ЗапросОстатки.Выполнить();
		Если Рез.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаОстатки = Рез.Выбрать();
		
		ТекстЗапросаОстаткиПоСкладам(СкладКомпании,ХарактеристикиОтключены,ДатаОстатков,ВыборкаОстатки,ТекстЗапроса,РезультатОбмена);
		
		
	КонецЦикла;
	
	
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция СформироватьПакетВыгрузкиДокументыНазначениеСкидок_OLD(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст ="ВЫБРАТЬ
	|	ТК_НазначениеСкидок.Ссылка КАК Ссылка,
	|	ТК_НазначениеСкидок.ХозОперация КАК ХозОперация,
	|	ТК_НазначениеСкидок.КлассификаторПродажи КАК КлассификаторПродажи,
	|	ТК_НазначениеСкидок.ТипСкидки КАК ТипСкидки,
	|	ТК_НазначениеСкидок.Дата КАК Дата,
	|	ТК_НазначениеСкидок.Номер КАК Номер,
	|	ТК_НазначениеСкидок.ПометкаУдаления КАК ПометкаУдаления,
	|	ТК_НазначениеСкидок.Проведен КАК Проведен,
	|	ТК_НазначениеСкидок.Скидки.(
	|		ФлагВытеснения КАК ФлагВытеснения,
	|		ЗначениеСкидки КАК ЗначениеСкидки,
	|		СпособВычисления КАК СпособВычисления,
	|		Номенклатура КАК Номенклатура,
	|		Договор КАК Договор,
	|		Количество КАК Количество,
	|		Правило КАК Правило,
	|		СуммаЧека КАК СуммаЧека,
	|		СуммаСтроки КАК СуммаСтроки,
	|		ДисконтнаяКарта КАК ДисконтнаяКарта,
	|		СуммаНакопления КАК СуммаНакопления,
	|		НачалоДействия КАК НачалоДействия,
	|		КонецДействия КАК КонецДействия,
	|		ДниНедели КАК ДниНедели,
	|		МаксимальнаяСуммаПодарка КАК МаксимальнаяСуммаПодарка,
	|		Номенклатура.Артикул КАК ТоварАртикул
	|	) КАК Скидки,
	|	ТК_НазначениеСкидок.ТорговыеПлощадки.(
	|		ТорговаяПлощадка КАК ТорговаяПлощадка
	|	) КАК ТорговыеПлощадки
	|ИЗ
	|	Документ.ТК_НазначениеСкидок КАК ТК_НазначениеСкидок
	|ГДЕ
	|	ТК_НазначениеСкидок.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И ТК_НазначениеСкидок.ПодразделениеКомпании В(&ПодразделениеКомпании)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаНазначениеСкидок(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
	
КонецФункции
Функция СформироватьПакетВыгрузкиДокументыНазначениеСкидок(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ПараметрыОбмена.ПодразделениеКомпании);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст ="ВЫБРАТЬ
	              |	ТК_НазначениеСкидокСкидки.Ссылка КАК Ссылка,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.ХозОперация КАК ХозОперация,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.КлассификаторПродажи КАК КлассификаторПродажи,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.ТипСкидки КАК ТипСкидки,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.Дата КАК Дата,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.Номер КАК Номер,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.ПометкаУдаления КАК ПометкаУдаления,
	              |	ТК_НазначениеСкидокСкидки.Ссылка.Проведен КАК Проведен,
	              |	ТК_НазначениеСкидокСкидки.НомерСтроки КАК НомерСтроки,
	              |	ТК_НазначениеСкидокСкидки.ФлагВытеснения КАК ФлагВытеснения,
	              |	ТК_НазначениеСкидокСкидки.ЗначениеСкидки КАК ЗначениеСкидки,
	              |	ТК_НазначениеСкидокСкидки.СпособВычисления КАК СпособВычисления,
	              |	ТК_НазначениеСкидокСкидки.Номенклатура КАК Номенклатура,
	              |	ТК_НазначениеСкидокСкидки.Договор КАК Договор,
	              |	ТК_НазначениеСкидокСкидки.Количество КАК Количество,
	              |	ТК_НазначениеСкидокСкидки.Правило КАК Правило,
	              |	ТК_НазначениеСкидокСкидки.СуммаЧека КАК СуммаЧека,
	              |	ТК_НазначениеСкидокСкидки.СуммаСтроки КАК СуммаСтроки,
	              |	ТК_НазначениеСкидокСкидки.ДисконтнаяКарта КАК ДисконтнаяКарта,
	              |	ТК_НазначениеСкидокСкидки.СуммаНакопления КАК СуммаНакопления,
	              |	ТК_НазначениеСкидокСкидки.НачалоДействия КАК НачалоДействия,
	              |	ТК_НазначениеСкидокСкидки.КонецДействия КАК КонецДействия,
	              |	ТК_НазначениеСкидокСкидки.ДниНедели КАК ДниНедели,
	              |	ТК_НазначениеСкидокСкидки.МаксимальнаяСуммаПодарка КАК МаксимальнаяСуммаПодарка,
	              |	ТК_НазначениеСкидокСкидки.Номенклатура.Артикул КАК ТоварАртикул,
	              |	ТК_НазначениеСкидокСкидки.КоэффициентНабора КАК КоэффициентНабора
	              |ИЗ
	              |	Документ.ТК_НазначениеСкидок.Скидки КАК ТК_НазначениеСкидокСкидки
	              |ГДЕ
	              |	ТК_НазначениеСкидокСкидки.Ссылка В
	              |			(ВЫБРАТЬ
	              |				ТаблицаИзменений.Ссылка КАК Ссылка
	              |			ИЗ
	              |				ТаблицаИзменений КАК ТаблицаИзменений)
	              |	И ТК_НазначениеСкидокСкидки.Ссылка.ПодразделениеКомпании В(&ПодразделениеКомпании)
	              |
	              |УПОРЯДОЧИТЬ ПО
	              |	Ссылка,
	              |	НомерСтроки
	              |ИТОГИ
	              |	МИНИМУМ(НачалоДействия),
	              |	МАКСИМУМ(КонецДействия)
	              |ПО
	              |	Ссылка,
	              |	Номенклатура
	              |АВТОУПОРЯДОЧИВАНИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТекстЗапросаНазначениеСкидок(Выборка,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиДокументыИнвентаризацияИтоги(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодразделениеКомпании",ОбменФронтПовтИсп.МассивПодразделенийАттика());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст ="ВЫБРАТЬ
	|	ТаблицаИзменений.Ссылка КАК Ссылка,
	|	ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).Дата КАК Дата,
	|	ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).Номер КАК Номер,
	|	ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).СкладКомпании КАК СкладКомпании,
	|	ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).ТипЦенИтогов КАК ТипЦен
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).ПодразделениеКомпании В (&ПодразделениеКомпании)
	|	И ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).СкладКомпании.Розничный
	|	И ВЫРАЗИТЬ(ТаблицаИзменений.Ссылка КАК Документ.Инвентаризация).Проведен";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		ТаблицаИтогов = Документы.Инвентаризация.СформироватьТаблицуИтоговПоИнвентаризации(ВыборкаДетальныеЗаписи.Ссылка,ВыборкаДетальныеЗаписи.Дата,ВыборкаДетальныеЗаписи.ТипЦен);
		ТекстЗапросаИнвентаризацияИтоги(ВыборкаДетальныеЗаписи,ТаблицаИтогов,ТекстЗапроса,РезультатОбмена);
	КонецЦикла;
	
	Возврат ТекстЗапроса;
	
КонецФункции



Функция СформироватьПакетВыгрузкиПрофильКассира(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрофилиКассиров.Ссылка КАК Ссылка,
	|	ПрофилиКассиров.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ПрофилиКассиров.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ПрофилиКассиров.НастройкаПравКассира.(
	|		ИдентификаторПрава КАК ИдентификаторПрава,
	|		ВЫБОР
	|			КОГДА ПрофилиКассиров.НастройкаПравКассира.Разрешено
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Разрешено
	|	) КАК НастройкаПравКассира
	|ИЗ
	|	Справочник.ПрофилиКассиров КАК ПрофилиКассиров
	|ГДЕ
	|	ПрофилиКассиров.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаПоПрофилюКассира(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиКассиры(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка,
	|	ФизическиеЛица.ПрофильКассира КАК ПрофильКассира,
	|	ФизическиеЛица.Наименование КАК ФИО,
	|	ФизическиеЛица.Наименование КАК Наименование,
	|	ФизическиеЛица.ШтрихКодНаКассу КАК ШтрихКодНаКассу,
	|	ФизическиеЛица.ПарольНаКассу КАК ПарольНаКассу,
	|	ФизическиеЛица.ЛогинНаКассу КАК ЛогинНаКассу,
	|	0 КАК ДоступПоТТ,
	|	ВЫБОР
	|		КОГДА ФизическиеЛица.ЭтоКассир
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КассирОтключен,
	|	ВЫБОР
	|		КОГДА ФизическиеЛица.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)
	|	И ФизическиеЛица.ЭтоКассир
	|	И НЕ ФизическиеЛица.ПрофильКассира = ЗНАЧЕНИЕ(Справочник.ПрофилиКассиров.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаВыгрузкиКассира(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СформироватьПакетВыгрузкиСтатьиВозврата(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиВозврата.Ссылка КАК Ссылка,
	|	СтатьиВозврата.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА СтатьиВозврата.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	Справочник.СтатьиВозврата КАК СтатьиВозврата
	|ГДЕ
	|	СтатьиВозврата.Ссылка В
	|			(ВЫБРАТЬ
	|				ТаблицаИзменений.Ссылка КАК Ссылка
	|			ИЗ
	|				ТаблицаИзменений КАК ТаблицаИзменений)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстЗапросаСтатьиВозврата(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена);
	
	Возврат ТекстЗапроса;
	
КонецФункции



Функция СформироватьПакетВыгрузкиШтрихКодыТоваров(МенеджерВременныхТаблиц,РезультатОбмена,ПараметрыОбмена)
	
	ТекстЗапроса = Новый ТекстовыйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =   "ВЫБРАТЬ
	|	ТаблицаИзменений.ШтрихКод КАК ШтрихКодИзменения,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	|	ШтрихкодыНоменклатуры.Упаковка КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL
	|				ИЛИ ШтрихкодыНоменклатуры.Номенклатура.ПометкаУдаления
	|				ИЛИ ШтрихкодыНоменклатуры.Характеристика.ПометкаУдаления
	|				ИЛИ ШтрихкодыНоменклатуры.Упаковка.ПометкаУдаления
	|				ИЛИ ШтрихкодыНоменклатуры.Упаковка.НеВыгружатьВОборудование
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаИзменений.ШтрихКод = ШтрихкодыНоменклатуры.Штрихкод";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		GUID       = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ЕдиницаИзмерения);
		GUIDTYPE   = "0";
		EXBARCBODY = "'"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихКодИзменения),30))+"'";
		DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_EXBARC] "
		+GUID
		+","+GUIDTYPE
		+","+EXBARCBODY
		+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		
	КонецЦикла;
	
	
	Запрос.Текст =  "ВЫБРАТЬ
	|	ТаблицаИзменений.ШтрихКод КАК ШтрихКодИзменения,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ХарактеристикиНоменклатуры.Ссылка КАК СсылкаХарактеристика,
	|	ШтрихкодыНоменклатуры.Упаковка КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL
	|				ИЛИ ШтрихкодыНоменклатуры.Номенклатура.ПометкаУдаления
	|				ИЛИ ХарактеристикиНоменклатуры.ПометкаУдаления
	|				ИЛИ ШтрихкодыНоменклатуры.Упаковка.ПометкаУдаления
	|				ИЛИ ШтрихкодыНоменклатуры.Упаковка.НеВыгружатьВОборудование
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПометкаУдаления
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|			ПО ШтрихкодыНоменклатуры.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	|		ПО ТаблицаИзменений.ШтрихКод = ШтрихкодыНоменклатуры.Штрихкод";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		
		GUID   = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ЕдиницаИзмерения)+Прав(ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СсылкаХарактеристика),32);   //Гуид ЕдиницыИзмерения
		GUIDTYPE   = "0";
		EXBARCBODY = "'"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихКодИзменения),30))+"'";
		DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_EXBARC] "
		+GUID
		+","+GUIDTYPE
		+","+EXBARCBODY
		+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		
	КонецЦикла;
	
	
	Возврат ТекстЗапроса;
	
КонецФункции






#Область ЗапросыOS

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаПоСтавкамНДС(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID     = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		TAXGRPID = Формат(ВыборкаДетальныеЗаписи.Порядок,"ЧЦ=15; ЧН=; ЧГ=");
		TAXGRPNAME = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
		TAXGRPRATE = Формат(ВыборкаДетальныеЗаписи.Ставка,"ЧЦ=15; ЧН=; ЧГ=");
		DELFLAG =   ВыборкаДетальныеЗаписи.ПометкаУдаления;
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_TAXGRP] "+GUID+","+TAXGRPID+","+TAXGRPNAME+","+TAXGRPRATE+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаВалюты(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	//GUID	binary(16)	binary(16)	ГУИД элента в 1С	
	//CURRENCYID	INTEGER	INTEGER	Код валюты	
	//CURRENCYNAME	NVARCHAR(50)	NVARCHAR(50)	Название валюты	
	//CURRENCYSNAME	NVARCHAR(50)	NVARCHAR(50)	Краткое название валюты	
	//CURRENCYCODE	INTEGER	INTEGER	Код валюты	
	//CURRENCYRATE	DECIMAL(18,6)	DECIMAL(18,6)	Курс по отношению к базовой валюте. Коэффициент, на который умножается текущая валюта с целью получения значения в базовой.	
	//DELFLAG	SMALLINT	SMALLINT	Признак удаления (0 - запись не удалена, 1 - запись удалена).	
	
	
	
	//EXECUTE [Integration1C].[dbo].[UPSERT_CURRENCY] 0x9DB00017A477003C11DDA4CE757E8AF0  ,'Гривна'  ,'UAH'  ,'980'  ,1.000000  ,0
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID          = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		CURRENCYCODE  = "'" +Формат(ВыборкаДетальныеЗаписи.Код,"ЧЦ=15; ЧН=; ЧГ=")+"'";
		CURRENCYNAME  = "'" + СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),50))+"'";
		CURRENCYSNAME = "'" + СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.НаименованиеПолное),50))+"'";
		CURRENCYRATE  = Формат(ВыборкаДетальныеЗаписи.Курс ,"ЧЦ=15; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
		DELFLAG       = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=10; ЧН=; ЧГ=");
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_CURRENCY] "+GUID+","+CURRENCYSNAME+","+CURRENCYNAME+","+CURRENCYCODE+","+CURRENCYRATE+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаКлассификаторЕдиницИзмерения(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		GUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		UNITNAME =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
		UNITFULLNAME = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.НаименованиеПолное))+"'";
		Если НРег(СокрЛП(ВыборкаДетальныеЗаписи.Наименование)) ="кг" Тогда 
			DEFAULTPACKDTYPE  = 0;
		Иначе
			DEFAULTPACKDTYPE  = 1;
		КонецЕсли;
		DEFAULTQUANTMASK = 0;
		DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_UNIT] "+GUID+","+UNITNAME+","+UNITFULLNAME+","+DEFAULTPACKDTYPE+","+DEFAULTQUANTMASK+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаОрганизации(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	
	ВалютаРегл = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		FIRMGRPID      = "0x0";
		LOYALTYCLUBID  = "0x0";
		CURRENCYID     = ПолучитьВнутрСсылку(ВалютаРегл);
		//DOCPREFIX      = "'OS-{D7}'";
		CERTIFICATENUM = "'"+СокрЛП(ВыборкаДетальныеЗаписи.НомерСвидетельства)+"'"; 
		PHONES         = "'"+СокрЛП(Лев(ВыборкаДетальныеЗаписи.Телефон,50))+"'";
		REGISTRYNUM    = "'"+СокрЛП(Лев(ВыборкаДетальныеЗаписи.ОКПО,50))+"'";
		TAXIDENTNUM    = "'"+СокрЛП(Лев(ВыборкаДетальныеЗаписи.ИНН,50))+"'";
		ASKPINCODE     = "0";
		FIRMNAME       = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		FIRMADDR       = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.ЮрАдрес),100))+"'";
		CEO            = "''";
		CHIEFACCOUNTANT = "''";
		MAILADDR       = "''";
		//COSTPRICETYPE  = "0";
		//PAYVAT         = Формат(ВыборкаДетальныеЗаписи.ПлательшикНДС,"ЧЦ=1; ЧН=; ЧГ=");
		DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_FIRM] "
		+GUID
		+","+FIRMGRPID
		+","+LOYALTYCLUBID
		+","+CURRENCYID
		//	+","+DOCPREFIX
		+","+CERTIFICATENUM
		+","+PHONES
		+","+REGISTRYNUM
		+","+TAXIDENTNUM
		+","+ASKPINCODE
		+","+FIRMNAME
		+","+FIRMADDR
		+","+CEO
		+","+CHIEFACCOUNTANT
		+","+MAILADDR
		//	+","+COSTPRICETYPE
		//	+","+PAYVAT
		+","+DELFLAG+";";
		
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Контрагент) Тогда
			
			GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Контрагент);
			COMPANYGRPID   = "0x0";
			PRCLEVELID     = "0x0";
			CERTIFICATENUM = "''"; 
			PHONES         = "''";
			REGISTRYNUM    = "'"+СокрЛП(ВыборкаДетальныеЗаписи.Контрагент.КодПоЕДРПОУ)+"'";
			TAXIDENTNUM    = "''";
			COMPANYNAME    = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Контрагент.Наименование),100))+"'";
			COMPANYSNAME   = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Контрагент.Наименование),100))+"'";
			MAILADDR       = "''";
			JURIDICALADDR  = "''";
			PAYVAT         = ?(ВыборкаДетальныеЗаписи.Контрагент.ПлательщикНДС,"0","1");
			DELFLAG =   ?(ВыборкаДетальныеЗаписи.Контрагент.ПометкаУдаления,"1","0");
			
			тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_COMPANY] "+GUID
			+","+COMPANYGRPID
			+","+PRCLEVELID
			+","+CERTIFICATENUM
			+","+PHONES
			+","+REGISTRYNUM
			+","+TAXIDENTNUM
			+","+COMPANYNAME
			+","+COMPANYSNAME
			+","+MAILADDR
			+","+JURIDICALADDR
			+","+PAYVAT
			+","+DELFLAG
			+";";
			
			ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			
			
		КонецЕсли;
		
	КонецЦикла;
	
	
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаТипыЦен(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID          = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		PRCLEVELNAME  =  "'" + СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),250))+"'";
		DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_PRCLEVEL] "+GUID+","+PRCLEVELNAME+",NULL,"+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ТекстЗапросаКлассификаторПродаж(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	//	
	//Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	//	GUID          = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
	//	PRCLEVELNAME  =  "'" + СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),250))+"'";
	//	DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
	//	
	//	тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_PRCLEVEL] "+GUID+","+PRCLEVELNAME+",NULL,"+DELFLAG+";";
	//	
	//	ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	//КонецЦикла;
	
	
КонецПроцедуры




//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаМаскиШтрихКодов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		BARCNAME =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
		BARCMASK = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Маска))+"'";
		DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_BARC] "+GUID+","+BARCNAME+","+BARCMASK+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаТорговыеПлощадкиАдреса(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		GUID          = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		LOCATIONNAME  =  "'" + СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		DELFLAG = Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=10; ЧН=; ЧГ=");
		
		Если  УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВJSON(ВыборкаДетальныеЗаписи.АдресЗначение) Тогда
			Чтение = Новый ЧтениеJSON;
			Чтение.УстановитьСтроку(ВыборкаДетальныеЗаписи.АдресЗначение);
			СтруктураАдреса =  ПрочитатьJSON(Чтение);
			Если СтруктураАдреса.Свойство("city") Тогда
				CITY  = "'м."+ ЭкранироватьСимволы(СтруктураАдреса.city) +"'";
			Иначе     
				CITY  = "''";
			КонецЕсли;
			
			Если  СтруктураАдреса.Свойство("street") Тогда
				ADDRESS = "'"+ ЭкранироватьСимволы(СтруктураАдреса.street);
				Если  СтруктураАдреса.Свойство("streetType") Тогда
					ADDRESS  = ADDRESS +" "+ ЭкранироватьСимволы(СтруктураАдреса.streetType) +"'"; 
				Иначе
					ADDRESS = ADDRESS +"'";
				КонецЕсли;
				
			Иначе
				ADDRESS = "''";
			КонецЕсли;
			
		Иначе
			CITY    = "''";
			ADDRESS = "''";
			
		КонецЕсли;
		
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_LOCATION] "+GUID+","+LOCATIONNAME+","+ "'Україна'"+","+CITY+","+ADDRESS+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаСкладыКомпании(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	ОсновнойТипЦен = ПолучитьВнутрСсылку(Справочники.ТипыЦен.ОсновнойТипЦенПродажи);	
	ТекстЗапросаОтключения = Новый ТекстовыйДокумент;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		PRCLEVELID      = ?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТипЦенРозница),ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ТипЦенРозница),ОсновнойТипЦен);
		CASHPROFILEID       = "2";
		ARTHOTKEYSETID      = "NULL";
		WARHOUSEID          = GUID;
		PRNSCHEMEID         = "NULL";
		CURRENCYID          = "NULL";
		WAREHOUSENAME       = "'"+СокрЛП(ВыборкаДетальныеЗаписи.СкладКод) +" "+ СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),50))+"'";
		SAREANAME           = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),255))+"'";
		SAREADDR            = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Адрес),1024))+"'";
		ASKPINCODE          = "0";
		RECEIPTHEADER       = "NULL";
		RECEIPTNFOOTER      = "NULL";
		RECEIPTFIRSTHEADER  = "NULL";
		RECEIPTENDFOOTER    = "NULL";
		LOCATIONID     		= ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ТорговаяПлощадка);
		FIRMID      		= ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Организация);
		DELFLAG 			= Формат(ВыборкаДетальныеЗаписи.ПометкаУдаления,"ЧЦ=1; ЧН=; ЧГ=");
		
		//Склад
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_WAREHOUSE] "
		+GUID
		+","+LOCATIONID
		+","+WAREHOUSENAME
		+","+FIRMID
		+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		
		
		ВыборкаПорядокСклад =  ВыборкаДетальныеЗаписи.ДоступныеЦены.Выбрать();
		КоличествоДоступныхЦен = ВыборкаПорядокСклад.Количество();
		
		//Торговая площадка
		тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_SAREA] "
		+GUID
		+","+?(КоличествоДоступныхЦен = 0,PRCLEVELID,ОсновнойТипЦен)
		+","+CASHPROFILEID
		+","+ARTHOTKEYSETID
		+","+WARHOUSEID
		+","+PRNSCHEMEID
		+","+CURRENCYID
		+","+SAREANAME
		+","+SAREADDR
		+","+ASKPINCODE
		+","+RECEIPTHEADER
		+","+RECEIPTNFOOTER
		+","+RECEIPTFIRSTHEADER
		+","+RECEIPTENDFOOTER
		+","+DELFLAG+";";
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		
		
		//Отключим все разрешенные цены
		тСтрокаЗапроса = "declare @ver int = (select recordnum from Integration1C.dbo.datapump where dirname = 'SAREAPRC')+1  update Integration1C.dbo.sareaprc set delflag=1,updatenum=@ver where sareaid in(select sareaid from Integration1C.dbo.sarea where  [GUID]="+GUID+") if @@ROWCOUNT>0  update Integration1C.dbo.datapump set RECORDNUM=@ver where dirname = 'SAREAPRC'";
		ТекстЗапросаОтключения.ДобавитьСтроку(тСтрокаЗапроса);
		
		
		
		//Разрешенные цены
		
		Если КоличествоДоступныхЦен > 0 Тогда
			Пока ВыборкаПорядокСклад.Следующий() Цикл
				PRCLEVELID1 =  ПолучитьВнутрСсылку(ВыборкаПорядокСклад.ТипЦенТорговли);
				OrderKey   = Формат(ВыборкаПорядокСклад.Порядок,"ЧЦ=5; ЧГ=");
				тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_SAREAPRC] "
				+GUID
				+","+PRCLEVELID1
				+","+OrderKey
				+","+DELFLAG+";";
				ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			КонецЦикла;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТипЦенРозница) Тогда
			
			OrderKey   = Формат(1,"ЧЦ=5; ЧГ=");
			тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_SAREAPRC] "
			+GUID
			+","+PRCLEVELID
			+","+OrderKey
			+","+DELFLAG+";";
			ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекстЗапросаОтключения.КоличествоСтрок() > 0 Тогда
		РезультатОбмена.Вставить("ТекстЗапросаОтключения",ТекстЗапросаОтключения);
	КонецЕсли;
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаУпаковокНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.UNITID) Тогда
			Продолжить;
		КонецЕсли;
		
		
		GUID   = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.GUID);   //Гуид ЕдиницыИзмерения
		ARTID  = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ARTID);  //Гуид Номенклатура
		BARCID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.BARCID);  //Гуид МаскиШтрихКодов
		TAREID = "NULL"; //Гуид тары
		UNITID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.UNITID);  //Гуид КлассификаторЕдиницИзмерения
		OFFERID = "NULL"; //Гуид спец предложения
		Если ВыборкаДетальныеЗаписи.ПодакцизныйТовар = Истина Тогда
			PROPERTYGRPID = "56";
		Иначе
			PROPERTYGRPID = "NULL"; //Гуид доп свойства
		КонецЕсли;
		PACKNAME =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.PACKNAME))+"'";  //ЕдиницыИзмерения.Наименование
		
		PACKQUANT = Формат(ВыборкаДетальныеЗаписи.PACKQUANT,"ЧЦ=10; ЧН=; ЧГ="); // ЕдиницыИзмерения.Коэффициент
		QUANTMASK = "0";
		PACKDTYPE = ВыборкаДетальныеЗаписи.PACKDTYPE;    //Тип класса упаковки.
		ISDEFAULT = ВыборкаДетальныеЗаписи.ISDEFAULT; //ОсновнаяЕдиницаИзмерения
		ISBASE    = ВыборкаДетальныеЗаписи.ISBASE; //БазоваяЕдиницаИзмерения
		ISCOMPOSCARDTARGET   = ВыборкаДетальныеЗаписи.ISCOMPOSCARDTARGET; // признак производства
		PACKWEIGHT    = Формат(ВыборкаДетальныеЗаписи.PACKWEIGHT,"ЧЦ=10; ЧН=; ЧГ=");//Вес
		PACKSHELFLIFE    = Формат(ВыборкаДетальныеЗаписи.PACKSHELFLIFE,"ЧЦ=10; ЧН=; ЧГ=");//СрокХранения
		
		DELFLAG =  ВыборкаДетальныеЗаписи.DELFLAG;
		//
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_PACK] "
		+GUID+","
		+ARTID+","
		+BARCID+","
		+TAREID+","
		+OFFERID+","
		+UNITID+","
		+PROPERTYGRPID+","
		+PACKNAME+","
		+PACKQUANT+","
		+QUANTMASK+","
		+PACKDTYPE+","
		+PACKSHELFLIFE+","
		+ISDEFAULT+","
		+ISBASE+","
		+ISCOMPOSCARDTARGET+","
		+PACKWEIGHT+","
		+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		Если ВыборкаДетальныеЗаписи.ВыгружатьШтрихкодПоставщика Тогда
			GUIDTYPE   = "0";
			EXBARCBODY = "'"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихКодПоставщика),30))+"'";
			
			
			тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_EXBARC] "
			+GUID
			+","+GUIDTYPE
			+","+EXBARCBODY
			+","+DELFLAG+";";
			ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			
		КонецЕсли;
		
		
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаХарактеристикНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СсылкаЕдиницаИзмерений) 
			ИЛИ Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СсылкаХарактеристика) ИЛИ Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СсылкаЕдиницаПоКлассификатору) Тогда
			Продолжить;
		КонецЕсли;
		
		
		GUID   = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СсылкаЕдиницаИзмерений)+Прав(ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СсылкаХарактеристика),32);   //Гуид ЕдиницыИзмерения
		ARTID  = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СсылкаНоменклатура);  //Гуид Номенклатура
		BARCID = "NULL";  //Гуид МаскиШтрихКодов
		TAREID = "NULL"; //Гуид тары
		UNITID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СсылкаЕдиницаПоКлассификатору);  //Гуид КлассификаторЕдиницИзмерения
		OFFERID = "NULL"; //Гуид спец предложения
		PROPERTYGRPID = "NULL"; //Гуид доп свойства
		PACKNAME =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+" - "+ ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.НаименованиеХарактеристики)+"'";  //ЕдиницыИзмерения.Наименование
		
		PACKQUANT = Формат(ВыборкаДетальныеЗаписи.PACKQUANT,"ЧЦ=10; ЧН=; ЧГ="); // ЕдиницыИзмерения.Коэффициент
		QUANTMASK = "0";
		PACKDTYPE = "1";    //Тип класса упаковки.
		ISDEFAULT = ВыборкаДетальныеЗаписи.ISDEFAULT; //ОсновнаяЕдиницаИзмерения
		ISBASE    = "0"; //БазоваяЕдиницаИзмерения
		ISCOMPOSCARDTARGET   = "0"; // признак производства
		PACKWEIGHT    = Формат(ВыборкаДетальныеЗаписи.PACKWEIGHT,"ЧЦ=10; ЧН=; ЧГ=");//Вес
		PACKSHELFLIFE    = Формат(ВыборкаДетальныеЗаписи.PACKSHELFLIFE,"ЧЦ=10; ЧН=; ЧГ=");//Срок годности в днях
		
		DELFLAG =  ВыборкаДетальныеЗаписи.DELFLAG;
		//
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_PACK] "
		+GUID+","
		+ARTID+","
		+BARCID+","
		+TAREID+","
		+OFFERID+","
		+UNITID+","
		+PROPERTYGRPID+","
		+PACKNAME+","
		+PACKQUANT+","
		+QUANTMASK+","
		+PACKDTYPE+","
		+PACKSHELFLIFE+","
		+ISDEFAULT+","
		+ISBASE+","
		+ISCOMPOSCARDTARGET+","
		+PACKWEIGHT+","
		+DELFLAG+";";
		//
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ШтрихКод)  Тогда
			EXBARCBODY = "'"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихКод),30))+"'";
			DELFLAGШК    = ?(ВыборкаДетальныеЗаписи.НеАктивная,"1","0");
			тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_EXBARC] "
			+GUID
			+","+"0"
			+","+EXBARCBODY
			+","+DELFLAGШК+";";
			ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаЦеныНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена,ЦеныНадату)
	
	
	PACKPRCDATE = Формат(ЦеныНадату, "ДФ=yyyyMMddЧЧммсс");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Характеристика) И ВыборкаДетальныеЗаписи.ОтклХарактеристика  Тогда
			Продолжить;
		ИначеЕсли ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Характеристика) И ВыборкаДетальныеЗаписи.НеВыгружать Тогда
			Продолжить;
		КонецЕсли;
		
		PACKGUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ЕденицаИзмерения) +?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Характеристика),Прав(ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Характеристика),32),""); //ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ЕдиницаИзмерения);			
		PRCGUID  = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ТипЦен);
		PACKPRICE   = Формат(ВыборкаДетальныеЗаписи.Цена,"ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
		Если ВыборкаДетальныеЗаписи.ТипМинЦен = 1 Тогда
			PACKMINPRICE = 	PACKPRICE;
		ИначеЕсли ВыборкаДетальныеЗаписи.ТипМинЦен = 2 Тогда
			
			МинимальнаяРозничнаяЦена = ЦенообразованиеСервер.ПолучитьМинимальнуюРозничнуюЦену(ВыборкаДетальныеЗаписи.Номенклатура);
			Если МинимальнаяРозничнаяЦена <> Неопределено И ВыборкаДетальныеЗаписи.тЦена < МинимальнаяРозничнаяЦена Тогда
				ЦенаПоПравилам = Окр(МинимальнаяРозничнаяЦена,6);/// ЦенообразованиеСервер.ОкруглитьЦену(ВыборкаДетальныеЗаписи.ТипЦен, ВыборкаДетальныеЗаписи.тЦена);
				БольшаяЦена    = (Цел(МинимальнаяРозничнаяЦена * Pow(10,6) + 1)) / Pow(10,6);
				Если ЦенаПоПравилам < БольшаяЦена Тогда
					PACKMINPRICE = Формат(БольшаяЦена*100*ВыборкаДетальныеЗаписи.КоэффициентТовара,"ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
				Иначе
					PACKMINPRICE = Формат(ЦенаПоПравилам*100*ВыборкаДетальныеЗаписи.КоэффициентТовара,"ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
				КонецЕсли;
			ИначеЕсли  МинимальнаяРозничнаяЦена <> Неопределено Тогда
				PACKMINPRICE  = Формат(МинимальнаяРозничнаяЦена*100*ВыборкаДетальныеЗаписи.КоэффициентТовара,"ЧЦ=18; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
				
			Иначе
				PACKMINPRICE  = "0";
			КонецЕсли;
		Иначе
			PACKMINPRICE  = "0";
		КонецЕсли;
		
		PACKEXTMINPRICE   = "0";
		PACKBONUSMINPRICE = "0"; 
		DELFLAG = ?(ВыборкаДетальныеЗаписи.Цена=0 ИЛИ ВыборкаДетальныеЗаписи.НеВыгружать ,"1","0");
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_PACKPRC] "
		+PACKGUID
		+","+PRCGUID
		+","+DELFLAG+";";
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_PACKPRCHISTORY] "
		+PACKGUID
		+","+PRCGUID
		+","+PACKPRCDATE 
		+","+PACKPRICE   
		+","+PACKMINPRICE  
		+","+PACKEXTMINPRICE  
		+","+PACKBONUSMINPRICE  
		+","+DELFLAG+";";
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаШтрихКодаНоменклатуры(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ШтрихКод) Тогда
			
			Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Упаковка) Тогда
				Продолжить;
			КонецЕсли;
			
			
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Характеристика) Тогда
				GUID   = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Упаковка)+Прав(ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Характеристика),32);   //Гуид ЕдиницыИзмерения
			Иначе
				GUID       = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Упаковка);
			КонецЕсли;
			GUIDTYPE   = "0";
			EXBARCBODY = "'"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихКод),30))+"'";
			DELFLAG = ?(ВыборкаДетальныеЗаписи.НеАктивная,"1","0");
			
			тСтрокаЗапроса = "EXECUTE [Integration1C].[dbo].[UPSERT_EXBARC] "
			+GUID
			+","+GUIDTYPE
			+","+EXBARCBODY
			+","+DELFLAG+";";
		Иначе
			
			тСтрокаЗапроса = "EXEC [dbo].[DELETE_EXBARC]  @EXBARCBODY='"+СокрЛП(Лев(ОбработатьШтрихКод(ВыборкаДетальныеЗаписи.ШтрихкодИзменения),30))+"';";
		КонецЕсли;
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаПоГруппамТовара(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ГУИД =  ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		КодГруппы = Формат(ВыборкаДетальныеЗаписи.Артикул,"ЧЦ=15; ЧН=; ЧГ=");
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Родитель) Тогда
			КодРодителя = Формат(ВыборкаДетальныеЗаписи.АртикулРодитель,"ЧЦ=15; ЧН=; ЧГ=");
		Иначе
			КодРодителя = "Null";
		КонецЕсли;
		Наименование = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
		ПометкаУдаления = ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_GRP] "+ГУИД+","+КодГруппы+",Null,"+КодРодителя+","+Наименование+",Null,"+ПометкаУдаления+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаПоТоварам(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СтавкаНДСПорядок) Тогда
			тОшибка =  "Ошибка выгрузки товра: " +"["+ВыборкаДетальныеЗаписи.Артикул+"] "+ ВыборкаДетальныеЗаписи.Наименование+" пустая ставка НДС";
			РезультатОбмена.ТекстСообщений.ДобавитьСтроку(тОшибка);
			Продолжить;
			
		КонецЕсли;
		
		
		Попытка
			GUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
			ARTID = Формат(Число(ВыборкаДетальныеЗаписи.Артикул),"ЧЦ=15; ЧН=; ЧГ=");
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.АртикулГруппы) Тогда
				GRPID = Формат(Число(ВыборкаДетальныеЗаписи.АртикулГруппы),"ЧЦ=15; ЧН=; ЧГ=");
			Иначе
				GRPID ="0";
			КонецЕсли;
			OFFERID = "NULL";
			ARTCODE =  ARTID;
			ARTNAME =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.НаименованиеПолное))+"'";
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Наименование2) Тогда
				ARTSNAME = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование2))+"'";
			Иначе
				ARTSNAME = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.КодУКТВЭД) Тогда
				//UKEZEDCODE =   "'" +ЭкранироватьСимволы(ФорматироватьСтроку(Строка(ВыборкаДетальныеЗаписи.КодУКТВЭД)))+ВыборкаДетальныеЗаписи.Импорт+"'"
				UKEZEDCODE =   "'" +ЭкранироватьСимволы(ФорматироватьСтроку(Строка(ВыборкаДетальныеЗаписи.КодУКТВЭД)))+"'";	//	Хвост 31/07/2023
			Иначе
				UKEZEDCODE ="NULL";
			КонецЕсли;
			
			
			TAXGRPID = Формат(ВыборкаДетальныеЗаписи.СтавкаНДСПорядок,"ЧЦ=15; ЧН=; ЧГ=");
			DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		Исключение
			тОшибка =  "Ошибка выгрузки товра: " +"["+ВыборкаДетальныеЗаписи.Артикул+"] "+ ВыборкаДетальныеЗаписи.Наименование+" "+ ОписаниеОшибки();
			РезультатОбмена.ТекстСообщений.ДобавитьСтроку(тОшибка);
			Продолжить;
		КонецПопытки;
		
		
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_ART] "
		+GUID
		+","+ARTID
		+","+GRPID
		+","+OFFERID
		+","+ARTCODE
		+","+ARTNAME
		+","+ARTSNAME
		+","+UKEZEDCODE
		+","+TAXGRPID
		+","+DELFLAG
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаОстаткиПоСкладам(СкладКомпании,ХарактеристикиОтключены,ДатаОстатков,ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	WarehouseGUID = ПолучитьВнутрСсылку(СкладКомпании);
	SliceType     = ?(ХарактеристикиОтключены,"0","1");
	EntityDate    = Формат(ДатаОстатков, "ДФ=yyyyMMddЧЧммсс");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Номенклатура) ИЛИ НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ОсновнаяЕдиница) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.ИспользоватьХарактеристику И ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры) Тогда
			идХар = Прав(ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры),32);
		Иначе
			идХар = "";
		КонецЕсли;
		
		
		
		тСтрокаЗапроса = "EXECUTE [dbo].[Set_RestSliceElement] " 
		+ SliceType
		+","+ WarehouseGUID
		+","+ ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Номенклатура)
		+","+ ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ОсновнаяЕдиница)+идХар
		+","+ EntityDate
		+","+Формат(ВыборкаДетальныеЗаписи.КоличествоОстаток,"ЧЦ=15; ЧН=0; ЧГ=")+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаКонтрагенты(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		COMPANYGRPID   = "0x0";
		PRCLEVELID     = "0x0";
		CERTIFICATENUM = "''"; 
		PHONES         = "''";
		REGISTRYNUM    = "'"+СокрЛП(ВыборкаДетальныеЗаписи.КодПоЕДРПОУ)+"'";
		TAXIDENTNUM    = "''";
		COMPANYNAME    = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		COMPANYSNAME   = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		MAILADDR       = "''";
		JURIDICALADDR  = "''";
		PAYVAT         = ВыборкаДетальныеЗаписи.ПлательщикНДС;
		DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_COMPANY] "+GUID
		+","+COMPANYGRPID
		+","+PRCLEVELID
		+","+CERTIFICATENUM
		+","+PHONES
		+","+REGISTRYNUM
		+","+TAXIDENTNUM
		+","+COMPANYNAME
		+","+COMPANYSNAME
		+","+MAILADDR
		+","+JURIDICALADDR
		+","+PAYVAT
		+","+DELFLAG
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаНазначениеСкидок_OLD(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		глДата =  Дата("20200101");
		ДатаПорядка = Формат(ВыборкаДетальныеЗаписи.Дата, "ДФ=ггггММддЧЧммсс");
		НомерПорядка = СокрЛП(ВыборкаДетальныеЗаписи.Номер);
		DOCGUID   = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		
		ТекстЗапроса.ДобавитьСтроку("EXECUTE [dbo].[PREP_OFFER] "+DOCGUID+";");
		
		ТипСкидки = ВыборкаДетальныеЗаписи.ТипСкидки;
		КлассификаторПродажи = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.КлассификаторПродажи);
		АкцияНаименование      = "'"+Строка(ВыборкаДетальныеЗаписи.КлассификаторПродажи)+"'";
		
		Если ВыборкаДетальныеЗаписи.Проведен Тогда
			
			ВыборкаСтрокАкций = ВыборкаДетальныеЗаписи.Скидки.Выбрать();
			Сч = 0;
			
			Пока ВыборкаСтрокАкций.Следующий()Цикл 
				Сч = Сч + 1;
				
				Если ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаТовар Тогда
					Если ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Абсолютная") Тогда
						РазмерСкидки  = "'$"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=")+"'";
					ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Относительная") Тогда
						РазмерСкидки  = "'%"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=")+"'";
					ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
						РазмерСкидки  = "'P"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=")+"'";
					Иначе
						РазмерСкидки = "''";
					КонецЕсли;
					
					УсловиеСкидки = "''";
					
				ИначеЕсли  ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаКоличествоТовара Тогда
					РазмерСкидки  = "'"+?(ВыборкаСтрокАкций.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная,"P","%")+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=")+"'";
					УсловиеСкидки = "'N(1000,{W,"+Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ=")+":"+СокрЛП(ВыборкаСтрокАкций.ТоварАртикул)+"})'";
				ИначеЕсли ТипСкидки = Справочники.ТК_ТипыСкидок.СпециальнаяЦена Тогда
					
					Если ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
						РазмерСкидки  = "'P"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=")+"'";
					Иначе
						РазмерСкидки = "''";
					КонецЕсли;
					
					УсловиеСкидки = "''";
					
				Иначе
					Продолжить;
				КонецЕсли;
				
				ВремяОкончания = Формат(Окр((глДата - ВыборкаСтрокАкций.КонецДействия) / 86400, 0), "ЧГ=0");
				
				Пока СтрДлина(ВремяОкончания) < 5 Цикл
					ВремяОкончания = "0" + ВремяОкончания;	
				КонецЦикла;
				Порядок	= Формат(ВыборкаСтрокАкций.НачалоДействия, "ДФ=ггггММдд") + ВремяОкончания + ДатаПорядка + НомерПорядка;
				НоменклатураGUID       = ПолучитьВнутрСсылку(ВыборкаСтрокАкций.Номенклатура);
				СтрокаНачалоДействия   = Формат(ВыборкаСтрокАкций.НачалоДействия,"ДФ=yyyyMMddЧЧммсс");
				СтрокаКонецДействия    = Формат(ВыборкаСтрокАкций.КонецДействия,"ДФ=yyyyMMddЧЧммсс");
				ДниДействия = "'P("+Формат(ВыборкаСтрокАкций.НачалоДействия,"ДФ=yyyyMMdd")+","+Формат(ВыборкаСтрокАкций.КонецДействия,"ДФ=yyyyMMdd")+")'";
				
				СтрокаЗапрос = "EXECUTE  [dbo].[UPSERT_OFFER] "
				+ DOCGUID
				+","+Формат(Сч,"ЧЦ=10; ЧГ=")
				+","+НоменклатураGUID
				+","+СтрокаНачалоДействия
				+","+СтрокаКонецДействия
				+","+АкцияНаименование
				+","+РазмерСкидки
				+","+УсловиеСкидки
				+","+"null"
				+","+ДниДействия
				+","+"'"+Лев(Порядок,37)+"'"
				+","+КлассификаторПродажи+";";
				ТекстЗапроса.ДобавитьСтроку(СтрокаЗапрос);
				
				
				
			КонецЦикла;
			
			ВыборкаТорговыхПолощадок = ВыборкаДетальныеЗаписи.ТорговыеПлощадки.Выбрать();
			Пока ВыборкаТорговыхПолощадок.Следующий()Цикл 
				Если НЕ ЗначениеЗаполнено(ВыборкаТорговыхПолощадок.ТорговаяПлощадка.ПодразделениеКомпании) Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстЗапроса.ДобавитьСтроку("EXECUTE  [dbo].[BIND_OFFER2SAREA] "+DOCGUID+","+ПолучитьВнутрСсылку(ВыборкаТорговыхПолощадок.ТорговаяПлощадка)+";");
			КонецЦикла;
			
			
			
			
		КонецЕсли;
		
		ТекстЗапроса.ДобавитьСтроку("EXECUTE [dbo].[FINISH_OFFER] "+DOCGUID+";");
		
		
	КонецЦикла;
	
	
КонецПроцедуры
Процедура ТекстЗапросаНазначениеСкидок(ВыборкаДок,ТекстЗапроса,РезультатОбмена)
	
	Пока ВыборкаДок.Следующий() Цикл
		
		глДата		= Дата("20200101");
		ДатаПорядка	= Формат(ВыборкаДок.Дата, "ДФ=ггггММддЧЧммсс");
		НомерПорядка= СокрЛП(ВыборкаДок.Номер);
		DOCGUID		= ПолучитьВнутрСсылку(ВыборкаДок.Ссылка);
		
		ТекстЗапроса.ДобавитьСтроку("EXECUTE [dbo].[PREP_OFFER] "+DOCGUID+";");
		
		ТипСкидки			 = ВыборкаДок.ТипСкидки;
		КлассификаторПродажи = ПолучитьВнутрСсылку(ВыборкаДок.КлассификаторПродажи);
		АкцияНаименование	 = "'"+Строка(ВыборкаДок.КлассификаторПродажи)+"'";

		Если ВыборкаДок.Проведен Тогда
			
			ВыборкаТовар = ВыборкаДок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Сч = 0;
			Пока ВыборкаТовар.Следующий()Цикл
				
				Сч = Сч + 1;
				
				ВремяОкончания = Формат(Окр((глДата - ВыборкаТовар.КонецДействия) / 86400, 0), "ЧГ=0");
				
				Пока СтрДлина(ВремяОкончания) < 5 Цикл
					ВремяОкончания = "0" + ВремяОкончания;	
				КонецЦикла;
				
				Порядок				= Формат(ВыборкаТовар.НачалоДействия, "ДФ=ггггММдд") + ВремяОкончания + ДатаПорядка + НомерПорядка;
				НоменклатураGUID	= ПолучитьВнутрСсылку(ВыборкаТовар.Номенклатура);
				ОбщееНачалоДействия	= Формат(ВыборкаТовар.НачалоДействия,"ДФ=yyyyMMddЧЧммсс");
				ОбщееКонецДействия	= Формат(ВыборкаТовар.КонецДействия,"ДФ=yyyyMMddЧЧммсс");
				
				ВыборкаСтрокАкций = ВыборкаТовар.Выбрать();
				
				РазмерСкидки = "";
				УсловиеСкидки = "";
				ДниДействия = "";
				
				Пока ВыборкаСтрокАкций.Следующий()Цикл 
					
					Если ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Абсолютная") Тогда
						_РазмерСкидки  = "$"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
					ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Относительная") Тогда
						_РазмерСкидки  = "%"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
					ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
						_РазмерСкидки  = "P"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
					Иначе
						_РазмерСкидки = "";
					КонецЕсли;
					
					Если ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаТовар Тогда
						//Если ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Абсолютная") Тогда
						//	_РазмерСкидки = "$"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						//ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.Относительная") Тогда
						//	_РазмерСкидки = "%"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						//ИначеЕсли ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
						//	_РазмерСкидки = "P"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						//Иначе
						//	_РазмерСкидки = "";
						//КонецЕсли;
						//
						_УсловиеСкидки = "";
					ИначеЕсли  ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаКоличествоТовара Тогда
						//_РазмерСкидки  = ?(ВыборкаСтрокАкций.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная,"P","%")+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						_УсловиеСкидки = "Q("+Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ=")+",)";
						//_УсловиеСкидки = "N(1000,{W,"+Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ=")+":"+СокрЛП(ВыборкаСтрокАкций.ТоварАртикул)+"})";
					ИначеЕсли  ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаНабор Тогда
						//_РазмерСкидки  = ?(ВыборкаСтрокАкций.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная,"P","%")+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						//_УсловиеСкидки = "N("+?(ВыборкаСтрокАкций.Правило = Перечисления.ПравилаРасчетаСкидкиПоКоличеству.Упаковка,Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ="),"1000")+",{W,"+Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ=")+":"+СокрЛП(ВыборкаСтрокАкций.ТоварАртикул)+"})";
						_УсловиеСкидки = "N("+?(ЗначениеЗаполнено(ВыборкаСтрокАкций.КоэффициентНабора),Формат(ВыборкаСтрокАкций.КоэффициентНабора*1000,"ЧЦ=15; ЧГ="),"1000")+",{W,"+Формат(ВыборкаСтрокАкций.Количество*1000,"ЧЦ=15; ЧГ=")+":"+СокрЛП(ВыборкаСтрокАкций.ТоварАртикул)+"})";
					ИначеЕсли  ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаСуммуЧека Тогда
						//_РазмерСкидки  = ?(ВыборкаСтрокАкций.СпособВычисления = Перечисления.СкидкиСпособВычисления.Абсолютная,"P","%")+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						_УсловиеСкидки = "T("+Формат(ВыборкаСтрокАкций.СуммаЧека*100,"ЧЦ=10; ЧГ=")+",)";
					ИначеЕсли ТипСкидки = Справочники.ТК_ТипыСкидок.СпециальнаяЦена Тогда
						
						//Если ВыборкаСтрокАкций.СпособВычисления = ПредопределенноеЗначение("Перечисление.СкидкиСпособВычисления.ФиксированнаяЦена") Тогда
						//	_РазмерСкидки = "P"+Формат(ВыборкаСтрокАкций.ЗначениеСкидки*100,"ЧЦ=10; ЧГ=");
						//Иначе
						//	_РазмерСкидки = "";
						//КонецЕсли;
						
						_УсловиеСкидки = "";
					Иначе
						Продолжить;
					КонецЕсли;
					
					СтрокаНачалоДействия= Формат(ВыборкаСтрокАкций.НачалоДействия,"ДФ=yyyyMMddЧЧммсс");
					СтрокаКонецДействия	= Формат(ВыборкаСтрокАкций.КонецДействия,"ДФ=yyyyMMddЧЧммсс");
					_ДниДействия		= "P("+Формат(ВыборкаСтрокАкций.НачалоДействия,"ДФ=yyyyMMdd")+","+Формат(ВыборкаСтрокАкций.КонецДействия,"ДФ=yyyyMMdd")+")";
					
					РазмерСкидки	= РазмерСкидки + ?(ПустаяСтрока(РазмерСкидки),"",";") + _РазмерСкидки;
					УсловиеСкидки	= УсловиеСкидки + ?(ПустаяСтрока(УсловиеСкидки),"",";") + _УсловиеСкидки;
					ДниДействия		= ДниДействия + ?(ПустаяСтрока(ДниДействия),"",";") + _ДниДействия;
					
				КонецЦикла;
				
				СтрокаЗапрос = "EXECUTE  [dbo].[UPSERT_OFFER] "
				+ DOCGUID
				+","+Формат(Сч,"ЧЦ=10; ЧГ=")
				+","+НоменклатураGUID
				+","+ОбщееНачалоДействия
				+","+ОбщееКонецДействия
				+","+АкцияНаименование
				+","+"'"+РазмерСкидки+"'"
				+","+"'"+УсловиеСкидки+"'"
				+","+"null"
				+","+"'"+ДниДействия+"'"
				+","+"'"+Лев(Порядок,37)+"'"
				+","+КлассификаторПродажи+";";
				//+","+НомерПорядка+";";
				ТекстЗапроса.ДобавитьСтроку(СтрокаЗапрос);
				
			КонецЦикла;
			
			Для Каждого строкаТП Из ВыборкаДок.Ссылка.ТорговыеПлощадки Цикл 
				Если НЕ ЗначениеЗаполнено(строкаТП.ТорговаяПлощадка.ПодразделениеКомпании) Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстЗапроса.ДобавитьСтроку("EXECUTE  [dbo].[BIND_OFFER2SAREA] "+DOCGUID+","+ПолучитьВнутрСсылку(строкаТП.ТорговаяПлощадка)+";");
			КонецЦикла;
			
			
			
		КонецЕсли;
		
		ТекстЗапроса.ДобавитьСтроку("EXECUTE [dbo].[FINISH_OFFER] "+DOCGUID+";");
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТекстЗапросаИнвентаризацияИтоги(ВыборкаДетальныеЗаписи,ТаблицаИтогов,ТекстЗапроса,РезультатОбмена)
	
	ОтклХарактерисика = Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ВыборкаДетальныеЗаписи.СкладКомпании, ВыборкаДетальныеЗаписи.Дата);
	
	DOCGUID     = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
	ТекстЗапроса.ДобавитьСтроку("EXECUTE  [dbo].[UPSERT_INVENTORYRESULT] 0,"+DOCGUID+";");
	WAREHOUSEGUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.СкладКомпании);
	ENTITYDATE    = Формат(ВыборкаДетальныеЗаписи.Дата,"ДФ=yyyyMMddЧЧммсс");
	DOCNUM        = "'"+СокрЛП(ВыборкаДетальныеЗаписи.Номер)+"'";
	
	нпп = 0;
	Для Каждого СтрИтог Из ТаблицаИтогов Цикл 
		Если (СтрИтог.ОстатокПоУчету - СтрИтог.Фактически) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		нпп = нпп + 1;
		ITEMNUM = Формат(нпп,"ЧЦ=10; ЧГ=");  
		ARTGUID = ПолучитьВнутрСсылку(СтрИтог.НоменклатураСсылка); 
		Если ОтклХарактерисика Тогда
			PACKGUID = ПолучитьВнутрСсылку(СтрИтог.ЕдиницаИзмерения);
		Иначе
			PACKGUID = ПолучитьВнутрСсылку(СтрИтог.ЕдиницаИзмерения) +?(ЗначениеЗаполнено(СтрИтог.ХарактеристикаНоменклатуры),Прав(ПолучитьВнутрСсылку(СтрИтог.ХарактеристикаНоменклатуры),32),"")
		КонецЕсли;
		EXQTY  = Формат(СтрИтог.ОстатокПоУчету,"ЧЦ=10; ЧН=; ЧГ=");
		QTY    = Формат(СтрИтог.Фактически,"ЧЦ=10; ЧН=; ЧГ=");
		PRICE  = Формат(СтрИтог.Цена*100,"ЧЦ=15; ЧРД=.; ЧН=; ЧГ=");
		
		СтрокаЗапрос ="EXECUTE  [dbo].[UPSERT_INVENTORYRESULT]  1"
		+","+DOCGUID
		+","+WAREHOUSEGUID
		+","+ENTITYDATE
		+","+DOCNUM
		+","+ITEMNUM
		+","+ARTGUID
		+","+PACKGUID
		+","+EXQTY
		+","+QTY
		+","+PRICE
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(СтрокаЗапрос);
	КонецЦикла;
	ТекстЗапроса.ДобавитьСтроку("EXECUTE  [dbo].[UPSERT_INVENTORYRESULT] 2,"+DOCGUID+";");
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаПоПрофилюКассира(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	///Профиль
	//  "EXECUTE [dbo].[UPSERT_CASHIERGRP] 
	// @GUID = 0xAD6E0050568C002911E3997458738EC2
	//,@CASHIERGRPNAME = 'Кассир Киев'
	//,@CASHIERGRPDISABLED = 0
	//,@DELFLAG = 0"
	
	
	///Право профиля
	//"EXECUTE [dbo].[UPSERT_CASHIERPVG] 
	// @GUID = 0xAD6E0050568C002911E3997458738EC2
	//,@CASHIERPVGKEY = 200
	//,@CASHIERPVGVALUE = 0
	//,@DELFLAG = 0"
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		GUID     = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		CASHIERGRPNAME = "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование))+"'";
		DELFLAG =   ВыборкаДетальныеЗаписи.ПометкаУдаления;
		CASHIERGRPDISABLED =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_CASHIERGRP] "+GUID+","+CASHIERGRPNAME+","+CASHIERGRPDISABLED+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
		ВыборкаПравоПрофиля = ВыборкаДетальныеЗаписи.НастройкаПравКассира.Выбрать();
		
		Пока ВыборкаПравоПрофиля.Следующий() Цикл
			
			CASHIERPVGKEY   =  Формат(ВыборкаПравоПрофиля.ИдентификаторПрава,"ЧЦ=15; ЧН=; ЧГ=");
			CASHIERPVGVALUE =  ВыборкаПравоПрофиля.Разрешено;
			
			тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_CASHIERPVG] "+GUID+","+CASHIERPVGKEY+","+CASHIERPVGVALUE+","+DELFLAG+";";
			
			ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
			
		КонецЦикла;
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаВыгрузкиКассира(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	
	
	//  
	//  "EXECUTE [dbo].[UPSERT_CASHIER] 
	// @GUID = 0xAD6E0050568C002911E3997458738EC2
	//,@CASHIERGRPGUID = 0xAD6E0050568C002911E3997458738EC3
	//,@CASHIERKEY = '6598453111'
	//,@CASHIERPASSWORD = ''
	//,@CASHIERNAME = 'Кассир Кассирович Кассиров'
	//,@CASHIERDISABLED = 0
	//,@CASHIERSAREACONTROL = 0
	//,@DELFLAG = 0"
	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		КассирОтключен = ВыборкаДетальныеЗаписи.КассирОтключен;
		
		Если ПустаяСтрока(ВыборкаДетальныеЗаписи.ШтрихКодНаКассу) Тогда
			КассирОтключен = 1;
		КонецЕсли;
		
		МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВыборкаДетальныеЗаписи.ФИО," ",Истина,Истина); 
		
		Если МассивСтрок.Количество() =3 Тогда
			CASHIERNAME = МассивСтрок[0] +" " +Лев(МассивСтрок[1],1)+"."+Лев(МассивСтрок[2],1)+".";
		Иначе
			CASHIERNAME =ВыборкаДетальныеЗаписи.ФИО; 
		КонецЕсли;
		
		
		GUID     = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		GUIDP    = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.ПрофильКассира);
		CASHIERKEY  =  "'" + СокрЛП(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.ШтрихКодНаКассу))+"'";
		CASHIERPASSWORD =  "''";
		CASHIERNAME     =  "'" + СокрЛП(ЭкранироватьСимволы(CASHIERNAME))+"'";
		CASHIERDISABLED =   КассирОтключен;
		CASHIERSAREACONTROL = ВыборкаДетальныеЗаписи.ДоступПоТТ;
		DELFLAG =   ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXEC   [Integration1C].[dbo].[UPSERT_CASHIER] "+GUID+","+GUIDP+","+CASHIERKEY+","+CASHIERPASSWORD+","+CASHIERNAME+","+CASHIERDISABLED+","+CASHIERSAREACONTROL+","+DELFLAG+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаСтатьиВозврата(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	//
	//@GUID	16-я строка	ГУИД основания операции
	//@REASONTXT	строка(100)	Название основания
	//@DELFLAG	целое	пометка удаления
	//EXECUTE [dbo].[UPSERT_REASONS] @GUID = 0x010203040506070809000A0B0C0D0F, @REASONTXT = 'Брак',@DELFLAG = 0
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		REASONTXT   = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_REASONS] "+GUID
		+","+REASONTXT
		+","+DELFLAG
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаОтделов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	//  
	//  EXECUTE [dbo].[UPSERT_ARTDEPARTMENT] 
	// @GUID    -- ГУИД отдела
	//,@DEPNAME    -- Название отдела
	//,@DELFLAG    -- Пометка удаления
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID           = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Ссылка);
		DEPNAME   = "'"+СокрЛП(Лев(ЭкранироватьСимволы(ВыборкаДетальныеЗаписи.Наименование),100))+"'";
		DELFLAG =  ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_ARTDEPARTMENT] "+GUID
		+","+DEPNAME
		+","+DELFLAG
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры

//Выборка результата запроса
//Текстовый документ 
//Струтура
Процедура ТекстЗапросаНоменклатураОтделов(ВыборкаДетальныеЗаписи,ТекстЗапроса,РезультатОбмена)
	////  
	//EXECUTE [dbo].[UPSERT_ARTDEPCONTENTS] 
	//@WHGUID    -- ГУИД склада
	//,@DEPGUID    -- ГУИД отдела
	//,@GRPGUID    -- ГУИД Группы товаров
	//,@DELFLAG    -- Пометка удаления
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		WHGUID  = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Склад);
		DEPGUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Отдел);
		GRPGUID = ПолучитьВнутрСсылку(ВыборкаДетальныеЗаписи.Номенклатура);
		DELFLAG = ВыборкаДетальныеЗаписи.ПометкаУдаления;
		
		тСтрокаЗапроса = "EXECUTE [dbo].[UPSERT_ARTDEPCONTENTS] "+WHGUID
		+","+DEPGUID
		+","+GRPGUID
		+","+DELFLAG
		+";";
		
		ТекстЗапроса.ДобавитьСтроку(тСтрокаЗапроса);
		
	КонецЦикла;
	
	
КонецПроцедуры





#КонецОбласти




Функция РезультатЗапросаВыборкиИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения = Неопределено, РазмерПорции = Неопределено, МенеджерВременныхТаблиц = Неопределено,НаДату = Неопределено) Экспорт
	
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных) ИЛИ Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		
		Возврат РезультатЗапросаВыбораИзмененийСсылочногоТипа(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, РазмерПорции, МенеджерВременныхТаблиц);
		
	ИначеЕсли (Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) И ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый) Тогда
		
		Возврат РезультатЗапросаВыбораИзмененийНабораЗаписей(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, РазмерПорции, МенеджерВременныхТаблиц,НаДату);
		
		//ИначеЕсли (Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) И ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору)
		//			Или Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных)
		//			Или Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных)
		//			Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда
		
		//	Возврат РезультатЗапросаВыбораИзмененийНабораЗаписейПодчиненногоРегистратору(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, РазмерПорции, МенеджерВременныхТаблиц);
		//	
	КонецЕсли;
	
	
	ВызватьИсключение "Неверный аргумент ОбъектМетаданных метода РезультатЗапросаВыборкиИзменений()";
	
КонецФункции

Функция СформироватьФильтрВыборкиИзРезультатаЗапроса(ОбъектМетаданных, РезультатЗапроса) Экспорт
	
	Если Метаданные.Справочники.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.Документы.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.ПланыВидовРасчета.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.ПланыСчетов.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.ПланыОбмена.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.БизнесПроцессы.Содержит(ОбъектМетаданных)
		ИЛИ Метаданные.Задачи.Содержит(ОбъектМетаданных) Тогда
		
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	ИначеЕсли Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) 
		Или Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных)
		Или Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных)
		Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда		
		
		ФильтрВыборки = Новый Массив;
		МенеджерОбъекта = МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя());
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = МенеджерОбъекта.СоздатьНаборЗаписей();
			УстановитьЗначенияОтбора(НаборЗаписей.Отбор, Выборка);
			ФильтрВыборки.Добавить(НаборЗаписей);
			
		КонецЦикла;
		
		Возврат ФильтрВыборки;
		
	КонецЕсли;
	
	ВызватьИсключение "Неверный аргумент ОбъектМетаданных метода СформироватьФильтрВыборкиИзРезультатаЗапроса()";
	
КонецФункции

// Возвращает менеджер объекта по полному имени объекта метаданных.
// Ограничение: не обрабатываются точки маршрутов бизнес-процессов.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя объекта метаданных. Пример: "Справочник.Организации".
//
// Возвращаемое значение:
//  СправочникМенеджер, ДокументМенеджер.
// 
Функция МенеджерОбъектаПоПолномуИмени(ПолноеИмя) Экспорт
	Перем КлассОМ, ИмяОМ, Менеджер;
	
	ЧастиИмени = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмя, ".");
	
	Если ЧастиИмени.Количество() >= 2 Тогда
		КлассОМ = ЧастиИмени[0];
		ИмяОМ  = ЧастиИмени[1];
	КонецЕсли;
	
	Если      ВРег(КлассОМ) = "ПЛАНОБМЕНА" Тогда
		Менеджер = ПланыОбмена;
		
	ИначеЕсли ВРег(КлассОМ) = "СПРАВОЧНИК" Тогда
		Менеджер = Справочники;
		
	ИначеЕсли ВРег(КлассОМ) = "ДОКУМЕНТ" Тогда
		Менеджер = Документы;
		
	ИначеЕсли ВРег(КлассОМ) = "ЖУРНАЛДОКУМЕНТОВ" Тогда
		Менеджер = ЖурналыДокументов;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЕРЕЧИСЛЕНИЕ" Тогда
		Менеджер = Перечисления;
		
	ИначеЕсли ВРег(КлассОМ) = "ОТЧЕТ" Тогда
		Менеджер = Отчеты;
		
	ИначеЕсли ВРег(КлассОМ) = "ОБРАБОТКА" Тогда
		Менеджер = Обработки;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВХАРАКТЕРИСТИК" Тогда
		Менеджер = ПланыВидовХарактеристик;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНСЧЕТОВ" Тогда
		Менеджер = ПланыСчетов;
		
	ИначеЕсли ВРег(КлассОМ) = "ПЛАНВИДОВРАСЧЕТА" Тогда
		Менеджер = ПланыВидовРасчета;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРСВЕДЕНИЙ" Тогда
		Менеджер = РегистрыСведений;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРНАКОПЛЕНИЯ" Тогда
		Менеджер = РегистрыНакопления;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРБУХГАЛТЕРИИ" Тогда
		Менеджер = РегистрыБухгалтерии;
		
	ИначеЕсли ВРег(КлассОМ) = "РЕГИСТРРАСЧЕТА" Тогда
		Если ЧастиИмени.Количество() = 2 Тогда
			// Регистр расчета
			Менеджер = РегистрыРасчета;
		Иначе
			КлассПодчиненногоОМ = ЧастиИмени[2];
			ИмяПодчиненногоОМ = ЧастиИмени[3];
			Если ВРег(КлассПодчиненногоОМ) = "ПЕРЕРАСЧЕТ" Тогда
				// Перерасчет
				Попытка
					Менеджер = РегистрыРасчета[ИмяОМ].Перерасчеты;
					ИмяОм = ИмяПодчиненногоОМ;
				Исключение
					Менеджер = Неопределено;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВРег(КлассОМ) = "БИЗНЕСПРОЦЕСС" Тогда
		Менеджер = БизнесПроцессы;
		
	ИначеЕсли ВРег(КлассОМ) = "ЗАДАЧА" Тогда
		Менеджер = Задачи;
		
	ИначеЕсли ВРег(КлассОМ) = "КОНСТАНТА" Тогда
		Менеджер = Константы;
		
	ИначеЕсли ВРег(КлассОМ) = "ПОСЛЕДОВАТЕЛЬНОСТЬ" Тогда
		Менеджер = Последовательности;
	КонецЕсли;
	
	Если Менеджер <> Неопределено Тогда
		Попытка
			Возврат Менеджер[ИмяОМ];
		Исключение
			Менеджер = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Неизвестный тип объекта метаданных ""%1""'"), ПолноеИмя);
	
КонецФункции

Процедура УстановитьЗначенияОтбора(Отбор, Источник) Экспорт
	
	Для каждого ЭлементОтбора Из Отбор Цикл
		ЭлементОтбора.Установить(Источник[ЭлементОтбора.Имя]);
	КонецЦикла;
	
КонецПроцедуры

Функция РезультатЗапросаВыбораИзмененийСсылочногоТипа(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения = Неопределено, РазмерПорции = Неопределено, МенеджерВременныхТаблиц = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелПланаОбмена", УзелПланаОбмена);
	
	Если НЕ НомерСообщения = Неопределено Тогда
		Запрос.УстановитьПараметр("НомерСообщения", НомерСообщения);
	КонецЕсли;
	
	Если НЕ МенеджерВременныхТаблиц = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ" + ?(ТипЗнч(РазмерПорции) = Тип("Число") И РазмерПорции > 0, " ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=0"), "") + "
	|	ТаблицаИзменений.Ссылка
	|" + ?(МенеджерВременныхТаблиц = Неопределено, "", "ПОМЕСТИТЬ ТаблицаИзменений") + "
	|ИЗ
	|	" + ОбъектМетаданных.ПолноеИмя() + ".Изменения КАК ТаблицаИзменений
	|ГДЕ
	|	ТаблицаИзменений.Узел = &УзелПланаОбмена
	|	И ТаблицаИзменений.НомерСообщения " + ?(НомерСообщения = Неопределено, "ЕСТЬ NULL", "= &НомерСообщения") + "
	|" + ?(МенеджерВременныхТаблиц = Неопределено, "", "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Ссылка
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений");
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаВыбораИзмененийНабораЗаписей(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения = Неопределено, РазмерПорции = Неопределено, МенеджерВременныхТаблиц = Неопределено,ДатаОтбора = Неопределено)
	
	Если НЕ ОбщегоНазначения.ЭтоРегистрСведений(ОбъектМетаданных) Тогда
		ВызватьИсключение ТекстНеверныйАргументМетода("ОбъектМетаданных", "РезультатЗапросаВыбораИзмененийНабораЗаписей");
	КонецЕсли;
	
	ПериодВыборки = Неопределено;
	Если ОбъектМетаданных.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		ПоляЗапроса = "Период, ";
		Если ДатаОтбора = Неопределено Тогда
			ПериодВыборки = ТекущаяДатаСеанса();
		Иначе
			ПериодВыборки = ТекущаяДатаСеанса();
		КонецЕсли;
	Иначе
		ПоляЗапроса = "";
	КонецЕсли;
	
	Для каждого Измерение Из ОбъектМетаданных.Измерения Цикл
		ПоляЗапроса = ПоляЗапроса + Измерение.Имя + ",";
	КонецЦикла;
	ПоляЗапроса = Лев(ПоляЗапроса, СтрДлина(ПоляЗапроса) - 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелПланаОбмена", УзелПланаОбмена);
	
	Если НЕ НомерСообщения = Неопределено Тогда
		Запрос.УстановитьПараметр("НомерСообщения", НомерСообщения);
	КонецЕсли;
	Если НЕ ПериодВыборки = Неопределено Тогда
		Запрос.УстановитьПараметр("ПериодВыборки", ПериодВыборки);
	КонецЕсли;
	
	
	Если НЕ МенеджерВременныхТаблиц = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ" + ?(ТипЗнч(РазмерПорции) = Тип("Число") И РазмерПорции > 0, " ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=0"), "") + "
	|	" + ПоляЗапроса + "
	|" + ?(МенеджерВременныхТаблиц = Неопределено, "", "ПОМЕСТИТЬ ТаблицаИзменений") + "
	|ИЗ
	|	" + ОбъектМетаданных.ПолноеИмя() + ".Изменения КАК ТаблицаИзменений
	|ГДЕ
	|	ТаблицаИзменений.Узел = &УзелПланаОбмена
	|	И ТаблицаИзменений.НомерСообщения " + ?(НомерСообщения = Неопределено, "ЕСТЬ NULL", "= &НомерСообщения") + "
	|" + ?(ПериодВыборки = Неопределено, ""," И ТаблицаИзменений.Период <= &ПериодВыборки")+"
	|" + ?(МенеджерВременныхТаблиц = Неопределено, "", "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	" + ПоляЗапроса + "
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений");
	
	Возврат Запрос.Выполнить();
	
КонецФункции



Функция МассивКлючейАктивныхФоновыхЗаданий(ИмяМетодаФоновогоЗадания) Экспорт
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	СтруктураОтбора.Вставить("ИмяМетода", ИмяМетодаФоновогоЗадания);
	
	МассивКлючей = Новый Массив;
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(СтруктураОтбора);
	Для каждого ФоновоеЗадание Из МассивФоновыхЗаданий Цикл
		МассивКлючей.Добавить(ФоновоеЗадание.Ключ);
	КонецЦикла; 
	
	Возврат МассивКлючей;
	
КонецФункции



// Регистрация изменений
Процедура ЗпуститьФоновоеЗаданиеРегистрацииСправочникаЕденицИзмерений(МассивУзлов,СправочникСсылка) Экспорт
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(МассивУзлов);
	ПараметрыЗадания.Добавить(СправочникСсылка);
	
	ФоновыеЗадания.Выполнить("ОбменФронт.ЗарегистрироватьИзмененияЕдиницыИзмерения",ПараметрыЗадания,,"Регестрация изменения ед. изм.");
	
КонецПроцедуры

Процедура ЗапуститьФоновоеЗаданиеРегистацииВыгрузкиОстатков(ТипДокумента,ДатаИзменения,СкладРегистрации,талицаТоваров,МассивУзлов,ТаблицаРегистрации)Экспорт
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(ДатаИзменения);
	ПараметрыЗадания.Добавить(СкладРегистрации);
	ПараметрыЗадания.Добавить(талицаТоваров);
	ПараметрыЗадания.Добавить(МассивУзлов);
	ПараметрыЗадания.Добавить(ТаблицаРегистрации);
	
	Если ТипДокумента = Тип("ДокументОбъект.ПересортицаТоваров") Тогда
		ИмяЗадания ="ОбменФронт.ЗарегистрироватьОстаткиТаблицаТоваровПересортицы"; 
	Иначе
		ИмяЗадания ="ОбменФронт.ЗарегистрироватьОстаткиТаблицаТоваров"; 
	КонецЕсли;
	ФоновыеЗадания.Выполнить(ИмяЗадания,ПараметрыЗадания,,"Регестрация товаров выгрузки остатков");
	
КонецПроцедуры



Процедура ЗапуститьФоновыеЗаданияРегистрацииИзмененийВсехСообщений(УзелПланаОбмена, ОбъектМетаданных, КодУзлаПланаОбмена = "") Экспорт
	
	МассивКлючейАктивныхФоновыхЗаданий = МассивКлючейАктивныхФоновыхЗаданий("ОбменФронт.ОбработатьРегистрациюИзменений");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелПланаОбмена", УзелПланаОбмена);
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ НомерСообщения ИЗ " + ОбъектМетаданных.ПолноеИмя() + ".Изменения ГДЕ Узел = &УзелПланаОбмена И НЕ НомерСообщения ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		КлючЗадания = КлючЗадания(УзелПланаОбмена, ОбъектМетаданных, Выборка.НомерСообщения, КодУзлаПланаОбмена);
		
		Если МассивКлючейАктивныхФоновыхЗаданий.Найти(КлючЗадания) = Неопределено Тогда
			ЗапуститьФоновоеЗаданиеРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, Выборка.НомерСообщения, КодУзлаПланаОбмена);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗапуститьФоновоеЗаданиеУдаленияРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена = "") Экспорт
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(УзелПланаОбмена);
	ПараметрыЗадания.Добавить(ОбъектМетаданных.ПолноеИмя());
	ПараметрыЗадания.Добавить(НомерСообщения);
	
	ФоновыеЗадания.Выполнить("ОбменФронт.ОбработатьРегистрациюИзменений",
	ПараметрыЗадания,
	КлючЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена),
	НаименованиеЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена));
	
КонецПроцедуры

Процедура ЗапуститьФоновоеЗаданиеРегистрацииИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена = "") Экспорт
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(УзелПланаОбмена);
	ПараметрыЗадания.Добавить(ОбъектМетаданных.ПолноеИмя());
	ПараметрыЗадания.Добавить(НомерСообщения);
	ПараметрыЗадания.Добавить(Истина);
	
	
	ФоновыеЗадания.Выполнить(	"ОбменФронт.ОбработатьРегистрациюИзменений",
	ПараметрыЗадания,
	КлючЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена),
	НаименованиеЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, КодУзлаПланаОбмена));
	
КонецПроцедуры

Процедура ОбработатьРегистрациюИзменений(УзелПланаОбмена, Знач ОбъектМетаданных, НомерСообщения,Регистрировать = Ложь) Экспорт
	
	Если ТипЗнч(ОбъектМетаданных) = Тип("Строка") Тогда
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОбъектМетаданных);
	КонецЕсли;
	
	Если ТипЗнч(ОбъектМетаданных) <> Тип("ОбъектМетаданных") Тогда
		ВызватьИсключение "Неверный аргумент ОбъектМетаданных метода ОбработатьРегистрациюИзменений";
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если Регистрировать Тогда
		ЗарегистрироватьИзмененияПоНомеруСообщения(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения);
	Иначе
		УдалитьРегистрациюИзмененийПоНомеруСообщения(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияПоНомеруСообщения(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения)
	
	
	РезультатЗапроса = РезультатЗапросаВыборкиИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения);
	
	ФильтрВыборки = СформироватьФильтрВыборкиИзРезультатаЗапроса(ОбъектМетаданных, РезультатЗапроса);
	Для каждого Данные Из ФильтрВыборки Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Данные);
	КонецЦикла;
	
	
КонецПроцедуры

Процедура УдалитьРегистрациюИзмененийПоНомеруСообщения(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения)
	
	
	ПрочитатьДанные = Истина;
	
	КоличествоПопыток = 3;
	Для НомерПопытки = 1 По КоличествоПопыток Цикл
		
		Если ПрочитатьДанные Тогда
			
			// Первое чтение для получения данных для блокировки
			РезультатЗапроса = РезультатЗапросаВыборкиИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения);
			
			Если РезультатЗапроса.Пустой() Тогда
				Возврат;
			КонецЕсли;
			
			КоличествоЗаписейДоБлокировки = РезультатЗапроса.Выбрать().Количество();
			
			Блокировка = Новый БлокировкаДанных;
			
			Если (Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) И ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору)
				Или Метаданные.РегистрыБухгалтерии.Содержит(ОбъектМетаданных)
				Или Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных)
				Или Метаданные.РегистрыРасчета.Содержит(ОбъектМетаданных) Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					ЭлементБлокировки = Блокировка.Добавить(ОбъектМетаданных.ПолноеИмя()+".НаборЗаписей");
					ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				КонецЦикла;
				
			Иначе
				
				ЭлементБлокировки = Блокировка.Добавить(ОбъектМетаданных.ПолноеИмя());
				ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
				Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Колонка.Имя, Колонка.Имя);
				КонецЦикла;
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
			КонецЕсли;
			
			ПрочитатьДанные = Ложь;
			
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка.Заблокировать();
			
			// Получение данных для удаления регистрации
			РезультатЗапроса = РезультатЗапросаВыборкиИзменений(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения);
			
			КоличествоЗаписейПослеБлокировки = РезультатЗапроса.Выбрать().Количество();
			Если КоличествоЗаписейДоБлокировки <> КоличествоЗаписейПослеБлокировки Тогда
				ОтменитьТранзакцию();
				ПрочитатьДанные = Истина;
				Продолжить;
			КонецЕсли;
			
			ФильтрВыборки = СформироватьФильтрВыборкиИзРезультатаЗапроса(ОбъектМетаданных, РезультатЗапроса);
			Для каждого Данные Из ФильтрВыборки Цикл
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелПланаОбмена, Данные);
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			Прервать;
			
		Исключение
			
			Если НомерПопытки = КоличествоПопыток Тогда
				ВызватьИсключение;
			Иначе
				ОтменитьТранзакцию();
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияЕдиницыИзмерения(МассивУзлов,СправочникСсылка)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Ссылка",СправочникСсылка);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	спрТипыЦен.Ссылка КАК ТипЦен,
	|	спрЕдиницыИзмерения.Владелец КАК Номенклатура,
	|	ЕСТЬNULL(спрХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.Характеристикиноменклатуры.ПустаяСсылка)) КАК Характеристика,
	|	&Период КАК Период
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК спрЕдиницыИзмерения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК спрХарактеристикиНоменклатуры
	|		ПО спрЕдиницыИзмерения.Владелец = спрХарактеристикиНоменклатуры.Владелец
	|			И (НЕ спрХарактеристикиНоменклатуры.НеАктивная),
	|	Справочник.ТипыЦен КАК спрТипыЦен
	|ГДЕ
	|	спрЕдиницыИзмерения.Ссылка = &Ссылка
	|	И спрТипыЦен.ЭтоГруппа = ЛОЖЬ
	|	И спрТипыЦен.ПометкаУдаления = ЛОЖЬ
	|	И спрТипыЦен.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СкладыКомпанииДоступныеЦены.ТипЦен КАК ТипЦен
	|			ИЗ
	|				Справочник.СкладыКомпании.ДоступныеЦены КАК СкладыКомпанииДоступныеЦены
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СкладыКомпании.ТипЦенРозничнойТорговли
	|			ИЗ
	|				Справочник.СкладыКомпании КАК СкладыКомпании)";
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.ЦеныДляРегистрации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(ВыборкаДетальныеЗаписи.Период);
		НаборЗаписей.Отбор.ТипЦен.Установить(ВыборкаДетальныеЗаписи.ТипЦен);
		НаборЗаписей.Отбор.Номенклатура.Установить(ВыборкаДетальныеЗаписи.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(ВыборкаДетальныеЗаписи.Характеристика);
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора,ВыборкаДетальныеЗаписи,"ТипЦен,Номенклатура,Характеристика,Период");
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
		
	КонецЦикла;
	
	
	Запрос.Текст ="ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК СсылкаХарактеристика
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ЕдиницыИзмерения.Владелец = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	ЕдиницыИзмерения.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, ВыборкаДетальныеЗаписи.СсылкаХарактеристика);
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗарегистрироватьОстаткиТаблицаТоваров(ДатаИзменения,СкладРегистрации,талицаТоваров,МассивУзлов,ТаблицаРегистрации)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//Для Каждого СтрТовары Из талицаТоваров Цикл 
	//	
	//	НаборЗаписей = РегистрыСведений.ТК_НоменклатураДляИзмененияOS.СоздатьНаборЗаписей();
	//	НаборЗаписей.Отбор.Период.Установить(ДатаИзменения);
	//	НаборЗаписей.Отбор.СкладКомпании.Установить(СкладРегистрации);
	//	НаборЗаписей.Отбор.Номенклатура.Установить(СтрТовары.Номенклатура);
	//	НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Установить(СтрТовары.ХарактеристикаНоменклатуры);
	//	
	//	ЗаписьНабора = НаборЗаписей.Добавить();
	//	ЗаписьНабора.Период = ДатаИзменения;
	//	ЗаписьНабора.СкладКомпании = СкладРегистрации; 
	//	ЗаписьНабора.Номенклатура = СтрТовары.Номенклатура; 
	//	ЗаписьНабора.ХарактеристикаНоменклатуры = СтрТовары.ХарактеристикаНоменклатуры; 
	//	
	//	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
	//	
	//	
	//КонецЦикла;
	//
	Если ТаблицаРегистрации ="ТК_НоменклатураДляИзмененияOS" Тогда
		НаборЗаписей = РегистрыСведений.ТК_НоменклатураДляИзмененияOS.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Значение = ДатаИзменения;
		НаборЗаписей.Отбор.Период.Использование = Истина;
	Иначе
		НаборЗаписей = РегистрыСведений.ТК_OS_РегистрацияНаВыгрузкуОстатков.СоздатьНаборЗаписей();
	КонецЕсли;
	
	НаборЗаписей.Отбор.СкладКомпании.Значение = СкладРегистрации;
	НаборЗаписей.Отбор.СкладКомпании.Использование = Истина;
	НаборЗаписей.Отбор.Номенклатура.Использование = Истина;
	НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Использование = Истина;
	
	Для Каждого СтрТовары Из талицаТоваров Цикл 
		
		НаборЗаписей.Отбор.Номенклатура.Значение = СтрТовары.Номенклатура;
		НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Значение = СтрТовары.ХарактеристикаНоменклатуры;
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗарегистрироватьОстаткиТаблицаТоваровПересортицы(ДатаИзменения,СкладРегистрации,талицаТоваров,МассивУзлов,ТаблицаРегистрации)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//Для Каждого СтрТовары Из талицаТоваров Цикл 
	//	
	//	НаборЗаписей = РегистрыСведений.ТК_НоменклатураДляИзмененияOS.СоздатьНаборЗаписей();
	//	НаборЗаписей.Отбор.Период.Установить(ДатаИзменения);
	//	НаборЗаписей.Отбор.СкладКомпании.Установить(СкладРегистрации);
	//	НаборЗаписей.Отбор.Номенклатура.Установить(СтрТовары.НоменклатураРасход);
	//	НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Установить(СтрТовары.ХарактеристикаНоменклатурыРасход);
	//	
	//	ЗаписьНабора = НаборЗаписей.Добавить();
	//	ЗаписьНабора.Период = ДатаИзменения;
	//	ЗаписьНабора.СкладКомпании = СкладРегистрации; 
	//	ЗаписьНабора.Номенклатура = СтрТовары.НоменклатураРасход; 
	//	ЗаписьНабора.ХарактеристикаНоменклатуры = СтрТовары.ХарактеристикаНоменклатурыРасход; 
	//	
	//	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
	//	
	//	НаборЗаписей = РегистрыСведений.ТК_НоменклатураДляИзмененияOS.СоздатьНаборЗаписей();
	//	НаборЗаписей.Отбор.Период.Установить(ДатаИзменения);
	//	НаборЗаписей.Отбор.СкладКомпании.Установить(СкладРегистрации);
	//	НаборЗаписей.Отбор.Номенклатура.Установить(СтрТовары.НоменклатураПриход);
	//	НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Установить(СтрТовары.ХарактеристикаНоменклатурыПриход);
	//	
	//	ЗаписьНабора = НаборЗаписей.Добавить();
	//	ЗаписьНабора.Период = ДатаИзменения;
	//	ЗаписьНабора.СкладКомпании = СкладРегистрации; 
	//	ЗаписьНабора.Номенклатура = СтрТовары.НоменклатураПриход; 
	//	ЗаписьНабора.ХарактеристикаНоменклатуры = СтрТовары.ХарактеристикаНоменклатурыПриход; 
	//	
	//	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
	//	
	//КонецЦикла;
	Если ТаблицаРегистрации ="ТК_НоменклатураДляИзмененияOS" Тогда
		НаборЗаписей = РегистрыСведений.ТК_НоменклатураДляИзмененияOS.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Значение = ДатаИзменения;
		НаборЗаписей.Отбор.Период.Использование = Истина;
	Иначе
		НаборЗаписей = РегистрыСведений.ТК_OS_РегистрацияНаВыгрузкуОстатков.СоздатьНаборЗаписей();
		
	КонецЕсли;
	
	НаборЗаписей.Отбор.СкладКомпании.Значение = СкладРегистрации;
	НаборЗаписей.Отбор.СкладКомпании.Использование = Истина;
	НаборЗаписей.Отбор.Номенклатура.Использование = Истина;
	НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Использование = Истина;
	
	Для Каждого СтрТовары Из талицаТоваров Цикл 
		
		НаборЗаписей.Отбор.Номенклатура.Значение = СтрТовары.НоменклатураРасход;
		НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Значение = СтрТовары.ХарактеристикаНоменклатурыРасход;
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
		
		НаборЗаписей.Отбор.Номенклатура.Значение = СтрТовары.НоменклатураПриход;
		НаборЗаписей.Отбор.ХарактеристикаНоменклатуры.Значение = СтрТовары.ХарактеристикаНоменклатурыПриход;
		ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
		
	КонецЦикла;
	
	
	
КонецПроцедуры

Процедура ЗарегистрироватОстаткиСклада(СкладРегистрации,МассивУзлов)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ТК_ДатыОстатковOS.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СкладКомпании.Значение = СкладРегистрации;
	НаборЗаписей.Отбор.СкладКомпании.Использование = Истина;
	
	
	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, НаборЗаписей);
	
КонецПроцедуры




Функция КлючЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, Знач КодУзлаПланаОбмена = "")
	
	Если НЕ ЗначениеЗаполнено(КодУзлаПланаОбмена) Тогда
		КодУзлаПланаОбмена = УзелПланаОбмена.Код;
	КонецЕсли;
	
	ПолныйКодУзлаОбмена = УзелПланаОбмена.Метаданные().Имя + "." + КодУзлаПланаОбмена;
	
	СтруктураКлюча = Новый Структура("ПолныйКодУзлаОбмена, НомерСообщения, ИмяТаблицыОбъектаМетаданных",
	ПолныйКодУзлаОбмена, НомерСообщения, ОбъектМетаданных.ПолноеИмя());
	
	ЗаписьJSON = новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJSON, СтруктураКлюча);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

Функция НаименованиеЗадания(УзелПланаОбмена, ОбъектМетаданных, НомерСообщения, Знач КодУзлаПланаОбмена = "")
	
	Если НЕ ЗначениеЗаполнено(КодУзлаПланаОбмена) Тогда
		КодУзлаПланаОбмена = УзелПланаОбмена.Код;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Обработка регистрации: %1, %2, %3 [%4]", ОбъектМетаданных.Имя, НомерСообщения, КодУзлаПланаОбмена, УзелПланаОбмена.Метаданные().Синоним);
	
КонецФункции


Функция УстановитьНомерСообщения(УзелПланаОбмена,ФильтрВыборки)
	
	// Получим, установим номер сообщения текущей выгрузки
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("ПланОбмена." + УзелПланаОбмена.Метаданные().Имя);
	ЭлементБлокировки.УстановитьЗначение("Ссылка", УзелПланаОбмена);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	
	КоличествоПопыток = 3;
	Для НомерПопытки = 1 По КоличествоПопыток Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка.Заблокировать();
			
			НомерСообщения = ПолучитьНомерСообщения(УзелПланаОбмена);
			ПланыОбмена.ВыбратьИзменения(УзелПланаОбмена, НомерСообщения, ФильтрВыборки);
			
			ЗафиксироватьТранзакцию();
			Возврат НомерСообщения;
			
		Исключение
			
			Если НомерПопытки = КоличествоПопыток Тогда
				ВызватьИсключение;
			Иначе
				ОтменитьТранзакцию();
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецФункции

Функция ПолучитьНомерСообщения(УзелПланаОбмена)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелПланаОбмена", УзелПланаОбмена);
	Запрос.Текст = ТекстЗапросаПолученияНомераСообщения(УзелПланаОбмена.Метаданные().Имя);
	
	НомерСообщения = Запрос.Выполнить().Выгрузить()[0][0];
	Если НомерСообщения = null Тогда
		Возврат 1;
	Иначе
		Возврат НомерСообщения + 1;
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаПолученияНомераСообщения(ИмяПланаОбмена) Экспорт
	
	ШаблонСекции = 
	"	ВЫБРАТЬ
	|		МАКСИМУМ(ТаблицаИзменений.НомерСообщения) КАК НомерСообщения
	|	ИЗ
	|		&ПолноеИмяТаблицы.Изменения КАК ТаблицаИзменений
	|	ГДЕ
	|		ТаблицаИзменений.Узел = &УзелПланаОбмена
	|		И НЕ ТаблицаИзменений.НомерСообщения ЕСТЬ NULL
	|";
	
	ТекстРазделителя = "
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаНомеровСообщений.НомерСообщения) КАК НомерСообщения
	|ИЗ
	|	(
	|&ТекстВложенногоЗапроса
	|	) КАК ТаблицаНомеровСообщений";
	
	ТекстВложенногоЗапроса = "";
	
	ПервыйЭлементСостава = Истина;
	Для каждого ЭлементСостава Из Метаданные.ПланыОбмена[ИмяПланаОбмена].Состав Цикл
		
		Если НЕ ПервыйЭлементСостава Тогда
			ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + ТекстРазделителя;
		Иначе
			ПервыйЭлементСостава = Ложь;
		КонецЕсли;
		
		ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + СтрЗаменить(ШаблонСекции, "&ПолноеИмяТаблицы", ЭлементСостава.Метаданные.ПолноеИмя());
		
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ТекстВложенногоЗапроса", ТекстВложенногоЗапроса);
	
	Возврат ТекстЗапроса
	
КонецФункции
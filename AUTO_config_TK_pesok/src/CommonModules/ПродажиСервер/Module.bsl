////////////////////////////////////////////////////////////////////////////////
// Модуль "ПродажиСервер", содержит процедуры и функции для
// проверки корректности документов продажи и для обработки введенных данных
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс



#КонецОбласти

#Область ПроцедурыПроверкиКорректностиЗаполненияДокументов

// Проверяет корректность заполнения товаров документа возврата в соответствии 
// с реализованными товарами с учетом корректировок.
// Вызывается из процедуры документа "ОбработкаПроверкиЗаполнения"
//
// Параметры:
// ДокументПродажи  - ДокументОбъект, для которого необходимо осуществить проверки
// ИмяТаблицы - Имя таблицы Товары
// Отказ            - Булево - Флаг отказа от проведения документа
//
Процедура ПроверитьКорректностьВозвращаемыхТоваров(Знач ДокументПродажи, ИмяТаблицы, Отказ, ТаблицаПроверяемыеТовары = Неопределено) Экспорт
	
	ТипДокументаВозврата = ТипЗнч(ДокументПродажи);
	
	Если ТаблицаПроверяемыеТовары = Неопределено Тогда
		ТаблицаПроверяемыеТовары = ДокументПродажи[ИмяТаблицы].Выгрузить();		
		Если ТипДокументаВозврата = Тип("ДокументОбъект.ВозвратОтПокупателя") Тогда 
			ТаблицаПроверяемыеТовары.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ДокументПродажи", "Количество");
		Иначе
			ТаблицаПроверяемыеТовары.Свернуть("Номенклатура, ХарактеристикаНоменклатуры", "Количество");
		КонецЕсли;
	КонецЕсли;
		
	Если ТаблицаПроверяемыеТовары.Количество() > 0 Тогда		
		Запрос = Новый Запрос;
		
		Если ТипДокументаВозврата = Тип("ДокументОбъект.ВозвратОтПокупателя") Тогда 
			ТекстЗапроса = "ВЫБРАТЬ
			               |	ВозвратТоваровОтКлиентаТовары.Номенклатура КАК Номенклатура,
			               |	ВозвратТоваровОтКлиентаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
			               |	ВозвратТоваровОтКлиентаТовары.ДокументПродажи КАК ДокументПродажи,
			               |	ВозвратТоваровОтКлиентаТовары.Количество КАК Количество
			               |ПОМЕСТИТЬ ЗаполняемыеТовары
			               |ИЗ
			               |	&ТаблицаТовары КАК ВозвратТоваровОтКлиентаТовары
			               |
			               |ИНДЕКСИРОВАТЬ ПО
			               |	Номенклатура,
			               |	Характеристика,
			               |	ДокументПродажи
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
			               |	ВложенныйЗапрос.Характеристика КАК Характеристика,
			               |	ВложенныйЗапрос.ДокументПродажи КАК ДокументПродажи,
			               |	ВложенныйЗапрос.НомерРеализации КАК НомерРеализации,
			               |	ВложенныйЗапрос.Количество КАК Количество
			               |ИЗ
			               |	(ВЫБРАТЬ
			               |		ЗаполняемыеТовары.Номенклатура КАК Номенклатура,
			               |		ЗаполняемыеТовары.Характеристика КАК Характеристика,
			               |		ЗаполняемыеТовары.ДокументПродажи КАК ДокументПродажи,
			               |		ЗаполняемыеТовары.Количество - ЕСТЬNULL(РеализацияТоваровТовары.Количество, 0) КАК Количество,
			               |		РеализацияТоваровТовары.Ссылка.Номер КАК НомерРеализации
			               |	ИЗ
			               |		ЗаполняемыеТовары КАК ЗаполняемыеТовары
			               |			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
			               |			ПО ЗаполняемыеТовары.Номенклатура = РеализацияТоваровТовары.Номенклатура
			               |				И ЗаполняемыеТовары.Характеристика = РеализацияТоваровТовары.ХарактеристикаНоменклатуры
			               |				И ЗаполняемыеТовары.ДокументПродажи = РеализацияТоваровТовары.Ссылка
			               |	ГДЕ
			               |		ЗаполняемыеТовары.ДокументПродажи ССЫЛКА Документ.РеализацияТоваров
			               |		И ЗаполняемыеТовары.ДокументПродажи <> ЗНАЧЕНИЕ(Документ.РеализацияТоваров.ПустаяСсылка)) КАК ВложенныйЗапрос
			               |ГДЕ
			               |	ВложенныйЗапрос.Количество > 0"
					
			
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаПроверяемыеТовары);
		Запрос.УстановитьПараметр("ДокументВозврата", ДокументПродажи.Ссылка);
	
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить().Выгрузить();
		
		Если Результат.Количество() > 0 Тогда
			Отказ = Истина;
			СообщитьОбОшибкахКорректностиВозвращаемыхТоваров(Результат);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область ПроцедурыДляВыводаСообщенийОбОшибкахСоответствияДокументовУсловиямПродаж

// Выводит сообщения об ошибках корректности заполнения возвращаемых товаров
//
// Параметры:
// ТаблицаОшибок          - Выгрузка результата запроса
// ВозвратПоЧеку          - Булево
//
Процедура СообщитьОбОшибкахКорректностиВозвращаемыхТоваров(Знач ТаблицаОшибок)
	
	Для каждого СтрокаОшибки Из ТаблицаОшибок Цикл
		СообщениеОбОшибке = НСтр("ru = 'Возврат по номенклатуре %Номенклатура% \ %Характеристика% превышает количество реализованных товаров по документу продажи %НомерРеализации% на %Количество%'; uk = 'Повернення по номенклатурі %Номенклатура% \ %Характеристика%%Серия% перевищує кількість реалізованих товарів за документом продажу %НомерРеализации% на %Количество%'");
		
		СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Номенклатура%",         СтрокаОшибки.Номенклатура);
		СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Характеристика%",       СтрокаОшибки.Характеристика);
		СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Количество%",           СтрокаОшибки.Количество);		
		СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%НомерРеализации%",      СтрокаОшибки.НомерРеализации);
			
		Если НЕ ЗначениеЗаполнено(СтрокаОшибки.Характеристика) Тогда
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "\",      "");
		КонецЕсли; 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИФункцииКонтроляКвот

Процедура ТаблицаОграниченийПоКвотамОтгрузки(Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВидКвоты", ДополнительныеСвойства.ВидКвоты);
	Запрос.УстановитьПараметр("ВидКвотыФМУ", ДополнительныеСвойства.ВидКвотыФМУ);
	Запрос.УстановитьПараметр("Область", ДополнительныеСвойства.Область);
	Запрос.УстановитьПараметр("Группа_Сигареты", Справочники.Номенклатура.НайтиПоКоду("D0001002"));  //Vov41k 23.10.2021
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕСТЬNULL(ВтТаблицаТоваров.Производитель, ЗНАЧЕНИЕ(Справочник.ТК_Производитель.ПустаяСсылка)) КАК Производитель,
	               |	ВтТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	СУММА(ВтТаблицаТоваров.Количество) КАК Количество
	               |ПОМЕСТИТЬ втСвернутыеТовары
	               |ИЗ
	               |	ВтТаблицаТоваров КАК ВтТаблицаТоваров
	               |ГДЕ
	               |	ВтТаблицаТоваров.Номенклатура В ИЕРАРХИИ(&Группа_Сигареты)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаТоваров.Номенклатура,
	               |	ЕСТЬNULL(ВтТаблицаТоваров.Производитель, ЗНАЧЕНИЕ(Справочник.ТК_Производитель.ПустаяСсылка))
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСвернутыеТовары.Производитель КАК Производитель,
	               |	СУММА(втСвернутыеТовары.Количество) КАК Количество
	               |ПОМЕСТИТЬ Вт_ИтогиПоПроизводителям
	               |ИЗ
	               |	втСвернутыеТовары КАК втСвернутыеТовары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втСвернутыеТовары.Производитель
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(втСвернутыеТовары.Количество) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Порог КАК Порог,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Процент КАК Процент,
	               |	1 КАК Порядок
	               |ПОМЕСТИТЬ вт_ЗначенияКвот
	               |ИЗ
	               |	РегистрСведений.ТК_КвотыНаОтгрузку.СрезПоследних(
	               |			&Дата,
	               |			ВидКвоты = &ВидКвоты
	               |				И Область = &Область
	               |				И Номенклатура В
	               |					(ВЫБРАТЬ
	               |						втСвернутыеТовары.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						втСвернутыеТовары КАК втСвернутыеТовары)) КАК ТК_КвотыНаОтгрузкуСрезПоследних
	               |ГДЕ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Состояние = 1
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Номенклатура,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Порог,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Процент,
	               |	1
	               |ИЗ
	               |	РегистрСведений.ТК_КвотыНаОтгрузку.СрезПоследних(
	               |			&Дата,
	               |			ВидКвоты = &ВидКвотыФМУ
	               |				И Область = &Область
	               |				И Номенклатура В
	               |					(ВЫБРАТЬ
	               |						втСвернутыеТовары.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						втСвернутыеТовары КАК втСвернутыеТовары)) КАК ТК_КвотыНаОтгрузкуСрезПоследних
	               |ГДЕ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Состояние = 1
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Номенклатура,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Порог,
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Процент,
	               |	2
	               |ИЗ
	               |	РегистрСведений.ТК_КвотыНаОтгрузку.СрезПоследних(
	               |			&Дата,
	               |			ВидКвоты = ЗНАЧЕНИЕ(Перечисление.ТК_ВидыКвот.ПустаяСсылка)
	               |				И Область = ЗНАЧЕНИЕ(Справочник.ТК_Области.ПустаяСсылка)
	               |				И Номенклатура В
	               |					(ВЫБРАТЬ
	               |						втСвернутыеТовары.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						втСвернутыеТовары КАК втСвернутыеТовары)) КАК ТК_КвотыНаОтгрузкуСрезПоследних
	               |ГДЕ
	               |	ТК_КвотыНаОтгрузкуСрезПоследних.Состояние = 1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПорядокКвот.Номенклатура КАК Номенклатура,
	               |	вт_ЗначенияКвот.Порог КАК Порог,
	               |	вт_ЗначенияКвот.Процент КАК Процент
	               |ПОМЕСТИТЬ вт_ЗначенияКвотСверн
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		вт_ЗначенияКвот.Номенклатура КАК Номенклатура,
	               |		МИНИМУМ(вт_ЗначенияКвот.Порядок) КАК Порядок
	               |	ИЗ
	               |		вт_ЗначенияКвот КАК вт_ЗначенияКвот
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		вт_ЗначенияКвот.Номенклатура) КАК ПорядокКвот
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ЗначенияКвот КАК вт_ЗначенияКвот
	               |		ПО ПорядокКвот.Номенклатура = вт_ЗначенияКвот.Номенклатура
	               |			И ПорядокКвот.Порядок = вт_ЗначенияКвот.Порядок
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ вт_ЗначенияКвот
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСвернутыеТовары.Номенклатура КАК Номенклатура,
	               |	втСвернутыеТовары.Производитель КАК Производитель,
	               |	втСвернутыеТовары.Количество КАК Количество,
	               |	ВЫРАЗИТЬ(втСвернутыеТовары.Количество / Вт_ИтогиПоПроизводителям.Количество * 100 КАК ЧИСЛО(15, 2)) КАК текПроцент,
	               |	вт_ЗначенияКвотСверн.Порог КАК Порог,
	               |	вт_ЗначенияКвотСверн.Процент КАК Процент,
	               |	Вт_ИтогиПоПроизводителям.Количество КАК ИтогоПоПроизводителю,
	               |	втСвернутыеТовары.Количество > вт_ЗначенияКвотСверн.Порог КАК ПревышенПорог,
	               |	вт_ЗначенияКвотСверн.Процент > 0 КАК ПроверятьПревышениеПроцента,
	               |	вт_ЗначенияКвотСверн.Процент > 0
	               |		И (ВЫРАЗИТЬ(втСвернутыеТовары.Количество / Вт_ИтогиПоПроизводителям.Количество * 100 КАК ЧИСЛО(15, 2))) > вт_ЗначенияКвотСверн.Процент КАК ПревышенПроцент
	               |ПОМЕСТИТЬ ОграниченияПоКвотам
	               |ИЗ
	               |	втСвернутыеТовары КАК втСвернутыеТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ЗначенияКвотСверн КАК вт_ЗначенияКвотСверн
	               |		ПО втСвернутыеТовары.Номенклатура = вт_ЗначенияКвотСверн.Номенклатура
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Вт_ИтогиПоПроизводителям КАК Вт_ИтогиПоПроизводителям
	               |		ПО втСвернутыеТовары.Производитель = Вт_ИтогиПоПроизводителям.Производитель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОграниченияПоКвотам.Номенклатура КАК Номенклатура,
	               |	ОграниченияПоКвотам.Производитель КАК Производитель,
	               |	ОграниченияПоКвотам.Количество КАК Количество,
	               |	ОграниченияПоКвотам.текПроцент КАК текПроцент,
	               |	ОграниченияПоКвотам.Порог КАК Порог,
	               |	ОграниченияПоКвотам.Процент КАК Процент,
	               |	ОграниченияПоКвотам.ИтогоПоПроизводителю КАК ИтогоПоПроизводителю,
	               |	ОграниченияПоКвотам.ПревышенПорог КАК ПревышенПорог,
	               |	ОграниченияПоКвотам.ПроверятьПревышениеПроцента КАК ПроверятьПревышениеПроцента,
	               |	ОграниченияПоКвотам.ПревышенПроцент КАК ПревышенПроцент
	               |ПОМЕСТИТЬ ПревышенияПоКвотам
	               |ИЗ
	               |	ОграниченияПоКвотам КАК ОграниченияПоКвотам
	               |ГДЕ
	               |	ОграниченияПоКвотам.ПревышенПорог
	               |	И (НЕ ОграниченияПоКвотам.ПроверятьПревышениеПроцента
	               |			ИЛИ ОграниченияПоКвотам.ПроверятьПревышениеПроцента
	               |				И ОграниченияПоКвотам.ПревышенПроцент)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ втСвернутыеТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ Вт_ИтогиПоПроизводителям
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ вт_ЗначенияКвотСверн
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ОграниченияПоКвотам";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗаполнитьТаблицуОшибокПоОграничениямКвот(МенеджерВременныхТаблиц, ЕстьОшибки = Ложь) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПревышенияПоКвотам.Номенклатура КАК Номенклатура,
	               |	ПревышенияПоКвотам.Производитель КАК Производитель,
	               |	ПревышенияПоКвотам.Количество КАК Количество,
	               |	ПревышенияПоКвотам.текПроцент КАК текПроцент,
	               |	ПревышенияПоКвотам.Порог КАК Порог,
	               |	ПревышенияПоКвотам.Процент КАК Процент,
	               |	ПревышенияПоКвотам.ИтогоПоПроизводителю КАК ИтогоПоПроизводителю,
	               |	ПревышенияПоКвотам.ПревышенПорог КАК ПревышенПорог,
	               |	ПревышенияПоКвотам.ПроверятьПревышениеПроцента КАК ПроверятьПревышениеПроцента,
	               |	ПревышенияПоКвотам.ПревышенПроцент КАК ПревышенПроцент
	               |ИЗ
	               |	ПревышенияПоКвотам КАК ПревышенияПоКвотам";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		ЕстьОшибки = Ложь;
		Возврат Неопределено;
	Иначе
	    ЕстьОшибки = Истина;
		Возврат Результат.Выгрузить();
	КонецЕсли;
	
КонецФункции

#КонецОбласти


#Область ПроцедурыИФункцииРазделенияТоваровПоОрганизациям

Процедура ЗаполнитьВТЧОрганизациюПоНоменклатуре(ТаблицаТовары, Организация, ТорговаяПлощадка, КолонкаНоменклатура = "Номенклатура") Экспорт 	
	
	Если ТаблицаТовары.Колонки.Найти(КолонкаНоменклатура) = Неопределено Тогда 
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'В переданной ТаблицеТоваров не найдена колонка ""%1"". Обратитесь к разработчику...'; uk = 'У переданій ТаблиціТоварів не знайдена колонка ""%1"". Зверніться до розробника...'"),
							КолонкаНоменклатура);
							
		ВызватьИсключение ТекстОшибки;							
	КонецЕсли;
	
	Если ТаблицаТовары.Колонки.Найти("Организация") = Неопределено Тогда 
		ТаблицаТовары.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	КонецЕсли;
	
	СписокНоменклатуры = ТаблицаТовары.ВыгрузитьКолонку(КолонкаНоменклатура);
	
	СоответствиеНоменклатуры = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(
									Организация,
									СписокНоменклатуры,
									ТорговаяПлощадка);
									 
	Для Каждого Стр Из ТаблицаТовары Цикл 
		СтруктураПринадлежности = СоответствиеНоменклатуры.Получить(Стр[КолонкаНоменклатура]);
		Если СтруктураПринадлежности = Неопределено Тогда 
			СтруктураПринадлежности = Новый Структура;
		КонецЕсли;
		
		Стр.Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураПринадлежности, "Организация", Организация);		
	КонецЦикла;									
	
	
КонецПроцедуры



#КонецОбласти


Функция ПолучитьСтруктуруАгента(НастройкиАгента)
	УстановитьПривилегированныйРежим(Истина);
	
	Структура = Новый Структура;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПортативныеУстройстваВвода.Ссылка КАК ПортативноеУстройствоВвода,
	|	ПортативныеУстройстваВвода.Организация КАК Организация,
	|	ПортативныеУстройстваВвода.Подразделение КАК ПодразделениеКомпании,
	|	ПортативныеУстройстваВвода.СкладКомпании КАК СкладКомпании,
	|	ПортативныеУстройстваВвода.Менеджер КАК Менеджер,
	|	ПортативныеУстройстваВвода.НЕ_Резервировать КАК НЕ_Резервировать,
	|	ПортативныеУстройстваВвода.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПортативныеУстройстваВвода.СкладКомпании.ОбщийСклад КАК ОбщийСклад,
	|	ПортативныеУстройстваВвода.ЕдиницаЗаказаУпаковка КАК ЕдиницаЗаказаУпаковка
	|ИЗ
	|	Справочник.ПортативныеУстройстваВвода КАК ПортативныеУстройстваВвода
	|ГДЕ
	|	ПортативныеУстройстваВвода.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",НастройкиАгента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий(); 
	
	Структура.Вставить("Организация",Выборка.Организация);
	Структура.Вставить("ПортативноеУстройствоВвода",Выборка.ПортативноеУстройствоВвода);
	Структура.Вставить("ПодразделениеКомпании",Выборка.ПодразделениеКомпании);
	Структура.Вставить("СкладКомпании",Выборка.СкладКомпании);
	Структура.Вставить("ТорговаяПлощадка",Выборка.ТорговаяПлощадка);
	Структура.Вставить("Менеджер",Выборка.Менеджер);
	Структура.Вставить("Автор",Пользователи.ТекущийПользователь());
	Структура.Вставить("НЕРезервировать",Выборка.НЕ_Резервировать);
	Структура.Вставить("ЕдиницаЗаказаУпаковка",Выборка.ЕдиницаЗаказаУпаковка);
	
	
	МассивСкладов = Новый Массив;
	МассивСкладов.Добавить(Выборка.СкладКомпании);
	
	Если ЗначениеЗаполнено(Выборка.ОбщийСклад) Тогда
		МассивСкладов.Добавить(Выборка.ОбщийСклад);
	КонецЕсли;
	Структура.Вставить("МассивСкладов",МассивСкладов);
	
	
	
	Возврат Структура;
	
КонецФункции


Функция ВосстановлениеJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	//	Парам = ВРег(Свойство);
	Парам = Свойство;
	//Если Парам = "ORDERID" Тогда
	//	Попытка
	//		Рез = Новый УникальныйИдентификатор(Значение);
	//	Исключение
	//		Рез = Неопределено;
	//		ДополнительныеПараметры.Status = Ложь;
	//		ДополнительныеПараметры.Messages.ДобавитьСтроку(Значение + " - не коректний ID")
	//	КонецПопытки;
	Если Парам = "pos_code" Тогда
		Рез = Справочники.ТочкиДоставки.НайтиАдресДоставкиПоGlobal_ID(Значение);
		Если Рез.Пустая() Тогда
			ДополнительныеПараметры.Status = Ложь;
			ДополнительныеПараметры.Messages.ДобавитьСтроку(Строка(Значение) + " - не знайдено адресу доставки")
		КонецЕсли;
	ИначеЕсли Парам = "product_code" Тогда
		Рез = Справочники.Номенклатура.НайтиНоменклатуруПоАртикулу(Формат(Значение,"ЧЦ=6; ЧН=0; ЧГ="));
		Если Рез.Пустая() Тогда
			ДополнительныеПараметры.Status = Ложь;
			ДополнительныеПараметры.Messages.ДобавитьСтроку(Строка(Значение) + " - не знайдено номенклатуру")
		КонецЕсли;
		
	Иначе
		
		Рез = Неопределено;
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции

Функция ВосстановлениеCSV(ТелоЗапроса,ДополнительныеПараметры) Экспорт	
	
	
	СтруктураЗаказа = Новый Массив;
	счСтрока = 1;
	Строка = СтрПолучитьСтроку(ТелоЗапроса, счСтрока);
	
	КолонкиШапки = СтрРазделить(Строка, "|", Ложь);
	Источник = Новый ТаблицаЗначений;
	ПозицияКолонкиВФайле = Новый Соответствие();
	
	Позиция = 1;
	Для каждого Колонка Из КолонкиШапки Цикл
		НоваяКолонка = Источник.Колонки.Добавить();
		НоваяКолонка.Имя = Колонка;
		НоваяКолонка.Заголовок = Колонка;
		
		ПозицияКолонкиВФайле.Вставить(Позиция,Колонка);
		Позиция = Позиция + 1;
	КонецЦикла;
	
	//Если Источник.Колонки.Количество() = 0 Тогда
	//	Возврат;
	//КонецЕсли;
	счСтрока = счСтрока + 1;
	Строка = СтрПолучитьСтроку(ТелоЗапроса, счСтрока);
	
	Пока Строка <> "" ИЛИ  счСтрока > 5000 Цикл
		НоваяСтрока = Источник.Добавить();
		Позиция = СтрНайти(Строка, "|");
		Индекс = 0;
		Пока Позиция > 0 Цикл
			Если Источник.Колонки.Количество() < Индекс + 1 Тогда
				Прервать;
			КонецЕсли;
			ИмяКолонки = ПозицияКолонкиВФайле.Получить(Индекс + 1);
			Если ИмяКолонки <> Неопределено Тогда
				НоваяСтрока[ИмяКолонки] = Лев(Строка, Позиция - 1);
			КонецЕсли;
			Строка = Сред(Строка, Позиция + 1);
			Позиция = СтрНайти(Строка, "|");
			Индекс = Индекс + 1;
		КонецЦикла;
		Если Источник.Колонки.Количество() = Индекс + 1  Тогда
			НоваяСтрока[Индекс] = Строка;
		КонецЕсли;
		счСтрока = счСтрока+1;
		Строка = СтрПолучитьСтроку(ТелоЗапроса, счСтрока);
		
	КонецЦикла;
	
	Возврат СтруктураЗаказа
	
КонецФункции

Процедура ЗаписатьПереводЗаказа(МассивЗаказов,Поставщик)Экспорт
	
	Для Каждого Заказ Из МассивЗаказов Цикл 
		
		Набор = РегистрыСведений.ТК_ПереводЗаказов.СоздатьНаборЗаписей();
		Набор.Отбор.ID.Установить(Заказ.visit_id);
		Набор.Отбор.Поставщик.Установить(Поставщик);
		
		ДатаЗагрузки = ТекущаяДатаСеанса();
		Для Каждого СтрокаТовары Из Заказ.order Цикл 
			
			НоваяЗапись = Набор.Добавить();
			НоваяЗапись.ID = Заказ.visit_id;
			НоваяЗапись.Номенклатура = СтрокаТовары.product_code;
			НоваяЗапись.ДатаЗаказа = Заказ.dt;
			НоваяЗапись.Поставщик = Поставщик;
			НоваяЗапись.ДатаЗагрузки = ДатаЗагрузки;
			НоваяЗапись.ТочкаДоставки = Заказ.pos_code; 
			НоваяЗапись.Количество = СтрокаТовары.qty;
			
		КонецЦикла;
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры


Функция ПолучитьНастройкиАгена(ИдентификаторУстройства) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		Устройство = Справочники.ПортативныеУстройстваВвода.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдентификаторУстройства));
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		УстройствоОбъект = Устройство.ПолучитьОбъект();
		
		Если УстройствоОбъект = Неопределено Тогда
			ОбъектУстройство = Справочники.ПортативныеУстройстваВвода.СоздатьЭлемент();
			ОбъектУстройство.УстановитьСсылкуНового(Устройство);
			ОбъектУстройство.Наименование = СокрЛП(ТекущийПользователь);
			ОбъектУстройство.ID = ИдентификаторУстройства;
			ОбъектУстройство.Агент = ТекущийПользователь; 
			ОбъектУстройство.Организация =  ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"ОсновнаяОрганизация");
			ОбъектУстройство.Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(ТекущийПользователь);
			ОбъектУстройство.СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"ОсновнойСклад");
			ОбъектУстройство.ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(ОбъектУстройство.СкладКомпании);
			ОбъектУстройство.ОбменДанными.Загрузка = Истина;
			ОбъектУстройство.Записать();
		КонецЕсли;
	Исключение
		Устройство = Неопределено;
	КонецПопытки;
	
	
	Возврат Устройство;
	
КонецФункции

Функция СформироватьПакетВыгрузкиТоваровПоАрткулу(НастройкиАгента,Артикул) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	спрНоменклатура.Ссылка КАК Товар,
	|	спрНоменклатура.Артикул КАК Артикул,
	|	ЕСТЬNULL(спрНоменклатура.Родитель.Артикул, 0) КАК АртикулГруппа,
	|	спрНоменклатура.Наименование КАК Наименование,
	|	спрНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
	|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК КодОснЕденица,
	|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК НаименованиеОснЕденица,
	|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК КофОснЕд,
	|	спрНоменклатура.ЕдиницаИзмеренияУпаковки КАК Упаковка,
	|	спрНоменклатура.ЕдиницаИзмеренияУпаковки.Код КАК КодУпаковка,
	|	спрНоменклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КофУпаковка,
	|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК КодЗаказаЕденица,
	|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК НаименованиеЗаказаЕденица,
	|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.Коэффициент КАК КофЗаказаЕденица
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ЕдиницыИзмеренияЗаказов КАК ТК_ЕдиницыИзмеренияЗаказов
	|		ПО спрНоменклатура.Ссылка = ТК_ЕдиницыИзмеренияЗаказов.Номенклатура
	|			И (ТК_ЕдиницыИзмеренияЗаказов.Подразделение = &Подразделение)
	|ГДЕ
	|	спрНоменклатура.Артикул = &Артикул
	|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Артикул", Формат(Артикул,"ЧЦ=9; ЧГ="));
	Запрос.УстановитьПараметр("Подразделение", НастройкиАгента.Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение("Не найден товар по коду "+Артикул);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
		Возврат СтрокаДляОтвета;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	СсылкаНоменклатура = Выборка.Товар;
	СсылкаСкладАгента =  НастройкиАгента.СкладКомпании;
	ИспользоватьУпаковки = НастройкиАгента.ЕдиницаЗаказаУпаковка;
	МассивСкладов = Новый Массив;
	МассивСкладов.Добавить(СсылкаСкладАгента);
	
	Если ЗначениеЗаполнено(СсылкаСкладАгента.ОбщийСклад) Тогда
		МассивСкладов.Добавить(СсылкаСкладАгента.ОбщийСклад);
	КонецЕсли;
	
	
	СтрокаНоменклатура =  НастройкиАгента.Товары.Найти(СсылкаНоменклатура,"Номенклатура");
	Если СтрокаНоменклатура = Неопределено Тогда
		НастройкиАгентаОбъект = НастройкиАгента.ПолучитьОбъект();
		НоваяСтрокаНоменклатура =  НастройкиАгентаОбъект.Товары.Добавить();
		НоваяСтрокаНоменклатура.Номенклатура = СсылкаНоменклатура;
		НастройкиАгентаОбъект.ОбменДанными.Загрузка = Истина;
		НастройкиАгентаОбъект.Записать();
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("Товар");
	
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("Артикул");
	ЗаписьJSON.ЗаписатьЗначение(Выборка.Артикул);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("Владелец");
	ЗаписьJSON.ЗаписатьЗначение(Строка(Выборка.АртикулГруппа));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.Наименование));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("НаименованиеПолное");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НаименованиеПолное));
	
	
	ЗаписьJSON.ЗаписатьИмяСвойства("КОЕ");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КодОснЕденица));
	ЗаписьJSON.ЗаписатьИмяСвойства("НОЕ");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НаименованиеОснЕденица));
	ЗаписьJSON.ЗаписатьИмяСвойства("КФОЕ");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КофОснЕд));
	
	Если ИспользоватьУпаковки и ЗначениеЗаполнено(Выборка.Упаковка) Тогда
		КЗЕ = СокрЛП(Выборка.КодУпаковка);
		НЗЕ = Строка(Выборка.Упаковка);
		КФЗЕ = СокрЛП(Выборка.КофУпаковка); 
	Иначе
		КЗЕ= СокрЛП(Выборка.КодЗаказаЕденица);
		НЗЕ= СокрЛП(Выборка.НаименованиеЗаказаЕденица);
		КФЗЕ= СокрЛП(Выборка.КофЗаказаЕденица);
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("КЗЕ");
	ЗаписьJSON.ЗаписатьЗначение(КЗЕ);
	ЗаписьJSON.ЗаписатьИмяСвойства("НЗЕ");
	ЗаписьJSON.ЗаписатьЗначение(НЗЕ);
	ЗаписьJSON.ЗаписатьИмяСвойства("КФЗЕ");
	ЗаписьJSON.ЗаписатьЗначение(КФЗЕ);
	
	//
	//
	//
	//ЗаписьJSON.ЗаписатьИмяСвойства("КЗЕ");
	//ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КодЗаказаЕденица));
	//ЗаписьJSON.ЗаписатьИмяСвойства("НЗЕ");
	//ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НаименованиеЗаказаЕденица));
	//ЗаписьJSON.ЗаписатьИмяСвойства("КФЗЕ");
	//ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КофЗаказаЕденица));
	//
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК КодНоменклатуры,
	|	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|				,
	|				СкладКомпании В (&Склад)
	|					И Номенклатура = &Номенклатура) КАК ОстаткиТоваровКомпанииОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОстатки.Номенклатура,
	|		-РезервыТоваровОстатки.РезервОстаток
	|	ИЗ
	|		РегистрНакопления.РезервыТоваров.Остатки(
	|				,
	|				Склад В (&Склад)
	|					И Номенклатура = &Номенклатура) КАК РезервыТоваровОстатки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Артикул";
	
	Запрос.УстановитьПараметр("Номенклатура", СсылкаНоменклатура);
	Запрос.УстановитьПараметр("Склад", МассивСкладов);
	РезультатЗапроса = Запрос.Выполнить();
	
	ОстатокТовара = 0;
	Если НЕ  РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		ОстатокТовара = ВыборкаДетальныеЗаписи.Остаток;
	КонецЕсли;
	
	
	ЗаписьJSON.ЗаписатьИмяСвойства("ОстаткиТовара");
	
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("КодНоменклатуры");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.Артикул));
	ЗаписьJSON.ЗаписатьИмяСвойства("Остаток");
	ЗаписьJSON.ЗаписатьЗначение(ОстатокТовара);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
КонецФункции

Функция СформироватьПакетВыгрузкиКонтрагента(НастройкиАгента,ОКПО)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	спрКонтрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК спрКонтрагенты
	|ГДЕ
	|	спрКонтрагенты.Код = &КодПоЕДРПОУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	спрКонтрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК спрКонтрагенты
	|ГДЕ
	|	спрКонтрагенты.КодПоЕДРПОУ = &КодПоЕДРПОУ";
	
	Запрос.УстановитьПараметр("КодПоЕДРПОУ", ОКПО);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение("Не найден контрагент по ОКПО "+ОКПО);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
		Возврат СтрокаДляОтвета;
		
	КонецЕсли;
	
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Контрагент = Выборка.Контрагент;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Код КАК Код,
	|	Контрагенты.Наименование КАК Наименование,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	|	Контрагенты.КодПоЕДРПОУ КАК КодПоОКПО,
	|	ТочкиДоставки.Код КАК КодАдресДоставки,
	|	ТочкиДоставки.Представление КАК АдресДоставки,
	|	ТочкиДоставки.Действует КАК Действует
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТочкиДоставки КАК ТочкиДоставки
	|		ПО Контрагенты.Ссылка = ТочкиДоставки.Владелец
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	
	ЗапросДоговора = Новый Запрос;
	ЗапросДоговора.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыВзаиморасчетов.Контрагент.Код КАК КодКонтрагента,
	|	ДоговорыВзаиморасчетов.Код КАК КодТК,
	|	ДоговорыВзаиморасчетов.Наименование КАК Наименование,
	|	ДоговорыВзаиморасчетов.Номер КАК Номер,
	|	ДоговорыВзаиморасчетов.ДатаНачалаДействия КАК Дата
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыВзаиморасчетов
	|ГДЕ
	|	ДоговорыВзаиморасчетов.Контрагент = &Контрагент
	|	И ДоговорыВзаиморасчетов.Подразделение = &Подразделение
	|	И ДоговорыВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.Продажа)
	|	И ДоговорыВзаиморасчетов.Организация В(&Организация)
	|	И ДоговорыВзаиморасчетов.ПометкаУдаления = ЛОЖЬ
	|	И ДоговорыВзаиморасчетов.Статус = ЗНАЧЕНИЕ(перечисление.СтатусыДоговоровКонтрагентов.Действует)";
	ЗапросДоговора.УстановитьПараметр("Подразделение",НастройкиАгента.Подразделение);
	ЗапросДоговора.УстановитьПараметр("Контрагент",Контрагент);
	
	МассивОрганизация = Новый Массив;
	МассивОрганизация.Добавить(НастройкиАгента.Организация);
	МассивОрганизация.Добавить(Справочники.Организации.НайтиПоКоду("D109")); //Аттика
	
	ЗапросДоговора.УстановитьПараметр("Организация",МассивОрганизация);
	
	
	ДоговораКонтрагентов = ЗапросДоговора.Выполнить().Выгрузить();
	
	Если ДоговораКонтрагентов.Количество() = 0 Тогда
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение("Нет договора контрагента по ОКПО "+ОКПО);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
		Возврат СтрокаДляОтвета;
		
		
	КонецЕсли;
	
	
	СтрокаКонтрагент =  НастройкиАгента.Контрагенты.Найти(Контрагент,"Контрагент");
	Если СтрокаКонтрагент = Неопределено Тогда
		НастройкиАгентаОбъект = НастройкиАгента.ПолучитьОбъект();
		НоваяСтрокаКонтрагент =  НастройкиАгентаОбъект.Контрагенты.Добавить();
		НоваяСтрокаКонтрагент.Контрагент = Контрагент;
		НастройкиАгентаОбъект.ОбменДанными.Загрузка = Истина;
		НастройкиАгентаОбъект.Записать();
	КонецЕсли;
	
	
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("Контрагенты");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	
	Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("Код") Цикл
		
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.Код));
		ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.Наименование));
		ЗаписьJSON.ЗаписатьИмяСвойства("НаименованиеПолное");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.НаименованиеПолное));
		ЗаписьJSON.ЗаписатьИмяСвойства("КодПоОКПО");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодПоОКПО));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("АдресаДоставки");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.КодАдресДоставки) Тогда
				Продолжить;	
			КонецЕсли;
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодАдресДоставки));
			ЗаписьJSON.ЗаписатьИмяСвойства("АдресДоставки");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.АдресДоставки));
			ЗаписьJSON.ЗаписатьИмяСвойства("Действует");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Действует);
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Договора");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Для Каждого сДоговор Из ДоговораКонтрагентов Цикл 
			
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.КодТК));
			ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Наименование));
			ЗаписьJSON.ЗаписатьИмяСвойства("Номер");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Номер));
			ЗаписьJSON.ЗаписатьИмяСвойства("Дата");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Дата));
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	
	
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
КонецФункции

Функция СформироватьПакетВыгрузкиЦенКонтрагентаПоДоговору(НастройкиАгента,КодДоговора)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыВзаиморасчетов.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыВзаиморасчетов
	|ГДЕ
	|	ДоговорыВзаиморасчетов.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодДоговора);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение("Не найден договор контрагента по коду: "+КодДоговора);
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
		Возврат СтрокаДляОтвета;
		
		
	КонецЕсли;
	
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Договор   = Выборка.Договор;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураНоменклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК НоменклатураНоменклатура
	|ГДЕ
	|	НоменклатураНоменклатура.Ссылка В ИЕРАРХИИ(&МассивТоваров)
	|	И НоменклатураНоменклатура.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("МассивТоваров", НастройкиАгента.Товары.ВыгрузитьКолонку("Номенклатура"));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение("Нет товаро для выгрузки");
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
		Возврат СтрокаДляОтвета;
		
	КонецЕсли;
	
	массивТоваров = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УсловияЦенообразованияСрезПоследних.Объект КАК ДоговорКонтрагента,
	|	УсловияЦенообразованияСрезПоследних.ГруппаЦенообразования КАК ГруппаЦенообразования,
	|	ВЫБОР
	|		КОГДА УсловияЦенообразованияСрезПоследних.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.РегламентированныйУчет)
	|			ТОГДА 1
	|		КОГДА УсловияЦенообразованияСрезПоследних.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НеРегламентированныйУчет)
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Учет,
	|	УсловияЦенообразованияСрезПоследних.ТипЦены КАК ТипЦены
	|ПОМЕСТИТЬ ВТ_ТипыЦен
	|ИЗ
	|	РегистрСведений.ТК_УсловияЦенообразования.СрезПоследних(&НаДату, Объект = &Договор) КАК УсловияЦенообразованияСрезПоследних
	|ГДЕ
	|	УсловияЦенообразованияСрезПоследних.Действует
	|	И &НаДату МЕЖДУ УсловияЦенообразованияСрезПоследних.НачалоДействия И УсловияЦенообразованияСрезПоследних.КонецДействия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	|	ЦеныСрезПоследних.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования,
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ЦеныСрезПоследних.Цена) КАК Цена
	|ПОМЕСТИТЬ ВТ_СрезЦен
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&НаДату,
	|			ТипЦен В
	|					(ВЫБРАТЬ
	|						ВТ_ТипыЦен.ТипЦены
	|					ИЗ
	|						ВТ_ТипыЦен КАК ВТ_ТипыЦен
	|					СГРУППИРОВАТЬ ПО
	|						ВТ_ТипыЦен.ТипЦены)
	|				И Номенклатура В (&МассивНоменклатуры)) КАК ЦеныСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныСрезПоследних.Номенклатура,
	|	ЦеныСрезПоследних.ТипЦен,
	|	ЦеныСрезПоследних.Номенклатура.ГруппаЦенообразования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТипыЦен.ТипЦены КАК ТипЦены,
	|	ВТ_СрезЦен.Номенклатура КАК Номенклатура,
	|	ВТ_СрезЦен.Номенклатура.Артикул КАК КодНоменклатуры,
	|	ВТ_ТипыЦен.Учет КАК Учет,
	|	ВТ_СрезЦен.Цена КАК Цена
	|ИЗ
	|	ВТ_ТипыЦен КАК ВТ_ТипыЦен
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СрезЦен КАК ВТ_СрезЦен
	|		ПО ВТ_ТипыЦен.ТипЦены = ВТ_СрезЦен.ТипЦен
	|			И ВТ_ТипыЦен.ГруппаЦенообразования = ВТ_СрезЦен.ГруппаЦенообразования";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("МассивНоменклатуры", массивТоваров);
	Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("ТККодДоговора");
	ЗаписьJSON.ЗаписатьЗначение(СокрЛП(КодДоговора));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("ЦеныКонтрагентаПоДоговору");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("ТККодНоменклтуры");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодНоменклатуры));
		ЗаписьJSON.ЗаписатьИмяСвойства("Учет");
		ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Учет);
		ЗаписьJSON.ЗаписатьИмяСвойства("Цена");
		ЗаписьJSON.ЗаписатьЗначение(Окр(ВыборкаДетальныеЗаписи.Цена,2));
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
	
КонецФункции

Функция ВыгрузитьОстатки(НастройкиАгента)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураНастроекАгента = ПолучитьСтруктуруАгента(НастройкиАгента);
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 	"ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|ГДЕ
	|	спрНоменклатура.ЭтоГруппа = ЛОЖЬ
	|	И спрНоменклатура.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				ПортативныеУстройстваВводаТовары.Номенклатура
	|			ИЗ
	|				Справочник.ПортативныеУстройстваВвода.Товары КАК ПортативныеУстройстваВводаТовары
	|			ГДЕ
	|				ПортативныеУстройстваВводаТовары.Ссылка = &Настройки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК КодНоменклатуры,
	|	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Характеристика.ЗначениеМРЦ КАК МРЦ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ИспользованиеХарактеристик
	|				ТОГДА ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Характеристика,
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|				,
	|				СкладКомпании В (&Склад)
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_Товары.Номенклатура
	|						ИЗ
	|							ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровКомпанииОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОстатки.Номенклатура,
	|		ВЫБОР
	|			КОГДА РезервыТоваровОстатки.Номенклатура.ИспользованиеХарактеристик
	|				ТОГДА РезервыТоваровОстатки.Характеристика
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		-РезервыТоваровОстатки.РезервОстаток
	|	ИЗ
	|		РегистрНакопления.РезервыТоваров.Остатки(
	|				,
	|				Склад В (&Склад)
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_Товары.Номенклатура
	|						ИЗ
	|							ВТ_Товары КАК ВТ_Товары)) КАК РезервыТоваровОстатки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Артикул,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Характеристика.ЗначениеМРЦ";
	
	
	Запрос.Текст = ТекстЗапроса;
	
	
	Запрос.УстановитьПараметр("Склад", СтруктураНастроекАгента.МассивСкладов);
	Запрос.УстановитьПараметр("Настройки",СтруктураНастроекАгента.ПортативноеУстройствоВвода);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	ЗаписьJSON.ЗаписатьИмяСвойства("ОстаткиТоваров");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Пока Выборка.Следующий() Цикл
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("КодНоменклатуры");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КодНоменклатуры));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("МРЦ");
		Если ЗначениеЗаполнено(Выборка.МРЦ) Тогда
			ЗаписьJSON.ЗаписатьЗначение(Выборка.МРЦ);
		Иначе
			ЗаписьJSON.ЗаписатьЗначение(0);
			
		КонецЕсли;
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Остаток");
		ЗаписьJSON.ЗаписатьЗначение(Выборка.Остаток);
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;
	ЗаписьJSON.ЗаписатьКонецМассива();
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
	
КонецФункции

Процедура ПолучитьТекущиеОстаткиТоваров(Параметры,ЗаписьJSON)
	СкладКомпании = Неопределено;
	Параметры.Свойство("СкладКомпании",СкладКомпании);
	Если СкладКомпании = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаАртикулов = Неопределено;
	Параметры.Свойство("ТаблицаАртикулов",ТаблицаАртикулов);
	
	Если ТаблицаАртикулов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТаблицаАртикулов.Свернуть("Артикул");
	
	ТекстЗапроса =   "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Артикул В(&мАртикул)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Товары.Ссылка КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВТ_Товары.Ссылка.Артикул КАК КодНоменклатуры,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.Остаток, 0)) КАК Остаток,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВложенныйЗапрос.Характеристика, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВложенныйЗапрос.Характеристика.ЗначениеМРЦ
	|	КОНЕЦ КАК МРЦ
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|			ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
	|			ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	|		ИЗ
	|			РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|					,
	|					Номенклатура В
	|							(ВЫБРАТЬ
	|								ВТ_Товары.Ссылка
	|							ИЗ
	|								ВТ_Товары КАК ВТ_Товары)
	|						И СкладКомпании В (&Склад)) КАК ОстаткиТоваровКомпанииОстатки
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			РезервыТоваровОстатки.Номенклатура,
	|			РезервыТоваровОстатки.Характеристика,
	|			-РезервыТоваровОстатки.РезервОстаток
	|		ИЗ
	|			РегистрНакопления.РезервыТоваров.Остатки(
	|					,
	|					Номенклатура В
	|							(ВЫБРАТЬ
	|								ВТ_Товары.Ссылка
	|							ИЗ
	|								ВТ_Товары КАК ВТ_Товары)
	|						И Склад В (&Склад)) КАК РезервыТоваровОстатки) КАК ВложенныйЗапрос
	|		ПО ВТ_Товары.Ссылка = ВложенныйЗапрос.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Товары.Ссылка,
	|	ВТ_Товары.Ссылка.Артикул,
	|	ВложенныйЗапрос.Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВложенныйЗапрос.Характеристика, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВложенныйЗапрос.Характеристика.ЗначениеМРЦ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Остатки.Номенклатура КАК Номенклатура,
	|	ВТ_Остатки.Характеристика КАК Характеристика,
	|	ВТ_Остатки.КодНоменклатуры КАК КодНоменклатуры,
	|	ВТ_Остатки.Остаток КАК Остаток,
	|	ВТ_Остатки.МРЦ КАК МРЦ
	|ИЗ
	|	ВТ_Остатки КАК ВТ_Остатки" ;
	Запрос.УстановитьПараметр("мАртикул", ТаблицаАртикулов.ВыгрузитьКолонку("Артикул"));
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Склад", СкладКомпании);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	
	ЗаписьJSON.ЗаписатьИмяСвойства("ОстаткиТоваров");
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Пока Выборка.Следующий() Цикл
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("КодНоменклатуры");
		ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КодНоменклатуры));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("МРЦ");
		Если ЗначениеЗаполнено(Выборка.МРЦ) Тогда
			ЗаписьJSON.ЗаписатьЗначение(Выборка.МРЦ);
		Иначе
			ЗаписьJSON.ЗаписатьЗначение("0");
		КонецЕсли;
		
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Остаток");
		ЗаписьJSON.ЗаписатьЗначение(Выборка.Остаток);
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;
	ЗаписьJSON.ЗаписатьКонецМассива();
	
КонецПроцедуры


Функция СформироватьПакетВыгрузки(НастройкиАгента,Параметры)Экспорт 
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	
	Если Параметры = "Номенклатура" Тогда
		
		
		МассивТоваров = НастройкиАгента.Товары.ВыгрузитьКолонку("Номенклатура");
		Если МассивТоваров.Количество() = 0 Тогда
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			ЗаписьJSON.ЗаписатьИмяСвойства("info");
			ЗаписьJSON.ЗаписатьЗначение("Нет рабочих товаров");
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
			СтрокаДляОтвета = ЗаписьJSON.Закрыть();
			
			Возврат СтрокаДляОтвета;
			
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	спрНоменклатура.ТоварнаяМарка КАК ТоварнаяМарка,
		|	спрНоменклатура.ТоварнаяМарка.Родитель.Код КАК КодГруппы,
		|	спрНоменклатура.ТоварнаяМарка.Родитель КАК Группа
		|ИЗ
		|	Справочник.Номенклатура КАК спрНоменклатура
		|ГДЕ
		|	спрНоменклатура.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ПортативныеУстройстваВводаТовары.Номенклатура
		|			ИЗ
		|				Справочник.ПортативныеУстройстваВвода.Товары КАК ПортативныеУстройстваВводаТовары
		|			ГДЕ
		|				ПортативныеУстройстваВводаТовары.Ссылка = &Ссылка)
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|	И НЕ спрНоменклатура.ТоварнаяМарка = ЗНАЧЕНИЕ(Справочник.ТоварныеМарки.ПустаяСсылка)
		|	И НЕ спрНоменклатура.ТоварнаяМарка.Родитель = ЗНАЧЕНИЕ(Справочник.ТоварныеМарки.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	спрНоменклатура.ТоварнаяМарка,
		|	спрНоменклатура.ТоварнаяМарка.Родитель.Код,
		|	спрНоменклатура.ТоварнаяМарка.Родитель";
		Запрос.УстановитьПараметр("Ссылка", НастройкиАгента);
		
		//Группировать будем по группам товарных марок
		таблицаТоварныхМарок = Запрос.Выполнить().Выгрузить();
		таблицаТоварныхМарок.Индексы.Добавить("ТоварнаяМарка");		
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("ГруппыТоваров");
		
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Для Каждого тГруппа Из таблицаТоварныхМарок Цикл 
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Артикул");
			ЗаписьJSON.ЗаписатьЗначение(тГруппа.КодГруппы);
			ЗаписьJSON.ЗаписатьИмяСвойства("Владелец");
			ЗаписьJSON.ЗаписатьЗначение("0");
			ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(тГруппа.Группа));
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		ИспользоватьУпаковки = НастройкиАгента.ЕдиницаЗаказаУпаковка; 
		СсылкаСкладАгента = НастройкиАгента.СкладКомпании;
		МассивСкладов = Новый Массив;
		МассивСкладов.Добавить(СсылкаСкладАгента);
		
		Если ЗначениеЗаполнено(СсылкаСкладАгента.ОбщийСклад) Тогда
			МассивСкладов.Добавить(СсылкаСкладАгента.ОбщийСклад);
		КонецЕсли;
		
		
		Запрос = Новый Запрос;
		Запрос.Текст =	"ВЫБРАТЬ
		|	ТК_ЕдиницыИзмеренияЗаказов.Номенклатура.Артикул КАК Артикул,
		|	ЕСТЬNULL(спрНоменклатура.Родитель.Артикул, 0) КАК АртикулГруппа,
		|	спрНоменклатура.Наименование КАК Наименование,
		|	спрНоменклатура.НаименованиеПолное КАК НаименованиеПолное,
		|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК КодОснЕденица,
		|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК НаименованиеОснЕденица,
		|	спрНоменклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК КофОснЕд,
		|	спрНоменклатура.ТоварнаяМарка КАК ТоварнаяМарка,
		|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК КодЗаказаЕденица,
		|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Наименование КАК НаименованиеЗаказаЕденица,
		|	ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.Коэффициент КАК КофЗаказаЕденица,
		|	спрНоменклатура.ЕдиницаИзмеренияУпаковки КАК Упаковка,
		|	спрНоменклатура.ЕдиницаИзмеренияУпаковки.Код КАК КодУпаковка,
		|	спрНоменклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КофУпаковка
		|ИЗ
		|	Справочник.Номенклатура КАК спрНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ЕдиницыИзмеренияЗаказов КАК ТК_ЕдиницыИзмеренияЗаказов
		|		ПО спрНоменклатура.Ссылка = ТК_ЕдиницыИзмеренияЗаказов.Номенклатура
		|			И (ТК_ЕдиницыИзмеренияЗаказов.Подразделение = &Подразделение)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(, СкладКомпании В (&Склад)) КАК ОстаткиТоваровКомпанииОстатки
		|		ПО спрНоменклатура.Ссылка = ОстаткиТоваровКомпанииОстатки.Номенклатура
		|ГДЕ
		|	спрНоменклатура.Ссылка В ИЕРАРХИИ(&МассивСсылок)
		|	И спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|	И НЕ спрНоменклатура.ТоварнаяМарка = ЗНАЧЕНИЕ(Справочник.ТоварныеМарки.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("МассивСсылок",МассивТоваров);
		Запрос.УстановитьПараметр("Склад",МассивСкладов);
		Запрос.УстановитьПараметр("Подразделение", НастройкиАгента.Подразделение);
		
		Результат  = Запрос.Выполнить();
		
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Товары");
		
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Артикул");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.Артикул));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Владелец");
			
			сТоварнаяМарка = таблицаТоварныхМарок.Найти(Выборка.ТоварнаяМарка,"ТоварнаяМарка");
			Если сТоварнаяМарка = Неопределено Тогда
				ЗаписьJSON.ЗаписатьЗначение("0");
			Иначе
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сТоварнаяМарка.КодГруппы));
			КонецЕсли;
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.Наименование));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("НаименованиеПолное");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НаименованиеПолное));
			
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КОЕ");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КодОснЕденица));
			ЗаписьJSON.ЗаписатьИмяСвойства("НОЕ");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НаименованиеОснЕденица));
			ЗаписьJSON.ЗаписатьИмяСвойства("КФОЕ");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.КофОснЕд));
			
			Если ИспользоватьУпаковки и ЗначениеЗаполнено(Выборка.Упаковка) Тогда
				КЗЕ = СокрЛП(Выборка.КодУпаковка);
				НЗЕ = Строка(Выборка.Упаковка);
				КФЗЕ = СокрЛП(Выборка.КофУпаковка); 
			Иначе
				КЗЕ= СокрЛП(Выборка.КодЗаказаЕденица);
				НЗЕ= СокрЛП(Выборка.НаименованиеЗаказаЕденица);
				КФЗЕ= СокрЛП(Выборка.КофЗаказаЕденица);
			КонецЕсли;
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КЗЕ");
			ЗаписьJSON.ЗаписатьЗначение(КЗЕ);
			ЗаписьJSON.ЗаписатьИмяСвойства("НЗЕ");
			ЗаписьJSON.ЗаписатьЗначение(НЗЕ);
			ЗаписьJSON.ЗаписатьИмяСвойства("КФЗЕ");
			ЗаписьJSON.ЗаписатьЗначение(КФЗЕ);
			
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	ИначеЕсли   Параметры = "Контрагенты" Тогда
		
		МассивКонтрагентов = НастройкиАгента.Контрагенты.ВыгрузитьКолонку("Контрагент");
		Если МассивКонтрагентов.Количество() = 0 Тогда
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			ЗаписьJSON.ЗаписатьИмяСвойства("info");
			ЗаписьJSON.ЗаписатьЗначение("Нет рабочих контрагентов");
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
			СтрокаДляОтвета = ЗаписьJSON.Закрыть();
			
			Возврат СтрокаДляОтвета;
			
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Код КАК Код,
		|	Контрагенты.Наименование,
		|	Контрагенты.НаименованиеПолное,
		|	Контрагенты.КодПоЕДРПОУ КАК КодПоОКПО,
		|	ТочкиДоставки.Код КАК КодАдресДоставки,
		|	ТочкиДоставки.Представление КАК АдресДоставки,
		|	ТочкиДоставки.Действует
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТочкиДоставки КАК ТочкиДоставки
		|		ПО Контрагенты.Ссылка = ТочкиДоставки.Владелец
		|ГДЕ
		|	 Контрагенты.Ссылка В(&МассивКонтрагентов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Код";
		
		Запрос.УстановитьПараметр("МассивКонтрагентов", МассивКонтрагентов);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			ЗаписьJSON.ЗаписатьИмяСвойства("info");
			ЗаписьJSON.ЗаписатьЗначение("Нет рабочих контрагентов");
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
			СтрокаДляОтвета = ЗаписьJSON.Закрыть();
			
			Возврат СтрокаДляОтвета;
			
		КонецЕсли;
		
		
		ЗапросДоговора = Новый Запрос;
		ЗапросДоговора.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыВзаиморасчетов.Контрагент.Код КАК КодКонтрагента,
		|	ДоговорыВзаиморасчетов.Код КАК КодТК,
		|	ДоговорыВзаиморасчетов.Наименование КАК Наименование,
		|	ДоговорыВзаиморасчетов.Номер КАК Номер,
		|	ДоговорыВзаиморасчетов.ДатаНачалаДействия КАК Дата
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыВзаиморасчетов
		|ГДЕ
		|	ДоговорыВзаиморасчетов.Контрагент В(&МассивКонтрагентов)
		|	И ДоговорыВзаиморасчетов.Подразделение = &Подразделение
		|	И ДоговорыВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.Продажа)
		|	И ДоговорыВзаиморасчетов.Организация В(&Организация)
		|	И ДоговорыВзаиморасчетов.ПометкаУдаления = ЛОЖЬ
		|	И ДоговорыВзаиморасчетов.Статус = ЗНАЧЕНИЕ(перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодКонтрагента";
		ЗапросДоговора.УстановитьПараметр("Подразделение",НастройкиАгента.Подразделение);
		ЗапросДоговора.УстановитьПараметр("МассивКонтрагентов",МассивКонтрагентов);
		
		МассивОрганизация = Новый Массив;
		МассивОрганизация.Добавить(НастройкиАгента.Организация);
		МассивОрганизация.Добавить(Справочники.Организации.НайтиПоКоду("D109")); //Аттика
		
		ЗапросДоговора.УстановитьПараметр("Организация",МассивОрганизация);
		
		
		ДоговораКонтрагентов = ЗапросДоговора.Выполнить().Выгрузить();
		ДоговораКонтрагентов.Индексы.Добавить("КодКонтрагента");
		
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("Контрагенты");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("Код") Цикл
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.Код));
			ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.Наименование));
			ЗаписьJSON.ЗаписатьИмяСвойства("НаименованиеПолное");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.НаименованиеПолное));
			ЗаписьJSON.ЗаписатьИмяСвойства("КодПоОКПО");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодПоОКПО));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("АдресаДоставки");
			ЗаписьJSON.ЗаписатьНачалоМассива();
			
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
				Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.КодАдресДоставки) Тогда
					Продолжить;	
				КонецЕсли;
				
				ЗаписьJSON.ЗаписатьНачалоОбъекта();
				
				ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодАдресДоставки));
				ЗаписьJSON.ЗаписатьИмяСвойства("АдресДоставки");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.АдресДоставки));
				ЗаписьJSON.ЗаписатьИмяСвойства("Действует");
				ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Действует);
				
				ЗаписьJSON.ЗаписатьКонецОбъекта();
				
			КонецЦикла;
			ЗаписьJSON.ЗаписатьКонецМассива();
			
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Договора");
			ЗаписьJSON.ЗаписатьНачалоМассива();
			
			МассивДоговоров = ДоговораКонтрагентов.НайтиСтроки(Новый Структура("КодКонтрагента",ВыборкаДетальныеЗаписи.Код));
			Для Каждого сДоговор Из МассивДоговоров Цикл 
				
				
				ЗаписьJSON.ЗаписатьНачалоОбъекта();
				
				ЗаписьJSON.ЗаписатьИмяСвойства("ТККод");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.КодТК));
				ЗаписьJSON.ЗаписатьИмяСвойства("Наименование");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Наименование));
				ЗаписьJSON.ЗаписатьИмяСвойства("Номер");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Номер));
				ЗаписьJSON.ЗаписатьИмяСвойства("Дата");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(сДоговор.Дата));
				
				ЗаписьJSON.ЗаписатьКонецОбъекта();
				
			КонецЦикла;
			
			ЗаписьJSON.ЗаписатьКонецМассива();
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	ИначеЕсли  Параметры = "Цены" Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПортативныеУстройстваВводаКонтрагенты.Контрагент КАК Контрагент
		|ПОМЕСТИТЬ вКонтр
		|ИЗ
		|	Справочник.ПортативныеУстройстваВвода.Контрагенты КАК ПортативныеУстройстваВводаКонтрагенты
		|ГДЕ
		|	ПортативныеУстройстваВводаКонтрагенты.Ссылка = &Устройство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Номенклатура
		|ПОМЕСТИТЬ сТов
		|ИЗ
		|	Справочник.Номенклатура КАК спрНоменклатура
		|ГДЕ
		|	спрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|	И спрНоменклатура.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				ПортативныеУстройстваВводаТовары.Номенклатура КАК Номенклатура
		|			ИЗ
		|				Справочник.ПортативныеУстройстваВвода.Товары КАК ПортативныеУстройстваВводаТовары
		|			ГДЕ
		|				ПортативныеУстройстваВводаТовары.Ссылка = &Устройство)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ рДог
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК спрДоговорыКонтрагентов
		|ГДЕ
		|	спрДоговорыКонтрагентов.Контрагент В
		|			(ВЫБРАТЬ
		|				вКонтр.Контрагент КАК Контрагент
		|			ИЗ
		|				вКонтр КАК вКонтр)
		|	И спрДоговорыКонтрагентов.Подразделение = &Подразделение
		|	И спрДоговорыКонтрагентов.Организация = &Организация
		|	И спрДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.Продажа)
		|	И спрДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|	И спрДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТК_УсловияЦенообразованияСрезПоследних.Объект КАК ДоговорКонтрагента,
		|	ТК_УсловияЦенообразованияСрезПоследних.Объект.Код КАК ТККодДоговора,
		|	ТК_УсловияЦенообразованияСрезПоследних.ГруппаЦенообразования КАК ГруппаЦенообразования,
		|	ВЫБОР
		|		КОГДА ТК_УсловияЦенообразованияСрезПоследних.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.РегламентированныйУчет)
		|			ТОГДА 1
		|		КОГДА ТК_УсловияЦенообразованияСрезПоследних.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НеРегламентированныйУчет)
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК Учет,
		|	ТК_УсловияЦенообразованияСрезПоследних.ТипЦены КАК ТипЦены
		|ПОМЕСТИТЬ рТипыЦен
		|ИЗ
		|	РегистрСведений.ТК_УсловияЦенообразования.СрезПоследних(
		|			&НаДату,
		|			Объект В
		|				(ВЫБРАТЬ
		|					рДог.Ссылка КАК Ссылка
		|				ИЗ
		|					рДог КАК рДог)) КАК ТК_УсловияЦенообразованияСрезПоследних
		|ГДЕ
		|	ТК_УсловияЦенообразованияСрезПоследних.Действует
		|	И &НаДату МЕЖДУ ТК_УсловияЦенообразованияСрезПоследних.НачалоДействия И ТК_УсловияЦенообразованияСрезПоследних.КонецДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
		|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ЦеныСрезПоследних.Цена) КАК Цена
		|ПОМЕСТИТЬ вЦеныПод
		|ИЗ
		|	РегистрСведений.Цены.СрезПоследних(
		|			&НаДату,
		|			ТипЦен В
		|				(ВЫБРАТЬ
		|					рТипыЦен.ТипЦены КАК ТипЦены
		|				ИЗ
		|					рТипыЦен КАК рТипыЦен)) КАК ЦеныСрезПоследних
		|ГДЕ
		|	ЦеныСрезПоследних.Цена <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныСрезПоследних.ТипЦен,
		|	ЦеныСрезПоследних.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вЦеныПод.ТипЦен КАК ТипЦен,
		|	вЦеныПод.Номенклатура КАК Номенклатура,
		|	вЦеныПод.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования,
		|	вЦеныПод.Цена КАК Цена
		|ПОМЕСТИТЬ вЦены
		|ИЗ
		|	вЦеныПод КАК вЦеныПод
		|ГДЕ
		|	вЦеныПод.Номенклатура В
		|			(ВЫБРАТЬ
		|				сТов.Номенклатура КАК Номенклатура
		|			ИЗ
		|				сТов КАК сТов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	рТипыЦен.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	рТипыЦен.ТККодДоговора КАК ТККодДоговора,
		|	вЦены.Номенклатура.Артикул КАК КодНоменклатуры,
		|	рТипыЦен.Учет КАК Учет,
		|	вЦены.Цена КАК Цена
		|ИЗ
		|	вЦены КАК вЦены
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ рТипыЦен КАК рТипыЦен
		|		ПО вЦены.ТипЦен = рТипыЦен.ТипЦены
		|			И вЦены.ГруппаЦенообразования = рТипыЦен.ГруппаЦенообразования
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоговорКонтрагента";
		
		//	Запрос.УстановитьПараметр("МассивДоговора", МассивДоговора);
		//	Запрос.УстановитьПараметр("МассивНоменклатуры", МассивНоменклатура);
		Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
		Запрос.УстановитьПараметр("Подразделение",НастройкиАгента.Подразделение);
		Запрос.УстановитьПараметр("Организация",НастройкиАгента.Организация);
		Запрос.УстановитьПараметр("Устройство",НастройкиАгента);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("ЦеныКонтрагентов");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		
		Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("ДоговорКонтрагента") Цикл
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ТККодДоговора");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.ТККодДоговора));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ЦеныКонтрагентаПоДоговору");
			ЗаписьJSON.ЗаписатьНачалоМассива();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
				
				ЗаписьJSON.ЗаписатьНачалоОбъекта();
				ЗаписьJSON.ЗаписатьИмяСвойства("КодНоменклатуры");
				ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодНоменклатуры));
				ЗаписьJSON.ЗаписатьИмяСвойства("Учет");
				ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Учет);
				ЗаписьJSON.ЗаписатьИмяСвойства("Цена");
				ЗаписьJSON.ЗаписатьЗначение(Окр(ВыборкаДетальныеЗаписи.Цена,2));
				ЗаписьJSON.ЗаписатьКонецОбъекта();
				
			КонецЦикла;
			
			ЗаписьJSON.ЗаписатьКонецМассива();
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	ИначеЕсли Параметры = "ОграничениеПоТоварам" Тогда	
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта(); 
		ЗаписьJSON.ЗаписатьИмяСвойства("ГлавныйМенеджер");
		ЗаписьJSON.ЗаписатьЗначение(НастройкиАгента.Администратор);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("ОграничениеТоваровМобильныхУстройств");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	ИначеЕсли Параметры = "ОстаткиВзаиморасчетов" Тогда	
		
		
		Запрос = Новый Запрос;
		Запрос.Текст ="ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет
		|			ТОГДА 0
		|		ИНАЧЕ РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток
		|	КОНЕЦ КАК СуммаУпрОстаток,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет
		|			ТОГДА РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаФ1,
		|	ВЫБОР
		|		КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет
		|			ТОГДА РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаРегл,
		|	НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ) КАК ДатаЗакрытияДолга,
		|	РасчетыСПокупателямиПоДокументамОстатки.Контрагент КАК Контрагент,
		|	РасчетыСПокупателямиПоДокументамОстатки.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РасчетыСПокупателямиПоДокументамОстатки.ДоговорВзаиморасчетов.НеПроверятьПросроченнуюЗадолженность КАК НеКонтролировать,
		|	РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Сделка,
		|	РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет КАК РегламентированныйУчет
		|ПОМЕСТИТЬ вДолги
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(
		|			,
		|			Контрагент В (&Контрагент)
		|				И ДоговорВзаиморасчетов.Подразделение = &Подразделение
		|				И ДоговорВзаиморасчетов.Организация В (&Организация)) КАК РасчетыСПокупателямиПоДокументамОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вДолги.СуммаУпрОстаток КАК СуммаУпрОстаток,
		|	вДолги.СуммаФ1 КАК СуммаФ1,
		|	вДолги.СуммаРегл КАК СуммаРегл,
		|	вДолги.ДатаЗакрытияДолга КАК ДатаЗакрытияДолга,
		|	вДолги.Контрагент КАК Контрагент,
		|	вДолги.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	вДолги.НеКонтролировать КАК НеКонтролировать,
		|	ЕСТЬNULL(СрокиОплатПоДоговорамСрезПоследних.Дней, 0) КАК СрокОплатыЗадолженностиФ1,
		|	ЕСТЬNULL(СрокиОплатПоДоговорамСрезПоследних.Дней2, 0) КАК СрокОплатыЗадолженностиФ2,
		|	вДолги.Сделка КАК Сделка,
		|	вДолги.РегламентированныйУчет КАК РегламентированныйУчет
		|ПОМЕСТИТЬ вДолгиСрок
		|ИЗ
		|	вДолги КАК вДолги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СрокиОплатПоДоговорам.СрезПоследних(&ДатаЗапроса, ) КАК СрокиОплатПоДоговорамСрезПоследних
		|		ПО вДолги.ДоговорВзаиморасчетов = СрокиОплатПоДоговорамСрезПоследних.ДоговорКонтрагента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вДолгиСрок.СуммаУпрОстаток КАК СуммаУпрОстаток,
		|	вДолгиСрок.СуммаФ1 КАК СуммаФ1,
		|	вДолгиСрок.СуммаРегл КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА вДолгиСрок.РегламентированныйУчет
		|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(вДолгиСрок.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, вДолгиСрок.СрокОплатыЗадолженностиФ1 + 1)
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(вДолгиСрок.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, вДолгиСрок.СрокОплатыЗадолженностиФ2 + 1)
		|	КОНЕЦ КАК ДатаСделки,
		|	вДолгиСрок.ДатаЗакрытияДолга КАК ДатаЗакрытияДолга,
		|	вДолгиСрок.Контрагент КАК Контрагент,
		|	вДолгиСрок.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	вДолгиСрок.НеКонтролировать КАК НеКонтролировать
		|ПОМЕСТИТЬ вДолгиИтог
		|ИЗ
		|	вДолгиСрок КАК вДолгиСрок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.КодКонтрагент КАК КодКонтрагент,
		|	ВложенныйЗапрос.КодДоговор КАК КодДоговор,
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаФ1) КАК СуммаФ1,
		|	СУММА(ВложенныйЗапрос.СуммаДолга) КАК СуммаДолга,
		|	СУММА(ВложенныйЗапрос.СуммаДолгаФ1) КАК СуммаДолгаФ1
		|ИЗ
		|	(ВЫБРАТЬ
		|		вДолгиИтог.Контрагент.Код КАК КодКонтрагент,
		|		вДолгиИтог.ДоговорВзаиморасчетов.Код КАК КодДоговор,
		|		вДолгиИтог.СуммаУпрОстаток КАК Сумма,
		|		вДолгиИтог.СуммаФ1 КАК СуммаФ1,
		|		ВЫБОР
		|			КОГДА вДолгиИтог.НеКонтролировать
		|				ТОГДА 0
		|			ИНАЧЕ ВЫБОР
		|					КОГДА СУММА(ВЫБОР
		|								КОГДА вДолгиИтог.ДатаСделки <= вДолгиИтог.ДатаЗакрытияДолга
		|									ТОГДА вДолгиИтог.СуммаУпрОстаток
		|								ИНАЧЕ 0
		|							КОНЕЦ) < 0
		|						ТОГДА 0
		|					ИНАЧЕ СУММА(ВЫБОР
		|								КОГДА вДолгиИтог.ДатаСделки <= вДолгиИтог.ДатаЗакрытияДолга
		|									ТОГДА вДолгиИтог.СуммаУпрОстаток
		|								ИНАЧЕ 0
		|							КОНЕЦ)
		|				КОНЕЦ
		|		КОНЕЦ КАК СуммаДолга,
		|		ВЫБОР
		|			КОГДА вДолгиИтог.НеКонтролировать
		|				ТОГДА 0
		|			ИНАЧЕ ВЫБОР
		|					КОГДА СУММА(ВЫБОР
		|								КОГДА вДолгиИтог.ДатаСделки <= вДолгиИтог.ДатаЗакрытияДолга
		|									ТОГДА вДолгиИтог.СуммаРегл
		|								ИНАЧЕ 0
		|							КОНЕЦ) < 0
		|						ТОГДА 0
		|					ИНАЧЕ СУММА(ВЫБОР
		|								КОГДА вДолгиИтог.ДатаСделки <= вДолгиИтог.ДатаЗакрытияДолга
		|									ТОГДА вДолгиИтог.СуммаРегл
		|								ИНАЧЕ 0
		|							КОНЕЦ)
		|				КОНЕЦ
		|		КОНЕЦ КАК СуммаДолгаФ1
		|	ИЗ
		|		вДолгиИтог КАК вДолгиИтог
		|	
		|	СГРУППИРОВАТЬ ПО
		|		вДолгиИтог.Контрагент.Код,
		|		вДолгиИтог.ДоговорВзаиморасчетов.Код,
		|		вДолгиИтог.СуммаУпрОстаток,
		|		вДолгиИтог.СуммаФ1,
		|		вДолгиИтог.НеКонтролировать) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.КодКонтрагент,
		|	ВложенныйЗапрос.КодДоговор";
		
		Запрос.УстановитьПараметр("ДатаЗапроса",КонецДня(ТекущаяДата()));
		Запрос.УстановитьПараметр("Контрагент",НастройкиАгента.Контрагенты.ВыгрузитьКолонку("Контрагент"));
		Запрос.УстановитьПараметр("Подразделение",НастройкиАгента.Подразделение);
		
		МассивОрганизация = Новый Массив;
		МассивОрганизация.Добавить(НастройкиАгента.Организация);
		МассивОрганизация.Добавить(Справочники.Организации.НайтиПоКоду("D109")); //Аттика
		
		Запрос.УстановитьПараметр("Организация",МассивОрганизация);
		
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта(); 
		
		ЗаписьJSON.ЗаписатьИмяСвойства("ОстаткиВзаиморасчетов");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодКонтрагент");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодКонтрагент));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодДоговор");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодДоговор));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Сумма");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Сумма);
			
			ЗаписьJSON.ЗаписатьИмяСвойства("СуммаДолга");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.СуммаДолга);
			
			ЗаписьJSON.ЗаписатьИмяСвойства("СуммаФ1");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.СуммаФ1);
			
			ЗаписьJSON.ЗаписатьИмяСвойства("СуммаДолгаФ1");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.СуммаДолгаФ1);
			
			
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	ИначеЕсли Параметры = "ПредварительноеПоступление" Тогда	
		
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта(); 
		
		ЗаписьJSON.ЗаписатьИмяСвойства("ПредварительноеПоступление");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	ИначеЕсли Параметры = "ПереводЗаказов" Тогда	
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_ПереводЗаказовФМУ.ID КАК ID,
		|	ТК_ПереводЗаказовФМУ.Количество КАК Количество,
		|	ТК_ПереводЗаказовФМУ.ДатаЗаказа КАК ДатаЗаказа,
		|	ТК_ПереводЗаказовФМУ.ТочкаДоставки.Код КАК КодТочкаДоставки,
		|	ТК_ПереводЗаказовФМУ.ТочкаДоставки.Владелец.Код КАК КодКонтрагент,
		|	ТК_ПереводЗаказовФМУ.Номенклатура.Артикул КАК КодТовара
		|ИЗ
		|	РегистрСведений.ТК_ПереводЗаказовФМУ КАК ТК_ПереводЗаказовФМУ
		|ГДЕ
		|	ТК_ПереводЗаказовФМУ.ТочкаДоставки.Менеджер = &Менеджер";
		
		Запрос.УстановитьПараметр("Менеджер", НастройкиАгента.Менеджер);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта(); 
		ЗаписьJSON.ЗаписатьИмяСвойства("ПереводЗаказов");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ID");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.ID);
			
			ЗаписьJSON.ЗаписатьИмяСвойства("Количество");
			ЗаписьJSON.ЗаписатьЗначение(ВыборкаДетальныеЗаписи.Количество);
			
			ЗаписьJSON.ЗаписатьИмяСвойства("ДатаЗаказа");
			ЗаписьJSON.ЗаписатьЗначение(ЗаписатьДатуJSON(ВыборкаДетальныеЗаписи.ДатаЗаказа,ФорматДатыJSON.ISO,ВариантЗаписиДатыJSON.ЛокальнаяДата));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодТочкаДоставки");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодТочкаДоставки));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодКонтрагент");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодКонтрагент));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодТовара");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(ВыборкаДетальныеЗаписи.КодТовара));
			
			
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
			
		КонецЦикла;
		
		
		
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
		
		
		
	КонецЕсли;	
	
	СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	
	Возврат СтрокаДляОтвета;
	
	
КонецФункции

Функция ПринятьЗаказы(НастройкиАгента,Запрос) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	
	Попытка
		ПотокДанных = Запрос.ПолучитьТелоКакПоток();
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.ОткрытьПоток(ПотокДанных,КодировкаТекста.UTF8);
		Попытка
			МассивДанных = ПрочитатьJSON(ЧтениеJSON);
		Исключение
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			ЗаписьJSON.ЗаписатьИмяСвойства("info");
			ЗаписьJSON.ЗаписатьЗначение(ОписаниеОшибки());
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
			СтрокаДляОтвета = ЗаписьJSON.Закрыть();
			
			Возврат СтрокаДляОтвета;
			
		КонецПопытки;
		
		СтруктураНастроекАгента = ПолучитьСтруктуруАгента(НастройкиАгента);
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("РезультатЗагрузки");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		ТаблицаАртикулов = Новый ТаблицаЗначений;
		ТаблицаАртикулов.Колонки.Добавить("Артикул");
		
		
		Для Каждого  Документ Из МассивДанных Цикл 
			СтруктураДокумента = ПолучитьСтруктуруДокумента(Документ,СтруктураНастроекАгента);
			
			ДокументыЗаказа = СформироватьДокументЗаказа(СтруктураДокумента);
			
			Если ДокументыЗаказа.ДокументКорректен = 1 Тогда
				
				
				СтркуктураРезультат = ЗарегистрироватьДокумент(ДокументыЗаказа,СтруктураНастроекАгента.МассивСкладов);
				Если СтркуктураРезультат.ДокументКорректен = 1 Тогда
					ЗаписатьРезультатРегистрацииДокумента(СтркуктураРезультат,ЗаписьJSON);
				Иначе
					ЗаписьJSON.ЗаписатьНачалоОбъекта();
					
					ЗаписьJSON.ЗаписатьИмяСвойства("info");
					ЗаписьJSON.ЗаписатьЗначение(СтркуктураРезультат.info);
					ЗаписьJSON.ЗаписатьИмяСвойства("UUID");
					ЗаписьJSON.ЗаписатьЗначение(Документ.UUID);
					
					ЗаписьJSON.ЗаписатьИмяСвойства("stat");
					ЗаписьJSON.ЗаписатьЗначение(1);
					
					
					ЗаписьJSON.ЗаписатьКонецОбъекта();
					
				КонецЕсли;
			Иначе
				ЗаписьJSON.ЗаписатьНачалоОбъекта();
				
				ЗаписьJSON.ЗаписатьИмяСвойства("info");
				ЗаписьJSON.ЗаписатьЗначение(ДокументыЗаказа.info);
				ЗаписьJSON.ЗаписатьИмяСвойства("UUID");
				ЗаписьJSON.ЗаписатьЗначение(Документ.UUID);
				
				
				ЗаписьJSON.ЗаписатьИмяСвойства("stat");
				ЗаписьJSON.ЗаписатьЗначение(1);
				
				
				
				ЗаписьJSON.ЗаписатьКонецОбъекта();
				
				
			КонецЕсли;
			ДополнитьТаблицуАртикулов(Документ.Строки,ТаблицаАртикулов)	
		КонецЦикла;
		
		ЗаписьJSON.ЗаписатьКонецМассива();
		
		Параметры = Новый Структура("СкладКомпании,ТаблицаАртикулов",СтруктураНастроекАгента.МассивСкладов,ТаблицаАртикулов);
		ПолучитьТекущиеОстаткиТоваров(Параметры,ЗаписьJSON);
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
		
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
	Исключение
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение(ОписаниеОшибки());
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
		СтрокаДляОтвета = ЗаписьJSON.Закрыть();
		
	КонецПопытки;
	
	
	Возврат СтрокаДляОтвета;
	
	
КонецФункции


Функция ПреобразоватьВДату(date)
	
	стрДата = СтрЗаменить(date,"-","");
	стрДата = СтрЗаменить(стрДата,"T","");
	стрДата = СтрЗаменить(стрДата,":","");
	Возврат Дата(стрДата);
КонецФункции

Функция СформироватьДокументЗаказа(СтруктураДокумента)
	Рез = Новый Структура;
	Рез.Вставить("ДокументКорректен",1);
	Рез.Вставить("info","");
	Рез.Вставить("GuidСистемыСоздания",СтруктураДокумента.GuidСистемыСоздания);
	
	Если СтруктураДокумента.ДокументКорректен = 0 Тогда
		Рез.ДокументКорректен = 0;
		Рез.info = СтруктураДокумента.info;
		Возврат Рез;
	КонецЕсли;
	
	
	
	текстОшибки ="";
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДокументов = СтруктураДокумента.МассивДокументов;
	МассивЗаказ = Новый Массив;
	
	НачатьТранзакцию();
	
	Попытка
		Для Каждого тДокумент Из МассивДокументов Цикл 
			
			
			
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ЗаказПокупателя");
			ЭлементБлокировки.УстановитьЗначение("Организация",тДокумент.Организация);
			ЭлементБлокировки.УстановитьЗначение("GuidСистемыСоздания",СтруктураДокумента.GuidСистемыСоздания);
			ЭлементБлокировки.УстановитьЗначение("ПортативноеУстройствоВвода",тДокумент.Документ.ПортативноеУстройствоВвода);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			ДокументСсылка = Документы.ЗаказПокупателя.НайтиЗаказАгента(тДокумент.Организация,тДокумент.Документ.ПортативноеУстройствоВвода,СтруктураДокумента.GuidСистемыСоздания);
			//ДокументСсылка = Документы.ЗаказПокупателя.ПустаяСсылка();
			Если НЕ ДокументСсылка.Пустая() Тогда
				текстОшибки = текстОшибки + "Ошибка создания документа, возможен дубль!"+Символы.ПС; 
				Продолжить;
			КонецЕсли;
			
			
			
			СтруктураДокумента = тДокумент.Документ;
			
			ДокументОбъект = Документы.ЗаказПокупателя.СоздатьДокумент();
			
			ДокументОбъект.ДатаСоздания = ТекущаяДатаСеанса();
			
			ДокументОбъект.ХозОперация = Справочники.ХозОперации.ЗаказПокупателя;
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект,СтруктураДокумента,,"Товары");	
			
			Для Каждого СтрокаТовары Из СтруктураДокумента.Товары Цикл 
				НоваяСтрока =  ДокументОбъект.Товары.Добавить();
				НоваяСтрока.Номенклатура = СтрокаТовары.Номенклатура;
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТовары,"ЕдиницаИзмерения,Количество,КоличествоБазовое,Коэффициент");
			КонецЦикла;
			тзТовары = ДокументОбъект.Товары.Выгрузить();
			тзТовары.Колонки.Добавить("ХарактеристикиИспользуются");
			тзТовары.Колонки.Добавить("ГруппаЦенообразования");
			тзТовары.Колонки.Добавить("ТипЦен");
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",ДокументОбъект.Организация,ДокументОбъект.ТорговаяПлощадка,"Реализация"));
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
			СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
			СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(ДокументОбъект));
			СтруктураПолейДляЗаполненияЦен = Новый Структура;	
			
			СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокументОбъект));
			
			СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);
			СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);
			
			ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(тзТовары,СтруктураДействий,Неопределено);
			
			ДокументОбъект.Товары.Загрузить(тзТовары);
			
			ОбработкаТабличнойЧастиСервер.СортироватьТЧ(ДокументОбъект.Товары);
			
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
			
			Если Не ДокументОбъект.ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
			КонецЕсли;
			
			
			
			Если ДокументОбъект.ПроверитьЗаполнение()Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный);
				СтруктураЗаказ = Новый Структура("Организация,ДокументЗаказа,СтруктураОбработкиТЧ",ДокументОбъект.Организация,ДокументОбъект.Ссылка,СтруктураДействий);
				МассивЗаказ.Добавить(СтруктураЗаказ);
				
				Если СтруктураДокумента.Борт Тогда
					ДопСвойстваДокумента = Новый Массив;
					ДопСвойствоБорт = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","Борт");
					Если НЕ ДопСвойствоБорт.Пустая() Тогда
						ДопСвойстваДокумента.Добавить(Новый Структура("Свойство, Значение", ДопСвойствоБорт, Истина));
						УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ДокументОбъект.Ссылка, ДопСвойстваДокумента);
					КонецЕсли;
				КонецЕсли;
				
				
				
				
				Если НЕ тДокумент.Документ.Перевод_ID = Неопределено Тогда
					ТекДатаПеревод = ТекущаяДатаСеанса();
					Для Каждого СтрокаТовары Из СтруктураДокумента.Товары Цикл 
						
						Если СтрокаТовары.Перевод Тогда
							НаборЗаПисейПеревод = РегистрыСведений.ТК_ПереводЗаказов.СоздатьНаборЗаписей();
							НаборЗаПисейПеревод.Отбор.ID.Установить(тДокумент.Документ.Перевод_ID);
							НаборЗаПисейПеревод.Отбор.Номенклатура.Установить(СтрокаТовары.Номенклатура);
							НаборЗаПисейПеревод.Прочитать();
							Если НаборЗаПисейПеревод.Количество()>0 Тогда
								стрПеревод = НаборЗаПисейПеревод[0];
								
								НаборЗаписейПереводЖурнал = РегистрыСведений.ТК_ПереводЗаказовЖурнал.СоздатьНаборЗаписей();
								НаборЗаписейПереводЖурнал.Отбор.ID.Установить(стрПеревод.ID);
								НаборЗаписейПереводЖурнал.Отбор.Номенклатура.Установить(СтрокаТовары.Номенклатура);
								ЗаписьПереводЖурнал = НаборЗаписейПереводЖурнал.Добавить();
								ЗаписьПереводЖурнал.ID = стрПеревод.ID;
								ЗаписьПереводЖурнал.Номенклатура = СтрокаТовары.Номенклатура;
								ЗаписьПереводЖурнал.Поставщик = стрПеревод.Поставщик;
								ЗаписьПереводЖурнал.КоличествоПеревод = стрПеревод.Количество;
								ЗаписьПереводЖурнал.КоличествоЗаказано = СтрокаТовары.Количество;
								ЗаписьПереводЖурнал.ДатаЗаписи = ТекДатаПеревод;
								ЗаписьПереводЖурнал.ДатаЗаказа = стрПеревод.ДатаЗаказа;
								ЗаписьПереводЖурнал.ДатаЗагрузки = стрПеревод.ДатаЗагрузки;
								ЗаписьПереводЖурнал.ЗаказПокупателя = ДокументОбъект.Ссылка;
								
								НаборЗаписейПереводЖурнал.Записать();
								
								НаборЗаПисейПеревод.Очистить();
								НаборЗаПисейПеревод.Записать();
								
							КонецЕсли;
							
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
				
				
				
				
				
			Иначе
				Если ДокументОбъект.ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
					текстОшибки = текстОшибки + ДокументОбъект.ДополнительныеСвойства.РезультатОбработки.ПолучитьТекст()+Символы.ПС;	
				Иначе
					текстОшибки = текстОшибки + "Ошибка проверки корректности документа"+Символы.ПС;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПустаяСтрока(текстОшибки) Тогда
			ЗафиксироватьТранзакцию();
			Рез.Вставить("ДокументыЗаказа",МассивЗаказ);
		Иначе
			ОтменитьТранзакцию();
			Рез.info = текстОшибки;
			рез.ДокументКорректен = 0;
		КонецЕсли;
		
		
	Исключение
		
		ОтменитьТранзакцию();
		текстОшибки = текстОшибки + ОписаниеОшибки();
		Рез.info = текстОшибки;
		рез.ДокументКорректен = 0;
		
	КонецПопытки;
	
	
	
	Возврат Рез;
	
КонецФункции

Функция ПолучитьСтруктуруДокумента(Документ,СтруктураНастроекАгента)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВсеОК = Истина;
	тОшибки = "";
	
	
	//найдем контрагента
	Запрос = Новый Запрос;
	Запрос.Текст =	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Код = &Код
	|	И Контрагенты.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Код",СокрЛП(Документ.Контрагент));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ВсеОК = Ложь;
		тОшибки = тОшибки + "Не найден контрагент по ОКПО "+СокрЛП(Документ.Контрагент)+Символы.ПС;
		КонтрагентЗаказа = Справочники.Контрагенты.ПустаяСсылка();
		
	Иначе	
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КонтрагентЗаказа = Выборка.Ссылка;
		
	КонецЕсли;
	///
	
	//найдем договор
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыВзаиморасчетов.Ссылка,
	|	ДоговорыВзаиморасчетов.ВалютаВзаиморасчетов КАК ВалютаДокумента
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыВзаиморасчетов
	|ГДЕ
	|	ДоговорыВзаиморасчетов.Контрагент = &Контрагент
	|	И ДоговорыВзаиморасчетов.Код = &Код";
	Запрос.УстановитьПараметр("Контрагент",КонтрагентЗаказа);	
	Запрос.УстановитьПараметр("Код",СокрЛП(Документ.Договор));	
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ВсеОК = Ложь;
		тОшибки = тОшибки + "Не найден договор контрагента: "+ Строка(КонтрагентЗаказа) + " по коду: "+СокрЛП(Документ.Договор)+Символы.ПС;
		ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	Иначе	
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДоговорКонтрагента = Выборка.Ссылка;
	КонецЕсли;
	///
	
	//Найдем адрес доставки
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТочкиДоставки.Ссылка
	|ИЗ
	|	Справочник.ТочкиДоставки КАК ТочкиДоставки
	|ГДЕ
	|	ТочкиДоставки.Владелец = &Контрагент
	|	И ТочкиДоставки.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", Документ.АдресДоставки);
	Запрос.УстановитьПараметр("Контрагент", КонтрагентЗаказа);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ВсеОК = Ложь;
		тОшибки = тОшибки + "Не найден адрес доставки контрагента: "+ Строка(КонтрагентЗаказа) + " по коду: "+СокрЛП(Документ.АдресДоставки)+Символы.ПС;
		АдресДоставки = Справочники.ТочкиДоставки.ПустаяСсылка();
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		АдресДоставки = Выборка.Ссылка;
	КонецЕсли;
	
	Перевод_ID = Неопределено;
	Если Документ.Свойство("Перевод_ID") Тогда
		Перевод_ID = Документ.Перевод_ID;
	КонецЕсли;
	
	Борт = Ложь;
	Если Документ.Свойство("Борт") Тогда
		Борт = Документ.Борт;
	КонецЕсли;
	
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Код");
	ТаблицаТоваров.Колонки.Добавить("Упаковка");
	ТаблицаТоваров.Колонки.Добавить("Номенклатура");
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаТоваров.Колонки.Добавить("Количество");
	ТаблицаТоваров.Колонки.Добавить("Перевод",Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("КоличествоБазовое");
	ТаблицаТоваров.Колонки.Добавить("Коэффициент");
	
	Для Каждого Стр Из Документ.Строки Цикл 
		НоваяСтрока =  ТаблицаТоваров.Добавить();
		НоваяСтрока.Код = СокрЛП(Стр.Номенклатура);	
		НоваяСтрока.Упаковка = Стр.Упаковка;	
		НоваяСтрока.Коэффициент = Стр.Коэффициент;	
		НоваяСтрока.Количество = Стр.Количество;	
		НоваяСтрока.КоличествоБазовое = Стр.Количество*Стр.Коэффициент;
		Если Стр.Свойство("Перевод") Тогда
			НоваяСтрока.Перевод = Стр.Перевод; 
		КонецЕсли;
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Номенклатура,
	|	спрЕдиницыИзмерения.Ссылка КАК Еденица,
	|	спрНоменклатура.Артикул КАК Артикул,
	|	спрЕдиницыИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕденицаКод,
	|	спрЕдиницыИзмерения.Коэффициент КАК Коэффициент
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК спрЕдиницыИзмерения
	|		ПО спрНоменклатура.Ссылка = спрЕдиницыИзмерения.Владелец
	|ГДЕ
	|	спрНоменклатура.Артикул В(&МассивАртикулов)";
	
	Запрос.УстановитьПараметр("МассивАртикулов", ТаблицаТоваров.ВыгрузитьКолонку("Код"));
	
	Таблица_Товаров = Новый ТаблицаЗначений;
	Таблица_Товаров.Колонки.Добавить("Номенклатура");
	Таблица_Товаров.Колонки.Добавить("Еденица");
	Таблица_Товаров.Колонки.Добавить("Артикул");
	Таблица_Товаров.Колонки.Добавить("ЕденицаКод");
	Таблица_Товаров.Колонки.Добавить("Коэффициент",ОбщегоНазначения.ОписаниеТипаЧисло(10,3));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		стр = Таблица_Товаров.Добавить();
		ЗаполнитьЗначенияСвойств(Стр,Выборка,,"ЕденицаКод,Артикул");
		стр.Артикул = СокрЛП(Выборка.Артикул);
		стр.ЕденицаКод = СокрЛП(Выборка.ЕденицаКод);
	КонецЦикла;
	
	
	
	Для Каждого Стр Из ТаблицаТоваров Цикл 
		//нСтроки = 	Таблица_Товаров.НайтиСтроки(Новый Структура("Артикул,ЕденицаКод,Коэффициент",СокрЛП(Стр.Код),СокрЛП(Стр.Упаковка),Число(Стр.Коэффициент)));
		нСтроки = 	Таблица_Товаров.НайтиСтроки(Новый Структура("Артикул,Коэффициент",СокрЛП(Стр.Код),Число(Стр.Коэффициент)));
		
		Если нСтроки.Количество() = 0 Тогда
			ВсеОК = Ложь;
			тОшибки = тОшибки + "Не определен товар по коду: " + Стр.Код+" "+ТипЗнч(Стр.Код) +" упаковке: "+ Стр.Упаковка +" "+ТипЗнч(Стр.Упаковка) + " коф: "+ Стр.Коэффициент +" "+ТипЗнч(Стр.Коэффициент)+" Товаров "+Таблица_Товаров.Количество() + Символы.ПС;
			Стр.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
			Стр.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
		Иначе
			нСтрока = нСтроки[0];	
			Стр.Номенклатура =  нСтрока.Номенклатура;
			Стр.ЕдиницаИзмерения =  нСтрока.Еденица;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		ВсеОК = Ложь;
		тОшибки =  тОшибки+"Нет товаров для заказа"+Символы.ПС;
	КонецЕсли;
	
	ТаблицаТоваров.Колонки.Добавить("Организация");
	
	ОрагнизацияТП = Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(СтруктураНастроекАгента.ТорговаяПлощадка,ТекущаяДатаСеанса());
	
	Если СтруктураНастроекАгента.ТорговаяПлощадка.РазделятьТоварыПоОрганизациям Тогда
		
		МассивТоваров = ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура");
		СписокТоварОрганизация = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(ОрагнизацияТП,МассивТоваров,СтруктураНастроекАгента.ТорговаяПлощадка);
		
		Для Каждого стТовар Из ТаблицаТоваров Цикл 
			стТовар.Организация = СписокТоварОрганизация.Получить(стТовар.Номенклатура).Организация;
		КонецЦикла;
	Иначе
		ТаблицаТоваров.ЗаполнитьЗначения(ОрагнизацияТП,"Организация")	
	КонецЕсли;
	
	ТаблицаОрганизаций = ТаблицаТоваров.Скопировать(,"Организация");
	ТаблицаОрганизаций.Свернуть("Организация");
	
	СтруктураДокументВозврат = Новый Структура;
	СтруктураДокументВозврат.Вставить("ДокументКорректен",1);
	
	МассивВозврат = Новый Массив;
	
	
	Для Каждого стОрганизация Из ТаблицаОрганизаций Цикл 
		
		СтруктураДокумента = Новый Структура;
		СтруктураДокумента.Вставить("ДокументКорректен",1);
		СтруктураДокумента.Вставить("ПортативноеУстройствоВвода",СтруктураНастроекАгента.ПортативноеУстройствоВвода);
		СтруктураДокумента.Вставить("РегламентированныйУчет",Документ.Учет);          
		СтруктураДокумента.Вставить("Дата",ПреобразоватьВДату(Документ.Дата));
		СтруктураДокумента.Вставить("СрокПоставки",ПреобразоватьВДату(Документ.ДатаОтгрузки));
		Попытка
			СтруктураДокумента.Вставить("Комментарий",Документ.Комментарий);
		Исключение
		КонецПопытки;
		СтруктураДокумента.Вставить("GuidСистемыСоздания",Документ.UUID);
		СтруктураДокумента.Вставить("Организация",стОрганизация.Организация);
		СтруктураДокумента.Вставить("ПодразделениеКомпании",СтруктураНастроекАгента.ПодразделениеКомпании);
		СтруктураДокумента.Вставить("СкладКомпании",СтруктураНастроекАгента.СкладКомпании);
		СтруктураДокумента.Вставить("ТорговаяПлощадка",СтруктураНастроекАгента.ТорговаяПлощадка);
		СтруктураДокумента.Вставить("Менеджер",СтруктураНастроекАгента.Менеджер);
		СтруктураДокумента.Вставить("Автор",СтруктураНастроекАгента.Автор);
		СтруктураДокумента.Вставить("НЕРезервировать",СтруктураНастроекАгента.НЕРезервировать);
		СтруктураДокумента.Вставить("ТочкаДоставки",АдресДоставки);
		СтруктураДокумента.Вставить("Контрагент",КонтрагентЗаказа);
		СтруктураДокумента.Вставить("Перевод_ID",Перевод_ID);
		СтруктураДокумента.Вставить("Борт",Борт);
		
		СтруктураДокумента.Вставить("КурсДокумента",1);
		Если ЗначениеЗаполнено(ДоговорКонтрагента)Тогда
			Если ДоговорКонтрагента.Организация = стОрганизация.Организация Тогда
				СтруктураДокумента.Вставить("ДоговорВзаиморасчетов",ДоговорКонтрагента);
				СтруктураДокумента.Вставить("ВалютаДокумента",ДоговорКонтрагента.ВалютаВзаиморасчетов);
				
			Иначе
				мВидДоговора = Новый Массив;
				мВидДоговора.Добавить(Справочники.ВидыДоговоров.Продажа);
				НовыйДоговор = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(
				КонтрагентЗаказа,стОрганизация.Организация,СтруктураДокумента.ПодразделениеКомпании,мВидДоговора,,СтруктураДокумента.Дата,,СтруктураНастроекАгента.ТорговаяПлощадка,ДоговорКонтрагента.Менеджер);
				
				Если ЗначениеЗаполнено(НовыйДоговор) Тогда
					СтруктураДокумента.Вставить("ДоговорВзаиморасчетов",НовыйДоговор);
					СтруктураДокумента.Вставить("ВалютаДокумента",НовыйДоговор.ВалютаВзаиморасчетов);
					
				Иначе
					
					тОшибки = тОшибки + "Не найден договор контрагента: "+ Строка(КонтрагентЗаказа) + " по организации: "+СокрЛП(стОрганизация.Организация)+Символы.ПС;
					
					ВсеОК = Ложь;
					
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			ВсеОК = Ложь;
		КонецЕсли;
		
		ТоварыОрганизации = ТаблицаТоваров.Скопировать(ТаблицаТоваров.НайтиСтроки(Новый Структура("Организация",стОрганизация.Организация)));
		СтруктураДокумента.Вставить("Товары",ТоварыОрганизации);
		
		
		Если ВсеОК Тогда	
			//Найдем заказ покупателя
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ЗаказПокупателя.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
			|ГДЕ
			|	ЗаказПокупателя.GuidСистемыСоздания = &GuidСистемыСоздания
			|	И ЗаказПокупателя.ПортативноеУстройствоВвода = &ПортативноеУстройствоВвода";
			
			Запрос.УстановитьПараметр("GuidСистемыСоздания", Документ.UUID);
			//Запрос.УстановитьПараметр("Организация", стОрганизация.Организация);
			Запрос.УстановитьПараметр("ПортативноеУстройствоВвода", СтруктураНастроекАгента.ПортативноеУстройствоВвода);
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				тЗаказ = Выборка.Ссылка;
				ВсеОК = Ложь;
				
				Если тЗаказ.ПометкаУдаления тогда
					тОшибки = тОшибки + "Выгружаемый заказ отменен на сервере"+Символы.ПС;
				ИначеЕсли тЗаказ.Проведен Тогда
					тОшибки =   тОшибки +"Выгружаемый заказ уже проведен на сервере"+Символы.ПС;
				Иначе
					тОшибки =   тОшибки +"Выгружаемый заказ был выгружен ранее"+Символы.ПС;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		
		МассивВозврат.Добавить(Новый Структура("Организация,Документ",стОрганизация.Организация,СтруктураДокумента));
		
	КонецЦикла;
	
	
	СтруктураДокументВозврат.Вставить("МассивДокументов",МассивВозврат);
	СтруктураДокументВозврат.Вставить("GuidСистемыСоздания",Документ.UUID);
	
	Если НЕ ВсеОК Тогда
		СтруктураДокументВозврат.Вставить("ДокументКорректен",0);
		СтруктураДокументВозврат.Вставить("info",тОшибки);
	КонецЕсли;
	
	Возврат СтруктураДокументВозврат;	
КонецФункции

Функция ЗарегистрироватьДокумент(СтруктураДокумента,СкладыРезерва)
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ДокументКорректен",1);
	СтруктураВозврата.Вставить("GuidСистемыСоздания",СтруктураДокумента.GuidСистемыСоздания);
	
	ТекстОшибки = "";
	МассивРезервов = Новый Массив; 
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого мЗаказ Из СтруктураДокумента.ДокументыЗаказа Цикл 
			
			ДокументСсылка	=   мЗаказ.ДокументЗаказа;
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Документ.ЗаказПокупателя");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ДокументСсылка);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПодчиненныеДокументы.Ссылка
			|ИЗ
			|	КритерийОтбора.СвязанныеДокументы(&Док) КАК ПодчиненныеДокументы
			|ГДЕ
			|	ПодчиненныеДокументы.Ссылка ССЫЛКА Документ.РезервированиеЗаказовПокупателя
			|	И ПодчиненныеДокументы.Ссылка.Проведен";
			
			Запрос.УстановитьПараметр("Док", ДокументСсылка);
			
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				
				ТекстОшибки = ТекстОшибки+ "Существует резерв по заказу организации "+Строка(мЗаказ.Организаци)+Символы.ПС;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			СтруктураВозврата.Вставить("ДокументКорректен",0);
			СтруктураВозврата.Вставить("info","Ошибки проверки резерва"+Символы.ПС+"Есть ошибки "+ТекстОшибки);
			
			ВызватьИсключение "Ошибки проверки резерва";
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбъединениеРезервированиеЗаказов") Тогда
			
			МассивЗаказ = Новый Массив;
			
			Для Каждого мЗаказ Из СтруктураДокумента.ДокументыЗаказа Цикл 
				МассивЗаказ.Добавить(мЗаказ.ДокументЗаказа);
			КонецЦикла;
			
			
			// РЕЗЕРВИРУЕМ
			ОбъектРезерв = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
			ОбъектРезерв.Заполнить(МассивЗаказ);
			ОбъектРезерв.Дата = ТекущаяДатаСеанса();
			ОбъектРезерв.УстановитьНовыйНомер();
			
			
			Для Каждого СкладРезерва из СкладыРезерва Цикл 
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваров");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
				ЭлементБлокировки.УстановитьЗначение("Склад",СкладРезерва);
				ЭлементБлокировки.ИсточникДанных  =  ОбъектРезерв.Товары;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура","Номенклатура");
				
				Блокировка.Заблокировать();
			КонецЦикла;
			
			Документы.РезервированиеЗаказовПокупателя.УдалитьСтрокиБезОстатка(ОбъектРезерв);
			Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ОбъектРезерв);
			Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(ОбъектРезерв);
			
			//Проверим квоты после заполнения характеристик, может быть еще уменьшено кол-во
			Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ОбъектРезерв);
			
			
			Если ОбъектРезерв.Товары.Количество() >0 Тогда
				ОбъектРезерв.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный); 
				МассивРезервов.Добавить(Новый Структура("Заказ,Резерв",мЗаказ.ДокументЗаказа,ОбъектРезерв.Ссылка));
			Иначе
				ТекстОшибки = ТекстОшибки + "нет товаров для резерва по организации "+Строка(ОбъектРезерв.Организация)+Символы.ПС;
			КонецЕсли;
			
			
			
		Иначе
			Для Каждого мЗаказ Из СтруктураДокумента.ДокументыЗаказа Цикл 
				
				// РЕЗЕРВИРУЕМ
				ОбъектРезерв = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
				ОбъектРезерв.Заполнить(мЗаказ.ДокументЗаказа);
				ОбъектРезерв.Дата = ТекущаяДатаСеанса();
				ОбъектРезерв.УстановитьНовыйНомер();
				
				Для Каждого СкладРезерва из СкладыРезерва Цикл 
					Блокировка = Новый БлокировкаДанных;
					ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваров");
					ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
					ЭлементБлокировки.УстановитьЗначение("Склад",СкладРезерва);
					ЭлементБлокировки.ИсточникДанных  =  ОбъектРезерв.Товары;
					ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура","Номенклатура");
					Блокировка.Заблокировать();
				КонецЦикла;
				
				Документы.РезервированиеЗаказовПокупателя.УдалитьСтрокиБезОстатка(ОбъектРезерв);
				Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ОбъектРезерв);
				Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(ОбъектРезерв);
				
				//Проверим квоты после заполнения характеристик, может быть еще уменьшено кол-во
				Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ОбъектРезерв);
				
				
				Если ОбъектРезерв.Товары.Количество() >0 Тогда
					ОбъектРезерв.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный); 
					МассивРезервов.Добавить(Новый Структура("Заказ,Резерв",мЗаказ.ДокументЗаказа,ОбъектРезерв.Ссылка));
				Иначе
					ТекстОшибки = ТекстОшибки + "нет товаров для резерва по организации "+Строка(ОбъектРезерв.Организация)+Символы.ПС;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если МассивРезервов.Количество() = 0 Тогда
			ОтменитьТранзакцию();
			СтруктураВозврата.Вставить("ДокументКорректен",0);
		Иначе
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ТекстОшибки+ОписаниеОшибки();
		СтруктураВозврата.Вставить("ДокументКорректен",0);
	КонецПопытки;
	
	
	
	Если СтруктураВозврата.ДокументКорректен = 1 Тогда
		тзРезервыПоТовару = Новый ТаблицаЗначений;
		тзРезервыПоТовару.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		тзРезервыПоТовару.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		тзРезервыПоТовару.Колонки.Добавить("ЕдиницаИзмерения",Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
		тзРезервыПоТовару.Колонки.Добавить("Коэффициент",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10,3)));
		тзРезервыПоТовару.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("Цена",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
		тзРезервыПоТовару.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
		тзРезервыПоТовару.Колонки.Добавить("КоличествоБазовое",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("СтавкаНДС",Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
		тзРезервыПоТовару.Колонки.Добавить("ЦенаБезНалогов",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("СуммаВсего",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("СуммаНДС",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("СуммаБезНалогов",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		тзРезервыПоТовару.Колонки.Добавить("ПроцентСкидки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
		тзРезервыПоТовару.Колонки.Добавить("СуммаСкидки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
		
		
		для Каждого мРезерв Из МассивРезервов Цикл 
			тзТовары   =  мРезерв.Резерв.Товары.Выгрузить();
			тзТовары.Колонки.Добавить("Цена",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			тзТовары.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
			тзТовары.Колонки.Добавить("КоличествоБазовое",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("СтавкаНДС",Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
			тзТовары.Колонки.Добавить("ЦенаБезНалогов",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("СуммаВсего",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("СуммаНДС",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("СуммаБезНалогов",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("ПроцентСкидки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
			тзТовары.Колонки.Добавить("СуммаСкидки",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
			тзТовары.Колонки.Добавить("ХарактеристикиИспользуются");
			тзТовары.Колонки.Добавить("ГруппаЦенообразования");
			тзТовары.Колонки.Добавить("ТипЦен");
			
			
			СтруктураДействий = Неопределено;
			Для Каждого смДокументыЗаказа Из СтруктураДокумента.ДокументыЗаказа Цикл 
				Если мРезерв.Заказ =  смДокументыЗаказа.ДокументЗаказа Тогда
					СтруктураДействий =  смДокументыЗаказа.СтруктураОбработкиТЧ;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			СтруктураДействий.Вставить("ЦеныБезХарактеристики",Ложь);
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(тзТовары,СтруктураДействий,Неопределено);
			
			
			для Каждого сттзТовары Из тзТовары Цикл 
				нстрРезервыТоваров = тзРезервыПоТовару.Добавить();
				ЗаполнитьЗначенияСвойств(нстрРезервыТоваров,сттзТовары);
			КонецЦикла;
			
			
		КонецЦикла;
		тзРезервыПоТовару.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,Коэффициент","Количество,Сумма");
		СтруктураВозврата.Вставить("РезервыТоваров",тзРезервыПоТовару);
		
	КонецЕсли;
	
	
	СтруктураВозврата.Вставить("info",ТекстОшибки);
	
	Возврат  СтруктураВозврата;
	
КонецФункции

Процедура ЗаписатьРезультатРегистрацииДокумента(СтркуктураРезультат,ЗаписьJSON)
	
	Если СтркуктураРезультат.Свойство("РезервыТоваров") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.ХарактеристикаНоменклатуры,
		|	ТаблицаТоваров.ЕдиницаИзмерения,
		|	ТаблицаТоваров.Коэффициент,
		|	ТаблицаТоваров.Количество,
		|	ТаблицаТоваров.Сумма
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура.Артикул КАК НоменклатураКодТК,
		|	ВТ_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_Товары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКодТК,
		|	ВТ_Товары.Коэффициент,
		|	ВТ_Товары.Количество,
		|	ВТ_Товары.Сумма
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";
		
		Запрос.УстановитьПараметр("ТаблицаТоваров",СтркуктураРезультат.РезервыТоваров);
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("UUID");
		ЗаписьJSON.ЗаписатьЗначение(СтркуктураРезультат.GuidСистемыСоздания);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("Товары");
		ЗаписьJSON.ЗаписатьНачалоМассива();
		
		Пока Выборка.Следующий() Цикл
			
			ЗаписьJSON.ЗаписатьНачалоОбъекта();
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодНоменклатуры");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.НоменклатураКодТК));
			ЗаписьJSON.ЗаписатьИмяСвойства("Характеристика");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.ХарактеристикаНоменклатуры));
			
			ЗаписьJSON.ЗаписатьИмяСвойства("КодЕденицы");
			ЗаписьJSON.ЗаписатьЗначение(СокрЛП(Выборка.ЕдиницаИзмеренияКодТК));
			ЗаписьJSON.ЗаписатьИмяСвойства("Коэффициент");
			ЗаписьJSON.ЗаписатьЗначение(Выборка.Коэффициент);
			ЗаписьJSON.ЗаписатьИмяСвойства("Количество");
			ЗаписьJSON.ЗаписатьЗначение(Выборка.Количество);
			ЗаписьJSON.ЗаписатьИмяСвойства("Сумма");
			ЗаписьJSON.ЗаписатьЗначение(Выборка.Сумма);
			
			ЗаписьJSON.ЗаписатьКонецОбъекта();
			
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецМассива();
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	Иначе
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("info");
		ЗаписьJSON.ЗаписатьЗначение(СтркуктураРезультат.info);
		ЗаписьJSON.ЗаписатьИмяСвойства("UUID");
		ЗаписьJSON.ЗаписатьЗначение(СтркуктураРезультат.GuidСистемыСоздания);
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьТаблицуАртикулов(СтрокиДокумента, ТаблицаАртикулов)
	
	Для Каждого Строка Из СтрокиДокумента Цикл 
		НоваяСтрока =    ТаблицаАртикулов.Добавить();
		НоваяСтрока.Артикул =  Строка.Номенклатура;
	КонецЦикла;
	
КонецПроцедуры




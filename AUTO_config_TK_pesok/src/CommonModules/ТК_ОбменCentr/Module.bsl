
#Область ОбменСБазойТК

Процедура ЗарегистрироватьИзмененияВУзлах(Объект, МассивУзлов, Замещение) Экспорт
	
	Если МассивУзлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Объект.Ссылка);
		
		
	
КонецПроцедуры

Функция ЗарегистрироватьИзменения()Экспорт
	
	
	УстановитьПривилегированныйРежим(Истина);
	ВсеОК = Истина;
	Попытка 
		Состав = Метаданные.ПланыОбмена.ОбменТК_Center.Состав;  
		
		Узел = ТК_ОбменCentrПовтИсп.ПолучитьУзелОбменаТК_Центр();
		
		Для Каждого ДокСостав Из Состав Цикл 
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДокИзменения.Ссылка КАК Ссылка
			|ИЗ
			|	Документ."+ДокСостав.Метаданные.Имя+".Изменения КАК ДокИзменения
			|ГДЕ
			|	ДокИзменения.Узел = &Узел
			|	И НЕ ДокИзменения.НомерСообщения ЕСТЬ NULL";
			
			Запрос.УстановитьПараметр("Узел", Узел);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ПланыОбмена.ЗарегистрироватьИзменения(Узел,ВыборкаДетальныеЗаписи.Ссылка);
			КонецЦикла;
			
		КонецЦикла;
		
	Исключение
		ВсеОК = Ложь;
	КонецПопытки;
	
	Возврат ВсеОК;
	
КонецФункции

Функция УдалитьРегистрацию() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	ВсеОК = Истина;
	Попытка 
		
		Узел = ТК_ОбменCentrПовтИсп.ПолучитьУзелОбменаТК_Центр();
		ПланыОбмена.УдалитьРегистрациюИзменений(Узел,1);
	Исключение
		ВсеОк = Ложь;
	КонецПопытки;
	
	Возврат ВсеОк;
	
КонецФункции


#КонецОбласти


#Область СозданиеМРЦ

Функция ОтправитьЗапросНаСозданиеМРЦ(Номенклатура, МРЦ, ДопПараметры) Экспорт 
	
	текДата = ТекущаяДата();	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;	
	
	Успешно = Истина;
	Сообщение = "";
	ГУИД_МРЦ = "";
	
	ВыводитьСообщение	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "ВыводитьСообщение", Истина);
	ОтправлятьНаПочту 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "ОтправлятьНаПочту", Истина);
	КлючОбработки		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "КлючОбработки", "Вызов процедуры Создание МРЦ");
	СоздаватьВБазеТК	= Ложь; //Vov41k 21.01.2022 //ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "СоздаватьВБазеТК", Истина);
	
	Артикул 			= Номенклатура.Артикул;
	Пользователь 		= Пользователи.ТекущийПользователь();	
	ИдентификаторОбработки = "Создание_МРЦ_ТК";
	регРезультатВыполнения = РегистрыСведений.РезультатыВыполненияОбработок;	

	СтруктураЗаписи = регРезультатВыполнения.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписи.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписи.КлючОбработки = КлючОбработки;
			
  	регРезультатВыполнения.ДобавитьКомментарий(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Параметры создания МРЦ:
                  |
                  |Номенклатура - [%1] %2;
                  |Характеристика - МРЦ %3;
                  |
                  |Пользователь - %4'; uk = 'Параметри створення МРЦ:
                  |
                  |Номенклатура - [%1] %2;
                  |Характеристика - МРЦ %3;
                  |
                  |Користувач - %4'"),
			Артикул,
			Номенклатура,
			Формат(МРЦ, "ЧДЦ=2"),
			Пользователь),
		СтруктураЗаписи);
	
	Попытка
		Если СоздаватьВБазеТК Тогда 			
			Определение = Новый WSОпределения("https://orders-1c.digma.ua/work_global_center/cwd2.1cws?wsdl",,,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));		
			WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			//WS.Пароль ="bynthghbpf";
			//WS.Пользователь="ObmenSSL";
			
			Ответ = WS.SetNewMRP(Строка(Номенклатура.УникальныйИдентификатор()), МРЦ);			
			
			Успешно = Ответ.Successful;
			Сообщение = Ответ.Message;
			ГУИД_МРЦ = Ответ.Guid_MRP;			
		КонецЕсли;
		
		Если Успешно Тогда 
			Ответ = СоздатьМРЦВТекущейБазе(Номенклатура, МРЦ, Артикул, ГУИД_МРЦ);
			
			Успешно = Ответ.Успешно;
			Сообщение = Ответ.Сообщение;
			ГУИД_МРЦ = Ответ.ГУИД_МРЦ;
		КонецЕсли;
		
			
		Если ПустаяСтрока(Сообщение) Тогда 
			Если Успешно Тогда 
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Для товара [%1] %2 создана характеристика МРЦ %3'; uk = 'Для товару [%1] %2 створена характеристика МРЦ %3'"),
								Артикул,
								Номенклатура,
								Формат(МРЦ, "ЧДЦ=2"));
			Иначе
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Ошибка создания МРЦ %1 для [%2] %3'; uk = 'Помилка створення МРЦ %1 для  [%2] %3'"),
								Формат(МРЦ, "ЧДЦ=2"),
								Артикул,
								Номенклатура);
				
			КонецЕсли;
		КонецЕсли;
	Исключение		
		
		Успешно = Ложь;
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка создания МРЦ %1 для [%2] %3.
                              |Описание ошибки: %4'; uk = 'Помилка створення МРЦ %1 для  [%2] %3.
                              |Опис помилки: %4'"),
						Формат(МРЦ, "ЧДЦ=2"),
						Артикул,
						Номенклатура,
						ОписаниеОшибки());
						
		регРезультатВыполнения.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписи, СтатусСообщения.ОченьВажное);						
	КонецПопытки;

	регРезультатВыполнения.ДобавитьКомментарий("Результат: " + Сообщение, СтруктураЗаписи);
		
	Если Успешно И ОтправлятьНаПочту Тогда 
		
		СписокПолучателей = Новый Соответствие;
		//СписокПолучателей.Вставить("Жиденко Татьяна Викторовна", "Tatiyana.Jidenko@digma.ua");
		//СписокПолучателей.Вставить("Волошина Галина Вікторівна", "Galina.Voloshina@digma.ua");
		СписокПолучателей.Вставить("Хилинская Наталья Александровна", "Nataliya.Hilinskaya@digma.ua");
		//СписокПолучателей.Вставить("Борисова Анна Владимировна", "Anna.Borisova@digma.ua");
		//СписокПолучателей.Вставить("Лавренко Юлия Олеговна", "Julia.Lavrenko@digma.ua");
		//СписокПолучателей.Вставить("Ружинская Галина Анатольевна", "galina.ruzhinskaya@digma.ua");
		//СписокПолучателей.Вставить("Набойченко Михаил Анатольевич", "Mihail.Naboychenko@digma.ua");
		//СписокПолучателей.Вставить("Миргород Владимир Михайлович", "vladimir.mirgorod@digma.ua");
		СписокПолучателей.Вставить("Грищенко Владимир Васильевич", "vladimir.grishenko@digma.ua");
		СписокПолучателей.Вставить("Костин Александр Валентинович", "aleksandr.kostin@digma.ua");
		СписокПолучателей.Вставить("Конова Марина Николаевна", "Marina.Konova@digma.ua");
		СписокПолучателей.Вставить("Шевченко Игорь Владимирович", "Igor.Shevchenko@digma.ua");
		
		ПредставлениеПолучателей = "";
		АдресаПолучателей = "";
		
		Для Каждого Стр Из СписокПолучателей Цикл 			
			ПредставлениеПолучателей = ПредставлениеПолучателей + Символы.ПС + Стр.Ключ + " <" + Стр.Значение + ">";
			АдресаПолучателей = ?(Не ПустаяСтрока(АдресаПолучателей), АдресаПолучателей + ", ", "") + Стр.Значение;
		КонецЦикла;
		
		ТемаПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"Новое МРЦ %1 для [%2] %3",
						Формат(МРЦ, "ЧДЦ=2"),
						Артикул, 
						Номенклатура);
						
		ТелоПисьма = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"%1 создано новое МРЦ %2.
	                      |Номенклатура - [%3] %4,
	                      |
	                      |Пользователь - %5",
						текДата, 
						Формат(МРЦ, "ЧДЦ=2"),
						Артикул,
						Номенклатура,
						Пользователь);
						
		ПараметрыПисьма = Новый Структура;
		ПараметрыПисьма.Вставить("Кому", АдресаПолучателей);
		ПараметрыПисьма.Вставить("Тема", ТемаПисьма);
		ПараметрыПисьма.Вставить("Тело", ТелоПисьма);
		//ПараметрыПисьма.Вставить("Вложения",МассивВложений);
		
		Попытка
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
			РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);	
			
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Отправлено эл. письмо получателям:
                     |%1",
					ПредставлениеПолучателей),
				СтруктураЗаписи);				
				
		Исключение
			регРезультатВыполнения.ДобавитьКомментарий(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Ошибка отправки эл. письма получателям:
                          |%1
                          |Описание ошибки:
                          |%2",
					ПредставлениеПолучателей,
					ОписаниеОшибки()), СтруктураЗаписи);
					
			регРезультатВыполнения.СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписи, СтатусСообщения.ОченьВажное);						
		КонецПопытки;
	КонецЕсли;	
	
	Если ВыводитьСообщение Тогда 
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
	КонецЕсли;
	
	регРезультатВыполнения.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписи, Успешно);	
	
	Результат.Вставить("Успешно", Успешно);	
	Результат.Вставить("Сообщение", Сообщение);
	
	Если Успешно Тогда 
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД_МРЦ));
		Результат.Вставить("ГУИД_МРЦ", ГУИД_МРЦ);
		Результат.Вставить("Характеристика", Характеристика);
	КонецЕсли;		
	
	Возврат Результат
КонецФункции

Функция СоздатьМРЦВТекущейБазе(Номенклатура, МРЦ, Артикул, ГУИД_МРЦ = Неопределено)
		
	Успешно = Истина;
	Сообщение = "";
	ПереданГУИД = ЗначениеЗаполнено(ГУИД_МРЦ);	
	
	Если ПереданГУИД Тогда 
		СсылкаМРЦ = Справочники.ХарактеристикиНоменклатуры.ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД_МРЦ));		
		Если ОбщегоНазначения.СсылкаСуществует(СсылкаМРЦ) Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка,
			               |	ХарактеристикиНоменклатуры.ПометкаУдаления КАК ПометкаУдаления,
			               |	ХарактеристикиНоменклатуры.Владелец КАК Владелец,
			               |	ХарактеристикиНоменклатуры.Наименование КАК Наименование,
			               |	ХарактеристикиНоменклатуры.ЗначениеМРЦ КАК ЗначениеМРЦ
			               |ИЗ
			               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
			               |ГДЕ
			               |	ХарактеристикиНоменклатуры.Ссылка = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", СсылкаМРЦ);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			//Проверка полученного МРЦ
			Если Выборка.Владелец <> Номенклатура 
				ИЛИ Выборка.ЗначениеМРЦ <> МРЦ Тогда 
				
				Успешно = Ложь;
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								"Ошибка! Характеристикане по указанному ГУИД <%1> не соответствует переданным параметрам (%1, МРЦ %2)",
								ГУИД_МРЦ,
								Номенклатура,
								МРЦ);
			Иначе
				Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Для товара [%1] %2 уже существует МРЦ %3",
					Артикул,
					Номенклатура,
					МРЦ);
					
			КонецЕсли;
							
			Возврат Новый Структура("Успешно,Сообщение,ГУИД_МРЦ", Успешно, Сообщение, ГУИД_МРЦ);				
		КонецЕсли;
	Иначе	
		ГУИД_МРЦ = "";			
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	               |ГДЕ
	               |	ХарактеристикиНоменклатуры.Владелец = &Номенклатура
	               |	И ХарактеристикиНоменклатуры.ЗначениеМРЦ = &МРЦ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХарактеристикиНоменклатуры.Владелец КАК Владелец,
	               |	МАКСИМУМ(ХарактеристикиНоменклатуры.Сортировка) КАК Сортировка
	               |ИЗ
	               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	               |ГДЕ
	               |	ХарактеристикиНоменклатуры.Владелец = &Номенклатура
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХарактеристикиНоменклатуры.Владелец";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("МРЦ", МРЦ);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезМРЦ = МассивРезультатов[0];
	ВыборкаСортировка = МассивРезультатов[1].Выбрать();
	
	Если РезМРЦ.Пустой() Тогда 
		Если ВыборкаСортировка.Следующий() Тогда 
			Сортировка = ВыборкаСортировка.Сортировка + 1;
		Иначе
			Сортировка = 1;
		КонецЕсли;
		
		Характеристика = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
		
		Если ПереданГУИД Тогда 
			Характеристика.УстановитьСсылкуНового(СсылкаМРЦ);
		КонецЕсли;
		
		Характеристика.Владелец = Номенклатура;
		Характеристика.ЗначениеМРЦ = МРЦ;
		Характеристика.Сортировка = Сортировка;
		Характеристика.Наименование = "МРЦ " + Формат(МРЦ, "ЧДЦ=2");
		
		Попытка
			Характеристика.Записать();
			Успешно = Истина;				
			ГУИД_МРЦ = Характеристика.Ссылка.УникальныйИдентификатор();
			
			Если НЕ Номенклатура.Родитель.Код ="777812  " Тогда
				ЦенообразованиеСервер.ПереоЦенкаМРЦ(Номенклатура, МРЦ, ГУИД_МРЦ);
			КонецЕсли;
			
		Исключение
			Успешно = Ложь;
			Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"Ошибка создания МРЦ %1 для товара [%2] %3
                            |Описание ошибки: %4",
							МРЦ,
							Артикул,
							Номенклатура,
							ОписаниеОшибки());

		КонецПопытки;
	Иначе
		Успешно = Ложь;
		Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"Для товара [%1] %2 уже существует МРЦ %3",
						Артикул,
						Номенклатура,
						МРЦ);
	КонецЕсли;
	
	
	Возврат Новый Структура("Успешно,Сообщение,ГУИД_МРЦ", Успешно, Сообщение, ГУИД_МРЦ);
	
КонецФункции

#КонецОбласти
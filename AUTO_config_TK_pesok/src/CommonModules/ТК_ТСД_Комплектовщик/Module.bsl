
Функция ЗаписатьОтветJSON(СтруктураВозврата)
	
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	НастройкиСериализации = Новый НастройкиСериализацииJSON;
	НастройкиСериализации.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ЗаписатьJSON(ЗаписьJSON, СтруктураВозврата,НастройкиСериализации);	
	СтрокаВозврата = ЗаписьJSON.Закрыть();	
	
	Возврат СтрокаВозврата;
	
КонецФункции

Функция ОбработатьGET(Запрос)Экспорт
	

	UUIDТорговаяПлощадка  = Запрос.ПараметрыURL["Market"];
	ПараметрЗапроса = Запрос.ПараметрыURL["param"];
	UUIDСотрудник = Запрос.ПараметрыЗапроса.Получить("uuidcom");

	ИД_Запроса =  Запрос.Заголовки.Получить("X-1C-Request-UID");

	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Результат",Ложь);
	СтруктураВозврата.Вставить("ОписаниеОшибки","");
	СтруктураВозврата.Вставить("Ответ");
	
	ТорговаяПлощадкаСсылка = ТК_ТСД_РаботаHTTP_Сервис.ПодразделениеКорректно(UUIDТорговаяПлощадка,СтруктураВозврата);
	
	Если ТорговаяПлощадкаСсылка = Неопределено Тогда
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если UUIDСотрудник = Неопределено Тогда
		СтруктураВозврата.ОписаниеОшибки = "Працівника не знайдено";
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);	
	Иначе
		МенеджерСборки = ТК_ТСД_РаботаHTTP_СервисПовтИсп.ПолучитьСотрудника(UUIDСотрудник);
		Если МенеджерСборки.Пустая() Тогда
			СтруктураВозврата.ОписаниеОшибки = "Працівника не знайдено";
			Возврат ЗаписатьОтветJSON(СтруктураВозврата);
		КонецЕсли;
	КонецЕсли;
	

	
	Если ПараметрЗапроса = "rezerv" Тогда
		ПолучитьКоличествоРезервов(СтруктураВозврата,ТорговаяПлощадкаСсылка,МенеджерСборки);
	ИначеЕсли ПараметрЗапроса = "rezervrab" Тогда
		ПолучитьСтруктурРезерва(СтруктураВозврата,ТорговаяПлощадкаСсылка,МенеджерСборки);
	ИначеЕсли ПараметрЗапроса = "rezervotmena" Тогда
		UUIDДокумент = Запрос.ПараметрыЗапроса.Получить("uuiddoc");
		ОтменитьСборкуРезерва(СтруктураВозврата,ТорговаяПлощадкаСсылка,UUIDДокумент,МенеджерСборки); 
	КонецЕсли;
	
	Попытка 
		СтрокаВозврата	= ЗаписатьОтветJSON(СтруктураВозврата);
	Исключение
		СтрокаВозврата = ОписаниеОшибки();
	КонецПопытки;

	
	Возврат СтрокаВозврата;
	

КонецФункции

Функция ОбработатьPOST(Запрос)Экспорт
	
	UUIDТорговаяПлощадка        = Запрос.ПараметрыURL["Market"];
	Param = Запрос.ПараметрыURL["param"];
	ИД_Запроса =  Запрос.Заголовки.Получить("X-1C-Request-UID");

	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Результат",Ложь);
	СтруктураВозврата.Вставить("ОписаниеОшибки","");
	СтруктураВозврата.Вставить("Ответ");
	
	ТорговаяПлощадкаСсылка = ТК_ТСД_РаботаHTTP_Сервис.ПодразделениеКорректно(UUIDТорговаяПлощадка,СтруктураВозврата);
	
	Если ТорговаяПлощадкаСсылка = Неопределено Тогда
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);
	КонецЕсли;

	ПотокДанных = Запрос.ПолучитьТелоКакПоток();
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьПоток(ПотокДанных,КодировкаТекста.UTF8);
	
	Попытка
		МассивДанных = ПрочитатьJSON(ЧтениеJSON,,"НачалоКонтроля",ФорматДатыJSON.ISO);
	Исключение
		СтруктураВозврата.ОписаниеОшибки ="Ошибка четения списка товаров "+ ОписаниеОшибки();
		СтруктураВозврата.Результат = Ложь;
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);
	КонецПопытки;

	
	УстановитьПривилегированныйРежим(Истина);
	МенеджерСборки = ТК_ТСД_РаботаHTTP_СервисПовтИсп.ПолучитьСотрудника(МассивДанных.UUID_Сотрудник);
	Если МенеджерСборки.Пустая() Тогда
		СтруктураВозврата.ОписаниеОшибки = "Працівника не знайдено";
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);
	КонецЕсли;


	
	СкладСборки = РегистрыСведений.СборщикиРезервов.ПолучитьСкладСборкиСотрудника(МенеджерСборки);
	Если СкладСборки.Пустая() Тогда
		СтруктураВозврата.ОписаниеОшибки = "У працівника не вказано склад збирання товару";
		Возврат ЗаписатьОтветJSON(СтруктураВозврата);	
	КонецЕсли;

	
	ТК_ТСД_РаботаHTTP_Сервис.СформироватьОтгрузкуКонтролер(ТорговаяПлощадкаСсылка,МассивДанных,СтруктураВозврата);
	ОчередьСборки = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.ПолучитьКоличествоРезервовНаСборку(ТорговаяПлощадкаСсылка,СкладСборки);	 
	Если  ТипЗнч(СтруктураВозврата.Ответ) = Тип("Структура") Тогда
		СтруктураВозврата.Ответ.Вставить("ОчередьСборки",ОчередьСборки);
	Иначе
		СтруктураВозврата.Вставить("Ответ",Новый Структура("ОчередьСборки",ОчередьСборки));
	КонецЕсли;
	Попытка 
		СтрокаВозврата	= ЗаписатьОтветJSON(СтруктураВозврата);
	Исключение
		СтрокаВозврата = ОписаниеОшибки();
	КонецПопытки;

	
	Возврат СтрокаВозврата;
	

КонецФункции





Процедура ПолучитьКоличествоРезервов(СтруктураВозврата,ТорговаяПлощадкаСсылка,МенеджерСборки)
	
	СкладСборки = РегистрыСведений.СборщикиРезервов.ПолучитьСкладСборкиСотрудника(МенеджерСборки);
	Если СкладСборки.Пустая() Тогда
		СтруктураВозврата.ОписаниеОшибки = "У працівника не вказано склад збирання товару";
		Возврат;	
	КонецЕсли;
	
	ОчередьСборки = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.ПолучитьКоличествоРезервовНаСборку(ТорговаяПлощадкаСсылка,СкладСборки);	
	СтруктураВозврата.Результат = Истина;
	СтруктураВозврата.Ответ = Новый Структура("ОчередьСборки",ОчередьСборки);

	
КонецПроцедуры

Процедура ПолучитьСтруктурРезерва(СтруктураВозврата,ТорговаяПлощадкаСсылка,МенеджерСборки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	//МенеджерСборки = ТК_ТСД_РаботаHTTP_СервисПовтИсп.ПолучитьСотрудника(UUIDСотрудник);
	//Если МенеджерСборки.Пустая() Тогда
	//	СтруктураВозврата.ОписаниеОшибки = "Сотрудник не найден";
	//	Возврат;	
	//КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ТК_АктивныеЗапросыНаОтборРезерваТСД");
		ЭлементБлокировки.УстановитьЗначение("ТорговаяПлощадка", ТорговаяПлощадкаСсылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТК_ОчередьНаСборкуРезервов.Документ КАК Документ,
		|	ВЫРАЗИТЬ(ТК_ОчередьНаСборкуРезервов.Документ КАК Документ.РезервированиеЗаказовПокупателя).Дата КАК Дата
		|ИЗ
		|	РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
		|ГДЕ
		|	ТК_ОчередьНаСборкуРезервов.Статус = &Статус
		|	И ВЫРАЗИТЬ(ТК_ОчередьНаСборкуРезервов.Документ КАК Документ.РезервированиеЗаказовПокупателя).ТорговаяПлощадка = &ТорговаяПлощадка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТК_ОчередьНаСборкуРезервов.Приоритет УБЫВ,
		|	Дата";
		Запрос.УстановитьПараметр("ТорговаяПлощадка",ТорговаяПлощадкаСсылка);
		Запрос.УстановитьПараметр("Статус",Перечисления.СтатусыСборкиРезервов.ГотовКСборке);
		//
		Рез = Запрос.Выполнить();
		Если Рез.Пустой() Тогда
			ВызватьИсключение "Нет документа для сборки";		
		КонецЕсли;
		Выборка = Рез.Выбрать();      
		Выборка.Следующий();
		
		ДокументРезерв = Выборка.Документ;
		СтруктураОчередиНаСборку = Новый Структура("СотрудникКонтроля,Статус,НачалоКонтроля",МенеджерСборки,Перечисления.СтатусыСборкиРезервов.ВСборке,ТекущаяДатаСеанса());
		РегистрыСведений.ТК_ОчередьНаСборкуРезервов.ОбновитьОчередь(ДокументРезерв,СтруктураОчередиНаСборку);
		РегистрыСведений.ТК_АктивныеЗапросыНаОтборРезерваТСД.УстановитьОтборРезерва(ТорговаяПлощадкаСсылка,ДокументРезерв,МенеджерСборки);
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезервированиеЗаказовПокупателя.Номер КАК Номер,
		|	РезервированиеЗаказовПокупателя.Дата КАК Дата,
		|	РезервированиеЗаказовПокупателя.ДокументОснование.Дата КАК ДатаЗаказа,
		|	РезервированиеЗаказовПокупателя.ТочкаДоставки.Наименование КАК ТочкаДоставки,
		|	РезервированиеЗаказовПокупателя.ТочкаДоставки.Порядок КАК Порядок,
		|	РезервированиеЗаказовПокупателя.ТочкаДоставки.ПорядокСигаретный КАК ПорядокСигаретный,
		|	РезервированиеЗаказовПокупателя.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
		|ГДЕ
		|	РезервированиеЗаказовПокупателя.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезервированиеЗаказовПокупателяТовары.Номенклатура.Артикул КАК Артикул,
		|	РезервированиеЗаказовПокупателяТовары.Номенклатура.Наименование КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РезервированиеЗаказовПокупателяТовары.Номенклатура.ТоварнаяМарка) КАК ТоварнаяМарка,
		|	ЕСТЬNULL(РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры.Наименование, """") КАК НаименованиеМРЦ,
		|	ЕСТЬNULL(РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры.ЗначениеМРЦ, 0) КАК ЗначениеМРЦ,
		|	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
		|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
		|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество,
		|	РезервированиеЗаказовПокупателяТовары.Количество * РезервированиеЗаказовПокупателяТовары.Коэффициент КАК КоличествоБаз
		|ИЗ
		|	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
		|ГДЕ
		|	РезервированиеЗаказовПокупателяТовары.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезервированиеЗаказовПокупателяТовары.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
		|	ШтрихкодыНоменклатуры.Номенклатура.Артикул КАК Артикул,
		|	ШтрихкодыНоменклатуры.Упаковка.Наименование КАК Упаковка,
		|	ШтрихкодыНоменклатуры.Упаковка.Коэффициент КАК Коэффициент
		|ИЗ
		|	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО РезервированиеЗаказовПокупателяТовары.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
		|ГДЕ
		|	РезервированиеЗаказовПокупателяТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументРезерв);
		
		МассивРезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ВыборкаДокумент = МассивРезультатЗапроса[0].Выбрать();
		ВыборкаДокумент.Следующий();
		
		СтруктураДокумента = Новый Структура;
		СтруктураДокумента.Вставить("ДокументUUID",Строка(ДокументРезерв.УникальныйИдентификатор()));
		СтруктураДокумента.Вставить("НомерРезерва",ВыборкаДокумент.Номер + " від "+Формат(ВыборкаДокумент.Дата,"ДФ=dd.MM.yyyy"));
		СтруктураДокумента.Вставить("ТочкаДоставки",ВыборкаДокумент.ТочкаДоставки);
		СтруктураДокумента.Вставить("ДатаЗаказа",Формат(ВыборкаДокумент.ДатаЗаказа,"ДФ=dd.MM.yyyy"));
		стрПорядок = ?(ВыборкаДокумент.Порядок = 0,ВыборкаДокумент.ПорядокСигаретный,ВыборкаДокумент.Порядок);
		
		СтруктураДокумента.Вставить("Порядок",Цел(стрПорядок / 1000));
		//Товары
		ВыборкаТовары = МассивРезультатЗапроса[1].Выбрать();
		МассивТоваров = Новый Массив;
		стрСтруктура = "Артикул,Номенклатура,ТоварнаяМарка,НаименованиеМРЦ,ЗначениеМРЦ,ЕдиницаИзмерения,Коэффициент,Количество,КоличествоБаз";
		
		Пока ВыборкаТовары.Следующий() Цикл 
			СтрокаТоваров = Новый Структура(стрСтруктура);
			ЗаполнитьЗначенияСвойств(СтрокаТоваров,ВыборкаТовары);
			МассивТоваров.Добавить(СтрокаТоваров);
		КонецЦикла;
		СтруктураДокумента.Вставить("Товары",МассивТоваров);
		//Штрихкода
		ВыборкаШтрихкода = МассивРезультатЗапроса[2].Выбрать();
		МассивШтрихкода = Новый Массив;
		ШтрихкодаСтруктура = "Штрихкод,Артикул,Упаковка,Коэффициент";
		
		Пока ВыборкаШтрихкода.Следующий() Цикл
			СтрокаШтрихкода = Новый Структура(ШтрихкодаСтруктура);
			ЗаполнитьЗначенияСвойств(СтрокаШтрихкода,ВыборкаШтрихкода);
			МассивШтрихкода.Добавить(СтрокаШтрихкода);
		КонецЦикла;
		СтруктураДокумента.Вставить("ШтрихКода",МассивШтрихкода);
		//
		//ВыборкаМестаХранения = МассивРезультатЗапроса[3].Выбрать();
		//МассивМестаХранения = Новый Массив;
		//
		//Пока ВыборкаМестаХранения.Следующий() Цикл

		//	
		//	СтрокаМестаХранения = Новый Структура("Артикул,Порядок,ПорядокМесто,МестоХранение,МестоХранениеИД,Улица,Ряд,Этаж"
		//		,ВыборкаМестаХранения.Артикул
		//		,ВыборкаМестаХранения.Порядок
		//		,ВыборкаМестаХранения.ПорядокМестоХранения
		//		,Строка(ВыборкаМестаХранения.МестоХранения)
		//		,Строка(ВыборкаМестаХранения.МестоХранения.УникальныйИдентификатор())
		//		,ВыборкаМестаХранения.МестоХраненияУлица
		//		,ВыборкаМестаХранения.МестоХраненияРяд
		//		,ВыборкаМестаХранения.МестоХраненияЭтаж);
		//	МассивМестаХранения.Добавить(СтрокаМестаХранения);
		//КонецЦикла;
		//
		//СтруктураДокумента.Вставить("МестаХранения",МассивМестаХранения);

		СтруктураВозврата.Результат = Истина;
		СтруктураВозврата.Ответ = СтруктураДокумента;
		
		ЗафиксироватьТранзакцию();
	Исключение
		СтруктураВозврата.Результат = Ложь;
		Инфо = ИнформацияОбОшибке();
		СтруктураВозврата.ОписаниеОшибки = КраткоеПредставлениеОшибки(Инфо);
		//	СтруктураВозврата.ОписаниеОшибки =ОписаниеОшибки();
		ОтменитьТранзакцию();
		
	КонецПопытки;

	
КонецПроцедуры

Процедура ОтменитьСборкуРезерва(СтруктураВозврата,ТорговаяПлощадкаСсылка,UUIDДокумент,МенеджерСборки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		ИД_Документа =	Новый УникальныйИдентификатор(UUIDДокумент);
	Исключение
		СтруктураВозврата.ОписаниеОшибки =  "Исключительная ошибка, документ не найден";
		Возврат;
	КонецПопытки;
	ДокументСсылка =  Документы.РезервированиеЗаказовПокупателя.ПолучитьСсылку(ИД_Документа);
	
	Если Не ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда 
		СтруктураВозврата.ОписаниеОшибки = "Документ не нейден.";  
		Возврат;
	КонецЕсли;
	
	Если ДокументСсылка.ПометкаУдаления Тогда
		СтруктураВозврата.ОписаниеОшибки = "Выбранный документ помечен на удаление!";  
		Возврат;
	КонецЕсли;
	
	СкладСборки = РегистрыСведений.СборщикиРезервов.ПолучитьСкладСборкиСотрудника(МенеджерСборки);
	Если СкладСборки.Пустая() Тогда
		СтруктураВозврата.ОписаниеОшибки = "У працівника не вказано склад збирання товару";
		Возврат;	
	КонецЕсли;

	
	СтруктураОчередиНаСборку = Новый Структура("Статус",Перечисления.СтатусыСборкиРезервов.ГотовКСборке);
	РегистрыСведений.ТК_ОчередьНаСборкуРезервов.ОбновитьОчередь(ДокументСсылка,СтруктураОчередиНаСборку);
	
	ОчередьСборки = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.ПолучитьКоличествоРезервовНаСборку(ТорговаяПлощадкаСсылка,СкладСборки);	
	СтруктураВозврата.Ответ = Новый Структура("ОчередьСборки",ОчередьСборки);
	СтруктураВозврата.Результат = Истина;

КонецПроцедуры



#Область ПрограммныйИнтерфейс

// Служебная функция, предназначенная для получения описания типов числа, заданной разрядности.
// 
// Параметры:
//  Разрядность 			- число, разряд числа.
//  РазрядностьДробнойЧасти - число, разряд дробной части.
//  Неотрицательный - Булево, если Истина, то числа только положительные, если Ложь, то любые.
//
// Возвращаемое значение:
//  Объект "ОписаниеТипов" для числа указанной разрядности.
//
Функция ПолучитьОписаниеТиповЧисла(Разрядность, РазрядностьДробнойЧасти, Неотрицательный = Ложь) Экспорт
	
	Если Неотрицательный Тогда
		Знак = ДопустимыйЗнак.Неотрицательный;
	Иначе
		Знак = ДопустимыйЗнак.Любой;
	КонецЕсли;
	
	Возврат Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(Разрядность, РазрядностьДробнойЧасти, Знак));
	
КонецФункции // ПолучитьОписаниеТиповЧисла()

// Служебная функция, предназначенная для получения описания типов даты
// 
// Параметры:
//  ЧастиДаты - системное перечисление ЧастиДаты.
// 
Функция ПолучитьОписаниеТиповДаты(ЧастиДаты) Экспорт
	
	Возврат Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты));
	
КонецФункции // ПолучитьОписаниеТиповДаты()



// Формирует пакет запросов и возвращает результат каждого запроса
//
// Параметры:
//	Запрос			- Запрос - запрос, параметры которого предварительно установлены.
//	ТекстыЗапроса	- Список значений - в списке перечислены тексты запросов и их имена.
//	ОбходРезультата - ОбходРезультатаЗапроса - вариант обхода результата запроса.
//
// Возвращаемое значение:
//   Структура   - структура в которую помещены полученные таблицы
//
Функция ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, ОбходРезультата = Неопределено, ДобавитьРазделитель = Ложь) Экспорт
	
	Таблицы = Новый Структура;
	
	// Инициализация варианта обхода результата запроса.
	Если ОбходРезультата = Неопределено Тогда
		ОбходРезультата = ОбходРезультатаЗапроса.Прямой;
	КонецЕсли;
	
	// Формирование текст запроса.
	Запрос.Текст = "";
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл
		Если ЗначениеЗаполнено(ТекстЗапроса.Представление) Тогда
			Запрос.Текст = Запрос.Текст 
			+ ?(Запрос.Текст <> "", Символы.ПС, "")
			+ "// " + ТекстЗапроса.Представление + Символы.ПС;	
		КонецЕсли; 
		Запрос.Текст = Запрос.Текст + ТекстЗапроса.Значение;
		Если ДобавитьРазделитель Тогда
			Запрос.Текст = Запрос.Текст + "
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|"
		КонецЕсли; 
	КонецЦикла;
	
	// Выполнение запроса.
	Результат = Запрос.ВыполнитьПакет();
	
	// Помещение результатов запроса в таблицы
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл
		
		ИмяТаблицы = ТекстЗапроса.Представление;
		
		Если Не ПустаяСтрока(ИмяТаблицы) Тогда
			
			Индекс = ТекстыЗапроса.Индекс(ТекстЗапроса);
			Таблицы.Вставить(ИмяТаблицы, Результат[Индекс].Выгрузить(ОбходРезультата));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Таблицы;
	
КонецФункции


Функция ВнутреннееСоединениеМассивов(МассивПервый, МассивВторой) Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого Элемент Из МассивПервый Цикл
		
		Если НЕ МассивВторой.Найти(Элемент) = Неопределено Тогда
			
			Результат.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ВнутреннееСоединениеМассивов(); 

//Заполняет колонку таблицы значений последовательными номерами
//Параметры:
//	Таблица - таблица значений, строки которой нужно пронумеровать
//	ИмяКолонкиНомераСтроки - колонка таблицы значений, в которой будут указаны номера строк
Процедура ПронумероватьТаблицуЗначений(Таблица, ИмяКолонкиНомераСтроки) Экспорт
	
	Таблица.Колонки.Добавить(ИмяКолонкиНомераСтроки, ПолучитьОписаниеТиповЧисла(15, 0));
	
	КоличествоСтрок = Таблица.Количество()-1;
	сч = 1;
	Для НомерСтроки = 0 По КоличествоСтрок Цикл
		Таблица[НомерСтроки][ИмяКолонкиНомераСтроки] = сч;
		сч = сч + 1;
	КонецЦикла;
	
КонецПроцедуры


// Производит формирование таблицы значений на основании дерева значений
// Используется например при иерархической сортировке таблиц, когда формируется промежуточный запрос с итогами по иерархии (дерево)
Функция ВыгрузитьДеревоВТЗ(тзПриемник, ДеревоИсточник, Колонки, СоздаватьКолонки=Истина) Экспорт
	
	Для Сч = 0 ПО ДеревоИсточник.Строки.Количество()-1 Цикл
		СтрокаДерева = ДеревоИсточник.Строки[Сч];
		
		стзПриемник = тзПриемник.Добавить();
		Для каждого Колонка Из Колонки Цикл
			Если тзПриемник.Колонки.Найти(Колонка.Имя)=Неопределено тогда
				Если СоздаватьКолонки тогда
					тзПриемник.Колонки.Добавить(Колонка.Имя);
				иначе
					Продолжить;
				КонецЕсли;				
			КонецЕсли;
			
			стзПриемник[Колонка.Имя] = СтрокаДерева[Колонка.Имя];
		КонецЦикла;
		
		Если СтрокаДерева.Строки.Количество()>0 тогда
			// проанализируем нижний уровень на предмет дублей
			Если СтрокаДерева.Строки.Количество()=1 тогда
				СтрокаНиже = СтрокаДерева.Строки[0];
				Если СтрокаНиже.Строки.Количество()=0 тогда // вполне возможно
					ЭтоДубль = Истина;
					Для каждого Колонка Из Колонки Цикл
						Если СтрокаДерева[Колонка.Имя]<>СтрокаНиже[Колонка.Имя] тогда
							ЭтоДубль = Ложь;
						КонецЕсли;
					КонецЦикла;
					Если ЭтоДубль тогда // таки да!
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			// есть вложенные строки - рекурсируем
			ВыгрузитьДеревоВТЗ(тзПриемник, СтрокаДерева, Колонки);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзПриемник;
КонецФункции


// Осуществляет проверку заполненности проверяемых реквизитов.
//
// Параметры:
// 		Объект                      - ДокументОбъект, СправочникОбъект - Проверяемый объект.
// 		МассивПроверяемыхРеквизитов - Массив - массив проверяемых реквизитов.
//
// Возвращаемое значение:
// 		Булево - Истина, если значение хотя бы одного реквизита не заполнено, иначе Ложь
//
Функция ПроверитьЗаполнениеРеквизитовОбъекта(Объект, МассивПроверяемыхРеквизитов) Экспорт
	
	Перем ПроверяемыеРеквизитыТЧ;
	Отказ = Ложь;
	
	РезультатОбработки = Неопределено;
	НакапливатьОшибки = Объект.ДополнительныеСвойства.Свойство("РезультатОбработки",РезультатОбработки);
	// Получение метаданных объекта
	МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	
	// Создание структуры стандартных реквизитов
	СтандартныеРеквизиты = Новый Структура;
	Для Каждого Реквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		СтандартныеРеквизиты.Вставить(Реквизит.Имя, ?(ЗначениеЗаполнено(Реквизит.Синоним), Реквизит.Синоним, Реквизит.Имя));
	КонецЦикла;
	
	// Создание структуры для хранения имен табличных частей и проверяемых реквизитов в них.
	// 		Ключ -  Имя табличной части
	// 		Значение - Массив - Массив строк, реквизитов этой табличной части для проверки
	ТабличныеЧасти = Новый Структура;
	
	// Создание шаблонов сообщений об ошибках не заполненных реквизитов и реквизитов табличных частей
	ШаблонОшибкиРеквизита = НСтр("ru='Поле ""%ИмяРеквизита%"" не заполнено';uk='Поле ""%ИмяРеквизита%"" не заповнено'");
	ШаблонОшибкиТЧ = НСтр("ru='Не введено ни одной строки в список ""%ИмяРеквизита%""';uk='Не введено жодного рядка в список ""%ИмяРеквизита%""'");
	ШаблонОшибкиРеквизитаТЧ = НСтр("ru='Не заполнена колонка ""%ИмяРеквизита%"" в строке %НомерСтроки% списка ""%ИмяТабличнойЧасти%""';uk='Не заповнена колонка ""%ИмяРеквизита%"" в рядку %НомерСтроки% списку ""%ИмяТабличнойЧасти%""'");
	
	// Проверка реквизитов объекта и заполнение структуры по реквизитам табличных частей
	Для Каждого Реквизит Из МассивПроверяемыхРеквизитов Цикл
		
		ПозицияТочки = СтрНайти(Реквизит,".");
		
		Если ПозицияТочки > 0 Тогда // В случае если указан реквизит табличной части
			
			ДлинаСтроки       = СтрДлина(Реквизит);
			ИмяТабличнойЧасти = Лев(Реквизит, ПозицияТочки-1);
			ИмяРеквизита      = Прав(Реквизит, ДлинаСтроки - ПозицияТочки);
			
			// Сохранение проверяемого реквизита табличной части в структуру
			Если НЕ ТабличныеЧасти.Свойство(ИмяТабличнойЧасти, ПроверяемыеРеквизитыТЧ) Тогда
				ПроверяемыеРеквизитыТЧ = Новый Массив;
				ТабличныеЧасти.Вставить(ИмяТабличнойЧасти, ПроверяемыеРеквизитыТЧ);
			КонецЕсли;
			ПроверяемыеРеквизитыТЧ.Добавить(ИмяРеквизита);
			
		Иначе // В случае если указан реквизит объекта
			
			Если Не ЗначениеЗаполнено(Объект[Реквизит]) Тогда
				
				Если МетаданныеОбъекта.Реквизиты.Найти(Реквизит) <> Неопределено Тогда // Если указано имя реквизита
					ТекстОшибки = СтрЗаменить(ШаблонОшибкиРеквизита, "%ИмяРеквизита%",
					МетаданныеОбъекта.Реквизиты[Реквизит].Синоним);
				ИначеЕсли СтандартныеРеквизиты.Свойство(Реквизит) Тогда // Если указано имя стандартного реквизита
					ТекстОшибки = СтрЗаменить(ШаблонОшибкиРеквизита, "%ИмяРеквизита%",
					СтандартныеРеквизиты[Реквизит]);
				Иначе // Если указано имя табличной части
					ТекстОшибки = СтрЗаменить(ШаблонОшибкиТЧ, "%ИмяРеквизита%",
					МетаданныеОбъекта.ТабличныеЧасти[Реквизит].Синоним);
				КонецЕсли;
				
				Если НакапливатьОшибки Тогда 
					РезультатОбработки.ДобавитьСтроку(ТекстОшибки);
				КонецЕсли;
				
				
				
				ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				Объект,
				Реквизит,
				,
				Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверка реквизитов в табличных частях
	Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
		
		ИмяТабличнойЧасти = ТабличнаяЧасть.Ключ;
		ТабличнаяЧастьОбъекта = Объект[ТабличнаяЧасть.Ключ];
		МассивРеквизитов = ТабличнаяЧасть.Значение;
		
		// Цикл по всем строкам табличной части.
		Для НомерСтроки=0 По ТабличнаяЧастьОбъекта.Количество()-1 Цикл
			
			// Цикл по всем проверяемым реквизитам для текущей табличной части.
			Для НомерРеквизита=0 По МассивРеквизитов.Количество()-1 Цикл
				
				ИмяРеквизита = МассивРеквизитов[НомерРеквизита];
				
				Если Не ЗначениеЗаполнено(ТабличнаяЧастьОбъекта[НомерСтроки][ИмяРеквизита]) Тогда
					
					ТекстОшибки = СтрЗаменить(ШаблонОшибкиРеквизитаТЧ, "%ИмяРеквизита%", МетаданныеОбъекта.ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты[ИмяРеквизита].Синоним);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", Формат(НомерСтроки+1, "ЧГ=0"));
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИмяТабличнойЧасти%", МетаданныеОбъекта.ТабличныеЧасти[ИмяТабличнойЧасти].Синоним);
					
					Если НакапливатьОшибки Тогда 
						РезультатОбработки.ДобавитьСтроку(ТекстОшибки);
					КонецЕсли;
					
					
					ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					Объект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТабличнойЧасти, НомерСтроки+1, ИмяРеквизита),
					,
					Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивПроверяемыхРеквизитов.Очистить();
	
	Возврат Отказ;
	
КонецФункции // ПроверитьЗаполнениеРеквизитовОбъекта()


//Проверяет заполнение активных отборов настроек компоновки данных
// Параметры:
// 		Объект                      - КомпоновщикНастроекКомпоновкиДанных, НастройкиКомпоновкиДанных - Проверяемый объект.
//
// Возвращаемое значение:
// 		Булево - Истина, если значение хотя бы одного реквизита не заполнено, иначе Ложь
//

Функция ПроверитьЗаполнениеНастроекОтчета(Объект)Экспорт
	
	Отказ = Ложь;
	ТипОбъекта = ТипЗнч(Объект);
	Если ТипОбъекта = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		НастройкиОтчета = Объект.ПолучитьНастройки();
	ИначеЕсли ТипОбъекта = Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиОтчета = Объект;
	Иначе
		Отказ = Истина;
		Возврат Отказ;
	КонецЕсли;
	
	Для сч = 0 По НастройкиОтчета.Отбор.Элементы.Количество() - 1 Цикл
		ЭлементОтбораКомпоновки      = НастройкиОтчета.Отбор.Элементы[сч];
		ПроверяемоеПолеОтбора        = ЭлементОтбораКомпоновки.ЛевоеЗначение;
		ИмяПроверяемогоПоляОтбора    = СокрЛП(ПроверяемоеПолеОтбора);
		Если ЭлементОтбораКомпоновки.Использование Тогда
			ЗначениеОтбора =  ЭлементОтбораКомпоновки.ПравоеЗначение;
			Если ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") Тогда
				Если ЗначениеОтбора.Количество() = 0 Тогда
					ОбщегоНазначения.СообщитьПользователю("Не заполнено поле отбора <"+ИмяПроверяемогоПоляОтбора+">",,,,Отказ);
				Иначе
					Для Каждого знОтбора Из ЗначениеОтбора Цикл 
						Если НЕ ЗначениеЗаполнено(знОтбора.Значение) Тогда
							ОбщегоНазначения.СообщитьПользователю("Не заполнено поле отбора <"+ИмяПроверяемогоПоляОтбора+">",,,,Отказ);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				//ИначеЕсли НЕ ЗначениеЗаполнено(ЗначениеОтбора) Тогда
				//	ОбщегоНазначения.СообщитьПользователю("Не заполнено поле отбора <"+ИмяПроверяемогоПоляОтбора+">",,,,Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции



// Осуществляет запись дополнительного свойства
//
// Параметры:
// 		Объект                      - ДокументСсылка, СправочникСсылка - Объект с дополнительными сведениями.
// 		Свойство 					- ПланВидовХарактеристик - устанавливаемое свойство.
// 		Значение 					- устанавливаемое значение.
//
//
Процедура ЗаписатьСвойствоУОбъекта(Ссылка, Свойство, Значение) Экспорт 
	
	Если ТипЗнч(Свойство) <> Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда 
		Если ТипЗнч(Свойство) = Тип("Строка") И Не ПустаяСтрока(Свойство) Тогда 
			Свойство = ЗначениеНастроекПовтИсп.ПолучитьДопСвойствоПоИмени(Свойство);
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дополнительное свойство не обнаружено'; uk = 'Додаткова властивість не знайдена'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Свойство) Тогда 
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось записать дополнительное свойство у объекта'; uk = 'Не вдалося записати додаткову властивість для об''єкта'"));
		Возврат;
	КонецЕсли;
	
	ТаблицаСвойств = Новый ТаблицаЗначений;
	ТаблицаСвойств.Колонки.Добавить("Свойство");
	ТаблицаСвойств.Колонки.Добавить("Значение");
	
	НовоеСвойство = ТаблицаСвойств.Добавить();
	НовоеСвойство.Свойство = Свойство;
	НовоеСвойство.Значение = Значение;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Ссылка, ТаблицаСвойств);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Позволяет получить индекс картинки состояния документа из коллекции СостоянияДокумента
// по свойствам Проведен/ПометкаУдаления/РучнаяКорректировка
//
// Параметры:
// Объект - основной реквизит формы документа, с типом ДанныеФормыСтруктура
//
Функция СостояниеДокумента(Объект) Экспорт
	
	Если Объект.ПометкаУдаления Тогда
		СостояниеДокумента = 3;
	ИначеЕсли Объект.Проведен Тогда
		СостояниеДокумента = 1;
	Иначе
		СостояниеДокумента = 0;
	КонецЕсли;	
	
	Возврат СостояниеДокумента;
	
КонецФункции

Функция ПолучитьКоличествоДокументов(Подразделение,Дата)
	
	ТЗНомеров = Новый ТаблицаЗначений;
	ТЗНомеров.Колонки.Добавить("Номер",Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10,0)));	
	ТЗНомеров.Колонки.Добавить("НомерРН");	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговаяНакладная.НомерРН КАК НомерРН
	|ИЗ
	|	Документ.Тк_НалоговаяНакладная КАК НалоговаяНакладная
	|ГДЕ
	|	НалоговаяНакладная.ПодразделениеКомпании = &ПодразделениеКомпании
	|	И НалоговаяНакладная.Дата МЕЖДУ &Дата1 И &Дата2
	|	И НалоговаяНакладная.РН";
	
	Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);
	Запрос.УстановитьПараметр("Дата1", НачалоДня(Дата));
	Запрос.УстановитьПараметр("Дата2", КонецДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Строка = ТЗНомеров.Добавить();
		Строка.НомерРН = Выборка.НомерРН;
		Попытка
			Строка.Номер = Число(Прав(Выборка.НомерРН,(СтрДлина(Выборка.НомерРН)-Найти(Выборка.НомерРН,"-"))));
		Исключение
		КонецПопытки;
	КонецЦикла;
	Если ТЗНомеров.Количество()>0 Тогда
		ТЗНомеров.Сортировать("Номер Убыв");
		Возврат ТЗНомеров[0].НомерРН;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

Функция ИзменитьНомерРН(Дата = Неопределено, Номер, Организация, Подразделение) Экспорт
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;
	
	НПрефикс	= СокрЛП(Организация.Префикс);
	НДата		= СтрЗаменить(Лев(Дата,5),".","");
	НПоследний	= ПолучитьКоличествоДокументов(Подразделение, Дата);
	Если НПоследний = 0 Или Найти(НПоследний,"-")=0 Тогда
		НПорядок = 1;
	Иначе
		НПорядок = 1+Прав(НПоследний,(СтрДлина(НПоследний)-Найти(НПоследний,"-")));
	КонецЕсли;
	
	НомерРН	= НПрефикс+"/"+НДата+"-"+НПорядок;
	
	Возврат НомерРН;
КонецФункции


Функция ПолучитьНомерНалоговогоДокументаДляБухгалтерии(Дата = Неопределено, Номер, Организация) Экспорт 
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;
	
	ПрефиксОрг = СокрЛП(Организация.Префикс);
	СтрМесяцДок = Сред("Ян|Фв|Мр|Ап|Ма|Ин|Ил|Ав|Сн|Ок|Но|Дк", Месяц(Дата)*3 - 2, 2);
	
	ЧислоНомерБезПрефикса = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПолучитьСтроковыйНомерБезПрефиксов(Номер));
	Если Не ЗначениеЗаполнено(ЧислоНомерБезПрефикса) Тогда 
		Возврат "";
	КонецЕсли;
	
	НомерБезПрефикса = Формат(ЧислоНомерБезПрефикса, "ЧЦ=12; ЧГ=0");
	
	Возврат ПрефиксОрг + СтрМесяцДок + НомерБезПрефикса;
	
КонецФункции

//Выделяет из переданой строки с номером числовой номер без префиксов
Функция ПолучитьСтроковыйНомерБезПрефиксов(Номер) Экспорт 
	
	НомерБезПрефиксов = "";
	Сч = СтрДлина(Номер);
	
	Пока Сч > 0 Цикл
		
		Символ = Сред(Номер, Сч, 1);
		
		Если (Символ >= "0" И Символ <= "9") Тогда
			
			НомерБезПрефиксов = Символ + НомерБезПрефиксов;
			
		Иначе
			
			Возврат НомерБезПрефиксов;
			
		КонецЕсли;
		
		Сч = Сч - 1;
		
	КонецЦикла;
	
	Возврат НомерБезПрефиксов;
	
КонецФункции

// функция пересчитывает Цену из ВалютаНач и возвращает значение Цена в ВалютаКон.
// в параметрах ПоКурсуВалютыНач и ПоКурсуВалютыНач могут передаваться либо сами курсы либо даты
Функция обПересчет(ЧислСумма,ВалютаНач,ПоКурсуВалютыНач,ВалютаКон,ПоКурсуВалютыКон,РежимОкр=Неопределено) Экспорт
	Если ВалютаНач=ВалютаКон Тогда
	Иначе
		// если ВалютаНач не совпадает с ВалютаКон
		ТипЗн=ТипЗнч(ПоКурсуВалютыНач);
		Если ТипЗн=Тип("Число") Тогда КурсВалютыНач=ПоКурсуВалютыНач;
		ИначеЕсли (ТипЗн=Тип("Дата")) ИЛИ (ТипЗн=Тип("МоментВремени")) ИЛИ (ТипЗн=Тип("Граница")) Тогда 
			стКурс=РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ПоКурсуВалютыНач,Новый Структура("Валюта",ВалютаНач));
			Попытка
				КурсВалютыНач=стКурс.Курс/?(стКурс.Кратность=0,1,стКурс.Кратность);
			Исключение
				КурсВалютыНач=0;
			КонецПопытки;
		Иначе 
			Сообщить("Неверный тип параметра при пересчете валюты!(1)"); Возврат ЧислСумма;
		КонецЕсли;
		
		ТипЗн=ТипЗнч(ПоКурсуВалютыКон);
		Если ТипЗн=Тип("Число") Тогда КурсВалютыКон=ПоКурсуВалютыКон;
		ИначеЕсли (ТипЗн=Тип("Дата")) ИЛИ (ТипЗн=Тип("МоментВремени")) ИЛИ (ТипЗн=Тип("Граница")) Тогда 
			стКурс=РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ПоКурсуВалютыКон,Новый Структура("Валюта",ВалютаКон));
			Попытка
				КурсВалютыКон=стКурс.Курс/?(стКурс.Кратность=0,1,стКурс.Кратность);
			Исключение
				КурсВалютыКон=0;
			КонецПопытки;
		Иначе 
			Сообщить("Неверный тип параметра при пересчете валюты!(2)"); Возврат ЧислСумма;
		КонецЕсли;
		
		Если (КурсВалютыНач*КурсВалютыКон)=0 Тогда
			Сообщить("При пересчете валюты обнаружен нулевой курс валюты."
			+?(КурсВалютыНач=0,ВалютаНач.Наименование,ВалютаКон.Наименование));
			Возврат ЧислСумма;
		КонецЕсли;
		Если КурсВалютыКон=0 Тогда
			Возврат 0;
		Иначе
			ЧислоРезультат=ЧислСумма*КурсВалютыНач/КурсВалютыКон;
			Если РежимОкр<>Неопределено Тогда
				//ЧислоРезультат=Окр(ЧислоРезультат,2,РежимОкр); // by master  26.03.2010 22:14:48
				ЧислоРезультат=Окр(ЧислоРезультат,6,РежимОкр); 
			КонецЕсли; 
			Возврат ЧислоРезультат;
		КонецЕсли; 
		
	КонецЕсли;
	Возврат ЧислСумма;	
КонецФункции	// глПересчет

// округляет рассчитанные значения в соответствии с параметрами округления
Функция ОкруглитьЗаказываемоеЗначение(Знач Заказать,КвантПоставки,ВариантОкругления,Знач Упаковка)	Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВариантОкругления)
		ИЛИ ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.НеОкруглять Тогда
		Возврат Заказать;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Заказать) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.ДробноеКоличествоВМеньшуюСторону Тогда
		Заказать = Цел(Заказать);
	ИначеЕсли ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.ДробноеКоличествоВБольшуюСторону Тогда
		Заказать = ?(Цел(Заказать) = Заказать,Заказать,Цел(Заказать)+1);
	ИначеЕсли ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВМеньшуюСторону И ЗначениеЗаполнено(КвантПоставки) Тогда
		Заказать = Цел(Заказать/КвантПоставки)*КвантПоставки;
	ИначеЕсли ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону И ЗначениеЗаполнено(КвантПоставки) Тогда
		Кол = (Заказать/КвантПоставки);
		Заказать = ?(Кол<>Цел(Кол),Цел(Кол)+1,Кол)*КвантПоставки;
	ИначеЕсли ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону Тогда
		КвантПоставкиВБольшуюСторону = Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону;
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Если Заказать > Цел(Упаковка / 2) Тогда
				Заказать = ОкруглитьЗаказываемоеЗначение(Заказать,Упаковка,КвантПоставкиВБольшуюСторону,Упаковка);
			Иначе
				Заказать = ОкруглитьЗаказываемоеЗначение(Заказать,КвантПоставки,КвантПоставкиВБольшуюСторону,Упаковка);
			КонецЕсли;
		Иначе
			Заказать = ОкруглитьЗаказываемоеЗначение(Заказать,КвантПоставки,КвантПоставкиВБольшуюСторону,Упаковка);
		КонецЕсли;
		//ИначеЕсли ВариантОкругления = Перечисления.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону Тогда
		//Если ЗначениеЗаполнено(Упаковка) Тогда
		//	КУпаковки = Заказать/Упаковка;
		//	КПачки = Окр(Упаковка * (КУпаковки - Цел(КУпаковки)),3,1);
		//	Если КПачки > Цел(Упаковка / 2) Тогда
		//		Заказать = ОкруглитьЗаказываемоеЗначение(Заказать,Упаковка,Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону,Упаковка)
		//	Иначе
		//		//Заказать = Упаковка * Цел(КУпаковки) + ?(КвантПоставки = 1,ОкруглитьЗаказываемоеЗначение(КПачки,КвантПоставки,Перечисления.ВариантыОкругленияКоличества.ДробноеКоличествоВБольшуюСторону,Упаковка),0);
		//		Заказать = Упаковка * Цел(КУпаковки) + ОкруглитьЗаказываемоеЗначение(КПачки,КвантПоставки,Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону,Упаковка);
		//	КонецЕсли;
		//Иначе
		//	Заказать = ОкруглитьЗаказываемоеЗначение(Заказать,КвантПоставки,Перечисления.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону,Упаковка)
		//КонецЕсли;
	КонецЕсли;
	
	Возврат Заказать;
	
КонецФункции

#КонецОбласти


#Область ОбработчикиПодписокНаСобытия

// Установка автора (подписка на событие)
Процедура УстановитьАвтораДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ЗначениеЗаполнено(ТекущийПользователь) И Источник.Автор <> ТекущийПользователь Тогда 
		Источник.Автор = ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

// Регистрация в очереди на обработку налоговой накладной (подписка на событие)
Процедура РегистрацияВОчередиНаОбработкуНН(Источник, Отказ) Экспорт 
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоНовый 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ЭтоНовый", Истина);
	РежимЗаписи 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "РежимЗаписи", Неопределено);
	НеСоздаватьНН 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "НеСоздаватьНН", Ложь);
	РеглУчет 		= Источник.РегламентированныйУчет;	
	
	Если НеСоздаватьНН
		ИЛИ (ЭтоНовый И Не РеглУчет)
		ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Запись) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаОбработкуНалоговыхНакладных.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = Источник.Ссылка;
	ЗаписьВОчереди.Записать(Истина);
	
КонецПроцедуры

// Регистрация в очереди на обработку возврата от покупателя (подписка на событие)
Процедура РегистрацияВОчередиНаОбработкуПР2НН(Источник, Отказ) Экспорт 
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоНовый 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ЭтоНовый", Истина);
	РежимЗаписи 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "РежимЗаписи", Неопределено);
	НеСоздаватьДок 	= Источник.ДополнительныеСвойства.Свойство("НеСоздаватьНН");
	РеглУчет 		= Источник.РегламентированныйУчет;	
	
	Если НеСоздаватьДок
		ИЛИ (ЭтоНовый И Не РеглУчет)
		ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Запись) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаОбработкуВозвратаОтПокупателя.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = Источник.Ссылка;
	ЗаписьВОчереди.Записать(Истина);
	
КонецПроцедуры

// Регистрация в очереди на обработку Консолидированых приходов (подписка на событие)
Процедура РегистрацияВОчередиНаОбработкуКонс(Источник, Отказ) Экспорт 
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоНовый 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ЭтоНовый", Истина);
	РежимЗаписи 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "РежимЗаписи", Неопределено);
	НеСоздаватьДок 	= Источник.ДополнительныеСвойства.Свойство("НеСоздаватьКонс");
	РН 		        = Источник.РН;	
	Внутренний   	= Источник.Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Внутренний;
	
	Если НеСоздаватьДок
		ИЛИ (ЭтоНовый и НЕ РН и НЕ Внутренний)
		ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Запись) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаОбработкуКонсолидированыхПриходов.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = Источник.Ссылка;
	ЗаписьВОчереди.Записать(Истина);
	
КонецПроцедуры

// Регистрация в очереди на обработку Консолидированых Расходов (подписка на событие)
Процедура РегистрацияВОчередиНаОбработкуКонсРасход(Источник, Отказ) Экспорт 
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоНовый 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ЭтоНовый", Истина);
	РежимЗаписи 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "РежимЗаписи", Неопределено);
	НеСоздаватьДок 	= Источник.ДополнительныеСвойства.Свойство("НеСоздаватьКонс");
	Внутренний   	= Источник.Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Внутренний;
	Если ЗначениеЗаполнено(Источник.НалоговаяНакладная) Тогда 
		РН = Источник.НалоговаяНакладная.РН;
	Иначе
		РН = Ложь
	КонецЕсли;
	
	Если НеСоздаватьДок
		ИЛИ (ЭтоНовый и НЕ РН и НЕ Внутренний)
		ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Запись) Тогда 
		
		Возврат;
	КонецЕсли;
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаОбработкуКонсолидированыхРасходов.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = Источник.Ссылка;
	ЗаписьВОчереди.Записать(Истина);
	
КонецПроцедуры

// Регистрация в очереди на сборку чеков (подписка на событие)
Процедура ТК_РегистрацияВОчередиНаСборкуПриЗаписи(Источник, Отказ) Экспорт 
	
	Если Отказ ИЛИ Источник.ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РежимЗаписи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "РежимЗаписи", Неопределено);
	Собирается 	= СборкаРезерва(Источник.Ссылка); 
	
	Если (РежимЗаписи = РежимЗаписиДокумента.Запись) Или Собирается Тогда 
		Возврат;
	ИначеЕсли (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) И НЕ Собирается Тогда
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ = Источник.Ссылка;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ЗаписьВОчереди.Удалить();
		КонецЕсли;
		
		РегистрыСведений.ТК_СостояниеДокументов.УдалитьСостояниеДокумента(Источник.Ссылка);
	Иначе
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ  = Источник.Ссылка;
		ЗаписьВОчереди.Статус    = Перечисления.СтатусыСборкиРезервов.ГотовКСборке;
		ЗаписьВОчереди.Приоритет = Перечисления.ПриоритетыСборки.ОченьНизкий;
		
		ЗаписьВОчереди.Записать(Истина);
		
		СтруктураСостояние = Новый  Структура("Состояние,ДатаПоследнегоИзменения,АвторИзменения,ТорговаяПлощадка"
		,Перечисления.СтатусыСборкиРезервов.ГотовКСборке,ТекущаяДатаСеанса(),Пользователи.ТекущийПользователь().ФизическоеЛицо,Источник.ТорговаяПлощадка);
		РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(Источник.Ссылка,СтруктураСостояние);
		
	КонецЕсли;
	
	
КонецПроцедуры


Процедура АктуальностьТочкиПередЗаписью(Источник, Отказ) Экспорт
	ИсточникДатаЗакрытия = Источник.ДатаЗакрытия;
	Если ЗначениеЗаполнено(ИсточникДатаЗакрытия) И ТекущаяДата() > ИсточникДатаЗакрытия Тогда	//	Закрыта
		Возврат;
	КонецЕсли;
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.найтиПоРеквизиту("Имя", "КураторПлощадки");
	
	стрИсточникКуратор = Источник.ДополнительныеРеквизиты.Найти(Свойство, "Свойство");
	Если стрИсточникКуратор = Неопределено Тогда	//	Без куратора
		Возврат;
	Иначе
		ИсточникКуратор = стрИсточникКуратор.Значение;
		ИсточникСсылка = Источник.Ссылка;
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			стрСсылкаКуратор = ИсточникСсылка.ДополнительныеРеквизиты.Найти(Свойство, "Свойство");
			Если стрСсылкаКуратор = Неопределено Тогда	//	На точке появился куратор
				ЗарегистрироватьТерриторию(Источник);
				ЗарегистрироватьКуратора(ИсточникКуратор);
			ИначеЕсли Не ЗначениеЗаполнено(ИсточникДатаЗакрытия) И ИсточникДатаЗакрытия <> Источник.Ссылка.ДатаЗакрытия И ИсточникКуратор = стрСсылкаКуратор.Значение Тогда	//	Открылась, куратор был
				ЗарегистрироватьТерриторию(Источник);
			Иначе	//	Старая точка, поменяли куратора
				ЗарегистрироватьКуратора(ИсточникКуратор);
				Возврат;
			КонецЕсли;
		Иначе	//	Новая точка с куратором
			Источник.ДополнительныеСвойства.Вставить("ЗарегистрироватьТерриториюДляFirestore", Истина)
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура АктуальностьТочкиПриЗаписи(Источник, Отказ) Экспорт
	ЗарегистрироватьТерриториюДляFirestore = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Источник.ДополнительныеСвойства, "ЗарегистрироватьТерриториюДляFirestore", Ложь);
	Если ЗарегистрироватьТерриториюДляFirestore Тогда
		ЗарегистрироватьТерриторию(Источник);
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьТерриторию(Источник)
	Точка = Источник.Ссылка;
	УзелПланаОбмена = ПланыОбмена.ОбменFirestore.НайтиПоКоду("DBG");
	
	Запрос1 = Новый Запрос;
	Запрос1.Текст = 
	"ВЫБРАТЬ
	|	ТерриторииИсполнителей.Ссылка КАК Территория
	|ИЗ
	|	Справочник.ТерриторииИсполнителей КАК ТерриторииИсполнителей";
	
	РезультатЗапроса1 = Запрос1.Выполнить();
	
	ВыборкаТерриторий = РезультатЗапроса1.Выбрать();
	
	Пока ВыборкаТерриторий.Следующий() Цикл
		
		Запрос2 = Новый Запрос;
		Запрос2.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТорговыеПлощадки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		|ГДЕ
		|	ТорговыеПлощадки.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ТерриторииИсполнителейТочки.Точка КАК Точка
		|			ИЗ
		|				Справочник.ТерриторииИсполнителей.Точки КАК ТерриторииИсполнителейТочки
		|			ГДЕ
		|				ТерриторииИсполнителейТочки.Ссылка = &Территория
		|				И ТерриторииИсполнителейТочки.Точка <> &Точка)
		|	И ТорговыеПлощадки.Ссылка = &Точка";
		
		Запрос2.УстановитьПараметр("Точка", Точка);
		Запрос2.УстановитьПараметр("Территория", ВыборкаТерриторий.Территория);
		
		РезультатЗапроса2 = Запрос2.Выполнить();
		
		Если НЕ РезультатЗапроса2.Пустой() Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, ВыборкаТерриторий.Территория);
		КонецЕсли;
		//	Выборка = РезультатЗапроса2.Выбрать();
		//
		//Пока Выборка.Следующий() Цикл
		//	ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Выборка.Ссылка);
		//КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьКуратора(Куратор)
	УзелПланаОбмена = ПланыОбмена.ОбменFirestore.НайтиПоКоду("DBG");
	ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Куратор);
КонецПроцедуры

Процедура ТК_ПриЗаписиПеремещениеТоваровПриЗаписи(Источник, Отказ) Экспорт
	
	//Возврат;
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляПечати = ПроверкаНеобходимостиПечатиМЛ(Источник);
	Если НЕ ДанныеДляПечати.Пустая() Тогда
		РаспечататьМаршрутныйЛист(ДанныеДляПечати,Источник);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти


#Область ЗаменаСсылокПоИнформационнойБазе


// Заменяет ссылки по информационной базе
//Параметры:
//	ПарыЗамены - Соответствие - ключи содержат замещаемых, значения содержат заменители
//	Исключения - Массив - необязателен, значения типа ОбъектМетаданных, в экземплярах которых замены проводить нельзя
Процедура ЗаменитьСсылки(ПарыЗамен, Исключения = Неопределено) Экспорт
	Английский = Метаданные.СвойстваОбъектов.ВариантВстроенногоЯзыка.Английский;
	ДвиженияССубконтоИмя = ?(Метаданные.ВариантВстроенногоЯзыка = Английский, ".RecordsWithExtDimensions", ".ДвиженияССубконто");
	
	Если Исключения = Неопределено Тогда
		Исключения = Новый Массив;
	КонецЕсли;
	
	// [ссылающийся объект](.Метаданные, .Замены[(.Замещаемое, .Заменитель)], .ТипыЗамещаемых[])
	ИндексЗамены = ИндексЗамены(ПарыЗамен);
	КешПолей = Новый Соответствие;
	// обходим индекс и в каждом ключе-объекта полностью замещаем все ссылки, подлежащие замене
	Для Каждого УзелЗамены Из ИндексЗамены Цикл
		Ссылка = УзелЗамены.Ключ;
		МетаданныеУзла = УзелЗамены.Значение.Метаданные;
		Замены = УзелЗамены.Значение.Замены;
		ТипыЗамещаемых = УзелЗамены.Значение.ТипыЗамещаемых;
		
		Если Исключения.Найти(МетаданныеУзла) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоСсылочныйОбъектМетаданных(МетаданныеУзла) Тогда
			// любой ссылочный объект
			ПолноеИмя = МетаданныеУзла.ПолноеИмя();
			ОбъектДанных = Ссылка.ПолучитьОбъект();
			ИменаПолей = ИменаПолейСТипами(КешПолей, ПолноеИмя, ТипыЗамещаемых, "Ссылка, Ref");
			ЗаменитьЗначения(ОбъектДанных, ИменаПолей, Замены);
			// табчасти объекта
			ЗаменитьВТабчастях(
			КешПолей, МетаданныеУзла.ТабличныеЧасти, ОбъектДанных, ПолноеИмя, Замены, ТипыЗамещаемых, Исключения);
			// стандартные табчасти планов
			Если Метаданные.ПланыСчетов.Содержит(МетаданныеУзла) Или Метаданные.ПланыВидовРасчета.Содержит(МетаданныеУзла) Тогда
				ЗаменитьВТабчастях(
				КешПолей, МетаданныеУзла.СтандартныеТабличныеЧасти, ОбъектДанных, ПолноеИмя, Замены, ТипыЗамещаемых, Исключения);
			КонецЕсли;
			// пишем сам объект
			ЗаписатьДанные(ОбъектДанных);
			ОбъектДанных = Неопределено;
		ИначеЕсли Метаданные.Константы.Содержит(МетаданныеУзла) Тогда
			// значения в константах
			Константа = Константы[МетаданныеУзла.Имя];
			Константа.Установить(НовоеЗначение(Константа.Получить(), Замены));
		ИначеЕсли Метаданные.РегистрыСведений.Содержит(МетаданныеУзла) Тогда
			// необъектные таблицы
			ИменаПолей = ИменаПолейСТипами(КешПолей, МетаданныеУзла.ПолноеИмя(), ТипыЗамещаемых);
			Отборы = ОтборыРегистраСведений(МетаданныеУзла, Ссылка);
			Набор = НаборЗаписей(РегистрыСведений[МетаданныеУзла.Имя], Отборы);
			
			Таблица = Набор.Выгрузить();
			Набор.Очистить();
			ЗаписатьДанные(Набор);
			
			ЗаменитьЗначения(Таблица[0], ИменаПолей, Замены);
			Для Каждого ИмяПоля Из ИменаПолей Цикл
				Если Не Отборы.Свойство(ИмяПоля) Тогда
					Продолжить;
				КонецЕсли;
				Набор.Отбор[ИмяПоля].Установить(НовоеЗначение(Отборы[ИмяПоля], Замены));
			КонецЦикла;
			Набор.Загрузить(Таблица);
			ЗаписатьДанные(Набор);
		КонецЕсли;
		// обработка движений документа
		Если Метаданные.Документы.Содержит(МетаданныеУзла) Тогда
			Для Каждого Движение Из МетаданныеУзла.Движения Цикл
				ДопТаблица = "";
				Если Исключения.Найти(Движение) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Если Метаданные.РегистрыНакопления.Содержит(Движение) Тогда
					Регистр = РегистрыНакопления[Движение.Имя];
				ИначеЕсли Метаданные.РегистрыСведений.Содержит(Движение) Тогда
					Регистр = РегистрыСведений[Движение.Имя];
				ИначеЕсли Метаданные.РегистрыБухгалтерии.Содержит(Движение) Тогда
					ДопТаблица = ДвиженияССубконтоИмя;
					Регистр = РегистрыБухгалтерии[Движение.Имя];
				ИначеЕсли Метаданные.РегистрыРасчета.Содержит(Движение) Тогда
					Регистр = РегистрыРасчета[Движение.Имя];
				КонецЕсли;
				ЗаменитьВПодчиненномРегистре(КешПолей, Регистр, Ссылка, Движение.ПолноеИмя() + ДопТаблица, Замены, ТипыЗамещаемых);
			КонецЦикла;
			// обработка последовательностей, включающих документ
			Для Каждого Движение Из Метаданные.Последовательности Цикл
				Если Исключения.Найти(Движение) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Если Движение.Документы.Содержит(МетаданныеУзла) Тогда
					ЗаменитьВПодчиненномРегистре(
					КешПолей, Последовательности[Движение.Имя], Ссылка, Движение.ПолноеИмя(), Замены, ТипыЗамещаемых);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


// Строим соответствие вида [ссылающийся объект](.Метаданные, .Замены[(.Замещаемое, .Заменитель)], .ТипыЗамещаемых[])
// в итоге представляем результаты поиска по ссылкам в индексе с ключом-объектом, содержащим замещаемые ссылки
Функция ИндексЗамены(ПарыЗамен)
	
	СписокСсылок = Новый Массив;
	Для Каждого Пара Из ПарыЗамен Цикл
		СписокСсылок.Добавить(Пара.Ключ);
	КонецЦикла;
	РезультатыПоиска = НайтиПоСсылкам(СписокСсылок);
	// (.Ссылка: исходная ссылка; .Данные: ссылающийся объект; .Метаданные: метаданные ссылающегося объекта)
	
	ИндексЗамены = Новый Соответствие;
	Для Каждого Результат Из РезультатыПоиска Цикл
		УзелЗамены = ИндексЗамены[Результат.Данные];
		Если Неопределено = УзелЗамены Тогда
			УзелЗамены =
			Новый Структура("Метаданные, Замены, ТипыЗамещаемых", Результат.Метаданные, Новый Массив, Новый Массив);
			ИндексЗамены.Вставить(Результат.Данные, УзелЗамены);
		КонецЕсли;
		
		УзелЗамены.Замены.Добавить(
		Новый Структура("Замещаемое, Заменитель", Результат.Ссылка, ПарыЗамен[Результат.Ссылка]));
		
		ТипЗамещаемого = ТипЗнч(Результат.Ссылка);
		Если Неопределено = УзелЗамены.ТипыЗамещаемых.Найти(ТипЗамещаемого) Тогда
			УзелЗамены.ТипыЗамещаемых.Добавить(ТипЗамещаемого);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИндексЗамены;
КонецФункции

Процедура ЗаменитьВТабчастях(КешПолей, ОписанияТабчастей, Объект, ИмяОсновнойТаблицы, Замены, ТипыЗамещаемых, Исключения)
	Для Каждого Описание Из ОписанияТабчастей Цикл
		Если Исключения.Найти(Описание) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИменаПолей = ИменаПолейСТипами(КешПолей, ИмяОсновнойТаблицы + "." + Описание.Имя, ТипыЗамещаемых, "Ссылка, Ref");
		Для Каждого Табстрока Из Объект[Описание.Имя] Цикл
			ЗаменитьЗначения(Табстрока, ИменаПолей, Замены);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаменитьВПодчиненномРегистре(КешПолей, МенеджерРегистра, Ссылка, ИмяТаблицыРегистра, Замены, ТипыЗамещаемых)
	ИменаПолей = ИменаПолейСТипами(КешПолей, ИмяТаблицыРегистра, ТипыЗамещаемых, "Регистратор, Recorder");
	Набор = НаборЗаписей(МенеджерРегистра, Новый Структура("Регистратор", Ссылка));
	ЗначениеЗаменено = Ложь;
	Для Каждого Запись Из Набор Цикл
		ЗаменитьЗначения(Запись, ИменаПолей, Замены, ЗначениеЗаменено);
	КонецЦикла;
	ЗаписатьДанные(Набор, ЗначениеЗаменено);
КонецПроцедуры

Функция ИменаПолейСТипами(КешПолейТаблиц, ИмяТаблицы, ТипыДанных, ИменаИсключений = "")
	ИменаПолей = Новый Массив;
	
	ТекстЗапроса = СтрЗаменить("ВЫБРАТЬ * ИЗ ТаблицаВыборки КАК Т ГДЕ Ложь", "ТаблицаВыборки", ИмяТаблицы);
	ПоляТаблицы = КешПолейТаблиц.Получить(ИмяТаблицы);
	Если Неопределено = ПоляТаблицы Тогда
		Запрос = Новый Запрос(ТекстЗапроса);
		ПоляТаблицы = Запрос.Выполнить().Колонки;
		КешПолейТаблиц.Вставить(ИмяТаблицы, ПоляТаблицы);
	КонецЕсли;
	
	Исключения = Новый Структура(ИменаИсключений);
	Для Каждого Поле Из ПоляТаблицы Цикл
		Если Исключения.Свойство(Поле.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ТипДанных Из ТипыДанных Цикл
			Если Поле.ТипЗначения.СодержитТип(ТипДанных) И Неопределено = ИменаПолей.Найти(Поле.Имя) Тогда
				ИменаПолей.Добавить(Поле.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ИменаПолей;
КонецФункции

Процедура ЗаменитьЗначения(Данные, ИменаПолей, Замены, Заменено = Ложь)
	Для Каждого ИмяПоля Из ИменаПолей Цикл
		НовоеЗначение = НовоеЗначение(Данные[ИмяПоля], Замены);
		Если НовоеЗначение <> Данные[ИмяПоля] Тогда;
			Заменено = Истина;
			Данные[ИмяПоля] = НовоеЗначение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция НовоеЗначение(СтароеЗначение, Замены)
	Для Каждого Замена Из Замены Цикл
		Если СтароеЗначение = Замена.Замещаемое Тогда
			Возврат Замена.Заменитель;
		КонецЕсли;
	КонецЦикла;
	Возврат СтароеЗначение;
КонецФункции

Процедура ЗаписатьДанные(Данные, Принудительно = Ложь)
	Если Данные.Модифицированность() Или Принудительно Тогда
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
	КонецЕсли;
КонецПроцедуры

Функция ОтборыРегистраСведений(МетаданныеРегистра, Запись)
	Отборы = Новый Структура;
	Если МетаданныеРегистра.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
		Отборы.Вставить("Период", Запись.Период);
	КонецЕсли;
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		Отборы.Вставить(Измерение.Имя, Запись[Измерение.Имя]);
	КонецЦикла;
	Возврат Отборы;
КонецФункции

Функция НаборЗаписей(МенеджерРегистра, Отборы)
	Набор = МенеджерРегистра.СоздатьНаборЗаписей();
	Для Каждого Отбор Из Отборы Цикл
		Набор.Отбор[Отбор.Ключ].Установить(Отбор.Значение);
	КонецЦикла;
	Набор.Прочитать();
	Возврат Набор;
КонецФункции

Функция ЭтоСсылочныйОбъектМетаданных(ОбъектМетаданных)
	Возврат Метаданные.Справочники.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.Документы.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.Перечисления.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.ПланыВидовХарактеристик.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.ПланыСчетов.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.ПланыВидовРасчета.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.БизнесПроцессы.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.Задачи.Содержит(ОбъектМетаданных)
	ИЛИ Метаданные.ПланыОбмена.Содержит(ОбъектМетаданных);
КонецФункции

//ТК_ПриЗаписиПеремещениеТоваровПриЗаписи

Функция ПроверкаНеобходимостиПечатиМЛ(Источник)
	
	Если ТипЗнч(Источник.ДокументОснование) <> Тип("ДокументСсылка.ЗаказПокупателя") Или 
		ТипЗнч(Источник.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") и Источник.ДокументОснование.ТочкаДоставки.ПорядокСигаретный = 0 Тогда
		Возврат Справочники.ДанныеДляПечатиМашрутныхЛистов.ПустаяСсылка();		                                                                         	
	КонецЕсли; 
	
	Маршрут = Окр(Источник.ДокументОснование.ТочкаДоставки.ПорядокСигаретный/100,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляПечатиМашрутныхЛистов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДанныеДляПечатиМашрутныхЛистов КАК ДанныеДляПечатиМашрутныхЛистов
	|ГДЕ
	|	НЕ ДанныеДляПечатиМашрутныхЛистов.ПометкаУдаления
	|	И ДанныеДляПечатиМашрутныхЛистов.Склад = &Склад
	|	И ДанныеДляПечатиМашрутныхЛистов.Маршрут = &Маршрут
	|	И ДанныеДляПечатиМашрутныхЛистов.Организация = &Организация
	|	И НЕ ДанныеДляПечатиМашрутныхЛистов.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	Запрос.УстановитьПараметр("Склад", Источник.СкладКомпании);
	Запрос.УстановитьПараметр("Организация", Источник.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ДанныеДляПечатиМашрутныхЛистов.ПустаяСсылка();
	Иначе 
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли; 	
	
КонецФункции

Процедура РаспечататьМаршрутныйЛист(ДанныеДляПечати,Источник)
	
	Маршрут = Окр(Источник.ДокументОснование.ТочкаДоставки.ПорядокСигаретный/100,0);
	
	Результат = новый Структура;
	Результат.Вставить("ВсеОк", Ложь);
	Результат.Вставить("ДанныеДляПечати", ДанныеДляПечати);
	Результат.Вставить("Маршрут", Маршрут);
	Результат.Вставить("Источник", Источник);
	
	Если НЕ ПроверкаПоследнийЗаказ(Результат) Тогда
		Возврат;	
	КонецЕсли;
	
	СоздатьТТН(Результат);
	
	Если Результат.ВсеОк Тогда
		РаспечататьТТН(Результат);
	КонецЕсли; 
	
КонецПроцедуры  

Функция ПроверкаПоследнийЗаказ(Результат)
	
	НачалоМаршрута = Результат.Маршрут*100;
	КонецМаршрута = (Результат.Маршрут+1)*100;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Вт_Заказы
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Проведен
	|	И ЗаказПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаказПокупателя.ТочкаДоставки.ПорядокСигаретный МЕЖДУ &НачалоМаршрута И &КонецМаршрута
	|	И ЗаказПокупателя.СкладКомпании = &СкладКомпании
	|	И ЗаказПокупателя.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК ДокументОтгрузки,
	|	РеализацияТоваров.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Вт_Отгрузки
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И РеализацияТоваров.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровРаспределениеОтгрузки.Ссылка,
	|	РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя
	|ИЗ
	|	Документ.РеализацияТоваров.РаспределениеОтгрузки КАК РеализацияТоваровРаспределениеОтгрузки
	|ГДЕ
	|	РеализацияТоваровРаспределениеОтгрузки.Ссылка.Проведен
	|	И РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЗаказаПокупателя.Ссылка,
	|	КорректировкаЗаказаПокупателя.ДокументОснование
	|ИЗ
	|	Документ.КорректировкаЗаказаПокупателя КАК КорректировкаЗаказаПокупателя
	|ГДЕ
	|	КорректировкаЗаказаПокупателя.Проведен
	|	И КорректировкаЗаказаПокупателя.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка,
	|	ПеремещениеТоваров.ДокументОснование
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Заказы.Ссылка КАК Заказ,
	|	Вт_Отгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ПОМЕСТИТЬ Вт_Итог
	|ИЗ
	|	Вт_Заказы КАК Вт_Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Отгрузки КАК Вт_Отгрузки
	|		ПО Вт_Заказы.Ссылка = Вт_Отгрузки.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Итог.Заказ КАК Заказ
	|ИЗ
	|	Вт_Итог КАК Вт_Итог
	|ГДЕ
	|	Вт_Итог.ДокументОтгрузки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Заказы.Ссылка КАК Ссылка
	|ИЗ
	|	Вт_Заказы КАК Вт_Заказы";
	
	Запрос.УстановитьПараметр("НачалоМаршрута", НачалоМаршрута);
	Запрос.УстановитьПараметр("КонецМаршрута", КонецМаршрута);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Результат.Источник.Дата)-2*24*60*60);
	Запрос.УстановитьПараметр("КонецПериода", Результат.Источник.Дата);
	Запрос.УстановитьПараметр("СкладКомпании", Результат.Источник.СкладКомпании);
	Запрос.УстановитьПараметр("Организация", Результат.Источник.Организация);
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	Если МассивРезультатовЗапроса[МассивРезультатовЗапроса.ВГраница()].Пустой()  Тогда
		Возврат Ложь;
	КонецЕсли; 	
	
	Если МассивРезультатовЗапроса[МассивРезультатовЗапроса.ВГраница()-1].Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли; 	
	
КонецФункции  

Процедура СоздатьТТН(Результат)
	
	ДанныеДляТТН = Справочники.ДанныеДляПечатиМашрутныхЛистов.ПолучитьДанныеДляПечати(Результат.Маршрут,Результат.Источник.СкладКомпании, Результат.Источник.Организация);
	Если ДанныеДляТТН.Количество() = 0 Тогда
		МассивПользователей = Результат.ДанныеДляПечати.Пользователи.Выгрузить().ВыгрузитьКолонку("Пользователь");
		
		Уведомление = "Не удалось распечатать пакет документов по маршруту "+Результат.Маршрут+" для склада "+ Результат.Источник.СкладКомпании+". Нет данных для печати!!!";
		ОтправитьУведомлениеПользователям(Уведомление,МассивПользователей);
		Возврат;	
	КонецЕсли; 
	
	ТаблицаОтгрузок = ПолучитьОтгрузкиПоМаршруту(Результат.Маршрут,Результат.Источник);
	
	Если ТаблицаОтгрузок.Количество()=0 Тогда
		Возврат;		
	КонецЕсли;  
	
	НомерПрав = Справочники.ФизическиеЛица.ПолучитьНомерВодительскогоУдостоверения(ДанныеДляТТН[0].Водитель, Результат.Источник.Дата);
	РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(ТекущаяДатаСеанса(), Результат.Источник.Организация);
	
	Док = Документы.ТК_ТТН.СоздатьДокумент();
	Док.Дата = ТекущаяДатаСеанса();
	ЗаполнитьЗначенияСвойств(Док, ДанныеДляТТН[0]);
	Док.УстановитьНовыйНомер();
	Док.Автор = Пользователи.АвторизованныйПользователь();
	
	Док.ПодразделениеКомпании = Результат.Источник.ПодразделениеКомпании;
	Док.Отправитель = Результат.Источник.Организация.НаименованиеПолное;
	Док.НомерПрав = НомерПрав;
	Док.ДолжностьОтветственногоОтправителя = "Директор";
	Док.ОтветственныйОтправитель = РеквизитыОрганизации.Руководитель; 
	Док.ПунктПолучатель = "Згідно накладної";
	Док.Статус = Перечисления.СтатусыТТН.Отгружен;
	Док.Комментарий = "Создано процедурой печати маршрутных листов";
	
	НовыйРеквизит = Док.ДополнительныеРеквизиты.Добавить();
	НовыйРеквизит.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Маршрут");
	НовыйРеквизит.Значение = Результат.Маршрут;
	
	Для Каждого стр Из ТаблицаОтгрузок Цикл
		НоваяСтрока = Док.Источники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
	КонецЦикла; 
	
	Попытка
		Док.Записать();
		Результат.Вставить("ВсеОК", Истина);
	Исключение
		Док.Записать(РежимЗаписиДокумента.Запись);
		Результат.Вставить("ВсеОК", Ложь);
	КонецПопытки;
	
	Результат.Вставить("ТТН", Док);
	
КонецПроцедуры 

Процедура РаспечататьТТН(Результат)
	
	МассивТТН = Новый массив;
	МассивТТН.Добавить(Результат.ТТН);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	ТекТабДок = Документы.ТК_ТТН.ПечатьМаршрутныйЛист(Результат.ТТН.Ссылка, Новый СписокЗначений,Новый Структура);
	ТабличныйДокумент.Присоединить(ТекТабДок);
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла(".PDF");
	
	ТабличныйДокумент.Записать(ИмяВрФайла,ТипФайлаТабличногоДокумента.PDF);
	Ошибка = "";
	ТК_ТСД_РаботаHTTP_Сервис.НапечататьФайл(ИмяВрФайла,1,Результат.ДанныеДляПечати.Принтер,Ошибка);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	МассивОбьектов = Результат.ТТН.Источники.ВыгрузитьКолонку("Отгрузка");
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("QR_Заголовок", Истина);
	ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
	ПараметрыПечати.Вставить("ВыводитьТМ", Истина);
	ПараметрыПечати.Вставить("ВыводитьМРЦ", Истина);
	ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Истина);
	ПараметрыПечати.Вставить("ИтогиПоБазовомуКоличеству", Истина);
	
	Для Каждого стр Из МассивОбьектов  Цикл
		Для Сч=0 По 2 Цикл
			Если ТипЗнч(МассивОбьектов[0])= Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
				ТекТабДок = Документы.ПеремещениеТоваров.ПечатьПеремещениеТоваров(стр, Новый СписокЗначений,Новый Структура);
			ИначеЕсли ТипЗнч(МассивОбьектов[0]) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
				ТекТабДок = Документы.РеализацияТоваров.ПечатьРасходнаяНакладная(стр, Новый СписокЗначений,ПараметрыПечати);
			КонецЕсли; 
			ТабличныйДокумент.Присоединить(ТекТабДок);
		КонецЦикла; 
	КонецЦикла; 
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла(".PDF");
	
	ТабличныйДокумент.Записать(ИмяВрФайла,ТипФайлаТабличногоДокумента.PDF);
	Ошибка = "";
	ТК_ТСД_РаботаHTTP_Сервис.НапечататьФайл(ИмяВрФайла,1,Результат.ДанныеДляПечати.Принтер,Ошибка);
	
	МассивПользователей = Результат.ДанныеДляПечати.Пользователи.Выгрузить().ВыгрузитьКолонку("Пользователь");
	Уведомление = "";
	
	Если Ошибка <> "" Тогда
		Ошибка = "Не удалось распечатать документы по маршруту "+Результат.Маршрут+", "+Ошибка;
		ОтправитьУведомлениеПользователям(Ошибка,МассивПользователей);
	Иначе
		Уведомление = "Распечатан пакет документов по маршруту "+Результат.Маршрут+" для склада "+ Результат.Источник.СкладКомпании;
		ОтправитьУведомлениеПользователям(Уведомление,МассивПользователей);
	КонецЕсли; 
	
КонецПроцедуры 

Функция ПолучитьОтгрузкиПоМаршруту(Маршрут,Источник)
	
	НачалоМаршрута = Маршрут*100;
	КонецМаршрута = (Маршрут+1)*100;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Вт_Заказы
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Проведен
	|	И ЗаказПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаказПокупателя.ТочкаДоставки.ПорядокСигаретный МЕЖДУ &НачалоМаршрута И &КонецМаршрута
	|	И ЗаказПокупателя.СкладКомпании = &СкладКомпании
	|	И ЗаказПокупателя.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателя.Ссылка КАК Резерв,
	|	РезервированиеЗаказовПокупателя.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Вт_Резервы
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	|ГДЕ
	|	РезервированиеЗаказовПокупателя.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И РезервированиеЗаказовПокупателя.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка,
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя.РаспределениеОтгрузки КАК РезервированиеЗаказовПокупателяРаспределениеОтгрузки
	|ГДЕ
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК ДокументОтгрузки,
	|	РеализацияТоваров.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Вт_Отгрузки
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И РеализацияТоваров.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровРаспределениеОтгрузки.Ссылка,
	|	РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя
	|ИЗ
	|	Документ.РеализацияТоваров.РаспределениеОтгрузки КАК РеализацияТоваровРаспределениеОтгрузки
	|ГДЕ
	|	РеализацияТоваровРаспределениеОтгрузки.Ссылка.Проведен
	|	И РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка,
	|	ПеремещениеТоваров.ДокументОснование
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.ДокументОснование В
	|			(ВЫБРАТЬ
	|				Вт_Заказы.Ссылка КАК Ссылка
	|			ИЗ
	|				Вт_Заказы КАК Вт_Заказы)
	|	И ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТК_ТТНИсточники.Документ КАК Документ,
	|	ТК_ТТНИсточники.Отгрузка КАК Отгрузка
	|ПОМЕСТИТЬ Вт_ДокументыВТТН
	|ИЗ
	|	Документ.ТК_ТТН.Источники КАК ТК_ТТНИсточники
	|ГДЕ
	|	ТК_ТТНИсточники.Отгрузка В
	|			(ВЫБРАТЬ
	|				Вт_Отгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|			ИЗ
	|				Вт_Отгрузки КАК Вт_Отгрузки)
	|	И НЕ ТК_ТТНИсточники.Ссылка.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Заказы.Ссылка КАК Заказ,
	|	Вт_Резервы.Резерв КАК Резерв,
	|	Вт_Отгрузки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Вт_ДокументыВТТН.Отгрузка КАК ОтгрузкаТТН
	|ПОМЕСТИТЬ Вт_Итог
	|ИЗ
	|	Вт_Заказы КАК Вт_Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Резервы КАК Вт_Резервы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Вт_ДокументыВТТН КАК Вт_ДокументыВТТН
	|			ПО Вт_Резервы.Резерв = Вт_ДокументыВТТН.Документ
	|		ПО Вт_Заказы.Ссылка = Вт_Резервы.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Отгрузки КАК Вт_Отгрузки
	|		ПО Вт_Заказы.Ссылка = Вт_Отгрузки.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вт_Итог.Резерв КАК Документ,
	|	Вт_Итог.ДокументОтгрузки КАК Отгрузка
	|ИЗ
	|	Вт_Итог КАК Вт_Итог
	|ГДЕ
	|	Вт_Итог.ОтгрузкаТТН ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("НачалоМаршрута", НачалоМаршрута);
	Запрос.УстановитьПараметр("КонецМаршрута", КонецМаршрута);
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Источник.Дата)-2*24*60*60);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Источник.Дата)+24*60*60);
	Запрос.УстановитьПараметр("Организация", Источник.Организация);
	Запрос.УстановитьПараметр("СкладКомпании", Источник.СкладКомпании);
	
	ТаблицаОтгрузок = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаОтгрузок;
	
	
КонецФункции 

Процедура ОтправитьУведомлениеПользователям(Уведомление,МассивПользователей)
	
	Сообщение = Обсуждения.ОписаниеСообщения(Уведомление);
	ПользовательАдмин = Справочники.Пользователи.НайтиПоНаименованию("admin");
	
	Для Каждого Получатель Из МассивПользователей Цикл
		НовыйМассив = Новый Массив;
		НовыйМассив.Добавить(Получатель);
		Обсуждения.ОтправитьСообщение(ПользовательАдмин,НовыйМассив,Сообщение);	
	КонецЦикла; 
	
КонецПроцедуры 

//ТК_ПриЗаписиПеремещениеТоваровПриЗаписи


#КонецОбласти

Процедура ИнициализироватьРеквизитыФормыДляПолнотекстовогоПоиска(Форма, ИмяФОИспользованияППД) Экспорт
	
	Форма.ИнформационнаяБазаФайловая      = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	Форма.ИспользоватьПолнотекстовыйПоиск = ОбщегоНазначенияТКВызовСервера.ИспользуетсяПолнотекстовыйПоиск(ИмяФОИспользованияППД);
	
	Если Форма.ИспользоватьПолнотекстовыйПоиск Тогда
		
		Форма.ИндексПолнотекстовогоПоискаАктуален = ПолнотекстовыйПоискСервер.ИндексПоискаАктуален();
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЧастиИмени(ФамилияИмяОтчество) Экспорт 
	
	Результат = Новый Структура("Фамилия,Имя,Отчество");
	
	ЧастиИмени = СтрРазделить(ФамилияИмяОтчество, " ", Ложь);
	
	Если ЧастиИмени.Количество() >= 1 Тогда
		Результат.Фамилия = ЧастиИмени[0];
	КонецЕсли;
	
	Если ЧастиИмени.Количество() >= 2 Тогда
		Результат.Имя = ЧастиИмени[1];
	КонецЕсли;
	
	Если ЧастиИмени.Количество() >= 3 Тогда
		Результат.Отчество = ЧастиИмени[2];
	КонецЕсли;
	
	Если ЧастиИмени.Количество() > 3 Тогда
		ДополнительныеЧастиОтчества = Новый Массив;
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'оглы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'улы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'уулу'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'кызы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'гызы'"));
		ДополнительныеЧастиОтчества.Добавить(НСтр("ru = 'угли'"));
		
		Если ДополнительныеЧастиОтчества.Найти(НРег(ЧастиИмени[3])) <> Неопределено Тогда
			Результат.Отчество = Результат.Отчество + " " + ЧастиИмени[3];
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//Функция возвращает ФИО в Формате Фамилия И.О.
Функция ФамилияИнициалы(Знач ФамилияИмяОтчество) Экспорт 
	
	Если ТипЗнч(ФамилияИмяОтчество) = Тип("Строка") Тогда
		ФамилияИмяОтчество = ЧастиИмени(ФамилияИмяОтчество);
	Иначе
		Возврат "";
	КонецЕсли;
	
	Фамилия = ФамилияИмяОтчество.Фамилия;
	Имя = ФамилияИмяОтчество.Имя;
	Отчество = ФамилияИмяОтчество.Отчество;
	
	Если ПустаяСтрока(Имя) Тогда
		Возврат Фамилия;
	КонецЕсли;
	
	Если ПустаяСтрока(Отчество) Тогда
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2.", Фамилия, Лев(Имя, 1));
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1. %2. %3", Фамилия, Лев(Имя, 1), Лев(Отчество, 1));
	
КонецФункции

// Осуществляет проверку заполненности характеристики.
//
// Параметры:
// 		Дата	- Дата документа.
// 		Склад	- склад документа.
// 		ТЧ		- табличная часть документа.
//
// Возвращаемое значение:
// 		СписокЗначений - номера строк с не заполненной харрактеристикой где это нужно
//
Функция ПроверитьЗаполнениеХарактеристикиТЧ(НаДату, Склад, ТЧ) Экспорт
	
	спНомеровСтрок = Новый СписокЗначений;
	
	Если НЕ Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(Склад, НаДату) Тогда
		Для Каждого строка Из ТЧ Цикл
			Если строка.Номенклатура.ИспользованиеХарактеристик и НЕ ЗначениеЗаполнено(строка.ХарактеристикаНоменклатуры) Тогда
				спНомеровСтрок.Добавить(строка.НомерСтроки);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат спНомеровСтрок;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕРКИ НАЛИЧИЯ И ЗАПОЛНЕНИЯ РЕВИЗИТОВ



// Позволяет определить есть ли среди реквизитов шапки документа
// реквизит с переданным именем.
//
// Параметры:
//  ИмяРеквизита - строковое имя искомого реквизита,
//  МетаданныеДокумента - объект описания метаданных документа, среди реквизитов которого производится поиск.
//
// Возвращаемое значение:
//  Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента) Экспорт
	
	Возврат НЕ (МетаданныеДокумента.Реквизиты.Найти(ИмяРеквизита) = Неопределено);
	
КонецФункции // ЕстьРеквизитДокумента()

// Позволяет определить есть ли табличная часть документа с переданным именем.
//
// Параметры:
//  ИмяТабЧасти - строковое имя искомой табличной части,
//  МетаданныеДокумента - объект описания метаданных документа, среди реквизитов которого производится поиск.
//
// Возвращаемое значение:
//  Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьТабЧастьДокумента(ИмяТабЧасти, МетаданныеДокумента) Экспорт
	
	Возврат НЕ (МетаданныеДокумента.ТабличныеЧасти.Найти(ИмяТабЧасти) = Неопределено);
	
КонецФункции // ЕстьТабЧастьДокумента()

// Позволяет определить есть ли среди реквизитов табличной части документа
// реквизит с переданным именем.
//
// Параметры:
//  ИмяРеквизита - строковое имя искомого реквизита,
//  МетаданныеДокумента - объект описания метаданных документа, среди реквизитов которого производится поиск.
//  ИмяТабЧасти  - строковое имя табличной части документа, среди реквизитов которого производится поиск
//
// Возвращаемое значение:
//  Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьРеквизитТабЧастиДокумента(ИмяРеквизита, МетаданныеДокумента, ИмяТабЧасти) Экспорт
	
	ТабЧасть = МетаданныеДокумента.ТабличныеЧасти.Найти(ИмяТабЧасти);
	
	Если ТабЧасть = Неопределено Тогда // Нет такой таб. части в документе
		Возврат Ложь;
		
	Иначе
		Возврат НЕ (ТабЧасть.Реквизиты.Найти(ИмяРеквизита) = Неопределено);
		
	КонецЕсли;
	
КонецФункции // ЕстьРеквизитТабЧастиДокумента()

Функция ЕстьНезаполненныйРеквизитДокумента(ИмяРеквизита, ДокументОбъект, МетаданныеДокумента) Экспорт
	
	Результат =
	ОбщегоНазначенияТК.ЕстьРеквизитДокумента(ИмяРеквизита, МетаданныеДокумента)
	И НЕ ЗначениеЗаполнено(ДокументОбъект[ИмяРеквизита]);
	
	Возврат Результат;
	
КонецФункции

Функция ЕстьНезаполненныйРеквизитТабЧастиДокумента(ИмяРеквизита, СтрокаТабЧасти, МетаданныеДокумента, ИмяТабЧасти) Экспорт
	
	Результат =
	ЕстьРеквизитТабЧастиДокумента(ИмяРеквизита, МетаданныеДокумента, ИмяТабЧасти)
	И НЕ ЗначениеЗаполнено(СтрокаТабЧасти[ИмяРеквизита]);
	
	Возврат Результат;
	
КонецФункции

Функция СборкаРезерва(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТК_ОчередьНаСборкуРезервов.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
	|ГДЕ
	|	ТК_ОчередьНаСборкуРезервов.Документ = &Документ
	|	И ТК_ОчередьНаСборкуРезервов.Статус <> &Статус";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыСборкиРезервов.ГотовКСборке);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;		
	КонецЕсли;
	
КонецФункции	
// Удаляет из списка проверяемых реквизитов табличные части, если хотя бы одна из них заполнена
//
// Параметры:
//  Объект - ДокументОбъект, СправочникОбъект - объект с табличными частями
//  ИменаТабличныхЧастей - Массив, Строка - Перечень имен проверяемых табличных частей
//  ПроверяемыеРеквизиты - Массив - Перечень проверяемых реквизитов
Процедура ИсключитьИзПроверкиОсновныеТабличныеЧасти(Объект, Знач ИменаТабличныхЧастей, ПроверяемыеРеквизиты) Экспорт
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипЗнч(ИменаТабличныхЧастей) = Тип("Строка") Тогда
		ИменаТабличныхЧастей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТабличныхЧастей);
	КонецЕсли;
	
	ЕстьЗаписи = Ложь;
	Для Каждого ИмяТабличнойЧасти Из ИменаТабличныхЧастей Цикл
		
		Если Объект[СокрЛП(ИмяТабличнойЧасти)].Количество() > 0 Тогда
			ЕстьЗаписи = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьЗаписи Тогда
		Для Каждого ИмяТабличнойЧасти Из ИменаТабличныхЧастей Цикл
			МассивНепроверяемыхРеквизитов.Добавить(СокрЛП(ИмяТабличнойЧасти));
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// Удаляет из списка проверяемых реквизитов неиспользуемые табличные части и все их реквизиты
//
// Параметры:
//  ПроверяемыеРеквизиты - Массив - Перечень проверяемых реквизитов
//  НеИспользуемыеТабличныеЧасти - Массив - Перечень имен неиспользуемых табличных частей
Процедура ИсключитьИзПроверкиНеиспользуемыеТабличныеЧасти(ПроверяемыеРеквизиты, НеИспользуемыеТабличныеЧасти) Экспорт
	
	// Исключим из проверки сами списки
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НеИспользуемыеТабличныеЧасти);
	
	// Исключим из проверки колонки списков
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Для Каждого ИмяТабличнойЧасти Из НеИспользуемыеТабличныеЧасти Цикл
		ПрефиксРеквизита = ИмяТабличнойЧасти + ".";
		ДлинаПрефикса = СтрДлина(ПрефиксРеквизита);
		Для Каждого ПроверяемыйРеквизит Из ПроверяемыеРеквизиты Цикл
			Если СтрДлина(ПроверяемыйРеквизит) > ДлинаПрефикса 
				И Лев(ПроверяемыйРеквизит, ДлинаПрефикса) = ПрефиксРеквизита Тогда
				МассивНепроверяемыхРеквизитов.Добавить(ПроверяемыйРеквизит);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// Позволяет определить, есть ли среди реквизитов формы реквизит с переданным именем.
//
// Параметры:
//  Форма - УправляемаяФорма - форма, в которой ищется реквизит
//  ИмяРеквизита - имя реквизита для поиска
//
// Возвращаемое значение:
//  Истина - нашли реквизит с таким именем, Ложь - не нашли.
//
Функция ЕстьРеквизитФормы(Форма, ИмяРеквизита) Экспорт
	
	Для Каждого РеквизитФормы Из Форма.ПолучитьРеквизиты() Цикл
		Если ВРег(РеквизитФормы.Имя) = ВРег(ИмяРеквизита) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает значение второго параметра в случае, если значение первого параметра NULL.
// В противном случае будет возвращено значение первого параметра
//
// Параметры:
//  ПроверяемоеЗначение  - произвольный тип - проверяемое значение
//  ВозвращаемоеЗначение - произвольный тип - возвращаемое значение, если значение ПроверяемоеЗначение есть NULL
//
// Возвращаемое значение:
//  ПроверяемоеЗначение - если его значение не NULL, ВозвращаемоеЗначение - в ином случае.
//
Функция ЕстьNull(Знач ПроверяемоеЗначение, ВозвращаемоеЗначение) Экспорт
	
	Если ПроверяемоеЗначение = Null Тогда
		Возврат ВозвращаемоеЗначение;
	Иначе
		Возврат ПроверяемоеЗначение;
	КонецЕсли;
	
КонецФункции







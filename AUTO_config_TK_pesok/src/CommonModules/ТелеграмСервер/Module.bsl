Процедура ПолучитьОбновленияБота(Бот = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Бот", Бот);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмБоты.Ссылка КАК Бот,
	|	ТелеграмБоты.Жетон КАК Жетон,
	|	ТелеграмБоты.Представление КАК Имя,
	|	ТелеграмБоты.КаталогСохраненияФайлов КАК КаталогСохраненияФайлов,
	|	ЕСТЬNULL(ТелеграмНастройкиБотов.Смещение, 0) КАК Смещение
	|ИЗ
	|	Справочник.ТелеграмБоты КАК ТелеграмБоты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТелеграмНастройкиБотов КАК ТелеграмНастройкиБотов
	|		ПО ТелеграмБоты.Ссылка = ТелеграмНастройкиБотов.Бот
	|ГДЕ
	|	(ТелеграмБоты.Ссылка = &Бот
	|			ИЛИ &Бот = НЕОПРЕДЕЛЕНО)
	|	И ТелеграмБоты.Статус = ЗНАЧЕНИЕ(Перечисление.ТелеграмСтатусыИспользования.Используется)
	|	И ТелеграмБоты.СпособПолученияОбновлений = ЗНАЧЕНИЕ(Перечисление.ТелеграмСпособыПолученияОбновлений.ПериодическийЗапрос)";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		ТекстИсключения = СтрШаблон("Действующий бот %1 не найден", Бот);
		ВызватьИсключение ТекстИсключения;
	Иначе
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеБота = Новый Структура("Бот, Жетон, Имя, КаталогСохраненияФайлов, Смещение");
			ЗаполнитьЗначенияСвойств(ДанныеБота, Выборка);
			ВходящиеОбновления = ПолучитьВходящиеОбновленияБота(ДанныеБота);
			Если ТипЗнч(ВходящиеОбновления) = Тип("ОбъектXDTO") Тогда
				ОбработатьВходящиеОбновленияБота(ВходящиеОбновления, ДанныеБота);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Функция ПолучитьДанныеБотаПоКлючу(ВебхукКлюч) Экспорт
	
	ДанныеБота = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВебхукКлюч", ВебхукКлюч);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмБоты.Ссылка КАК Бот,
	|	ТелеграмБоты.Жетон КАК Жетон,
	|	ТелеграмБоты.Представление КАК Имя,
	|	ТелеграмБоты.КаталогСохраненияФайлов КАК КаталогСохраненияФайлов,
	|	ЕСТЬNULL(ТелеграмНастройкиБотов.Смещение, 0) КАК Смещение
	|ИЗ
	|	Справочник.ТелеграмБоты КАК ТелеграмБоты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТелеграмНастройкиБотов КАК ТелеграмНастройкиБотов
	|		ПО ТелеграмБоты.Ссылка = ТелеграмНастройкиБотов.Бот
	|ГДЕ
	|	ТелеграмБоты.ВебхукКлюч = &ВебхукКлюч
	|	И ТелеграмБоты.Статус = ЗНАЧЕНИЕ(Перечисление.ТелеграмСтатусыИспользования.Используется)
	|	И ТелеграмБоты.СпособПолученияОбновлений = ЗНАЧЕНИЕ(Перечисление.ТелеграмСпособыПолученияОбновлений.Вебхуки)";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеБота = Новый Структура("Бот, Жетон, Имя, КаталогСохраненияФайлов, Смещение");
		ЗаполнитьЗначенияСвойств(ДанныеБота, Выборка);
		Прервать;
	КонецЦикла;
	
	Возврат ДанныеБота;
	
КонецФункции


Функция ПараметрыЗаписиJSON() Экспорт
	
	ПереносСтрок = ПереносСтрокJSON.Авто;
	СимволыОтступа = Символы.Таб;
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрок, СимволыОтступа);
	Возврат ПараметрыЗаписиJSON;
	
КонецФункции


Функция ФорматироватьСтрокуJSON(СтрокаJSON) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	ЗначениеОбъект = ПрочитатьJSON(ЧтениеJSON);
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON());
	ЗаписатьJSON(ЗаписьJSON, ЗначениеОбъект);
	РезультатФорматирования = ЗаписьJSON.Закрыть();
	
	Возврат РезультатФорматирования;
	
КонецФункции


Функция ПреобразоватьТаблицуКонтекстаВСоответствие(ТаблицаКонтекста) Экспорт
	
	Ответ = Новый Соответствие;
	Для Каждого СтрокаТаблицы Из ТаблицаКонтекста Цикл
		Ответ.Вставить(СтрокаТаблицы.Параметр, СтрокаТаблицы.Значение);
	КонецЦикла;
	Возврат Ответ;
	
КонецФункции


Функция ЗначениеСтрокой(ЛюбоеЗначение) Экспорт
	
	Ответ = "";
	Если ТипЗнч(ЛюбоеЗначение) = Тип("Число") Тогда
		Ответ = Формат(ЛюбоеЗначение, "ЧГ=");
	Иначе
		Ответ = Строка(ЛюбоеЗначение);
	КонецЕсли;
	Возврат Ответ;
	
КонецФункции


Функция СравнениеИстинно(ЛевоеЗначение, ВидСравнения, ПравоеЗначение) Экспорт
	
	Перем Ответ;
	
	ВидыСравнения = Перечисления.ТелеграмВидыСравнения;
	
	ТипЛ = ТипЗнч(ЛевоеЗначение);
	ТипП = ТипЗнч(ПравоеЗначение);
	
	ТипЧисло 	= Тип("Число");
	ТипБулево 	= Тип("Булево");
	ТипСтрока 	= Тип("Строка");
	ТипДата 	= Тип("Дата");
	
	Ответ = Ложь;
	
	Если ВидСравнения = ВидыСравнения.Больше  Тогда
		Попытка
			Ответ = ЛевоеЗначение > ПравоеЗначение;
		Исключение
			Ответ = Ложь;
		КонецПопытки;
	ИначеЕсли ВидСравнения = ВидыСравнения.БольшеИлиРавно  Тогда
		Попытка
			Ответ = ЛевоеЗначение >= ПравоеЗначение;
		Исключение
			Ответ = Ложь;
		КонецПопытки;
	ИначеЕсли ВидСравнения = ВидыСравнения.Заполнено  Тогда
		Ответ = ЗначениеЗаполнено(ЛевоеЗначение);
	ИначеЕсли ВидСравнения = ВидыСравнения.Меньше  Тогда
		Попытка
			Ответ = ЛевоеЗначение < ПравоеЗначение;
		Исключение
			Ответ = Ложь;
		КонецПопытки;
	ИначеЕсли ВидСравнения = ВидыСравнения.МеньшеИлиРавно  Тогда
		Попытка
			Ответ = ЛевоеЗначение <= ПравоеЗначение;
		Исключение
			Ответ = Ложь;
		КонецПопытки;
	ИначеЕсли ВидСравнения = ВидыСравнения.НачинаетсяС  Тогда
		Если ТипЛ = ТипСтрока И ТипП = ТипСтрока Тогда
			ДлинаП = СтрДлина(ПравоеЗначение);
			ЧастьЛ = Лев(ЛевоеЗначение, ДлинаП);
			Ответ = (ВРег(ЧастьЛ) = ВРег(ПравоеЗначение));
		Иначе
			Ответ = Ложь;
		КонецЕсли;
	ИначеЕсли ВидСравнения = ВидыСравнения.НеЗаполнено  Тогда
		Ответ = НЕ ЗначениеЗаполнено(ЛевоеЗначение);
	ИначеЕсли ВидСравнения = ВидыСравнения.НеНачинаетсяС  Тогда
		Если ТипЛ = ТипСтрока И ТипП = ТипСтрока Тогда
			ДлинаП = СтрДлина(ПравоеЗначение);
			ЧастьЛ = Лев(ЛевоеЗначение, ДлинаП);
			Ответ = (ВРег(ЧастьЛ) <> ВРег(ПравоеЗначение));
		Иначе
			Ответ = Ложь;
		КонецЕсли;
	ИначеЕсли ВидСравнения = ВидыСравнения.НеРавно  Тогда
		Если ТипЗнч(ЛевоеЗначение) = ТипСтрока И ТипЗнч(ПравоеЗначение) = ТипСтрока Тогда
			Ответ = ВРег(ЛевоеЗначение) <> ВРег(ПравоеЗначение);
		Иначе
			Ответ = (ЛевоеЗначение <> ПравоеЗначение);
		КонецЕсли;
	ИначеЕсли ВидСравнения = ВидыСравнения.НеСодержит  Тогда
		Если ТипЛ = ТипСтрока И ТипП = ТипСтрока Тогда
			Ответ = (СтрНайти(ВРег(ЛевоеЗначение), ВРег(ПравоеЗначение)) = 0)
		Иначе
			Ответ = Ложь;
		КонецЕсли;
	ИначеЕсли ВидСравнения = ВидыСравнения.Равно  Тогда
		Если ТипЗнч(ЛевоеЗначение) = ТипСтрока И ТипЗнч(ПравоеЗначение) = ТипСтрока Тогда
			Ответ = ВРег(ЛевоеЗначение) = ВРег(ПравоеЗначение);
		Иначе
			Ответ = (ЛевоеЗначение = ПравоеЗначение);
		КонецЕсли;
	ИначеЕсли ВидСравнения = ВидыСравнения.Содержит  Тогда
		Если ТипЛ = ТипСтрока И ТипП = ТипСтрока Тогда
			Ответ = (СтрНайти(ВРег(ЛевоеЗначение), ВРег(ПравоеЗначение)) > 0)
		Иначе
			Ответ = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции


Функция ПолучитьВходящиеОбновленияБота(ДанныеБота) Экспорт
	
	СтруктураПараметров = Новый Структура;
	Если ЗначениеЗаполнено(ДанныеБота.Смещение) Тогда
		СтруктураПараметров.Вставить("offset", ДанныеБота.Смещение + 1);
	КонецЕсли;
	
	СтрокаJSON = ТелеграмAPI.ВыполнитьМетод(ДанныеБота, "getUpdates", СтруктураПараметров, Перечисления.ТелеграмСпособыЗапросов.Простой);
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	
	ВходящиеДанные = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON);
	Если ВходящиеДанные.ok = Ложь Тогда
		ТекстИсключения = СтрШаблон("Сервер Telegram вернул ошибку: error_code: %1, description: %2", ВходящиеДанные.error_code, ВходящиеДанные.description);
		ВызватьИсключение ТекстИсключения;
	Иначе
		Возврат ВходящиеДанные.result;
	КонецЕсли;
	
КонецФункции


Процедура ОбработатьВходящиеОбновленияБота(ВходящиеОбновления, ДанныеБота) Экспорт
	
	КоллекцияСвойств = ВходящиеОбновления.Свойства();
	Если КоллекцияСвойств.Получить("result") <> Неопределено Тогда
		
		Если ТипЗнч(ВходящиеОбновления.result) = Тип("ОбъектXDTO") Тогда
			Обновления = Новый Массив;
			Обновления.Добавить(ВходящиеОбновления.result);
		Иначе // СписокXDTO
			Обновления = ВходящиеОбновления.result;
		КонецЕсли;
		
		Для Каждого Обновление Из Обновления Цикл
			ОбработатьВходящееОбновлениеБота(Обновление, ДанныеБота);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработатьВходящееОбновлениеБота(Обновление, ДанныеБота) Экспорт
	
	ИдентификаторОбновления = Обновление.update_id;
	
	КоллекцияСвойств = Обновление.Свойства();
	ОтветКонтекстнойКлавиатуры = Неопределено;
	Если КоллекцияСвойств.Получить("message") <> Неопределено Тогда
		ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.Сообщение;
		Сообщение = Обновление.message;
	ИначеЕсли КоллекцияСвойств.Получить("inline_query") <> Неопределено Тогда
		ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.ВстроенныйЗапрос;
		Сообщение = Неопределено;
	ИначеЕсли КоллекцияСвойств.Получить("chosen_inline_result") <> Неопределено Тогда
		ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.ВыбранныйЛинейныйРезультат;
		Сообщение = Неопределено;
	ИначеЕсли КоллекцияСвойств.Получить("callback_query") <> Неопределено Тогда
		ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.ОтветКонтекстнойКлавиатуры;
		КоллекцияСвойств = Обновление.callback_query.Свойства();
		Если КоллекцияСвойств.Получить("message") <> Неопределено Тогда
			Сообщение = Обновление.callback_query.message;
		Иначе
			Сообщение = Неопределено;
		КонецЕсли;
		ОтветКонтекстнойКлавиатуры = Обновление.callback_query.data;
	КонецЕсли;

	Если Сообщение = Неопределено Тогда
		ТекстСообщения = "";
		ИдентификаторЧата = Неопределено;
		ИдентификаторСообщения = Неопределено;
	Иначе
		КоллекцияСвойств = Сообщение.Свойства();
		Если КоллекцияСвойств.Получить("text") <> Неопределено Тогда
			ТекстСообщения = Сообщение.text;
		Иначе
			ТекстСообщения = "";
		КонецЕсли;
		ИдентификаторЧата = Сообщение.chat.id;
		ИдентификаторСообщения = Сообщение.message_id;
	КонецЕсли;
	
	НастройкиПользователя = РегистрыСведений.ТелеграмНастройкиПользователей.ПолучитьНастройкиПользователя(ИдентификаторЧата);
			
	НакопленныеДанные = Новый Структура;
	НакопленныеДанные.Вставить("ДанныеБота", 					ДанныеБота);
	НакопленныеДанные.Вставить("ИдентификаторОбновления",		ИдентификаторОбновления);
	НакопленныеДанные.Вставить("ИдентификаторСообщения",		ИдентификаторСообщения);
	НакопленныеДанные.Вставить("ИдентификаторЧата", 			ИдентификаторЧата);
	НакопленныеДанные.Вставить("ВидВходящегоОбновления", 		ВидВходящегоОбновления);
	НакопленныеДанные.Вставить("Обновление", 					Обновление);
	НакопленныеДанные.Вставить("Сообщение", 					Сообщение);
	НакопленныеДанные.Вставить("ТекстСообщения",				ТекстСообщения);
	НакопленныеДанные.Вставить("НастройкиПользователя",			НастройкиПользователя);
	НакопленныеДанные.Вставить("ОтветКонтекстнойКлавиатуры",	ОтветКонтекстнойКлавиатуры);			
	
	РегистрыСведений.ТелеграмНастройкиБотов.СохранитьНастройкиБота(НакопленныеДанные); // Зафиксировать смещение до формирования ответа
	
	ТелеграмСервисы = Справочники.ТелеграмСервис.ПолучитьСервисыДляВыполнения(НакопленныеДанные);
	
	Попытка
		КоличествоВыполненныхОбработок = 0;
		Для Каждого ТелеграмСервис Из ТелеграмСервисы Цикл
			ТелеграмОбработки = Справочники.ТелеграмОбработка.ПолучитьОбработкиДляВыполнения(ТелеграмСервис, НакопленныеДанные);
			Для Каждого ТелеграмОбработка Из ТелеграмОбработки Цикл
				Справочники.ТелеграмОбработка.ВыполнитьОбработку(ТелеграмОбработка, НакопленныеДанные);
				КоличествоВыполненныхОбработок = КоличествоВыполненныхОбработок + 1;
			КонецЦикла;
		КонецЦикла;
			
		Если КоличествоВыполненныхОбработок = 0 Тогда
			// Необходимо всё равно что-то вывести в ответ. За "что-то" отвечают обработки-исключения
			ТелеграмОбработки = Справочники.ТелеграмОбработка.ПолучитьОбработкиИсключения(НакопленныеДанные);
			Для Каждого ТелеграмОбработка Из ТелеграмОбработки Цикл
				Справочники.ТелеграмОбработка.ВыполнитьОбработку(ТелеграмОбработка, НакопленныеДанные);
			КонецЦикла;
		КонецЕсли;
	Исключение
		Сообщение = Новый Структура;
		Сообщение.Вставить("ТекстСообщения", "При формировании ответа возникла ошибка: " + ОписаниеОшибки());
		ОтправитьСообщение(Сообщение, НакопленныеДанные);
		ВызватьИсключение ОписаниеОшибки(); // Передать ошибку дальше, в фоновое задание
	КонецПопытки;
	
КонецПроцедуры


Функция ВставитьЭмодзи(РезультатСборки) Экспорт
	
	Ответ = РезультатСборки;
	
	ПозицияЭмодзи = СтрНайти(Ответ, "Эмодзи_");
	Пока ПозицияЭмодзи > 0 Цикл
		
		ПредставлениеЭмодзи = Сред(РезультатСборки, ПозицияЭмодзи + 7, 5);
		Эмодзи = Справочники.ТелеграмЭмодзи.НайтиПоНаименованию(ПредставлениеЭмодзи, Истина);
		КодЭмодзи = Эмодзи.Код;
		Если ЗначениеЗаполнено(КодЭмодзи) Тогда
			Если СтрДлина(КодЭмодзи) = 4 Тогда
				НоваяСтрока = "\u" + КодЭмодзи;
			Иначе // 8
				НоваяСтрока = "\u" + Лев(КодЭмодзи, 4) + "\u" + Сред(КодЭмодзи, 5);
			КонецЕсли;
		Иначе
			НоваяСтрока = "";
		КонецЕсли;
		
		СтрокаЗамены = "Эмодзи_" + ПредставлениеЭмодзи;
		Ответ = СтрЗаменить(Ответ, СтрокаЗамены, НоваяСтрока);
		ПозицияЭмодзи = СтрНайти(Ответ, "Эмодзи_");
		
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции


Функция ПолучитьМодельДляСборкиМетода(Метод) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Метод", Метод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмМетоды.Метод КАК Метод,
	|	ТелеграмМетоды.Параметр КАК Параметр,
	|	ТелеграмМетоды.Параметр1С КАК Параметр1С,
	|	ТелеграмМетоды.ОбязательныйДляЗаполнения КАК ОбязательныйДляЗаполнения,
	|	ТелеграмМетоды.ОбязательныйДляПередачи КАК ОбязательныйДляПередачи,
	|	ТелеграмМетоды.ОсобеннаяСборка КАК ОсобеннаяСборка
	|ИЗ
	|	РегистрСведений.ТелеграмМетоды КАК ТелеграмМетоды
	|ГДЕ
	|	ТелеграмМетоды.Метод = &Метод
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмМетоды.Сортировка";
	Результат = Запрос.Выполнить();
	МодельДляСборки = Результат.Выгрузить();
	Возврат МодельДляСборки;
	
КонецФункции


Процедура ОсобеннаяСборкаПараметра(СтруктураОтправки, Сообщение, ЭлементМодели, НакопленныеДанные) Экспорт
	
	Если ЭлементМодели.Параметр = "chat_id" Тогда
		
		Если Сообщение.Свойство("ИдентификаторЧата") Тогда
			СтруктураОтправки.Вставить(ЭлементМодели.Параметр,
				Сообщение.ИдентификаторЧата);
		Иначе
			СтруктураОтправки.Вставить(ЭлементМодели.Параметр,
				НакопленныеДанные.ИдентификаторЧата);
		КонецЕсли;
		
	ИначеЕсли ЭлементМодели.Параметр = "reply_markup" Тогда
		
		Если Сообщение.Свойство("Клавиатура") Тогда
			СобраннаяКлавиатура = Справочники.ТелеграмКлавиатуры.СобратьКлавиатуру(
				Сообщение.Клавиатура, НакопленныеДанные);
				
			Если СобраннаяКлавиатура = Неопределено Тогда
				ТекстИсключения = "Не удалось собрать клавиатуру " + Сообщение.Клавиатура;
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			СтруктураОтправки.Вставить("reply_markup", СобраннаяКлавиатура);
		КонецЕсли;
		
	ИначеЕсли ЭлементМодели.Параметр = "message_id" Тогда
		
		Если Сообщение.Свойство("ИдентификаторСообщения") Тогда
			СтруктураОтправки.Вставить(ЭлементМодели.Параметр,
				Сообщение.ИдентификаторСообщения);
		Иначе
			СтруктураОтправки.Вставить(ЭлементМодели.Параметр,
				НакопленныеДанные.ИдентификаторСообщения);
		КонецЕсли;
		
	ИначеЕсли ЭлементМодели.Параметр = "photo"
		ИЛИ ЭлементМодели.Параметр = "video"
		ИЛИ ЭлементМодели.Параметр = "document"
		ИЛИ ЭлементМодели.Параметр = "audio"
		ИЛИ ЭлементМодели.Параметр = "voice" Тогда
		
		Если Сообщение.Свойство("ВходнойФайлСтруктура") = Истина Тогда
			Если Сообщение.ВходнойФайлСтруктура.ВидИсточника = "СерверТелеграм" Тогда
				СтруктураОтправки.Вставить(ЭлементМодели.Параметр, Сообщение.ВходнойФайлСтруктура.Источник);
			Иначе
				СтруктураОтправки.Вставить("ВходнойФайлСтруктура", Сообщение.ВходнойФайлСтруктура);
				СтруктураОтправки.ВходнойФайлСтруктура.Вставить("Ключ", ЭлементМодели.Параметр);
			КонецЕсли;
		Иначе
			ТекстИсключения = СтрШаблон("При формировании структуры отправки для метода %1 не обнаружено значение ВходнойФайлСтруктура", ЭлементМодели.Параметр);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Функция СобратьСтруктуруОтправки(Сообщение, МодельДляСборки, НакопленныеДанные) Экспорт
	
	СтруктураОтправки = Новый Структура;
	
	Для Каждого ЭлементМодели Из МодельДляСборки Цикл
		Если ЭлементМодели.ОсобеннаяСборка Тогда
			ОсобеннаяСборкаПараметра(СтруктураОтправки, Сообщение, ЭлементМодели, НакопленныеДанные);
		Иначе
			ЗначениеПараметра = "";
			НаличиеПараметра = Сообщение.Свойство(ЭлементМодели.Параметр1С, ЗначениеПараметра);
			Если НаличиеПараметра = Истина Тогда
				СтруктураОтправки.Вставить(ЭлементМодели.Параметр, ЗначениеПараметра);
			ИначеЕсли ЭлементМодели.ОбязательныйДляЗаполнения = Истина Тогда
				ТекстИсключения = СтрШаблон("При формировании структуры отправки для метода %1 не обнаружен обязательный для заполнения параметр %2", ЭлементМодели.Метод, ЭлементМодели.Параметр1С);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
		Если ЭлементМодели.ОбязательныйДляПередачи Тогда
			ПараметрЗаполнен = СтруктураОтправки.Свойство(ЭлементМодели.Параметр);
			Если НЕ ПараметрЗаполнен Тогда
				Если ЭлементМодели.Параметр1С = "ВходнойФайлСтруктура" Тогда
					Если СтруктураОтправки.Свойство("ВходнойФайлСтруктура") Тогда
						ПараметрЗаполнен = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Если НЕ ПараметрЗаполнен Тогда
				ТекстИсключения = СтрШаблон("При формировании структуры отправки для метода %1 не заполнен обязательный для передачи параметр %2", ЭлементМодели.Метод, ЭлементМодели.Параметр);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураОтправки;
	
КонецФункции


Процедура ВыполнитьКодСборкиОтвета(КодСборки, НакопленныеДанные) Экспорт
	
	Выполнить(КодСборки);
	
КонецПроцедуры


// sendMessage
Процедура ОтправитьСообщение(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendMessage";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendPhoto
Процедура ОтправитьФотографию(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendPhoto";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendVideo
Процедура ОтправитьВидео(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendVideo";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendDocument
Процедура ОтправитьДокумент(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendDocument";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendAudio
Процедура ОтправитьАудио(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendAudio";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendVoice
Процедура ОтправитьГолос(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendVoice";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendLocation
Процедура ОтправитьМестоположение(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendLocation";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// sendVenue
Процедура ОтправитьМестоНаКарте(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "sendVenue";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


// editMessageReplyMarkup
Процедура РедактироватьКонтекстнуюКлавиатуру(Сообщение, НакопленныеДанные) Экспорт
	
	Метод = "editMessageReplyMarkup";
	СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные);
	
КонецПроцедуры


Процедура СобратьВыполнитьМетод(Метод, Сообщение, НакопленныеДанные) Экспорт
	
	МодельДляСборки 	= ПолучитьМодельДляСборкиМетода(Метод);
	СтруктураОтправки 	= СобратьСтруктуруОтправки(Сообщение, МодельДляСборки, НакопленныеДанные);
	ТелеграмAPI.ВыполнитьМетод(НакопленныеДанные.ДанныеБота, Метод, СтруктураОтправки);
	
КонецПроцедуры


Функция ПолучитьИнформациюОФайлахСообщения(Сообщение)
	
	Ответ = Новый Массив;
	
	Если ТипЗнч(Сообщение) = Тип("ОбъектXDTO") Тогда
		
		КоллекцияСвойств = Сообщение.Свойства();
		
		Если КоллекцияСвойств.Получить("audio") <> Неопределено Тогда
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Сообщение.audio.file_id,
				"");
			Ответ.Добавить(ЭлементОтвета);	
		КонецЕсли;
		
		Если КоллекцияСвойств.Получить("document") <> Неопределено Тогда
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Сообщение.document.file_id,
				Сообщение.document.file_name);
			Ответ.Добавить(ЭлементОтвета);	
		КонецЕсли;
		
		Если КоллекцияСвойств.Получить("photo") <> Неопределено Тогда
			Для Каждого Фото Из Сообщение.photo.photo Цикл
			КонецЦикла;
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Фото.file_id,
				"");
			Ответ.Добавить(ЭлементОтвета); // последний элемент не был сжат
		КонецЕсли;
		
		Если КоллекцияСвойств.Получить("sticker") <> Неопределено Тогда
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Сообщение.sticker.file_id,
				"");
			Ответ.Добавить(ЭлементОтвета);	
		КонецЕсли;
		
		Если КоллекцияСвойств.Получить("video") <> Неопределено Тогда
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Сообщение.video.file_id,
				"");
			Ответ.Добавить(ЭлементОтвета);	
		КонецЕсли;
		
		Если КоллекцияСвойств.Получить("voice") <> Неопределено Тогда
			ЭлементОтвета = Новый Структура(
				"ИдентификаторФайла, ИмяФайла",
				Сообщение.voice.file_id,
				"");
			Ответ.Добавить(ЭлементОтвета);	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции


Функция ПолучитьКаталогСохранения(НакопленныеДанные)
	
	ИмяКаталогаБота 		= КаталогВременныхФайлов();//НакопленныеДанные.ДанныеБота.КаталогСохраненияФайлов;
	ИмяПапкиПоДате			= Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd");
	ИмяКаталогаСохранения 	= ИмяКаталогаБота + "\" + ИмяПапкиПоДате;
	
	Если НЕ ЗначениеЗаполнено(ИмяКаталогаБота) Тогда
		ТекстИсключения = "Не заполнено значение Каталог сохранения файлов бота " + НакопленныеДанные.ДанныеБота.Имя;
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	КаталогСохранения = Новый Файл(ИмяКаталогаСохранения);
	Если КаталогСохранения.Существует() = ЛОЖЬ Тогда
		СоздатьКаталог(ИмяКаталогаСохранения);
	КонецЕсли;
	
	Возврат ИмяКаталогаСохранения;
	
КонецФункции


Функция СобратьИмяФайлаСохранения(НакопленныеДанные, СтрокаИнформации, ПутьФайлаСервер)
	
	ВремяПолучения 			= Формат(ТекущаяДата(), "ДФ=ЧЧ-мм-сс");
	ИдентификаторЧата 		= Формат(НакопленныеДанные.ИдентификаторЧата, "ЧГ=");
	ИдентификаторСообщения 	= Формат(НакопленныеДанные.ИдентификаторСообщения, "ЧГ=");
	
	Если НЕ ПустаяСтрока(СтрокаИнформации.ИмяФайла) Тогда
		ПравоеИмяФайла = СтрокаИнформации.ИмяФайла;
	Иначе
		ПравоеИмяФайла = СтрЗаменить(ПутьФайлаСервер, "/", "-");
	КонецЕсли;
	
	ИмяФайлаСохранения = ВремяПолучения + "-" + ИдентификаторЧата + "-" + ИдентификаторСообщения + "-" + ПравоеИмяФайла;
	Возврат ИмяФайлаСохранения;
	
КонецФункции


Процедура ЗагрузитьФайл(НакопленныеДанные) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	НакопленныеДанные.Вставить("ЗагруженныйФайл", Неопределено);
	
	Сообщение 			= НакопленныеДанные.Сообщение;
	ИнформацияОФайлах 	= ПолучитьИнформациюОФайлахСообщения(Сообщение);
	
	Запрос 		= ТелеграмAPI.СоздатьЗапрос();
	Соединение 	= ТелеграмAPI.СоздатьСоединение();
	
	Для Каждого СтрокаИнформации Из ИнформацияОФайлах Цикл
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("file_id", СтрокаИнформации.ИдентификаторФайла);
		ОтветJSON = ТелеграмAPI.ВыполнитьМетод(НакопленныеДанные.ДанныеБота, "getFile", СтруктураПараметров, Перечисления.ТелеграмСпособыЗапросов.Простой);
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ОтветJSON);
		
		ОтветСтруктура = ПрочитатьJSON(ЧтениеJSON);
		Если ОтветСтруктура.ok = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		ПутьФайлаСервер 	= ОтветСтруктура.result.file_path;
		ЛокальныйПутьФайла 	= ПолучитьКаталогСохранения(НакопленныеДанные);
		ЛокальноеИмяФайла	= СобратьИмяФайлаСохранения(НакопленныеДанные, СтрокаИнформации, ПутьФайлаСервер);
		ПолноеИмяФайла 		= ЛокальныйПутьФайла + "\" + ЛокальноеИмяФайла;
		
		Запрос.АдресРесурса = СтрШаблон("/file/bot%1/%2", НакопленныеДанные.ДанныеБота.Жетон, ПутьФайлаСервер);
		Соединение.Получить(Запрос, ПолноеИмяФайла); // Исключения передаются наверх и попадают в ответ
		НакопленныеДанные.ЗагруженныйФайл = ПолноеИмяФайла;
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ФорматЧисла(Значение)Экспорт
	Возврат Формат(Значение, "ЧЦ=15; ЧН=0; ЧГ=");
КонецФункции


///функции проверки

Функция ЗначениеКонтекстаВыбрано(НакопленныеДанные,ИмяПараметра)Экспорт
	пКонтекст = Неопределено;
	Если НЕ НакопленныеДанные.Свойство("Контекст",пКонтекст) Тогда
		  НакопленныеДанные.Свойство("КонтекстСеанса",пКонтекст)
	КонецЕсли;
	Если пКонтекст = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	

	Параметр = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени(ИмяПараметра);	
	ЗначениеПараметра = пКонтекст[Параметр];
//	ЗначениеПараметра = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьЗначениеПоПараметру(НакопленныеДанные.ИдентификаторЧата,Параметр);
	Рез = ЗначениеЗаполнено(ЗначениеПараметра);
	
	Возврат Рез;
	
	
КонецФункции


Функция ЭтоЗаявкаПользователя(НакопленныеДанные)Экспорт
	
	Параметр = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован");
	ЗначениеПараметраПользователь = НакопленныеДанные.Контекст[Параметр];
	Если ЗначениеЗаполнено(НакопленныеДанные.ОтветКонтекстнойКлавиатуры) Тогда
		НомерНакладной = НакопленныеДанные.ОтветКонтекстнойКлавиатуры;
	Иначе
		НомерНакладной = НакопленныеДанные.Контекст[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	КонецЕсли;
	
	Возврат Документы.Задачи.ЭтоЗадачаПользователя(НомерНакладной,ЗначениеПараметраПользователь)

КонецФункции

Функция ПолучитьЗначениеКонтекста(НакопленныеДанные,ИмяПараметра)Экспорт
	
	пКонтекст = Неопределено;
	Если НЕ НакопленныеДанные.Свойство("Контекст",пКонтекст) Тогда
		НакопленныеДанные.Свойство("КонтекстСеанса",пКонтекст)
	КонецЕсли;
	Если пКонтекст = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	
	Параметр = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени(ИмяПараметра);	
	ЗначениеПараметра = пКонтекст[Параметр];
	
	Возврат ЗначениеПараметра;
	
	
КонецФункции


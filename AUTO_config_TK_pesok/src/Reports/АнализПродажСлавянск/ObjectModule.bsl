
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)	
	
	СтандартнаяОбработка = Ложь;
	
	ТекстСообщения = "";	
	Если Не Отчеты.АнализПродажСлавянск.ПодключениеКВнешнемуИсточнику(ТекстСообщения) Тогда 
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
 
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
	
	ДатаНачала = МакетКомпоновки.ЗначенияПараметров.Найти("ДатаНачала").Значение;
	ДатаКонца = МакетКомпоновки.ЗначенияПараметров.Найти("ДатаКонца").Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	dbo_vRetailSales.Period КАК Дата,
		|	dbo_vRetailSales.OUTLETEXTID КАК НомерТочки,
		|	dbo_vRetailSales.PRODUCTEXTID КАК Артикул,
		|	dbo_vRetailSales.SALES_PACKS КАК Количество,
		|	dbo_vOUTLETS.PARTNERNAME КАК Контрагент,
		|	dbo_vOUTLETS.ZKPO КАК ЭКПОКонтрагента,
		|	dbo_vRetailSales.MRP_PRICE / 100 КАК МРЦ,
		|	dbo_vRetailSales.OUTLETADDRESS КАК АдресДоставки,
		|	dbo_vRetailSales.SALES_SUM / 100 КАК Сумма
		|ИЗ
		|	ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vRetailSales КАК dbo_vRetailSales
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vOUTLETS КАК dbo_vOUTLETS
		|		ПО dbo_vRetailSales.OUTLETID = dbo_vOUTLETS.OUTLETID
		|ГДЕ
		|	dbo_vRetailSales.Period МЕЖДУ &ДатаНачала И &ДатаКонца";
	
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	
	РезультатЗапроса = Запрос.Выполнить();
	Тз = РезультатЗапроса.Выгрузить();
	

	ВнешнийНаборДанных = Новый Структура("тзТаблица", Тз); 
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНаборДанных, ДанныеРасшифровки); 
	
	ДокументРезультат.Очистить();
	
 	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

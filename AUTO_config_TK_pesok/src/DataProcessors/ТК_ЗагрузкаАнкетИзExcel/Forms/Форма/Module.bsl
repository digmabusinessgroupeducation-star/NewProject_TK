
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураШаблоновАнкет = Новый Структура;
	СтруктураШаблоновАнкет.Вставить("КИОСК_НАЛИЧНЫЙ", 		Справочники.ШаблоныАнкет.НайтиПоКоду("000000001")); 
	СтруктураШаблоновАнкет.Вставить("КИОСК_БЕЗНАЛИЧНЫЙ", 	Справочники.ШаблоныАнкет.НайтиПоКоду("000000004")); 
	СтруктураШаблоновАнкет.Вставить("ПАВИЛЬОН_НАЛИЧНЫЙ", 	Справочники.ШаблоныАнкет.НайтиПоКоду("000000002")); 
	СтруктураШаблоновАнкет.Вставить("ПАВИЛЬОН_БЕЗНАЛИЧНЫЙ", Справочники.ШаблоныАнкет.НайтиПоКоду("000000003")); 

	МассивШаблонов = Новый Массив;
	Для Каждого Стр Из СтруктураШаблоновАнкет Цикл 
		МассивШаблонов.Добавить(Стр.Значение);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивШаблонов", МассивШаблонов);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВопросыШаблонаАнкеты.Владелец КАК ШаблонАнкеты,
	               |	ВопросыШаблонаАнкеты.Родитель.Код * 1000 + ВопросыШаблонаАнкеты.Код КАК Порядок,
	               |	ВопросыШаблонаАнкеты.ЭлементарныйВопрос.Код КАК Код,
	               |	ВопросыШаблонаАнкеты.Ссылка КАК Вопрос,
	               |	ВопросыШаблонаАнкеты.ЭлементарныйВопрос КАК ЭлементарныйВопрос,
	               |	ВопросыШаблонаАнкеты.Обязательный КАК Обязательный,
	               |	ВопросыШаблонаАнкеты.ЭлементарныйВопрос.ТипОтвета КАК ТипОтвета
	               |ИЗ
	               |	Справочник.ВопросыШаблонаАнкеты КАК ВопросыШаблонаАнкеты
	               |ГДЕ
	               |	ВопросыШаблонаАнкеты.Владелец В(&МассивШаблонов)
	               |	И НЕ ВопросыШаблонаАнкеты.ПометкаУдаления
	               |	И НЕ ВопросыШаблонаАнкеты.ЭтоГруппа
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ШаблонАнкеты,
	               |	Порядок";
	
	КэшВопросов.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокАнкетПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("УстановитьОтборВопросов",0.3, Истина);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьАнкеты(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;	
		
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Фильтр = "(*.xlsx)|*.xlsx|(*.xls)|*.xls|(*.ods)|*.ods";	
	ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файлы'; uk = 'Оберіть файли'");
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПродолжениеПомещенияФайлов", ЭтаФорма);
	
	НачатьПомещениеФайловНаСервер(ОповещениеЗавершения,,,ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьАнкеты(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение()
		ИЛИ СписокАнкет.Количество() = 0 
		ИЛИ СписокВопросов.Количество() = 0 
		ИЛИ СписокАнкет.НайтиСтроки(Новый Структура("Выбор", Истина)).Количество() = 0 Тогда 
		
		Возврат;
	КонецЕсли;
	
	СоздатьАнкетыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьАнкетыНаСервере()
	
	МассивАнкет = СписокАнкет.НайтиСтроки(Новый Структура("Выбор", Истина));
	
	Для Каждого стрАнкета Из МассивАнкет Цикл 
		стрАнкета.Выбор = Ложь;
		
		обАнкета = Документы.Анкета.СоздатьДокумент();
		
		ЗаполнитьЗначенияСвойств(обАнкета, стрАнкета, "Дата,Респондент,ШаблонАнкеты,Реализатор");
		
		обАнкета.ДатаРедактирования = ТекущаяДатаСеанса();
		обАнкета.Интервьюер = Пользователь;
		обАнкета.РежимАнкетирования = Перечисления.РежимыАнкетирования.Интервью;
		обАнкета.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Анкета загружена из Excel
                                          |
                                          |Чек №%1 от %2
                                          |РК: %3
                                          |Сумма: %4
                                          |Кассир: %5
                                          |'; uk = 'Анкета завантажена із Excel
                                          |
                                          |Чек №%1 от %2
                                          |РК: %3
                                          |Сумма: %4
                                          |Касир: %5
                                          |'"),
									стрАнкета.НомерЧека,
									стрАнкета.Дата,
									стрАнкета.РК,
									стрАнкета.СуммаЧека,
									стрАнкета.ПредставлениеРеализатора);
									
		МассивВопросов = СписокВопросов.НайтиСтроки(Новый Структура("ИД", стрАнкета.ИД));
		
		Для Каждого СтрВопрос Из МассивВопросов Цикл 
			НоваяСтрока = обАнкета.Состав.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрВопрос, "Вопрос,ЭлементарныйВопрос,НомерЯчейки,Ответ,ОткрытыйОтвет");
		КонецЦикла;
		
		Попытка
			обАнкета.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Создан документ ""%1""'; uk = 'Створений документ ""%1""'"),
					Строка(обАнкета)),
				обАнкета.Ссылка);
		Исключение						
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПродолжениеПомещенияФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПомещенныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	МассивФайлов = Новый Массив;
	Для Каждого нФайл Из ПомещенныеФайлы Цикл 
		Если нФайл.ПомещениеФайлаОтменено Тогда 
			Продолжить;
		КонецЕсли;
		
		СтруктураФайла = Новый Структура;
		СтруктураФайла.Вставить("Адрес", нФайл.Адрес);
		СтруктураФайла.Вставить("Имя", нФайл.СсылкаНаФайл.Имя);
		СтруктураФайла.Вставить("Расширение", нФайл.СсылкаНаФайл.Расширение);
		
		МассивФайлов.Добавить(СтруктураФайла);
	КонецЦикла;	
	
	Если МассивФайлов.Количество() > 0 Тогда 
		ПрочитатьНаСервере(МассивФайлов[0]);
	Иначе 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выбранных файлов'; uk = 'Немає обраних файлів'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере(Файл)

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Файл.Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла(Файл.Расширение);	
	ДвоичныеДанные.Записать(ИмяФайла);
	
	ТабличныйДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
	УдалитьФайлы(ИмяФайла);
	
	СписокАнкет.Очистить();
	СписокВопросов.Очистить();
	
	ПолучитьАнкетыИзТабДокумента();
	
КонецПроцедуры	

&НаСервере
Процедура ПолучитьАнкетыИзТабДокумента()
	
	Если ТабличныйДокумент.Области.Количество() = 0 Тогда 
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'В загружаемом файле не найдены анкеты!'; uk = 'У завантажуємому файлі не знайдені анкети!'"));
		Возврат;	
	КонецЕсли;
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипЧисло = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(1,0, ДопустимыйЗнак.Неотрицательный));
	
	Для Каждого Лист Из ТабличныйДокумент.Области Цикл 
		ИмяЛиста = Лист.Имя;
		
		Если ИмяЛиста = "Сводная" ИЛИ ИмяЛиста = "Настройки" Тогда
			Продолжить;
		КонецЕсли;
		
		ВертСмещЛиста = Лист.Верх-1;		
		МаксСтрок 	= Лист.Низ;
		
		КодСклад 	= СокрЛП(ТабличныйДокумент.Область(ВертСмещЛиста + 4, 2, ВертСмещЛиста + 4, 2).Текст);
		стрДата 	= СокрЛП(ТабличныйДокумент.Область(ВертСмещЛиста + 5, 2, ВертСмещЛиста + 5, 2).Текст);
		НомерЧека	= СокрЛП(ТабличныйДокумент.Область(ВертСмещЛиста + 6, 2, ВертСмещЛиста + 6, 2).Текст);
		
		Если ПустаяСтрока(КодСклад) 
			ИЛИ ПустаяСтрока(стрДата)
			ИЛИ ПустаяСтрока(НомерЧека) Тогда 
			
			Продолжить;
		КонецЕсли;
		
		Ошибки = Новый ТекстовыйДокумент;
		ВсеОк = Истина;
		
		Склад = Справочники.СкладыКомпании.НайтиПоКоду(КодСклад);		
		Если Склад.Пустая() Тогда 
			Ошибки.ДобавитьСтроку(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не найден склад с кодом ""%1""'; uk = 'Не знайдений склад з кодом ""%1""'"),
					КодСклад));
			ВсеОк = Ложь;
		КонецЕсли;
		
		Дата  = СтроковыеФункцииКлиентСервер.СтрокаВДату(стрДата, ЧастиДаты.Дата);
		Если Не ЗначениеЗаполнено(Дата) Тогда 
			Ошибки.ДобавитьСтроку(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось преобразовать к дате значение ""%1""'; uk = 'Не вдалося перетворити на дату значення ""%1""'"),					
					стрДата));
			ВсеОк = Ложь;			
		КонецЕсли;
		
		Если ВсеОк Тогда 
			ИД_Анкеты = Новый УникальныйИдентификатор;
			НовСтрАнкеты = СписокАнкет.Добавить();
			ШаблонАнкеты = Неопределено;
			
			ДанныеСклада = ПолучитьДанныеТочки(Склад);	
			ЗаполнитьЗначенияСвойств(НовСтрАнкеты, ДанныеСклада);
			
			ДанныеЧека = ПолучитьДанныеЧека(КодСклад, Дата, НомерЧека);
			
			Если ДанныеЧека.Количество() > 0 Тогда 
				ЗаполнитьЗначенияСвойств(НовСтрАнкеты, ДанныеЧека);
				
				КлючАнкеты = ВРег(Строка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСклада, "ТипТочки", ""))) + "_" + ВРег(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЧека, "ТипОплаты", ""));			
				
				Если СтруктураШаблоновАнкет.Свойство(КлючАнкеты) Тогда 
					ШаблонАнкеты = СтруктураШаблоновАнкет[КлючАнкеты];
					НовСтрАнкеты.ШаблонАнкеты = ШаблонАнкеты;
				Иначе
					Ошибки.ДобавитьСтроку("Не удалось определить шаблон анкеты");
					ВсеОк = Ложь;
				КонецЕсли;											
			Иначе
				Ошибки.ДобавитьСтроку(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось получить данные чека ""%1"" от %2'; uk = 'Не вдалося отримати дані чеку ""%1"" від %2'"),
						НомерЧека,
						Формат(Дата, "ДФ=dd.MM.yyyy")));
						
				ВсеОк = Ложь;
			КонецЕсли;
					
			НовСтрАнкеты.Выбор = ВсеОк;
			НовСтрАнкеты.ИД = ИД_Анкеты;
		КонецЕсли;
		
		Если ВсеОк Тогда 
			тзОтветы = Новый ТаблицаЗначений;
			тзОтветы.Колонки.Добавить("ИД");
			тзОтветы.Колонки.Добавить("Вопрос");
			тзОтветы.Колонки.Добавить("ЭлементарныйВопрос");
			тзОтветы.Колонки.Добавить("НомерЯчейки");
			тзОтветы.Колонки.Добавить("Ответ");
			тзОтветы.Колонки.Добавить("ОткрытыйОтвет");
			
			тзОтветы.Колонки.Добавить("Код");
			тзОтветы.Колонки.Добавить("Порядок");
			тзОтветы.Колонки.Добавить("Обязательный");
			тзОтветы.Колонки.Добавить("ТипОтвета");
			
			МассивВопросовШаблона = КэшВопросов.НайтиСтроки(Новый Структура("ШаблонАнкеты", ШаблонАнкеты));			

			Для Каждого Вопрос Из МассивВопросовШаблона Цикл 
				НоваяСтрока = тзОтветы.Добавить();			
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Вопрос);				
				НоваяСтрока.ИД = ИД_Анкеты;
				НоваяСтрока.ОткрытыйОтвет = "";
				Если НоваяСтрока.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Число Тогда 
					НоваяСтрока.Ответ = 0;
				ИначеЕсли НоваяСтрока.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Булево Тогда 
					НоваяСтрока.Ответ = Ложь;
				КонецЕсли;
			КонецЦикла;
			
			Для Ит = 9 + ВертСмещЛиста По МаксСтрок Цикл 
				КодВопроса = СокрЛП(ТабличныйДокумент.Область(Ит, 1, Ит, 1).Текст);	
				Если ПустаяСтрока(КодВопроса) Тогда 
					Продолжить;
				КонецЕсли;
				
				текВопрос = тзОтветы.Найти(КодВопроса, "Код");
				Если текВопрос = Неопределено Тогда 
					Ошибки.ДобавитьСтроку(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не найден вопрос [%1] ""%2"" в шаблоне ""%3""'; uk = 'Не знайдение питання [%1] ""%2"" в шаблоні ""%3""'"),
							КодВопроса,
							СокрЛП(ТабличныйДокумент.Область(Ит, 2, Ит, 2).Текст),
							ШаблонАнкеты));
							
					ВсеОк = Ложь;
					Продолжить;
				КонецЕсли;
				
				стрОтвет = СокрЛП(ТабличныйДокумент.Область(Ит, 3, Ит, 3).Текст);				
				
				Исключить = стрОтвет = "-";											
				Если Не текВопрос.Обязательный И Исключить Тогда 
					текВопрос.НомерЯчейки = 1;
				КонецЕсли;
							
				Если Не ПустаяСтрока(стрОтвет) Тогда 
					Если текВопрос.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Число Тогда 
						Ответ = ТипЧисло.ПривестиЗначение(стрОтвет);	
					Иначе
						Ответ = ТипБулево.ПривестиЗначение(стрОтвет);
					КонецЕсли;
					
					текВопрос.Ответ = Ответ;	
				КонецЕсли;
				
				ОткрытыйОтвет = СокрЛП(ТабличныйДокумент.Область(Ит, 4, Ит, 4).Текст);
				текВопрос.ОткрытыйОтвет = ОткрытыйОтвет;
			КонецЦикла;
			
			тзОтветы.Сортировать("Порядок");
			
			Для Каждого СтрОтвет Из тзОтветы Цикл 
				НовСтрОтвет = СписокВопросов.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрОтвет, СтрОтвет, "ИД,Вопрос,ЭлементарныйВопрос,НомерЯчейки,Ответ,ОткрытыйОтвет");
			КонецЦикла;
		КонецЕсли;
		
		Если Не ВсеОк Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка загрузки анкеты с листа ""%1"":
                        |%2
                        |'; uk = 'Помилка завантаження акети з листа ""%1"":
                        |%2'"),
					ИмяЛиста,
					Ошибки.ПолучитьТекст()));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборВопросов() Экспорт 
	
	текАнкета = Элементы.СписокАнкет.ТекущиеДанные;
	
	Если текАнкета = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.СписокВопросов.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("ИД", текАнкета.ИД));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеТочки(Знач Склад)
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Склад", Склад);
	
	Если ЗначениеЗаполнено(Склад) Тогда 
		УстановитьПривилегированныйРежим(Истина);
		
		ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Склад);				
		СтруктураРеквизитов.Вставить("Респондент", ТорговаяПлощадка);
		
		ТаблицаРеквизитов = УправлениеСвойствами.ЗначенияСвойств(ТорговаяПлощадка, Истина, Ложь);
		
		Для Каждого Стр Из ТаблицаРеквизитов Цикл 
			Если Не ЗначениеЗаполнено(Стр.Значение) Тогда 
				Продолжить;
			КонецЕсли;
			
			ИмяСвойства = Стр.Свойство.Имя;
			
			Если ИмяСвойства = "КураторПлощадки" Тогда 
				СтруктураРеквизитов.Вставить("Куратор", Стр.Значение);
			ИначеЕсли ИмяСвойства = "ВидПлощадки" Тогда 
				СтруктураРеквизитов.Вставить("ТипТочки", Стр.Значение);
			ИначеЕсли ИмяСвойства = "ГрафикРаботы" Тогда 
				СтруктураРеквизитов.Вставить("ГрафикРаботы", Стр.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Анкета.Дата КАК Дата,
		               |	РАЗНОСТЬДАТ(Анкета.Дата, &ТекДата, ДЕНЬ) КАК Период,
		               |	Анкета.Комментарий КАК Комментарий
		               |ИЗ
		               |	Документ.Анкета КАК Анкета
		               |ГДЕ
		               |	Анкета.Проведен
		               |	И Анкета.Респондент ССЫЛКА Справочник.ТорговыеПлощадки
		               |	И Анкета.Респондент = &ТорговаяПлощадка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Анкета.Дата УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Значение
		               |ИЗ
		               |	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		               |ГДЕ
		               |	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка = &ТорговаяПлощадка
		               |	И ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""ПартнерскаяПрограмма""
		               |	И ТорговыеПлощадкиДополнительныеРеквизиты.Значение ССЫЛКА Справочник.ЗначенияСвойствОбъектов
		               |			И ВЫРАЗИТЬ(ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Справочник.ЗначенияСвойствОбъектов).Наименование = ""ФМУ""";
		
		Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
		Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);				
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		Результат = МассивРезультатов[0];
		РезультатФМ = МассивРезультатов[1];		
		
		РезультатПрошлойПроверки = "";
		Если Результат.Пустой() Тогда 
			РезультатПрошлойПроверки = НСтр("ru = 'Точка еще не участвовала в анкетировании'; uk = 'Точка ще не приймала участь у анкетуванні'");
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
						
			МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Выборка.Комментарий, Символы.ПС, Истина, Истина);
			МассивПроверки = Новый Массив;
			Для Каждого Подстрока Из МассивПодстрок Цикл 
				НачалоСтроки = НРег(Лев(СокрЛП(Подстрока), 3));
				
				//Если НачалоСтроки = "чек" 
				Если НачалоСтроки = "кас" Тогда 				
					МассивПроверки.Добавить(Подстрока);
				КонецЕсли;
			КонецЦикла;
			
			РезультатПрошлойПроверки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Последняя проверка %1 (%2 дней назад)'; uk = 'Остання перевірка %1 (%2 днів назад)'"),
											Выборка.Дата,
											Выборка.Период);
											
			Если МассивПроверки.Количество() > 0 Тогда 
				Для Каждого Подстрока Из МассивПроверки Цикл 
					РезультатПрошлойПроверки = РезультатПрошлойПроверки + Символы.ПС + Подстрока;
				КонецЦикла;
			КонецЕсли;									
		КонецЕсли;
		
		СтруктураРеквизитов.Вставить("Информация", РезультатПрошлойПроверки);
		
		Если Не РезультатФМ.Пустой() Тогда 
			СтруктураРеквизитов.Вставить("ПрограммаФМ", Истина);	
		КонецЕсли;
		
		//ВидТочки = Неопределено;
		//Если СтруктураРеквизитов.Свойство("Вид", ВидТочки) Тогда 						
		//	стрВидТочки = СокрЛП(ВидТочки.Наименование);
		//	ШаблонАнкеты = Неопределено;
		//	Если стрВидТочки = "Киоск" Тогда 
		//		ШаблонАнкеты = Справочники.ШаблоныАнкет.НайтиПоКоду("000000001");
		//	ИначеЕсли стрВидТочки = "Павильон" Тогда 
		//		ШаблонАнкеты = Справочники.ШаблоныАнкет.НайтиПоКоду("000000002");
		//	КонецЕсли;
		//	
		//	Если ЗначениеЗаполнено(ШаблонАнкеты) Тогда 
		//		СтруктураРеквизитов.Вставить("ШаблонАнкеты", ШаблонАнкеты);
		//	КонецЕсли;
		//КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

	Возврат СтруктураРеквизитов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеЧека(КодСклада, Дата, НомерЧека)
	
	СтруктураЧека = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Формат(Дата, "ДФ=yyyy-MM-dd"));
	Запрос.УстановитьПараметр("Склад", КодСклада);
	Запрос.УстановитьПараметр("НомерЧека", НомерЧека);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	dbo_vRCPTS.Дата КАК Дата,
	               |	dbo_vRCPTS.Время КАК Время,
	               |	dbo_vRCPTS.PK КАК РК,
	               |	СУММА(dbo_vRCPTS.Сумма) КАК Сумма,
	               |	dbo_vRCPTS.ТипОплаты КАК ТипОплаты,
	               |	dbo_vRCPTS.ИннКассира КАК ИннКассира
	               |ИЗ
	               |	ВнешнийИсточникДанных.ОпПродажи.Таблица.dbo_vRCPTS КАК dbo_vRCPTS
	               |ГДЕ
	               |	dbo_vRCPTS.Склад = &Склад
	               |	И dbo_vRCPTS.Дата = &Дата
	               |	И НЕ dbo_vRCPTS.Отмена
	               |	И dbo_vRCPTS.Чек_ = &НомерЧека
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	dbo_vRCPTS.PK,
	               |	dbo_vRCPTS.Дата,
	               |	dbo_vRCPTS.ИннКассира,
	               |	dbo_vRCPTS.ТипОплаты,
	               |	dbo_vRCPTS.Время";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		СтруктураЧека.Вставить("Дата", СтроковыеФункцииКлиентСервер.СтрокаВДату(Выборка.Дата + " " + Выборка.Время, ЧастиДаты.ДатаВремя));	
		СтруктураЧека.Вставить("РК", Выборка.РК);
		СтруктураЧека.Вставить("НомерЧека", НомерЧека);
		СтруктураЧека.Вставить("СуммаЧека", Выборка.Сумма);
		СтруктураЧека.Вставить("ТипОплаты", Выборка.ТипОплаты);
		
		ИНН = СокрЛП(Выборка.ИннКассира);			
		Реализатор = Справочники.ФизическиеЛица.НайтиПоКоду(ИНН); 
		
		Если Не Реализатор.Пустая() Тогда 
			СтруктураЧека.Вставить("Реализатор", Реализатор);
			СтруктураЧека.Вставить("ПредставлениеРеализатора", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1) %2", ИНН, Строка(Реализатор)));
		КонецЕсли;
		
		СтруктураЧека.Вставить("ИНН", ИНН);
	КонецЕсли;

	Возврат СтруктураЧека;	
КонецФункции


#КонецОбласти




#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
 
#Область ПрограммныйИнтерфейс
  
Процедура ВыгрузитьДанные() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	УзелОбменаЗУП = ПланыОбмена.ОбменZUP.НайтиПоКоду("ZUP");
	УзелОбменаЗУП_Аттика = ПланыОбмена.ОбменZUP.НайтиПоКоду("ZUP_A");
	
	МассивУзловОбменаЗУП = ПланыОбмена.ОбменZUP.ПолучитьМассивУзловПланаОбмена();
	
	Если МассивУзловОбменаЗУП.Найти(УзелОбменаЗУП_Аттика) <> Неопределено Тогда 
		Параметры = Новый Структура;
		Параметры.Вставить("ИмяБазы", 		"ЗУП_Аттика");
		Параметры.Вставить("Пользователь", 	"obmenTK_ws");
		Параметры.Вставить("Пароль", 		"<fhf<fy");
		Параметры.Вставить("УзелОбмена", 	УзелОбменаЗУП_Аттика);
		Параметры.Вставить("АдресСервиса",	"https://orders-1c.digma.ua/work_UU_Zarplata_Attika/ws/cwd2.1cws?wsdl");
		
		ВыполнитьВыгрузкуГрафикиРаботы(Параметры);
	КонецЕсли;
	
	Если МассивУзловОбменаЗУП.Найти(УзелОбменаЗУП) <> Неопределено Тогда 
		Параметры = Новый Структура;
		Параметры.Вставить("ИмяБазы", 		"ЗУП_УУ");
		Параметры.Вставить("Пользователь", 	"ObmenSSL");
		Параметры.Вставить("Пароль", 		"bynthghbpf");
		Параметры.Вставить("УзелОбмена", 	УзелОбменаЗУП);
		Параметры.Вставить("АдресСервиса",	"https://orders-1c.digma.ua/zuUU/ws/cwd2.1cws?wsdl");
		
		ВыполнитьВыгрузкуГрафикиРаботы(Параметры);
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти
 
 
#Область СлужебныеПроцедурыИФункции 

Функция ПолучитьТаблицуРежимовРаботыТТДляВыгрузкиЗУП(УзелОбмена, ТекстОшибки = "", Отказ = Ложь) Экспорт 	
	
	Отказ = Ложь;
	Ошибки = Новый ТекстовыйДокумент;
	
	ТаблицаРежимов = Новый ТаблицаЗначений;
	ТаблицаРежимов.Колонки.Добавить("ТорговаяПлощадка");
	ТаблицаРежимов.Колонки.Добавить("Код", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	ТаблицаРежимов.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРежимов.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(61));
	ТаблицаРежимов.Колонки.Добавить("РежимРаботыСтрокой", ОбщегоНазначения.ОписаниеТипаСтрока(50));	
	
	Если Не Отказ Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТорговыеПлощадкиИзменения.Ссылка КАК Ссылка
		               |ПОМЕСТИТЬ втТорговыеПлощадки
		               |ИЗ
		               |	Справочник.ТорговыеПлощадки.Изменения КАК ТорговыеПлощадкиИзменения
		               |ГДЕ
		               |	ТорговыеПлощадкиИзменения.Узел = &Узел
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	втТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
		               |	втТорговыеПлощадки.Ссылка.Код КАК Код,
		               |	ЕСТЬNULL(ТК_РежимыРаботыТорговыхПлощадок.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК Период,
		               |	ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Наименование КАК Наименование,
		               |	ВЫБОР
		               |		КОГДА НЕ ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Ссылка ЕСТЬ NULL
		               |			ТОГДА РАЗНОСТЬДАТ(ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Будни.Начало, ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Будни.Окончание, МИНУТА)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК РежимБудни,
		               |	ВЫБОР
		               |		КОГДА НЕ ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Ссылка ЕСТЬ NULL
		               |			ТОГДА РАЗНОСТЬДАТ(ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Выходные.Начало, ТК_РежимыРаботыТорговыхПлощадок.РежимРаботы.Выходные.Окончание, МИНУТА)
		               |		ИНАЧЕ 0
		               |	КОНЕЦ КАК РежимВыходные
		               |ИЗ
		               |	втТорговыеПлощадки КАК втТорговыеПлощадки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РежимыРаботыТорговыхПлощадок КАК ТК_РежимыРаботыТорговыхПлощадок
		               |		ПО втТорговыеПлощадки.Ссылка = ТК_РежимыРаботыТорговыхПлощадок.ТорговаяПлощадка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТорговаяПлощадка,
		               |	Период";
		
		Запрос.УстановитьПараметр("Узел", УзелОбмена);			
		
		Результат = Запрос.Выполнить();				
		
		Если Не Результат.Пустой() Тогда 
			Выборка = Результат.Выбрать();
			Разделитель = "|";
			Пока Выборка.Следующий() Цикл 
				НоваяСтрока = ТаблицаРежимов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "Код,Период,Наименование,ТорговаяПлощадка");
				
				РежимРаботыСтрокой = "";
				РежимРаботыСтрокой = ДописатьВСтрокуЗначения(РежимРаботыСтрокой, Выборка.РежимБудни, 5, Разделитель);
				РежимРаботыСтрокой = ДописатьВСтрокуЗначения(РежимРаботыСтрокой, Выборка.РежимВыходные, 2, Разделитель);
				
				РежимРаботыСтрокой = СтрЗаменить(РежимРаботыСтрокой, ",", ".");
				РежимРаботыСтрокой = СтрЗаменить(РежимРаботыСтрокой, Разделитель, ",");
				
				НоваяСтрока.РежимРаботыСтрокой = РежимРаботыСтрокой;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Ошибки.КоличествоСтрок() > 0 Тогда 
		ТекстОшибки = Ошибки.ПолучитьТекст();
	КонецЕсли;
	
	Возврат ТаблицаРежимов;
	
КонецФункции

Процедура ВыполнитьВыгрузкуГрафикиРаботы(Параметры, Отказ = Ложь)
			
	ИмяБазы 		= Параметры.ИмяБазы;
	Пользователь	= Параметры.Пользователь;
	Пароль 			= Параметры.Пароль;
	АдресСервиса 	= Параметры.АдресСервиса;
	УзелОбмена		= Параметры.УзелОбмена;
	
	ИдентификаторОбработки = "ТК_ГрафикиРаботы_ЗУП";		
	КлючОбработки = "ВыгрузкаГрафикиРаботы_В_" + ИмяБазы;
	
	СтруктураЗаписиЖурнала = РегистрыСведений.РезультатыВыполненияОбработок.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = ИдентификаторОбработки;
	СтруктураЗаписиЖурнала.КлючОбработки = КлючОбработки;
	РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Начало обработки.", СтруктураЗаписиЖурнала);
		
	ТаблицаРежимов = Неопределено;	
	ТекстОшибки = "";	
	Попытка
		ТаблицаРежимов = ПолучитьТаблицуРежимовРаботыТТДляВыгрузкиЗУП(УзелОбмена, ТекстОшибки, Отказ);		
	Исключение
		ТекстОшибки = ОписаниеОшибки();	
		Отказ = Истина;		
	КонецПопытки;
	
	ЕстьДанныеДляВыгрузки = Истина;
	
	Если Отказ Тогда  
		Если ПустаяСтрока(ТекстОшибки) Тогда 
			ТекстОшибки = "Ошибка получения данных для обмена";
		КонецЕсли;
		
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния: Ошибка получения данных", СтруктураЗаписиЖурнала);
		РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание: " + ТекстОшибки, СтруктураЗаписиЖурнала);												
	ИначеЕсли ТаблицаРежимов.Количество() = 0 Тогда 		
		ЕстьДанныеДляВыгрузки = Ложь;
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий("Нет данных для выгрузки", СтруктураЗаписиЖурнала);		
	КонецЕсли;	
	
	Если Не Отказ И ЕстьДанныеДляВыгрузки Тогда 
		Попытка
			Определение = Новый WSОпределения(АдресСервиса, Пользователь,Пароль,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
			WS = Новый WSПрокси(Определение,"http://www.digma.ua", "CommunicationWithDatabases", "CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			WS.Пользователь = Пользователь;
			WS.Пароль = Пароль;
		Исключение 	
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния: Не удалось подключится к базе """+ ИмяБазы +"""", СтруктураЗаписиЖурнала);
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание: " + ОписаниеОшибки(), СтруктураЗаписиЖурнала);												
		КонецПопытки;
	КонецЕсли;
	
	
	Если Не Отказ И ЕстьДанныеДляВыгрузки Тогда 
		мТорговыеПлощадки = ТаблицаРежимов.ВыгрузитьКолонку("ТорговаяПлощадка");
		мТорговыеПлощадки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мТорговыеПлощадки);
		
		ПланыОбмена.ВыбратьИзменения(УзелОбмена, 1, мТорговыеПлощадки);
		
		ТаблицаРежимов.Колонки.Удалить("ТорговаяПлощадка");	
		ХранилищеЗначений = Новый ХранилищеЗначения(ТаблицаРежимов, Новый СжатиеДанных(9));
				
		РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выгружено %1 записей по %2 точкам'"),
				ТаблицаРежимов.Количество(),
				мТорговыеПлощадки.Количество()),
			СтруктураЗаписиЖурнала);		
		
		
		ТекстОшибки = "";	
		Попытка
			Ответ = WS.UpdateTradePointsOperationHours(ХранилищеЗначений);					
			
			Если Ответ.Status Тогда 
				Если Не ПустаяСтрока(Ответ.Description) Тогда 
					РегистрыСведений.РезультатыВыполненияОбработок.ДобавитьКомментарий(Ответ.Description, СтруктураЗаписиЖурнала);
				КонецЕсли;
				
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, 1);
			Иначе
				Отказ = Истина;
				ТекстОшибки = Ответ.Description;
			КонецЕсли;
		Исключение
			Отказ = Истина;		
			ТекстОшибки = ОписаниеОшибки();
		КонецПопытки;
		
		Если Отказ Тогда 
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("КодСостояния: Ошибка обмена данными", СтруктураЗаписиЖурнала);
			РегистрыСведений.РезультатыВыполненияОбработок.СообщитьОбОшибке("Описание: " + ТекстОшибки, СтруктураЗаписиЖурнала);												
		КонецЕсли;
	КонецЕсли;
	
	РегистрыСведений.РезультатыВыполненияОбработок.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиЖурнала, Не Отказ);		
	
КонецПроцедуры


#КонецОбласти
 
#Область ПрочиеПроцедурыИФункции

Функция ДописатьВСтрокуЗначения(Знач ТекстСтрока, Знач Значение, КоличествоПовторов, Разделитель = ",")
	
	Если ПустаяСтрока(Значение) Тогда 
		Возврат ТекстСтрока;
	КонецЕсли;
	
	РежимЧас = Окр(Значение / 60, 1);
	
	Для Ит = 1 По КоличествоПовторов Цикл 
		Если Не ПустаяСтрока(ТекстСтрока) Тогда 
			ТекстСтрока = ТекстСтрока + Разделитель;
		КонецЕсли;
		
		ТекстСтрока = ТекстСтрока + СокрЛП(РежимЧас);
	КонецЦикла;
		
	Возврат ТекстСтрока;

	
КонецФункции


#КонецОбласти
 
  
#КонецЕсли

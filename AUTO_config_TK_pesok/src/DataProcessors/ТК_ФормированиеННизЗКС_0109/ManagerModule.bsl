
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

Функция ПолучитьСпискиНалоговыхНакладных(
			Знач ДеревоНН,
			Знач ДеревоМРЦ,
			Знач ДеревоБезНДС,
			Знач ДеревоКомпенсирующие,
			Знач ТаблицаПриложения2,
			Знач ПараметрыОперации) Экспорт 
			
	УстановитьПривилегированныйРежим(Истина);				
	
	СтруктураВидовНН = ПараметрыОперации.СтруктураВидовНН;
	
	НалоговыеИзЗКС = СтруктураВидовНН.Свойство("НалоговыеИзЗКС");
	НалоговыеМРЦ = СтруктураВидовНН.Свойство("НалоговыеИзЗКСМРЦ");
	НалоговыеБезНДС = СтруктураВидовНН.Свойство("НалоговыеБезНДС");
	НалоговыеКомпенсирующие = СтруктураВидовНН.Свойство("НалоговыеКомпенсирующие");
	
	Если НалоговыеИзЗКС ИЛИ НалоговыеМРЦ ИЛИ  НалоговыеБезНДС Тогда 
		ЗаполнитьДанныеИзЗКС(ДеревоНН, ДеревоМРЦ, ДеревоБезНДС, ТаблицаПриложения2, ПараметрыОперации);
	КонецЕсли;
	
	Если НалоговыеКомпенсирующие Тогда 
		ЗаполнитьДанныеКомпенсирующиеНН(ДеревоКомпенсирующие, ПараметрыОперации);	
	КонецЕсли;
	
	
	Результат = Новый Структура;
	
	Если НалоговыеИзЗКС И ДеревоНН.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоЗначений", ДеревоНН);
	КонецЕсли;
	
	Если НалоговыеМРЦ И ДеревоМРЦ.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоННМРЦ", ДеревоМРЦ);
	КонецЕсли;
	
	Если НалоговыеБезНДС И ДеревоБезНДС.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоБезНДС", ДеревоБезНДС);
	КонецЕсли;
	
	Если НалоговыеКомпенсирующие И ДеревоКомпенсирующие.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоКомпенсирующие", ДеревоКомпенсирующие);
	КонецЕсли;
	
	Если ТаблицаПриложения2.Количество() > 0 Тогда 
		Результат.Вставить("ТаблицаПриложения2", ТаблицаПриложения2);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьНалоговыеНакладные(
			Знач ДеревоНалоговые,
			Знач ДеревоМРЦ,
			Знач ДеревоБезНДС, 
			Знач ДеревоКомпенсирующие,
			Знач ТаблицаПриложения2, 
			Знач ПараметрыОперации) Экспорт 
			
	ДанныеОрганизаций = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДанныеОрганизаций", Неопределено);			
	Контрагент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", КонецДня(ТекущаяДата()));
	ПерезаполнитьНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ПерезаполнитьНН", Ложь);
	
	СтруктураВидовНН = ПараметрыОперации.СтруктураВидовНН;
	
	НалоговыеИзЗКС = СтруктураВидовНН.Свойство("НалоговыеИзЗКС");
	НалоговыеМРЦ = СтруктураВидовНН.Свойство("НалоговыеИзЗКСМРЦ");
	НалоговыеБезНДС = СтруктураВидовНН.Свойство("НалоговыеБезНДС");
	НалоговыеКомпенсирующие = СтруктураВидовНН.Свойство("НалоговыеКомпенсирующие");

	ТорговаяПлощадка = Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000115");
	Подразделение = ТорговаяПлощадка.ПодразделениеКомпании;		
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Контрагент", Контрагент);
	ДанныеЗаполнения.Вставить("ТорговаяПлощадка", ТорговаяПлощадка); //Бизнес Групп Дигма
	ДанныеЗаполнения.Вставить("ДоговорВзаиморасчетов", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());	
	ДанныеЗаполнения.Вставить("ПодразделениеКомпании", Подразделение); 	
	ДанныеЗаполнения.Вставить("ОрганизацияФС", ПараметрыОперации.ОрганизацияФС);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НалоговыеИзЗКС Тогда 		
		Для Каждого СтрОрганизация Из ДеревоНалоговые.Строки Цикл 
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 				
				
				Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 								
					ТаблицаВозвраты = Неопределено;
					
					СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, ДанныеЗаполнения,ложь,2);
					
					Если ТаблицаВозвраты.Количество() > 0 Тогда
						СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, ДанныеЗаполнения);
					КонецЕсли;								
				КонецЕсли;
							
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если НалоговыеМРЦ Тогда 
		Для Каждого СтрОрганизация Из ДеревоМРЦ.Строки Цикл 
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 				
				
				Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 								
					ТаблицаВозвраты = Неопределено;
					
					СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, ДанныеЗаполнения,ложь,1,Истина);
					
					Если ТаблицаВозвраты.Количество() > 0 Тогда
						СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, ДанныеЗаполнения);
					КонецЕсли;								
				КонецЕсли;
							
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;	
	
	Если НалоговыеБезНДС Тогда 
		Для Каждого СтрОрганизация Из ДеревоБезНДС.Строки Цикл 
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 
				
				Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 
					ТаблицаВозвраты = Неопределено;
					
					СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, ДанныеЗаполнения, Истина);
					
					Если ТаблицаВозвраты.Количество() > 0 Тогда 
						СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, ДанныеЗаполнения);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если НалоговыеКомпенсирующие Тогда 
		Для Каждого СтрОрганизация Из ДеревоКомпенсирующие.Строки Цикл 
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 
				
				Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 
					СоздатьДокументНН_Компенсирующий(ИтогиДня, ДанныеЗаполнения);	
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Если НалоговыеИзЗКС И ДеревоНалоговые.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоЗначений", ДеревоНалоговые);
	КонецЕсли;
	
	Если НалоговыеМРЦ И ДеревоМРЦ.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоМРЦ", ДеревоМРЦ);
	КонецЕсли;
	
	Если НалоговыеБезНДС И ДеревоБезНДС.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоБезНДС", ДеревоБезНДС);
	КонецЕсли;
	
	Если НалоговыеКомпенсирующие И ДеревоКомпенсирующие.Строки.Количество() > 0 Тогда 
		Результат.Вставить("ДеревоКомпенсирующие", ДеревоКомпенсирующие);
	КонецЕсли;
	
	Если ТаблицаПриложения2.Количество() > 0 Тогда 
		Результат.Вставить("ТаблицаПриложения2", ТаблицаПриложения2);
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьВыгруженныеФайлы(
			ДеревоНалоговые,
			ДеревоМРЦ,
			ДеревоБезНДС, 
			ДеревоКомпенсирующие,
			ТаблицаПриложения2, 
			Знач ПараметрыОперации) Экспорт 
			
	УстановитьПривилегированныйРежим(Истина);				
	
	МассивНН = Новый Массив;
	МассивП2НН = Новый Массив;	
	
	НалоговыеИзЗКС 			= ПараметрыОперации.Свойство("НалоговыеИзЗКС");
	НалоговыеИзЗКСМРЦ 		= ПараметрыОперации.Свойство("НалоговыеИзЗКСМРЦ");
	НалоговыеБезНДС 		= ПараметрыОперации.Свойство("НалоговыеБезНДС");
	НалоговыеКомпенсирующие = ПараметрыОперации.Свойство("НалоговыеКомпенсирующие");
	
	Если НалоговыеИзЗКС Тогда 
		Для Каждого СтрОрганизация Из ДеревоНалоговые.Строки Цикл
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл
				Если Не ИтогиДня.ДокументНН.Пустая() Тогда 
					МассивНН.Добавить(ИтогиДня.ДокументНН);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если НалоговыеИзЗКСМРЦ Тогда 
		Для Каждого СтрОрганизация Из ДеревоМРЦ.Строки Цикл
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл
				Если Не ИтогиДня.ДокументНН.Пустая() Тогда 
					МассивНН.Добавить(ИтогиДня.ДокументНН);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

	
	Если НалоговыеБезНДС Тогда 
		Для Каждого СтрОрганизация Из ДеревоБезНДС.Строки Цикл
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл
				Если Не ИтогиДня.ДокументНН.Пустая() Тогда 
					МассивНН.Добавить(ИтогиДня.ДокументНН);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если НалоговыеКомпенсирующие Тогда 
		Для Каждого СтрОрганизация Из ДеревоКомпенсирующие.Строки Цикл
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл
				Если Не ИтогиДня.ДокументНН.Пустая() Тогда 
					МассивНН.Добавить(ИтогиДня.ДокументНН);
				КонецЕсли;
			КонецЦикла;     
		КонецЦикла;
	КонецЕсли;
	
	Если (НалоговыеИзЗКС ИЛИ НалоговыеИзЗКСМРЦ ИЛИ НалоговыеБезНДС) И ТаблицаПриложения2.Количество() > 0 Тогда 
		МассивП2НН = ТаблицаПриложения2.ВыгрузитьКолонку("Документ");
	КонецЕсли;
	
	МассивВыгрузки = Новый Массив;
	
	Если МассивНН.Количество() > 0 Тогда 
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			МассивВыгрузки,
			Документы.ТК_НалоговаяНакладная.СформироватьНалоговую(МассивНН));
			
	КонецЕсли;
		
	Если МассивП2НН.Количество() Тогда 
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			МассивВыгрузки,
			Документы.ТК_Приложение2КНалоговойНакладной.СформироватьПриложение2кНН(МассивП2НН));
		
	КонецЕсли;
		
	УИД = Новый УникальныйИдентификатор;
	МассивФайлов = Новый Массив;
	
	Для Каждого СтруктураДокумента Из МассивВыгрузки Цикл 
		МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(СтруктураДокумента.Выгрузка.ИмяФайла, ПоместитьВоВременноеХранилище(СтруктураДокумента.Выгрузка.ДанныеXML,УИД)));
	КонецЦикла;
	
	
	Возврат МассивФайлов;	
	
КонецФункции

Процедура ВыгрузитьВБухгалтерию(МассивНН) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("НалоговаяНакладнаяМассив", МассивНН);
	
	Результат = Обработки.ВыгрузкаДокументовВБухгалтерию.ВыгрузитьДокументыВБухгалтерию(ПараметрыВыгрузки);
	
	Если Не Результат.Статус Тогда 
		Сообщение = Результат.Сообщение;
	Иначе
		Сообщение = "Документы выгружены!";
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(Сообщение);
КонецПроцедуры


#КонецОбласти
	
		    
#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДанныеИзЗКС(ДеревоСтроки, ДеревоМРЦ, ДеревоБезНДС, ТаблицаПриложения2, ПараметрыОперации) 
						
	ДанныеОрганизаций = Неопределено;
	ТекДата = ТекущаяДата();
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Организация", Справочники.Организации.ПустаяСсылка());
	Контрагент  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаНачала	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", НачалоДня(ТекДата));
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаОкончания", КонецДня(ТекДата));
	
	СтруктураВидовНН = ПараметрыОперации.СтруктураВидовНН;
	
	НалоговыеИзЗКС = СтруктураВидовНН.Свойство("НалоговыеИзЗКС");
	НалоговыеИзЗКСМРЦ = СтруктураВидовНН.Свойство("НалоговыеИзЗКСМРЦ");
	ПолучитьННБезНДС = СтруктураВидовНН.Свойство("НалоговыеБезНДС");
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("Украина", Справочники.СтраныМира.НайтиПоКоду("804"));
	Запрос.УстановитьПараметр("ТорговаяПлощадка", Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000115"));
	Запрос.УстановитьПараметр("ПолучитьТоварыБезНДС", ПолучитьННБезНДС);
	Запрос.УстановитьПараметр("ЭтоФабрикаСыра", Организация = ПараметрыОперации.ОрганизацияФС);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	МИНИМУМ(ВЫБОР
	               |			КОГДА тчТовары.Возврат
	               |					И НЕ тчТовары.ДатаВозврата = ДАТАВРЕМЯ(1, 1, 1)
	               |				ТОГДА тчТовары.ДатаВозврата
	               |			ИНАЧЕ НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ)
	               |		КОНЕЦ) КАК ДатаОперации,
	               |	тчТовары.Ссылка.Организация КАК Организация,
	               |	тчТовары.Номенклатура.Артикул КАК Артикул,
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.Номенклатура.КодУКТВЭД КАК КодУКТВЭД,
	               |	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	тчТовары.СтавкаНДС.Ставка КАК Ставка,
	               |	СУММА(тчТовары.КоличествоБазовое) КАК Количество,
	               |	СУММА(тчТовары.СуммаВсего - тчТовары.СуммаНДС - тчТовары.СуммаАкцизногоНалога) КАК СуммаБезНДС,
	               |	СУММА(тчТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(тчТовары.СуммаВсего - тчТовары.СуммаАкцизногоНалога) КАК Сумма,
	               |	тчТовары.СуммаАкцизногоНалога <> 0 КАК ПодакцизныйТовар,
	               |	тчТовары.Номенклатура.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	               |		И тчТовары.Номенклатура.СтранаПроисхождения <> &Украина КАК Импорт,
	               |	ВЫБОР
	               |		КОГДА &ЭтоФабрикаСыра
	               |			ТОГДА тчТовары.Цена
	               |		ИНАЧЕ ВЫРАЗИТЬ(тчТовары.СуммаВсего / тчТовары.КоличествоБазовое КАК ЧИСЛО(15, 3))
	               |	КОНЕЦ КАК Цена
	               |ПОМЕСТИТЬ втПродажиЗКС
	               |ИЗ
	               |	Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК тчТовары
	               |ГДЕ
	               |	тчТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И тчТовары.Ссылка.Организация = &Организация
	               |	И тчТовары.Ссылка.Проведен
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тчТовары.Ссылка.Организация,
	               |	тчТовары.Номенклатура.КодУКТВЭД,
	               |	тчТовары.СуммаАкцизногоНалога <> 0,
	               |	тчТовары.Номенклатура.Артикул,
	               |	НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ),
	               |	тчТовары.СтавкаНДС.Ставка,
	               |	тчТовары.Номенклатура,
	               |	тчТовары.Номенклатура.СтранаПроисхождения <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	               |		И тчТовары.Номенклатура.СтранаПроисхождения <> &Украина,
	               |	тчТовары.ХарактеристикаНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА &ЭтоФабрикаСыра
	               |			ТОГДА тчТовары.Цена
	               |		ИНАЧЕ ВЫРАЗИТЬ(тчТовары.СуммаВсего / тчТовары.КоличествоБазовое КАК ЧИСЛО(15, 3))
	               |	КОНЕЦ
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(тчТовары.КоличествоБазовое) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_НалоговаяНакладная.Ссылка КАК ДокументНН,
	               |	ТК_НалоговаяНакладная.Дата КАК ДатаДокумента,
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладная.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Номер КАК Номер,
	               |	ТК_НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
	               |	ТК_НалоговаяНакладная.Комментарий КАК Комментарий,
	               |	ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая) КАК БезНДС
	               |ПОМЕСТИТЬ ВтНалоговые
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	НЕ ТК_НалоговаяНакладная.ПометкаУдаления
	               |	И ТК_НалоговаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладная.Организация = &Организация
	               |	И ТК_НалоговаяНакладная.Контрагент = &Контрагент
	               |	И (ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
	               |			ИЛИ ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая))
	               |	И ТК_НалоговаяНакладная.ТорговаяПлощадка = &ТорговаяПлощадка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_Приложение2КНалоговойНакладной.Дата КАК Дата,
	               |	ТК_Приложение2КНалоговойНакладной.Организация КАК Организация,
	               |	ТК_Приложение2КНалоговойНакладной.СуммаДокумента КАК Сумма,
	               |	ТК_Приложение2КНалоговойНакладной.СуммаНДСДокумента КАК СуммаНДС,
	               |	ТК_Приложение2КНалоговойНакладной.Ссылка КАК Документ,
	               |	ТК_Приложение2КНалоговойНакладной.Номер КАК Номер,
	               |	ТК_Приложение2КНалоговойНакладной.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
	               |	ТК_Приложение2КНалоговойНакладной.ТорговаяПлощадка КАК ТорговаяПлощадка
	               |ПОМЕСТИТЬ ВтПрил2
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной КАК ТК_Приложение2КНалоговойНакладной
	               |ГДЕ
	               |	ТК_Приложение2КНалоговойНакладной.Проведен
	               |	И ТК_Приложение2КНалоговойНакладной.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_Приложение2КНалоговойНакладной.Организация = &Организация
	               |	И ТК_Приложение2КНалоговойНакладной.Контрагент = &Контрагент
	               |	И ТК_Приложение2КНалоговойНакладной.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтНалоговые.ДокументНН КАК ДокументНН,
	               |	ВтНалоговые.ДатаДокумента КАК ДатаДокумента,
	               |	ВтНалоговые.Дата КАК Дата,
	               |	ВтНалоговые.Организация КАК Организация,
	               |	ВтНалоговые.Номер КАК Номер,
	               |	ВтНалоговые.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
	               |	ВтНалоговые.Комментарий КАК Комментарий,
	               |	ВтНалоговые.БезНДС КАК БезНДС,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Признак = 0
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК МРЦ
	               |ИЗ
	               |	ВтНалоговые КАК ВтНалоговые
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			СУММА(ВЫБОР
	               |					КОГДА ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |						ТОГДА 0
	               |					ИНАЧЕ 1
	               |				КОНЕЦ) КАК Признак,
	               |			ТК_НалоговаяНакладнаяТовары.Ссылка.Ссылка КАК Ссылка
	               |		ИЗ
	               |			ВтНалоговые КАК ВтНалоговые,
	               |			Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ТК_НалоговаяНакладнаяТовары.Ссылка.Ссылка) КАК ВложенныйЗапрос
	               |		ПО ВтНалоговые.ДокументНН = ВложенныйЗапрос.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтПрил2.Дата КАК Дата,
	               |	ВтПрил2.Организация КАК Организация,
	               |	СРЕДНЕЕ(ВтПрил2.Сумма) КАК Сумма,
	               |	СРЕДНЕЕ(ВтПрил2.СуммаНДС) КАК СуммаНДС,
	               |	ВтПрил2.Документ КАК Документ,
	               |	ВтПрил2.Номер КАК Номер,
	               |	ВтПрил2.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
	               |	ВтПрил2.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Признак = 0
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК МРЦ
	               |ИЗ
	               |	ВтПрил2 КАК ВтПрил2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВтПрил2.Документ КАК Ссылка,
	               |			ВЫБОР
	               |				КОГДА ТК_Приложение2КНалоговойНакладнойТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.Пустаяссылка)
	               |					ТОГДА 0
	               |				ИНАЧЕ 1
	               |			КОНЕЦ КАК Признак
	               |		ИЗ
	               |			ВтПрил2 КАК ВтПрил2
	               |				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |				ПО ВтПрил2.Документ = ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Ссылка) КАК ВложенныйЗапрос
	               |		ПО ВтПрил2.Документ = ВложенныйЗапрос.Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтПрил2.Дата,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Признак = 0
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ,
	               |	ВтПрил2.Номер,
	               |	ВтПрил2.Документ,
	               |	ВтПрил2.Организация,
	               |	ВтПрил2.НомерДляБухгалтерии,
	               |	ВтПрил2.ТорговаяПлощадка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втПродажиЗКС.Дата КАК Дата,
	               |	втПродажиЗКС.ДатаОперации КАК ДатаОперации,
	               |	втПродажиЗКС.Организация КАК Организация,
	               |	втПродажиЗКС.Артикул КАК Артикул,
	               |	втПродажиЗКС.Номенклатура КАК Номенклатура,
	               |	втПродажиЗКС.КодУКТВЭД КАК КодУКТВЭД,
	               |	втПродажиЗКС.Ставка КАК Ставка,
	               |	втПродажиЗКС.Количество КАК Количество,
	               |	втПродажиЗКС.СуммаБезНДС КАК СуммаБезНДС,
	               |	втПродажиЗКС.СуммаНДС КАК СуммаНДС,
	               |	втПродажиЗКС.Сумма КАК Сумма,
	               |	втПродажиЗКС.ПодакцизныйТовар КАК ПодакцизныйТовар,
	               |	втПродажиЗКС.Импорт КАК Импорт,
	               |	втПродажиЗКС.Цена КАК Цена
	               |ИЗ
	               |	втПродажиЗКС КАК втПродажиЗКС
	               |ГДЕ
	               |	втПродажиЗКС.Ставка <> 0
	               |	И втПродажиЗКС.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.характеристикиноменклатуры.пустаяссылка)
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(СуммаБезНДС),
	               |	СУММА(СуммаНДС),
	               |	СУММА(Сумма)
	               |ПО
	               |	Организация,
	               |	Дата
	               |АВТОУПОРЯДОЧИВАНИЕ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втПродажиЗКС.Дата КАК Дата,
	               |	втПродажиЗКС.ДатаОперации КАК ДатаОперации,
	               |	втПродажиЗКС.Организация КАК Организация,
	               |	втПродажиЗКС.Артикул КАК Артикул,
	               |	втПродажиЗКС.Номенклатура КАК Номенклатура,
	               |	втПродажиЗКС.КодУКТВЭД КАК КодУКТВЭД,
	               |	втПродажиЗКС.Ставка КАК Ставка,
	               |	втПродажиЗКС.Количество КАК Количество,
	               |	втПродажиЗКС.СуммаБезНДС КАК СуммаБезНДС,
	               |	втПродажиЗКС.СуммаНДС КАК СуммаНДС,
	               |	втПродажиЗКС.Сумма КАК Сумма,
	               |	втПродажиЗКС.ПодакцизныйТовар КАК ПодакцизныйТовар,
	               |	втПродажиЗКС.Импорт КАК Импорт,
	               |	втПродажиЗКС.Цена КАК Цена
	               |ИЗ
	               |	втПродажиЗКС КАК втПродажиЗКС
	               |ГДЕ
	               |	втПродажиЗКС.Ставка <> 0
	               |	И втПродажиЗКС.ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.характеристикиноменклатуры.пустаяссылка)
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(СуммаБезНДС),
	               |	СУММА(СуммаНДС),
	               |	СУММА(Сумма)
	               |ПО
	               |	Организация,
	               |	Дата
	               |АВТОУПОРЯДОЧИВАНИЕ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втПродажиЗКС.Дата КАК Дата,
	               |	втПродажиЗКС.ДатаОперации КАК ДатаОперации,
	               |	втПродажиЗКС.Организация КАК Организация,
	               |	втПродажиЗКС.Артикул КАК Артикул,
	               |	втПродажиЗКС.Номенклатура КАК Номенклатура,
	               |	втПродажиЗКС.КодУКТВЭД КАК КодУКТВЭД,
	               |	втПродажиЗКС.Ставка КАК Ставка,
	               |	втПродажиЗКС.Количество КАК Количество,
	               |	втПродажиЗКС.СуммаБезНДС КАК СуммаБезНДС,
	               |	втПродажиЗКС.СуммаНДС КАК СуммаНДС,
	               |	втПродажиЗКС.Сумма КАК Сумма,
	               |	втПродажиЗКС.ПодакцизныйТовар КАК ПодакцизныйТовар,
	               |	втПродажиЗКС.Импорт КАК Импорт,
	               |	втПродажиЗКС.Цена КАК Цена
	               |ИЗ
	               |	втПродажиЗКС КАК втПродажиЗКС
	               |ГДЕ
	               |	&ПолучитьТоварыБезНДС
	               |	И втПродажиЗКС.Ставка = 0
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(СуммаБезНДС),
	               |	СУММА(СуммаНДС),
	               |	СУММА(Сумма)
	               |ПО
	               |	Организация,
	               |	Дата
	               |АВТОУПОРЯДОЧИВАНИЕ";
				   	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КолВоРез = МассивРезультатов.ВГраница();
	
	ТаблицаНН 			= МассивРезультатов[КолВоРез-4].Выгрузить();
	ВыборкаП2кНН  		= МассивРезультатов[КолВоРез-3].Выбрать();
	
	
	Ошибки = Новый Соответствие;	
	
	СписокНомеровБухгалтерии = Новый ТаблицаЗначений;
	СписокНомеровБухгалтерии.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаСтрока(15));
	СписокНомеровБухгалтерии.Колонки.Добавить("Номер", ОбщегоНазначения.ОписаниеТипаСтрока(25));	

	
	Если НалоговыеИзЗКС Тогда 
		ВыборкаОрганизация	= МассивРезультатов[КолВоРез-2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
		// Налоговые накладные
		Пока ВыборкаОрганизация.Следующий() Цикл 
			СтрОрганизация = ДеревоСтроки.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрОрганизация, ВыборкаОрганизация, "Организация, СуммаБезНДС, СуммаНДС, Сумма");
			
			ВыборкаДата = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДата.Следующий() Цикл 
				ИД_Строки = Новый УникальныйИдентификатор;
				СтрДата = СтрОрганизация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрДата, ВыборкаДата, "Дата, Организация, СуммаБезНДС, СуммаНДС, Сумма");
				СтрДата.ИД_Строки = ИД_Строки;
				
				СуммыДня = Новый Структура("СуммаБезНДС_7, СуммаБезНДС_20, НДС7, НДС20", 0,0,0,0);

				ВыборкаТовары = ВыборкаДата.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл 
					Если ВыборкаТовары.Ставка = 20 Тогда 
						СуммыДня.НДС20 			= СуммыДня.НДС20 + ВыборкаТовары.СуммаНДС;
						СуммыДня.СуммаБезНДС_20	= СуммыДня.СуммаБезНДС_20 + ВыборкаТовары.СуммаБезНДС;
					ИначеЕсли ВыборкаТовары.Ставка = 7 Тогда
						СуммыДня.НДС7 			= СуммыДня.НДС7 + ВыборкаТовары.СуммаНДС;
						СуммыДня.СуммаБезНДС_7	= СуммыДня.СуммаБезНДС_7 + ВыборкаТовары.СуммаБезНДС;
					КонецЕсли;
			
					// Проверка
					Если ВыборкаТовары.Импорт Тогда
						КодУКТВЭД = СтрЗаменить(СокрЛП(ВыборкаТовары.КодУКТВЭД), " ", "");
						
						Если СтрДлина(КодУКТВЭД) <> 10 
							И Ошибки.Получить(ВыборкаТовары.Артикул) = Неопределено Тогда 
							
							ОбщегоНазначения.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Для товара [%1] %2 указан неправильный код УКТВЭД (%3)'; uk = 'Для товару [%1] %2 вказаний неправильний код УКТЗЕД (%3)'"),
							ВыборкаТовары.Артикул,
							ВыборкаТовары.Номенклатура,
							КодУКТВЭД),
							ВыборкаТовары.Номенклатура);
							
							Ошибки.Вставить(ВыборкаТовары.Артикул);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				ЗаполнитьЗначенияСвойств(СтрДата, СуммыДня);
				
				ОтборТоварыНН = Новый Структура("Дата, Организация, БезНДС, МРЦ", ВыборкаДата.Дата, ВыборкаОрганизация.Организация, Ложь, Ложь);					
				
				мНалоговые = ТаблицаНН.НайтиСтроки(ОтборТоварыНН);
				Если мНалоговые.Количество() > 0 Тогда 
					текНН = мНалоговые[0];
					СтрДата.ДокументНН = текНН.ДокументНН;
					СтрДата.НомерНакладной = ПолучитьНомерДокумента(ВыборкаДата.Дата, текНН.НомерДляБухгалтерии);
					СтрДата.НомерДляБухгалтерии = текНН.НомерДляБухгалтерии;
					
					НовСтрНН = СписокНомеровБухгалтерии.Добавить();
					НовСтрНН.Дата = Формат(текНН.ДатаДокумента, "ДФ=yyyyMMddHHmmss");
					НовСтрНН.Номер = СокрЛП(текНН.НомерДляБухгалтерии);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НалоговыеИзЗКСМРЦ Тогда 
		ВыборкаОрганизация	= МассивРезультатов[КолВоРез-1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
		// Налоговые накладные
		Пока ВыборкаОрганизация.Следующий() Цикл 
			СтрОрганизация = ДеревоМРЦ.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрОрганизация, ВыборкаОрганизация, "Организация, СуммаБезНДС, СуммаНДС, Сумма");
			
			ВыборкаДата = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДата.Следующий() Цикл 
				ИД_Строки = Новый УникальныйИдентификатор;
				СтрДата = СтрОрганизация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрДата, ВыборкаДата, "Дата, Организация, СуммаБезНДС, СуммаНДС, Сумма");
				СтрДата.ИД_Строки = ИД_Строки;
				
				СуммыДня = Новый Структура("СуммаБезНДС_20, НДС20", 0,0);
				
				ВыборкаТовары = ВыборкаДата.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл 
						СуммыДня.НДС20 			= СуммыДня.НДС20 + ВыборкаТовары.СуммаНДС;
						СуммыДня.СуммаБезНДС_20	= СуммыДня.СуммаБезНДС_20 + ВыборкаТовары.СуммаБезНДС;
					
					// Проверка
					Если ВыборкаТовары.Импорт Тогда
						КодУКТВЭД = СтрЗаменить(СокрЛП(ВыборкаТовары.КодУКТВЭД), " ", "");
						
						Если СтрДлина(КодУКТВЭД) <> 10 
							И Ошибки.Получить(ВыборкаТовары.Артикул) = Неопределено Тогда 
							
							ОбщегоНазначения.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Для товара [%1] %2 указан неправильный код УКТВЭД (%3)'; uk = 'Для товару [%1] %2 вказаний неправильний код УКТЗЕД (%3)'"),
							ВыборкаТовары.Артикул,
							ВыборкаТовары.Номенклатура,
							КодУКТВЭД),
							ВыборкаТовары.Номенклатура);
							
							Ошибки.Вставить(ВыборкаТовары.Артикул);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				ЗаполнитьЗначенияСвойств(СтрДата, СуммыДня);
				
				ОтборТоварыНН = Новый Структура("Дата, Организация, БезНДС, МРЦ", ВыборкаДата.Дата, ВыборкаОрганизация.Организация, Ложь, Истина);					
				
				мНалоговые = ТаблицаНН.НайтиСтроки(ОтборТоварыНН);
				Если мНалоговые.Количество() > 0 Тогда 
					текНН = мНалоговые[0];
					СтрДата.ДокументНН = текНН.ДокументНН;
					СтрДата.НомерНакладной = ПолучитьНомерДокумента(ВыборкаДата.Дата, текНН.НомерДляБухгалтерии);
					СтрДата.НомерДляБухгалтерии = текНН.НомерДляБухгалтерии;
					
					НовСтрНН = СписокНомеровБухгалтерии.Добавить();
					НовСтрНН.Дата = Формат(текНН.ДатаДокумента, "ДФ=yyyyMMddHHmmss");
					НовСтрНН.Номер = СокрЛП(текНН.НомерДляБухгалтерии);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;	
	
	// Налоговые накладные без НДС
	Если ПолучитьННБезНДС Тогда 
		
		ВыборкаОргБезНДС = МассивРезультатов[КолВоРез].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОргБезНДС.Следующий() Цикл 
			СтрОрганизация = ДеревоБезНДС.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрОрганизация, ВыборкаОргБезНДС, "Организация, Сумма");
			
			ВыборкаДата = ВыборкаОргБезНДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДата.Следующий() Цикл 
				СтрДата = СтрОрганизация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрДата, ВыборкаДата, "Дата, Организация, Сумма");
				
				ВыборкаТовары = ВыборкаДата.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл 					
					// Проверка
					Если ВыборкаТовары.Импорт Тогда
						КодУКТВЭД = СтрЗаменить(СокрЛП(ВыборкаТовары.КодУКТВЭД), " ", "");
						
						Если СтрДлина(КодУКТВЭД) <> 10 
							И Ошибки.Получить(ВыборкаТовары.Артикул) = Неопределено Тогда 
							
							ОбщегоНазначения.СообщитьПользователю(
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Для товара [%1] %2 указан неправильный код УКТВЭД (%3)'; uk = 'Для товару [%1] %2 вказаний неправильний код УКТЗЕД (%3)'"),
									ВыборкаТовары.Артикул,
									ВыборкаТовары.Номенклатура,
									КодУКТВЭД),
								ВыборкаТовары.Номенклатура);
							
							Ошибки.Вставить(ВыборкаТовары.Артикул);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
					
				ОтборТоварыНН = Новый Структура("Дата, Организация, БезНДС", ВыборкаДата.Дата, ВыборкаОргБезНДС.Организация, Истина);					
				
				мНалоговые = ТаблицаНН.НайтиСтроки(ОтборТоварыНН);
				Если мНалоговые.Количество() > 0 Тогда 
					текНН = мНалоговые[0];
					СтрДата.ДокументНН = текНН.ДокументНН;
					СтрДата.НомерНакладной = ПолучитьНомерДокумента(ВыборкаДата.Дата, текНН.НомерДляБухгалтерии);
					СтрДата.НомерДляБухгалтерии = текНН.НомерДляБухгалтерии;
					
					НовСтрНН = СписокНомеровБухгалтерии.Добавить();
					НовСтрНН.Дата = Формат(текНН.ДатаДокумента, "ДФ=yyyyMMddHHmmss");
					НовСтрНН.Номер = СокрЛП(текНН.НомерДляБухгалтерии);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	// Заполняем Приложения 2
	Пока ВыборкаП2кНН.Следующий() Цикл 
		НоваяСтрока = ТаблицаПриложения2.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаП2кНН);
	КонецЦикла;
	
	// Проверка выгружены ли накладные в Бухгалтерию	
	Если СписокНомеровБухгалтерии.Количество() > 0 И ПравоДоступа("Использование", Метаданные.Обработки.ВыгрузкаДокументовВБухгалтерию) Тогда 
		ПроверкаВыгрузкиВБухгалтерию(ДеревоСтроки, СписокНомеровБухгалтерии);	
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗаполнитьДанныеКомпенсирующиеНН(ДеревоКомпенсирующие, ПараметрыОперации)
	
	ДанныеОрганизаций = Неопределено;
	ТекДата = ТекущаяДата();
	
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Организация", Справочники.Организации.ПустаяСсылка());
	Контрагент  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаНачала	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", НачалоДня(ТекДата));
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаОкончания", КонецДня(ТекДата));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Организация КАК Организация,
	               |	ОстаткиОрганизации.Партия КАК Партия,
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиОрганизации.АналитикаУчета КАК АналитикаУчета,
	               |	СУММА(ОстаткиОрганизации.Количество) КАК Количество,
	               |	СУММА(ОстаткиОрганизации.Сумма) КАК Сумма,
	               |	СУММА(ОстаткиОрганизации.СуммаНДС) КАК СуммаНДС
	               |ПОМЕСТИТЬ втСписанныеПартии
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиОрганизации КАК ОстаткиОрганизации
	               |		ПО ТК_НалоговаяНакладнаяТовары.Ссылка = ОстаткиОрганизации.Регистратор
	               |			И ТК_НалоговаяНакладнаяТовары.АналитикаУчетаНоменклатурыОрганизаций = ОстаткиОрганизации.АналитикаУчета
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Проведен
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяСводнаяКомпенсирующая)
	               |	И ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	И ТК_НалоговаяНакладнаяТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры,
	               |	ОстаткиОрганизации.АналитикаУчета,
	               |	ОстаткиОрганизации.Партия,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Организация,
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСписанныеПартии.Дата КАК Дата,
	               |	втСписанныеПартии.Организация КАК Организация,
	               |	втСписанныеПартии.Номенклатура КАК Номенклатура,
	               |	втСписанныеПартии.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втСписанныеПартии.АналитикаУчета КАК АналитикаУчета,
	               |	втСписанныеПартии.Партия КАК Партия,
	               |	втСписанныеПартии.Количество КАК Количество,
	               |	втСписанныеПартии.Сумма КАК Сумма,
	               |	втСписанныеПартии.СуммаНДС КАК СуммаНДС
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	втСписанныеПартии КАК втСписанныеПартии
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиОрганизации КАК ОстаткиОрганизации
	               |		ПО втСписанныеПартии.АналитикаУчета = ОстаткиОрганизации.АналитикаУчета
	               |			И втСписанныеПартии.Партия = ОстаткиОрганизации.Партия
	               |			И (ОстаткиОрганизации.Период > ДАТАВРЕМЯ(2021, 12, 31, 23, 59, 59))
	               |			И (ОстаткиОрганизации.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	               |			И (ОстаткиОрганизации.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НДС_МРЦ))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.Организация КАК Организация,
	               |	втТовары.Дата КАК Дата,
	               |	СУММА(втТовары.Сумма - втТовары.СуммаНДС) КАК СуммаБезНДС,
	               |	СУММА(втТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(втТовары.Сумма) КАК Сумма
	               |ИЗ
	               |	втТовары КАК втТовары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТовары.Дата,
	               |	втТовары.Организация
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Организация,
	               |	Дата
	               |ИТОГИ
	               |	СУММА(СуммаБезНДС),
	               |	СУММА(СуммаНДС),
	               |	СУММА(Сумма)
	               |ПО
	               |	Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладная.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_НалоговаяНакладная.Дата КАК ДатаДок,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Ссылка КАК ДокументНН,
	               |	ТК_НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерии
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	ТК_НалоговаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладная.Проведен
	               |	И ТК_НалоговаяНакладная.Организация = &Организация
	               |	И ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяСводнаяКомпенсирующая)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Организация,
	               |	ДатаДок УБЫВ";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КолвоРез = МассивРезультатов.ВГраница();
	
	тзДокНН = МассивРезультатов[КолвоРез].Выгрузить();
	тзДокНН.Индексы.Добавить("Организация, Дата");
	
	ВыборкаОрганизация = МассивРезультатов[КолвоРез-1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Пока ВыборкаОрганизация.Следующий() Цикл 
		нСтрОрганизация = ДеревоКомпенсирующие.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(нСтрОрганизация, ВыборкаОрганизация, "Организация,СуммаБезНДС,СуммаНДС,Сумма");
		
		ВыборкаДата = ВыборкаОрганизация.Выбрать();
		Пока ВыборкаДата.Следующий() Цикл 
			нСтрДата = нСтрОрганизация.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(нСтрДата, ВыборкаДата);
			
			мДокНН = тзДокНН.НайтиСтроки(Новый Структура("Организация, Дата", ВыборкаДата.Организация, ВыборкаДата.Дата));
			Если мДокНН.Количество() > 0 Тогда 
				нСтрДата.ДокументНН = мДокНН[0].ДокументНН;
				нСтрДата.НомерДляБухгалтерии = мДокНН[0].НомерДляБухгалтерии;
				нСтрДата.НомерНакладной = ПолучитьНомерДокумента(ВыборкаДата.Дата, мДокНН[0].НомерДляБухгалтерии);				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры



Процедура СоздатьДокументНН(ДанныеДня, ТаблицаВозвраты, Знач ДанныеЗаполнения, БезНДС = Ложь, МРЦ = 3, ОкруглятьДень=Ложь)
	
	ДанныеЗаполнения.Вставить("Дата", КонецДня(ДанныеДня.Дата));
	ДанныеЗаполнения.Вставить("Организация", ДанныеДня.Организация);
	ДанныеЗаполнения.Вставить("Комментарий", "Документ создан обработкой ""Формирование Налоговых накладных"" на дату " + Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy"));
	ДанныеЗаполнения.Вставить("ХозОперация", ?(БезНДС, Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая, Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа));
	
	Если Не ДанныеДня.ДокументНН.Пустая() Тогда 		
		Док = ДанныеДня.ДокументНН.ПолучитьОбъект();
		НовыйДокумент = Ложь;
	Иначе
		Док = Документы.ТК_НалоговаяНакладная.СоздатьДокумент();
		НовыйДокумент = Истина;
	КонецЕсли;
	
	Док.Заполнить(ДанныеЗаполнения);
	
	СписокКодУКТВЭД = Новый СписокЗначений;
	СписокКодУКТВЭД.Добавить(Справочники.КлассификаторУКТВЭД.НайтиПоКоду("2402 20 90 20"));
	СписокКодУКТВЭД.Добавить(Справочники.КлассификаторУКТВЭД.НайтиПоКоду("2402 10 00 90"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ДанныеДня.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("Контрагент", ДанныеЗаполнения.Контрагент);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НовыйДокумент", НовыйДокумент);
	Запрос.УстановитьПараметр("ДокНН", Док.Ссылка);	
	Запрос.УстановитьПараметр("БезНДС", БезНДС);
	Запрос.УстановитьПараметр("МРЦ", МРЦ);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000115"));
	Запрос.УстановитьПараметр("СписокКодов", СписокКодУКТВЭД);
	Запрос.УстановитьПараметр("ЭтоФабрикаСыра", ДанныеДня.Организация = ДанныеЗаполнения.ОрганизацияФС);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	МИНИМУМ(ВЫБОР
	               |			КОГДА тчТовары.Возврат
	               |					И тчТовары.ДатаВозврата <> ДАТАВРЕМЯ(1, 1, 1)
	               |				ТОГДА тчТовары.ДатаВозврата
	               |			ИНАЧЕ НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ)
	               |		КОНЕЦ) КАК ДатаОперации,
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	СУММА(тчТовары.Количество) КАК КоличествоБазовое,
	               |	тчТовары.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(тчТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(тчТовары.Сумма - тчТовары.СуммаАкцизногоНалога) КАК Сумма,
	               |	СУММА(тчТовары.Сумма - тчТовары.СуммаНДС - тчТовары.СуммаАкцизногоНалога) КАК СуммаБезНалогов,
	               |	СУММА(тчТовары.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(тчТовары.СуммаВсего - тчТовары.СуммаАкцизногоНалога) КАК СуммаВсего,
	               |	ВЫБОР
	               |		КОГДА &ЭтоФабрикаСыра
	               |			ТОГДА тчТовары.Цена
	               |		ИНАЧЕ ВЫРАЗИТЬ(тчТовары.СуммаВсего / тчТовары.КоличествоБазовое КАК ЧИСЛО(15, 3))
	               |	КОНЕЦ КАК Цена,
	               |	тчТовары.СуммаАкцизногоНалога <> 0 КАК ПодакцизныйТовар,
	               |	СУММА(тчТовары.СуммаАкцизногоНалога) КАК Акциз
	               |ПОМЕСТИТЬ ТоварыЗКС
	               |ИЗ
	               |	Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК тчТовары
	               |ГДЕ
	               |	тчТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И тчТовары.Ссылка.Проведен
	               |	И тчТовары.Ссылка.Организация = &Организация
	               |	И ВЫБОР
	               |			КОГДА &БезНДС
	               |				ТОГДА тчТовары.СуммаНДС = 0
	               |			ИНАЧЕ тчТовары.СуммаНДС <> 0
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	тчТовары.СтавкаНДС,
	               |	тчТовары.Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |	тчТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
	               |	НАЧАЛОПЕРИОДА(тчТовары.Ссылка.Дата, ДЕНЬ),
	               |	тчТовары.СуммаАкцизногоНалога <> 0,
	               |	ВЫБОР
	               |		КОГДА &ЭтоФабрикаСыра
	               |			ТОГДА тчТовары.Цена
	               |		ИНАЧЕ ВЫРАЗИТЬ(тчТовары.СуммаВсего / тчТовары.КоличествоБазовое КАК ЧИСЛО(15, 3))
	               |	КОНЕЦ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&НачалоПериода КАК Дата,
	               |	ТоварыЗКС.ДатаОперации КАК ДатаОперации,
	               |	ТоварыЗКС.Номенклатура КАК Номенклатура,
	               |	ТоварыЗКС.Цена КАК Цена,
	               |	СУММА(ТоварыЗКС.КоличествоБазовое) КАК КоличествоБазовое,
	               |	СУММА(ТоварыЗКС.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ТоварыЗКС.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(ТоварыЗКС.СуммаБезНалогов) КАК СуммаБезНалогов,
	               |	МАКСИМУМ(ТоварыЗКС.СтавкаНДС) КАК СтавкаНДС
	               |ПОМЕСТИТЬ втВозвраты
	               |ИЗ
	               |	ТоварыЗКС КАК ТоварыЗКС
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТоварыЗКС.Номенклатура,
	               |	ТоварыЗКС.Цена,
	               |	ТоварыЗКС.ДатаОперации
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ТоварыЗКС.КоличествоБазовое) < 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Ссылка КАК ДокументНН,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.СуммаНДСДокумента КАК СуммаНДСДокумента
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
	               |	И НЕ ТК_НалоговаяНакладнаяТовары.Ссылка.ПометкаУдаления
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Контрагент = &Контрагент
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.ТорговаяПлощадка = &ТорговаяПлощадка
	               |	И ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ТоварыЗКС.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ТоварыЗКС.СуммаВсего) КАК СуммаВсего
	               |ИЗ
	               |	ТоварыЗКС КАК ТоварыЗКС
	               |ГДЕ
	               |	ТоварыЗКС.КоличествоБазовое > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТоварыЗКС.ДатаОперации КАК ДатаОперации,
	               |	ТоварыЗКС.Номенклатура КАК Номенклатура,
	               |	ТоварыЗКС.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТоварыЗКС.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТоварыЗКС.Коэффициент КАК Коэффициент,
	               |	ТоварыЗКС.Цена КАК Цена,
	               |	СУММА(ТоварыЗКС.КоличествоБазовое) КАК КоличествоБазовое,
	               |	ТоварыЗКС.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ТоварыЗКС.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ТоварыЗКС.Сумма) КАК Сумма,
	               |	СУММА(ТоварыЗКС.СуммаБезНалогов) КАК СуммаБезНалогов,
	               |	СУММА(ТоварыЗКС.СуммаСкидки) КАК СуммаСкидки,
	               |	СУММА(ТоварыЗКС.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(ТоварыЗКС.Акциз) КАК Акциз
	               |ИЗ
	               |	ТоварыЗКС КАК ТоварыЗКС
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА &МРЦ = 1
	               |				ТОГДА ТоварыЗКС.ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |						ИЛИ ТоварыЗКС.Номенклатура.КодУКТВЭД В (&СписокКодов)
	               |			КОГДА &МРЦ = 2
	               |				ТОГДА ТоварыЗКС.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |						И НЕ ТоварыЗКС.Номенклатура.КодУКТВЭД В (&СписокКодов)
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТоварыЗКС.ЕдиницаИзмерения,
	               |	ТоварыЗКС.СтавкаНДС,
	               |	ТоварыЗКС.Номенклатура,
	               |	ТоварыЗКС.Коэффициент,
	               |	ТоварыЗКС.Цена,
	               |	ТоварыЗКС.ДатаОперации,
	               |	ТоварыЗКС.ХарактеристикаНоменклатуры
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ТоварыЗКС.КоличествоБазовое) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втВозвраты.Дата КАК Дата,
	               |	втВозвраты.ДатаОперации КАК ДатаОперации,
	               |	втВозвраты.Номенклатура КАК Номенклатура,
	               |	втВозвраты.Цена КАК Цена,
	               |	СУММА(втВозвраты.КоличествоБазовое) КАК КоличествоБазовое,
	               |	СУММА(ЕСТЬNULL(ВложенныйЗапрос.Корректировка, 0)) КАК Корректировка,
	               |	СУММА(втВозвраты.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(втВозвраты.СуммаВсего) КАК СуммаВсего,
	               |	СУММА(втВозвраты.СуммаБезНалогов) КАК СуммаБезНалогов,
	               |	втВозвраты.СтавкаНДС КАК СтавкаНДС,
	               |	МАКСИМУМ(ЕСТЬNULL(ВложенныйЗапрос.ДокументП2НН, НЕОПРЕДЕЛЕНО)) КАК ДокументП2НН
	               |ИЗ
	               |	втВозвраты КАК втВозвраты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			НАЧАЛОПЕРИОДА(ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная.Дата, ДЕНЬ) КАК ДатаОперации,
	               |			ТК_НалоговаяНакладнаяТовары.Ссылка КАК ДокументП2НН,
	               |			ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура КАК Номенклатура,
	               |			ВЫРАЗИТЬ(ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК ЧИСЛО(15, 3)) КАК Цена,
	               |			СУММА(ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеКоличество * ТК_Приложение2КНалоговойНакладнойТовары.Коэффициент) - СУММА(ЕСТЬNULL(ТК_НалоговаяНакладнаяТовары.КоличествоБазовое, 0)) КАК Корректировка
	               |		ИЗ
	               |			втВозвраты КАК втВозвраты
	               |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |					ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |					ПО ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная = ТК_НалоговаяНакладнаяТовары.Ссылка
	               |						И ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура = ТК_НалоговаяНакладнаяТовары.Номенклатура
	               |						И ТК_Приложение2КНалоговойНакладнойТовары.НомерСтроки = ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН
	               |				ПО (НЕ &НовыйДокумент)
	               |					И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.ДокументОснование = &ДокНН)
	               |					И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ТК_НалоговаяНакладная)
	               |					И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Проведен)
	               |					И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат))
	               |					И втВозвраты.Номенклатура = ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура
	               |					И втВозвраты.Цена = ТК_Приложение2КНалоговойНакладнойТовары.Цена
	               |					И (втВозвраты.ДатаОперации = НАЧАЛОПЕРИОДА(ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная.Дата, ДЕНЬ))
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			НАЧАЛОПЕРИОДА(ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная.Дата, ДЕНЬ),
	               |			ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура,
	               |			ВЫРАЗИТЬ(ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК ЧИСЛО(15, 3)),
	               |			ТК_НалоговаяНакладнаяТовары.Ссылка) КАК ВложенныйЗапрос
	               |		ПО втВозвраты.Номенклатура = ВложенныйЗапрос.Номенклатура
	               |			И втВозвраты.Цена = ВложенныйЗапрос.Цена
	               |			И втВозвраты.ДатаОперации = ВложенныйЗапрос.ДатаОперации
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втВозвраты.Дата,
	               |	втВозвраты.СтавкаНДС,
	               |	втВозвраты.Номенклатура,
	               |	втВозвраты.ДатаОперации,
	               |	втВозвраты.Цена
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(втВозвраты.КоличествоБазовое) - СУММА(ЕСТЬNULL(ВложенныйЗапрос.Корректировка, 0)) <> 0";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КолвоРез = МассивРезультатов.ВГраница();
	
	Выборка = МассивРезультатов[КолвоРез-1].Выбрать();
	ТаблицаВозвраты = МассивРезультатов[КолвоРез].Выгрузить();
	ВыборкаНН = МассивРезультатов[КолвоРез-3].Выбрать(); 
	ВыборкаДня = МассивРезультатов[КолвоРез-2].Выбрать();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать, ПересчитыватьПроцентСкидки", Ложь, Истина));	
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Док.Товары.Очистить();
	
	НакопленнаяРазницаСуммаБНДС = 0;
	НакопленнаяРазницаСумма = 0;
	
	Всего = Выборка.Количество();
	Ит = 0;
	РазницаМрц = 0;
	ДопДопРазница = 0;
	ДопРазницаНДС =0;
	Пока Выборка.Следующий() Цикл 
		Ит = Ит + 1;
		
		Стр = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка, "Номенклатура,ЕдиницаИзмерения,Коэффициент,КоличествоБазовое,СтавкаНДС,СуммаНДС,СуммаСкидки");
		
		Если МРЦ = 1 или БезНДС Тогда
			стр.ХарактеристикаНоменклатуры = Выборка.ХарактеристикаНоменклатуры;
		КонецЕсли;
		
		Если Стр.Коэффициент > 0 Тогда 
			Стр.Количество = Стр.КоличествоБазовое / Стр.Коэффициент;		
		КонецЕсли;
		
		Если БезНДС Тогда
			Стр.СуммаБезНалогов = ОкруглитьЧисло(Выборка.СуммаБезНалогов, 2, НакопленнаяРазницаСуммаБНДС);
			Стр.СуммаВсего = ОкруглитьЧисло(Выборка.СуммаВсего, 2, НакопленнаяРазницаСумма);
		Иначе
			Стр.СуммаБезНалогов = ОкруглитьЧисло(Выборка.СуммаБезНалогов, 3, НакопленнаяРазницаСуммаБНДС);
			Стр.СуммаВсего = ОкруглитьЧисло(Выборка.СуммаВсего, 3, НакопленнаяРазницаСумма);
		КонецЕсли;
		
		Если БезНДС и Выборка.Акциз = 0 И ЗначениеЗаполнено(Выборка.ХарактеристикаНоменклатуры) Тогда
			Акциз = Окр(Стр.СуммаВсего/1.05*0.05,2);
			Стр.СуммаБезНалогов = Стр.СуммаВсего-Акциз;
			Стр.СуммаВсего = Стр.СуммаВсего-Акциз;
			нЦена = Стр.СуммаВсего/стр.КоличествоБазовое;
			Стр.Цена = Окр(нЦена,6);
			//Убрать на время
			//ЗначениеМРЦ = Справочники.ХарактеристикиНоменклатуры.ЗначениеМРЦ(Выборка.ХарактеристикаНоменклатуры);
			//Если НЕ ЗначениеМРЦ = 0 Тогда
			//	Стр.Цена = ЗначениеМРЦ;
			//	Сумма = стр.Цена*стр.КоличествоБазовое;
			//	Стр.СуммаБезНалогов = Сумма;
			//	Стр.СуммаВсего = Сумма;
			//КонецЕсли;
		//ИначеЕсли НЕ Выборка.Акциз = 0 И ЗначениеЗаполнено(Выборка.ХарактеристикаНоменклатуры) тогда
		ИначеЕсли БезНДС Тогда
			нЦена = Стр.СуммаВсего/стр.КоличествоБазовое;
			Стр.Цена = Окр(нЦена,6);
		Иначе
			Стр.Цена = Выборка.Цена;
		КонецЕсли;
		
		Если Ит = Всего Тогда
			//ИтогСуммаБезНДС = Док.Товары.Итог("СуммаБезНалогов") + НакопленнаяРазницаСуммаБНДС;
			//ИтогСуммаНДС = Док.Товары.Итог("СуммаНДС") + НакопленнаяРазницаСуммаБНДС;
			ИтогСумма = Док.Товары.Итог("СуммаВсего") + НакопленнаяРазницаСумма;			
			
			ДопРазница = (Окр(ИтогСумма, 2) - Окр(Окр(ИтогСумма, 3), 2)) / 10;						
			
			Стр.СуммаБезНалогов = Стр.СуммаБезНалогов + НакопленнаяРазницаСуммаБНДС;
			Стр.СуммаВсего = Стр.СуммаВсего + НакопленнаяРазницаСумма + ДопРазница;
			
			Если ОкруглятьДень Тогда
				Если ВыборкаНН.Количество()>0 ИЛИ ВыборкаДня.Количество()>0 Тогда					
					Если ВыборкаНН.Следующий() Тогда 
						ВыборкаННСуммаНДСДокумента = ВыборкаНН.СуммаНДСДокумента;
						ВыборкаННСуммаДокумента = ВыборкаНН.СуммаДокумента;
					Иначе
						ВыборкаННСуммаНДСДокумента = 0;
						ВыборкаННСуммаДокумента = 0;
					КонецЕсли;
										
					Если ВыборкаДня.Следующий() Тогда 
						ВыборкаДняСуммаНДС = ВыборкаДня.СуммаНДС;
						ВыборкаДняСуммаВсего = ВыборкаДня.СуммаВсего;
					Иначе
						ВыборкаДняСуммаНДС = 0;
						ВыборкаДняСуммаВсего = 0;
					КонецЕсли;
					
					РазницаМрцНДС = ВыборкаДняСуммаНДС - ВыборкаННСуммаНДСДокумента; 	
					РазницаМрц = окр(ВыборкаДняСуммаВсего,2) - окр(ВыборкаННСуммаДокумента,2);
					
					ДопРазницаНДС = РазницаМрцНДС - Док.Товары.Итог("СуммаНДС");
					ДопДопРазница = РазницаМрц - Док.Товары.Итог("СуммаВсего");
				КонецЕсли;
			КонецЕсли;
			стр.СуммаНДС = стр.СуммаНДС+ДопРазницаНДС;
			Стр.СуммаВсего = Стр.СуммаВсего+ДопДопРазница;
			
			Если БезНДС Тогда 
				Стр.СуммаБезНалогов = Окр(Стр.СуммаВсего, 2);
				Стр.СуммаВсего = Окр(Стр.СуммаВсего, 2);
			КонецЕсли;
			
		КонецЕсли;
		
		Если БезНДС Тогда 
			Стр.СуммаБезНалогов = Стр.СуммаВсего;
		КонецЕсли;
		
		Стр.Сумма = Стр.СуммаВсего + Стр.СуммаСкидки;	
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
		
	Если Док.ПроверитьЗаполнение() Тогда 
		Попытка
			Док.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Док.Записать();									
		КонецПопытки;
	Иначе
		Док.Записать();
	КонецЕсли;
	
	ДанныеДня.ДокументНН = Док.Ссылка;
	ДанныеДня.НомерНакладной = ПолучитьНомерДокумента(Док.Дата, Док.НомерДляБухгалтерии);
	ДанныеДня.НомерДляБухгалтерии = Док.НомерДляБухгалтерии;
	
	ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%2 документ %1'; uk = '%2 документ %1'"),
			Строка(Док.Ссылка),
			?(Док.Проведен, НСтр("ru = 'Проведен'; uk = 'Проведений'"), НСтр("ru = 'Записан'; uk = 'Записаний'"))),
			Док.Ссылка);
						
	//УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СоздатьДокументНН_Компенсирующий(ДанныеДня, Знач ДанныеЗаполнения)
	          
	ДанныеЗаполнения.Вставить("Дата", КонецДня(ДанныеДня.Дата));
	ДанныеЗаполнения.Вставить("Организация", ДанныеДня.Организация);
	ДанныеЗаполнения.Вставить("Комментарий", "Документ создан обработкой ""Формирование Налоговых накладных"" на дату " + Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy"));
	ДанныеЗаполнения.Вставить("ХозОперация", Справочники.ХозОперации.НалоговаяНакладнаяСводнаяКомпенсирующая);
	
	НовыйДокумент = Истина;
	
	Если Не ДанныеДня.ДокументНН.Пустая() Тогда 		
		Док = ДанныеДня.ДокументНН.ПолучитьОбъект();
		НовыйДокумент = Ложь;
	Иначе
		Док = Документы.ТК_НалоговаяНакладная.СоздатьДокумент();		
	КонецЕсли;
	
	Док.Заполнить(ДанныеЗаполнения);
			
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("Организация", ДанныеДня.Организация);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Организация КАК Организация,
	               |	ОстаткиОрганизации.Партия КАК Партия,
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиОрганизации.АналитикаУчета КАК АналитикаУчета,
	               |	СУММА(ОстаткиОрганизации.Количество) КАК Количество,
	               |	СУММА(ТК_НалоговаяНакладнаяТовары.СуммаВсего) КАК Сумма,
	               |	СУММА(ТК_НалоговаяНакладнаяТовары.СуммаНДС) КАК СуммаНДС
	               |ПОМЕСТИТЬ втСписанныеПартии
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиОрганизации КАК ОстаткиОрганизации
	               |		ПО ТК_НалоговаяНакладнаяТовары.Ссылка = ОстаткиОрганизации.Регистратор
	               |			И ТК_НалоговаяНакладнаяТовары.АналитикаУчетаНоменклатурыОрганизаций = ОстаткиОрганизации.АналитикаУчета
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Проведен
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяСводнаяКомпенсирующая)
	               |	И ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |	И ТК_НалоговаяНакладнаяТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры,
	               |	ОстаткиОрганизации.АналитикаУчета,
	               |	ОстаткиОрганизации.Партия,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Организация,
	               |	НАЧАЛОПЕРИОДА(ТК_НалоговаяНакладнаяТовары.Ссылка.Дата, ДЕНЬ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСписанныеПартии.Дата КАК Дата,
	               |	втСписанныеПартии.Организация КАК Организация,
	               |	втСписанныеПартии.Номенклатура КАК Номенклатура,
	               |	втСписанныеПартии.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втСписанныеПартии.АналитикаУчета КАК АналитикаУчета,
	               |	втСписанныеПартии.Партия КАК Партия,
	               |	втСписанныеПартии.Количество КАК Количество,
	               |	втСписанныеПартии.Сумма КАК Сумма,
	               |	втСписанныеПартии.СуммаНДС КАК СуммаНДС
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	втСписанныеПартии КАК втСписанныеПартии
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиОрганизации КАК ОстаткиОрганизации
	               |		ПО втСписанныеПартии.АналитикаУчета = ОстаткиОрганизации.АналитикаУчета
	               |			И втСписанныеПартии.Партия = ОстаткиОрганизации.Партия
	               |			И (ОстаткиОрганизации.Период > ДАТАВРЕМЯ(2021, 12, 31, 23, 59, 59))
	               |			И (ОстаткиОрганизации.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	               |			И (ОстаткиОрганизации.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.НДС_МРЦ))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.Номенклатура КАК Номенклатура,
	               |	втТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	СУММА(втТовары.Количество) КАК КоличествоБазовое,
	               |	СУММА(втТовары.Сумма) КАК Сумма,
	               |	втТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	               |	втТовары.АналитикаУчета КАК АналитикаУчетаНоменклатурыОрганизаций,
	               |	втТовары.Партия КАК ВходящийДокумент
	               |ИЗ
	               |	втТовары КАК втТовары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТовары.Партия,
	               |	втТовары.ХарактеристикаНоменклатуры,
	               |	втТовары.Номенклатура,
	               |	втТовары.АналитикаУчета,
	               |	втТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |	втТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
	               |	втТовары.Номенклатура.СтавкаНДС
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВходящийДокумент,
	               |	Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать, ПересчитыватьПроцентСкидки", Ложь, Истина));	
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Док.Товары.Очистить();
	
	НакопленнаяРазницаСуммаБНДС = 0;
	НакопленнаяРазницаСумма = 0;
	
	Всего = Выборка.Количество();
	Ит = 0;
	Пока Выборка.Следующий() Цикл 
		Ит = Ит + 1;
		
		Стр = Док.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка);
		
		Если Стр.Коэффициент > 0 Тогда 
			Стр.Количество = Стр.КоличествоБазовое / Стр.Коэффициент;		
		КонецЕсли;
						
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
		
	Если Док.ПроверитьЗаполнение() Тогда 
		Попытка
			Док.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Док.Записать();									
		КонецПопытки;
	Иначе
		Док.Записать();
	КонецЕсли;
	
	ДанныеДня.ДокументНН = Док.Ссылка;
	ДанныеДня.НомерНакладной = ПолучитьНомерДокумента(Док.Дата, Док.НомерДляБухгалтерии);
	ДанныеДня.НомерДляБухгалтерии = Док.НомерДляБухгалтерии;
	
	ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%2 документ %1'; uk = '%2 документ %1'"),
			Строка(Док.Ссылка),
			?(Док.Проведен, НСтр("ru = 'Проведен'; uk = 'Проведений'"), НСтр("ru = 'Записан'; uk = 'Записаний'"))),
			Док.Ссылка);						
	
КонецПроцедуры

Процедура СоздатьПриложения2кНН(ДанныеДня, ТаблицаВозвраты, ТаблицаПриложения2, Контрагент)
	
	тзРаспределение = Новый ТаблицаЗначений;
	тзРаспределение.Колонки.Добавить("Дата");
	тзРаспределение.Колонки.Добавить("Номенклатура");
	тзРаспределение.Колонки.Добавить("Цена");
	тзРаспределение.Колонки.Добавить("СтавкаНДС");
	тзРаспределение.Колонки.Добавить("ДокументНН");
	тзРаспределение.Колонки.Добавить("КоличествоНН");
	тзРаспределение.Колонки.Добавить("КоличествоП2НН");
	тзРаспределение.Колонки.Добавить("ОстатокНН");
	тзРаспределение.Колонки.Добавить("МаксНомерСтроки");
	тзРаспределение.Колонки.Добавить("Списать");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ДанныеДня.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДанныеДня.Дата));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ТаблицаВозвраты", ТаблицаВозвраты);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТоварыВозврат.Дата КАК Дата,
	               |	ТоварыВозврат.Номенклатура КАК Номенклатура,
	               |	ТоварыВозврат.СтавкаНДС КАК СтавкаНДС,
	               |	ТоварыВозврат.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТоварыВозврат.СуммаНДС КАК СуммаНДС,
	               |	ТоварыВозврат.СуммаВсего КАК СуммаВсего,
	               |	ТоварыВозврат.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТоварыВозврат.КоличествоКорректировка КАК КоличествоКорректировка,
	               |	ТоварыВозврат.Цена КАК Цена
	               |ПОМЕСТИТЬ ТоварыВозврат
	               |ИЗ
	               |	&ТаблицаВозвраты КАК ТоварыВозврат
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Дата КАК Дата,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка КАК ДокументНН,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтроки КАК НомерСтроки,
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_НалоговаяНакладнаяТовары.Коэффициент КАК Коэффициент,
	               |	ТК_НалоговаяНакладнаяТовары.Количество КАК Количество,
	               |	ТК_НалоговаяНакладнаяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ВЫРАЗИТЬ(ТК_НалоговаяНакладнаяТовары.Цена КАК ЧИСЛО(15, 3)) КАК Цена,
	               |	ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.Сумма КАК Сумма,
	               |	ТК_НалоговаяНакладнаяТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаВсего КАК СуммаВсего,
	               |	ТК_НалоговаяНакладнаяТовары.ДатаОтгрузкиОплаты КАК ДатаОтгрузкиОплаты,
	               |	ТК_НалоговаяНакладнаяТовары.Содержание КАК Содержание,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	ТК_НалоговаяНакладнаяТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ СтрокиНН
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыВозврат КАК ТоварыВозврат
	               |		ПО ТК_НалоговаяНакладнаяТовары.Номенклатура = ТоварыВозврат.Номенклатура
	               |			И ((ВЫРАЗИТЬ(ТК_НалоговаяНакладнаяТовары.Цена КАК ЧИСЛО(15, 3))) = ТоварыВозврат.Цена)
	               |			И (ТК_НалоговаяНакладнаяТовары.Ссылка.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&НачалоПериода, МЕСЯЦ, -1) И ДОБАВИТЬКДАТЕ(&НачалоПериода, СЕКУНДА, -1))
	               |			И (ТК_НалоговаяНакладнаяТовары.Ссылка.Проведен)
	               |			И (ТК_НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация)
	               |			И (ТК_НалоговаяНакладнаяТовары.Ссылка.Контрагент = &Контрагент)
	               |			И (ТК_НалоговаяНакладнаяТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтрокиНН.Дата КАК ДатаНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Дата КАК ДатаП2НН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная КАК ДокументНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка КАК ДокументП2кНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.НомерСтроки КАК НомерСтроки,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура КАК Номенклатура,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Коэффициент КАК Коэффициент,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Количество КАК Количество,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ВЫРАЗИТЬ(ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК ЧИСЛО(15, 3)) КАК Цена,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Сумма КАК Сумма,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаВсего КАК СуммаВсего,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ДатаКорректировки КАК ДатаКорректировки,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Причина КАК Причина,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеКоличество КАК ИзменениеКоличество,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеЦенаБезНалогов КАК ИзменениеЦенаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаНДС КАК ИзменениеСуммаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.КодПричины КАК КодПричины,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.НомерГруппы КАК НомерГруппы,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ СтрокиП2кНН
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтрокиНН КАК СтрокиНН
	               |		ПО ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная = СтрокиНН.ДокументНН
	               |			И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Проведен)
	               |			И ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура = СтрокиНН.Номенклатура
	               |			И ((ВЫРАЗИТЬ(ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК ЧИСЛО(15, 3))) = СтрокиНН.Цена)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.Дата КАК Дата,
	               |	ВложенныйЗапрос.ДокументНН КАК ДокументНН,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ВложенныйЗапрос.КоличествоНН) КАК КоличествоНН,
	               |	СУММА(ВложенныйЗапрос.КоличествоП2НН) КАК КоличествоП2НН,
	               |	СУММА(ВложенныйЗапрос.Остаток) КАК Остаток,
	               |	ВложенныйЗапрос.Цена КАК Цена
	               |ПОМЕСТИТЬ СписокНН
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		СтрокиНН.Дата КАК Дата,
	               |		СтрокиНН.ДокументНН КАК ДокументНН,
	               |		СтрокиНН.Номенклатура КАК Номенклатура,
	               |		СтрокиНН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		СтрокиНН.СтавкаНДС КАК СтавкаНДС,
	               |		СтрокиНН.КоличествоБазовое КАК КоличествоНН,
	               |		0 КАК КоличествоП2НН,
	               |		СтрокиНН.КоличествоБазовое КАК Остаток,
	               |		СтрокиНН.Цена КАК Цена
	               |	ИЗ
	               |		СтрокиНН КАК СтрокиНН
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		СтрокиП2кНН.ДатаНН,
	               |		СтрокиП2кНН.ДокументНН,
	               |		СтрокиП2кНН.Номенклатура,
	               |		СтрокиП2кНН.ХарактеристикаНоменклатуры,
	               |		СтрокиП2кНН.СтавкаНДС,
	               |		0,
	               |		СтрокиП2кНН.ИзменениеКоличество * СтрокиП2кНН.Коэффициент,
	               |		СтрокиП2кНН.ИзменениеКоличество * СтрокиП2кНН.Коэффициент,
	               |		СтрокиП2кНН.Цена
	               |	ИЗ
	               |		СтрокиП2кНН КАК СтрокиП2кНН) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ДокументНН,
	               |	ВложенныйЗапрос.СтавкаНДС,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры,
	               |	ВложенныйЗапрос.Номенклатура,
	               |	ВложенныйЗапрос.Дата,
	               |	ВложенныйЗапрос.Цена
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВложенныйЗапрос.Остаток) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.ДокументНН КАК ДокументНН,
	               |	МАКСИМУМ(ВложенныйЗапрос.НомерСтроки) КАК МаксНомерСтроки
	               |ПОМЕСТИТЬ МаксСтрокиНН
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ТК_НалоговаяНакладнаяТовары.Ссылка КАК ДокументНН,
	               |		МАКСИМУМ(ТК_НалоговаяНакладнаяТовары.НомерСтроки) КАК НомерСтроки
	               |	ИЗ
	               |		Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |	ГДЕ
	               |		ТК_НалоговаяНакладнаяТовары.Ссылка В
	               |				(ВЫБРАТЬ
	               |					СписокНН.ДокументНН КАК ДокументНН
	               |				ИЗ
	               |					СписокНН КАК СписокНН
	               |				СГРУППИРОВАТЬ ПО
	               |					СписокНН.ДокументНН)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ТК_НалоговаяНакладнаяТовары.Ссылка
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		СтрокиП2кНН.ДокументНН,
	               |		МАКСИМУМ(ТК_Приложение2КНалоговойНакладнойТовары.НомерСтрокиНН)
	               |	ИЗ
	               |		СтрокиП2кНН КАК СтрокиП2кНН
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокНН КАК СписокНН
	               |			ПО СтрокиП2кНН.ДокументНН = СписокНН.ДокументНН
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |			ПО СтрокиП2кНН.ДокументП2кНН = ТК_Приложение2КНалоговойНакладнойТовары.Ссылка
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		СтрокиП2кНН.ДокументНН) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ДокументНН
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписокНН.Дата КАК Дата,
	               |	ТоварыВозврат.Номенклатура КАК Номенклатура,
	               |	ТоварыВозврат.СтавкаНДС КАК СтавкаНДС,
	               |	ТоварыВозврат.КоличествоБазовое * -1 КАК Количество,
	               |	СписокНН.Дата КАК ДатаНН,
	               |	СписокНН.ДокументНН КАК ДокументНН,
	               |	СписокНН.КоличествоНН КАК КоличествоНН,
	               |	СписокНН.КоличествоП2НН КАК КоличествоП2НН,
	               |	СписокНН.Остаток КАК ОстатокНН,
	               |	МаксСтрокиНН.МаксНомерСтроки КАК МаксНомерСтроки,
	               |	ТоварыВозврат.Цена КАК Цена
	               |ИЗ
	               |	ТоварыВозврат КАК ТоварыВозврат
	               |		ЛЕВОЕ СОЕДИНЕНИЕ СписокНН КАК СписокНН
	               |			ЛЕВОЕ СОЕДИНЕНИЕ МаксСтрокиНН КАК МаксСтрокиНН
	               |			ПО СписокНН.ДокументНН = МаксСтрокиНН.ДокументНН
	               |		ПО ТоварыВозврат.Номенклатура = СписокНН.Номенклатура
	               |			И ТоварыВозврат.Цена = СписокНН.Цена
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата,
	               |	Номенклатура,
	               |	ДатаНН УБЫВ,
	               |	ОстатокНН УБЫВ
	               |ИТОГИ
	               |	МИНИМУМ(Количество)
	               |ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтрокиНН.Дата КАК Дата,
	               |	СтрокиНН.ДокументНН КАК ДокументНН,
	               |	СтрокиНН.НомерСтроки КАК НомерСтроки,
	               |	СтрокиНН.Номенклатура КАК Номенклатура,
	               |	СтрокиНН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СтрокиНН.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СтрокиНН.Коэффициент КАК Коэффициент,
	               |	СтрокиНН.Количество КАК Количество,
	               |	СтрокиНН.КоличествоБазовое КАК КоличествоБазовое,
	               |	СтрокиНН.Цена КАК Цена,
	               |	СтрокиНН.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	СтрокиНН.СтавкаНДС КАК СтавкаНДС,
	               |	СтрокиНН.СуммаНДС КАК СуммаНДС,
	               |	СтрокиНН.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	СтрокиНН.Сумма КАК Сумма,
	               |	СтрокиНН.ПроцентСкидки КАК ПроцентСкидки,
	               |	СтрокиНН.СуммаСкидки КАК СуммаСкидки,
	               |	СтрокиНН.СуммаВсего КАК СуммаВсего,
	               |	СтрокиНН.ДатаОтгрузкиОплаты КАК ДатаОтгрузкиОплаты,
	               |	СтрокиНН.Содержание КАК Содержание,
	               |	СтрокиНН.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	СтрокиНН.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ИЗ
	               |	СтрокиНН КАК СтрокиНН
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксСтрокиНН КАК МаксСтрокиНН
	               |		ПО СтрокиНН.ДокументНН = МаксСтрокиНН.ДокументНН
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СтрокиП2кНН.ДатаНН КАК ДатаНН,
	               |	СтрокиП2кНН.ДатаП2НН КАК ДатаП2НН,
	               |	СтрокиП2кНН.ДокументНН КАК ДокументНН,
	               |	СтрокиП2кНН.ДокументП2кНН КАК ДокументП2кНН,
	               |	СтрокиП2кНН.НомерСтроки КАК НомерСтроки,
	               |	СтрокиП2кНН.Номенклатура КАК Номенклатура,
	               |	СтрокиП2кНН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СтрокиП2кНН.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СтрокиП2кНН.Коэффициент КАК Коэффициент,
	               |	СтрокиП2кНН.Количество КАК Количество,
	               |	СтрокиП2кНН.КоличествоБазовое КАК КоличествоБазовое,
	               |	СтрокиП2кНН.Цена КАК Цена,
	               |	СтрокиП2кНН.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	СтрокиП2кНН.СтавкаНДС КАК СтавкаНДС,
	               |	СтрокиП2кНН.СуммаНДС КАК СуммаНДС,
	               |	СтрокиП2кНН.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	СтрокиП2кНН.Сумма КАК Сумма,
	               |	СтрокиП2кНН.СуммаСкидки КАК СуммаСкидки,
	               |	СтрокиП2кНН.СуммаВсего КАК СуммаВсего,
	               |	СтрокиП2кНН.ДатаКорректировки КАК ДатаКорректировки,
	               |	СтрокиП2кНН.Причина КАК Причина,
	               |	СтрокиП2кНН.ИзменениеКоличество КАК ИзменениеКоличество,
	               |	СтрокиП2кНН.ИзменениеЦенаБезНалогов КАК ИзменениеЦенаБезНалогов,
	               |	СтрокиП2кНН.ИзменениеСуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
	               |	СтрокиП2кНН.ИзменениеСуммаНДС КАК ИзменениеСуммаНДС,
	               |	СтрокиП2кНН.КодПричины КАК КодПричины,
	               |	СтрокиП2кНН.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	СтрокиП2кНН.НомерГруппы КАК НомерГруппы,
	               |	СтрокиП2кНН.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ИЗ
	               |	СтрокиП2кНН КАК СтрокиП2кНН
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксСтрокиНН КАК МаксСтрокиНН
	               |		ПО СтрокиП2кНН.ДокументНН = МаксСтрокиНН.ДокументНН";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	МаксИндекс = МассивРезультатов.ВГраница();
	
	ВыборкаНоменклатура = МассивРезультатов[МаксИндекс-2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	тзСтрокиНН 	 = МассивРезультатов[МаксИндекс-1].Выгрузить();
	тзСтрокиП2НН = МассивРезультатов[МаксИндекс].Выгрузить();
	
	Пока ВыборкаНоменклатура.Следующий() Цикл 
		Количество = ВыборкаНоменклатура.Количество;
		
		ВыборкаНН = ВыборкаНоменклатура.Выбрать();
		Пока ВыборкаНН.Следующий() Цикл 
			Если ВыборкаНН.ОстатокНН = Null Тогда 
				Продолжить;
			КонецЕсли;
			
			Списать = Мин(Количество, ВыборкаНН.ОстатокНН);	
			
			НоваяСтрока = тзРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНН, "Номенклатура,Цена,СтавкаНДС,ДокументНН,КоличествоНН,КоличествоП2НН,ОстатокНН,МаксНомерСтроки");
			НоваяСтрока.Дата = ВыборкаНН.ДатаНН;
			НоваяСтрока.Списать = Списать;
			
			Количество = Количество - Списать;
			Если Количество = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Количество > 0 Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось найти для корректировки товар ""%1"" по цене %2 не списано %3 шт. (%4)'; uk = 'Не вдалося знайти для корегування товар ""%1"" по ціні %2 не списано %3 шт. (%4)'"),
					ВыборкаНоменклатура.Номенклатура,
					Формат(ВыборкаНН.Цена, "ЧДЦ=2"),
					Количество,
					Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy")),
				ВыборкаНоменклатура.ДокументНН)					
		КонецЕсли;
	КонецЦикла;
	
	Если тзРаспределение.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	тзДокументыНН = тзРаспределение.Скопировать(, "Дата,ДокументНН,МаксНомерСтроки");
	тзДокументыНН.Свернуть("Дата,ДокументНН,МаксНомерСтроки");
	тзДокументыНН.Сортировать("Дата Убыв");
	
	Для Каждого стрТЗ Из тзДокументыНН Цикл 				
		ДокНН 	 = стрТЗ.ДокументНН;		
		ТоварыНН = тзРаспределение.НайтиСтроки(Новый Структура("ДокументНН", ДокНН));
		ТекДата  = ТекущаяДатаСеанса();
		
		
		ДокП2НН = Документы.ТК_Приложение2КНалоговойНакладной.СоздатьДокумент();
		ДокП2НН.Заполнить(ДокНН);
		ДокП2НН.Товары.Очистить();
		
		
		ДокП2НН.Дата = ТекДата;
		ДокП2НН.ХозОперация = Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат;
		ДокП2НН.Автор = Пользователи.ТекущийПользователь();
		ДокП2НН.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Документ сформирован автоматически. Возврат от '; uk = 'Документ створений автоматично. Возврат от '"),
								Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy"));
		
		Для Каждого СтрТовары Из ТоварыНН Цикл 
			СтруктураОтбора = Новый Структура("ДокументНН,Номенклатура,Цена");
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрТовары);
			
			НоваяСтрокаП2НН = Неопределено;			
			
			Если СтрТовары.КоличествоП2НН <> 0 Тогда 								
				ТоварыКорректировки = тзСтрокиП2НН.Скопировать(СтруктураОтбора);
				ТоварыКорректировки.Сортировать("НомерСтроки Убыв");
				
				Для Каждого СтрКор Из ТоварыКорректировки Цикл 
					Если СтрКор.ИзменениеКоличество > 0 Тогда 
						НоваяСтрокаП2НН = ДокП2НН.Товары.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаП2НН, СтрКор);
						НоваяСтрокаП2НН.ИзменениеКоличество = НоваяСтрокаП2НН.ИзменениеКоличество * -1;
						
						РассчитатьСуммыИзмененийП2НН(НоваяСтрокаП2НН);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ТоварыКорректировки = тзСтрокиНН.Скопировать(СтруктураОтбора);
				СтрКор = ТоварыКорректировки[0];
				
				НоваяСтрокаП2НН = ДокП2НН.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаП2НН, СтрКор);
				НоваяСтрокаП2НН.НомерСтрокиНН = СтрКор.НомерСтроки;
				НоваяСтрокаП2НН.ИзменениеКоличество = НоваяСтрокаП2НН.Количество * -1;
				
				РассчитатьСуммыИзмененийП2НН(НоваяСтрокаП2НН);
			КонецЕсли;
			
			Если НоваяСтрокаП2НН = Неопределено Тогда 
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка списания %1'"),
						СтрТовары.Номенклатура));
						
				Продолжить;
			КонецЕсли;
			
			Если СтрТовары.Списать < СтрТовары.ОстатокНН Тогда 
				КолВоБазовое = СтрТовары.ОстатокНН - СтрТовары.Списать;
				КолВо = КолВоБазовое / НоваяСтрокаП2НН.Коэффициент;
				
				НовСтрНН = ДокП2НН.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрНН, НоваяСтрокаП2НН);
				
				стрТЗ.МаксНомерСтроки = стрТЗ.МаксНомерСтроки + 1;
				
				НовСтрНН.НомерСтрокиНН = стрТЗ.МаксНомерСтроки;
				НовСтрНН.Количество = КолВо;
				НовСтрНН.ИзменениеКоличество = КолВо;
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
				СтруктураДействий.Вставить("ПересчитатьСумму");
				СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");				
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));

				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НовСтрНН, СтруктураДействий, Неопределено);
				РассчитатьСуммыИзмененийП2НН(НовСтрНН);
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ДокП2НН.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
			
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%2 документ %1'; uk = '%2 документ %1'"),
					Строка(ДокП2НН.Ссылка),
					?(ДокП2НН.Проведен, НСтр("ru = 'Проведен'; uk = 'Проведений'"), НСтр("ru = 'Записан'; uk = 'Записаний'"))),
				ДокП2НН.Ссылка);

		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;

	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьПриложение2кНН_Нов(ДанныеДня, ТаблицаВозвраты, ТаблицаПриложения2, Знач ДанныеЗаполнения)
	
	Контрагент = ДанныеЗаполнения.Контрагент;
	ТорговаяПлощадка = ДанныеЗаполнения.ТорговаяПлощадка;
	
	
	тзРаспределение = Новый ТаблицаЗначений;
	тзРаспределение.Колонки.Добавить("Дата");
	тзРаспределение.Колонки.Добавить("Номенклатура");
	тзРаспределение.Колонки.Добавить("Цена");
	тзРаспределение.Колонки.Добавить("ДокументНН");
	тзРаспределение.Колонки.Добавить("КоличествоНН");
	тзРаспределение.Колонки.Добавить("КоличествоП2НН");
	тзРаспределение.Колонки.Добавить("ОстатокНН");
	тзРаспределение.Колонки.Добавить("МаксНомерСтроки");
	тзРаспределение.Колонки.Добавить("Списать");	
	
	тзСтрокиНН = Новый ТаблицаЗначений;	
	тзСтрокиП2НН = Новый ТаблицаЗначений;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Дата КАК Дата,
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка КАК ДокументНН,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтроки КАК НомерСтроки,
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_НалоговаяНакладнаяТовары.Коэффициент КАК Коэффициент,
	               |	ТК_НалоговаяНакладнаяТовары.Количество КАК Количество,
	               |	ТК_НалоговаяНакладнаяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_НалоговаяНакладнаяТовары.Цена КАК Цена,
	               |	ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.Сумма КАК Сумма,
	               |	ТК_НалоговаяНакладнаяТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаВсего КАК СуммаВсего,
	               |	ТК_НалоговаяНакладнаяТовары.ДатаОтгрузкиОплаты КАК ДатаОтгрузкиОплаты,
	               |	ТК_НалоговаяНакладнаяТовары.Содержание КАК Содержание,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	ТК_НалоговаяНакладнаяТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ втСтрокиНН
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Организация = &Организация
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.Контрагент = &Контрагент
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.ТорговаяПлощадка = &ТорговаяПлощадка
	               |	И ТК_НалоговаяНакладнаяТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
	               |	И ТК_НалоговаяНакладнаяТовары.Номенклатура = &Номенклатура
	               |	И (ВЫРАЗИТЬ(ТК_НалоговаяНакладнаяТовары.Цена КАК ЧИСЛО(15, 3))) = &Цена
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная КАК ДокументНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка КАК ДокументП2НН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура КАК Номенклатура,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Коэффициент КАК Коэффициент,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Количество КАК Количество,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК Цена,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Сумма КАК Сумма,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.СуммаВсего КАК СуммаВсего,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ДатаКорректировки КАК ДатаКорректировки,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Причина КАК Причина,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеКоличество КАК ИзменениеКоличество,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеЦенаБезНалогов КАК ИзменениеЦенаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаНДС КАК ИзменениеСуммаНДС,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.КодПричины КАК КодПричины,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.НомерГруппы КАК НомерГруппы,
	               |	ТК_Приложение2КНалоговойНакладнойТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ втСтрокиП2НН
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |ГДЕ
	               |	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная В
	               |			(ВЫБРАТЬ
	               |				втСтрокиНН.ДокументНН КАК ДокументНН
	               |			ИЗ
	               |				втСтрокиНН КАК втСтрокиНН
	               |			СГРУППИРОВАТЬ ПО
	               |				втСтрокиНН.ДокументНН)
	               |	И ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Проведен
	               |	И ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура = &Номенклатура
	               |	И ТК_Приложение2КНалоговойНакладнойТовары.Цена = &Цена
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.ДокументНН КАК ДокументНН,
	               |	МАКСИМУМ(ВложенныйЗапрос.НомерСтроки) КАК МаксНомерСтроки,
	               |	ВложенныйЗапрос.Дата КАК Дата
	               |ПОМЕСТИТЬ втМаксНомерСтроки
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втСтрокиНН.Дата КАК Дата,
	               |		втСтрокиНН.ДокументНН КАК ДокументНН,
	               |		МАКСИМУМ(ТК_НалоговаяНакладнаяТовары.НомерСтроки) КАК НомерСтроки
	               |	ИЗ
	               |		Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтрокиНН КАК втСтрокиНН
	               |			ПО ТК_НалоговаяНакладнаяТовары.Ссылка = втСтрокиНН.ДокументНН
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втСтрокиНН.ДокументНН,
	               |		втСтрокиНН.Дата
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втСтрокиНН.Дата,
	               |		ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная,
	               |		МАКСИМУМ(ТК_Приложение2КНалоговойНакладнойТовары.НомерСтрокиНН)
	               |	ИЗ
	               |		Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтрокиНН КАК втСтрокиНН
	               |			ПО ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная = втСтрокиНН.ДокументНН
	               |				И (ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.Проведен)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ТК_Приложение2КНалоговойНакладнойТовары.Ссылка.НалоговаяНакладная,
	               |		втСтрокиНН.Дата) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ДокументНН,
	               |	ВложенныйЗапрос.Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСтрокиНН.ДокументНН КАК ДокументНН,
	               |	втСтрокиНН.НомерСтроки КАК НомерСтроки,
	               |	втСтрокиНН.Номенклатура КАК Номенклатура,
	               |	втСтрокиНН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втСтрокиНН.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втСтрокиНН.Коэффициент КАК Коэффициент,
	               |	втСтрокиНН.Количество КАК Количество,
	               |	втСтрокиНН.КоличествоБазовое КАК КоличествоБазовое,
	               |	втСтрокиНН.Цена КАК Цена,
	               |	втСтрокиНН.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	втСтрокиНН.СтавкаНДС КАК СтавкаНДС,
	               |	втСтрокиНН.СуммаНДС КАК СуммаНДС,
	               |	втСтрокиНН.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	втСтрокиНН.Сумма КАК Сумма,
	               |	втСтрокиНН.ПроцентСкидки КАК ПроцентСкидки,
	               |	втСтрокиНН.СуммаСкидки КАК СуммаСкидки,
	               |	втСтрокиНН.СуммаВсего КАК СуммаВсего,
	               |	втСтрокиНН.ДатаОтгрузкиОплаты КАК ДатаОтгрузкиОплаты,
	               |	втСтрокиНН.Содержание КАК Содержание,
	               |	втСтрокиНН.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	втСтрокиНН.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ИЗ
	               |	втСтрокиНН КАК втСтрокиНН
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСтрокиП2НН.ДокументНН КАК ДокументНН,
	               |	втСтрокиП2НН.ДокументП2НН КАК ДокументП2НН,
	               |	втСтрокиП2НН.Номенклатура КАК Номенклатура,
	               |	втСтрокиП2НН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втСтрокиП2НН.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втСтрокиП2НН.Коэффициент КАК Коэффициент,
	               |	втСтрокиП2НН.Количество КАК Количество,
	               |	втСтрокиП2НН.КоличествоБазовое КАК КоличествоБазовое,
	               |	втСтрокиП2НН.Цена КАК Цена,
	               |	втСтрокиП2НН.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	втСтрокиП2НН.СтавкаНДС КАК СтавкаНДС,
	               |	втСтрокиП2НН.СуммаНДС КАК СуммаНДС,
	               |	втСтрокиП2НН.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	втСтрокиП2НН.Сумма КАК Сумма,
	               |	втСтрокиП2НН.СуммаСкидки КАК СуммаСкидки,
	               |	втСтрокиП2НН.СуммаВсего КАК СуммаВсего,
	               |	втСтрокиП2НН.ДатаКорректировки КАК ДатаКорректировки,
	               |	втСтрокиП2НН.Причина КАК Причина,
	               |	втСтрокиП2НН.ИзменениеКоличество КАК ИзменениеКоличество,
	               |	втСтрокиП2НН.ИзменениеЦенаБезНалогов КАК ИзменениеЦенаБезНалогов,
	               |	втСтрокиП2НН.ИзменениеСуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
	               |	втСтрокиП2НН.ИзменениеСуммаНДС КАК ИзменениеСуммаНДС,
	               |	втСтрокиП2НН.КодПричины КАК КодПричины,
	               |	втСтрокиП2НН.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	втСтрокиП2НН.НомерГруппы КАК НомерГруппы,
	               |	втСтрокиП2НН.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ИЗ
	               |	втСтрокиП2НН КАК втСтрокиП2НН
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтрокиНН УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втМаксНомерСтроки.Дата КАК Дата,
	               |	ВложенныйЗапрос.ДокументНН КАК ДокументНН,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	СУММА(ВложенныйЗапрос.КолиествоНН) КАК КоличествоНН,
	               |	СУММА(ВложенныйЗапрос.КоличествоП2НН) КАК КоличествоП2НН,
	               |	МАКСИМУМ(втМаксНомерСтроки.МаксНомерСтроки) КАК МаксНомерСтроки,
	               |	СУММА(ВложенныйЗапрос.КолиествоНН) + СУММА(ВложенныйЗапрос.КоличествоП2НН) КАК ОстатокНН
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втСтрокиНН.ДокументНН КАК ДокументНН,
	               |		втСтрокиНН.Номенклатура КАК Номенклатура,
	               |		втСтрокиНН.КоличествоБазовое КАК КолиествоНН,
	               |		0 КАК КоличествоП2НН
	               |	ИЗ
	               |		втСтрокиНН КАК втСтрокиНН
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втСтрокиП2НН.ДокументНН,
	               |		втСтрокиП2НН.Номенклатура,
	               |		0,
	               |		втСтрокиП2НН.ИзменениеКоличество * втСтрокиП2НН.Коэффициент
	               |	ИЗ
	               |		втСтрокиП2НН КАК втСтрокиП2НН) КАК ВложенныйЗапрос
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втМаксНомерСтроки КАК втМаксНомерСтроки
	               |		ПО ВложенныйЗапрос.ДокументНН = втМаксНомерСтроки.ДокументНН
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ДокументНН,
	               |	ВложенныйЗапрос.Номенклатура,
	               |	втМаксНомерСтроки.Дата
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Организация", ДанныеДня.Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
		
	Для Каждого СтрВозврат Из ТаблицаВозвраты Цикл 			
		Если СтрВозврат.Корректировка <> 0 Тогда 
			Если СтрВозврат.Корректировка <> СтрВозврат.КоличествоБазовое Тогда
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка! %1 по цене %2 количество корректировки %3. Требуется откорректировать %4'; uk = 'Помилка! %1 по ціні %2 кількість корегування %3. Потрібно відкорегувати %4'"),
						СтрВозврат.Номенклатура,
						Формат(СтрВозврат.Цена, "ЧДЦ=2"),
						Макс(СтрВозврат.Корректировка, -СтрВозврат.Корректировка),
						Макс(СтрВозврат.КоличествоБазовое, -СтрВозврат.КоличествоБазовое)),
					СтрВозврат.ДокументП2НН);
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(СтрВозврат.ДатаОперации));
		Запрос.УстановитьПараметр("КонецПериода", КонецДня(СтрВозврат.ДатаОперации));
		Запрос.УстановитьПараметр("Номенклатура", СтрВозврат.Номенклатура);
		Запрос.УстановитьПараметр("Цена", СтрВозврат.Цена);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		МаксИндекс = МассивРезультатов.ВГраница();
		
		СтрокиНН = МассивРезультатов[МаксИндекс-2].Выгрузить();
		СтрокиП2НН = МассивРезультатов[МаксИндекс-1].Выгрузить();		
		
		Количество = -СтрВозврат.КоличествоБазовое;
		ВыборкаНН = МассивРезультатов[МаксИндекс].Выбрать();					
		Пока ВыборкаНН.Следующий() Цикл 
			Если Не ВыборкаНН.ОстатокНН > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Списать = Мин(Количество, ВыборкаНН.ОстатокНН);
			НоваяСтрока = тзРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНН, "Дата, Номенклатура, ДокументНН, КоличествоНН, КоличествоП2НН, ОстатокНН, МаксНомерСтроки");
			НоваяСтрока.Цена = СтрВозврат.Цена;
			НоваяСтрока.Списать = Списать;
			
			//Заполним таблицу строк НН			
			мСтрНН = СтрокиНН.НайтиСтроки(Новый Структура("ДокументНН", ВыборкаНН.ДокументНН));						
			Если тзСтрокиНН.Колонки.Количество() = 0 Тогда 
				тзСтрокиНН = СтрокиНН.Скопировать(мСтрНН);
			Иначе 
				Для Каждого СтрНН Из мСтрНН Цикл 
					новСтрНН = тзСтрокиНН.Добавить();
					ЗаполнитьЗначенияСвойств(новСтрНН, СтрНН);
				КонецЦикла;
			КонецЕсли;
			
			//Заполним таблицу строк Приложения 2
			мСтрП2НН = СтрокиП2НН.НайтиСтроки(Новый Структура("ДокументНН", ВыборкаНН.ДокументНН));						
			Если тзСтрокиП2НН.Колонки.Количество() = 0 Тогда 
				тзСтрокиП2НН = СтрокиП2НН.Скопировать(мСтрП2НН);
			Иначе 
				Для Каждого СтрП2НН Из мСтрП2НН Цикл 
					новСтрП2НН = тзСтрокиП2НН.Добавить();
					ЗаполнитьЗначенияСвойств(новСтрП2НН, СтрП2НН);
				КонецЦикла;
			КонецЕсли;
			
			
			Количество = Количество - Списать;			
			Если Количество = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Количество > 0 Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось списать ""%1"" по цене %2 %3 шт. На %4'; uk = 'Не вдалося списати ""%1"" по ціні %2 %3 шт. За (%4'"),
					СтрВозврат.Номенклатура,
					Формат(СтрВозврат.Цена, "ЧДЦ=2"),
					Количество,
					Формат(СтрВозврат.ДатаОперации, "ДФ=dd.MM.yyyy")),
				ДанныеДня.ДокументНН);					
		КонецЕсли;
	КонецЦикла;
	
	Если тзРаспределение.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	//Дата
	//ДатаОперации
	//Номенклатура
	//Цена
	//КоличествоБазовое
	//Корректировка
	//СуммаНДС
	//СуммаВсего
	//СуммаБезНалогов
	//СтавкаНДС
	//ДокументП2НН
			
	тзДокументыНН = тзРаспределение.Скопировать(, "Дата,ДокументНН,МаксНомерСтроки");
	тзДокументыНН.Свернуть("Дата,ДокументНН,МаксНомерСтроки");
	тзДокументыНН.Сортировать("Дата Убыв");
	
	Для Каждого стрТЗ Из тзДокументыНН Цикл 				
		ДокНН 	 = стрТЗ.ДокументНН;		
		ТоварыНН = тзРаспределение.НайтиСтроки(Новый Структура("ДокументНН", ДокНН));
		ТекДата  = ТекущаяДатаСеанса();
		
		
		ДокП2НН = Документы.ТК_Приложение2КНалоговойНакладной.СоздатьДокумент();
		ДокП2НН.Заполнить(ДокНН);
		
		ДокП2НН.Товары.Очистить();
				
		ДокП2НН.Дата = КонецДня(ДанныеДня.Дата); // ТекДата;		
		ДокП2НН.ХозОперация = Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат;
		ДокП2НН.Автор = Пользователи.ТекущийПользователь();
		ДокП2НН.ДокументОснование = ДанныеДня.ДокументНН;								
		ДокП2НН.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Документ сформирован автоматически. Возврат от %1'; uk = 'Документ створений автоматично. Возврат от %1'"),
								Формат(ДанныеДня.Дата, "ДФ=dd.MM.yyyy"));								
									
		НомерГруппыКорректировки = 1;						
								
		Для Каждого СтрТовары Из ТоварыНН Цикл 
			СтруктураОтбора = Новый Структура("ДокументНН,Номенклатура,Цена");
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрТовары);
			
			НоваяСтрокаП2НН = Неопределено;			
			СписатьПолностью = СтрТовары.Списать = СтрТовары.ОстатокНН;
			
			Если СтрТовары.КоличествоП2НН <> 0 Тогда 								
				ТоварыКорректировки = тзСтрокиП2НН.Скопировать(СтруктураОтбора);
				ТоварыКорректировки.Сортировать("НомерСтрокиНН Убыв");
				
				Для Каждого СтрКор Из ТоварыКорректировки Цикл 
					Если СтрКор.ИзменениеКоличество > 0 Тогда 
						НоваяСтрокаП2НН = ДокП2НН.Товары.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаП2НН, СтрКор);
						НоваяСтрокаП2НН.ИзменениеКоличество = НоваяСтрокаП2НН.ИзменениеКоличество * -1;
						
						НоваяСтрокаП2НН.КодПричины = ?(СписатьПолностью, 103, 102);
						НоваяСтрокаП2НН.НомерГруппы = НомерГруппыКорректировки;
						
						РассчитатьСуммыИзмененийП2НН(НоваяСтрокаП2НН);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ТоварыКорректировки = тзСтрокиНН.Скопировать(СтруктураОтбора);
				СтрКор = ТоварыКорректировки[0];
				
				НоваяСтрокаП2НН = ДокП2НН.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаП2НН, СтрКор);
				НоваяСтрокаП2НН.НомерСтрокиНН = СтрКор.НомерСтроки;
				НоваяСтрокаП2НН.ИзменениеКоличество = НоваяСтрокаП2НН.Количество * -1;
				
				НоваяСтрокаП2НН.КодПричины = ?(СписатьПолностью, 103, 102);
				НоваяСтрокаП2НН.НомерГруппы = НомерГруппыКорректировки;
				
				РассчитатьСуммыИзмененийП2НН(НоваяСтрокаП2НН);
			КонецЕсли;
			
			Если НоваяСтрокаП2НН = Неопределено Тогда 
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка списания %1'"),
						СтрТовары.Номенклатура));
						
				Продолжить;
			КонецЕсли;
			
			Если СтрТовары.Списать < СтрТовары.ОстатокНН Тогда 
				КолВоБазовое = СтрТовары.ОстатокНН - СтрТовары.Списать;
				КолВо = КолВоБазовое / НоваяСтрокаП2НН.Коэффициент;
				
				НовСтрНН = ДокП2НН.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрНН, НоваяСтрокаП2НН);
				
				стрТЗ.МаксНомерСтроки = стрТЗ.МаксНомерСтроки + 1;
				
				НовСтрНН.НомерСтрокиНН = стрТЗ.МаксНомерСтроки;
				НовСтрНН.Количество = КолВо;
				НовСтрНН.ИзменениеКоличество = КолВо;
				новСтрНН.КодПричины = ?(СписатьПолностью, 103, 102);
				новСтрНН.НомерГруппы = НомерГруппыКорректировки;
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
				СтруктураДействий.Вставить("ПересчитатьСумму");
				СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");				
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));

				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НовСтрНН, СтруктураДействий, Неопределено);
				РассчитатьСуммыИзмененийП2НН(НовСтрНН);
			КонецЕсли;
			
			НомерГруппыКорректировки = НомерГруппыКорректировки + 1;
		КонецЦикла;
		
		Попытка
			ДокП2НН.Записать(РежимЗаписиДокумента.Проведение);
			
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%2 документ %1'; uk = '%2 документ %1'"),
					Строка(ДокП2НН.Ссылка),
					?(ДокП2НН.Проведен, НСтр("ru = 'Проведен'; uk = 'Проведений'"), НСтр("ru = 'Записан'; uk = 'Записаний'"))),
				ДокП2НН.Ссылка);

			НовСтрП2НН = ТаблицаПриложения2.Добавить();
			НовСтрП2НН.Документ = ДокП2НН.Ссылка;
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;

	КонецЦикла;
	
	
КонецПроцедуры

Функция ВыгрузитьВXMLНаСервере(ДанныеДня, мТовары, РеквизитыОрганизации)	
	
	 R03G7 = ДанныеДня.НДС20; //СуммаНДС20
	 R01G7 = ДанныеДня.СуммаБезНДС_20;  //СуммаБНДС20
	 R03G109 = ДанныеДня.НДС7; //СуммаНДС7
	 R01G109 = ДанныеДня.СуммаБезНДС_7;//СуммаБНДС7		
	
	// R03G7 = 0; //СуммаНДС20
	// R01G7 = 0;  //СуммаБНДС20
	// R03G109 = 0; //СуммаНДС7
	// R01G109 = 0;//СуммаБНДС7
	
	 //Для Каждого стТовары Из мТовары Цикл 
	 //    Если стТовары.Ставка = 7 Тогда
	 //   	R03G109 = R03G109 + стТовары.СуммаНДС;
	 //   	R01G109 = R01G109 + стТовары.СуммаБезНДС;
	 //    ИначеЕсли стТовары.Ставка = 20 Тогда
	 //   	R03G7  = R03G7 + стТовары.СуммаНДС;
	 //   	R01G7  = R01G7 + стТовары.СуммаБезНДС;
	 //    Иначе
	 //   	ВызватьИсключение "СТАВКА НДС!!!"; 
	 //    КонецЕсли;
	 //    
	 //КонецЦикла;
	 
	 
	ПотокФайла = Новый ПотокВПамяти();
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.ОткрытьПоток(ПотокФайла);
	
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLAR");
	
		Схема = "J1201010.xsd";
	
		ОбъектXML.ЗаписатьНачалоАтрибута("xmlns:xsi"); 
			ОбъектXML.ЗаписатьТекст("http://www.w3.org/2001/XMLSchema-instance"); 
		ОбъектXML.ЗаписатьКонецАтрибута();
		
		ОбъектXML.ЗаписатьНачалоАтрибута("xsi:noNamespaceSchemaLocation");
			ОбъектXML.ЗаписатьТекст(Схема);
		ОбъектXML.ЗаписатьКонецАтрибута();
		
		Схема = СтрЗаменить(Схема, ".xsd", "");
		
		///DECLARHEAD	
		ОбъектXML.ЗаписатьНачалоЭлемента("DECLARHEAD");		
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодПоЕДРПОУ, "Строка"), "TIN");
			ЗаписатьXML(ОбъектXML, Лев(Схема, 3), "C_DOC");
			ЗаписатьXML(ОбъектXML, Сред(Схема, 4, 3), "C_DOC_SUB");
			ЗаписатьXML(ОбъектXML, Прав(Схема, 2), "C_DOC_VER");
			ЗаписатьXML(ОбъектXML, Прав(Схема, 1), "C_DOC_TYPE");
			ЗаписатьXML(ОбъектXML, "1242", "C_DOC_CNT");
			ЗаписатьXML(ОбъектXML, Месяц(ДанныеДня.Дата),"PERIOD_MONTH");
			ЗаписатьXML(ОбъектXML, 1,"PERIOD_TYPE");
			ЗаписатьXML(ОбъектXML, Год(ДанныеДня.Дата), "PERIOD_YEAR");
			ЗаписатьXML(ОбъектXML, 1,"C_DOC_STAN");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Дата, "Дата"), "D_FILL");
		ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARHEAD	
		
		///DECLARBODY	
		ОбъектXML.ЗаписатьНачалоЭлемента("DECLARBODY");
			ЗаписатьXML(ОбъектXML, "1", "HORIG1");	
			ЗаписатьXML(ОбъектXML, "11", "HTYPR");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Дата, "Дата"), "HFILL");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.НомерНакладной), "HNUM");	//номер документа
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.НаименованиеПолное, "Строка"), "HNAMESEL");	
			ЗаписатьXML(ОбъектXML, "Неплатник", "HNAMEBUY");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.ИНН, "Строка"), "HKSEL");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодПоЕДРПОУ, "Строка"), "HTINSEL");
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.КодИсточникаНалоговогоНомера, "Число"), "HKS");
			ЗаписатьXML(ОбъектXML, "100000000000", "HKBUY");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.Сумма, "Число"), "R04G11");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(ДанныеДня.СуммаНДС, "Число"), "R03G11");	
			//20
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(R03G7, "Число"), "R03G7");//НДС20	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(R01G7, "Число"), "R01G7");//БНДС20	
			//7
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(R03G109, "Число"), "R03G109");//НДС7	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(R01G109, "Число"), "R01G109");//БНДС7	
			
			
			СоответствиеКолонок = Новый Соответствие;
			СоответствиеКолонок.Вставить("RXXXXG3S",	"ПредставлениеТовара");
			СоответствиеКолонок.Вставить("RXXXXG4", 	"КодУКТВЭД");
			СоответствиеКолонок.Вставить("RXXXXG008", 	"СтавкаНДС");
			СоответствиеКолонок.Вставить("RXXXXG010", 	"СуммаБезНДС");
			СоответствиеКолонок.Вставить("RXXXXG11_10", "СуммаНДС");
			//Новые
			СоответствиеКолонок.Вставить("RXXXXG5", 	"Объем");
			СоответствиеКолонок.Вставить("RXXXXG6", 	"ЦенаБез");
			СоответствиеКолонок.Вставить("RXXXXG4S", 	"ЕдиницаИзмерения");
			СоответствиеКолонок.Вставить("RXXXXG105_2S","КодЕИ");			
			
			СтруктураЕдИзм = Новый Структура;
			СтруктураЕдИзм.Вставить("шт", "2009");
			СтруктураЕдИзм.Вставить("пляш", "2061");
			СтруктураЕдИзм.Вставить("пач", "2112");
			
			Для Каждого Параметр Из СоответствиеКолонок Цикл
				Номер = 0;
				Для Каждого Стр Из мТовары Цикл					
					Значение = Неопределено;
					
					Номер = Номер + 1;
					
					Если Параметр.Значение = "КодУКТВЭД" Тогда    
						Значение = СтрЗаменить(СокрЛП(Стр.КодУКТВЭД), " ", ""); 
					ИначеЕсли Параметр.Значение = "СтавкаНДС" Тогда
						Значение = Строка(Формат(Стр.Ставка, "ЧДЦ=0"));
					ИначеЕсли Параметр.Значение = "Объем" Тогда
						Значение = Стр.Количество;
					ИначеЕсли Параметр.Значение = "ЦенаБез" Тогда
						Значение = Формат((Стр.СуммаБезНДС/Стр.Количество),"ЧДЦ=6; ЧГ=0");						
					Иначе
						Значение = Стр[Параметр.Значение];
					КонецЕсли;
										
					Значение = ФорматВыгрузки(Значение);
										
					ОбъектXML.ЗаписатьНачалоЭлемента(Параметр.Ключ);											
						ОбъектXML.ЗаписатьАтрибут("ROWNUM", ФорматВыгрузки(Номер, "Число"));
						ОбъектXML.ЗаписатьТекст(Строка(Значение));						
					ОбъектXML.ЗаписатьКонецЭлемента();
				КонецЦикла;
			КонецЦикла;			
			
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.Руководитель), "HBOS");	
			ЗаписатьXML(ОбъектXML, ФорматВыгрузки(РеквизитыОрганизации.РуководительКод), "HKBOS");	
			
		ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARBODY	
	ОбъектXML.ЗаписатьКонецЭлемента(); //DECLAR
	
	ОбъектXML.Закрыть();	
	
		
	ИмяФайла = СокрЛП(Строка(ДанныеДня.Организация)) + "_" + Формат(ДанныеДня.Дата, "ДФ=yyyyMMdd") + "_НН.xml"; 	
	ДвоичныеДанные = ПотокФайла.ЗакрытьИПолучитьДвоичныеДанные();
	
	//ОписаниеФайла = Новый ОписаниеПередаваемогоФайла;
	//ОписаниеФайла.Имя = ИмяФайла;
	//ОписаниеФайла.Хранение = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Имя", ИмяФайла);
	ОписаниеФайла.Вставить("ДвоичныеДанные", ДвоичныеДанные);
	
	Возврат ОписаниеФайла;
	
КонецФункции

Процедура СформироватьТаблицуНН(ДеревоНалоговые, МассивДанных, Параметры)
	
	Если ДеревоНалоговые.Строки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаНачала = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДатаНачала", НачалоДня(ТекущаяДата()));
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДатаОкончания", КонецДня(ТекущаяДата()));
	Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Организация", "");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;

	Макет = ПолучитьМакет("Расхождения");
	//Макет.КодЯзыка = КодЯзыкаПечать;	
	ОбластьШапка  = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Для Каждого СтрОрганизация Из ДеревоНалоговые.Строки Цикл 				
		Для Каждого СтрДата Из СтрОрганизация.Строки Цикл 
			СтруктураСтроки = Новый Структура("Дата,Организация,СуммаБезНДС,СуммаНДС,Сумма,СуммаБезНДС_ТЧ,СуммаНДС_ТЧ,НДС20,НДС7,НДС20_ТЧ,НДС7_ТЧ");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрДата);
			СтруктураСтроки.Вставить("Сумма_ТЧ", СтруктураСтроки.СуммаНДС_ТЧ + СтруктураСтроки.СуммаБезНДС_ТЧ);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
	КонецЦикла;
	
	ИмяВрФайла = ПолучитьИмяВременногоФайла(".xlsx");
	ТабличныйДокумент.Записать(ИмяВрФайла, ТипФайлаТабличногоДокумента.XLSX);	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВрФайла);
	
	ИмяФайла = СокрЛП(Организация) + "_" + ПредставлениеПериода(ДатаНачала, ДатаОкончания) + ".xlsx";
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Имя", ИмяФайла);
	ОписаниеФайла.Вставить("ДвоичныеДанные", ДвоичныеДанные);	
	
	МассивДанных.Добавить(ОписаниеФайла);
	
КонецПроцедуры

Процедура ПроверкаВыгрузкиВБухгалтерию(ДеревоЗначений, СписокНомеровБухгалтерии)
	
	ХранилищеБух = Новый ХранилищеЗначения(СписокНомеровБухгалтерии, Новый СжатиеДанных(9));
	
	СвойстваПодключения = ТК_БУХ_РаботаWebСервиса.ПолучитьWSСсылкуБухгалтерия();
	
	Если Не СвойстваПодключения.Успешно Тогда 
		Сообщение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваПодключения, "Сообщение", НСтр("ru = 'Не удалось подключится к базе ""Бухгалтерия""'; uk = 'Не вдалося підключитися до бази ""Бухгалтерія""'"));
		
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
		Возврат;
	КонецЕсли;
	
	WS = СвойстваПодключения.WS;	
		
	Попытка
		ОтветБух = WS.CheckTaxInvoiceList(ХранилищеБух);
		
		Если ОтветБух.Status Тогда 
			Для Каждого Стр Из ОтветБух.List Цикл 
				нСтроки = ДеревоЗначений.Строки.НайтиСтроки(Новый Структура("НомерДляБухгалтерии", СокрЛП(Стр.TaxInvoiceNumber)), Истина);
				Если нСтроки.Количество() > 0 Тогда 
					нСтроки[0].ВыгруженВБух = Истина;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(ОтветБух.Description);	
		КонецЕсли;
	Исключение
		ОбщегоНазначения.СообщитьПользователю("Ошибка подключения к бае Бухгалтерия");
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;	
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

Функция ПолучитьНомерДокумента(Дата, НомерДляБухгалтерии)
	ТекНомерНН = "";
	ТекНомерФилиала = "";
	МесячныйПрефиксВНомереДляБух = ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата);
	ТекНомерННСФилиалом = ?(Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) = 0, НомерДляБухгалтерии, Прав(НомерДляБухгалтерии, СтрДлина(НомерДляБухгалтерии) - Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) - 1)); 

	ПозицияСлеша = Найти(ТекНомерННСФилиалом, "/");
	Если ПозицияСлеша = 0 Тогда
		ТекНомерНН = ТекНомерННСФилиалом;
	Иначе
		ТекНомерНН = Лев(ТекНомерННСФилиалом, ПозицияСлеша - 1);
		ТекНомерФилиала = Сред(ТекНомерННСФилиалом, ПозицияСлеша + 1, СтрДлина(ТекНомерННСФилиалом)-ПозицияСлеша);			
	КонецЕсли;
	
	Возврат ТекНомерНН;
КонецФункции

Функция ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата)

	НомерМесяца = Месяц(Дата);
	Индекс = НомерМесяца*3 - 2;
	
	Возврат Сред("Ян|Фв|Мр|Ап|Ма|Ин|Ил|Ав|Сн|Ок|Но|Дк", Индекс, 2);	

КонецФункции

Функция ПолучитьРеквизитыОрганизации(ТекДата, текОрганизация, ДанныеОрганизаций, ДатаОкончания)
	
	Если ТипЗнч(ДанныеОрганизаций) <> Тип("Структура") Тогда 
		ДанныеОрганизаций = Новый Структура;
	КонецЕсли;
	
	ИД_Организации = "Орг_" + СтрЗаменить(Строка(текОрганизация.УникальныйИдентификатор()), "-", "_");
	
	СохранитьДанные = Ложь;	
	СтруктураРеквизитов = Неопределено;
	СтрокаПараметровИНН = "ПлательщикЕН, ПлательщикНДС, ИНН, НомерСвидетельства, ПлательщикНалогаНаПрибыль";
	СтрокаПараметровРуководители = "Руководитель, РуководительКод";
	
	Если Не ДанныеОрганизаций.Свойство(ИД_Организации, СтруктураРеквизитов) ИЛИ СтруктураРеквизитов = Неопределено Тогда 
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", текОрганизация);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	Организации.НаименованиеПолное КАК НаименованиеПолное,
		               |	Организации.ЮрФизЛицо КАК ЮрФизЛицо,
		               |	Организации.КодПоЕДРПОУ КАК КодПоЕДРПОУ
		               |ИЗ
		               |	Справочник.Организации КАК Организации
		               |ГДЕ
		               |	Организации.Ссылка = &Организация
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	УчетнаяПолитикаОрганизаций.Период КАК Период,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикЕН КАК ПлательщикЕН,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикНДС КАК ПлательщикНДС,
		               |	УчетнаяПолитикаОрганизаций.ИННПлательщикаНДС КАК ИНН,
		               |	УчетнаяПолитикаОрганизаций.НомерСвидетельстваПлательщикаНДС КАК НомерСвидетельства,
		               |	УчетнаяПолитикаОрганизаций.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль
		               |ИЗ
		               |	РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
		               |ГДЕ
		               |	УчетнаяПолитикаОрганизаций.Организация = &Организация
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОтветственныеЛицаОрганизаций.Период КАК Период,
		               |	ОтветственныеЛицаОрганизаций.ФизическоеЛицо.Представление КАК Руководитель,
		               |	ОтветственныеЛицаОрганизаций.ФизическоеЛицо.Код КАК РуководительКод
		               |ИЗ
		               |	РегистрСведений.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
		               |ГДЕ
		               |	ОтветственныеЛицаОрганизаций.СтруктурнаяЕдиница = &Организация
		               |	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период";
		
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Реквизиты = МассивРезультатов[0].Выбрать();
		ВыборкаИНН = МассивРезультатов[1].Выбрать();
		ВыборкаРуководители = МассивРезультатов[2].Выбрать();
						
		Реквизиты.Следующий();
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("НаименованиеПолное", Реквизиты.НаименованиеПолное);
		СтруктураРеквизитов.Вставить("ЮрЛицо", Реквизиты.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
		СтруктураРеквизитов.Вставить("КодПоЕДРПОУ", Реквизиты.КодПоЕДРПОУ);		
		
		ДатаОкончания = ДобавитьМесяц(ДатаОкончания, 1);
		
		МассивПериодовИНН = Новый Массив;
		ЗаполнитьПараметрыРекурсивно(ВыборкаИНН, СтрокаПараметровИНН, МассивПериодовИНН, ДатаОкончания);
		СтруктураРеквизитов.Вставить("МассивПериодовИНН", МассивПериодовИНН);
		
		МассивРуководителей = Новый Массив;		
		ЗаполнитьПараметрыРекурсивно(ВыборкаРуководители, СтрокаПараметровРуководители, МассивРуководителей, ДатаОкончания);
		СтруктураРеквизитов.Вставить("МассивРуководителей", МассивРуководителей);
		
		УстановитьПривилегированныйРежим(Ложь);	
		
		ДанныеОрганизаций.Вставить(ИД_Организации, СтруктураРеквизитов);	
	КонецЕсли;
	
	Если Не (СтруктураРеквизитов.Свойство("ТекПериодИНН") 
		И СтруктураРеквизитов.ТекПериодИНН.ДатаНачало <= ТекДата
		И СтруктураРеквизитов.ТекПериодИНН.ДатаКонец > ТекДата) Тогда 
		
		СтруктураИНН = Новый Структура(СтрокаПараметровИНН);
		Для Каждого ПериодИНН Из СтруктураРеквизитов.МассивПериодовИНН Цикл 
			Если ПериодИНН.ДатаНачало <= ТекДата И ПериодИНН.ДатаКонец > ТекДата Тогда 
				ЗаполнитьЗначенияСвойств(СтруктураИНН, ПериодИНН, СтрокаПараметровИНН);
				СтруктураИНН.Вставить("ТекПериодИНН", Новый Структура("ДатаНачало, ДатаКонец", ПериодИНН.ДатаНачало, ПериодИНН.ДатаКонец));
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		СтруктураИНН.Вставить("КодИсточникаНалоговогоНомера", Документы.ТК_НалоговаяНакладная.КодИсточникаНалоговогоНомера(СтруктураРеквизитов.КодПоЕДРПОУ, СтруктураРеквизитов.ЮрЛицо));
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, СтруктураИНН, Истина);
	КонецЕсли;
	
	Если Не (СтруктураРеквизитов.Свойство("ТекПериодРуководитель")
		И СтруктураРеквизитов.ТекПериодРуководитель.ДатаНачало <= ТекДата
		И СтруктураРеквизитов.ТекПериодРуководитель.ДатаКонец > ТекДата) Тогда 
		
		СтруктураРуководитель = Новый Структура(СтрокаПараметровРуководители);
		Для Каждого ПериодРуководитель Из СтруктураРеквизитов.МассивРуководителей Цикл 
			Если ПериодРуководитель.ДатаНачало <= ТекДата И ПериодРуководитель.ДатаКонец > ТекДата Тогда 
				ЗаполнитьЗначенияСвойств(СтруктураРуководитель, ПериодРуководитель, СтрокаПараметровРуководители);
				СтруктураРуководитель.Вставить("ТекПериодРуководитель", Новый Структура("ДатаНачало, ДатаКонец", ПериодРуководитель.ДатаНачало, ПериодРуководитель.ДатаКонец)); 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, СтруктураРуководитель, Истина);
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
КонецФункции

Функция ЗаполнитьПараметрыРекурсивно(Выборка, Знач СтрокаПараметров, Массив, Знач ДатаОкончания)	
	
	Если Выборка.Следующий() Тогда  
		СтруктураПараметров = Новый Структура(СтрокаПараметров);			
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка, СтрокаПараметров);
		СтруктураПараметров.Вставить("ДатаНачало", НачалоДня(Выборка.Период));
		СтруктураПараметров.Вставить("ДатаКонец", ЗаполнитьПараметрыРекурсивно(Выборка, СтрокаПараметров, Массив, ДатаОкончания));
		
		Массив.Добавить(СтруктураПараметров);
		
		ДатаОкончания = СтруктураПараметров.ДатаНачало-1;
	КонецЕсли;
	
	Возврат ДатаОкончания;
КонецФункции

Функция ФорматВыгрузки(Значение, Тип = Неопределено)
	ОтформатированноеЗначение = Неопределено;
	
	Если Тип = "Дата" Тогда
		ОтформатированноеЗначение = Формат(Значение, "ДФ=ddMMyyyy");		
	ИначеЕсли Тип = "Число" Тогда
		ОтформатированноеЗначение = Формат(Значение, "ЧРД=.; ЧГ=0");	
	ИначеЕсли Тип = "Строка" Тогда
		ОтформатированноеЗначение = СокрЛП(Строка(Значение));
	ИначеЕсли Тип = Неопределено Тогда
		ФорматЗнч = ТипЗнч(Значение);
		ОтформатированноеЗначение = ФорматВыгрузки(Значение, Строка(ФорматЗнч));
	Иначе
		ОтформатированноеЗначение = Значение;
	КонецЕсли;
	
	Возврат ОтформатированноеЗначение;
КонецФункции

Функция ОкруглитьЧисло(Число, Разрядность = 0, Разница = 0)
	
	ОкрЧисло = Окр(Число, Разрядность);
	
	Разница = Разница + (Число - ОкрЧисло);
	
	Возврат ОкрЧисло;
КонецФункции

Процедура РассчитатьСуммыИзмененийП2НН(СтрокаТЧ)
	
	Коэф = 0;
	Если СтрокаТЧ.ИзменениеКоличество <> 0 Тогда
		
		ИсходноеКоличество = СтрокаТЧ.Количество;		
		Если ИсходноеКоличество = 0 Тогда
			ИсходноеКоличество = 1;
		КонецЕсли;
			 
		Коэф =  СтрокаТЧ.ИзменениеКоличество / ИсходноеКоличество;
		
	ИначеЕсли СтрокаТЧ.ИзменениеЦенаБезНалогов <> 0 Тогда
		
		ИсходнаяЦена = СтрокаТЧ.ЦенаБезНалогов;		
		Если ИсходнаяЦена = 0 Тогда
			ИсходнаяЦена = 1;
		КонецЕсли;

		Коэф =  СтрокаТЧ.ИзменениеЦенаБезНалогов/ИсходнаяЦена;	
		
	КонецЕсли;
	
	СтрокаТЧ.ИзменениеСуммаБезНалогов	= СтрокаТЧ.СуммаБезНалогов * Коэф;
	СтрокаТЧ.ИзменениеСуммаНДС			= СтрокаТЧ.СуммаНДС * Коэф;		
	
КонецПроцедуры



Функция ПолучитьВыгруженныеФайлы_Стар(ДеревоНалоговые, ДеревоНалоговыеМРЦ, ДеревоННБезНДС, ТаблицаПриложения2, Знач ПараметрыОперации) Экспорт 
			
	ДанныеОрганизаций = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДанныеОрганизаций", Неопределено);			
	Контрагент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ДатаНачала", КонецДня(ТекущаяДата()));
	ГУИД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ГУИД", Неопределено);
	ПерезаполнитьНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "ПерезаполнитьНН", Ложь);
	СоздаватьННБезНДС = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОперации, "СоздаватьННБезНДС", Ложь);
	
	МассивНН = Новый Массив;
	МассивП2НН = Новый Массив;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрОрганизация Из ДеревоНалоговые.Строки Цикл 
		Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 				
			
			Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 								
				ТаблицаВозвраты = Неопределено;
				
				СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, Контрагент);
				
				Если ТаблицаВозвраты.Количество() > 0 Тогда
					СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, Контрагент);
				КонецЕсли;								
			КонецЕсли;
			
			МассивНН.Добавить(ИтогиДня.ДокументНН);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СтрОрганизация Из ДеревоНалоговыеМРЦ.Строки Цикл 
		Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 				
			
			Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 								
				ТаблицаВозвраты = Неопределено;
				
				СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, Контрагент,,1,Истина);
				
				Если ТаблицаВозвраты.Количество() > 0 Тогда
					СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, Контрагент);
				КонецЕсли;								
			КонецЕсли;
			
			МассивНН.Добавить(ИтогиДня.ДокументНН);
		КонецЦикла;
	КонецЦикла;

	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если СоздаватьННБезНДС Тогда 
		УстановитьПривилегированныйРежим(Истина);	
		
		Для Каждого СтрОрганизация Из ДеревоННБезНДС.Строки Цикл 
			Для Каждого ИтогиДня Из СтрОрганизация.Строки Цикл 
				Если ИтогиДня.ДокументНН.Пустая() ИЛИ ПерезаполнитьНН Тогда 
					ТаблицаВозвраты = Неопределено;
					
					СоздатьДокументНН(ИтогиДня, ТаблицаВозвраты, Контрагент, Истина);
					
					Если ТаблицаВозвраты.Количество() > 0 Тогда 
						СоздатьПриложение2кНН_Нов(ИтогиДня, ТаблицаВозвраты, ТаблицаПриложения2, Контрагент);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	МассивВыгрузки = Документы.ТК_НалоговаяНакладная.СформироватьНалоговую(МассивНН);	
	
	Если ТаблицаПриложения2.Количество() > 0 Тогда 
		МассивП2НН = ТаблицаПриложения2.ВыгрузитьКолонку("Документ");
		
		МассивВыгрузкиП2НН = Документы.ТК_Приложение2КНалоговойНакладной.СформироватьПриложение2кНН(МассивП2НН);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивВыгрузки, МассивВыгрузкиП2НН);
	КонецЕсли;
	
	УИД = Новый УникальныйИдентификатор;
	МассивФайлов = Новый Массив;
	
	Для Каждого СтруктураДокумента Из МассивВыгрузки Цикл 
		МассивФайлов.Добавить(Новый ОписаниеПередаваемогоФайла(СтруктураДокумента.Выгрузка.ИмяФайла, ПоместитьВоВременноеХранилище(СтруктураДокумента.Выгрузка.ДанныеXML,УИД)));
	КонецЦикла;
	
	Возврат МассивФайлов;

КонецФункции


#КонецОбласти



#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли


&НаСервере
Процедура ВыгрузитьВнешнихНаСервере()
	МассивПосетителей = Новый Массив;
	Для Каждого стрПосетитель Из Внешние Цикл
		Посетитель = Новый Структура();
		Посетитель.Вставить("Наименование",стрПосетитель.Посетитель.Наименование);
		Посетитель.Вставить("ДоступРазрешен",стрПосетитель.ДоступРазрешен);
		Посетитель.Вставить("КодПоДРФО","ext"+СокрЛП(стрПосетитель.Посетитель.Код));
		Посетитель.Вставить("Телефон",стрПосетитель.Посетитель.Телефон);
		Посетитель.Вставить("Организация",СокрЛП(стрПосетитель.Посетитель.Контрагент));
		Посетитель.Вставить("Должность",СокрЛП(стрПосетитель.Посетитель.Должность));
		Посетитель.Вставить("Фото",стрПосетитель.Посетитель.Фото);
		МассивПосетителей.Добавить(Посетитель);
	КонецЦикла;
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	ЗаписатьJSON(ЗаписьJSON, МассивПосетителей); 
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Определение = Новый WSОпределения("https://orders-1c.digma.ua/SS/ws1.1cws?WSDL",,,,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
	WS = Новый WSПрокси(Определение,"http://www.StartStop.ua","CommunicationStartStop","CommunicationStartStopSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
	
	ОтветРезультат = WS.RegisterVisitors(СтрокаJSON);
	
	ВсеОК = ОтветРезультат.StatusDownload;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОтветРезультат.Description);
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьВнутреннихНаСервере(Проверка)
	Определение = Новый WSОпределения("https://orders-1c.digma.ua/zuUU/ws/cwd2.1cws?wsdl","ObmenSSL","bynthghbpf",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
	WS = Новый WSПрокси(Определение,"http://www.digma.ua","CommunicationWithDatabases","CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
	WS.Пользователь="ObmenSSL";
	WS.Пароль ="bynthghbpf";
	
	МассивПосетителей = Новый Массив;
	
	Для Каждого стрПосетитель Из Внутренние Цикл
		Посетитель = Новый Структура();
		Посетитель.Вставить("Наименование",(стрПосетитель.Посетитель.Наименование));
		//Посетитель.Вставить("ДоступРазрешен",стрПосетитель.ДоступРазрешен);
		Посетитель.Вставить("КодПоДРФО",СокрЛП(стрПосетитель.Посетитель.Код));
		//Посетитель.Вставить("ТелефонТК",СокрЛП(стрПосетитель.Телефон));
		Посетитель.Вставить("ЭтоКуратор",стрПосетитель.ЭтоКуратор);
		МассивПосетителей.Добавить(Посетитель);
	КонецЦикла;
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	ЗаписатьJSON(ЗаписьJSON, МассивПосетителей); 
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Рез = WS.RegisterVisitors(СтрокаJSON, Проверка);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Рез);
	ОтветРезультат = ПрочитатьJSON(Чтение);
	
	Внутренние.Очистить();
	Если Проверка Тогда
		ОтветМассивПосетителей = ОтветРезультат;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОтветРезультат.Результат.Description);
		ОтветМассивПосетителей = ОтветРезультат.МассивПосетителей;
	КонецЕсли;
	
	Для Каждого стрПосетитель Из ОтветМассивПосетителей Цикл
		стр = Внутренние.Добавить();
		ЗаполнитьЗначенияСвойств(стр,стрПосетитель);
		стр.Посетитель = Справочники.ФизическиеЛица.НайтиПоКоду(стрПосетитель.КодПоДРФО);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "ГруппаВнешние" Тогда
		Если Внешние.Количество() > 0 Тогда
			ВыгрузитьВнешнихНаСервере();
		КонецЕсли;
	КонецЕсли;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "ГруппаВнутренние" Тогда
		Если Внутренние.Количество() > 0 Тогда
			ВыгрузитьВнутреннихНаСервере(Ложь);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ПолучитьКураторовНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Посетитель,
	|	ИСТИНА КАК ЭтоКуратор
	|ИЗ
	|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
	|ГДЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
	|
	|УПОРЯДОЧИТЬ ПО
	|	Посетитель
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Внутренние.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры


&НаКлиенте
Процедура ПолучитьКураторов(Команда)
	ПолучитьКураторовНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ПроверитьСотрудников(Команда)
	Если Внутренние.Количество() > 0 Тогда
		ВыгрузитьВнутреннихНаСервере(Истина);
	КонецЕсли;
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВнешниеПосетительДоступРазрешен(Посетитель)
	ДоступРазрешен = Посетитель.ДоступРазрешен;
	Возврат ДоступРазрешен;
КонецФункции


&НаКлиенте
Процедура ВнешниеПосетительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Внешние.ТекущиеДанные;
	ТекущиеДанные.ДоступРазрешен = ВнешниеПосетительДоступРазрешен(ТекущиеДанные.Посетитель);
КонецПроцедуры


&НаСервереБезКонтекста
Функция ЭтоКуратор(ФизЛицо)
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ 
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка КАК Ссылка,
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Посетитель
	|ИЗ
	|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
	|ГДЕ
	|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
	|	И ТорговыеПлощадкиДополнительныеРеквизиты.Значение = &ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Посетитель
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ФизЛицо",ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Не РезультатЗапроса.Пустой();
КонецФункции


&НаКлиенте
Процедура ВнутренниеПосетительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Внутренние.ТекущиеДанные;
	ФизЛицо = ТекущиеДанные.Посетитель;
	ТекущиеДанные.ЭтоКуратор = ЭтоКуратор(ФизЛицо);
КонецПроцедуры




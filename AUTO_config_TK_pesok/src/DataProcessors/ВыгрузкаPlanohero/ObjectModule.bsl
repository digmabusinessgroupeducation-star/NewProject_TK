///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	Перем РезВО Экспорт, СтрЗР Экспорт, ТекстОшибок Экспорт;
	Перем УзелПланаОбмена Экспорт, PlanoHeroAttika Экспорт;
	Перем СтруктураСоответствий;
	
	#Область СлужебныеПроцедурыИФункции
	
	Процедура НачатьВыгрузку()	Экспорт
		//РезультатВыполненияОбработки= РегистрыСведений.РезультатыВыполненияОбработок;	
		//СтруктураЗаписиРезультата	= РезультатВыполненияОбработки.СформироватьСтруктуруЗаписиРезультатов();
		//СтруктураЗаписиРезультата.ИдентификаторОбработки = "ВыгрузкаPlanohero";
		//СтруктураЗаписиРезультата.КлючОбработки = "Выгрузка Planohero";
		//РезультатВыполненияОбработки.ДобавитьКомментарий("Тест Задания.Planohero", СтруктураЗаписиРезультата);
		//РезультатВыполненияОбработки.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписиРезультата,Истина);
		
		ВыгрузитьДанные();	
	КонецПроцедуры	
	
	Функция Инициализация(Клиент) Экспорт
		
		РезВО = РегистрыСведений.РезультатыВыполненияОбработок;	
		СтрЗР = РезВО.СформироватьСтруктуруЗаписиРезультатов();
		СтрЗР.ИдентификаторОбработки = "ВыгрузкаPlanoHero";
		СтрЗР.КлючОбработки = "Выгрузка PlanoHero";
		
		УзелПланаОбмена = ПланыОбмена.ОбменPlanohero.НайтиПоКоду("BES");
		PlanoHeroAttika = ВнешниеИсточникиДанных.PlanoHeroAttika.Таблицы;
		
		СтруктураСоответствий = Новый Структура;
		СтруктураСоответствий.Вставить("dbo_shop",Новый Соответствие);
		СтруктураСоответствий.Вставить("dbo_product",Новый Соответствие);
		СтруктураСоответствий.Вставить("dbo_supplier",Новый Соответствие);
		СтруктураСоответствий.Вставить("dbo_assortmenttype",Новый Соответствие);
		
		Если НЕ Клиент Тогда
			Подразделения.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("000000002"));	//	ТС Аттика	
			Подразделения.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("AT000009"));	//	ТС Табак-Дигма	
			Подразделения.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("000000008"));	//	ТС Buffet
			Подразделения.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("AT000014"));	//	ТС Фабрика сыра
		КонецЕсли;
		
		ЗаполнитьАктивныеСклады();
		
		Структура = Новый Структура;
		
		СтруктураНачало = Новый Массив;
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",1,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Связь Склад Площадка","dbo_warehouse2sarea"));
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",2,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Связь магазинов и меток","dbo_shopmarkerm2m"));
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",3,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Связь товаров и маркеров","dbo_productmarkerm2m"));
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",4,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Связь Склад Тип цен","dbo_priceshoptype"));
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",5,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Количество фейсов","dbo_recommendedfaces"));
		СтруктураНачало.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",6,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Смена ассортимента","dbo_assortment"));
		
		Структура.Вставить("Начало",СтруктураНачало);
		
		СтруктураДанных = Новый Массив;
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",1,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"КлассификаторПродаж","dbo_promotiontype"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",2,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Контрагенты","dbo_supplier"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",3,ЛОЖЬ,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"Номенклатура","dbo_category"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",4,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Номенклатура","dbo_product"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",5,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ПодразделенияКомпании","dbo_shopgroup"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",6,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Пользователи","dbo_staff"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",7,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ФизическиеЛица","dbo_staff"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",8,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"СтатьиДоходовИРасходов","dbo_losstype"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",9,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ТипыБонусов","dbo_supplierbonustype"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",10,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ТипыЦен","dbo_pricetype"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",11,ЛОЖЬ,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ТоварныеМарки","dbo_producer"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",12,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ТоварныеМарки","dbo_brand"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",13,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"СкладыКомпании","dbo_shop"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",14,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"ТорговыеПлощадки","dbo_shop"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",15,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ИСТИНА,"ТочкиДоставки","dbo_shop"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",16,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"КассыККМ","dbo_terminal"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",20,,,,,"",""));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",21,ЛОЖЬ,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Остатки","dbo_productinventory"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",22,,,,,"",""));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",31,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ЗаказПоставщику","dbo_purchase"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",32,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ЗаказПоставщику Товар","dbo_purchaseproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",33,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ВозвратПоставщику","dbo_supplierrefund"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",34,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ВозвратПоставщику Товар","dbo_supplierrefundproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",35,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ПоступлениеТоваров","dbo_receive"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",36,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ПоступлениеТоваров Товар","dbo_receiveproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",37,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ПеремещениеТоваров","dbo_relocate"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",38,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ПеремещениеТоваров Товар","dbo_relocateproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",39,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"Инвентаризация","dbo_incoming"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",40,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"Инвентаризация Товар","dbo_incomingproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",41,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"СписаниеТоваров","dbo_loss"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",42,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"СписаниеТоваров Товар","dbo_lossproduct"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",43,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ИзменениеЦен","dbo_pricedocument"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",44,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ИзменениеЦен Товар","dbo_priceproduct"));
		//СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",45,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ИзменениеЦен Склад","dbo_priceshop"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",46,ИСТИНА,ЛОЖЬ,ИСТИНА,ЛОЖЬ,"ТК_НазначениеСкидок","dbo_promotion"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",47,ИСТИНА,ИСТИНА,ЛОЖЬ,ЛОЖЬ,"ТК_НазначениеСкидок Товар","dbo_promotionproductm2m"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",48,ИСТИНА,ИСТИНА,ИСТИНА,ЛОЖЬ,"ТК_ОптимальноеКоличествоНоменклатуры","dbo_recommendedfaces"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",49,ИСТИНА,ИСТИНА,ИСТИНА,ЛОЖЬ,"ИзменениеАссортимента","dbo_assortment"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",50,ИСТИНА,ИСТИНА,ИСТИНА,ЛОЖЬ,"ЗакрытиеСмены","dbo_sales_1c"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",60,,,,,"",""));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",61,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Типы ассортимента","dbo_assortmenttype"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",62,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Метки магазинов","dbo_shopmarker"));
		СтруктураДанных.Добавить(Новый Структура("Порядок,ЭтоДокумент,Набор,Группа,ТолькоОбновление,ИмяОбъекта,ИмяТаблицы",63,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,ЛОЖЬ,"Маркеры товаров","dbo_productmarker"));
		
		Структура.Вставить("Продолжение",СтруктураДанных);
		
		Возврат Структура 
		
	КонецФункции
	
	Процедура ВыгрузитьДанные(Клиент = Ложь,Остатки = Ложь, НаДату = Неопределено) Экспорт
		
		Инициализация(Клиент);
		ЗарегистрироватьСклады();
		ЗарегистрироватьНоменклатуру();
		
		ТекстОшибок = "";
		
		Для Каждого строкаДанных Из СтруктураДанных Цикл
			
			Группа = строкаДанных.Группа;
			Порядок = строкаДанных.Порядок;
			ИмяОбъекта = строкаДанных.ИмяОбъекта;
			ЭтоДокумент = строкаДанных.ЭтоДокумент;
			
			Если (ИмяОбъекта = "Остатки" И НЕ Остатки) Или (НЕ ИмяОбъекта = "Остатки" И Остатки)
				Или	ИмяОбъекта = "" Или (ЭтоДокумент И НЕ Группа) Или строкаДанных.Порядок > 60  Тогда
				Продолжить;
			КонецЕсли;
			
			ннн = 0;
			ИмяТаблицы = строкаДанных.ИмяТаблицы;
			
			Если ИмяОбъекта = "Остатки" Тогда
				Выгрузить_Остатки(ИмяОбъекта,ИмяТаблицы,НаДату);
				Продолжить;
			ИначеЕсли ИмяОбъекта = "" Тогда
				
				Продолжить;
			КонецЕсли;
			
			ЕстьЭтоГруппа = ?(НЕ ЭтоДокумент И Метаданные.Справочники[ИмяОбъекта].Иерархический
			И Метаданные.Справочники[ИмяОбъекта].ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов,Истина,Ложь);
			
			РезультатЗапроса = ПолучитьДанные(строкаДанных,ЕстьЭтоГруппа,УзелПланаОбмена);
			
			Если Не РезультатЗапроса.Пустой() Тогда
				
				втРезультат = РезультатЗапроса.Выгрузить();
				
				dbo = PlanoHeroAttika[ИмяТаблицы];
				
				Если Группа И НЕ ЭтоДокумент Тогда
					МассивСсылок = втРезультат.Скопировать(Новый Структура("ЭтоГруппа",Истина)).ВыгрузитьКолонку("Ссылка");
				Иначе
					МассивСсылок = втРезультат.ВыгрузитьКолонку("Ссылка");
				КонецЕсли;
				
				Если МассивСсылок.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ПланыОбмена.ВыбратьИзменения(УзелПланаОбмена, 1, МассивСсылок);
				
				Если ЭтоДокумент Тогда
					ДокиОтбор = ОтфильтроватьДокументы(МассивСсылок);
					Если ДокиОтбор.Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					Если ИмяОбъекта = "ТК_ОптимальноеКоличествоНоменклатуры" ИЛИ ИмяОбъекта = "ИзменениеАссортимента" ИЛИ ИмяОбъекта = "ЗакрытиеСмены" Тогда
						РезВО.ДобавитьКомментарий("Найдено " + ДокиОтбор.Количество() + " " + ИмяОбъекта, СтрЗР);
					Иначе
						Выгрузить_Документы(ИмяОбъекта,ДокиОтбор,dbo,ИмяТаблицы,ннн);
						РезВО.ДобавитьКомментарий("Выгружено " + ннн + " " + ИмяОбъекта, СтрЗР);
					КонецЕсли;
					ннн = 0;
					Выгрузить_ТоварыДокумента(ИмяОбъекта,ДокиОтбор,dbo,ИмяТаблицы,ннн);
				ИначеЕсли ИмяОбъекта = "КассыККМ" Тогда
				Иначе
					Выгрузить_Справочники(ИмяОбъекта,строкаДанных,МассивСсылок,ЕстьЭтоГруппа,dbo,ИмяТаблицы,ннн);
				КонецЕсли;
				
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелПланаОбмена,1);
				
				РезВО.ДобавитьКомментарий("Выгружено " + ннн + " " + ?(ЭтоДокумент,"Товары ","") + ИмяОбъекта, СтрЗР);
				
			КонецЕсли;
		КонецЦикла;
		
		РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР,НЕ ЗначениеЗаполнено(ТекстОшибок));
		
	КонецПроцедуры
	
	Функция Выгрузить_Документы(ИмяОбъекта,МассивСсылок,dbo,ИмяТаблицы,ннн)
		
		Если ИмяОбъекта = "ЗаказПоставщику" Тогда
			Выгрузить_ЗаказПоставщику(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяОбъекта = "ТК_НазначениеСкидок" Тогда
			Обработать_НазначениеСкидок(МассивСсылок,ИмяОбъекта,ннн);
		Иначе
			
			Для Каждого мСсылка Из МассивСсылок Цикл
				
				Гуид = Строка(мСсылка.УникальныйИдентификатор());
				
				Ссылка = ПолучитьСсылку(dbo, Гуид);
				
				Если Не Ссылка.Пустая() Тогда
					ОбъектНовый = Ссылка.ПолучитьОбъект();
				ИначеЕсли мСсылка.Проведен Тогда
					ОбъектНовый = dbo.СоздатьОбъект();
					ОбъектНовый.id = Гуид;
				Иначе
					Продолжить;
				КонецЕсли;
				
				code = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1 №%2 от %3",
				СокрЛП(мСсылка.ХозОперация),
				СокрЛП(мСсылка.Номер),
				мСсылка.Дата);
				
				ОбъектНовый.code		= code;
				ОбъектНовый.created_at	= мСсылка.Дата;
				ОбъектНовый.staff_id	= ПодставитьСсылку(мСсылка.Автор, "dbo_staff");
				ОбъектНовый.posted		= мСсылка.Проведен;
				ЗаполнитьОстальныеПоля(ОбъектНовый,мСсылка,ИмяТаблицы);
				
				ВсеОК = ЗаписатьНовый(ОбъектНовый,мСсылка,ИмяОбъекта,ИмяТаблицы,ннн);
				
			КонецЦикла;
		КонецЕсли;
		
		
	КонецФункции
	
	Функция Выгрузить_ТоварыДокумента(ИмяОбъекта,МассивСсылок,dbo_Владелец,ИмяТаблицы,ннн)
		
		Если ИмяОбъекта = "ТК_ОптимальноеКоличествоНоменклатуры" ИЛИ ИмяОбъекта = "ИзменениеАссортимента" ИЛИ ИмяОбъекта = "ЗакрытиеСмены" Тогда
			dbo = dbo_Владелец;
		ИначеЕсли ИмяОбъекта = "ИзменениеЦен" Тогда
			dbo = PlanoHeroAttika["dbo_priceproduct"];
		ИначеЕсли ИмяОбъекта = "КлассификаторПродаж" Тогда
			//dbo = PlanoHeroAttika["dbo_promotionproductm2m"];
		Иначе
			dbo = PlanoHeroAttika[ИмяТаблицы+"product"];
		КонецЕсли;
		
		Если ИмяОбъекта = "ЗаказПоставщику" Тогда
			Выгрузить_Товары_ЗаказПоставщику(МассивСсылок,dbo_Владелец,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяОбъекта = "КлассификаторПродаж" Тогда
		ИначеЕсли ИмяОбъекта = "ТК_ОптимальноеКоличествоНоменклатуры" Тогда
			Выгрузить_ОптимальноеКоличество(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн);			
		ИначеЕсли ИмяОбъекта = "ИзменениеАссортимента" Тогда
			Выгрузить_ИзменениеАссортимента(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн);			
		ИначеЕсли ИмяОбъекта = "ЗакрытиеСмены" Тогда
			Выгрузить_ЗакрытиеСмены(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн);			
		Иначе
			
			Запрос = Новый Запрос;
			
			Если ИмяОбъекта = "ИзменениеЦен" Тогда
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	Цены.Регистратор КАК Регистратор,
				|	Цены.Период КАК Период,
				|	Цены.ТипЦен КАК ТипЦен,
				|	Цены.Номенклатура КАК Номенклатура,
				|	МАКСИМУМ(Цены.Характеристика.Сортировка) КАК ХарактеристикаСортировка
				|ПОМЕСТИТЬ ВременнаяТаблица
				|ИЗ
				|	РегистрСведений.Цены КАК Цены
				|ГДЕ
				|	Цены.Регистратор В(&Регистратор)
				|
				|СГРУППИРОВАТЬ ПО
				|	Цены.Период,
				|	Цены.ТипЦен,
				|	Цены.Номенклатура,
				|	Цены.Регистратор
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВременнаяТаблица.Регистратор КАК Регистратор,
				|	ВременнаяТаблица.Период КАК Период,
				|	ВременнаяТаблица.ТипЦен КАК ТипЦен,
				|	ВременнаяТаблица.Номенклатура КАК Номенклатура,
				|	Цены.Характеристика КАК Характеристика,
				|	ВременнаяТаблица.ХарактеристикаСортировка КАК ХарактеристикаСортировка,
				|	Цены.Цена КАК Цена
				|ИЗ
				|	ВременнаяТаблица КАК ВременнаяТаблица
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены КАК Цены
				|		ПО ВременнаяТаблица.Регистратор = Цены.Регистратор
				|			И ВременнаяТаблица.Номенклатура = Цены.Номенклатура
				|			И ВременнаяТаблица.ХарактеристикаСортировка = Цены.Характеристика.Сортировка
				|ИТОГИ ПО
				|	Регистратор";
				
			Иначе	
				
				Если ИмяОбъекта= "ПеремещениеТоваров" ИЛИ ИмяОбъекта= "СписаниеТоваров" Тогда
					ВидДвижения = ВидДвиженияНакопления.Расход;
				Иначе
					ВидДвижения = ВидДвиженияНакопления.Приход;
				КонецЕсли;
				
				Если ИмяОбъекта = "ПеремещениеТоваров" Тогда
					
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	ПартииТоваровКомпании.Регистратор КАК Регистратор,
					|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
					|	ВЫБОР
					|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров
					|			ТОГДА ВЫРАЗИТЬ(ПартииТоваровКомпании.Партия КАК Документ.ПоступлениеТоваров).Контрагент
					|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ВозвратПоставщику
					|			ТОГДА ВЫРАЗИТЬ(ПартииТоваровКомпании.Партия КАК Документ.ВозвратПоставщику).Контрагент
					|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
					|	КОНЕЦ КАК Поставщик,
					|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
					|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма
					|ИЗ
					|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
					|ГДЕ
					|	ПартииТоваровКомпании.Регистратор В(&Регистратор)
					|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
					|
					|СГРУППИРОВАТЬ ПО
					|	ПартииТоваровКомпании.Регистратор,
					|	ПартииТоваровКомпании.Номенклатура,
					|	ВЫБОР
					|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров
					|			ТОГДА ВЫРАЗИТЬ(ПартииТоваровКомпании.Партия КАК Документ.ПоступлениеТоваров).Контрагент
					|		КОГДА ПартииТоваровКомпании.Партия ССЫЛКА Документ.ВозвратПоставщику
					|			ТОГДА ВЫРАЗИТЬ(ПартииТоваровКомпании.Партия КАК Документ.ВозвратПоставщику).Контрагент
					|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
					|	КОНЕЦ
					|ИТОГИ ПО
					|	Регистратор";
					
				Иначе
					
					Запрос.Текст = 
					"ВЫБРАТЬ
					|	ПартииТоваровКомпании.Регистратор КАК Регистратор,
					|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
					|	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
					|	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма
					|ИЗ
					|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
					|ГДЕ
					|	ПартииТоваровКомпании.Регистратор В(&Регистратор)
					|	И ПартииТоваровКомпании.ВидДвижения = &ВидДвижения
					|
					|СГРУППИРОВАТЬ ПО
					|	ПартииТоваровКомпании.Регистратор,
					|	ПартииТоваровКомпании.Номенклатура
					|ИТОГИ ПО
					|	Регистратор";
					
				КонецЕсли;
				
				Запрос.УстановитьПараметр("ВидДвижения", ВидДвижения);
				
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Регистратор", МассивСсылок);
			
			РезультатЗапроса = Запрос.Выполнить();
			//	РезультатЗапроса.Выгрузить()
			ВыборкаРегистратор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаРегистратор.Следующий() Цикл
				Гуид = Строка(ВыборкаРегистратор.Регистратор.УникальныйИдентификатор());
				Ссылка = ПолучитьСсылку(dbo_Владелец, Гуид);
				
				Если Ссылка.Пустая() Тогда
					Продолжить;;
				КонецЕсли;
				
				Набор = dbo.СоздатьНаборЗаписей();
				Набор.Отбор.document_id.Установить(Ссылка);
				
				Выборка = ВыборкаРегистратор.Выбрать();
				
				Пока Выборка.Следующий() Цикл
					Запись				= Набор.Добавить();
					Запись.document_id	= Ссылка;
					Запись.product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");;
					Если ИмяОбъекта = "ИзменениеЦен" Тогда
						Запись.price	= Выборка.Цена;
						Запись.orderkey	= Выборка.ХарактеристикаСортировка;
					Иначе
						Запись.qty				= ?(ИмяОбъекта = "ВозвратПоставщику",-Выборка.Количество,Выборка.Количество);
						Запись.original_price	= Выборка.Сумма / Выборка.Количество;
						Запись.total_price		= ?(ИмяОбъекта = "ВозвратПоставщику",-Выборка.Сумма,Выборка.Сумма);
						
						Если ИмяОбъекта = "ПоступлениеТоваров" Тогда
							ДопПоле = ПолучитьОтсрочкуПлатежа(ВыборкаРегистратор.Регистратор.ДоговорВзаиморасчетов,ВыборкаРегистратор.Регистратор.Дата,ВыборкаРегистратор.Регистратор.РегламентированныйУчет);	
						ИначеЕсли  ИмяОбъекта = "ПеремещениеТоваров" Тогда
							ДопПоле = Выборка.Поставщик;	
						КонецЕсли;
						
						ЗаполнитьОстальныеПоля(Запись,ВыборкаРегистратор.Регистратор,ИмяТаблицы+"product",,ДопПоле);
					КонецЕсли;
					
				КонецЦикла;
				
				ЗаписатьНовый(Набор,ВыборкаРегистратор.Регистратор,ИмяОбъекта,ИмяТаблицы+"product",ннн,Истина);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецФункции
	
	Функция Выгрузить_Справочники(ИмяОбъекта,строкаДанных,МассивСсылок,ЕстьЭтоГруппа,dbo,ИмяТаблицы,ннн)	Экспорт
		
		ТолькоОбновление = строкаДанных.ТолькоОбновление;
		
		Если ИмяОбъекта = "Пользователи" Тогда
			Префикс = "Автор ";
		ИначеЕсли ИмяОбъекта = "ФизическиеЛица" Тогда
			Префикс = "Кассир ";
		Иначе
			Префикс = "";
		КонецЕсли;
		
		Склад = Неопределено;
		
		Для Каждого мСсылка Из МассивСсылок Цикл
			
			Если ЕстьЭтоГруппа И мСсылка.ЭтоГруппа И НЕ строкаДанных.Группа Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИмяОбъекта = "СкладыКомпании" Тогда
				Если мСсылка.ПометкаУдаления
					ИЛИ НЕ (мСсылка.ВидСклада = Справочники.ВидыСкладов.ТорговыйЗал Или мСсылка.ВидСклада = Справочники.ВидыСкладов.ТорговыйПавильон) Тогда
					Продолжить;
				КонецЕсли;
				Гуид = Строка(мСсылка.ТорговаяПлощадка.УникальныйИдентификатор());
			ИначеЕсли ИмяОбъекта = "ТочкиДоставки" Тогда
				Склад = НайтиСклад("ТочкаДоставки",мСсылка);
				Если Не ЗначениеЗаполнено(Склад) Тогда
					Продолжить;
				КонецЕсли;
				Гуид = Строка(Склад.ТорговаяПлощадка.УникальныйИдентификатор());
			ИначеЕсли ИмяОбъекта = "ТорговыеПлощадки" Тогда
				Склад = НайтиСклад("ТорговаяПлощадка",мСсылка);
				Гуид = Строка(мСсылка.УникальныйИдентификатор());
			Иначе
				Гуид = Строка(мСсылка.УникальныйИдентификатор());
			КонецЕсли;
			
			Ссылка = ПолучитьСсылку(dbo, Гуид);
			
			Если Не Ссылка.Пустая() Тогда
				ОбъектНовый = Ссылка.ПолучитьОбъект();
			ИначеЕсли СоздаватьОбъекты И НЕ ТолькоОбновление Тогда
				ОбъектНовый = dbo.СоздатьОбъект();
				ОбъектНовый.id = Гуид;
			Иначе
				Продолжить;
			КонецЕсли;
			
			ОбъектНовый.name = Префикс + СокрЛП(мСсылка.Наименование);
			
			ЗаполнитьОстальныеПоля(ОбъектНовый,мСсылка,ИмяТаблицы,Склад);
			
			ЗаписатьНовый(ОбъектНовый,мСсылка,ИмяОбъекта,ИмяТаблицы,ннн);
			
			Если ИмяОбъекта = "СкладыКомпании" ИЛИ ИмяОбъекта = "ТорговыеПлощадки" Тогда
				хз = Обработать_СвязьМагазиновИМеток(ОбъектНовый.Ссылка,мСсылка,Склад);
			КонецЕсли;
			Если ИмяОбъекта = "СкладыКомпании" Тогда
				хз = Обработать_СвязьСкладПлощадка(Гуид,мСсылка);
				хз = Обработать_СвязьСкладТипЦен(мСсылка);
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецФункции
	
	Процедура ВыгрузитьНачальныеДанные(строкаДанных) Экспорт
		
		Инициализация(Истина);
		
		ТекстОшибок = "";
		
		ИмяОбъекта = строкаДанных.ИмяОбъекта;
		ИмяТаблицы = строкаДанных.ИмяТаблицы;
		dbo = PlanoHeroAttika[ИмяТаблицы];
		ннн = 0;
		
		Если ИмяТаблицы = "dbo_warehouse2sarea" Тогда
			Выгрузить_СвязьСкладПлощадка(dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяТаблицы = "dbo_shopmarkerm2m" Тогда
			Выгрузить_СвязМагазиновИМеток(dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяТаблицы = "dbo_priceshoptype" Тогда
			Выгрузить_СвязьСкладТипЦен(dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяТаблицы = "dbo_recommendedfaces" Тогда
			Выгрузить_КоличествоФейсов(dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		ИначеЕсли ИмяТаблицы = "dbo_assortment" Тогда
			Выгрузить_Ассортимент(dbo,ИмяОбъекта,ИмяТаблицы,ннн);
		КонецЕсли;
		
		РезВО.ДобавитьКомментарий("Выгружено " + ннн + " " + ИмяОбъекта, СтрЗР);
		
		РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР,НЕ ЗначениеЗаполнено(ТекстОшибок));
		
	КонецПроцедуры	
	
	
	#КонецОбласти
	
	
	#Область ВспомогательныеПроцедурыИФункции
	
	Процедура ЗарегистрироватьНоменклатуру()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	dbo_product.id КАК id,
		|	dbo_product.name КАК name,
		|	dbo_product.article КАК article
		|ИЗ
		|	ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_product КАК dbo_product
		|ГДЕ
		|	dbo_product.deleted";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Артикула = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("article");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул В(&Артикул)";
		
		Запрос.УстановитьПараметр("Артикул", Артикула);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
		РезВО.ДобавитьКомментарий("Зарегистрировано " + ВыборкаДетальныеЗаписи.Количество() + " номенклатуры", СтрЗР);
		
	КонецПроцедуры	
	
	Процедура ЗарегистрироватьСклады()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	dbo_shop.id КАК id,
		|	dbo_shop.name КАК name
		|ИЗ
		|	ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_shop КАК dbo_shop
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_warehouse2sarea КАК dbo_warehouse2sarea
		|		ПО dbo_shop.id = dbo_warehouse2sarea.sarea_id
		|ГДЕ
		|	dbo_warehouse2sarea.warehouse_id ЕСТЬ NULL";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ПлощадкаСсылка = Справочники.ТорговыеПлощадки.ПолучитьСсылку(Новый УникальныйИдентификатор(ВыборкаДетальныеЗаписи.id));
			СкладСсылка = НайтиСклад("ТорговаяПлощадка",ПлощадкаСсылка);
			
			Если ЗначениеЗаполнено(СкладСсылка) Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, СкладСсылка);
			КонецЕсли;
		КонецЦикла;
		
		РезВО.ДобавитьКомментарий("Зарегистрировано " + ВыборкаДетальныеЗаписи.Количество() + " складов", СтрЗР);
		
	КонецПроцедуры	
	
	Процедура ЗаполнитьАктивныеСклады()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	dbo_shop.Ссылка КАК Ссылка,
		|	dbo_shop.name КАК name,
		|	dbo_shop.id КАК shop_id,
		|	ЕСТЬNULL(dbo_warehouse2sarea.warehouse_id, ""00000000-0000-0000-0000-000000000000"") КАК warehouse_id,
		|	ЕСТЬNULL(dbo_warehouse2sarea.sarea_id, ""00000000-0000-0000-0000-000000000000"") КАК sarea_id,
		|	dbo_shop.id = dbo_warehouse2sarea.sarea_id КАК Поле
		|ИЗ
		|	ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_shop КАК dbo_shop
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_warehouse2sarea КАК dbo_warehouse2sarea
		|		ПО dbo_shop.id = dbo_warehouse2sarea.sarea_id
		|
		|УПОРЯДОЧИТЬ ПО
		|	name";
		
		РезультатЗапроса = Запрос.Выполнить();
		ТЗ = РезультатЗапроса.Выгрузить();
		ТЗ.Колонки.Добавить("СкладShop",Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
		
		Для Каждого строка Из ТЗ Цикл
			строка.СкладShop = Справочники.СкладыКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор(строка.warehouse_id));
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.Ссылка КАК вдСсылка,
		|	ТЗ.name КАК name,
		|	ТЗ.shop_id КАК shop_id,
		|	ТЗ.СкладShop КАК СкладShop
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК Склад,
		|	ДополнительныеСведения.Значение КАК Активный
		|ПОМЕСТИТЬ АктивныеСклады
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка,
		|	ИСТИНА
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.вдСсылка КАК Shop,
		|	СкладыКомпании.Ссылка КАК Склад,
		|	АктивныеСклады.Активный КАК Активный
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
		|		ПО ВТ.СкладShop = СкладыКомпании.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ АктивныеСклады КАК АктивныеСклады
		|		ПО (СкладыКомпании.Ссылка = АктивныеСклады.Склад)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Shop
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("ТЗ", ТЗ);
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		АктивныеСклады.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецПроцедуры
	
	Функция ПолучитьДанные(Данные,ЕстьЭтоГруппа,УзелПланаОбмена)
		
		ДопПоля = ?(ЕстьЭтоГруппа,"ОбъектИзменения.Ссылка.ЭтоГруппа КАК ЭтоГруппа,","Ложь КАК ЭтоГруппа,");
		
		ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"ВЫБРАТЬ  
		|	%3
		|	ОбъектИзменения.Ссылка,
		|	ОбъектИзменения.НомерСообщения КАК НомерСообщения
		|ИЗ
		|	%1.%2.Изменения КАК ОбъектИзменения
		|ГДЕ
		|	ОбъектИзменения.Узел = &Узел
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСообщения УБЫВ",
		?(Данные.ЭтоДокумент,"Документ","Справочник"),
		Данные.ИмяОбъекта,
		ДопПоля);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
		
		РезультатЗапроса = Запрос.Выполнить();
		тзДанные = РезультатЗапроса.Выгрузить();
		
		Возврат РезультатЗапроса;
		
	КонецФункции
	
	Функция ОтфильтроватьДокументы(МассивСсылок)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК СкладыКомпании,
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
		|ПОМЕСТИТЬ втПлощадки
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&ПодразделениеКомпании)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка,
		|	СкладыКомпании.ТорговаяПлощадка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПлощадки.СкладыКомпании.ТипЦенРозничнойТорговли КАК ТипЦен
		|ПОМЕСТИТЬ втТипыЦен
		|ИЗ
		|	втПлощадки КАК втПлощадки
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	СкладыКомпанииДоступныеЦены.ТипЦен
		|ИЗ
		|	Справочник.СкладыКомпании.ДоступныеЦены КАК СкладыКомпанииДоступныеЦены
		|ГДЕ
		|	СкладыКомпанииДоступныеЦены.Ссылка В
		|			(ВЫБРАТЬ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&Ссылка)
		|	И ЗаказПоставщикуТовары.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ЗаказПоставщикуТовары.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратПоставщику.Ссылка
		|ИЗ
		|	Документ.ВозвратПоставщику КАК ВозвратПоставщику
		|ГДЕ
		|	ВозвратПоставщику.Ссылка В(&Ссылка)
		|	И ВозвратПоставщику.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ВозвратПоставщику.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваров.Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка В(&Ссылка)
		|	И ПоступлениеТоваров.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ПоступлениеТоваров.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПеремещениеТоваров.Ссылка
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
		|ГДЕ
		|	ПеремещениеТоваров.Ссылка В(&Ссылка)
		|	И ПеремещениеТоваров.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ПеремещениеТоваров.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|	И ПеремещениеТоваров.СкладКомпанииПолучатель В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|	И ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваров)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Инвентаризация.Ссылка
		|ИЗ
		|	Документ.Инвентаризация КАК Инвентаризация
		|ГДЕ
		|	Инвентаризация.Ссылка В(&Ссылка)
		|	И Инвентаризация.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И Инвентаризация.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СписаниеТоваров.Ссылка
		|ИЗ
		|	Документ.СписаниеТоваров КАК СписаниеТоваров
		|ГДЕ
		|	СписаниеТоваров.Ссылка В(&Ссылка)
		|	И СписаниеТоваров.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И СписаниеТоваров.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИзменениеЦен.Ссылка
		|ИЗ
		|	Документ.ИзменениеЦен КАК ИзменениеЦен
		|ГДЕ
		|	ИзменениеЦен.Ссылка В(&Ссылка)
		|	И ИзменениеЦен.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ИзменениеЦен.ТипЦен В
		|			(ВЫБРАТЬ
		|				втТипыЦен.ТипЦен КАК ТипЦен
		|			ИЗ
		|				втТипыЦен КАК втТипыЦен)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзменениеАссортиментаТорговыеПлощадки.Ссылка
		|ИЗ
		|	Документ.ИзменениеАссортимента.ТорговыеПлощадки КАК ИзменениеАссортиментаТорговыеПлощадки
		|ГДЕ
		|	ИзменениеАссортиментаТорговыеПлощадки.Ссылка В(&Ссылка)
		|	И ИзменениеАссортиментаТорговыеПлощадки.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ИзменениеАссортиментаТорговыеПлощадки.ТорговаяПлощадка В
		|			(ВЫБРАТЬ
		|				втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка
		|ИЗ
		|	Документ.ТК_ОптимальноеКоличествоНоменклатуры.Товары КАК ТК_ОптимальноеКоличествоНоменклатурыТовары
		|ГДЕ
		|	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка В(&Ссылка)
		|	И ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ТК_ОптимальноеКоличествоНоменклатурыТовары.ТорговаяПлощадка В
		|			(ВЫБРАТЬ
		|				втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗакрытиеСмены.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеСмены КАК ЗакрытиеСмены
		|ГДЕ
		|	ЗакрытиеСмены.Ссылка В(&Ссылка)
		|	И ЗакрытиеСмены.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ЗакрытиеСмены.СкладКомпании В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				втПлощадки.СкладыКомпании КАК СкладыКомпании
		|			ИЗ
		|				втПлощадки КАК втПлощадки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТК_НазначениеСкидокТорговыеПлощадки.Ссылка
		|ИЗ
		|	Документ.ТК_НазначениеСкидок.ТорговыеПлощадки КАК ТК_НазначениеСкидокТорговыеПлощадки
		|ГДЕ
		|	ТК_НазначениеСкидокТорговыеПлощадки.Ссылка В(&Ссылка)
		|	И ТК_НазначениеСкидокТорговыеПлощадки.Ссылка.ПодразделениеКомпании В ИЕРАРХИИ(&ПодразделениеКомпании)
		|	И ТК_НазначениеСкидокТорговыеПлощадки.ТорговаяПлощадка В
		|			(ВЫБРАТЬ
		|				втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|			ИЗ
		|				втПлощадки КАК втПлощадки)";
		
		Запрос.УстановитьПараметр("Ссылка", МассивСсылок);
		Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецФункции
	
	Функция ПолучитьСсылку(dbo, Гуид, Поле = "id")
		Ссылка = dbo.НайтиПоПолю(Поле,Гуид);
		Возврат Ссылка;
	КонецФункции
	
	Функция ПодставитьСсылку(мСсылка, ИмяТаблицы, Родитель = Ложь, Склад = Неопределено)
		
		ПоСоответствию =  ИмяТаблицы = "dbo_shop" Или ИмяТаблицы = "dbo_product" Или ИмяТаблицы = "dbo_supplier" Или ИмяТаблицы = "dbo_assortmenttype";
		
		Если мСсылка <> 0 И НЕ ЗначениеЗаполнено(мСсылка) Тогда
			Возврат Null;
		КонецЕсли;
		
		Если ПоСоответствию Тогда
			Ссылка = СтруктураСоответствий[ИмяТаблицы].Получить(мСсылка);
			Если НЕ Ссылка = Неопределено Тогда				
				Возврат Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		Если Строка(ТипЗнч(мСсылка)) = "Пользователь" Тогда
			Префикс = "Автор ";
		ИначеЕсли Строка(ТипЗнч(мСсылка)) = "Физическое лицо" Тогда
			Префикс = "Кассир ";
		Иначе
			Префикс = "";
		КонецЕсли;
		
		Если ИмяТаблицы = "dbo_purchase" Тогда
			ГуидДок = Строка(мСсылка.УникальныйИдентификатор());
			ГуидСклад = Строка(Склад.ТорговаяПлощадка.УникальныйИдентификатор());
			Гуид = ГуидДок + " # " + ГуидСклад;
		Иначе
			Гуид = ?(Родитель,Строка(мСсылка),Строка(мСсылка.УникальныйИдентификатор()));
		КонецЕсли;
		
		dbo = PlanoHeroAttika[ИмяТаблицы];
		
		Ссылка = ПолучитьСсылку(dbo, Гуид);
		
		Если Ссылка.Пустая() Тогда
			ОбъектНовый = dbo.СоздатьОбъект();
			ОбъектНовый.id = Гуид;
			ОбъектНовый.name = Префикс + СокрЛП(?(Родитель,мСсылка,мСсылка.Наименование));
			хз = ЗаполнитьОстальныеПоля(ОбъектНовый,мСсылка,ИмяТаблицы,Склад);
			Попытка
				ОбъектНовый.Записать();
				Если ПоСоответствию Тогда
					СтруктураСоответствий[ИмяТаблицы].Вставить(мСсылка,ОбъектНовый.Ссылка);
				КонецЕсли;
				Возврат ОбъектНовый.Ссылка;
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
				Сообщить(ОписаниеОшибки);
				РезВО.СообщитьОбОшибке("Ошибка " + Строка(ТипЗнч(мСсылка)) + " (" + ИмяТаблицы + ") " + Символы.ПС + ОписаниеОшибки, СтрЗР);
			КонецПопытки;
		Иначе
			Если ПоСоответствию Тогда
				СтруктураСоответствий[ИмяТаблицы].Вставить(мСсылка,Ссылка);
			КонецЕсли;
			Возврат Ссылка;
		КонецЕсли;
	КонецФункции
	
	Функция ЗаписатьНовый(Новое,мСсылка="",ИмяОбъекта,ИмяТаблицы,ннн,Сумма=Истина)
		ВсеОК = Ложь;
		Попытка
			Новое.Записать();
			Если НЕ мСсылка = "" Тогда
				ннн = 1 + ннн;
			ИначеЕсли Не Сумма Тогда
				ннн = Новое.Количество();
			ИначеЕсли Сумма Тогда
				ннн = ннн + Новое.Количество();
			КонецЕсли;
			ВсеОК = Истина;
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
			ТекстОшибок = ТекстОшибок + ?(ЗначениеЗаполнено(ТекстОшибок),Символы.ПС,"") + ОписаниеОшибки;
			РезВО.СообщитьОбОшибке("Ошибка " + ИмяОбъекта + " (" + ИмяТаблицы + ") " + мСсылка + Символы.ПС + ОписаниеОшибки, СтрЗР);
			Если НЕ мСсылка = "" Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, мСсылка);
			КонецЕсли;
			
		КонецПопытки;
		Возврат ВсеОК;
	КонецФункции	
	
	
	Функция ЗаполнитьОстальныеПоля(ОбъектНовый,мСсылка,ИмяТаблицы, Склад = Неопределено,ДопПоле = Неопределено)
		Если ТипЗнч(мСсылка) = Тип("Число") Тогда
			Если ИмяТаблицы = "dbo_assortmenttype" Тогда
				Обработать_ТипыАссортимента(ОбъектНовый,мСсылка);	
			КонецЕсли;
		ИначеЕсли ТипЗнч(мСсылка) = Тип("Строка") Тогда
			Если ИмяТаблицы = "dbo_shopmarker" Тогда
				Обработать_МеткиМагазинов(ОбъектНовый,мСсылка);
			ИначеЕсли ИмяТаблицы = "dbo_category" Тогда
				Обработать_Номенклатура_Группа(ОбъектНовый,мСсылка);
			ИначеЕсли ИмяТаблицы = "dbo_supplier" Тогда
				Обработать_Контрагенты(ОбъектНовый,мСсылка);
			КонецЕсли;
		ИначеЕсли мСсылка.Метаданные().Имя = "СкладыКомпании" Тогда
			Обработать_СкладыКомпании(ОбъектНовый,мСсылка);
		ИначеЕсли мСсылка.Метаданные().Имя = "ТочкиДоставки" Тогда
			Обработать_ТочкиДоставки(ОбъектНовый,Склад);
		ИначеЕсли ИмяТаблицы = "dbo_terminal" Тогда
			Обработать_КассыККМ(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_category" Тогда
			Обработать_Номенклатура_Группа(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_product" Тогда
			Обработать_Номенклатура(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_shop" Тогда
			Обработать_ТорговыеПлощадки(ОбъектНовый,мСсылка,Склад);
		ИначеЕсли ИмяТаблицы = "dbo_shopgroup" Тогда
			Обработать_ПодразделенияКомпании(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_brand" Тогда
			Обработать_ТоварныеМарки_Группа(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_pricetype" Тогда
			Обработать_ТипыЦен(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_shopmarker" Тогда
			Обработать_МеткиМагазинов(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_supplier" Тогда
			Обработать_Контрагенты(ОбъектНовый,мСсылка);
			////	Документы	////
		ИначеЕсли ИмяТаблицы = "dbo_supplierrefund" Тогда
			Обработать_ВозвратПоставщику(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_receive" Тогда
			Обработать_ПоступлениеТоваров(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_relocate" Тогда
			Обработать_ПеремещениеТоваров(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_incoming" Тогда
			Обработать_Инвентаризация(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_loss" Тогда
			Обработать_СписаниеТоваров(ОбъектНовый,мСсылка);
		ИначеЕсли ИмяТаблицы = "dbo_pricedocument" Тогда
			Обработать_ИзменениеЦен(ОбъектНовый,мСсылка);
			//ИначеЕсли ИмяТаблицы = "dbo_promotion" Тогда
			//	Обработать_НазначениеСкидок(ОбъектНовый,мСсылка);
			//ИначеЕсли ИмяТаблицы = "dbo_assortment" Тогда
			//	Обработать_ИзменениеАссортимента(ОбъектНовый,мСсылка);
			////	Товары	////
		ИначеЕсли ИмяТаблицы = "dbo_receiveproduct" Тогда
			Обработать_Товары_ПоступлениеТоваров(ОбъектНовый,мСсылка,ИмяТаблицы,ДопПоле);
		ИначеЕсли ИмяТаблицы = "dbo_relocateproduct" Тогда
			Обработать_Товары_ПеремещениеТоваров(ОбъектНовый,мСсылка,ИмяТаблицы,ДопПоле);
			//ИначеЕсли ИмяТаблицы = "dbo_promotionproductm2m" Тогда
			//	Обработать_Товары_НазначениеСкидок(ОбъектНовый,мСсылка,ИмяТаблицы,ДопПоле);
		КонецЕсли;
	КонецФункции
	
	Функция НайтиСклад(Реквизит,Ссылка)
		
		Запрос = Новый Запрос;
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкладыКомпании.Ссылка КАК Ссылка,
		|	СкладыКомпании.ТочкаДоставки.Наименование КАК Адрес
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.%1 = &СсылкаРеквизит
		|	И НЕ СкладыКомпании.НеАктивен
		|	И (СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
		|			ИЛИ СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка УБЫВ",
		Реквизит);
		
		Запрос.УстановитьПараметр("СсылкаРеквизит", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат Null;
		Иначе
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			Возврат ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;
		
	КонецФункции
	
	Функция ПолучитьТипыАссортимента()
		
		ТипыАссортимента = Новый СписокЗначений;
		ТипыАссортимента.Добавить(0,"Выведен",Ложь);
		ТипыАссортимента.Добавить(1,"Введен",Истина);
		ТипыАссортимента.Добавить(2,"Распродажа",Истина);
		ТипыАссортимента.Добавить(9,"Вне матрицы",Ложь);
		
		Возврат ТипыАссортимента;
		
	КонецФункции
	
	Функция	ПолучитьМеткиМагазинов()
		
		Метки = Новый Массив;
		Метки.Добавить(Новый Структура("Наименование,Реквизит,МетаданныеИмя","Вид","ВидПлощадки","ЗначенияСвойствОбъектов"));
		Метки.Добавить(Новый Структура("Наименование,Реквизит,МетаданныеИмя","Город","Город","ТК_Города"));
		Метки.Добавить(Новый Структура("Наименование,Реквизит,МетаданныеИмя","Матрица","АссортиментнаяМатрица","АссортиментнаяМатрица"));
		Метки.Добавить(Новый Структура("Наименование,Реквизит,МетаданныеИмя","Район","Районы","Районы"));
		
		Возврат Метки;
		
	КонецФункции
	
	Функция ПолучитьРодителейМетокМагазинов(МетаданныеИмя)
		
		Метки = ПолучитьМеткиМагазинов();
		Для Каждого Метка Из Метки Цикл
			Если Метка.МетаданныеИмя = МетаданныеИмя Тогда
				Возврат Метка.Наименование;
			КонецЕсли;
		КонецЦикла;
		
	КонецФункции
	
	Функция ШтрихкодыНоменклатуры(Номенклатура)
		
		ВозвращаемоеЗначение = "";
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура";
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ВозвращаемоеЗначение = ВозвращаемоеЗначение + ВыборкаДетальныеЗаписи.Штрихкод + ",";
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ВозвращаемоеЗначение) Тогда
			ВозвращаемоеЗначение = Лев(ВозвращаемоеЗначение,СтрДлина(ВозвращаемоеЗначение)-1);
		КонецЕсли;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецФункции
	
	Функция ПолучитьОтсрочкуПлатежа(Договор,Дата,Регламент)
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СрокиОплатПоДоговорамСрезПоследних.ТипОплаты КАК ТипОплаты,
		|	СрокиОплатПоДоговорамСрезПоследних.Дней КАК Дней,
		|	СрокиОплатПоДоговорамСрезПоследних.Дней2 КАК Дней2
		|ИЗ
		|	РегистрСведений.СрокиОплатПоДоговорам.СрезПоследних(&НаДату, ДоговорКонтрагента = &Договор) КАК СрокиОплатПоДоговорамСрезПоследних";
		
		Запрос.УстановитьПараметр("Договор", Договор);
		Запрос.УстановитьПараметр("НаДату", Дата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Отсрочка = ВыборкаДетальныеЗаписи[Формат(Регламент,"БЛ=Дней2; БИ=Дней")];
		Иначе
			Отсрочка = 0;
		КонецЕсли;
		
		Возврат Отсрочка;
		
	КонецФункции
	
	
	
	#КонецОбласти
	
	
	#Область Обработать_Данные
	
	Функция Обработать_КассыККМ(ОбъектНовый,мСсылка)
		ЗначениеЗаполненоОбъект = ЗначениеЗаполнено(мСсылка.Объект);
		ОбъектНовый.shop_id	= ПодставитьСсылку(?(ЗначениеЗаполненоОбъект,мСсылка.Объект,"Магазин не заполнен"), "dbo_shop", НЕ ЗначениеЗаполненоОбъект);
		Возврат Истина;
	КонецФункции
	
	Функция Обработать_ТипыЦен(ОбъектНовый,мСсылка)
		ОбъектНовый.is_regular = НЕ мСсылка.ТипЦенЗакупки;
		Возврат Истина;
	КонецФункции
	
	Функция Обработать_ТорговыеПлощадки(ОбъектНовый,мСсылка,Склад)
		
		Если ЗначениеЗаполнено(Склад) Тогда
			ОбъектНовый.name	= СокрЛП(Склад.Наименование);
			ОбъектНовый.address	= СокрЛП(Склад.ТочкаДоставки.Наименование);
			//ОбъектНовый.opened_at	= Склад.Ссылка;
			//ОбъектНовый.latitude	= Склад.Ссылка;
			//ОбъектНовый.longitude	= Склад.Ссылка;
			ТорговаяПлощадь = УправлениеСвойствами.ЗначениеСвойства(Склад, "ТорговаяПлощадь");
			Если ЗначениеЗаполнено(ТорговаяПлощадь) Тогда
				ОбъектНовый.area = ТорговаяПлощадь;
			КонецЕсли;
		КонецЕсли;
		
		ОбъектНовый.group_id = ПодставитьСсылку(мСсылка.ПодразделениеКомпании,"dbo_shopgroup");
		
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_ПодразделенияКомпании(ОбъектНовый,мСсылка)
		ОбъектНовый.parent_id = ПодставитьСсылку(мСсылка.Родитель,"dbo_shopgroup");
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_ТоварныеМарки_Группа(ОбъектНовый,мСсылка)
		group_id = ПодставитьСсылку(мСсылка.Родитель,"dbo_producer");
		//group_id = ПодставитьСсылку(?(ЗначениеЗаполнено(мСсылка.Родитель),мСсылка.Родитель,"Нет Производителя"),"dbo_producer");
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_СкладыКомпании(ОбъектНовый,мСсылка)
		ОбъектНовый.group_id	= ПодставитьСсылку(мСсылка.ТорговаяПлощадка.ПодразделениеКомпании,"dbo_shopgroup");
		ОбъектНовый.address		= СокрЛП(мСсылка.ТочкаДоставки.Наименование);
		//ОбъектНовый.opened_at	= мСсылка.Ссылка;
		//ОбъектНовый.latitude	= мСсылка.Ссылка;
		//ОбъектНовый.longitude	= мСсылка.Ссылка;
		ТорговаяПлощадь = УправлениеСвойствами.ЗначениеСвойства(мСсылка, "ТорговаяПлощадь");
		Если ЗначениеЗаполнено(ТорговаяПлощадь) Тогда
			ОбъектНовый.area = ТорговаяПлощадь;
		КонецЕсли;
		
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_ТочкиДоставки(ОбъектНовый,Склад)
		ОбъектНовый.name	= СокрЛП(Склад.Наименование);
		ОбъектНовый.address	= СокрЛП(Склад.ТочкаДоставки.Наименование);
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_Номенклатура(ОбъектНовый,мСсылка)
		
		ЗначениеЗаполненоРодитель = ЗначениеЗаполнено(мСсылка.Родитель);
		
		ОбъектНовый.article		= мСсылка.Артикул;
		ОбъектНовый.barcode		= ШтрихкодыНоменклатуры(мСсылка);
		ОбъектНовый.category_id	= ПодставитьСсылку(?(ЗначениеЗаполненоРодитель,мСсылка.Родитель,"Корень справочника"), "dbo_category", НЕ ЗначениеЗаполненоРодитель);
		ОбъектНовый.brand_id	= ПодставитьСсылку(мСсылка.ТоварнаяМарка,"dbo_brand");
		ОбъектНовый.producer_id	= ПодставитьСсылку(мСсылка.ТоварнаяМарка.Родитель,"dbo_producer");
		ОбъектНовый.deleted		= Ложь;
		ОбъектНовый.width		= мСсылка.Ширина/100;
		ОбъектНовый.height		= мСсылка.Высота/100;
		ОбъектНовый.depth		= мСсылка.Глубина/100;
		ОбъектНовый.weight		= мСсылка.ОсновнаяЕдиницаИзмерения.Вес;
		//ОбъектНовый.height_compression_coef = ;
		//ОбъектНовый.width_compression_coef = ;
		//ОбъектНовый.depth_compression_coef = ;
		ОбъектНовый.is_loose	= мСсылка.ОсновнаяЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код = "0301";
		//ОбъектНовый.image = ;
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_Номенклатура_Группа(ОбъектНовый,мСсылка)
		
		Если ТипЗнч(мСсылка) = Тип("Строка") Тогда
			ОбъектНовый.parent_id = Null;
		Иначе
			ОбъектНовый.parent_id = ПодставитьСсылку(мСсылка.Родитель,"dbo_category");
		КонецЕсли;
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_МеткиМагазинов(ОбъектНовый,мСсылка)
		
		Если ТипЗнч(мСсылка) = Тип("Строка") Тогда
			ОбъектНовый.parent_id = Null;
		Иначе
			МетаданныеИмя = мСсылка.Метаданные().Имя;
			Если МетаданныеИмя = "ЗначенияСвойствОбъектов" Тогда
				Имя = мСсылка.Владелец.Наименование;
			Иначе
				Имя = ПолучитьРодителейМетокМагазинов(МетаданныеИмя);
			КонецЕсли;			
			
			ОбъектНовый.parent_id = ПодставитьСсылку(СокрЛП(Имя), "dbo_shopmarker", Истина);
			
		КонецЕсли;
		
		Возврат Истина;
	КонецФункции
	
	Функция Обработать_СвязьМагазиновИМеток(СсылкаМагазин,мСсылка,сСклад)
		
		Метки = ПолучитьМеткиМагазинов();
		
		dbo = PlanoHeroAttika.dbo_shopmarkerm2m;
		
		Если сСклад = Неопределено Тогда
			Склад = мСсылка;	
			Площадка = мСсылка.ТорговаяПлощадка;	
		Иначе
			Склад = сСклад;	
			Площадка = мСсылка;	
		КонецЕсли;
		
		Набор = dbo.СоздатьНаборЗаписей();
		Набор.Отбор.shop_id.Установить(СсылкаМагазин);
		
		Для Каждого стрМетка Из Метки Цикл
			
			Если стрМетка.Наименование = "Вид" Тогда
				Метка = УправлениеСвойствами.ЗначениеСвойства(Площадка, стрМетка.Реквизит);
			ИначеЕсли стрМетка.Наименование = "Город" Тогда
				Метка = Склад.ТочкаДоставки[стрМетка.Реквизит];
			Иначе
				Метка = Склад[стрМетка.Реквизит];
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Метка) Тогда
				Продолжить;
			КонецЕсли;
			
			СсылкаМетка = ПодставитьСсылку(Метка, "dbo_shopmarker", Ложь);
			
			Запись = Набор.Добавить();
			Запись.shop_id = СсылкаМагазин;
			Запись.marker_id = СсылкаМетка;
			
		КонецЦикла;
		
		Попытка
			Набор.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
			ТекстОшибок = ТекстОшибок + ?(ЗначениеЗаполнено(ТекстОшибок),Символы.ПС,"") + ОписаниеОшибки;
			РезВО.СообщитьОбОшибке("Ошибка Связь магазинов и меток (dbo_shopmarkerm2m) " + Символы.ПС + ОписаниеОшибки, СтрЗР);
		КонецПопытки;
		
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_Контрагенты(ОбъектНовый,мСсылка)
		
		Если ТипЗнч(мСсылка) = Тип("Строка") Тогда
			ОбъектНовый.code = "- - - -";
		Иначе
			ОбъектНовый.code = мСсылка.КодПоЕДРПОУ;
			//Объект.phone	= мСсылка.Наименование;
			//Объект.address = мСсылка.Наименование;
		КонецЕсли;
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_ТипыАссортимента(ОбъектНовый,мСсылка)
		ТипыАссортимента	= ПолучитьТипыАссортимента();
		ТипАссортимента		= ТипыАссортимента.НайтиПоЗначению(мСсылка);
		
		ОбъектНовый.name	= ТипАссортимента.Представление;
		ОбъектНовый.is_active = ТипАссортимента.Пометка;
		
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Обработать_СвязьСкладПлощадка(ГуидПлощадка,мСсылка)
		
		dbo = PlanoHeroAttika.dbo_warehouse2sarea;
		
		ГуидСклад = СокрЛП(мСсылка.УникальныйИдентификатор());
		
		Запись = dbo.СоздатьМенеджерЗаписи();
		
		Запись.warehouse_id	= ГуидСклад;
		Запись.sarea_id		= ГуидПлощадка;
		
		Попытка
			Запись.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
			ТекстОшибок = ТекстОшибок + ?(ЗначениеЗаполнено(ТекстОшибок),Символы.ПС,"") + ОписаниеОшибки;
			РезВО.СообщитьОбОшибке("Ошибка Связь Склад Площадка (dbo_warehouse2sarea) " + Символы.ПС + ОписаниеОшибки, СтрЗР);
		КонецПопытки;
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_СвязьСкладТипЦен(мСсылка)
		
		dbo = PlanoHeroAttika.dbo_priceshoptype;
		
		СсылкаСклад = ПодставитьСсылку(мСсылка.ТорговаяПлощадка, "dbo_shop", ,мСсылка);
		
		Набор = dbo.СоздатьНаборЗаписей();
		Набор.Отбор.shop_id.Установить(СсылкаСклад);
		
		Запись			= Набор.Добавить();
		Запись.shop_id	= СсылкаСклад;
		Запись.type_id	= ПодставитьСсылку(мСсылка.ТипЦенРозничнойТорговли, "dbo_pricetype");
		Запись.priority	= 0;
		
		Для Каждого строка Из мСсылка.ДоступныеЦены Цикл
			
			Запись			= Набор.Добавить();
			Запись.shop_id	= СсылкаСклад;
			Запись.type_id	= ПодставитьСсылку(строка.ТипЦен, "dbo_pricetype");
			Запись.priority	= строка.НомерСтроки;
			
		КонецЦикла;
		
		Попытка
			Набор.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
			ТекстОшибок = ТекстОшибок + ?(ЗначениеЗаполнено(ТекстОшибок),Символы.ПС,"") + ОписаниеОшибки;
			РезВО.СообщитьОбОшибке("Ошибка Связь Склад ТипЦен (dbo_priceshoptype) " + Символы.ПС
			+ мСсылка + " (" +мСсылка.ТипЦенРозничнойТорговли + ")" + Символы.ПС+ОписаниеОшибки, СтрЗР);
		КонецПопытки;
		
		Возврат Истина;
		
	КонецФункции
	
	////	Документы	////
	
	Функция Почистить_ЗаказПоставщику(МассивСсылок,ИмяОбъекта,ИмяТаблицы,ннн)
		
		dbo = PlanoHeroAttika[ИмяТаблицы+"product"];
		
		МассивГуид = Новый Массив;
		
		Для Каждого Заказ Из МассивСсылок Цикл
			МассивГуид.Добавить(Строка(Заказ.УникальныйИдентификатор()));
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	dbo_purchase.Ссылка КАК Ссылка,
		|	dbo_purchase.shop_id.id КАК shop_id,
		|	dbo_purchase.doc_id КАК doc_id
		|ИЗ
		|	ВнешнийИсточникДанных.PlanoHeroAttika.Таблица.dbo_purchase КАК dbo_purchase
		|ГДЕ
		|	dbo_purchase.doc_id В(&doc_id)";
		
		Запрос.УстановитьПараметр("doc_id", МассивГуид);
		
		ТЗ_dbo = Запрос.Выполнить().Выгрузить();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказПоставщикуТовары.Ссылка КАК Заказ,
		|	ЗаказПоставщикуТовары.СкладКомпании КАК Склад
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&МассивСсылок)";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		ТЗ.Колонки.Добавить("ГуидЗаказа",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(36)));
		ТЗ.Колонки.Добавить("ГуидСклада",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(36)));
		
		Для Каждого строка Из ТЗ Цикл
			строка.ГуидЗаказа = Строка(строка.Заказ.УникальныйИдентификатор());
			строка.ГуидСклада = Строка(строка.Склад.ТорговаяПлощадка.УникальныйИдентификатор());
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ_dbo.Ссылка КАК Ссылка,
		|	ТЗ_dbo.shop_id КАК shop_id,
		|	ТЗ_dbo.doc_id КАК doc_id
		|ПОМЕСТИТЬ ВТ_dbo
		|ИЗ
		|	&ТЗ_dbo КАК ТЗ_dbo
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЗ.Заказ КАК Заказ,
		|	ТЗ.Склад КАК Склад,
		|	ТЗ.ГуидЗаказа КАК ГуидЗаказа,
		|	ТЗ.ГуидСклада КАК ГуидСклада
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_dbo.Ссылка КАК Ссылка,
		|	ВТ_dbo.shop_id КАК shop_id,
		|	ВТ_dbo.doc_id КАК doc_id
		|ИЗ
		|	ВТ_dbo КАК ВТ_dbo
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ КАК ВТ
		|		ПО ВТ_dbo.doc_id = ВТ.ГуидЗаказа
		|			И ВТ_dbo.shop_id = ВТ.ГуидСклада
		|ГДЕ
		|	ВТ.Заказ ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ТЗ_dbo", ТЗ_dbo);
		Запрос.УстановитьПараметр("ТЗ", ТЗ);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Набор = dbo.СоздатьНаборЗаписей();
			Набор.Отбор.document_id.Установить(Выборка.Ссылка);
			
			УдОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Попытка
				Набор.Записать();
				УдОбъект.Удалить();
				ннн = 1 + ннн;
				ВсеОК = Истина;
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
				Сообщить(ОписаниеОшибки);
				ТекстОшибок = ТекстОшибок + ?(ЗначениеЗаполнено(ТекстОшибок),Символы.ПС,"") + ОписаниеОшибки;
				РезВО.СообщитьОбОшибке("Ошибка " + ИмяОбъекта + " (" + ИмяТаблицы + ") " + Выборка.Ссылка + Символы.ПС + ОписаниеОшибки, СтрЗР);
				ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Документы.ЗаказПоставщику.ПолучитьСсылку(Новый УникальныйИдентификатор(Выборка.doc_id)));
			КонецПопытки;
			
			
		КонецЦикла;
		
		
		
	КонецФункции
	Функция Выгрузить_ЗаказПоставщику(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн,ИзПН = Ложь)
		
		ннн = 0;
		
		Почистить_ЗаказПоставщику(МассивСсылок,ИмяОбъекта,ИмяТаблицы,ннн);
		Если НЕ ИзПН Тогда
			РезВО.ДобавитьКомментарий("Почищено " + ннн + " " + ИмяОбъекта, СтрЗР);
		КонецЕсли;
		ннн = 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказПоставщикуТовары.Ссылка КАК Заказ,
		|	ЗаказПоставщикуТовары.СкладКомпании КАК Склад
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказ,
		|	Склад
		|ИТОГИ ПО
		|	Заказ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		ВыборкаЗаказ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЗаказ.Следующий() Цикл
			
			
			Заказ = ВыборкаЗаказ.Заказ;
			ГуидДок = Строка(Заказ.УникальныйИдентификатор());
			
			ВыборкаСклад = ВыборкаЗаказ.Выбрать();
			
			Пока ВыборкаСклад.Следующий() Цикл
				
				ГуидСклад = Строка(ВыборкаСклад.Склад.ТорговаяПлощадка.УникальныйИдентификатор());
				
				id = ГуидДок + " # " + ГуидСклад;
				
				code = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1 №%2 от %3 # (%4) %5",
				СокрЛП(Заказ.ХозОперация),
				СокрЛП(Заказ.Номер),
				Заказ.Дата,
				СокрЛП(ВыборкаСклад.Склад.Код),
				СокрЛП(ВыборкаСклад.Склад));
				
				
				Ссылка = ПолучитьСсылку(dbo, id);
				Если Ссылка.Пустая() Тогда
					ОбъектНовый = dbo.СоздатьОбъект();
				Иначе
					ОбъектНовый = Ссылка.ПолучитьОбъект();
				КонецЕсли;	
				
				
				ОбъектНовый.id			= id;
				ОбъектНовый.code		= code;
				ОбъектНовый.created_at	= Заказ.Дата;
				ОбъектНовый.posted		= Заказ.Проведен;
				ОбъектНовый.shop_id		= ПодставитьСсылку(ВыборкаСклад.Склад.ТорговаяПлощадка, "dbo_shop", ,ВыборкаСклад.Склад);
				ОбъектНовый.supplier_id	= ПодставитьСсылку(Заказ.Контрагент, "dbo_supplier");
				ОбъектНовый.staff_id	= ПодставитьСсылку(Заказ.Автор, "dbo_staff");
				ОбъектНовый.doc_id		= ГуидДок;
				
				ЗаписатьНовый(ОбъектНовый,Заказ,ИмяОбъекта,ИмяТаблицы,ннн);
			КонецЦикла;
		КонецЦикла;
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_ВозвратПоставщику(ОбъектНовый,мСсылка)
		
		ОбъектНовый.supplier_id	= ПодставитьСсылку(мСсылка.Контрагент, "dbo_supplier");
		ОбъектНовый.shop_id		= ПодставитьСсылку(мСсылка.СкладКомпании.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		ОбъектНовый.note		= мСсылка.Комментарий;
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_ПоступлениеТоваров(ОбъектНовый,мСсылка)
		
		ДокументОснование = мСсылка.ДокументОснование;
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			ИмяОбъекта = "ЗаказПоставщику";
			ИмяТаблицы = "dbo_purchase";
			dbo		= PlanoHeroAttika[ИмяТаблицы];
			ГуидДок = Строка(ДокументОснование.УникальныйИдентификатор());
			ГуидСклад = Строка(мСсылка.СкладКомпании.ТорговаяПлощадка.УникальныйИдентификатор());
			Гуид	= ГуидДок + " # " + ГуидСклад;
			Ссылка	= ПолучитьСсылку(dbo, Гуид);
			Если Ссылка.Пустая() Тогда
				МассивСсылок = Новый Массив();
				МассивСсылок.Добавить(ДокументОснование);
				Выгрузить_ЗаказПоставщику(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,0,Истина);
				Выгрузить_Товары_ЗаказПоставщику(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,0);
			КонецЕсли;
		Иначе
			Ссылка = Неопределено;
		КонецЕсли;
		
		ОбъектНовый.purchase_id	= ?(ЗначениеЗаполнено(Ссылка),Ссылка,Null);
		ОбъектНовый.supplier_id	= ПодставитьСсылку(мСсылка.Контрагент, "dbo_supplier");
		ОбъектНовый.shop_id		= ПодставитьСсылку(мСсылка.СкладКомпании.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_ПеремещениеТоваров(ОбъектНовый,мСсылка)
		
		ОбъектНовый.shop_sender_id	= ПодставитьСсылку(мСсылка.СкладКомпании.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		ОбъектНовый.shop_receiver_id= ПодставитьСсылку(мСсылка.СкладКомпанииПолучатель.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_Инвентаризация(ОбъектНовый,мСсылка)
		
		ОбъектНовый.shop_id	= ПодставитьСсылку(мСсылка.СкладКомпании.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_СписаниеТоваров(ОбъектНовый,мСсылка)
		
		ЗначениеЗаполненоСтатьяСписания = ЗначениеЗаполнено(мСсылка.СтатьяСписанияТМЦ);
		ОбъектНовый.type_id	= ПодставитьСсылку(?(ЗначениеЗаполненоСтатьяСписания,мСсылка.СтатьяСписанияТМЦ,"Статья списания не заполнена"), "dbo_losstype", НЕ ЗначениеЗаполненоСтатьяСписания);
		ОбъектНовый.shop_id	= ПодставитьСсылку(мСсылка.СкладКомпании.ТорговаяПлощадка, "dbo_shop",, мСсылка.СкладКомпании);
		ОбъектНовый.note	= мСсылка.Комментарий;
		
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_ИзменениеЦен(ОбъектНовый,мСсылка)
		
		ОбъектНовый.created_at	= мСсылка.ДатаНачалаДействия;
		ОбъектНовый.expire_date	= Null;
		ОбъектНовый.type_id		= ПодставитьСсылку(мСсылка.ТипЦен, "dbo_pricetype");
		ОбъектНовый.note		= мСсылка.Комментарий;
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_НазначениеСкидок(МассивСсылок,ИмяОбъекта,ннн)
		
		ИмяОбъекта = "КлассификаторПродаж";
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТК_НазначениеСкидок.КлассификаторПродажи КАК КлассификаторПродажи
		|ИЗ
		|	Документ.ТК_НазначениеСкидок КАК ТК_НазначениеСкидок
		|ГДЕ
		|	ТК_НазначениеСкидок.Ссылка В(&Ссылка)
		|	И НЕ ТК_НазначениеСкидок.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("Ссылка", МассивСсылок);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			type_id = ПодставитьСсылку(ВыборкаДетальныеЗаписи.КлассификаторПродажи, "dbo_promotiontype");
		КонецЦикла;
		
		ннн = ВыборкаДетальныеЗаписи.Количество();
		
		Возврат Истина;
		
	КонецФункции
	
	Функция Выгрузить_ОптимальноеКоличество(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		ннн = 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
		|ПОМЕСТИТЬ втПлощадки
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.ТорговаяПлощадка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОптимальноеКоличествоНоменклатуры.Ссылка КАК Ссылка,
		|	ОптимальноеКоличествоНоменклатуры.Проведен КАК Проведен,
		|	ТК_ОптимальноеКоличествоНоменклатуры.Период КАК Период,
		|	ТК_ОптимальноеКоличествоНоменклатуры.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ТК_ОптимальноеКоличествоНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТК_ОптимальноеКоличествоНоменклатуры.ВитринаОстаток КАК Витрина
		|ИЗ
		|	Документ.ТК_ОптимальноеКоличествоНоменклатуры КАК ОптимальноеКоличествоНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ОптимальноеКоличествоНоменклатуры КАК ТК_ОптимальноеКоличествоНоменклатуры
		|		ПО ОптимальноеКоличествоНоменклатуры.Ссылка = ТК_ОптимальноеКоличествоНоменклатуры.Регистратор
		|			И (ТК_ОптимальноеКоличествоНоменклатуры.ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|				ИЗ
		|					втПлощадки КАК втПлощадки))
		|ГДЕ
		|	ОптимальноеКоличествоНоменклатуры.Ссылка В(&Ссылка)";
		
		Запрос.УстановитьПараметр("Ссылка", МассивСсылок);
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
			
			Гуид = Строка(Выборка.Ссылка.УникальныйИдентификатор());
			
			Набор = dbo.СоздатьНаборЗаписей();
			Набор.Отбор.doc_id.Установить(Гуид);
			
			Пока Выборка.Следующий() Цикл
				
				Если НЕ Выборка.Проведен Тогда
					Продолжить;
				КонецЕсли;
				
				Запись				= Набор.Добавить();
				Запись.doc_id		= Гуид;
				Запись.shop_id		= ПодставитьСсылку(Выборка.ТорговаяПлощадка, "dbo_shop");
				Запись.product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");
				Запись.qty			= Выборка.Витрина;
				Запись.modified_at	= Выборка.Период;
				Запись.posted		= Выборка.Проведен;
				
			КонецЦикла;
			
			ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
		КонецЦикла;
		
	КонецФункции
	Функция Выгрузить_ИзменениеАссортимента(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		ннн = 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
		|ПОМЕСТИТЬ втПлощадки
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.ТорговаяПлощадка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменениеАссортимента.Ссылка КАК Ссылка,
		|	ИзменениеАссортимента.Проведен КАК Проведен,
		|	ОграничениеАссортимента.Период КАК Период,
		|	ОграничениеАссортимента.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ОграничениеАссортимента.Номенклатура КАК Номенклатура,
		|	ОграничениеАссортимента.Состояние КАК Состояние
		|ИЗ
		|	Документ.ИзменениеАссортимента КАК ИзменениеАссортимента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОграничениеАссортимента КАК ОграничениеАссортимента
		|		ПО ИзменениеАссортимента.Ссылка = ОграничениеАссортимента.Регистратор
		|			И (ОграничениеАссортимента.ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|				ИЗ
		|					втПлощадки КАК втПлощадки))
		|ГДЕ
		|	ИзменениеАссортимента.Ссылка В(&Ссылка)";
		
		Запрос.УстановитьПараметр("Ссылка", МассивСсылок);
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
			
			Гуид = Строка(Выборка.Ссылка.УникальныйИдентификатор());
			
			Набор = dbo.СоздатьНаборЗаписей();
			Набор.Отбор.doc_id.Установить(Гуид);
			
			Если Выборка.Проведен Тогда
				type_id	= ПодставитьСсылку(Выборка.Состояние, "dbo_assortmenttype", Истина);
				Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
					product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");
					Пока Выборка.Следующий() Цикл
						
						Запись				= Набор.Добавить();
						Запись.id			= Новый УникальныйИдентификатор;
						Запись.type_id		= type_id;
						Запись.shop_id		= ПодставитьСсылку(Выборка.ТорговаяПлощадка, "dbo_shop");
						Запись.product_id	= product_id;
						Запись.modified_at	= Выборка.Период;
						Запись.doc_id		= Гуид;
						Запись.posted		= Выборка.Проведен;
						
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
		КонецЦикла;
		
	КонецФункции
	Функция Выгрузить_ЗакрытиеСмены(МассивСсылок,dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		ннн = 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗакрытиеСмены.Ссылка КАК Ссылка,
		|	ЗакрытиеСмены.Дата КАК Дата,
		|	ЗакрытиеСмены.Проведен КАК Проведен,
		|	Продажи.СкладКомпании КАК Склад,
		|	Продажи.Номенклатура КАК Номенклатура,
		|	Продажи.ХарактеристикаНоменклатуры КАК Характеристика,
		|	СУММА(Продажи.Количество) КАК Количество,
		|	СУММА(Продажи.Сумма) КАК Сумма,
		|	СУММА(Продажи.Себестоимость) КАК Себестоимость
		|ИЗ
		|	Документ.ЗакрытиеСмены КАК ЗакрытиеСмены
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи КАК Продажи
		|		ПО ЗакрытиеСмены.Ссылка = Продажи.Регистратор
		|ГДЕ
		|	ЗакрытиеСмены.Ссылка В(&МассивСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗакрытиеСмены.Ссылка,
		|	ЗакрытиеСмены.Дата,
		|	ЗакрытиеСмены.Проведен,
		|	Продажи.СкладКомпании,
		|	Продажи.Номенклатура,
		|	Продажи.ХарактеристикаНоменклатуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Номенклатура
		|ИТОГИ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		ВыборкаДок = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаДок.Следующий() Цикл
			
			
			ЗКС = ВыборкаДок.Ссылка;
			ГуидДок = Строка(ЗКС.УникальныйИдентификатор());
			
			Набор = dbo.СоздатьНаборЗаписей();
			Набор.Отбор.document_id.Установить(ГуидДок);
			
			Выборка = ВыборкаДок.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Если НЕ Выборка.Проведен Тогда
					Продолжить;
				КонецЕсли;
				
				Запись				= Набор.Добавить();
				Запись.document_id	= ГуидДок;
				Запись.sales_date	= ВыборкаДок.Дата;
				Запись.warehouse_id	= Строка(Выборка.Склад.ТорговаяПлощадка.УникальныйИдентификатор());
				Запись.product_id	= Строка(Выборка.Номенклатура.УникальныйИдентификатор());
				Запись.char_id		= Строка(Выборка.Характеристика.УникальныйИдентификатор());
				Запись.qty			= Выборка.Количество;
				Запись.original_sum	= Выборка.Себестоимость;
				Запись.sales_sum	= Выборка.Сумма;
				
				
			КонецЦикла;
			ЗаписатьНовый(Набор,ЗКС,ИмяОбъекта,ИмяТаблицы,ннн);
		КонецЦикла;
		
		
		Возврат Истина;
		
	КонецФункции
	
	////	Товары	////
	
	Функция Выгрузить_Товары_ЗаказПоставщику(МассивСсылок,dbo_Владелец,ИмяОбъекта,ИмяТаблицы,ннн)
		
		ннн = 0;
		dbo = PlanoHeroAttika[ИмяТаблицы+"product"];
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Ссылка КАК Заказ,
		|	ЗаказПоставщикуТовары.Ссылка.Проведен КАК Проведен,
		|	ЗаказПоставщикуТовары.СкладКомпании КАК Склад,
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ЗаказПоставщикуТовары.КоличествоБазовое) КАК КоличествоБазовое,
		|	СУММА(ЗаказПоставщикуТовары.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&МассивСсылок)
		|	И ЗаказПоставщикуТовары.КоличествоБазовое > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказПоставщикуТовары.СкладКомпании,
		|	ЗаказПоставщикуТовары.Ссылка,
		|	ЗаказПоставщикуТовары.Номенклатура,
		|	ЗаказПоставщикуТовары.Ссылка.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказ,
		|	Склад,
		|	Номенклатура
		|ИТОГИ ПО
		|	Заказ,
		|	Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		ВыборкаЗаказ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЗаказ.Следующий() Цикл
			ГуидДок = Строка(ВыборкаЗаказ.Заказ.УникальныйИдентификатор());
			
			
			ВыборкаСклад = ВыборкаЗаказ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаСклад.Следующий() Цикл
				ГуидСклад = Строка(ВыборкаСклад.Склад.ТорговаяПлощадка.УникальныйИдентификатор());
				id = ГуидДок + " # " + ГуидСклад;
				
				Ссылка = ПолучитьСсылку(dbo_Владелец, id);
				
				Если Ссылка.Пустая() Тогда
					Продолжить;;
				КонецЕсли;
				
				Набор = dbo.СоздатьНаборЗаписей();
				Набор.Отбор.document_id.Установить(Ссылка);
				
				Выборка = ВыборкаСклад.Выбрать();
				
				Пока Выборка.Следующий() Цикл
					Запись					= Набор.Добавить();
					Запись.document_id		= Ссылка;
					Запись.product_id		= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");;
					Запись.qty				= Выборка.КоличествоБазовое;
					Запись.original_price	= Выборка.Сумма / Выборка.КоличествоБазовое;
					Запись.total_price		= Выборка.Сумма;
				КонецЦикла;
				ЗаписатьНовый(Набор,ВыборкаЗаказ.Заказ,ИмяОбъекта,ИмяТаблицы+"product",ннн,Истина);
				
			КонецЦикла;
		КонецЦикла;
		
		
	КонецФункции
	Функция Обработать_Товары_ПоступлениеТоваров(ОбъектНовый,мСсылка,ИмяТаблицы,Отсрочка)
		
		ОбъектНовый.produced_at	= Null;
		ОбъектНовый.sell_by_date= Null;
		ОбъектНовый.deferment	= Отсрочка;
		Возврат Истина;
		
	КонецФункции
	Функция Обработать_Товары_ПеремещениеТоваров(ОбъектНовый,мСсылка,ИмяТаблицы,Поставщик)
		
		ЗначениеЗаполненоПоставщик	= ЗначениеЗаполнено(Поставщик);
		ОбъектНовый.supplier_id		= ПодставитьСсылку(?(ЗначениеЗаполненоПоставщик,Поставщик,"Неизвестный поставщик"), "dbo_supplier", НЕ ЗначениеЗаполненоПоставщик);
		
		
		Возврат Истина;
		
	КонецФункции
	#КонецОбласти
	
	
	#Область Выгрузить_Данные
	
	Процедура Выгрузить_СвязьСкладПлощадка(dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК Склад
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0";
		
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//втДанные = РезультатЗапроса.Выгрузить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Набор = dbo.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			Запись				= Набор.Добавить();
			Запись.warehouse_id	= СокрЛП(Выборка.Склад.УникальныйИдентификатор());
			Запись.sarea_id		= СокрЛП(Выборка.Склад.ТорговаяПлощадка.УникальныйИдентификатор());
			
		КонецЦикла;
		
		ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Ложь);
		
	КонецПроцедуры
	Процедура Выгрузить_СвязМагазиновИМеток(dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК Склад
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблица.Склад КАК Склад,
		|	ВременнаяТаблица.Склад.ТорговаяПлощадка КАК Площадка,
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Вид,
		|	ВременнаяТаблица.Склад.ТочкаДоставки.Город КАК Город,
		|	ВременнаяТаблица.Склад.АссортиментнаяМатрица КАК Матрица,
		|	ВременнаяТаблица.Склад.Районы КАК Район
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		|		ПО ВременнаяТаблица.Склад.ТорговаяПлощадка = ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка
		|			И (ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""ВидПлощадки"")";
		
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Набор = dbo.СоздатьНаборЗаписей();
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Если НЕ ЗначениеЗаполнено(Выборка.Площадка) Тогда
					Продолжить;
				КонецЕсли;
				
				Гуид = Строка(Выборка.Площадка.УникальныйИдентификатор());
				
				СсылкаСклад = ПодставитьСсылку(Выборка.Площадка, "dbo_shop", ,Выборка.Склад);
				
				Для Каждого Колонка ИЗ РезультатЗапроса.Колонки Цикл
					ИмяКолонки = Колонка.Имя;
					
					Если ИмяКолонки = "Склад" ИЛИ ИмяКолонки = "Площадка" Тогда
						Продолжить;
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(Выборка[ИмяКолонки]) Тогда
						Продолжить;
					КонецЕсли;
					
					Гуид = Строка(Выборка[ИмяКолонки].УникальныйИдентификатор());
					
					СсылкМетка = ПодставитьСсылку(Выборка[ИмяКолонки], "dbo_shopmarker");
					
					Запись			= Набор.Добавить();
					Запись.shop_id	= СсылкаСклад;
					Запись.marker_id= СсылкМетка;
					
				КонецЦикла;
				
				
			КонецЦикла;
			
			ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Ложь);
			
		КонецЕсли;
		
	КонецПроцедуры
	Процедура Выгрузить_СвязьСкладТипЦен(dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК Склад
		|ПОМЕСТИТЬ втСклады
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	втСклады.Склад КАК Склад,
		|	втСклады.Склад.ТипЦенРозничнойТорговли КАК ТипЦен,
		|	0 КАК Приоритет
		|ИЗ
		|	втСклады КАК втСклады
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпанииДоступныеЦены.Ссылка,
		|	СкладыКомпанииДоступныеЦены.ТипЦен,
		|	СкладыКомпанииДоступныеЦены.НомерСтроки
		|ИЗ
		|	Справочник.СкладыКомпании.ДоступныеЦены КАК СкладыКомпанииДоступныеЦены
		|ГДЕ
		|	СкладыКомпанииДоступныеЦены.Ссылка В
		|			(ВЫБРАТЬ
		|				втСклады.Склад КАК Склад
		|			ИЗ
		|				втСклады КАК втСклады)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад,
		|	Приоритет
		|ИТОГИ ПО
		|	Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСклад.Следующий() Цикл
			
			СсылкаСклад = ПодставитьСсылку(ВыборкаСклад.Склад.ТорговаяПлощадка, "dbo_shop", ,ВыборкаСклад.Склад);
			
			Набор = dbo.СоздатьНаборЗаписей();
			Набор.Отбор.shop_id.Установить(СсылкаСклад);
			
			Выборка = ВыборкаСклад.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Запись			= Набор.Добавить();
				Запись.shop_id	= СсылкаСклад;
				Запись.type_id	= ПодставитьСсылку(Выборка.ТипЦен, "dbo_pricetype");
				Запись.priority	= Выборка.Приоритет;
				
			КонецЦикла;
			ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
			
		КонецЦикла;
		
		
		
	КонецПроцедуры
	Процедура Выгрузить_КоличествоФейсов(dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
		|ПОМЕСТИТЬ втПлощадки
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.ТорговаяПлощадка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Регистратор КАК Регистратор,
		|	ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВитринаОстаток КАК Витрина,
		|	ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Период КАК Период
		|ИЗ
		|	РегистрСведений.ТК_ОптимальноеКоличествоНоменклатуры.СрезПоследних(
		|			,
		|			ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|				ИЗ
		|					втПлощадки КАК втПлощадки)) КАК ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	ТорговаяПлощадка,
		|	Номенклатура
		|ИТОГИ ПО
		|	Регистратор,
		|	ТорговаяПлощадка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Набор = dbo.СоздатьНаборЗаписей();
		
		ВыборкаРегистратор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаРегистратор.Следующий() Цикл
			Гуид = Строка(ВыборкаРегистратор.Регистратор.УникальныйИдентификатор());
			ВыборкаТорговаяПлощадка = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаТорговаяПлощадка.Следующий() Цикл
				shop_id = ПодставитьСсылку(ВыборкаТорговаяПлощадка.ТорговаяПлощадка, "dbo_shop");	
				Выборка = ВыборкаТорговаяПлощадка.Выбрать();
				Пока Выборка.Следующий() Цикл
					Запись				= Набор.Добавить();
					Запись.doc_id		= Гуид;
					Запись.shop_id		= shop_id;
					Запись.product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");
					Запись.qty			= Выборка.Витрина;
					Запись.modified_at	= Выборка.Период;
					Запись.posted		= Истина;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
		
	КонецПроцедуры
	Процедура Выгрузить_Ассортимент(dbo,ИмяОбъекта,ИмяТаблицы,ннн)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
		|ПОМЕСТИТЬ втПлощадки
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.ТорговаяПлощадка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОграничениеАссортиментаСрезПоследних.Регистратор КАК Регистратор,
		|	ОграничениеАссортиментаСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ОграничениеАссортиментаСрезПоследних.Номенклатура КАК Номенклатура,
		|	ОграничениеАссортиментаСрезПоследних.Период КАК Период,
		|	ОграничениеАссортиментаСрезПоследних.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.ОграничениеАссортимента.СрезПоследних(
		|			,
		|			ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка
		|				ИЗ
		|					втПлощадки КАК втПлощадки)) КАК ОграничениеАссортиментаСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор,
		|	ТорговаяПлощадка,
		|	Номенклатура
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Набор = dbo.СоздатьНаборЗаписей();
		
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			
			Гуид = Строка(Выборка.Регистратор.УникальныйИдентификатор());
			type_id	= ПодставитьСсылку(Выборка.Состояние, "dbo_assortmenttype", Истина);
			
			//Набор = dbo.СоздатьНаборЗаписей();
			//Набор.Отбор.doc_id.Установить(Гуид);
			
			Пока Выборка.СледующийПоЗначениюПоля("ТорговаяПлощадка") Цикл
				shop_id	= ПодставитьСсылку(Выборка.ТорговаяПлощадка, "dbo_shop");
				Пока Выборка.Следующий() Цикл
					
					Запись				= Набор.Добавить();
					Запись.id			= Новый УникальныйИдентификатор;
					Запись.type_id		= type_id;
					Запись.shop_id		= shop_id;
					Запись.product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");
					Запись.modified_at	= Выборка.Период;
					Запись.doc_id		= Гуид;
					Запись.posted		= Истина;
					
				КонецЦикла;
			КонецЦикла;
			
			//ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
		КонецЦикла;
		ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Истина);
		
	КонецПроцедуры
	
	Процедура Выгрузить_Остатки(ИмяОбъекта,ИмяТаблицы,НаДату = Неопределено)	Экспорт
		
		ннн = 0;
		Если НаДату = Неопределено Тогда
			НаДату = НачалоДня(ТекущаяДата())-1;
		КонецЕсли;
		
		dbo = ВнешниеИсточникиДанных.PlanoHeroAttika.Таблицы[ИмяТаблицы];
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании) КАК Склад
		|ПОМЕСТИТЬ втСклады
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = ИСТИНА
		|	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
		|	И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В ИЕРАРХИИ (&Подразделения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СкладыКомпании.Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ИД_Автомата > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия.Контрагент ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваровКомпанииОстатки.Партия.Контрагент
		|	КОНЕЦ КАК Контрагент,
		|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	&НаДату КАК НаДату,
		|	СУММА(ПартииТоваровКомпанииОстатки.КоличествоОстаток) КАК Количество,
		|	СУММА(ПартииТоваровКомпанииОстатки.СуммаОстаток) КАК Сумма
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
		|			&НаДату,
		|			СкладКомпании В
		|				(ВЫБРАТЬ
		|					втСклады.Склад КАК Склад
		|				ИЗ
		|					втСклады КАК втСклады)) КАК ПартииТоваровКомпанииОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваровКомпанииОстатки.СкладКомпании,
		|	ПартииТоваровКомпанииОстатки.Номенклатура,
		|	ВЫБОР
		|		КОГДА ПартииТоваровКомпанииОстатки.Партия.Контрагент ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваровКомпанииОстатки.Партия.Контрагент
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.СкладКомпании КАК СкладКомпании,
		|	ВТ.Контрагент КАК Контрагент,
		|	ВТ.Номенклатура КАК Номенклатура,
		|	ВТ.НаДату КАК НаДату,
		|	СУММА(ВТ.Количество) КАК Количество,
		|	СУММА(ВТ.Сумма) КАК Сумма
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.СкладКомпании,
		|	ВТ.Контрагент,
		|	ВТ.Номенклатура,
		|	ВТ.НаДату
		|
		|УПОРЯДОЧИТЬ ПО
		|	СкладКомпании,
		|	Контрагент,
		|	Номенклатура
		|ИТОГИ ПО
		|	СкладКомпании,
		|	Контрагент
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("НаДату", НаДату);
		Запрос.УстановитьПараметр("Подразделения", Подразделения);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Набор = dbo.СоздатьНаборЗаписей();
		Набор.Отбор.dt.Установить(НаДату);
		
		ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСклад.Следующий() Цикл
			shop_id = ПодставитьСсылку(ВыборкаСклад.СкладКомпании.ТорговаяПлощадка, "dbo_shop",,ВыборкаСклад.СкладКомпании);
			ВыборкаКонтрагент = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрагент.Следующий() Цикл
				ЗначениеЗаполненоПоставщик = ЗначениеЗаполнено(ВыборкаКонтрагент.Контрагент);
				supplier_id	= ПодставитьСсылку(?(ЗначениеЗаполненоПоставщик,ВыборкаКонтрагент.Контрагент,"Неизвестный поставщик"), "dbo_supplier", НЕ ЗначениеЗаполненоПоставщик);
				Выборка = ВыборкаКонтрагент.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					Запись				= Набор.Добавить();
					Запись.shop_id		= shop_id;
					Запись.product_id	= ПодставитьСсылку(Выборка.Номенклатура, "dbo_product");
					Запись.supplier_id	= supplier_id;
					Запись.dt			= Выборка.НаДату;
					Запись.qty			= Выборка.Количество;
					Запись.total_price	= Выборка.Сумма;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		ЗаписатьНовый(Набор,,ИмяОбъекта,ИмяТаблицы,ннн,Ложь);
		
		РезВО.ДобавитьКомментарий("Выгружено " + ннн + " " + ИмяОбъекта, СтрЗР);
		
	КонецПроцедуры
	
	
	
	#КонецОбласти
	
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
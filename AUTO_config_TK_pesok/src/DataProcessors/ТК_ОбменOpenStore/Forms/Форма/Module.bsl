
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ИнформацияОКонтексте = Новый Структура;
		
	Пользователь = Пользователи.ТекущийПользователь();	
	
	ИнформацияОКонтексте.Вставить("Пользователь", Пользователь);
	ИнформацияОКонтексте.Вставить("ПолныеПрава", Пользователи.ЭтоПолноправныйПользователь());
	
	ПодразделениеОпенСтор = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Пользователь);		
	ЗаполнитьПараметрыПодразделения();
	ЗаполнитьАктивныеКонтрагенты();	
	
	ЗагрузитьНастройкиОтбораПоУмолчанию();
	
	флЗагрузитьИнвентаризацияБезСозданныхНаТТ = Истина;
		
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НастройкиОтбора = Настройки.Получить("НастройкиОтбора");
	Если НастройкиОтбора <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтбора.Получить());
	Иначе
		ЗагрузитьНастройкиОтбораПоУмолчанию();
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьЗаголовкиЭлементов();	
	УстановитьДоступностьПараметровИнвентаризаций();
	УправлениеЭлементамиФормы();
	
КонецПроцедуры


#КонецОбласти

 
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АктивныеСкладыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	ОбновитьЗаголовкиЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура АктивныеКонтрагентыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОбновитьЗаголовкиЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура флВыгрузкаИнвентаризацияРезультатПриИзменении(Элемент)
	
	УстановитьДоступностьПараметровИнвентаризаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ЗаполнитьПараметрыПодразделения();
	ОбновитьЗаголовкиЭлементов();	
	УправлениеЭлементамиФормы();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузкаЗакрытиеСмены(Команда)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Проверка", флЗагрузитьЗКСНеЗагруженные);							
	ДопПараметры.Вставить("Подразделение", ИнформацияОКонтексте.Подразделение);
	
	ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ДопПараметры);
	
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ЗакрытиеСмены", ПараметрыФормы);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаТКС(Команда)
	
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ТКС");

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИнвентаризация(Команда)
	
	ДопПараметры = Новый Структура("БезСозданныхНаТочках, Проводить",
							флЗагрузитьИнвентаризацияБезСозданныхНаТТ,
							флЗагрузитьИнвентаризацияПроводить);														
							
	ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ДопПараметры);							
							
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "Инвентаризация", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаПоступлениеТоваров(Команда)
	
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ПоступлениеТоваров");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаПеремещениеТоваров(Команда)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ЗагружатьКорректировкиПеремещения", ИнформацияОКонтексте.ЗагружатьКорректировкиПеремещения);
							
	ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ДопПараметры);								
	
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ПеремещениеТоваров", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаВозвратПоставщику(Команда)
	
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ВозвратПоставщику");
	
КонецПроцедуры




&НаКлиенте
Процедура ВыгрузкаИнвентаризация(Команда)
	
	ПараметрыИнвентаризации = Новый Структура("Корректировки, Результат, ПоПлану, СозданныеВБазе",
										флВыгрузкаИнвентаризацияКорректировки,
										флВыгрузкаИнвентаризацияРезультат,
										Ложь,
										флВыгрузкаИнвентаризацияСозданныеВБазе);
		
	ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ПараметрыИнвентаризации);	
	
	ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "Инвентаризация", ПараметрыФормы);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаПоступлениеТоваров(Команда)
	
	ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ПоступлениеТоваров");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаВозвратПоставщику(Команда)

	ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ВозвратПоставщику");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаПеремещениеТоваров(Команда)
	
	ПараметрыПеремещений = Новый Структура;
	ПараметрыПеремещений.Вставить("ИзПланаОбмена", флВыгрузкаПеремещенияИзПланаОбмена);
	
	Если ИнформацияОКонтексте.Свойство("ПланОбмена") Тогда 
		ПараметрыПеремещений.Вставить("ПланОбмена", ИнформацияОКонтексте.ПланОбмена);
	КонецЕсли;	
		
	ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ПараметрыПеремещений);	
		
	ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ПеремещениеТоваров", ПараметрыФормы);	
	
КонецПроцедуры



&НаКлиенте
Процедура ОтчетЦеныOS(Команда)
	
	ЗапуститьФормированиеОтчета("ЦеныОС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетДоприходыЗакрытияСмены(Команда)
	
	ЗапуститьФормированиеОтчета("ДоприходыЗС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетПриходыПоставленныеВОС(Команда)

	ЗапуститьФормированиеОтчета("ПриходыОС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСкладыБезЗакрытийСмены(Команда)

	ЗапуститьФормированиеОтчета("ОбъектыБезЗС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетРасхожденияЯкобс(Команда)

	ЗапуститьФормированиеОтчета("РасхожденияЯкобс");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСверкаПродажOS(Команда)
	ОткрытьФормуСверкиПродаж();
КонецПроцедуры



&НаКлиенте
Процедура ЖурналЗаказЦенников(Команда)

	ОткрытьФормуЗаказаЦенников();	
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналИнвентаризация(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Период", ПериодЗагрузки);
	ДобавитьПараметрыОтбора(ПараметрыФормы);
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.СписокИнвентаризаций", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналСнятыеЗетОтчеты(Команда)
	
	ПараметрыФормы = Новый Структура("ПериодЗагрузки,Подразделение", 
		ПериодЗагрузки,
		ИнформацияОКонтексте.Подразделение);
		
	ДобавитьПараметрыОтбора(ПараметрыФормы);		
		
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.СнятыеЗетОтчеты", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);	
	
КонецПроцедуры



&НаКлиенте
Процедура УстановитьАктивныеОбъекты(Команда)
	УстановитьАктивныеОбъектыНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьАктивныеОбъектыНаСервере()

	СвойствоАктивности = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "OS_АктивныеОбъекты");
	
	Если СвойствоАктивности.Пустая() Тогда 
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоАктивности);
	
	Для Каждого Стр Из АктивныеОбъекты Цикл 
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Объект = Стр.Объект;
		НоваяЗапись.Свойство = СвойствоАктивности;
		НоваяЗапись.Значение = Истина;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьАктивныеКонтрагенты(Команда)
	УстановитьАктивныеКонтрагентыНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьАктивныеКонтрагентыНаСервере()

	СвойствоАктивности = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "OS_АктивныеКонтрагенты");
	
	Если СвойствоАктивности.Пустая() Тогда 
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоАктивности);
	
	Для Каждого Стр Из АктивныеКонтрагенты Цикл 
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Объект = Стр.Контрагент;
		НоваяЗапись.Свойство = СвойствоАктивности;
		НоваяЗапись.Значение = Истина;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьАктивныеСклады(Команда)
	ОчиститьСообщения();
	УстановитьАктивныеСкладыНаСервере();
КонецПроцедуры

&НаСервере
Процедура УстановитьАктивныеСкладыНаСервере()
	
	СвойствоАктивности = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "OS_АктивныеСклады");
	
	Если СвойствоАктивности.Пустая() Тогда 
		Возврат;
	КонецЕсли;
	
	текПодразделение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКонтексте, "Подразделение", Неопределено);		
	
	МассивПодразделений = ОбменФронтПовтИсп.МассивПодразделений(текПодразделение);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивАктивныхСкладов", АктивныеСклады.Выгрузить(,"Склад").ВыгрузитьКолонку("Склад"));
	Запрос.УстановитьПараметр("Подразделения", МассивПодразделений);
	Запрос.УстановитьПараметр("Свойство", СвойствоАктивности);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СкладыКомпании.Ссылка КАК Склад
	               |ПОМЕСТИТЬ втАктивныеСклады
	               |ИЗ
	               |	Справочник.СкладыКомпании КАК СкладыКомпании
	               |ГДЕ
	               |	СкладыКомпании.Ссылка В(&МассивАктивныхСкладов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СкладыКомпании.Ссылка КАК Склад
	               |ПОМЕСТИТЬ втДоступныеСклады
	               |ИЗ
	               |	Справочник.СкладыКомпании КАК СкладыКомпании
	               |ГДЕ
	               |	СкладыКомпании.Подразделение В(&Подразделения)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Склад
	               |ПОМЕСТИТЬ втЗначения
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Свойство = &Свойство
	               |	И (ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК БУЛЕВО)) = ИСТИНА
	               |	И ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.Склад КАК Склад
	               |ПОМЕСТИТЬ втСклады
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втЗначения.Склад КАК Склад
	               |	ИЗ
	               |		втЗначения КАК втЗначения
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втАктивныеСклады.Склад
	               |	ИЗ
	               |		втАктивныеСклады КАК втАктивныеСклады) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втСклады.Склад КАК Склад,
	               |	НЕ втДоступныеСклады.Склад ЕСТЬ NULL КАК ДоступноИзменение,
	               |	ВЫБОР
	               |		КОГДА НЕ втАктивныеСклады.Склад ЕСТЬ NULL
	               |			ТОГДА ИСТИНА
	               |		КОГДА втДоступныеСклады.Склад ЕСТЬ NULL
	               |			ТОГДА НЕ втЗначения.Склад ЕСТЬ NULL
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Значение,
	               |	НЕ втЗначения.Склад ЕСТЬ NULL КАК СтароеЗначение
	               |ИЗ
	               |	втСклады КАК втСклады
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втДоступныеСклады КАК втДоступныеСклады
	               |		ПО втСклады.Склад = втДоступныеСклады.Склад
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втАктивныеСклады КАК втАктивныеСклады
	               |		ПО втСклады.Склад = втАктивныеСклады.Склад
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втЗначения КАК втЗначения
	               |		ПО втСклады.Склад = втЗначения.Склад
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДоступноИзменение УБЫВ,
	               |	Значение УБЫВ";
	
	НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Свойство.Установить(СвойствоАктивности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		ЕстьИзменение = Выборка.Значение <> Выборка.СтароеЗначение;	
		
		Если Выборка.Значение И (Не ЕстьИзменение ИЛИ (ЕстьИзменение И Выборка.ДоступноИзменение)) Тогда 
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Объект = Выборка.Склад;
			НоваяЗапись.Свойство = СвойствоАктивности;
			НоваяЗапись.Значение = Истина;
			
			Если ЕстьИзменение Тогда 
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Склад ""%1"" добавлен в список Активности'; uk = 'Склад ""%1"" включений до списку Активності'"),
						Выборка.Склад),
					Выборка.Склад);					
			КонецЕсли;
			
		ИначеЕсли Не Выборка.Значение И ЕстьИзменение И Выборка.ДоступноИзменение Тогда 
			
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Склад ""%1"" исключен из списка Активности'; uk = 'Склад ""%1"" виключений зі списку Активності'"),
					Выборка.Склад),
				Выборка.Склад);									
				
		ИначеЕсли ЕстьИзменение И Не Выборка.ДоступноИзменение Тогда 
				
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нет доступа для изменения Активности для склада ""%1""'; uk = 'Немає доступу для зміни Активності для складу ""%1""'"),
					Выборка.Склад),
				Выборка.Склад);
				
		КонецЕсли;	
			
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуПодтверждения(РежимФормы, ТипДокумента, ПараметрыФормы = Неопределено)
	
	Если ПараметрыФормы = Неопределено Тогда 
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ПериодЗагрузки", 	ПериодЗагрузки);
	ПараметрыФормы.Вставить("ТипДокумент", 		ТипДокумента);
	ПараметрыФормы.Вставить("РежимФормы", 		РежимФормы);
	ПараметрыФормы.Вставить("Подразделение", 	ИнформацияОКонтексте.Подразделение);
		
	ДобавитьПараметрыОтбора(ПараметрыФормы);
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыПодтверждения", ЭтаФорма, ПараметрыФормы);
	
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.ФормаПодтверждения", ПараметрыФормы, ЭтаФорма,,,, ОповещениеЗакрытия, РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыПодтверждения(РезультатЗакрытия, ДопПараметры) Экспорт 
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда 
		СообщенияПользователю = Неопределено;
		Если РезультатЗакрытия.Свойство("СообщенияПользователю", СообщенияПользователю) Тогда 			
			Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
				Для Каждого Сообщение Из СообщенияПользователю Цикл 
					Если Не Найти(Сообщение.Текст, "{СтандартныеПодсистемы.ДлительныеОперации}") Тогда 
						Сообщение.Сообщить();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаказаЦенников()
	
	ПараметрыФормы = Новый Структура("ПериодЗагрузки,Подразделение", 
		ПериодЗагрузки,
		ИнформацияОКонтексте.Подразделение);
		
	ДобавитьПараметрыОтбора(ПараметрыФормы);		
		
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.ЗаказЦенников", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСверкиПродаж()
	
	ПараметрыФормы = Новый Структура("ПериодЗагрузки,Подразделение", 
		ПериодЗагрузки,
		ИнформацияОКонтексте.Подразделение);
		
	ДобавитьПараметрыОтбора(ПараметрыФормы);		
		
	ОткрытьФорму("Обработка.ТК_ОбменOpenStore.Форма.ФормаСверкаПродажOS", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета(ВидОтчета)
	
	ОчиститьСообщения();
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВидОтчета",			ВидОтчета);
	
	Если ВидОтчета = "ЦеныОС" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Цены Open Store'; uk = 'Ціны Open Store'"));		
	ИначеЕсли ВидОтчета = "ПриходыОС" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Приходы поставленные в Open Store'; uk = 'Надходження поставлені в Open Store'"));		
	ИначеЕсли ВидОтчета = "ДоприходыЗС" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Доприходы закрытий смены'; uk = 'Доприбуткування закриття зміни'"));		
	ИначеЕсли ВидОтчета = "ОбъектыБезЗС" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Объекты без ""Закрытия смены""'; uk = 'Об'єкти без ""Закриття зміни""'"));
	ИначеЕсли ВидОтчета = "РасхожденияЯкобс" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Расхождения по продажам автоматов Якобс'; uk = 'Розбіжності з продажу автоматів Якобс'"));
	Иначе
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Отчет'; uk = 'Звіт'"));			
	КонецЕсли;
	
	ПараметрыЗагрузки.Вставить("НачалоПериода", 	ПериодЗагрузки.ДатаНачала);
	ПараметрыЗагрузки.Вставить("КонецПериода", 		ПериодЗагрузки.ДатаОкончания);
	ПараметрыЗагрузки.Вставить("Подразделение", 	ИнформацияОКонтексте.Подразделение);
		
	ДобавитьПараметрыОтбора(ПараметрыЗагрузки);
	
	ДлительнаяОперация = НачатьВыполнениеОперации("СформироватьОтчет", ПараметрыЗагрузки);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Формируем отчет'; uk = 'Формуємо звіт'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.OS";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	ПараметрыОжидания.Интервал = 2;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработкаРезультата", ЭтотОбъект, ПараметрыЗагрузки);	

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеОперации(Операция, ПараметрыОперации = Неопределено)
		
	//Если Операция = "СформироватьОтчет" Тогда 
				
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);				
				
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ОбменOpenStore."+Операция, ПараметрыОперации);

	//КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура ОбработкаРезультата(Операция, ПараметрыОтчета) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		Если Операция.Свойство("АдресРезультата") Тогда 			
			
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);
			
			Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ТабличныйДокумент") Тогда 
				
				КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ПараметрыОтчета.ВидОтчета);
				ТекКоллекция = КоллекцияПечатныхФорм[0];
				ТекКоллекция.ТабличныйДокумент = Результат.ТабличныйДокумент;
				ТекКоллекция.Экземпляров = 1;
				ТекКоллекция.СинонимМакета = ПараметрыОтчета.ПредставлениеВидаОтчета;
				
				ПараметрыПечати = Новый Структура;
				ПараметрыПечати.Вставить("ВладелецФормы", ЭтаФорма);
				ПараметрыПечати.Вставить("ЗаголовокФормы", ПараметрыОтчета.ПредставлениеВидаОтчета);
				
				УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, Неопределено, ПараметрыОтчета.ВидОтчета);
				
			КонецЕсли;			
			
		КонецЕсли;
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;

	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАктивныеОбъекты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Объект
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.ТорговыеПлощадки
	               |	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеОбъекты""
	               |	И ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК БУЛЕВО)";
	
	АктивныеОбъекты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАктивныеКонтрагенты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Контрагент
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.Контрагенты
	               |	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеКонтрагенты""
	               |	И ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК БУЛЕВО)";

	АктивныеКонтрагенты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАктивныеСклады()
	
	Перем текПодразделение;
	
	текПодразделение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИнформацияОКонтексте, "Подразделение", Неопределено);
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Склад
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.СкладыКомпании
	               |	И ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""				   
				   |	И ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК БУЛЕВО) = ИСТИНА";	               	
	
	Запрос = Новый Запрос;
	
	Если ЗначениеЗаполнено(текПодразделение) Тогда 
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + "И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).Подразделение В (&Подразделения)";		
		Запрос.УстановитьПараметр("Подразделения", ОбменФронтПовтИсп.МассивПодразделений(текПодразделение));
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	АктивныеСклады.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПодразделения()
	
	ПодразделениеOS = Строка(ПодразделениеОпенСтор);
	ИнформацияОКонтексте.Вставить("Подразделение", ПодразделениеОпенСтор);	
	ИнформацияОКонтексте.Вставить("ЗагружатьКорректировкиПеремещения", ПодразделениеОпенСтор.РежимЗагрузкиПеремещенийOS = 1);
	ИнформацияОКонтексте.Вставить("ЭтоПодразделениеФС", Ложь);
	
	Если ОбменФронтПовтИсп.ЭтоПодразделениеФабрикаСыра(ПодразделениеОпенСтор) Тогда 
		ИнформацияОКонтексте.Вставить("ЭтоПодразделениеФС", Истина);
		
		ПланОбмена = ПланыОбмена.ОбменФронт.НайтиПоКоду("000000003"); //OS - fabrika		
		Если Не ПланОбмена.Пустая() Тогда 
			ИнформацияОКонтексте.Вставить("ПланОбмена", ПланОбмена);
		Иначе
			Если ИнформацияОКонтексте.Свойство("ПланОбмена") Тогда 
				ИнформацияОКонтексте.Удалить("ПланОбмена");
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	ЗаполнитьАктивныеСклады();
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ГруппаОбмен");
	МассивВсехРеквизитов.Добавить("Подразделение");
	МассивВсехРеквизитов.Добавить("ПодразделениеOS");
	МассивВсехРеквизитов.Добавить("ГруппаВыгрузкаПеремещенияПараметры");
	
	МассивВидимыхРеквизитов = Новый Массив;
	
	Если ИнформацияОКонтексте.ПолныеПрава Тогда 
		МассивВидимыхРеквизитов.Добавить("ГруппаОбмен");
		МассивВидимыхРеквизитов.Добавить("Подразделение");
	Иначе
		МассивВидимыхРеквизитов.Добавить("ПодразделениеOS");
	КонецЕсли;
	
	Если ИнформацияОКонтексте.ЭтоПодразделениеФС Тогда 
		МассивВидимыхРеквизитов.Добавить("ГруппаВыгрузкаПеремещенияПараметры");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивВидимыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОтбораПоУмолчанию()
	
	ПериодЗагрузки.Вариант = ВариантСтандартногоПериода.Сегодня;
	
	СхемаКомпоновкиДанных = Обработки.ТК_ОбменOpenStore.ПолучитьМакет("ПоляШаблонаСклад");
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);	
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрыОтбора(ПараметрыФормы)
				
	МассивСкладов = ПолучитьМассивСкладовОтбора(КомпоновщикНастроек.Настройки, ИнформацияОКонтексте.Подразделение);
	ПараметрыФормы.Вставить("ОтборПоСкладам", МассивСкладов);
			
КонецПроцедуры

&НаКлиенте
Функция УстановленОтборПоСкладам()
	
	ОтборПоСкладам = Ложь;
	
	Для Каждого Стр Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл 
		Если Стр.Использование Тогда 
			ОтборПоСкладам = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
			
	Возврат ОтборПоСкладам;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьКнопокИнв()
	
	Элементы.флВыгрузкаИнвентаризацияКорректировки.Видимость = флВыгрузкаИнвентаризация;
	Элементы.флВыгрузкаИнвентаризацияРезультат.Видимость = флВыгрузкаИнвентаризация;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивСкладовОтбора(Знач КомпоновщикНастроек, Знач Подразделение)
	
	ТаблицаСкладов = Обработки.ТК_ОбменOpenStore.ПолучитьТаблицуСкладовСКД(КомпоновщикНастроек, Подразделение);
	
	Возврат ТаблицаСкладов.ВыгрузитьКолонку("Склад");
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗаголовкиЭлементов()
	
	ЗаголовокАктивныеСклады = НСтр("ru = 'Активные склады'; uk = 'Активні склади'");		
	Если АктивныеСклады.Количество() > 0 Тогда 
		ЗаголовокАктивныеСклады = ЗаголовокАктивныеСклады + " (" + АктивныеСклады.Количество() + ")";
	КонецЕсли;	
	Элементы.СтраницаАктивныеСклады.Заголовок = ЗаголовокАктивныеСклады;
	
	ЗаголовокАктивныеОбъекты = НСтр("ru = 'Активные объекты'; uk = 'Активні об''єкти'");
	Если АктивныеОбъекты.Количество() > 0 Тогда 
		ЗаголовокАктивныеОбъекты = ЗаголовокАктивныеОбъекты + " (" + АктивныеОбъекты.Количество() + ")";
	КонецЕсли;
	Элементы.СтраницаАктивныеТочки.Заголовок = ЗаголовокАктивныеОбъекты;
	
	ЗаголовокАктивныеКонтрагенты = НСтр("ru = 'Активные контрагенты'; uk = 'Активні контрагенти'");
	Если АктивныеКонтрагенты.Количество() > 0 Тогда 
		ЗаголовокАктивныеКонтрагенты = ЗаголовокАктивныеКонтрагенты + " (" + АктивныеКонтрагенты.Количество() + ")";
	КонецЕсли;
	Элементы.СтраницаАктивныеКонтрагенты.Заголовок = ЗаголовокАктивныеКонтрагенты;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПараметровИнвентаризаций()
	
	Если флВыгрузкаИнвентаризацияРезультат Тогда 
		флВыгрузкаИнвентаризацияКорректировки = Ложь;
	Иначе
		флВыгрузкаИнвентаризацияСозданныеВБазе = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "флВыгрузкаИнвентаризацияКорректировки", "Доступность", Не флВыгрузкаИнвентаризацияРезультат);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "флВыгрузкаИнвентаризацияСозданныеВБазе", "Доступность", флВыгрузкаИнвентаризацияРезультат);	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыгрузитьКонтрагентыНаСервере(ИнформацияОКонтексте)

	//для получения настроек подключения
	УзелОбмена = ПланыОбмена.ОбменФронт.ПолучитьУзелОбмена(ИнформацияОКонтексте.Подразделение);
	Если УзелОбмена =  Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбменФронтСобытия.ВыполнитьВыгрузкуКонтрагентов(УзелОбмена,НСтр("ru='Интерактивная выгрузка Контрагентов';uk='Інтерактивна вивантаження Контрагентов'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКонтрагенты(Команда)
	Если НЕ ЗначениеЗаполнено(ИнформацияОКонтексте.Подразделение) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо выбрать подразделение!'; uk = 'Необхідно вибрати підрозділ!'"));
	  	Возврат;
	КонецЕсли;
	
	
	ВыгрузитьКонтрагентыНаСервере(ИнформацияОКонтексте);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстатки(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыВыгрузки = Новый Структура("ПериодЗагрузки,Подразделение", 
	ПериодЗагрузки,
	ИнформацияОКонтексте.Подразделение);
	
	Если НЕ УстановленОтборПоСкладам() Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо выбрать склад для выгрузки!'; uk = 'Необхідно вибрати склад для вивантаження!'"));
	  	Возврат;
		
	КонецЕсли;
	
	ДобавитьПараметрыОтбора(ПараметрыВыгрузки);
	
	Если ПараметрыВыгрузки.ОтборПоСкладам.Количество()>1 Тогда
		ОповещениеЗакрытия = Новый ОписаниеОповещения("ПослеЗакрытияВопросаВыгрузкаОстатков",ЭтотОбъект,ПараметрыВыгрузки);
		ПоказатьВопрос(ОповещениеЗакрытия,НСтр("ru = 'Выбрано более одного склада."+Символы.ПС+"Продолжить выгрузку?'; uk = 'Обрано більше одного складу."+Символы.ПС+"Продовжити вивантаження?'"),РежимДиалогаВопрос.ДаНет,0);
	Иначе
		ВыгрузкаостатковФоново(ПараметрыВыгрузки);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаВыгрузкаОстатков(Результат,Параметры)Экспорт
	  Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
	КонецЕсли;
	ВыгрузкаостатковФоново(Параметры);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаостатковФоново(ПараметрыВыгрузки)
	 
	ДлительнаяОперация = НачатьВыполнениеОперации("ВыгрузитьОстатки", ПараметрыВыгрузки);
	
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка остатков'; uk = 'Вивантаження залишків'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.OS";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	ПараметрыОжидания.Интервал = 2;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеВыгрузкиОстатков", ЭтотОбъект, ПараметрыВыгрузки);	

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыгрузкиОстатков(Операция, Параметры)Экспорт
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Выгрузка остатков выполнена!'; uk = 'Вивантаження залишків виконана!'");	
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			

	Иначе
		
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
		
КонецПроцедуры

#КонецОбласти


#Область ОбменOS

&НаКлиенте
Процедура ВыгрузитьЦены(Команда)

	Если НЕ ЗначениеЗаполнено(ПодразделениеОбмена) Тогда
		Возврат;
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбрано подразделение!");

	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыВыгрузки = Новый Структура("Подразделение",ПодразделениеОбмена);

	 ДлительнаяОперация = НачатьВыполнениеОперации("ВыгрузитьЦены", ПараметрыВыгрузки);

	 
	 ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	 ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка цен'; uk = 'Вивантаження цін'");
	 ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	 ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	 ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	 ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.OS";
	 ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	 ПараметрыОжидания.ВыводитьСообщения = Ложь;
	 ПараметрыОжидания.Интервал = 2;	
	 
	 ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеВыгрузкиЦен", ЭтотОбъект, ПараметрыВыгрузки);	
	 
	 ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

	
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыгрузкиЦен(Операция, Параметры)Экспорт
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Выгрузка цен выполнена!'; uk = 'Вивантаження цін виконана!'");	
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			

	Иначе
		
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
		
КонецПроцедуры


#КонецОбласти



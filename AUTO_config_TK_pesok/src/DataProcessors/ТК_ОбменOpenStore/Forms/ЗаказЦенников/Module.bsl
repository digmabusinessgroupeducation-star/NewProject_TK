
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПериодЗагрузки", Период);	
	Параметры.Свойство("Подразделение", Подразделение);
	
	ОтборПоСкладам = Параметры.Свойство("ОтборПоСкладам"); 

	Если ОтборПоСкладам Тогда 		
		СписокСкладов.ЗагрузитьЗначения(Параметры.ОтборПоСкладам);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ЗаполнитьДерево();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоЦенниковФлагПриИзменении(Элемент)
	
	ТекДанные = Элементы.ДеревоЦенников.ТекущиеДанные;	
	
	ПодчиненныеЭл = ТекДанные.ПолучитьЭлементы();
	
	Если ПодчиненныеЭл.Количество() > 0 Тогда 
		ЗаполнитьФлажкиРекурсивно(ПодчиненныеЭл, ТекДанные.Флаг);
	КонецЕсли;
	
	ОбновитьКоличествоОтмеченных();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьДерево()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если Объект.ТаблицаДокументов.Количество() > 0 Тогда 
		
		Отказ = Истина;	
		
	КонецЕсли;
	                
	
	
	Если Не Отказ Тогда 		
		ДлительнаяОперация = НачатьВыполнениеОперации();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загружаем список ценников'; uk = 'Завантажуємо список цінників'");
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
		ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
		ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ОбменOpenStore";
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.Интервал = 2;		
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект,"Загрузка закрытия смены");		
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;		
	
	ОбновитьКоличествоОтмеченных();
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ЗаполнитьФлажкиРекурсивно(ДеревоЦенников.ПолучитьЭлементы(), Истина);
	
	ОбновитьКоличествоОтмеченных();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ЗаполнитьФлажкиРекурсивно(ДеревоЦенников.ПолучитьЭлементы(), Ложь);
	
	ОбновитьКоличествоОтмеченных();
	
КонецПроцедуры


&НаКлиенте
Процедура Печать(Команда)
	
	Результат = ПечатьНаСервере();
	
	СтрокаСозданияКоллекции = "";
	
	Если Результат.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Для Ит = 1 По Результат.Количество() Цикл 
		СтрокаСозданияКоллекции = СтрокаСозданияКоллекции + ?(Не ПустаяСтрока(СтрокаСозданияКоллекции), ", ", "") + "Ценники_" + Ит;
	КонецЦикла;
	
	КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(СтрокаСозданияКоллекции);
	
	Ит = 0;
	Для Каждого Стр Из Результат Цикл 		
		
		Структура = Стр.Значение;		
		ТекКоллекция = КоллекцияПечатныхФорм[Ит];		
		ЗаполнитьЗначенияСвойств(ТекКоллекция, Структура);
		
		Ит = Ит + 1;
	КонецЦикла;		
		
	ПараметрыПечати = Новый Структура;
	
	ПараметрыПечати.Вставить("ВладелецФормы", ЭтаФорма);
	ПараметрыПечати.Вставить("ЗаголовокФормы", НСтр("ru = 'Печать ценников'; uk = 'Друк цінників'"));
	
	УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, , ПараметрыПечати);
	
КонецПроцедуры

&НаСервере
Функция ПечатьНаСервере()
	
	Результат = Новый Структура;
	
	ТаблицаМал = Новый ТаблицаЗначений;
	ТаблицаМал.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
	ТаблицаМал.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаМал.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	ТаблицаБол = Новый ТаблицаЗначений;
	ТаблицаБол.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.СкладыКомпании"));
	ТаблицаБол.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));	
	ТаблицаБол.Колонки.Добавить("ХарактеристикаНоменклатуры", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	Для Каждого СтрСклад Из ДеревоЦенников.ПолучитьЭлементы() Цикл 
		
		Для Каждого СтрТовар Из СтрСклад.ПолучитьЭлементы() Цикл 
			
			Если Не СтрТовар.Флаг ИЛИ Не СтрТовар.Печатать Тогда 
				Продолжить;
			КонецЕсли;
			
			ТипЦенника = НРег(СтрТовар.ТипЦенника);
			
			Если ТипЦенника = "маленький" Тогда 
				нСтрока = ТаблицаМал.Добавить();
				нСтрока.Склад = СтрСклад.СкладНоменклатура;
				нСтрока.Номенклатура = СтрТовар.СкладНоменклатура;
				нСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
			ИначеЕсли ТипЦенника = "большой" Тогда 
				нСтрока = ТаблицаБол.Добавить();
				нСтрока.Склад = СтрСклад.СкладНоменклатура;
				нСтрока.Номенклатура = СтрТовар.СкладНоменклатура;				
				нСтрока.ХарактеристикаНоменклатуры = СтрТовар.ХарактеристикаНоменклатуры;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаМал.Количество() = 0 И ТаблицаБол.Количество() = 0 Тогда 
		ВызватьИсключение НСтр("ru = 'Нет выбранных ценников для печати'; uk = 'Немає обраних цінників для друку'");
	КонецЕсли;
	
	Если ТаблицаМал.Количество() > 0 Тогда 
		
		Структура = Новый Структура;
		Структура.Вставить("ТабличныйДокумент", СформироватьЦенникМалый(ТаблицаМал));
		Структура.Вставить("Экземпляров", 1);
		Структура.Вставить("СинонимМакета", НСтр("ru = 'Ценники ""Маленькие""'; uk = 'Цінники ""Маленькі""'"));
		
		Результат.Вставить("Маленькие", Структура);
		
	КонецЕсли;
	
	
	Если ТаблицаБол.Количество() > 0 Тогда 
		
		Структура = Новый Структура;
		Структура.Вставить("ТабличныйДокумент", СформироватьЦенникБольшой(ТаблицаБол));
		Структура.Вставить("Экземпляров", 1);
		Структура.Вставить("СинонимМакета", НСтр("ru = 'Ценники ""Большие""'; uk = 'Цінники ""Великі""'"));
		
		Результат.Вставить("Большие", Структура);		
		
	КонецЕсли;
	
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьВыполнениеОперации()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НачалоПериода", Период.ДатаНачала);
	ПараметрыОперации.Вставить("КонецПериода", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Подразделение", Подразделение);	
	ПараметрыОперации.Вставить("ОтборПоСкладам", ОтборПоСкладам);
	
	Если ОтборПоСкладам И СписокСкладов.Количество() > 0 Тогда 
		ПараметрыОперации.Вставить("МассивСкладов", СписокСкладов.ВыгрузитьЗначения());	
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);				
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ОбменOpenStore.ПолучитьДеревоЗаказовЦенников", ПараметрыОперации);
	
КонецФункции


&НаКлиенте
Процедура ОбработатьРезультат(Операция, СопровождающийТекст) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		ЗаполнитьДеревоЦенников(Операция, СообщениеЗавершения);
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЦенников(Операция, СообщенияЗавершения);
	
	Если Не Операция.Свойство("АдресРезультата") Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);	
		
	Если Результат.Свойство("ДеревоЦенников") Тогда 
		
		ЭлементыДерева = ДеревоЦенников.ПолучитьЭлементы();		
		ЭлементыДерева.Очистить();
		
		ЗаполнитьДеревоЦенниковРекурсивно(ЭлементыДерева, Результат.ДеревоЦенников);
		
		СообщениеЗавершения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СообщениеЗавершения", СообщениеЗавершения);	
				
	КонецЕсли;
			
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЦенниковРекурсивно(ДеревоПриемник, ДеревоИсточник);
	
	Для Каждого Стр Из ДеревоИсточник.Строки Цикл 						
		
		новСтр = ДеревоПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Стр);
		
		ЗаполнитьДеревоЦенниковРекурсивно(новСтр.ПолучитьЭлементы(), Стр);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФлажкиРекурсивно(Дерево, Значение)
	
	Для Каждого Стр Из Дерево Цикл 	
		
		СтрЭлементы = Стр.ПолучитьЭлементы();
		ЕстьПодчиненные = СтрЭлементы.Количество() > 0;
		
		Стр.Флаг = ?(Значение, Стр.Печатать ИЛИ ЕстьПодчиненные, Ложь);
		
		Если ЕстьПодчиненные Тогда 
			ЗаполнитьФлажкиРекурсивно(СтрЭлементы, Значение);
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоличествоОтмеченных()
	
	КоличествоОтмеченных = 0;
	
	Для Каждого СтрСклад Из ДеревоЦенников.ПолучитьЭлементы() Цикл 
		
		Для Каждого СтрТовары Из СтрСклад.ПолучитьЭлементы() Цикл 
			Если СтрТовары.Флаг Тогда 
				КоличествоОтмеченных = КоличествоОтмеченных + 1;
			КонецЕсли;
		КонецЦикла;
		
		//Если СтрСклад.Флаг Тогда 
		//	КоличествоОтмеченных = КоличествоОтмеченных + 1;
		//КонецЕсли;
	КонецЦикла;
	
	ЗаголовокКнопки = НСтр("ru = 'Печать'; uk = 'Надрукувати'");
	Если КоличествоОтмеченных > 0 Тогда 
		ЗаголовокКнопки = ЗаголовокКнопки + " (" + КоличествоОтмеченных + ")";
	КонецЕсли;
	
	Элементы.ФормаПечать.Заголовок = ЗаголовокКнопки;
	
КонецПроцедуры


&НаСервере
Функция СформироватьЦенникМалый(Таблица)
	
	Дата = ТекущаяДата();
	СкладовНаСтранице = 1;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	
	ТабДок.ПолеСверху = 5;
	ТабДок.ПолеСнизу = 5;
	ТабДок.ПолеСлева = 10;
	ТабДок.ПолеСправа = 10;
	
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку = Ложь;
	//ТабДок.КодЯзыка = КодЛокализацииИнформационнойБазы();
	
	ПФОбъект = РеквизитФормыВЗначение("Объект");
	Макет = ПФОбъект.ПолучитьМакет("ПФ_MXL_ЦенникМалый");
	
	ОбластьСклад = Макет.ПолучитьОбласть("Склад");
	ОбластьЦенник = Макет.ПолучитьОбласть("Товар|Ценник");
	ОбластьПусто = Макет.ПолучитьОбласть("Пусто|Ценник");
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ.Склад КАК СкладКомпании,
	               |	ВТ.Номенклатура КАК Номенклатура,
	               |	ВТ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	&ВТ КАК ВТ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).Артикул КАК Артикул,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ВТ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).Наименование2 КАК НаименованиеОсн,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).НаименованиеRB КАК НаименованиеРБ,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).ГруппаЦенообразования КАК ГруппаЦенообразования,
	               |	ВТ.СкладКомпании КАК СкладКомпании,
	               |	ВЫРАЗИТЬ(ВТ.СкладКомпании КАК Справочник.СкладыКомпании).ТипЦенРозничнойТорговли КАК ТипЦен
	               |ПОМЕСТИТЬ ТоварыКПечати
	               |ИЗ
	               |	ВТ КАК ВТ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	СкладКомпании,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	               |	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	               |	ЦеныСрезПоследних.Цена КАК Цена
	               |ПОМЕСТИТЬ втЦены
	               |ИЗ
	               |	РегистрСведений.Цены.СрезПоследних(
	               |			,
	               |			Номенклатура В
	               |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |						ТоварыКПечати.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						ТоварыКПечати КАК ТоварыКПечати)
	               |				И ТипЦен В
	               |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |						ТоварыКПечати.ТипЦен КАК ТипЦен
	               |					ИЗ
	               |						ТоварыКПечати КАК ТоварыКПечати)) КАК ЦеныСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	Характеристика,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТоварыКПечати.СкладКомпании КАК СкладКомпании,
	               |	ТоварыКПечати.Артикул КАК Артикул,
	               |	ТоварыКПечати.Номенклатура КАК Номенклатура,
	               |	ТоварыКПечати.НаименованиеОсн КАК НаименованиеОсн,
	               |	ТоварыКПечати.НаименованиеРБ КАК НаименованиеРБ,
	               |	ЕСТЬNULL(втЦены.Цена, 0) КАК Цена,
	               |	ТоварыКПечати.ГруппаЦенообразования КАК ГруппаЦенообразования
	               |ИЗ
	               |	ТоварыКПечати КАК ТоварыКПечати
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втЦены КАК втЦены
	               |		ПО ТоварыКПечати.Номенклатура = втЦены.Номенклатура
	               |			И ТоварыКПечати.ХарактеристикаНоменклатуры = втЦены.Характеристика
	               |			И ТоварыКПечати.ТипЦен = втЦены.ТипЦен
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТоварыКПечати.Номенклатура.Родитель.Родитель.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Родитель.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Наименование
	               |ИТОГИ ПО
	               |	СкладКомпании";
	
	Запрос.УстановитьПараметр("ВТ", Таблица);
	
	Запрос.Текст = ТекстЗапроса;
	ВыборкаСклады = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйСклад = Истина;
	КолВоСкладов = 0;
	НомерСтрокиНачало = 1;	
	Пока ВыборкаСклады.Следующий() Цикл 
		Если СкладыРаздельно И Не ПервыйСклад И КолВоСкладов = СкладовНаСтранице Тогда 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КолВоСкладов = 0;
		Иначе
			ПервыйСклад = Ложь;
		КонецЕсли;
				
		ТекСклад = ВыборкаСклады.СкладКомпании;
		ПредставлениеСклада = "Код: (" + СокрЛП(ТекСклад.Код) + ") " + ТекСклад;
		
		ОбластьСклад.Параметры.Склад = ПредставлениеСклада;
		ВывестиСклад = Истина;
		
		вСтроке = 0;
		Выборка = ВыборкаСклады.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл 			
			СтруктураЦенника = Новый Структура("Цена, Артикул, ГруппаЦенообразования, Наименование, Цена");
			ЗаполнитьЗначенияСвойств(СтруктураЦенника, Выборка);			
			СтруктураЦенника.Вставить("Дата", Формат(Дата, "ДФ=dd.MM.yy"));
			
			НаименованиеНоменклатуры = "";
			
			Если Не ПустаяСтрока(Выборка.НаименованиеРБ) Тогда 
				НаименованиеНоменклатуры = СокрЛП(Выборка.НаименованиеРБ);
			ИначеЕсли Не ПустаяСтрока(Выборка.НаименованиеОсн) Тогда 
				НаименованиеНоменклатуры = СокрЛП(Выборка.НаименованиеОсн);
			Иначе
				НаименованиеНоменклатуры = СокрЛП(Выборка.Номенклатура);
			КонецЕсли;
			
			СтруктураЦенника.Наименование = НаименованиеНоменклатуры;
			
			ОбластьЦенник.Параметры.Заполнить(СтруктураЦенника);
			
			Если вСтроке = 6 Тогда 
				вСтроке = 0;
			КонецЕсли;
			
			Если вСтроке = 0 Тогда 
				Если ВывестиСклад Тогда 
					ОбластьВывода = Новый Массив;
					ОбластьВывода.Добавить(ОбластьСклад);
					ОбластьВывода.Добавить(ОбластьЦенник);
					
					ВывестиСклад = Ложь;
				Иначе
					ОбластьВывода = ОбластьЦенник;
				КонецЕсли;
				
				ТК_УправлениеПечатью.ВывестиОбластьСПроверкойВывода(ТабДок, ОбластьВывода, НомерСтрокиНачало);				
			Иначе
				ТабДок.Присоединить(ОбластьЦенник);
			КонецЕсли;
			
			вСтроке = вСтроке + 1;
		КонецЦикла;		
		
		Если вСтроке < 6 Тогда 
			Для Ит = вСтроке + 1 По 6 Цикл 
				ТабДок.Присоединить(ОбластьПусто);	
			КонецЦикла;
		КонецЕсли;

		
		КолВоСкладов = КолВоСкладов + 1;
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

&НаСервере
Функция СформироватьЦенникБольшой(Таблица)
	
	Дата = ТекущаяДата();
	СкладовНаСтранице = 1;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Ложь;
	ТабДок.ПолеСверху = 5;
	ТабДок.ПолеСнизу = 5;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.ОтображатьСетку = Ложь;
	//ТабДок.КодЯзыка = КодЛокализацииИнформационнойБазы();
		
	ПФОбъект = РеквизитФормыВЗначение("Объект");
	//Макет = ПФОбъект.ПолучитьМакет("ПФ_MXL_ЦенникБольшой");
	//Макет = ПФОбъект.ПолучитьМакет("ПФ_MXL_МакетЦенника35х30");
	//Макет = ПФОбъект.ПолучитьМакет("ПФ_MXL_МакетЦенника40х30");
	Макет = ПФОбъект.ПолучитьМакет("ПФ_MXL_МакетЦенника");
	
	ОбластьСклад = Макет.ПолучитьОбласть("Склад");
	ОбластьЦенник = Макет.ПолучитьОбласть("Товар|Ценник");
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ.Склад КАК СкладКомпании,
	               |	ВТ.Номенклатура КАК Номенклатура,
	               |	ВТ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	&ВТ КАК ВТ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).Артикул КАК Артикул,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ВТ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).НаименованиеПолное КАК Наименование,
	               |	ВЫРАЗИТЬ(ВТ.Номенклатура КАК Справочник.Номенклатура).ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВТ.СкладКомпании КАК СкладКомпании,
	               |	ВЫРАЗИТЬ(ВТ.СкладКомпании КАК Справочник.СкладыКомпании).ТипЦенРозничнойТорговли КАК ТипЦен
	               |ПОМЕСТИТЬ ТоварыКПечати
	               |ИЗ
	               |	ВТ КАК ВТ
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	СкладКомпании,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	               |	ЦеныСрезПоследних.ТипЦен КАК ТипЦен,
	               |	ЦеныСрезПоследних.Цена КАК Цена
	               |ПОМЕСТИТЬ втЦены
	               |ИЗ
	               |	РегистрСведений.Цены.СрезПоследних(
	               |			,
	               |			Номенклатура В
	               |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |						ТоварыКПечати.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						ТоварыКПечати КАК ТоварыКПечати)
	               |				И ТипЦен В
	               |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |						ТоварыКПечати.ТипЦен КАК ТипЦен
	               |					ИЗ
	               |						ТоварыКПечати КАК ТоварыКПечати)) КАК ЦеныСрезПоследних
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	Характеристика,
	               |	ТипЦен
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	               |	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика,
	               |	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод
	               |ПОМЕСТИТЬ втШтрихКоды
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	(ШтрихкодыНоменклатуры.Номенклатура, ШтрихкодыНоменклатуры.Упаковка) В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				ТоварыКПечати.Номенклатура КАК Номенклатура,
	               |				ТоварыКПечати.ЕдиницаИзмерения КАК Упаковка
	               |			ИЗ
	               |				ТоварыКПечати КАК ТоварыКПечати)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ШтрихкодыНоменклатуры.Номенклатура,
	               |	ШтрихкодыНоменклатуры.Характеристика
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТоварыКПечати.СкладКомпании КАК СкладКомпании,
	               |	ТоварыКПечати.Артикул КАК Артикул,
	               |	ТоварыКПечати.Номенклатура КАК Номенклатура,
	               |	ТоварыКПечати.Наименование КАК Наименование,
	               |	ЕСТЬNULL(втШтрихКоды.Штрихкод, 0) КАК ШтрихКод,
	               |	втЦены.Цена КАК Цена,
	               |	ТоварыКПечати.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |ИЗ
	               |	ТоварыКПечати КАК ТоварыКПечати
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втЦены КАК втЦены
	               |		ПО ТоварыКПечати.Номенклатура = втЦены.Номенклатура
	               |			И ТоварыКПечати.ТипЦен = втЦены.ТипЦен
	               |			И ТоварыКПечати.ХарактеристикаНоменклатуры = втЦены.Характеристика
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втШтрихКоды КАК втШтрихКоды
	               |		ПО ТоварыКПечати.Номенклатура = втШтрихКоды.Номенклатура
	               |ГДЕ
	               |	втЦены.Цена > 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТоварыКПечати.Номенклатура.Родитель.Родитель.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Родитель.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
	               |	ТоварыКПечати.Номенклатура.Наименование
	               |ИТОГИ ПО
	               |	СкладКомпании";
	
	Запрос.УстановитьПараметр("ВТ", Таблица);
	
	ОтборСклады = "";
	//Если Объект.СкладыКомпании.Количество() > 0 Тогда 
	//	ОтборСклады = "И СкладКомпании В (&Склады)";
	//	Запрос.УстановитьПараметр("Склады", Объект.СкладыКомпании.Выгрузить(, "СкладКомпании"));
	//КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОтборСклады%", ОтборСклады);
	
	Запрос.Текст = ТекстЗапроса;
	ВыборкаСклады = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйСклад = Истина;
	КолВоСкладов = 0;
	Пока ВыборкаСклады.Следующий() Цикл
		Если Не ПервыйСклад И КолВоСкладов = СкладовНаСтранице Тогда 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КолВоСкладов = 0;
		Иначе
			ПервыйСклад = Ложь;
		КонецЕсли;
				
		ТекСклад = ВыборкаСклады.СкладКомпании;
		ПредставлениеСклада = "Код: (" + СокрЛП(ТекСклад.Код) + ") " + ТекСклад;
		
		ОбластьСклад.Параметры.Склад = ПредставлениеСклада;
		ТабДок.Вывести(ОбластьСклад);
		
		НаЛисте =1;
		вСтроке = 0;
		Выборка = ВыборкаСклады.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл 			
			СтруктураЦенника = Новый Структура("Цена, Артикул, ШтрихКод, Цена");
			ЗаполнитьЗначенияСвойств(СтруктураЦенника, Выборка);			
			СтруктураЦенника.Вставить("Дата", Формат(Дата, "ДФ=dd.MM.yy"));
			//СтруктураЦенника.Вставить("ТоварЕд", ?(Не ПустаяСтрока(Выборка.Наименование), Выборка.Наименование, СокрЛП(Выборка.Номенклатура)));
			
			//ПредставлениеТовара = ?(Не ПустаяСтрока(Выборка.Наименование), Выборка.Наименование, СокрЛП(Выборка.Номенклатура));

			//Если СтрДлина(ПредставлениеТовара) > 50 Тогда
			//	ОбластьЦенник.Область("R2C1:R5C3").Шрифт = Новый Шрифт(,7,Истина);
			//ИначеЕсли СтрДлина(ПредставлениеТовара) > 40 Тогда
			//	ОбластьЦенник.Область("R2C1:R5C3").Шрифт = Новый Шрифт(,8,Истина);
			//Иначе
			//	ОбластьЦенник.Область("R2C1:R5C3").Шрифт = Новый Шрифт(,9,Истина);
			//КонецЕсли;
			//
			ПредставлениеТовара = ?(ЗначениеЗаполнено(Выборка.Номенклатура.НаименованиеПолное), СокрЛП(Выборка.Номенклатура.НаименованиеПолное), СокрЛП(Выборка.Наименование2));
			Если Не Выборка.ХарактеристикаНоменклатуры.Пустая() Тогда 
				ПредставлениеТовара = ПредставлениеТовара + ", " + СокрЛП(Выборка.ХарактеристикаНоменклатуры);
			КонецЕсли;

			СтруктураЦенника.Вставить("ТоварЕд", ПредставлениеТовара);
			
			ОбластьЦенник.Параметры.Заполнить(СтруктураЦенника);
			
			Если Не ЗначениеЗаполнено(Выборка.ШтрихКод) Тогда 
				СтрокаQR = СокрЛП(Выборка.Артикул);
			Иначе
				СтрокаQR = СокрЛП(Выборка.ШтрихКод);
			КонецЕсли;
			
			СтрокаQR = СтрокаQR + "|" + Формат(Выборка.Цена, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=0");
			
			ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(СтрокаQR),1,86);
			ОбластьЦенник.Рисунки.QRКод.Картинка  =  Новый Картинка(ДанныеQRКода);				

			//ПараметрыШтрихКода = Новый Структура;
			//ПараметрыШтрихКода.Вставить("ТипКода", 4);
			//ПараметрыШтрихКода.Вставить("Высота", 	10);
			//ПараметрыШтрихКода.Вставить("Ширина", 20);
			//ПараметрыШтрихКода.Вставить("Штрихкод", СокрЛП(Выборка.ШтрихКод));
			//ПараметрыШтрихКода.Вставить("ОтображатьТекст", Ложь);
			//ПараметрыШтрихКода.Вставить("ВертикальноеВыравнивание", 2);
			//ПараметрыШтрихКода.Вставить("Масштабировать", Истина);
			//ПараметрыШтрихКода.Вставить("СохранятьПропорции", Ложь);			
			//
			//ДанныеШтрихКода = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			//ОбластьЦенник.Рисунки.Картинка.Картинка = ДанныеШтрихКода;		
			//
			Если вСтроке = 3 Тогда 
				вСтроке = 0;
			КонецЕсли;
			
			Если вСтроке = 0 Тогда 
				ТабДок.Вывести(ОбластьЦенник);
				НаЛисте = НаЛисте+1;
			Иначе
				ТабДок.Присоединить(ОбластьЦенник);
			КонецЕсли;
			
			Если НаЛисте = 7 Тогда
				НаЛисте = 0;
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			вСтроке = вСтроке + 1;
		КонецЦикла;		
		КолВоСкладов = КолВоСкладов + 1;
	КонецЦикла;
	
	Если ТабДок.ВысотаТаблицы > 0 Тогда 
		Возврат ТабДок;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции


#КонецОбласти


#Область ПрочиеПроцедурыИФункции



#КонецОбласти
 
 
 
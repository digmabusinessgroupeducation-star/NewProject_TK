
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = НачалоДня(ТекущаяДата() - 24*60*60);
	СписокОбновлен = Ложь;
	Пользователь = Пользователи.ТекущийПользователь();
	
	ПолучитьНастройкиКомпоновщикаПоУмолчанию();
	
	СтруктураШаблоновАнкет = Новый Структура;
	СтруктураШаблоновАнкет.Вставить("КИОСК_НАЛИЧНЫЙ", 		Справочники.ШаблоныАнкет.НайтиПоКоду("000000001")); 
	СтруктураШаблоновАнкет.Вставить("КИОСК_БЕЗНАЛИЧНЫЙ", 	Справочники.ШаблоныАнкет.НайтиПоКоду("000000004")); 
	СтруктураШаблоновАнкет.Вставить("ПАВИЛЬОН_НАЛИЧНЫЙ", 	Справочники.ШаблоныАнкет.НайтиПоКоду("000000002")); 
	СтруктураШаблоновАнкет.Вставить("ПАВИЛЬОН_БЕЗНАЛИЧНЫЙ", Справочники.ШаблоныАнкет.НайтиПоКоду("000000003")); 
	СтруктураШаблоновАнкет.Вставить("ОБЩИЙ_01_05_2023", 	Справочники.ШаблоныАнкет.НайтиПоКоду("000000005"));
	СтруктураШаблоновАнкет.Вставить("ОБЩИЙ_01_11_2023",		Справочники.ШаблоныАнкет.НайтиПоКоду("000000007"));
	СтруктураШаблоновАнкет.Вставить("ОБЩИЙ_01_09_2024",		Справочники.ШаблоныАнкет.НайтиПоКоду("000000009"));
	
	СтруктураШаблоновАнкет.Вставить("АкционныйТовар",		Справочники.ШаблоныАнкет.НайтиПоКоду("000000010"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоказатьПараметрыЧека();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	СписокОбновлен = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЧековПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьПараметрыЧека", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДопАнкетуПриИзменении(Элемент)
	
	УстановитьВидимостьДопШаблона();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервере
Процедура ОбновитьСписокЧековНаСервере()
	
	ДанныеТочки = ПолучитьДанныеТочки(Склад);
	
	ЕстьОтбор = Не ПустаяСтрока(Строка(КомпоновщикНастроек.Настройки.Отбор));
	
	втТаблица = Новый ТаблицаЗначений;
	втТаблица.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	втТаблица.Колонки.Добавить("РК", ОбщегоНазначения.ОписаниеТипаСтрока(123));
	втТаблица.Колонки.Добавить("Чек", ОбщегоНазначения.ОписаниеТипаСтрока(30));
	втТаблица.Колонки.Добавить("Артикул", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	втТаблица.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(255));
	втТаблица.Колонки.Добавить("Уп", ОбщегоНазначения.ОписаниеТипаСтрока(30));
	втТаблица.Колонки.Добавить("Кол", ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
	втТаблица.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15,4));
	втТаблица.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15,4));
	втТаблица.Колонки.Добавить("Возврат", Новый ОписаниеТипов("Булево"));
	втТаблица.Колонки.Добавить("ТипОплаты", ОбщегоНазначения.ОписаниеТипаСтрока(11));
	втТаблица.Колонки.Добавить("ИннКассира", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	втТаблица.Колонки.Добавить("ИД_Чек", ОбщегоНазначения.ОписаниеТипаСтрока(255));
		
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад", ДанныеТочки.Код);
	
	Если ОбменФронтПовтИсп.ЭтоПодрзаделениеБафит(ДанныеТочки.Подразделение) Тогда 
		ТаблицаПродаж = "ProdaziKiev";
	ИначеЕсли ОбменФронтПовтИсп.ЭтоПодразделениеФабрикаСыра(ДанныеТочки.Подразделение) Тогда 
		ТекстСообщения = "";
		Успешно = ОбменФронт.УстановитьПодключениеКВнешнемуИсточнику(Ложь,ДанныеТочки.Подразделение,ТекстСообщения);
		
		Если Не Успешно Тогда
			ЗаписьЖурналаРегистрации("Анкетирование",УровеньЖурналаРегистрации.Информация,,,"Соеденение не установлено "+ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Соединение не установлено: '; uk = 'З''єднання не встановлено: '") + ТекстСообщения);
		КонецЕсли;
		
		ТаблицаПродаж = "OpenStore";
	Иначе
		ТаблицаПродаж = "ОпПродажи";
	КонецЕсли;		
	
		
	Если ТаблицаПродаж = "OpenStore" Тогда 
		Запрос.УстановитьПараметр("Дата", Дата);
	Иначе
		Запрос.УстановитьПараметр("Дата", Формат(Дата, "ДФ=yyyy-MM-dd"));
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	dbo_vRCPTS.Дата КАК Дата,
	               |	dbo_vRCPTS.Время КАК Время,
	               |	dbo_vRCPTS.PK КАК РК,
	               |	dbo_vRCPTS.Чек_ КАК Чек,
	               |	dbo_vRCPTS.Артикул КАК Артикул,
	               |	dbo_vRCPTS.Наименование КАК Наименование,
	               |	dbo_vRCPTS.Уп КАК Уп,
	               |	dbo_vRCPTS.Кол КАК Кол,
	               |	dbo_vRCPTS.Цена КАК Цена,
	               |	dbo_vRCPTS.Сумма КАК Сумма,
	               |	dbo_vRCPTS.Отмена КАК Отмена,
	               |	dbo_vRCPTS.Возврат КАК Возврат,
	               |	dbo_vRCPTS.ТипОплаты КАК ТипОплаты,
	               |	dbo_vRCPTS.ИннКассира КАК ИннКассира
	               |ИЗ
	               |	ВнешнийИсточникДанных."+ТаблицаПродаж+".Таблица.dbo_vRCPTS КАК dbo_vRCPTS
	               |ГДЕ
	               |	dbo_vRCPTS.Склад = &Склад
	               |	И dbo_vRCPTS.Дата = &Дата
	               |	И НЕ dbo_vRCPTS.Отмена";
		
	Если ЕстьОтбор Тогда 
		МассивРК = ПолучитьМассивРКПоОтбору(Запрос, ТаблицаПродаж);
		Запрос.УстановитьПараметр("МассивРК", МассивРК);
		ТекстЗапроса = ТекстЗапроса + " И dbo_vRCPTS.PK В(&МассивРК)";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = втТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "РК,Чек,Артикул,Наименование,Уп,Кол,Цена,Возврат,ТипОплаты,ИннКассира");
				
		Если ТипЗнч(Выборка.Дата) = Тип("Дата") Тогда 
			ТекДата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
		Иначе
			ТекДата = Выборка.Дата;	
		КонецЕсли;
		
		НоваяСтрока.Дата = СтроковыеФункцииКлиентСервер.СтрокаВДату(ТекДата + " " + Выборка.Время, ЧастиДаты.ДатаВремя);
		НоваяСтрока.Сумма = Выборка.Сумма * ?(Выборка.Возврат, -1, 1);
		НоваяСтрока.ИД_Чек = Выборка.РК + "&" + Выборка.Чек + "&" + Формат(Выборка.Дата, "ДФ=ддММггччммсс");
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("втТаблица", втТаблица);
	Запрос.Текст = "ВЫБРАТЬ
	               |	dbo_vRCPTS.Дата КАК Дата,
	               |	dbo_vRCPTS.РК КАК РК,
	               |	dbo_vRCPTS.Чек КАК Чек,
	               |	dbo_vRCPTS.Артикул КАК Артикул,
	               |	dbo_vRCPTS.Наименование КАК Наименование,
	               |	dbo_vRCPTS.Уп КАК Уп,
	               |	dbo_vRCPTS.Кол КАК Кол,
	               |	dbo_vRCPTS.Цена КАК Цена,
	               |	dbo_vRCPTS.Сумма КАК Сумма,
	               |	dbo_vRCPTS.Возврат КАК Возврат,
	               |	dbo_vRCPTS.ТипОплаты КАК ТипОплаты,
	               |	dbo_vRCPTS.ИннКассира КАК ИннКассира,
	               |	dbo_vRCPTS.ИД_Чек КАК ИД_Чек
	               |ПОМЕСТИТЬ втТаблица
	               |ИЗ
	               |	&втТаблица КАК dbo_vRCPTS
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблица.Артикул КАК Артикул,
	               |	втТаблица.Наименование КАК Наименование,
	               |	втТаблица.Уп КАК Уп,
	               |	втТаблица.Кол КАК Кол,
	               |	втТаблица.Цена КАК Цена,
	               |	втТаблица.Сумма КАК Сумма,
	               |	втТаблица.Дата КАК Дата,
	               |	втТаблица.РК КАК РК,
	               |	втТаблица.Чек КАК Чек,
	               |	втТаблица.ИД_Чек КАК ИД_Чек
	               |ИЗ
	               |	втТаблица КАК втТаблица
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата,
	               |	Чек
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблица.Дата КАК Дата,
	               |	втТаблица.РК КАК РК,
	               |	втТаблица.Чек КАК Чек,
	               |	МАКСИМУМ(втТаблица.Возврат) КАК Возврат,
	               |	МАКСИМУМ(втТаблица.ТипОплаты) КАК ТипОплаты,
	               |	МАКСИМУМ(втТаблица.ИннКассира) КАК ИннКассира,
	               |	СУММА(втТаблица.Сумма) КАК СуммаЧека,
	               |	втТаблица.ИД_Чек КАК ИД_Чек
	               |ИЗ
	               |	втТаблица КАК втТаблица
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТаблица.Дата,
	               |	втТаблица.РК,
	               |	втТаблица.Чек,
	               |	втТаблица.ИД_Чек
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	тчТовары = МассивРезультатов[1].Выгрузить();
	Товары.Загрузить(тчТовары);
	
	ВыборкаЧеки = МассивРезультатов[2].Выбрать();
	СписокЧеков.Очистить();
	
	Пока ВыборкаЧеки.Следующий() Цикл 
		НоваяСтрока = СписокЧеков.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаЧеки, "Дата,РК,Чек,СуммаЧека,Возврат,ТипОплаты,ИннКассира,ИД_Чек");
		
		НоваяСтрока.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										"Чек №%1 от %2",
										ВыборкаЧеки.Чек,
										ВыборкаЧеки.Дата);
		
	КонецЦикла;
	
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокЧеков(Команда)
	
	ОчиститьСообщения();
	СписокЧеков.Очистить();
	Товары.Очистить();
	КешКассиров.Очистить();
	
	Если ПроверитьЗаполнение() Тогда 
		ОбновитьСписокЧековНаСервере();
		СписокОбновлен = Истина;
		РеквизитыТочкиОбновлены = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура НачатьАнкетированиеНаСервере()
	
	ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Склад);	
	ТекДата = ТекущаяДата();
	//Создаем сначала назначение опросов		
	
	// - проверка есть ли уже созданное Назначение опросов	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	НазначениеОпросовРеспонденты.Ссылка КАК НазначениеОпроса,
	               |	ВЫБОР
	               |		КОГДА НЕ Анкета.Ссылка ЕСТЬ NULL
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ОпросПроведен
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	Документ.НазначениеОпросов.Респонденты КАК НазначениеОпросовРеспонденты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Анкета КАК Анкета
	               |		ПО НазначениеОпросовРеспонденты.Ссылка = Анкета.Опрос
	               |			И НазначениеОпросовРеспонденты.Респондент = Анкета.Респондент
	               |			И (Анкета.Респондент ССЫЛКА Справочник.ТорговыеПлощадки)
	               |			И (Анкета.Проведен)
	               |			И (НЕ Анкета.ПометкаУдаления)
	               |ГДЕ
	               |	НазначениеОпросовРеспонденты.Респондент = &ТорговаяПлощадка
	               |	И НазначениеОпросовРеспонденты.Респондент ССЫЛКА Справочник.ТорговыеПлощадки
	               |	И НазначениеОпросовРеспонденты.Ссылка.ДатаНачала <= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	               |	И НазначениеОпросовРеспонденты.Ссылка.ДатаОкончания >= КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)
	               |	И НазначениеОпросовРеспонденты.Ссылка.Проведен
	               |	И НЕ НазначениеОпросовРеспонденты.Ссылка.ПометкаУдаления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.НазначениеОпроса КАК НазначениеОпроса
	               |ИЗ
	               |	ВТ КАК ВТ
	               |ГДЕ
	               |	НЕ ВТ.ОпросПроведен";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		НазначениеОпроса = Выборка.НазначениеОпроса;		
	Иначе				
		Если Не ЗначениеЗаполнено(ШаблонАнкеты) Тогда 
			ОбщегоНазначения.СообщитьПользователю("Не удалось обнаружить шаблон анкеты");			
			Возврат;
		КонецЕсли;
		
		обНазначениеОпроса = Документы.НазначениеОпросов.СоздатьДокумент();	
		обНазначениеОпроса.Дата = чДата;
		обНазначениеОпроса.ШаблонАнкеты = ШаблонАнкеты;
		обНазначениеОпроса.ДатаНачала = НачалоДня(чДата);
		обНазначениеОпроса.ДатаОкончания = КонецДня(ТекДата);
		обНазначениеОпроса.ВозможностьПредварительногоСохранения = Ложь;
		обНазначениеОпроса.Наименование = "Оценка работы точки от " + ТекДата; 
		обНазначениеОпроса.ПоказыватьВАрхивеАнкет = Истина;
		обНазначениеОпроса.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Создано обработкой ""Анкетирование по чекам"".
                                                  |Дата создания: %1;
                                                  |Пользватель; %2
                                                  |Чек: %3
                                                  |Дата чека: %4'"),
											ТекДата,
											Пользователи.ТекущийПользователь());
											
		НоваяСтрока = обНазначениеОпроса.Респонденты.Добавить();											
		НоваяСтрока.Респондент = ТорговаяПлощадка;
		
		Если Не обНазначениеОпроса.ПроверитьЗаполнение() Тогда 
			ОбщегоНазначения.СообщитьПользователю("Создание анкеты отменено!");
			Возврат;
		КонецЕсли;
				
		Попытка
			обНазначениеОпроса.Записать(РежимЗаписиДокумента.Проведение);						
			НазначениеОпроса = обНазначениеОпроса.Ссылка;
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка создания анкеты:
                          |%1'"),
					ОписаниеОшибки()));
			Возврат;
		КонецПопытки;	
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьАнкетирование(Команда)
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если ТорговаяПлощадка.Пустая() Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У склада ""%1"" не заполнен реквизит ""Торговая площадка""'; uk = 'Для складу ""%1"" не заповнений реквізит ""Торговий майданчик""'"),
				Склад),
			Склад,
			"Склад",, 
			Отказ);
	КонецЕсли;
			
	Если ШаблонАнкеты.Пустая() Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран шаблон анкеты'; uk = 'Не обраний шаблон анкети'"),,"ШаблонАнкеты",,Отказ); 			
	КонецЕсли;
	
	Если ИспользоватьДопАнкету И ШаблонАнкетыДоп.Пустая() Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран дополнительный шаблон анкеты'; uk = 'Не обраний додатковий шаблон анкети'"),,"ШаблонАнкетыДоп",,Отказ);
	КонецЕсли;
	
	Если Не Отказ И ИспользоватьДопАнкету И ШаблонАнкеты = ШаблонАнкетыДоп Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Шаблон ""%1"" уже используется в роли основного'; uk = 'Шаблон ""%1"" вже використовується в якості головного'"),
				СокрЛП(Строка(ШаблонАнкетыДоп))),,
			"ШаблонАнкетыДоп",,
			Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда 
		//АнкетированиеКлиент.НачатьИнтервью(ТорговаяПлощадка, ШаблонАнкеты);	
		//НачатьИнтервью(ТорговаяПлощадка, ШаблонАнкеты);
		НачатьИнтервьюВОбработке();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьПараметрыЧека()
	
	ТекущаяСтрока = Элементы.СписокЧеков.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПустыеДанные;
		Возврат;
	КонецЕсли;
		
	Отбор = Новый Структура;
	Отбор.Вставить("ИД_Чек", ТекущаяСтрока.ИД_Чек);
	
	Элементы.Товары.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
	
	чДата = ТекущаяСтрока.Дата;
	чВозврат = ТекущаяСтрока.Возврат;
	чТипОплаты = ТекущаяСтрока.ТипОплаты;
	чСуммаЧека = ТекущаяСтрока.СуммаЧека; 
	чКассир = ПолучитьКассира(ТекущаяСтрока.ИннКассира);
	
	Если СписокОбновлен И Не РеквизитыТочкиОбновлены Тогда
		чСклад = Склад;
		
		Если ДанныеТочки = Неопределено Тогда 
			ДанныеТочки = Новый Структура;
		КонецЕсли;
		
		ТорговаяПлощадка 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "ТорговаяПлощадка", Неопределено);	
		чТипТочки 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "Вид", "");
		чКуратор 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "Куратор", "");
		чГрафикРаботы 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "ГрафикРаботы", "");		
		УчастиеВПрограммеФМ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "ПрограммаФМ", Ложь);		
		
		ТекстПроверки 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "Проверка", "");
		Если Не ПустаяСтрока(ТекстПроверки) Тогда 
			Элементы.ГруппаНадписьРезультатАнкетирования.Видимость = Истина;
			Элементы.НадписьРезультатПрошлойПроверки.Заголовок = ТекстПроверки;
		Иначе
			Элементы.ГруппаНадписьРезультатАнкетирования.Видимость = Ложь;
		КонецЕсли;
		
		РеквизитыТочкиОбновлены = Истина;
	КонецЕсли;
	
	ВремяЧека = Час(чДата);
	
	Если ВремяЧека >= 7 И ВремяЧека < 21 Тогда
		чСмена = "День";
	Иначе
		чСмена = "Ночь";
	КонецЕсли;
	
	Если НачалоДня(Дата) >= Дата("20240901") Тогда 
		ШаблонАнкеты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураШаблоновАнкет, "ОБЩИЙ_01_09_2024", ПредопределенноеЗначение("Справочник.ШаблоныАнкет.ПустаяСсылка"));
	ИначеЕсли Дата >= Дата("20231101") Тогда
		ШаблонАнкеты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураШаблоновАнкет, "ОБЩИЙ_01_11_2023", ПредопределенноеЗначение("Справочник.ШаблоныАнкет.ПустаяСсылка"));
	ИначеЕсли Дата > Дата("20230101") Тогда 
		ШаблонАнкеты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураШаблоновАнкет, "ОБЩИЙ_01_05_2023", ПредопределенноеЗначение("Справочник.ШаблоныАнкет.ПустаяСсылка"));
	Иначе
		ИД_Шаблона = ВРег(Строка(чТипТочки)) + "_" + ВРег(ТекущаяСтрока.ТипОплаты);
		ШаблонАнкеты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураШаблоновАнкет, ИД_Шаблона, ПредопределенноеЗначение("Справочник.ШаблоныАнкет.ПустаяСсылка"));
	КонецЕсли;
	
	ИспользоватьДопАнкету = Ложь; //Принудительно сбрасываем опцию
	УстановитьВидимостьДопШаблона();
	ШаблонАнкетыДоп = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураШаблоновАнкет, "АкционныйТовар", ПредопределенноеЗначение("Справочник.ШаблоныАнкет.ПустаяСсылка"));
	
	Элементы.ЗаголовокЧека.Заголовок = ТекущаяСтрока.Представление;	
		
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаДанныеЧека;
	      
КонецПроцедуры


&НаКлиенте
Функция ПолучитьКассира(Знач ИНН)
	
	мСтроки = КешКассиров.НайтиСтроки(Новый Структура("ИНН", ИНН));
	
	Если мСтроки.Количество() > 0 Тогда 
		Кассир = мСтроки[0].Кассир;
	Иначе
		Кассир = ПолучитьСотрудникаПоИНН(ИНН);
		
		НовыйКассир = КешКассиров.Добавить();
		НовыйКассир.ИНН = ИНН;
		НовыйКассир.Кассир = Кассир;
	КонецЕсли;
	
	Возврат Кассир;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСотрудникаПоИНН(Знач ИНН)
	
	Кассир = Справочники.ФизическиеЛица.НайтиПоКоду(ИНН);
	
	Если Кассир.Пустая() Тогда 
		Кассир = "Кассир ИНН: " + ИНН;
	КонецЕсли;
	
	Возврат Кассир; 
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеТочки(Знач Склад)
	
	СтруктураРеквизитов = Новый Структура;
	
	Если ЗначениеЗаполнено(Склад) Тогда 
		УстановитьПривилегированныйРежим(Истина);
		
		ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Склад);				
		СтруктураРеквизитов.Вставить("ТорговаяПлощадка", ТорговаяПлощадка);
		СтруктураРеквизитов.Вставить("Подразделение", Склад.Подразделение);
		СтруктураРеквизитов.Вставить("Код", Склад.Код);
		
		ТаблицаРеквизитов = УправлениеСвойствами.ЗначенияСвойств(ТорговаяПлощадка, Истина, Ложь);
		
		Для Каждого Стр Из ТаблицаРеквизитов Цикл 
			Если Не ЗначениеЗаполнено(Стр.Значение) Тогда 
				Продолжить;
			КонецЕсли;
			
			ИмяСвойства = Стр.Свойство.Имя;
			
			Если ИмяСвойства = "КураторПлощадки" Тогда 
				СтруктураРеквизитов.Вставить("Куратор", Стр.Значение);
			ИначеЕсли ИмяСвойства = "ВидПлощадки" Тогда 
				СтруктураРеквизитов.Вставить("Вид", Стр.Значение);
			ИначеЕсли ИмяСвойства = "ГрафикРаботы" Тогда 
				СтруктураРеквизитов.Вставить("ГрафикРаботы", Стр.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Анкета.Дата КАК Дата,
		               |	РАЗНОСТЬДАТ(Анкета.Дата, &ТекДата, ДЕНЬ) КАК Период,
		               |	Анкета.Комментарий КАК Комментарий
		               |ИЗ
		               |	Документ.Анкета КАК Анкета
		               |ГДЕ
		               |	Анкета.Проведен
		               |	И Анкета.Респондент ССЫЛКА Справочник.ТорговыеПлощадки
		               |	И Анкета.Респондент = &ТорговаяПлощадка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Анкета.Дата УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Значение
		               |ИЗ
		               |	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		               |ГДЕ
		               |	ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка = &ТорговаяПлощадка
		               |	И ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""ПартнерскаяПрограмма""
		               |	И ТорговыеПлощадкиДополнительныеРеквизиты.Значение ССЫЛКА Справочник.ЗначенияСвойствОбъектов
		               |			И ВЫРАЗИТЬ(ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Справочник.ЗначенияСвойствОбъектов).Наименование = ""ФМУ""";
		
		Запрос.УстановитьПараметр("ТекДата", ТекущаяДата());
		Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);				
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		Результат = МассивРезультатов[0];
		РезультатФМ = МассивРезультатов[1];		
		
		РезультатПрошлойПроверки = "";
		Если Результат.Пустой() Тогда 
			РезультатПрошлойПроверки = НСтр("ru = 'Точка еще не участвовала в анкетировании'; uk = 'Точка ще не приймала участь у анкетуванні'");
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
						
			МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Выборка.Комментарий, Символы.ПС, Истина, Истина);
			МассивПроверки = Новый Массив;
			Для Каждого Подстрока Из МассивПодстрок Цикл 
				НачалоСтроки = НРег(Лев(СокрЛП(Подстрока), 3));
				
				//Если НачалоСтроки = "чек" 
				Если НачалоСтроки = "кас" Тогда 				
					МассивПроверки.Добавить(Подстрока);
				КонецЕсли;
			КонецЦикла;
			
			РезультатПрошлойПроверки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Последняя проверка %1 (%2 дней назад)'; uk = 'Остання перевірка %1 (%2 днів назад)'"),
											Выборка.Дата,
											Выборка.Период);
											
			Если МассивПроверки.Количество() > 0 Тогда 
				Для Каждого Подстрока Из МассивПроверки Цикл 
					РезультатПрошлойПроверки = РезультатПрошлойПроверки + Символы.ПС + Подстрока;
				КонецЦикла;
			КонецЕсли;									
		КонецЕсли;
		
		СтруктураРеквизитов.Вставить("Проверка", РезультатПрошлойПроверки);
		
		Если Не РезультатФМ.Пустой() Тогда 
			СтруктураРеквизитов.Вставить("ПрограммаФМ", Истина);	
		КонецЕсли;
		
		//ВидТочки = Неопределено;
		//Если СтруктураРеквизитов.Свойство("Вид", ВидТочки) Тогда 						
		//	стрВидТочки = СокрЛП(ВидТочки.Наименование);
		//	ШаблонАнкеты = Неопределено;
		//	Если стрВидТочки = "Киоск" Тогда 
		//		ШаблонАнкеты = Справочники.ШаблоныАнкет.НайтиПоКоду("000000001");
		//	ИначеЕсли стрВидТочки = "Павильон" Тогда 
		//		ШаблонАнкеты = Справочники.ШаблоныАнкет.НайтиПоКоду("000000002");
		//	КонецЕсли;
		//	
		//	Если ЗначениеЗаполнено(ШаблонАнкеты) Тогда 
		//		СтруктураРеквизитов.Вставить("ШаблонАнкеты", ШаблонАнкеты);
		//	КонецЕсли;
		//КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

	Возврат СтруктураРеквизитов;
	
КонецФункции

&НаСервере
Процедура ПолучитьНастройкиКомпоновщикаПоУмолчанию()
	
	СхемаКомпоновкиДанных = ПолучитьОбщийМакет("СхемаКомпоновкиОтбораНоменклатуры");			
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));	
	
	ВариантНастройки = СхемаКомпоновкиДанных.ВариантыНастроек.Найти("САртикулом");
	Если ВариантНастройки = Неопределено Тогда 
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Иначе
		Настройки = ВариантНастройки.Настройки;
	КонецЕсли;
	
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);			

КонецПроцедуры

&НаСервере
Функция ПолучитьМассивРКПоОтбору(Запрос, ТаблицаПродаж)
	
	СКД = ПолучитьОбщийМакет("СхемаКомпоновкиОтбораНоменклатуры");
	
	НастройкиКомпоновщика = КомпоновщикНастроек.Настройки;
	ПараметрыНастройки = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	//Получим макет компоновки    
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; 
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, НастройкиКомпоновщика,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")); 
	
	//Через процессор компоновки получим результат
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных); 
	
	ТаблицаРезультат = Новый ТаблицаЗначений; 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений; 
	
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 
	
	//МассивОтборов = ТаблицаРезультат.ВыгрузитьКолонку("Артикул");
	МассивАртикулов = Новый Массив;
	
	Для Каждого Стр Из ТаблицаРезультат Цикл 
		чАртикул = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Стр.Артикул);
		Если ЗначениеЗаполнено(чАртикул) Тогда 
			МассивАртикулов.Добавить(чАртикул);
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	dbo_vRCPTS.PK КАК РК
	               |ИЗ
	               |	ВнешнийИсточникДанных."+ТаблицаПродаж+".Таблица.dbo_vRCPTS КАК dbo_vRCPTS
	               |ГДЕ
	               |	dbo_vRCPTS.Склад = &Склад
	               |	И dbo_vRCPTS.Дата = &Дата
	               |	И НЕ dbo_vRCPTS.Отмена
	               |	И dbo_vRCPTS.Артикул В(&МассивАртикулов)";
	
	Запрос.УстановитьПараметр("МассивАртикулов", МассивАртикулов);
	Запрос.Текст = ТекстЗапроса;
	
	тзЧеки = Запрос.Выполнить().Выгрузить();
	
	Возврат тзЧеки.ВыгрузитьКолонку("РК");
	
КонецФункции

&НаКлиенте
Процедура НачатьИнтервью(Респондент, ШаблонАнкеты)
	
	ТекущаяСтрока = Элементы.СписокЧеков.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПустыеДанные;
		Возврат;
	КонецЕсли;
	
	
	Комментарий = Новый ТекстовыйДокумент;
	Комментарий.ДобавитьСтроку(ТекущаяСтрока.Представление);
	Комментарий.ДобавитьСтроку("РК: "  + ТекущаяСтрока.РК);
	Комментарий.ДобавитьСтроку("Сумма: "  + Формат(чСуммаЧека, "ЧДЦ=2"));
	Комментарий.ДобавитьСтроку("Кассир: (" + ТекущаяСтрока.ИннКассира + ") " + чКассир);
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Респондент", Респондент);
	ЗначенияЗаполнения.Вставить("ШаблонАнкеты", ШаблонАнкеты);
	ЗначенияЗаполнения.Вставить("РежимАнкетирования", ПредопределенноеЗначение("Перечисление.РежимыАнкетирования.Интервью"));
	ЗначенияЗаполнения.Вставить("Дата", чДата);
	ЗначенияЗаполнения.Вставить("Комментарий", Комментарий.ПолучитьТекст());
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ТолькоФормаЗаполнения", Истина);
	
	ФормаАнкеты = ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы);
	
	Если ФормаАнкеты <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ФормаАнкеты, ЗначенияЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДопШаблона()
	
	Элементы.ШаблонАнкетыДоп.Видимость = ИспользоватьДопАнкету;
	
КонецПроцедуры

#КонецОбласти
 
#Область ИнтервьюВстроенное

&НаКлиенте
Процедура НачатьИнтервьюВОбработке()
	
	ТекущаяСтрока = Элементы.СписокЧеков.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПустыеДанные;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеТочки) = Тип("Структура") Тогда 
		Подразделение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеТочки, "Подразделение", Неопределено);
	Иначе
		Подразделение = Неопределено;
	КонецЕсли;
		
	Комментарий = Новый ТекстовыйДокумент;
	Комментарий.ДобавитьСтроку(ТекущаяСтрока.Представление);
	Комментарий.ДобавитьСтроку("РК: "  + ТекущаяСтрока.РК);
	Комментарий.ДобавитьСтроку("Сумма: "  + Формат(чСуммаЧека, "ЧДЦ=2"));
	Комментарий.ДобавитьСтроку("Кассир: (" + ТекущаяСтрока.ИннКассира + ") " + чКассир);	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Дата", чДата);
	ПараметрыФормы.Вставить("Респондент", ТорговаяПлощадка);
	ПараметрыФормы.Вставить("ПодразделениеКомпании", Подразделение); 
	ПараметрыФормы.Вставить("Интервьюер", Пользователь);
	ПараметрыФормы.Вставить("ШаблонАнкеты", ШаблонАнкеты);
	ПараметрыФормы.Вставить("РежимАнкетирования", ПредопределенноеЗначение("Перечисление.РежимыАнкетирования.Интервью"));
	ПараметрыФормы.Вставить("Комментарий", Комментарий.ПолучитьТекст());
	ПараметрыФормы.Вставить("Реализатор", чКассир);
	
	Если ИспользоватьДопАнкету Тогда 	
		ПараметрыФормы.Вставить("ШаблонАнкетыДоп", ШаблонАнкетыДоп);
	КонецЕсли;
	
	Если ИспользоватьДопАнкету Тогда 
		ОписаниеЗакрытия = Новый ОписаниеОповещения("РезультатЗакрытияФормыАнкеты", ЭтаФорма, Неопределено);		
		ОткрытьФорму("Обработка.ТК_АнкетированиеПоЧекам.Форма.ФормаАнкеты", ПараметрыФормы, ЭтаФорма,,,, ОписаниеЗакрытия);
	Иначе
		ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗакрытияФормыАнкеты(мРезультаты, ДопПараметры) Экспорт 
	
	Если ТипЗнч(мРезультаты) <> Тип("Массив") Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Результат Из мРезультаты Цикл 
		Если Результат.Свойство("Сообщение") Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			Результат.Сообщение,
			?(Результат.Свойство("Ссылка"), Результат.Ссылка, Неопределено));
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти
 
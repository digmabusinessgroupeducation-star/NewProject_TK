
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаЗагрузки = НачалоДня(ТекущаяДата())-60*60*24;
	ЗагружатьЗКС = Истина;
	ЗагружатьПРРО = Истина;
КонецПроцедуры




&НаСервере
Функция ЗагрузитьДокументыНаСервере()
	
	
	 Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.ТК_zFoc.ВыполнитьЗагрузку",ПерегрузитьОбработанные,?(ЗначениеЗаполнено(ДатаЗагрузки),НачалоДня(ДатаЗагрузки),Ложь), ЗагружатьЗКС, ЗагружатьПРРО);

	
 КонецФункции
 
&НаСервере
Функция ВыполнитьФоновоеЗаданиеВыгрузки()
	 Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.ТК_zFoc.ЗапуститьВыгузку",ДатаВыгрузки,Продажи,Остатки);
КонецФункции


&НаКлиенте
Функция ПараметрыОжидания()
	
	ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ВыполнитьДействиеПрогрессВыполнения", ЭтотОбъект);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загрузка документов'; uk = 'Завантаження документів'");;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка._ДемоДлительнаяОперация";
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	Возврат ПараметрыОжидания;

КонецФункции


&НаКлиенте
Процедура ВыполнитьДействиеПрогрессВыполнения(Прогресс, ДополнительныеПараметры) Экспорт
	
	ТекстУведомления = НСтр("ru = 'Пожалуйста, подождите...'");
	
	ТекстУведомления = НСтр("ru = 'Загрузка документов'; uk = 'Завантаження документів'") + Символы.ПС + ТекстУведомления;

	Если Прогресс.Прогресс <> Неопределено Тогда
		ТекстУведомления = ТекстУведомления + ПрогрессСтрокой(Прогресс.Прогресс);
	КонецЕсли;
	Если Прогресс.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Прогресс.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	Элементы.ДлительнаяОперация.РасширеннаяПодсказка.Заголовок = ТекстУведомления;

КонецПроцедуры 

&НаКлиенте
Функция ПрогрессСтрокой(Прогресс)
	
	Результат = "";
	Если Прогресс = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Процент = 0;
	Если Прогресс.Свойство("Процент", Процент) Тогда
		Результат = Строка(Процент) + "%";
	КонецЕсли;
	Текст = 0;
	Если Прогресс.Свойство("Текст", Текст) Тогда
		Если Не ПустаяСтрока(Результат) Тогда
			Результат = Результат + " (" + Текст + ")";
		Иначе
			Результат = Текст;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДокументы(Команда)
	
	
	ОчиститьСообщения();
	ТекстСообщения =  НСтр("ru = 'Начало загрузки документов'; uk = 'Початок завантаження документів'");
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2...", 
	ОбщегоНазначенияКлиент.ДатаСеанса(), ТекстСообщения));
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ГруппаДлительнаяОперация","Видимость",Истина);
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ФормаЗагрузитьДокументы,ФормаСверка,ДатаЗагрузки,ПерегрузитьОбработанные","Видимость",Ложь);

	
	ДлительнаяОперация = ЗагрузитьДокументыНаСервере();
	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания());

	
КонецПроцедуры


&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДопПараметры) Экспорт
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ГруппаДлительнаяОперация","Видимость",Ложь);
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ФормаЗагрузитьДокументы,ФормаСверка,ДатаЗагрузки,ПерегрузитьОбработанные","Видимость",Истина);

	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ВывестиРезультат(Результат);
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(,Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;

	
КонецПроцедуры



&НаСервере
Процедура ВывестиРезультат(Результат,ДопПараметры="")
	
	Если Результат.Статус = "Выполнено" Тогда
		Если Результат.Свойство("АдресРезультата") И ЗначениеЗаполнено(Результат.АдресРезультата) Тогда
			ТекстСообщения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Иначе
			Если ДопПараметры = "Выгрузка" Тогда
				ТекстСообщения =  НСтр("ru = 'Выгрузка документов выполнена'; uk = 'Вивантаження документів виконана'");

			Иначе
				ТекстСообщения =  НСтр("ru = 'Загрузка документов выполнена'; uk = 'Завантаження документів виконана'");
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	Если Результат.Сообщения <> Неопределено Тогда
		Для каждого СообщениеПользователю Из Результат.Сообщения Цикл
			СообщениеПользователю.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'; uk = '%1 %2'"), ТекущаяДатаСеанса(), ТекстСообщения));
	
КонецПроцедуры


&НаКлиенте
Процедура Сверка(Команда)
	
	ПериодСверки = Новый СтандартныйПериод;
	ПериодСверки.ДатаНачала = ДатаЗагрузки;
	ПериодСверки.ДатаОкончания = КонецДня(ДатаЗагрузки);
	
		ПараметрыФормыЗетОтчетов = Новый Структура;
	ПараметрыФормыЗетОтчетов.Вставить("Период", ПериодСверки);

	
	ОткрытьФорму("Обработка.ТК_zFoc.Форма.ДанныеЗетОтчетов",ПараметрыФормыЗетОтчетов);
КонецПроцедуры


// Выгрузка данных

&НаКлиенте
Процедура ОбработатьРезультатВыгрузки(Результат, ДопПараметры) Экспорт

	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ВывестиРезультат(Результат,"Выгрузка");
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(,Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
КонецПроцедуры
	

&НаКлиенте
Процедура Выгрузить(Команда)
	
	
	 	
	ДлительнаяОперация = ВыполнитьФоновоеЗаданиеВыгрузки();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка'; uk = 'Вивантаження'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_zFoc";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатВыгрузки", ЭтотОбъект, Неопределено);	

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры



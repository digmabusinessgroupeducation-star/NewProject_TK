

Функция ПолучитьВидАссортимента(НаДату,Номенклатура,Площадки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОграничениеАссортиментаСрезПоследних.Номенклатура КАК Номенклатура,
	|	ОграничениеАссортиментаСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ОграничениеАссортиментаСрезПоследних.Состояние КАК Состояние,
	|	ОграничениеАссортиментаСрезПоследних.ВидАссортимента КАК ВидАссортимента,
	|	ВЫБОР
	|		КОГДА ОграничениеАссортиментаСрезПоследних.ВидАссортимента = ЗНАЧЕНИЕ(справочник.ВидыАссортимента.ВнутреннийЗаказ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Распродажа
	|ИЗ
	|	РегистрСведений.ОграничениеАссортимента.СрезПоследних(
	|			&НаДату,
	|			Номенклатура = &Номенклатура
	|				И ТорговаяПлощадка В (&ТорговаяПлощадка)) КАК ОграничениеАссортиментаСрезПоследних
	|ГДЕ
	|	ОграничениеАссортиментаСрезПоследних.Состояние <> 0";
	
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", Площадки);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса; 	
	
КонецФункции

Функция УдалимЦепочкуПодчиненных(СсылкаАК,СсылкаИА,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк)
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	|	ИзменениеАссортимента1.ДокументОснование КАК ДокументОснование1,
	|	ИзменениеАссортимента1.Ссылка КАК Ссылка1,
	|	ИзменениеАссортимента2.ДокументОснование КАК ДокументОснование2,
	|	ИзменениеАссортимента2.Ссылка КАК Ссылка2,
	|	ИзменениеАссортимента3.ДокументОснование КАК ДокументОснование3,
	|	ИзменениеАссортимента3.Ссылка КАК Ссылка3,
	|	ИзменениеАссортимента4.ДокументОснование КАК ДокументОснование4,
	|	ИзменениеАссортимента4.Ссылка КАК Ссылка4
	|ИЗ
	|	Документ.ИзменениеАссортимента КАК ИзменениеАссортимента1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеАссортимента КАК ИзменениеАссортимента2
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеАссортимента КАК ИзменениеАссортимента3
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеАссортимента КАК ИзменениеАссортимента4
	|				ПО ИзменениеАссортимента3.Ссылка = ИзменениеАссортимента4.ДокументОснование
	|			ПО ИзменениеАссортимента2.Ссылка = ИзменениеАссортимента3.ДокументОснование
	|		ПО ИзменениеАссортимента1.Ссылка = ИзменениеАссортимента2.ДокументОснование
	|ГДЕ
	|	ИзменениеАссортимента1.ДокументОснование = &ДокументОснование
	|	И ИзменениеАссортимента1.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаИА);
	Запрос.УстановитьПараметр("ДокументОснование", СсылкаАК);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Попытка
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка1) Тогда
				ВыборкаДетальныеЗаписи.Ссылка1.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка2) Тогда
				ВыборкаДетальныеЗаписи.Ссылка2.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка3) Тогда
				ВыборкаДетальныеЗаписи.Ссылка3.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка4) Тогда
				ВыборкаДетальныеЗаписи.Ссылка4.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			Возврат ВсЁОк;
		Исключение
			ВсЁОк = Ложь;
			ОписаниеОшибки = ОписаниеОшибки();
			РезультатыВыполнения.СообщитьОбОшибке("Не удалось УстановитьПометкуУдаления по причине:" + ОписаниеОшибки, СтруктураЗаписиЖурнала ,СтатусСообщения.Важное);
			Сообщить("Не удалось УстановитьПометкуУдаления по причине:" + ОписаниеОшибки, СтатусСообщения.Важное);
			Возврат ВсЁОк;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ЗаполнитьДокументАссортимента(СсылкаАК,ХозОперация,Номенклатура,тзПлощадки,СсылкаОснования,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк)
	
	ДокАссортимента = Документы.ИзменениеАссортимента.СоздатьДокумент();
	ДокАссортимента.Дата = ТекущаяДата();
	
	ДокАссортимента.ХозОперация = ХозОперация;
	ДокАссортимента.ПодразделениеКомпании = СсылкаАК.ПодразделениеКомпании;
	ДокАссортимента.ДатаВводаВАссортимент = ?(ЗначениеЗаполнено(СсылкаАК.ДатаНачалаДействия),СсылкаАК.ДатаНачалаДействия,СсылкаАК.Дата);
	ДокАссортимента.ДокументОснование = СсылкаОснования;
	
	//ДокАссортимента.Товары.Очистить();
	
	НоваяСтрока = ДокАссортимента.Товары.Добавить();
	НоваяСтрока.Номенклатура = Номенклатура;
	НоваяСтрока.ВидАссортимента = ?(ХозОперация = Справочники.ХозОперации.РаспродажаСПоследующимВыводомИзАссортимента,Справочники.ВидыАссортимента.ВнутреннийЗаказ,Справочники.ВидыАссортимента.Выводимый);
	
	Для Каждого СтрокаПлощадка из тзПлощадки Цикл
		НоваяСтрокаПлощадка = ДокАссортимента.ТорговыеПлощадки.Добавить();
		НоваяСтрокаПлощадка.ТорговаяПлощадка = СтрокаПлощадка.ТорговаяПлощадка;
	КонецЦикла;	
	
	ДокАссортимента.Комментарий = "АК "+СсылкаАК.Контрагент+Символы.ПС+Номенклатура+Символы.ПС+"Создан автоматически из Ассортиментного Комитета";
	ДокАссортимента.ДополнительныеСвойства.Вставить("ОбрабткаЗаданием",Истина);
	
	Попытка
		ДокАссортимента.Записать(РежимЗаписиДокумента.Проведение);	
		Возврат ДокАссортимента.Ссылка;
	Исключение
		ВсЁОк = Ложь;
		ОписаниеОшибки = ОписаниеОшибки();
		РезультатыВыполнения.СообщитьОбОшибке("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтруктураЗаписиЖурнала ,СтатусСообщения.Важное);
		Сообщить("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Процедура ЗаполнитьПоРаспродаже(ОбъектАК,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк)
	
	Для Каждого ТекСтрокаТовары из ОбъектАК.Товары Цикл                  
		Если ЗначениеЗаполнено(ТекСтрокаТовары.Номенклатура) Тогда
			ОтобранныеПлощадки = ОбъектАК.ПлощадкиПоСтроке.Выгрузить(Новый Структура("КлючСтроки", ТекСтрокаТовары.КлючСтроки),"ТорговаяПлощадка");
			
			Если ЗначениеЗаполнено(ТекСтрокаТовары.ДокументАссортимента) Тогда
				Если УдалимЦепочкуПодчиненных(ОбъектАК.Ссылка,ТекСтрокаТовары.ДокументАссортимента,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк) Тогда
				Иначе
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			вт = ПолучитьВидАссортимента(ОбъектАК.ДатаНачалаДействия,ТекСтрокаТовары.Номенклатура,ОтобранныеПлощадки);
			тзРаспродажа = вт.Скопировать(Новый Структура("Распродажа",Истина));
			тзВывод = вт.Скопировать(Новый Структура("Распродажа",Ложь));
			
			ЕстьРаспродажа = ЗначениеЗаполнено(тзРаспродажа);
			ЕстьВывод = ЗначениеЗаполнено(тзВывод);
			
			ДокАссСсылка = Неопределено;
			Если ЕстьВывод Тогда
				ДокАссСсылка = ЗаполнитьДокументАссортимента(ОбъектАК.Ссылка,Справочники.ХозОперации.ВыводТоваровИзАссортимента,ТекСтрокаТовары.Номенклатура,тзВывод,ОбъектАК.Ссылка,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк);
				Если ДокАссСсылка = Неопределено Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьРаспродажа Тогда
				ДокАссСсылкаР = ЗаполнитьДокументАссортимента(ОбъектАК.Ссылка,Справочники.ХозОперации.РаспродажаСПоследующимВыводомИзАссортимента,ТекСтрокаТовары.Номенклатура,тзРаспродажа,?(ЕстьВывод,ДокАссСсылка,ОбъектАК.Ссылка),РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк);
				Если ДокАссСсылкаР = Неопределено Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			Если ДокАссСсылка <> Неопределено Или ДокАссСсылкаР <> Неопределено Тогда
				ТекСтрокаТовары.ДокументАссортимента = ?(ДокАссСсылка = Неопределено,ДокАссСсылкаР,ДокАссСсылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьСтандартно(ОбъектАК,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк)
	
	Для Каждого ТекСтрокаТовары из ОбъектАК.Товары Цикл
		Если ЗначениеЗаполнено(ТекСтрокаТовары.Номенклатура) Тогда
			Если ЗначениеЗаполнено(ТекСтрокаТовары.ДокументАссортимента) Тогда
				ДокАссортимента = ТекСтрокаТовары.ДокументАссортимента.ПолучитьОбъект();
				Если ДокАссортимента.ПометкаУдаления Тогда
					ДокАссортимента.УстановитьПометкуУдаления(Ложь);
				КонецЕсли;
			Иначе
				ДокАссортимента = Документы.ИзменениеАссортимента.СоздатьДокумент();
				ДокАссортимента.Дата = ТекущаяДата();
			КонецЕсли;
			
			ДокАссортимента.ХозОперация = ?(ОбъектАК.ХозОперация = Справочники.ХозОперации.ВводНовогоТовараВАссортимент,Справочники.ХозОперации.ВводТоваровВАссортимент,ОбъектАК.ХозОперация);
			ДокАссортимента.ПодразделениеКомпании = ОбъектАК.ПодразделениеКомпании;
			ДокАссортимента.ДатаВводаВАссортимент = ?(ЗначениеЗаполнено(ОбъектАК.ДатаНачалаДействия),ОбъектАК.ДатаНачалаДействия,ОбъектАК.Дата);
			ДокАссортимента.ДокументОснование = ОбъектАК.Ссылка;
			
			ДокАссортимента.Товары.Очистить();
			
			НоваяСтрока = ДокАссортимента.Товары.Добавить();
			НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
			Если ОбъектАК.ХозОперация = Справочники.ХозОперации.РаспродажаСПоследующимВыводомИзАссортимента Тогда
				НоваяСтрока.ВидАссортимента = Справочники.ВидыАссортимента.Распродажа;
			ИначеЕсли ОбъектАК.ХозОперация = Справочники.ХозОперации.ВыводТоваровИзАссортимента Тогда
				НоваяСтрока.ВидАссортимента = Справочники.ВидыАссортимента.Выводимый;
			Иначе
				НоваяСтрока.БазовыйАссортимент = ТекСтрокаТовары.БазовыйАссортимент;
				НоваяСтрока.ВидАссортимента = ТекСтрокаТовары.ВидАссортимента;
			КонецЕсли;
			
			ДокАссортимента.ТорговыеПлощадки.Очистить();
			
			ОтобранныеСтрокиПлощадок = ОбъектАК.ПлощадкиПоСтроке.НайтиСтроки(Новый Структура("КлючСтроки", ТекСтрокаТовары.КлючСтроки));
			
			Для Каждого ТекСтрокаПлощадка из ОтобранныеСтрокиПлощадок Цикл
				НоваяСтрокаПлощадка = ДокАссортимента.ТорговыеПлощадки.Добавить();
				НоваяСтрокаПлощадка.ТорговаяПлощадка = ТекСтрокаПлощадка.ТорговаяПлощадка;
			КонецЦикла;	
			
			//ДокАссортимента.УстановитьНовыйНомер();  СообщитьОбОшибке
			ДокАссортимента.Комментарий = "АК "+ОбъектАК.Контрагент+Символы.ПС+ТекСтрокаТовары.Номенклатура+Символы.ПС+"Создан автоматически из Ассортиментного Комитета";
			ДокАссортимента.ДополнительныеСвойства.Вставить("ОбрабткаЗаданием",Истина);
			
			Попытка
				ДокАссортимента.Записать(РежимЗаписиДокумента.Проведение);	
				ТекСтрокаТовары.ДокументАссортимента = ДокАссортимента.Ссылка;
			Исключение
				ВсЁОк = Ложь;
				ОписаниеОшибки = ОписаниеОшибки();
				РезультатыВыполнения.СообщитьОбОшибке("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтруктураЗаписиЖурнала ,СтатусСообщения.Важное);
				Сообщить("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтатусСообщения.Важное);
				Прервать;
			КонецПопытки;
			
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры

Функция ОбработатьАссортиментныйКомитет()	Экспорт
	
	ВсЁОк = Истина;
	
	РезультатыВыполнения = РегистрыСведений.РезультатыВыполненияОбработок;
	СтруктураЗаписиЖурнала = РезультатыВыполнения.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = "ОбработатьАссортиментныйКомитет";
	СтруктураЗаписиЖурнала.КлючОбработки = "ОбработкаАссортиментногоКомитета";
	СтруктураЗаписиЖурнала.РегистрироватьРезультаты = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	|	АссортиментныйКомитет.Ссылка КАК Ссылка,
	|	АссортиментныйКомитет.Ссылка.ХозОперация КАК ХозОперация
	|ИЗ
	|	Документ.АссортиментныйКомитет КАК АссортиментныйКомитет
	|ГДЕ
	|	НЕ АссортиментныйКомитет.Ссылка.ПометкаУдаления
	|	И АссортиментныйКомитет.Статус = &Статус
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыАссортиментногоКомитета.Утвержден);
	
	Результат = Запрос.Выполнить();	//	Результат.Выгрузить()
	
	Выборка = Результат.Выбрать();
	
	РезультатыВыполнения.ДобавитьКомментарий("Начало обработки. 	Количество найденных документов		- "+Формат(Выборка.Количество(),"ЧН=0; ЧГ="), СтруктураЗаписиЖурнала);
	ОбработаноДокументов = 0;
	
	Пока Выборка.Следующий() Цикл
		//НачатьТранзакцию();
		ДокОбъектАК = Выборка.Ссылка.ПолучитьОбъект();
		ДокОбъектАК.ДополнительныеСвойства.Вставить("ОбрабткаЗаданием",Истина);
		ДокОбъектАК.Статус = Перечисления.СтатусыАссортиментногоКомитета.Обработан;
		
		Если Выборка.ХозОперация = Справочники.ХозОперации.РаспродажаСПоследующимВыводомИзАссортимента Тогда
			ЗаполнитьПоРаспродаже(ДокОбъектАК,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк);
		Иначе
			ЗаполнитьСтандартно(ДокОбъектАК,РезультатыВыполнения,СтруктураЗаписиЖурнала,ВсЁОк);
		КонецЕсли;
		
		Если НЕ ВсЁОк Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			ДокОбъектАК.Записать();	
			ОбработаноДокументов = ОбработаноДокументов + 1;
		Исключение
			ВсЁОк = Ложь;
			ОписаниеОшибки = ОписаниеОшибки();
			РезультатыВыполнения.СообщитьОбОшибке("Не удалось провести документ " + ДокОбъектАК + " по причине:" + ОписаниеОшибки, СтруктураЗаписиЖурнала ,СтатусСообщения.Важное);
			Сообщить("Не удалось провести документ " + ДокОбъектАК + " по причине:" + ОписаниеОшибки, СтатусСообщения.Важное);
		КонецПопытки;
		
		//Если ВсЁОк Тогда
		//	ЗафиксироватьТранзакцию();
		//КонецЕсли;
		
	КонецЦикла;
	
	РезультатыВыполнения.ДобавитьКомментарий("Конец обработки. 	Количество обработанных документов	- "+Формат(ОбработаноДокументов,"ЧН=0; ЧГ="), СтруктураЗаписиЖурнала);
	
	Попытка 
		РезультатыВыполнения.ЗаписатьЖурналВыполненияОбработок(СтруктураЗаписиЖурнала, ВсЁОк);
	Исключение
		//
	КонецПопытки;
	
КонецФункции

Функция ВывестиРаспроданныйТовар(НаДату)	Экспорт
	
	ВсЁОк = Истина;
	
	РезультатыВыполнения = РегистрыСведений.РезультатыВыполненияОбработок;
	СтруктураЗаписиЖурнала = РезультатыВыполнения.СформироватьСтруктуруЗаписиРезультатов();
	СтруктураЗаписиЖурнала.ИдентификаторОбработки = "ВывестиРаспроданныйТовар";
	СтруктураЗаписиЖурнала.КлючОбработки = "ВыводРаспроданногоТовара";
	СтруктураЗаписиЖурнала.РегистрироватьРезультаты = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	|	ОграничениеАссортиментаСрезПоследних.Регистратор КАК Регистратор,
	|	ОграничениеАссортиментаСрезПоследних.Регистратор.ХозОперация КАК ХозОперация,
	|	ОграничениеАссортиментаСрезПоследних.Номенклатура КАК Номенклатура,
	|	ОграничениеАссортиментаСрезПоследних.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ОграничениеАссортиментаСрезПоследних.Состояние КАК Состояние,
	|	ОграничениеАссортиментаСрезПоследних.ВидАссортимента КАК ВидАссортимента
	|ПОМЕСТИТЬ ТоварРаспродажи
	|ИЗ
	|	РегистрСведений.ОграничениеАссортимента.СрезПоследних(&НаДату, Регистратор ССЫЛКА Документ.ИзменениеАссортимента) КАК ОграничениеАссортиментаСрезПоследних
	|ГДЕ
	|	ОграничениеАссортиментаСрезПоследних.Состояние = 2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкладыКомпании.Ссылка КАК Ссылка,
	|	СкладыКомпании.АссортиментнаяМатрица КАК АссортиментнаяМатрица
	|ПОМЕСТИТЬ СкладыРЦ
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.АссортиментнаяМатрица = &АссортиментнаяМатрица
	|	И НЕ СкладыКомпании.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	СУММА(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиРЦ
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|			&НаДату,
	|			СкладКомпании В
	|					(ВЫБРАТЬ
	|						СкладыРЦ.Ссылка КАК Ссылка
	|					ИЗ
	|						СкладыРЦ КАК СкладыРЦ)
	|				И Номенклатура В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ТоварРаспродажи.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ТоварРаспродажи КАК ТоварРаспродажи)) КАК ОстаткиТоваровКомпанииОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварРаспродажи.Регистратор КАК Регистратор,
	|	ТоварРаспродажи.ХозОперация КАК ХозОперация,
	|	ТоварРаспродажи.Регистратор.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ТоварРаспродажи.Номенклатура КАК Номенклатура,
	|	ТоварРаспродажи.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТоварРаспродажи.Состояние КАК Состояние,
	|	ТоварРаспродажи.ВидАссортимента КАК ВидАссортимента,
	|	ЕСТЬNULL(ОстаткиРЦ.КоличествоОстаток, 0) КАК Остаток
	|ИЗ
	|	ТоварРаспродажи КАК ТоварРаспродажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиРЦ КАК ОстаткиРЦ
	|		ПО ТоварРаспродажи.Номенклатура = ОстаткиРЦ.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиРЦ.КоличествоОстаток, 0) <= 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор
	|ИТОГИ
	|	МАКСИМУМ(Номенклатура)
	|ПО
	|	Регистратор
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("АссортиментнаяМатрица", Справочники.АссортиментнаяМатрица.НайтиПоКоду("AT000005"));
	
	Результат = Запрос.Выполнить();	//	Результат.Выгрузить()
	
	ВыборкаРегистратор = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	РезультатыВыполнения.ДобавитьКомментарий("Начало обработки. 	Количество найденных документов		- "+Формат(ВыборкаРегистратор.Количество(),"ЧН=0; ЧГ="), СтруктураЗаписиЖурнала);
	ОбработаноДокументов = 0;
	
	Пока ВыборкаРегистратор.Следующий() Цикл
		ДокАссортимента = Документы.ИзменениеАссортимента.СоздатьДокумент();
		ДокАссортимента.Дата = ТекущаяДата();
		
		ДокАссортимента.ХозОперация = Справочники.ХозОперации.ВыводТоваровИзАссортимента;
		ДокАссортимента.ПодразделениеКомпании = ВыборкаРегистратор.ПодразделениеКомпании;
		ДокАссортимента.ДатаВводаВАссортимент = НаДату;
		ДокАссортимента.ДокументОснование = ВыборкаРегистратор.Регистратор;
		
		НоваяСтрока = ДокАссортимента.Товары.Добавить();
		НоваяСтрока.Номенклатура = ВыборкаРегистратор.Номенклатура;
		НоваяСтрока.ВидАссортимента = Справочники.ВидыАссортимента.Выводимый;
		
		Выборка = ВыборкаРегистратор.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрокаПлощадка = ДокАссортимента.ТорговыеПлощадки.Добавить();
			НоваяСтрокаПлощадка.ТорговаяПлощадка = Выборка.ТорговаяПлощадка;
		КонецЦикла;	
		
		ДокАссортимента.Комментарий = "Распродано "+ВыборкаРегистратор.Номенклатура+Символы.ПС+"Создан автоматически после распродажи товара";
		ДокАссортимента.ДополнительныеСвойства.Вставить("ОбрабткаЗаданием",Истина);
		
		Попытка
			ДокАссортимента.Записать(РежимЗаписиДокумента.Проведение);	
			ОбработаноДокументов = ОбработаноДокументов + 1;
		Исключение
			ВсЁОк = Ложь;
			ОписаниеОшибки = ОписаниеОшибки();
			РезультатыВыполнения.СообщитьОбОшибке("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтруктураЗаписиЖурнала ,СтатусСообщения.Важное);
			Сообщить("Не удалось провести документ " + ДокАссортимента + " по причине:" + ОписаниеОшибки, СтатусСообщения.Важное);
		КонецПопытки;
		
	КонецЦикла;
	
	РезультатыВыполнения.ДобавитьКомментарий("Конец обработки. 	Количество обработанных документов	- "+Формат(ОбработаноДокументов,"ЧН=0; ЧГ="), СтруктураЗаписиЖурнала);
	
	Попытка 
		РезультатыВыполнения.ЗаписатьЖурналВыполненияОбработок(СтруктураЗаписиЖурнала, ВсЁОк);
	Исключение
		//
	КонецПопытки;
	
КонецФункции

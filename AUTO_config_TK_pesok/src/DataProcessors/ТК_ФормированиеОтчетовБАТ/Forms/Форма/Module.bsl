
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодОтчета.Вариант = ВариантСтандартногоПериода.ПрошлаяНеделя;
	ИсточникДанных = "Все";
	
	ЗагрузитьНастройкиОтбораПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
		
	НастройкиОтбора = Настройки.Получить("НастройкиОтбора");
	Если НастройкиОтбора <> Неопределено Тогда 
		Попытка
			КомпоновщикНастроекОтборы.ЗагрузитьНастройки(НастройкиОтбора.Получить());
		Исключение КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	НастройкиОтбора = Новый ХранилищеЗначения(КомпоновщикНастроекОтборы.ПолучитьНастройки(), Новый СжатиеДанных(9));
	Настройки.Вставить("НастройкиОтбора", НастройкиОтбора);
	
КонецПроцедуры


#КонецОбласти

 
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКаталогаДокументовЗавершение", ЭтаФорма);
	ДиалогОткрытия = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);		
	ДиалогОткрытия.Заголовок = НСтр("ru = 'Выберите каталог сохранения .csv файлов'; uk = 'Оберіть каталог збереження .csv файлів'");
	ДиалогОткрытия.Каталог = КаталогВыгрузки;
	
	ДиалогОткрытия.Показать(ОписаниеОповещения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаДокументовЗавершение(ИмяКаталога, ДопПараметры) Экспорт 
	
	Если ТипЗнч(ИмяКаталога) = Тип("Массив") И ИмяКаталога.Количество() > 0 Тогда 
		КаталогВыгрузки = ИмяКаталога[0];
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчеты(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	СписокОтчетов = Новый Структура;
	Если глОтчетОптовыеПродажи Тогда 
		СписокОтчетов.Вставить("ОтчетОптовыеПродажи");
	КонецЕсли;	
	
	Если глОтчетДетальныеПродажи Тогда 
		СписокОтчетов.Вставить("ОтчетДетальныеПродажи");
	КонецЕсли;
	
	Если глОтчетПриходПродукции Тогда 
		СписокОтчетов.Вставить("ОтчетПриходПродукции");
	КонецЕсли;
	
	Если глСправочникТорговыеТочки Тогда 
		СписокОтчетов.Вставить("СправочникТорговыеТочки");
	КонецЕсли;
	
	Если глСправочникПродукция Тогда 
		СписокОтчетов.Вставить("СправочникПродукция");
	КонецЕсли;
	
	Если глСправочникСклады Тогда 
		СписокОтчетов.Вставить("СправочникСклады");
	КонецЕсли;
	
	Если СписокОтчетов.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран ни один отчет'; uk = 'Не обрано жоднго звіту'"));
		Возврат;
	КонецЕсли;
		
	ПараметрыОтчетов = Новый Структура;
	ПараметрыОтчетов.Вставить("НачалоПериода", ПериодОтчета.ДатаНачала);
	ПараметрыОтчетов.Вставить("КонецПериода", ПериодОтчета.ДатаОкончания);
	ПараметрыОтчетов.Вставить("ИзВнешнегоИсточника", (ИсточникДанных = "Все" ИЛИ ИсточникДанных = "Внешний"));
	ПараметрыОтчетов.Вставить("ИзВнутреннегоИсточника", (ИсточникДанных = "Все" ИЛИ ИсточникДанных = "Внутренний"));
	
	СтруктураОтборов = Новый Структура;
	Для Каждого Отбор Из КомпоновщикНастроекОтборы.Настройки.Отбор.Элементы Цикл 
		
		ПолноеИмяОтбора = Строка(Отбор.ЛевоеЗначение);
		
		Если Не Отбор.Использование ИЛИ ПустаяСтрока(ПолноеИмяОтбора) Тогда 
			Продолжить;
		КонецЕсли;
		
		тСтруктураОтбора = Новый Структура;
		тСтруктураОтбора.Вставить("ИмяПоля", ПолноеИмяОтбора);
		тСтруктураОтбора.Вставить("ПравоеЗначение", Отбор.ПравоеЗначение);
		тСтруктураОтбора.Вставить("ВидСравнения", Отбор.ВидСравнения);
		
		ИмяОснРеквизита = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмяОтбора, ".", Истина, Истина).Получить(0);
		Если Не СтруктураОтборов.Свойство(ИмяОснРеквизита) Тогда 	
			СтруктураОтборов.Вставить(ИмяОснРеквизита, Новый Массив);
		КонецЕсли;
		
		СтруктураОтборов[ИмяОснРеквизита].Добавить(тСтруктураОтбора);
	КонецЦикла;	
	ПараметрыОтчетов.Вставить("Отборы", СтруктураОтборов);		
	
	ДлительнаяОперация = НачатьДлительнуюОперацию(СписокОтчетов, ПараметрыОтчетов);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Формируем отчет'; uk = 'Формуємо звіт'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ФормированиеОтчетовБАТ";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 1;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеДлительнойОперации", ЭтотОбъект, Неопределено);	

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиОтборовПоУмолчанию(Команда)
	ЗагрузитьНастройкиОтбораПоУмолчанию();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройкиОтбораПоУмолчанию()

	СхемаКомпоновкиДанных = Обработки.ТК_ФормированиеОтчетовБАТ.ПолучитьМакет("ПоляОтбора");
	КомпоновщикНастроекОтборы.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));
	КомпоновщикНастроекОтборы.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);	
	
	
КонецПроцедуры

&НаСервере
Функция НачатьДлительнуюОперацию(СписокОтчетов, Знач ПараметрыОтчетов)
			
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ФормированиеОтчетовБАТ.СформироватьОтчеты", СписокОтчетов, ПараметрыОтчетов);
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеДлительнойОперации(Операция, ПараметрыОтчета) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		Если Операция.Свойство("АдресРезультата") Тогда 			
			
			Результаты = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);
			
			Если ТипЗнч(Результаты) = Тип("Структура") Тогда 
				
				Для Каждого текРезультат Из Результаты Цикл 
					ТабличныйДокумент = текРезультат.Значение.Документ;
					ИмяФайла = текРезультат.Значение.ИмяФайла;					
					ПолноеИмяФайла = КаталогВыгрузки + "\" + ИмяФайла;
					
					Попытка
						ТабличныйДокумент.Записать(ПолноеИмяФайла, "Windows-1251");
					Исключение
						ОбщегоНазначенияКлиент.СообщитьПользователю(
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось записать файл ""%1"". Ошибки: %2'; uk = 'Не вдалося записати файл ""%1"". Помилки: %2'"),
								ИмяФайла,
								ОписаниеОшибки()));
					КонецПопытки;
				КонецЦикла;					
			КонецЕсли;			
			
		КонецЕсли;
				
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;

	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции


#КонецОбласти

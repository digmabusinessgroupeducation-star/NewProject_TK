
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область ПрограммныйИнтерфейс
	
	Функция СформироватьОтчеты(Знач СписокОтчетов, Знач ПараметрыОтчетов) Экспорт 
		
		текДата 			= ТекущаяДата();
		НачалоПериода 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчетов, "НачалоПериода", НачалоДня(текДата));
		КонецПериода		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчетов, "КонецПериода", КонецДня(текДата));
		
		Выполнено = 0;
		КолвоОтчетов = СписокОтчетов.Количество();
		
		ДлительныеОперации.СообщитьПрогресс(0, НСтр("ru = 'Подготовка данных для отчетов'; uk = 'Підготовка даних для звітів'"));
		
		
		МенеджерВременныхТаблиц = ПолучитьТаблицыВнешнегоИсточника(СписокОтчетов, ПараметрыОтчетов);
		ПараметрыОтчетов.Вставить("Результат", Новый Структура);	
		
		Если СписокОтчетов.Свойство("ОтчетОптовыеПродажи") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Оптовые продажи'; uk = 'Оптові продажі'"),
			Выполнено + 1,
			КолвоОтчетов));
			
			СформироватьОтчетОптовыеПродажи(НачалоПериода, КонецПериода, ПараметрыОтчетов, МенеджерВременныхТаблиц);
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("ОтчетДетальныеПродажи") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Детальные продажи'; uk = 'Детальні продажі'"),
			Выполнено + 1,
			КолвоОтчетов));
			
			СформироватьОтчетДетальныеПродажи(НачалоПериода, КонецПериода, ПараметрыОтчетов, МенеджерВременныхТаблиц);
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("ОтчетПриходПродукции") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Приход продукции'; uk = 'Надоходження продукції'"),
			Выполнено + 1,
			КолвоОтчетов));
			
			СформироватьОтчетПриходПродукции(НачалоПериода, КонецПериода, ПараметрыОтчетов);				
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникТорговыеТочки") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Справочник торговые точки'; uk = 'Довідник торгові точки'"),
			Выполнено + 1,
			КолвоОтчетов));		
			
			СформироватьОтчетСправочникТорговыеТочки(НачалоПериода, КонецПериода, ПараметрыОтчетов, МенеджерВременныхТаблиц);
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникПродукция") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Справочник продукция'; uk = 'Довідник продукція'"),
			Выполнено + 1,
			КолвоОтчетов));		
			
			СформироватьОтчетСправочникПродукция(ПараметрыОтчетов);
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникСклады") Тогда 
			
			ДлительныеОперации.СообщитьПрогресс(Цел(Выполнено/(КолвоОтчетов + 1) * 100), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Формируется отчет ""%1""
			|%2 из %3'; uk = 'Формується звіт ""%1""
			|%2 із %3'"),
			НСтр("ru = 'Справочник склады'; uk = 'Довідник склади'"),
			Выполнено + 1,
			КолвоОтчетов));		
			
			СформироватьОтчетСправочникСклады(НачалоПериода, КонецПериода, ПараметрыОтчетов);
			
			Выполнено = Выполнено + 1;
		КонецЕсли;
		
		
		Возврат ПараметрыОтчетов.Результат;
		
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область СлужебныеПроцедурыИФункции
	
	Функция ПолучитьТаблицыВнешнегоИсточника(СписокОтчетов, ПараметрыОтчетов)
		
		СтруктураВнешТаблиц = ПолучитьСтруктуруВнешнихТаблиц(СписокОтчетов);
		Если СтруктураВнешТаблиц.Количество() = 0 Тогда 
			ПараметрыОтчетов.Вставить("ИзВнешнегоИсточника", Ложь);	
			Возврат Неопределено;
		КонецЕсли;
		
		ОшибкиПодключения = "";
		Подключено = ПодключениеКВнешнемуИсточнику(ОшибкиПодключения);
		Если Не Подключено Тогда 
			ОбщегоНазначения.СообщитьПользователю(ОшибкиПодключения);
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данные из внешнего источника не получены'; uk = 'Дані із зовнішнього джерела не отримані'"));
			ПараметрыОтчетов.Вставить("ИзВнешнегоИсточника", Ложь);	
			Возврат Неопределено;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		НачалоПериода 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчетов, "НачалоПериода", НачалоДня(ТекущаяДата()));
		КонецПериода		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчетов, "КонецПериода", КонецДня(ТекущаяДата()));	
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Если СтруктураВнешТаблиц.Свойство("dbo_vSTOCKS") Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vSTOCKS.PERIOD КАК PERIOD,
			|	dbo_vSTOCKS.RECORDER КАК RECORDER,
			|	dbo_vSTOCKS.WHOUSEID КАК WHOUSEID,
			|	dbo_vSTOCKS.PRODUCTID КАК PRODUCTID,
			|	dbo_vSTOCKS.BATCODE КАК BATCODE,
			|	dbo_vSTOCKS.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vSTOCKS.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vSTOCKS.STOCK_PACKS КАК STOCK_PACKS,
			|	dbo_vSTOCKS.PRODUCTEXTID КАК PRODUCTEXTID
			|ИЗ
			|	ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vSTOCKS КАК dbo_vSTOCKS
			|ГДЕ
			|	dbo_vSTOCKS.PERIOD = &PERIOD";
			
			Запрос.УстановитьПараметр("PERIOD", КонецДня(КонецПериода) + 1);
			ТЧ = Запрос.Выполнить().Выгрузить();
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Таблица", ТЧ);
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vSTOCKS.PERIOD КАК PERIOD,
			|	dbo_vSTOCKS.RECORDER КАК RECORDER,
			|	dbo_vSTOCKS.WHOUSEID КАК WHOUSEID,
			|	dbo_vSTOCKS.PRODUCTID КАК PRODUCTID,
			|	dbo_vSTOCKS.BATCODE КАК BATCODE,
			|	dbo_vSTOCKS.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vSTOCKS.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vSTOCKS.STOCK_PACKS КАК STOCK_PACKS,
			|	dbo_vSTOCKS.PRODUCTEXTID КАК PRODUCTEXTID
			|ПОМЕСТИТЬ dbo_vSTOCKS
			|ИЗ
			|	&Таблица КАК dbo_vSTOCKS";
			
			Запрос.Выполнить();
			
		КонецЕсли;
		
		Если СтруктураВнешТаблиц.Свойство("dbo_vBranchSales") Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vBranchSales.Period КАК Period,
			|	dbo_vBranchSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vBranchSales.WHOUSEGUID КАК WHOUSEGUID,
			|	dbo_vBranchSales.PRODUCTID КАК PRODUCTID,
			|	dbo_vBranchSales.PRODUCTGUID КАК PRODUCTGUID,
			|	dbo_vBranchSales.PRODUCTEXTID КАК PRODUCTEXTID,
			|	dbo_vBranchSales.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vBranchSales.BATCODE КАК BATCODE,
			|	dbo_vBranchSales.BARCODES КАК BARCODES,
			|	dbo_vBranchSales.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vBranchSales.SALES_PACKS КАК SALES_PACKS,
			|	dbo_vBranchSales.SALES_QTY КАК SALES_QTY,
			|	dbo_vBranchSales.SALES_SUM КАК SALES_SUM
			|ИЗ
			|	ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vBranchSales КАК dbo_vBranchSales
			|ГДЕ
			|	dbo_vBranchSales.Period МЕЖДУ &НачПериод И &КонПериод";
			
			Запрос.УстановитьПараметр("НачПериод", НачалоПериода);
			Запрос.УстановитьПараметр("КонПериод", КонецПериода);
			ТЧ = Запрос.Выполнить().Выгрузить();
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Таблица", ТЧ);
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vBranchSales.Period КАК Period,
			|	dbo_vBranchSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vBranchSales.WHOUSEGUID КАК WHOUSEGUID,
			|	dbo_vBranchSales.PRODUCTID КАК PRODUCTID,
			|	dbo_vBranchSales.PRODUCTGUID КАК PRODUCTGUID,
			|	dbo_vBranchSales.PRODUCTEXTID КАК PRODUCTEXTID,
			|	dbo_vBranchSales.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vBranchSales.BATCODE КАК BATCODE,
			|	dbo_vBranchSales.BARCODES КАК BARCODES,
			|	dbo_vBranchSales.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vBranchSales.SALES_PACKS КАК SALES_PACKS,
			|	dbo_vBranchSales.SALES_QTY КАК SALES_QTY,
			|	dbo_vBranchSales.SALES_SUM КАК SALES_SUM
			|ПОМЕСТИТЬ dbo_vBranchSales
			|ИЗ
			|	&Таблица КАК dbo_vBranchSales";
			
			Запрос.Выполнить();
			
		КонецЕсли;
		
		Если СтруктураВнешТаблиц.Свойство("dbo_vRetailSales") Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vRetailSales.Period КАК Period,
			|	dbo_vRetailSales.OUTLETID КАК OUTLETID,
			|	dbo_vRetailSales.OUTLETGUID КАК OUTLETGUID,
			|	dbo_vRetailSales.OUTLETEXTID КАК OUTLETEXTID,
			|	dbo_vRetailSales.OUTLETADDRESS КАК OUTLETADDRESS,
			|	dbo_vRetailSales.CITYCODE КАК CITYCODE,
			|	dbo_vRetailSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vRetailSales.WHOUSEGUID КАК WHOUSEGUID,
			|	dbo_vRetailSales.PRODUCTGUID КАК PRODUCTGUID,
			|	dbo_vRetailSales.PRODUCTEXTID КАК PRODUCTEXTID,
			|	dbo_vRetailSales.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vRetailSales.BATCODE КАК BATCODE,
			|	dbo_vRetailSales.BARCODES КАК BARCODES,
			|	dbo_vRetailSales.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vRetailSales.SALES_QTY КАК SALES_QTY,
			|	dbo_vRetailSales.SALES_PACKS КАК SALES_PACKS,
			|	dbo_vRetailSales.SALES_SUM КАК SALES_SUM
			|ИЗ
			|	ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vRetailSales КАК dbo_vRetailSales
			|ГДЕ
			|	dbo_vRetailSales.Period МЕЖДУ &НачПериод И &КонПериод";
			
			Запрос.УстановитьПараметр("НачПериод", НачалоПериода);
			Запрос.УстановитьПараметр("КонПериод", КонецПериода);
			ТЧ = Запрос.Выполнить().Выгрузить();
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Таблица", ТЧ);
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vRetailSales.Period КАК Period,
			|	dbo_vRetailSales.OUTLETID КАК OUTLETID,
			|	dbo_vRetailSales.OUTLETGUID КАК OUTLETGUID,
			|	dbo_vRetailSales.OUTLETEXTID КАК OUTLETEXTID,
			|	dbo_vRetailSales.OUTLETADDRESS КАК OUTLETADDRESS,
			|	dbo_vRetailSales.CITYCODE КАК CITYCODE,
			|	dbo_vRetailSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vRetailSales.WHOUSEGUID КАК WHOUSEGUID,
			|	dbo_vRetailSales.PRODUCTGUID КАК PRODUCTGUID,
			|	dbo_vRetailSales.PRODUCTEXTID КАК PRODUCTEXTID,
			|	dbo_vRetailSales.PRODUCTNAME КАК PRODUCTNAME,
			|	dbo_vRetailSales.BATCODE КАК BATCODE,
			|	dbo_vRetailSales.BARCODES КАК BARCODES,
			|	dbo_vRetailSales.MRP_PRICE КАК MRP_PRICE,
			|	dbo_vRetailSales.SALES_QTY КАК SALES_QTY,
			|	dbo_vRetailSales.SALES_PACKS КАК SALES_PACKS,
			|	dbo_vRetailSales.SALES_SUM КАК SALES_SUM
			|ПОМЕСТИТЬ dbo_vRetailSales
			|ИЗ
			|	&Таблица КАК dbo_vRetailSales";
			
			Запрос.Выполнить();
			
		КонецЕсли;
		
		Если СтруктураВнешТаблиц.Свойство("dbo_vOUTLETS") Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vOUTLETS.OUTLETID КАК OUTLETID,
			|	dbo_vOUTLETS.OUTLETGUID КАК OUTLETGUID,
			|	dbo_vOUTLETS.PARTNERNAME КАК PARTNERNAME,
			|	dbo_vOUTLETS.ZKPO КАК ZKPO,
			|	dbo_vOUTLETS.Area КАК Area,
			|	dbo_vOUTLETS.Territory КАК Territory,
			|	dbo_vOUTLETS.City КАК City,
			|	dbo_vOUTLETS.CityType КАК CityType,
			|	dbo_vOUTLETS.Street КАК Street,
			|	dbo_vOUTLETS.Home КАК Home,
			|	dbo_vOUTLETS.OUTLETEXTID КАК OUTLETEXTID,
			|	dbo_vOUTLETS.CITYCODE КАК CITYCODE
			|ИЗ
			|	ВнешнийИсточникДанных.NIELSENBIS.Таблица.dbo_vOUTLETS КАК dbo_vOUTLETS";
			
			ТЧ = Запрос.Выполнить().Выгрузить();
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Таблица", ТЧ);
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vOUTLETS.OUTLETID КАК OUTLETID,
			|	dbo_vOUTLETS.OUTLETGUID КАК OUTLETGUID,
			|	dbo_vOUTLETS.PARTNERNAME КАК PARTNERNAME,
			|	dbo_vOUTLETS.ZKPO КАК ZKPO,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.Area КАК СТРОКА(120)) КАК Area,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.Territory КАК СТРОКА(120)) КАК Territory,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.City КАК СТРОКА(120)) КАК City,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.CityType КАК СТРОКА(100)) КАК CityType,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.Street КАК СТРОКА(120)) КАК Street,
			|	ВЫРАЗИТЬ(dbo_vOUTLETS.Home КАК СТРОКА(20)) КАК Home,
			|	dbo_vOUTLETS.OUTLETEXTID КАК OUTLETEXTID,
			|	dbo_vOUTLETS.CITYCODE КАК CITYCODE
			|ПОМЕСТИТЬ dbo_vOUTLETS
			|ИЗ
			|	&Таблица КАК dbo_vOUTLETS";
			
			
			Запрос.Выполнить();
			
		КонецЕсли;
		
		
		Возврат МенеджерВременныхТаблиц;
		
	КонецФункции
	
	Функция СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, ПараметрыСКД = Неопределено, Отборы = Неопределено, СоответствиеПолейОтбора = Неопределено, Ошибки = "")
		
		ТаблицаРезультат = Новый ТаблицаЗначений; 		
		
		//Устанавливаем параметры
		Если ПараметрыСКД <> Неопределено Тогда 
			Для Каждого знПараметр Из ПараметрыСКД Цикл 
				нПараметр = Настройки.ПараметрыДанных.Элементы.Найти(знПараметр.Ключ);
				Если нПараметр <> Неопределено Тогда 
					нПараметр.Использование = Истина;
					нПараметр.Значение = знПараметр.Значение;			
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		//Устанавливаем отборы			
		Если Отборы <> Неопределено И СоответствиеПолейОтбора <> Неопределено Тогда 
			Для Каждого Стр Из СоответствиеПолейОтбора Цикл 
				ИмяПоляОтбора = Стр.Ключ;
				ИмяПоляСКД = Стр.Значение;
				
				мСтрОтборы = Неопределено;
				Если Отборы.Свойство(ИмяПоляОтбора, мСтрОтборы) Тогда 
					Для Каждого стрОтбор Из мСтрОтборы Цикл 
						
						Если ЗначениеЗаполнено(ИмяПоляСКД) И ИмяПоляОтбора <> ИмяПоляСКД Тогда 
							мСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(стрОтбор.ИмяПоля, ".", Истина, Истина);
							ИмяПоля = "";
							Для Ит = 0 По мСлов.ВГраница() Цикл 
								Если Ит = 0 Тогда 
									ИмяПоля = ИмяПоляСКД;
								Иначе
									ИмяПоля = ИмяПоля + "." + мСлов.Получить(Ит);
								КонецЕсли;
							КонецЦикла;
						Иначе
							ИмяПоля = стрОтбор.ИмяПоля;
						КонецЕсли;
						
						ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Настройки.Отбор, ИмяПоля, стрОтбор.ПравоеЗначение, стрОтбор.ВидСравнения,, Истина);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных; 
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпновки)); 
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки); 
		
		НастройкиКомпоновщика = КомпоновщикНастроек.Настройки; 
		ПараметрыНастройки = Настройки.ПараметрыДанных; 
		
		//Получим макет компоновки    
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; 
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпновки, НастройкиКомпоновщика,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")); 
		
		//Через процессор компоновки получим результат
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных); 
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;	
		
		ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат); 
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 
		
		Возврат ТаблицаРезультат;
		
	КонецФункции
	
	
	Процедура СформироватьОтчетОптовыеПродажи(НачалоПериода, КонецПериода, Параметры, Знач МенеджерВременныхТаблиц)
		
		текДата 				= ТекущаяДата();	
		ИзВнутреннегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнутреннегоИсточника", Ложь);
		ИзВнешнегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнешнегоИсточника", Ложь);
		
		Если Не ИзВнутреннегоИсточника И Не ИзВнешнегоИсточника Тогда 
			Возврат;
		КонецЕсли;
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		ТаблицаРезультат.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
		ТаблицаРезультат.Колонки.Добавить("BranchID");
		ТаблицаРезультат.Колонки.Добавить("BATCodeSKU");
		ТаблицаРезультат.Колонки.Добавить("ClientNameSKU");
		ТаблицаРезультат.Колонки.Добавить("STOCK_PACKS", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
		ТаблицаРезультат.Колонки.Добавить("PRICE_MRP", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
		ТаблицаРезультат.Колонки.Добавить("SALES_PACKS", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));	
		
		Если ИзВнешнегоИсточника И МенеджерВременныхТаблиц <> Неопределено Тогда 				
			
			мИсключенияБАТ = Новый Массив;
			мИсключенияБАТ.Добавить("ITOTALIMPERIAL");
			мИсключенияБАТ.Добавить("JTOTALJTI001");
			мИсключенияБАТ.Добавить("LTOTALLTF001");
			мИсключенияБАТ.Добавить("LTOTALLTFCIGARILLOS001");
			мИсключенияБАТ.Добавить("PTOTALHEETS001");
			мИсключенияБАТ.Добавить("PTOTALPMI001");
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("мИсключенияБАТ", мИсключенияБАТ);
			
			Запрос.Текст = "ВЫБРАТЬ
			|	ВложенныйЗапрос.WHOUSEID КАК WHOUSEID,
			|	ВложенныйЗапрос.BATCODE КАК BATCODE,
			|	ВложенныйЗапрос.MRP_PRICE КАК MRP_PRICE,
			|	СУММА(ВложенныйЗапрос.SALES_PACKS) КАК SALES_PACKS,
			|	СУММА(ВложенныйЗапрос.STOCK_PACKS) КАК STOCK_PACKS,
			|	ВложенныйЗапрос.PRODUCTNAME КАК PRODUCTNAME
			|ПОМЕСТИТЬ втОбщая
			|ИЗ
			|	(ВЫБРАТЬ
			|		dbo_vBranchSales.WHOUSEID КАК WHOUSEID,
			|		dbo_vBranchSales.PRODUCTID КАК PRODUCTID,
			|		dbo_vBranchSales.BATCODE КАК BATCODE,
			|		dbo_vBranchSales.MRP_PRICE КАК MRP_PRICE,
			|		dbo_vBranchSales.SALES_PACKS КАК SALES_PACKS,
			|		0 КАК STOCK_PACKS,
			|		dbo_vBranchSales.PRODUCTNAME КАК PRODUCTNAME
			|	ИЗ
			|		dbo_vBranchSales КАК dbo_vBranchSales
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		dbo_vSTOCKS.WHOUSEID,
			|		dbo_vSTOCKS.PRODUCTID,
			|		dbo_vSTOCKS.BATCODE,
			|		dbo_vSTOCKS.MRP_PRICE,
			|		0,
			|		dbo_vSTOCKS.STOCK_PACKS,
			|		dbo_vSTOCKS.PRODUCTNAME
			|	ИЗ
			|		dbo_vSTOCKS КАК dbo_vSTOCKS) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.WHOUSEID,
			|	ВложенныйЗапрос.MRP_PRICE,
			|	ВложенныйЗапрос.BATCODE,
			|	ВложенныйЗапрос.PRODUCTNAME
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втОбщая.WHOUSEID КАК WHOUSEID,
			|	втОбщая.BATCODE КАК BATCodeSKU,
			|	(ВЫРАЗИТЬ(втОбщая.MRP_PRICE КАК ЧИСЛО(15, 2))) / 100 КАК PRICE_MRP,
			|	втОбщая.SALES_PACKS КАК SALES_PACKS,
			|	втОбщая.STOCK_PACKS КАК STOCK_PACKS,
			|	втОбщая.PRODUCTNAME КАК ClientNameSKU
			|ИЗ
			|	втОбщая КАК втОбщая
			|ГДЕ
			|	НЕ втОбщая.BATCODE В (&мИсключенияБАТ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втОбщая.WHOUSEID КАК WHOUSEID,
			|	втОбщая.BATCODE КАК BATCodeSKU,
			|	втОбщая.BATCODE КАК ClientNameSKU,
			|	СУММА(втОбщая.SALES_PACKS) КАК SALES_PACKS,
			|	СУММА(втОбщая.STOCK_PACKS) КАК STOCK_PACKS
			|ИЗ
			|	втОбщая КАК втОбщая
			|ГДЕ
			|	втОбщая.BATCODE В(&мИсключенияБАТ)
			|
			|СГРУППИРОВАТЬ ПО
			|	втОбщая.WHOUSEID,
			|	втОбщая.BATCODE,
			|	втОбщая.BATCODE";
			
			МассивРезультатов = Запрос.ВыполнитьПакет();
			ВыборкаБАТ = МассивРезультатов[1].Выбрать();
			ВыборкаНеБАТ = МассивРезультатов[2].Выбрать();
			
			Пока ВыборкаБАТ.Следующий() Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				НоваяСтрока.Порядок = 1;
				НоваяСтрока.BranchID = "GL000434";			
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаБАТ, "BATCodeSKU,ClientNameSKU,PRICE_MRP,STOCK_PACKS,SALES_PACKS");
			КонецЦикла;
			
			Пока ВыборкаНеБАТ.Следующий() Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				НоваяСтрока.Порядок = 3;
				НоваяСтрока.BranchID = "GL000434";
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНеБАТ, "BATCodeSKU,ClientNameSKU,STOCK_PACKS,SALES_PACKS");			
			КонецЦикла;
		КонецЕсли;
		
		Если ИзВнутреннегоИсточника Тогда 
			
			СхемаКомпновки = ПолучитьМакет("ОтчетОптовыеПродажи");
			Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
			
			СтруктураПараметры = Новый Структура;
			СтруктураПараметры.Вставить("НачалоПериода", НачалоПериода);
			СтруктураПараметры.Вставить("КонецПериода", КонецПериода);
			СтруктураПараметры.Вставить("ТМ_БАТ", Справочники.ТоварныеМарки.НайтиПоКоду("Т10"));
			СтруктураПараметры.Вставить("КлассификаторПачки", Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("A06"));
			
			Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", новый Структура());
			
			////Соответствие имен полей отборов на форме и в схеме компоновки
			// - Ключ - Имя поля отбора формы
			// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
			//
			СоответствиеПолейОтбора = Новый Структура;
			СоответствиеПолейОтбора.Вставить("ПодразделениеКомпании", "ОтборПодразделениеКомпании");
			СоответствиеПолейОтбора.Вставить("ТорговаяПлощадка", "ОтборТорговаяПлощадка");
			СоответствиеПолейОтбора.Вставить("СкладКомпании", "ОтборСкладКомпании");
			СоответствиеПолейОтбора.Вставить("Номенклатура");				
			СоответствиеПолейОтбора.Вставить("Контрагент", "ОтборПокупатель");
			СоответствиеПолейОтбора.Вставить("ТорговаяПлощадкаПолучатель", "ОтборТорговаяПлощадкаПолучатель");
			
			ТаблицаРезультатВн = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, СтруктураПараметры, Отборы, СоответствиеПолейОтбора);		
			
			Для Каждого СтрТЧ Из ТаблицаРезультатВн Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТЧ);
				НоваяСтрока.Порядок = 2;
			КонецЦикла;
		КонецЕсли;
		
		ТаблицаРезультат.Сортировать("Порядок");
		
		СписокСтолбцов = Новый Структура("BranchID,BATCodeSKU,ClientNameSKU,STOCK_PACKS,PRICE_MRP,SALES_PACKS");
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		СтрокаЗаголовки = "";
		Для Каждого Колонка Из СписокСтолбцов Цикл 
			СтрокаЗаголовки = СтрокаЗаголовки + Колонка.Ключ + ";";
		КонецЦикла;
		ТекстовыйДокумент.ДобавитьСтроку(СтрокаЗаголовки);
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекСтрока = "";
			Для Каждого Колонка Из СписокСтолбцов Цикл 
				ИмяКолонки = Колонка.Ключ;
				
				Если ИмяКолонки = "STOCK_PACKS" 
					ИЛИ ИмяКолонки = "SALES_PACKS" Тогда 
					
					Значение = Формат(Стр[ИмяКолонки], "ЧДЦ=0; ЧГ=0");
				Иначе
					Значение = СокрЛП(Стр[ИмяКолонки]);
				КонецЕсли;
				
				ТекСтрока = ТекСтрока + Значение + ";";
			КонецЦикла;
			ТекстовыйДокумент.ДобавитьСтроку(ТекСтрока);
		КонецЦикла;
		
		Если КонецПериода - НачалоПериода > 604800 Тогда 
			Период = "m";
		ИначеЕсли КонецПериода - НачалоПериода = 86399 Тогда
			Период = "d";
		Иначе
			Период = "w";
		КонецЕсли;
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		Если Период = "d" Тогда
			СтруктураРезультат.Вставить("ИмяФайла", Формат(НачалоПериода, "ДФ=yyyy")+"_"+Период + "_BranchSales_"+Формат(НачалоПериода, "ДФ=yyyy-MM-dd") + ".csv"); 
		Иначе
			СтруктураРезультат.Вставить("ИмяФайла", Формат(текДата, "ДФ=yyyy")+"_"+Период+"_" + ОбщегоНазначенияТККлиентСервер.НеделяГода_ISO8601(НачалоПериода)+"_BranchSales_"+Формат(текДата, "ДФ=yyyy-MM-dd") + ".csv"); 
		КонецЕсли;
		Параметры.Результат.Вставить("ОтчетОптовыеПродажи", СтруктураРезультат);
		
	КонецПроцедуры
	
	Процедура СформироватьОтчетДетальныеПродажи(НачалоПериода, КонецПериода, Параметры, Знач МенеджерВременныхТаблиц)
		
		текДата 				= ТекущаяДата();	
		ИзВнутреннегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнутреннегоИсточника", Ложь);
		ИзВнешнегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнешнегоИсточника", Ложь);
		
		Если Не ИзВнутреннегоИсточника И Не ИзВнешнегоИсточника Тогда 
			Возврат;
		КонецЕсли;
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		ТаблицаРезультат.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(5, 0));
		ТаблицаРезультат.Колонки.Добавить("Date", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
		ТаблицаРезультат.Колонки.Добавить("CodeOutlet");
		ТаблицаРезультат.Колонки.Добавить("BranchID");
		ТаблицаРезультат.Колонки.Добавить("BATCodeSKU");
		ТаблицаРезультат.Колонки.Добавить("ClientNameSKU");
		ТаблицаРезультат.Колонки.Добавить("SALES_PACKS", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));	
		
		Если ИзВнешнегоИсточника И МенеджерВременныхТаблиц <> Неопределено Тогда 				
			
			мИсключенияБАТ = Новый Массив;
			мИсключенияБАТ.Добавить("ITOTALIMPERIAL");
			мИсключенияБАТ.Добавить("JTOTALJTI001");
			мИсключенияБАТ.Добавить("LTOTALLTF001");
			мИсключенияБАТ.Добавить("LTOTALLTFCIGARILLOS001");
			мИсключенияБАТ.Добавить("PTOTALHEETS001");
			мИсключенияБАТ.Добавить("PTOTALPMI001");
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("мИсключенияБАТ", мИсключенияБАТ);
			
			Запрос.Текст = "ВЫБРАТЬ
			|	dbo_vRetailSales.Period КАК Date,
			|	dbo_vRetailSales.OUTLETEXTID КАК CodeOutlet,
			|	dbo_vRetailSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vRetailSales.PRODUCTNAME КАК ClientNameSKU,
			|	dbo_vRetailSales.BATCODE КАК BATCodeSKU,
			|	СУММА(dbo_vRetailSales.SALES_PACKS) КАК SALES_PACKS
			|ИЗ
			|	dbo_vRetailSales КАК dbo_vRetailSales
			|ГДЕ
			|	НЕ dbo_vRetailSales.BATCODE В (&мИсключенияБАТ)
			|
			|СГРУППИРОВАТЬ ПО
			|	dbo_vRetailSales.OUTLETEXTID,
			|	dbo_vRetailSales.Period,
			|	dbo_vRetailSales.PRODUCTNAME,
			|	dbo_vRetailSales.BATCODE,
			|	dbo_vRetailSales.WHOUSEID
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	dbo_vRetailSales.Period КАК Date,
			|	dbo_vRetailSales.OUTLETEXTID КАК CodeOutlet,
			|	dbo_vRetailSales.WHOUSEID КАК WHOUSEID,
			|	dbo_vRetailSales.BATCODE КАК BATCodeSKU,
			|	dbo_vRetailSales.BATCODE КАК ClientNameSKU,
			|	СУММА(dbo_vRetailSales.SALES_PACKS) КАК SALES_PACKS
			|ИЗ
			|	dbo_vRetailSales КАК dbo_vRetailSales
			|ГДЕ
			|	dbo_vRetailSales.BATCODE В(&мИсключенияБАТ)
			|
			|СГРУППИРОВАТЬ ПО
			|	dbo_vRetailSales.Period,
			|	dbo_vRetailSales.BATCODE,
			|	dbo_vRetailSales.OUTLETEXTID,
			|	dbo_vRetailSales.WHOUSEID";
			
			МассивРезультатов = Запрос.ВыполнитьПакет();
			ВыборкаБАТ = МассивРезультатов[0].Выбрать();
			ВыборкаНеБАТ = МассивРезультатов[1].Выбрать();
			
			Пока ВыборкаБАТ.Следующий() Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				НоваяСтрока.Порядок = 1;
				НоваяСтрока.BranchID = "GL000434";			
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаБАТ, "Date,CodeOutlet,BATCodeSKU,ClientNameSKU,SALES_PACKS");
			КонецЦикла;
			
			Пока ВыборкаНеБАТ.Следующий() Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				НоваяСтрока.Порядок = 3;
				НоваяСтрока.BranchID = "GL000434";
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНеБАТ, "Date,CodeOutlet,BATCodeSKU,ClientNameSKU,SALES_PACKS");
			КонецЦикла;
		КонецЕсли;
		
		Если ИзВнутреннегоИсточника Тогда 
			
			СхемаКомпновки = ПолучитьМакет("ОтчетДетальныеПродажи");
			Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
			
			СтруктураПараметры = Новый Структура;
			СтруктураПараметры.Вставить("НачалоПериода", НачалоПериода);
			СтруктураПараметры.Вставить("КонецПериода", КонецПериода);
			СтруктураПараметры.Вставить("ТМ_БАТ", Справочники.ТоварныеМарки.НайтиПоКоду("Т10"));
			СтруктураПараметры.Вставить("КлассификаторПачки", Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("A06"));
			
			Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", новый Структура());
			
			////Соответствие имен полей отборов на форме и в схеме компоновки
			// - Ключ - Имя поля отбора формы
			// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
			//
			СоответствиеПолейОтбора = Новый Структура;
			СоответствиеПолейОтбора.Вставить("ПодразделениеКомпании");
			СоответствиеПолейОтбора.Вставить("ТорговаяПлощадка");
			СоответствиеПолейОтбора.Вставить("СкладКомпании");
			СоответствиеПолейОтбора.Вставить("Номенклатура");				
			СоответствиеПолейОтбора.Вставить("Контрагент", "Покупатель");
			СоответствиеПолейОтбора.Вставить("ТорговаяПлощадкаПолучатель");
			
			ТаблицаРезультатВн = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, СтруктураПараметры, Отборы, СоответствиеПолейОтбора);		
			
			Для Каждого СтрТЧ Из ТаблицаРезультатВн Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТЧ);
				НоваяСтрока.Порядок = 2;
			КонецЦикла;
		КонецЕсли;
		
		ТаблицаРезультат.Сортировать("Порядок,Date");
		
		СписокСтолбцов = Новый Структура("Date,CodeOutlet,BranchID,BATCodeSKU,ClientNameSKU,SALES_PACKS");
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		СтрокаЗаголовки = "";
		Для Каждого Колонка Из СписокСтолбцов Цикл 
			СтрокаЗаголовки = СтрокаЗаголовки + Колонка.Ключ + ";";
		КонецЦикла;
		ТекстовыйДокумент.ДобавитьСтроку(СтрокаЗаголовки);
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекСтрока = "";
			Для Каждого Колонка Из СписокСтолбцов Цикл 
				ИмяКолонки = Колонка.Ключ;
				
				Если ИмяКолонки = "Date" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ДФ=dd.MM.yyyy");
				ИначеЕсли ИмяКолонки = "CodeOutlet" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ЧГ=0");
				ИначеЕсли ИмяКолонки = "SALES_PACKS" Тогда 				
					Значение = Формат(Стр[ИмяКолонки], "ЧДЦ=0; ЧН=0; ЧГ=0");
				Иначе
					Значение = СокрЛП(Стр[ИмяКолонки]);
				КонецЕсли;
				
				ТекСтрока = ТекСтрока + Значение + ";";
			КонецЦикла;
			ТекстовыйДокумент.ДобавитьСтроку(ТекСтрока);
		КонецЦикла;
		
		Если КонецПериода - НачалоПериода > 604800 Тогда 
			Период = "m";
		Иначе
			Период = "w";
		КонецЕсли;
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		СтруктураРезультат.Вставить("ИмяФайла", Формат(текДата, "ДФ=yyyy")+"_"+Период+"_" + ОбщегоНазначенияТККлиентСервер.НеделяГода_ISO8601(НачалоПериода)+"_RetailSales_"+Формат(текДата, "ДФ=yyyy-MM-dd") + ".csv"); 
		
		Параметры.Результат.Вставить("ОтчетДетальныеПродажи", СтруктураРезультат);	
		
	КонецПроцедуры
	
	Процедура СформироватьОтчетСправочникТорговыеТочки(НачалоПериода, КонецПериода, Параметры, Знач МенеджерВременныхТаблиц)
		
		текДата 				= ТекущаяДата();	
		ИзВнутреннегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнутреннегоИсточника", Ложь);
		ИзВнешнегоИсточника 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзВнешнегоИсточника", Ложь);
		
		Если Не ИзВнутреннегоИсточника И Не ИзВнешнегоИсточника Тогда 
			Возврат;
		КонецЕсли;
		
		ТаблицаРезультат = Новый ТаблицаЗначений;
		ТаблицаРезультат.Колонки.Добавить("CodeOutlet");
		ТаблицаРезультат.Колонки.Добавить("Area");
		ТаблицаРезультат.Колонки.Добавить("Territory");
		ТаблицаРезультат.Колонки.Добавить("City");
		ТаблицаРезультат.Колонки.Добавить("CityType");
		ТаблицаРезультат.Колонки.Добавить("CustomerName");
		ТаблицаРезультат.Колонки.Добавить("ZKPO");
		ТаблицаРезультат.Колонки.Добавить("Street");
		ТаблицаРезультат.Колонки.Добавить("Home");
		ТаблицаРезультат.Колонки.Добавить("CityID");	
		ТаблицаРезультат.Колонки.Добавить("AgentName");	
		
		Если ИзВнешнегоИсточника И МенеджерВременныхТаблиц <> Неопределено Тогда 				
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	dbo_vOUTLETS.OUTLETID КАК OUTLETID,
			|	dbo_vOUTLETS.OUTLETGUID КАК OUTLETGUID,
			|	dbo_vOUTLETS.PARTNERNAME КАК CustomerName,
			|	dbo_vOUTLETS.ZKPO КАК ZKPO,
			|	dbo_vOUTLETS.Area КАК Area,
			|	dbo_vOUTLETS.Territory КАК Territory,
			|	dbo_vOUTLETS.City КАК City,
			|	dbo_vOUTLETS.CityType КАК CityType,
			|	dbo_vOUTLETS.Street КАК Street,
			|	dbo_vOUTLETS.Home КАК Home,
			|	dbo_vOUTLETS.OUTLETEXTID КАК CodeOutlet,
			|	dbo_vOUTLETS.CITYCODE КАК CityID
			|ИЗ
			|	dbo_vOUTLETS КАК dbo_vOUTLETS
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ dbo_vRetailSales КАК dbo_vRetailSales
			|		ПО dbo_vOUTLETS.OUTLETID = dbo_vRetailSales.OUTLETID";
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КонецЦикла;
		КонецЕсли;
		
		Если ИзВнутреннегоИсточника Тогда 
			
			СхемаКомпновки = ПолучитьМакет("ОтчетСправочникТТ");
			Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
			
			СтруктураПараметры = Новый Структура;
			СтруктураПараметры.Вставить("НачалоПериода", НачалоПериода);
			СтруктураПараметры.Вставить("КонецПериода", КонецПериода);
			
			Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", новый Структура());
			
			////Соответствие имен полей отборов на форме и в схеме компоновки
			// - Ключ - Имя поля отбора формы
			// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
			//
			СоответствиеПолейОтбора = Новый Структура;
			СоответствиеПолейОтбора.Вставить("ПодразделениеКомпании");
			СоответствиеПолейОтбора.Вставить("ТорговаяПлощадка");
			СоответствиеПолейОтбора.Вставить("СкладКомпании");
			СоответствиеПолейОтбора.Вставить("Номенклатура");				
			СоответствиеПолейОтбора.Вставить("Контрагент", "Покупатель");
			
			ТаблицаРезультатВн = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, СтруктураПараметры, Отборы, СоответствиеПолейОтбора);		
			
			Для Каждого СтрТЧ Из ТаблицаРезультатВн Цикл 
				НоваяСтрока = ТаблицаРезультат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТЧ);
			КонецЦикла;
		КонецЕсли;
		
		СписокСтолбцов = Новый Структура("CodeOutlet,Area,Territory,City,CityType,CustomerName,ZKPO,Street,Home,CityID,AgentName");
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		СтрокаЗаголовки = "";
		Для Каждого Колонка Из СписокСтолбцов Цикл 
			СтрокаЗаголовки = СтрокаЗаголовки + Колонка.Ключ + ";";
		КонецЦикла;
		ТекстовыйДокумент.ДобавитьСтроку(СтрокаЗаголовки);
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекСтрока = "";
			Для Каждого Колонка Из СписокСтолбцов Цикл 
				ИмяКолонки = Колонка.Ключ;
				
				Если ИмяКолонки = "CodeOutlet" 
					ИЛИ ИмяКолонки = "ZKPO" Тогда
					
					Значение = Формат(Стр[ИмяКолонки], "ЧГ=");
					
				ИначеЕсли  ИмяКолонки = "CityID" Тогда 
					Пусто = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Стр[ИмяКолонки],,Ложь);
					Если НЕ Пусто Тогда
						Значение = "";
					Иначе
						Значение = Формат(Стр[ИмяКолонки], "ЧГ=");
					КонецЕсли;
				Иначе
					Значение = СокрЛП(Стр[ИмяКолонки]);
				КонецЕсли;
				
				ТекСтрока = ТекСтрока + Значение + ";";
			КонецЦикла;
			ТекстовыйДокумент.ДобавитьСтроку(ТекСтрока);
		КонецЦикла;
		
		Если КонецПериода - НачалоПериода > 604800 Тогда 
			Период = "m";
		Иначе
			Период = "w";
		КонецЕсли;
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		СтруктураРезультат.Вставить("ИмяФайла", Формат(текДата, "ДФ=yyyy")+"_"+Период+"_" + ОбщегоНазначенияТККлиентСервер.НеделяГода_ISO8601(НачалоПериода)+"_OutletsDirectory_"+Формат(текДата, "ДФ=yyyy-MM-dd") + ".csv"); 
		
		Параметры.Результат.Вставить("СправочникТорговыеТочки", СтруктураРезультат);		
		
	КонецПроцедуры
	
	Процедура СформироватьОтчетСправочникПродукция(Параметры)
		
		СхемаКомпновки = ПолучитьМакет("ОтчетСправочникПродукция");
		Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
		
		Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", Неопределено);
		
		////Соответствие имен полей отборов на форме и в схеме компоновки
		// - Ключ - Имя поля отбора формы
		// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
		//
		СоответствиеПолейОтбора = Новый Структура("Номенклатура");
		
		ТаблицаРезультат = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки,, Отборы, СоответствиеПолейОтбора);
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.ДобавитьСтроку("BATCodeSKU;ClientNameSKU;");
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекстовыйДокумент.ДобавитьСтроку(СокрЛП(Стр.BATCodeSKU) + ";" + СокрЛП(Стр.ClientNameSKU) + ";");	
		КонецЦикла;                                
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		СтруктураРезультат.Вставить("ИмяФайла", "Product Directory_"+Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd") + ".csv");
		
		Параметры.Результат.Вставить("СправочникПродукция", СтруктураРезультат);		
		
	КонецПроцедуры
	
	Процедура СформироватьОтчетСправочникСклады(НачалоПериода, КонецПериода, Параметры)
		
		СхемаКомпновки = ПолучитьМакет("ОтчетСправочникСклады");
		Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
		
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("НачалоПериода", НачалоПериода);
		СтруктураПараметры.Вставить("КонецПериода", КонецПериода);	
		СтруктураПараметры.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
		СтруктураПараметры.Вставить("Вид", Справочники.ВидыКонтактнойИнформации.НайтиПоРеквизиту("ИдентификаторДляФормул", "ЮридическийАдресТорговойПлощадки"));
		
		Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", новый Структура());
		
		////Соответствие имен полей отборов на форме и в схеме компоновки
		// - Ключ - Имя поля отбора формы
		// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
		//
		СоответствиеПолейОтбора = Новый Структура;
		СоответствиеПолейОтбора.Вставить("ПодразделениеКомпании");
		СоответствиеПолейОтбора.Вставить("ТорговаяПлощадка");
		СоответствиеПолейОтбора.Вставить("СкладКомпании");
		СоответствиеПолейОтбора.Вставить("Номенклатура");				
		СоответствиеПолейОтбора.Вставить("Контрагент", "Покупатель");
		
		
		ТаблицаРезультат = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, СтруктураПараметры, Отборы, СоответствиеПолейОтбора);
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		
		СписокСтолбцов = Новый Структура("BranchID,BranchName,BranchAddress");
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		СтрокаЗаголовки = "";
		Для Каждого Колонка Из СписокСтолбцов Цикл 
			СтрокаЗаголовки = СтрокаЗаголовки + Колонка.Ключ + ";";
		КонецЦикла;
		ТекстовыйДокумент.ДобавитьСтроку(СтрокаЗаголовки);
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекСтрока = "";
			Для Каждого Колонка Из СписокСтолбцов Цикл 
				ИмяКолонки = Колонка.Ключ;
				Значение = СокрЛП(Стр[ИмяКолонки]);
				
				ТекСтрока = ТекСтрока + Значение + ";";
			КонецЦикла;
			ТекстовыйДокумент.ДобавитьСтроку(ТекСтрока);
		КонецЦикла;
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		СтруктураРезультат.Вставить("ИмяФайла", "Warehouse Directory_"+Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd") + ".csv");
		
		Параметры.Результат.Вставить("СправочникСклады", СтруктураРезультат);			
		
	КонецПроцедуры
	
	Процедура СформироватьОтчетПриходПродукции(НачалоПериода, КонецПериода, Параметры)
		
		СхемаКомпновки = ПолучитьМакет("ОтчетПриходПродукции");
		Настройки = СхемаКомпновки.НастройкиПоУмолчанию;
		
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("НачалоПериода", НачалоПериода);
		СтруктураПараметры.Вставить("КонецПериода", КонецПериода);	
		СтруктураПараметры.Вставить("ТМ_БАТ", Справочники.ТоварныеМарки.НайтиПоКоду("Т10"));
		СтруктураПараметры.Вставить("КлассификаторПачки", Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("A06"));
		
		Отборы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отборы", новый Структура());
		
		////Соответствие имен полей отборов на форме и в схеме компоновки
		// - Ключ - Имя поля отбора формы
		// - Значение - Имя поля отбора	Схемы компоновки (Пустое значение если имена не различаются)
		//
		СоответствиеПолейОтбора = Новый Структура;
		СоответствиеПолейОтбора.Вставить("ПодразделениеКомпании");
		СоответствиеПолейОтбора.Вставить("ТорговаяПлощадка");
		СоответствиеПолейОтбора.Вставить("СкладКомпании");
		СоответствиеПолейОтбора.Вставить("Номенклатура");				
		СоответствиеПолейОтбора.Вставить("Контрагент");
		
		ТаблицаРезультат = СформироватьТаблицуРезультатаСКД(СхемаКомпновки, Настройки, СтруктураПараметры, Отборы, СоответствиеПолейОтбора);
		
		СписокСтолбцов = Новый Структура("Date,SupplierName,ZKPO,SupplierBranchID,BranchID,BATCodeSKU,ClientNameSKU,PRICE_MRP,RECEIPTS_PACKS");
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		СтрокаЗаголовки = "";
		Для Каждого Колонка Из СписокСтолбцов Цикл 
			СтрокаЗаголовки = СтрокаЗаголовки + Колонка.Ключ + ";";
		КонецЦикла;
		ТекстовыйДокумент.ДобавитьСтроку(СтрокаЗаголовки);
		
		Для Каждого Стр Из ТаблицаРезультат Цикл 
			ТекСтрока = "";
			Для Каждого Колонка Из СписокСтолбцов Цикл 
				ИмяКолонки = Колонка.Ключ;
				
				Если ИмяКолонки = "Date" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ДФ=dd.MM.yyyy");
				ИначеЕсли ИмяКолонки = "ZKPO" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ЧГ=0");
				ИначеЕсли ИмяКолонки = "SupplierBranchID" Тогда
					//Коды складов:
					//TD Київ	- 326637013
					//TD Суми	- 326637015
					//TD Харків - 326637026
					//TD Полтава- 326637043
					
					//Коды складов по городам Галичина:
					//36497264001	GT Івано-Франківськ
					//36497264002	GT Луцьк
					//36497264003	GT Львів
					//36497264004	GT Мукачево
					//36497264007	GT Тернопіль
					//36497264010	GT Сарни
					//36497264009	GT Рівне
					//36497264008	GT Чернівці
					//36497264005	GT Хмельницький
					//36497264006	GT Вінниця
					//36497264011	GT Київ
					//36497264012	GT Первомайськ
					//36497264013	GT Одеса
					//36497264015	GT Житомир
					//36497264014	GT Кропивницький
					//36497264016	GT Полтава
					//36497264017	GT Миколаїв
					
					СоотТедис = Новый Соответствие;
					СоотТедис.Вставить("ХGL000322","326637013");	// Київ
					СоотТедис.Вставить("ХGL000153","326637015");	// Суми
					СоотТедис.Вставить("GL000160","326637026");		// Харків
					СоотТедис.Вставить("DaL005","326637026");		// Харків
					СоотТедис.Вставить("ХGL000300","326637043");	// Полтава
					
					СоотГаличина = Новый Соответствие;
					СоотГаличина.Вставить("ХGL000322","36497264011");
					СоотГаличина.Вставить("ХGL000153","36497264016");
					СоотГаличина.Вставить("GL000160","36497264016");
					СоотГаличина.Вставить("DaL005","36497264016");
					СоотГаличина.Вставить("ХGL000300","36497264016");
					
					Если Стр.ZKPO = "44895101" Тогда		//	Тедис
						Значение = СоотТедис.Получить(СокрЛП(Стр.BranchID));
					ИначеЕсли Стр.ZKPO = "36497264" Тогда	//	Галичина
						Значение = СоотГаличина.Получить(СокрЛП(Стр.BranchID));
					Иначе
						Значение = "";
					КонецЕсли;
					//Значение = Формат(Стр[ИмяКолонки], "ЧГ=0");
				ИначеЕсли ИмяКолонки = "PRICE_MRP" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ЧДЦ=2; ЧН=0; ЧГ=0");
				ИначеЕсли ИмяКолонки = "RECEIPTS_PACKS" Тогда 
					Значение = Формат(Стр[ИмяКолонки], "ЧДЦ=; ЧГ=0");
				Иначе
					Значение = СокрЛП(Стр[ИмяКолонки]);
				КонецЕсли;
				
				ТекСтрока = ТекСтрока + Значение + ";";
			КонецЦикла;
			ТекстовыйДокумент.ДобавитьСтроку(ТекСтрока);
		КонецЦикла;
		
		Если КонецПериода - НачалоПериода > 604800 Тогда 
			Период = "m";
		Иначе
			Период = "w";
		КонецЕсли;
		
		текДата = ТекущаяДата();	
		
		СтруктураРезультат = Новый Структура;
		СтруктураРезультат.Вставить("Документ", ТекстовыйДокумент);
		СтруктураРезультат.Вставить("ИмяФайла", Формат(текДата, "ДФ=yyyy")+"_"+Период+"_" + ОбщегоНазначенияТККлиентСервер.НеделяГода_ISO8601(НачалоПериода)+"_Receipt of goods_"+Формат(текДата, "ДФ=yyyy-MM-dd") + ".csv"); 
		
		Параметры.Результат.Вставить("ОтчетПриходПродукции", СтруктураРезультат);
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ПрочиеПроцедурыИФункции
	
	Функция ПодключениеКВнешнемуИсточнику(ТекстСообщения)
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ВнешниеИсточникиДанных.NIELSENBIS.ПолучитьСостояние() = ПредопределенноеЗначение("СостояниеВнешнегоИсточникаДанных.Подключен") Тогда
			Возврат Истина;
		КонецЕсли;	
		
		
		СистемнаяИнформация = Новый СистемнаяИнформация;
		Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
			СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
		ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда
			СтрокаПодключения = "DRIVER={SQL Server Native Client 11.0};";
		Иначе
			ТекстСообщения = ТекстСообщения + НСтр("ru='Операционная система "+СистемнаяИнформация.ТипПлатформы +" не поддерживается.';uk='Операційна система "+ СистемнаяИнформация.ТипПлатформы +" не підтримується'")+Символы.ПС;
			
			Возврат Ложь
		КонецЕсли;
		
		ПараметрыПодключенияВИ = ВнешниеИсточникиДанных.NIELSENBIS.ПолучитьОбщиеПараметрыСоединения();
		ПараметрыПодключенияВИ.АутентификацияСтандартная = Истина;
		ПараметрыПодключенияВИ.АутентификацияОС = Ложь;
		ПараметрыПодключенияВИ.ИмяПользователя = "1cuser"; 
		ПараметрыПодключенияВИ.Пароль = "fCG2eSvVd5mtqBpe";
		ПараметрыПодключенияВИ.СтрокаСоединения = СтрокаПодключения +";SERVER=NIELSENBIS;DATABASE=Attika_Tranzit;LANGUAGE=русский";
		
		ПараметрыПодключенияВИ.СУБД = "MSSQLServer";
		
		ВнешниеИсточникиДанных.NIELSENBIS.УстановитьОбщиеПараметрыСоединения(ПараметрыПодключенияВИ);
		
		Попытка
			ВнешниеИсточникиДанных.NIELSENBIS.УстановитьСоединение();
		Исключение
			ТекстСообщения = ТекстСообщения + ОписаниеОшибки();
		КонецПопытки;
		
		Если ВнешниеИсточникиДанных.NIELSENBIS.ПолучитьСостояние() = СостояниеВнешнегоИсточникаДанных.Подключен Тогда
			Возврат Истина;
		Иначе 
			Возврат Ложь;
		КонецЕсли;	
		
	КонецФункции
	
	Функция ПолучитьСтруктуруВнешнихТаблиц(СписокОтчетов)
		
		СтруктураВнешТаблиц = Новый Структура;
		
		Если СписокОтчетов.Свойство("ОтчетОптовыеПродажи") Тогда 
			СтруктураВнешТаблиц.Вставить("dbo_vSTOCKS");
			СтруктураВнешТаблиц.Вставить("dbo_vBranchSales");
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("ОтчетДетальныеПродажи") Тогда 
			СтруктураВнешТаблиц.Вставить("dbo_vRetailSales");
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникТорговыеТочки") Тогда 
			СтруктураВнешТаблиц.Вставить("dbo_vRetailSales");
			СтруктураВнешТаблиц.Вставить("dbo_vOUTLETS");
		КонецЕсли;
		
		
		Возврат СтруктураВнешТаблиц;
		
	КонецФункции
	
	Функция ПолучитьСписокТаблицОтборов(СписокОтчетов)
		
		СтруктураТаблицОтборов = Новый Структура;
		
		Если СписокОтчетов.Свойство("ОтчетОптовыеПродажи") Тогда 
			СтруктураТаблицОтборов.Вставить("ТаблицаСклады");
			СтруктураТаблицОтборов.Вставить("ТаблицаНоменклатура");
			СтруктураТаблицОтборов.Вставить("ТаблицаКонтрагенты");
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("ОтчетДетальныеПродажи") Тогда 
			
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("ОтчетПриходПродукции") Тогда 
			
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникТорговыеТочки") Тогда 
			
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникПродукция") Тогда 
			
		КонецЕсли;
		
		Если СписокОтчетов.Свойство("СправочникСклады") Тогда 
			
		КонецЕсли;
		
		
		Возврат СтруктураТаблицОтборов;
		
	КонецФункции
	
	
	#КонецОбласти
	
	
	
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

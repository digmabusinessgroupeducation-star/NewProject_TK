
&НаСервере
Функция  ЗагрузитьНаСервереАвто()
	
	МассивДокументов = Новый Массив;
	
	РезНоменклатура = ПолучитьНоменклатуру();
	
	Если РезНоменклатура.ВсеОк Тогда
		тзНоменклатура = РезНоменклатура.тзНоменклатура;
	Иначе
		МассивДокументов.Добавить(Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",Ложь,Неопределено,РезНоменклатура.ТекстОшибки));
		Возврат МассивДокументов;
	КонецЕсли;
	
	
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗ.Номенклатура КАК Номенклатура,
	|	ТЗ.Цена КАК Цена
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СогласованиеЦен.Номенклатура КАК Номенклатура,
	|	СогласованиеЦен.Контрагент КАК Контрагент,
	|	СогласованиеЦен.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	МАКСИМУМ(СогласованиеЦен.Регистратор.Дата) КАК РегистраторДата
	|ПОМЕСТИТЬ втСогласованиеЦен
	|ИЗ
	|	РегистрСведений.СогласованиеЦен КАК СогласованиеЦен
	|ГДЕ
	|	СогласованиеЦен.СрокДействия >= &Дата
	|	И СогласованиеЦен.Период <= &Дата
	|	И СогласованиеЦен.Номенклатура В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ.Номенклатура КАК Номенклатура
	|			ИЗ
	|				ВТ КАК ВТ)
	|	И СогласованиеЦен.ДоговорВзаиморасчетов.Контрагент В(&Контрагент)
	|	И СогласованиеЦен.ДоговорВзаиморасчетов.Подразделение В(&Подразделение)
	|	И СогласованиеЦен.ДоговорВзаиморасчетов В(&ДоговорВзаиморасчетов)
	|
	|СГРУППИРОВАТЬ ПО
	|	СогласованиеЦен.Контрагент,
	|	СогласованиеЦен.ДоговорВзаиморасчетов,
	|	СогласованиеЦен.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	втСогласованиеЦен.Номенклатура КАК Номенклатура,
	|	втСогласованиеЦен.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	втСогласованиеЦен.Контрагент КАК Контрагент,
	|	МАКСИМУМ(СогласованиеЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ втСогласованиеЦенОбщий
	|ИЗ
	|	втСогласованиеЦен КАК втСогласованиеЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СогласованиеЦен КАК СогласованиеЦен
	|		ПО втСогласованиеЦен.Номенклатура = СогласованиеЦен.Номенклатура
	|			И втСогласованиеЦен.ДоговорВзаиморасчетов = СогласованиеЦен.ДоговорВзаиморасчетов
	|			И втСогласованиеЦен.Контрагент = СогласованиеЦен.Контрагент
	|			И втСогласованиеЦен.РегистраторДата = СогласованиеЦен.Регистратор.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	втСогласованиеЦен.Номенклатура,
	|	втСогласованиеЦен.ДоговорВзаиморасчетов,
	|	втСогласованиеЦен.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов КАК Договор,
	|	втСогласованиеЦенОбщий.Номенклатура КАК Номенклатура,
	|	втСогласованиеЦенОбщий.Контрагент КАК Контрагент,
	|	ВТ.Цена КАК Цена
	|ИЗ
	|	втСогласованиеЦенОбщий КАК втСогласованиеЦенОбщий
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ КАК ВТ
	|		ПО втСогласованиеЦенОбщий.Номенклатура = ВТ.Номенклатура
	|ГДЕ
	|	втСогласованиеЦенОбщий.Цена > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втСогласованиеЦенОбщий.Контрагент,
	|	втСогласованиеЦенОбщий.Номенклатура,
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов,
	|	ВТ.Цена
	|ИТОГИ ПО
	|	Договор";
	
	Запрос.УстановитьПараметр("ТЗ", тзНоменклатура);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Контрагент", Контрагенты);
	Запрос.УстановитьПараметр("Подразделение", Подразделения);	
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", Договора);
	
	РезультатЗапроса = Запрос.Выполнить();
	ооо = РезультатЗапроса.Выгрузить();
	
	ВыборкаДоговор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДоговор.Следующий() Цикл
		
		Док = Документы.ПротоколСогласованияЦен.СоздатьДокумент();
		Док.Автор 					= ПараметрыСеанса.АвторизованныйПользователь;
		Док.ХозОперация 			= ?(Акция,Справочники.ХозОперации.ПротоколСогласованияЦенАкция,Справочники.ХозОперации.ПротоколСогласованияЦен);
		Док.Контрагент 				= ВыборкаДоговор.Договор.Контрагент;
		Док.ДоговорВзаиморасчетов	= ВыборкаДоговор.Договор;
		Док.ТипЦен					= Объект.ТипЦен;
		Док.ДатаНачалаДействия 		= Объект.НачалоПериода;
		Док.СрокДействия			= ?(Акция,Объект.КонецПериода,ДобавитьМесяц(Объект.НачалоПериода,48));
		Док.Организация 			= ВыборкаДоговор.Договор.Организация;
		Док.ПодразделениеКомпании 	= ВыборкаДоговор.Договор.Подразделение; 
		Док.Дата					= ТекущаяДата();
		Док.УстановитьНовыйНомер();
		Док.ВалютаДокумента 		= ВыборкаДоговор.Договор.ВалютаВзаиморасчетов;
		Док.КурсДокумента 			= 1;
		Док.Комментарий 			= Комментарий;
		
		ВыборкаДетальныеЗаписи = ВыборкаДоговор.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрокаТовары = Док.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,ВыборкаДетальныеЗаписи);
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога", НоваяСтрокаТовары.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", НоваяСтрокаТовары.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, Неопределено);
		КонецЦикла;
		
		ДокПроведен = Ложь;
		тСообщение = "";
		
		ТекПользователь = Пользователи.ТекущийПользователь();
		МожетПровести   = РегистрыСведений.СогласователиПротоколов.МожетСогласовывать(Объект.ТипЦен, ТекПользователь.ФизическоеЛицо);
		
		Если  НЕ Объект.ТипЦен.ТребуетСогласования Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.НеТребуетСогласования;
		ИначеЕсли НЕ МожетПровести И Объект.ТипЦен.ТребуетСогласования и НЕ Согласовано Тогда
			РежимЗаписи = РежимЗаписиДокумента.Запись;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.ТребуетСогласования;
		ИначеЕсли  МожетПровести И Объект.ТипЦен.ТребуетСогласования И Согласовано Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.Согласован;
		КонецЕсли;
		
		Попытка
			Док.Записать(РежимЗаписи);
			ДокПроведен = Истина;
			СтруктураВозрата = Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",ДокПроведен,Док.Ссылка,тСообщение);
			МассивДокументов.Добавить(СтруктураВозрата);
		Исключение
			тСообщение = "Документ не проведен! "+ОписаниеОшибки();
			СтруктураВозрата = Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",ДокПроведен,Неопределено,тСообщение);
			МассивДокументов.Добавить(СтруктураВозрата);
		КонецПопытки;
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

&НаСервере
Функция  ЗагрузитьНаСервере()
	
	МассивДокументов = Новый Массив;
	
	РезНоменклатура = ПолучитьНоменклатуру();
	
	Если РезНоменклатура.ВсеОк Тогда
		тзНоменклатура = РезНоменклатура.тзНоменклатура;
	Иначе
		МассивДокументов.Добавить(Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",Ложь,Неопределено,РезНоменклатура.ТекстОшибки));
		Возврат МассивДокументов;
	КонецЕсли;
	
	Для Каждого Договор Из Договора Цикл
		
		Если НЕ Объект.Контрагент = Договор.Значение.Контрагент Тогда
			ОбщегоНазначения.СообщитьПользователю("Договор " +Договор.Значение+" не пренадлежит контрагенту "+Объект.Контрагент);
			Продолжить;
		КонецЕсли;
		
		Док = Документы.ПротоколСогласованияЦен.СоздатьДокумент();
		Док.Автор 					= ПараметрыСеанса.АвторизованныйПользователь;
		Док.ХозОперация 			= ?(Акция,Справочники.ХозОперации.ПротоколСогласованияЦенАкция,Справочники.ХозОперации.ПротоколСогласованияЦен);
		Док.Контрагент 				= Объект.Контрагент;
		Док.ДоговорВзаиморасчетов	= Договор.Значение;
		Док.ТипЦен					= Объект.ТипЦен;
		Док.ДатаНачалаДействия 		= Объект.НачалоПериода;
		Док.СрокДействия			= ?(Акция,Объект.КонецПериода,ДобавитьМесяц(Объект.НачалоПериода,48));
		Док.Организация 			= Договор.Значение.Организация;
		Док.ПодразделениеКомпании 	= Договор.Значение.Подразделение; 
		Док.Дата					= ТекущаяДата();
		Док.УстановитьНовыйНомер();
		Док.ВалютаДокумента 		= Договор.Значение.ВалютаВзаиморасчетов;
		Док.КурсДокумента 			= 1;
		Док.Комментарий 			= Комментарий;
		
		Для Каждого Стр Из тзНоменклатура Цикл
			НоваяСтрокаТовары = Док.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары,стр);
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога", НоваяСтрокаТовары.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", НоваяСтрокаТовары.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, Неопределено);
		КонецЦикла;
		
		ДокПроведен = Ложь;
		тСообщение = "";
		
		ТекПользователь = Пользователи.ТекущийПользователь();
		МожетПровести   = РегистрыСведений.СогласователиПротоколов.МожетСогласовывать(Объект.ТипЦен, ТекПользователь.ФизическоеЛицо);
		
		Если  НЕ Объект.ТипЦен.ТребуетСогласования Тогда 
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.НеТребуетСогласования;
		ИначеЕсли НЕ МожетПровести И Объект.ТипЦен.ТребуетСогласования и НЕ Согласовано Тогда
			РежимЗаписи = РежимЗаписиДокумента.Запись;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.ТребуетСогласования;
		ИначеЕсли  МожетПровести И Объект.ТипЦен.ТребуетСогласования И Согласовано Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Док.Статус = Перечисления.СтатусыПротоколовСогласованияЦен.Согласован;
		КонецЕсли;

		Попытка
			Док.Записать(РежимЗаписи);
			ДокПроведен = Истина;
			СтруктураВозрата = Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",ДокПроведен,Док.Ссылка,тСообщение);
			МассивДокументов.Добавить(СтруктураВозрата);
		Исключение
			тСообщение = "Документ не проведен! "+ОписаниеОшибки();
			СтруктураВозрата = Новый Структура("ДокПроведен,ДокументСсылка,ТекстОшибки",ДокПроведен,Неопределено,тСообщение);
			МассивДокументов.Добавить(СтруктураВозрата);
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если Не ТабДок.ВысотаТаблицы > 1 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для загрузки'; uk = 'Немає даних для завантаження'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) ИЛИ
		НЕ ?(Акция,ЗначениеЗаполнено(Объект.КонецПериода), Истина) ИЛИ
		НЕ ?(АвтоДоговор,ЗначениеЗаполнено(Контрагенты),ЗначениеЗаполнено(Объект.Контрагент)) ИЛИ
		НЕ ?(АвтоДоговор,Истина,ЗначениеЗаполнено(Договора)) ИЛИ
		НЕ ЗначениеЗаполнено(Объект.ТипЦен) тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнены основные реквизиты'; uk = 'Немає даних для завантаження'"));
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();	
	
	Если АвтоДоговор Тогда
		Рез  = ЗагрузитьНаСервереАвто();
	Иначе
		Рез  = ЗагрузитьНаСервере();
	КонецЕсли;
	
	Для Каждого СтрМассива Из Рез Цикл
		Если СтрМассива.ДокПроведен Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Создан и проведен документ "+  СтрМассива.ДокументСсылка);
			Если Объект.СоздатьИзменениеЦен Тогда
				тПФ = Новый Структура("ИзменениеЦен, Округление, НачалоДействия", СтрМассива.ДокументСсылка, Объект.ОкруглятьДо, ДатаНачалаДействияИзмененияЦен);
				ОткрытьФорму("Обработка.ТК_ВводНаОснованииИзменениеЦен.Форма", тПФ); 
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю("Документ не создан!" + Символы.ПС + СтрМассива.ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	Объект.КонецПериода = КонецДня(Объект.КонецПериода);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТабДок = Обработки.ЗагрузкаСпецификаций.ПолучитьМакет("Макет");
	Элементы.Контрагент.Видимость = НЕ АвтоДоговор;
	Элементы.Контрагенты.Видимость = АвтоДоговор;
	Элементы.ДатаНачалаДействияИзмененияЦен.Видимость = Ложь;
	Элементы.ОкруглятьДо.Видимость = Ложь;
	Элементы.ФормаДоговора.Видимость = Ложь;
	Элементы.КонецПериода.Доступность = Ложь;
	
	врПодразделение = ПараметрыСеанса.АвторизованныйПользователь.Подразделение;
	Если ЗначениеЗаполнено(врПодразделение) Тогда
		Подразделения.Добавить(врПодразделение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИзменениеЦенПриИзменении(Элемент)
	Если Объект.СоздатьИзменениеЦен = Ложь Тогда
		Элементы.ОкруглятьДо.Видимость = Ложь;
		Элементы.ДатаНачалаДействияИзмененияЦен.Видимость = ЛОжь;
	Иначе
		Элементы.ОкруглятьДо.Видимость = Истина;
		Элементы.ДатаНачалаДействияИзмененияЦен.Видимость = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере()
	ТабДок.Очистить();
	ТабДок = Обработки.ЗагрузкаСпецификаций.ПолучитьМакет("Макет");
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	ОчиститьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДоговораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПодбора = Новый Структура();
	ПараметрыПодбора.Вставить("Контрагент",?(АвтоДоговор,Контрагенты,Объект.Контрагент));
	ПараметрыПодбора.Вставить("ВыбранныеДоговора",Договора.ВыгрузитьЗначения());
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	ПараметрыПодбора.Вставить("Отбор",Новый Структура("Подразделение",Подразделения));
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбораДоговора",ПараметрыПодбора,Элементы.Договора,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговораОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбранныеДоговораНаСервере(ВыбранноеЗначение);	
КонецПроцедуры


&НаСервере
Процедура ОбработатьВыбранныеДоговораНаСервере(АдресхВремХран)
	ВыбДог = ПолучитьИзВременногоХранилища(АдресхВремХран);
	Договора.Очистить();
	Для Каждого СтрДог Из ВыбДог Цикл 
		Договора.Добавить(СтрДог.ДоговорКонтрагента,Строка(СтрДог.ДоговорКонтрагента));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоДоговорПриИзменении(Элемент)
	Элементы.Контрагент.Видимость = НЕ АвтоДоговор;
	Элементы.Контрагенты.Видимость = АвтоДоговор;
	Элементы.ФормаДоговора.Видимость = АвтоДоговор И ЗначениеЗаполнено(Контрагенты);
КонецПроцедуры

&НаСервере
Функция ПолучитьНоменклатуру()
	
	ВсеОк = Истина;
	
	ТекстОшибки = "";
	
	кАртикул = 1;
	кШтрихкод = 2;
	кНоваяЦена = 3;
	кМРЦ = 4;
	
	ТаблицаЗагрузки = Новый ТаблицаЗначений;
	ТаблицаЗагрузки.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗагрузки.Колонки.Добавить("Цена",Новый ОписаниеТипов("Число"));
	ТаблицаЗагрузки.Колонки.Добавить("ХарактеристикаНоменклатуры",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	//ПереборТаблицы
	Для Стр = 2 По ТабДок.ВысотаТаблицы Цикл 
		
		тАртикул = СокрЛП(ТабДок.Область(Стр, кАртикул).Текст);
		тШтрихкод = СокрЛП(ТабДок.Область(Стр, кШтрихкод).Текст);
		тМРЦ = СокрЛП(ТабДок.Область(Стр, кМРЦ).Текст);
		
		Попытка
			тНоваяЦена = Число(СокрЛП(ТабДок.Область(Стр, кНоваяЦена).Текст));
		Исключение
			ТекстОшибки = ТекстОшибки + ?(ЗначениеЗаполнено(ТекстОшибки),Символы.ПС,"")+"Номер строки "+стр+". Артикул : " + тАртикул+". Проверьте правильность указания цен.";
			ВсеОк = Ложь;
			Продолжить;;
		КонецПопытки;
		
		Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", тАртикул);
		
		Если Номенклатура = Справочники.Номенклатура.ПустаяСсылка() Тогда
			Если ПустаяСтрока(тШтрихкод) Тогда
				ТекстОшибки = ТекстОшибки + ?(ЗначениеЗаполнено(ТекстОшибки),Символы.ПС,"")+"Номер строки "+стр+". Не найдена номеклатура с артикулом '" + тАртикул+"'. Проверьте правильность артикула и повторите загрузку.";
				ВсеОк = Ложь;
				Продолжить;
			Иначе
				Попытка
					чШтрихкод = Число(тШтрихкод);
					Номенклатура = РегистрыСведений.ШтрихкодыНоменклатуры.НайтиТоварПоШтрихКоду(тШтрихкод);
					Если Номенклатура = Неопределено Тогда
						ТекстОшибки = ТекстОшибки + ?(ЗначениеЗаполнено(ТекстОшибки),Символы.ПС,"")+"Номер строки "+стр+". Не найдена номеклатура по штрихкоду '" + тШтрихкод+"'. Проверьте правильность штрихкода и повторите загрузку.";
						ВсеОк = Ложь;
						Продолжить;
					КонецЕсли;
				Исключение
					ТекстОшибки = ТекстОшибки + ?(ЗначениеЗаполнено(ТекстОшибки),Символы.ПС,"")+"Номер строки "+стр+". Неверный штрихкод '" + тШтрихкод+"'. Проверьте правильность штрихкода и повторите загрузку.";
					ВсеОк = Ложь;
					Продолжить;;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаЗагрузки.Добавить();
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.Цена = тНоваяЦена;
		Если тМРЦ = "" Или тМРЦ = "0" Тогда
		Иначе
			Характеристика = Справочники.ХарактеристикиНоменклатуры.ВернутьХарактеристикуПоЗначениюМРЦ(Номенклатура,Число(тМРЦ), ,Истина);
			НоваяСтрока.ХарактеристикаНоменклатуры = Характеристика;
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураВозрата = Новый Структура("ВсеОк,тзНоменклатура,ТекстОшибки",ВсеОк,ТаблицаЗагрузки,ТекстОшибки);
	
	Возврат СтруктураВозрата;
	
КонецФункции

&НаСервере
//БезКонтекста
Функция ДоговораНаСервере()
	
	РезНоменклатура = ПолучитьНоменклатуру();
	
	Если РезНоменклатура.ВсеОк Тогда
		тзНоменклатура = РезНоменклатура.тзНоменклатура;
	Иначе
		Возврат РезНоменклатура.ТекстОшибки;
	КонецЕсли;
	
	Номенклатура = тзНоменклатура.ВыгрузитьКолонку("Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СогласованиеЦен.Номенклатура КАК Номенклатура,
	|	СогласованиеЦен.Контрагент КАК Контрагент,
	|	СогласованиеЦен.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	МАКСИМУМ(СогласованиеЦен.Регистратор.Дата) КАК РегистраторДата
	|ПОМЕСТИТЬ втСогласованиеЦен
	|ИЗ
	|	РегистрСведений.СогласованиеЦен КАК СогласованиеЦен
	|ГДЕ
	|	СогласованиеЦен.СрокДействия >= &Дата
	|	И СогласованиеЦен.Период <= &Дата
	|	И СогласованиеЦен.Номенклатура В(&Номенклатура)
	|	И СогласованиеЦен.ДоговорВзаиморасчетов.Контрагент В(&Контрагент)
	|	И СогласованиеЦен.ДоговорВзаиморасчетов.Подразделение В(&Подразделение)
	|
	|СГРУППИРОВАТЬ ПО
	|	СогласованиеЦен.Контрагент,
	|	СогласованиеЦен.ДоговорВзаиморасчетов,
	|	СогласованиеЦен.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	втСогласованиеЦен.Номенклатура КАК Номенклатура,
	|	втСогласованиеЦен.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	втСогласованиеЦен.Контрагент КАК Контрагент,
	|	МАКСИМУМ(СогласованиеЦен.Цена) КАК Цена
	|ПОМЕСТИТЬ втСогласованиеЦенОбщий
	|ИЗ
	|	втСогласованиеЦен КАК втСогласованиеЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СогласованиеЦен КАК СогласованиеЦен
	|		ПО втСогласованиеЦен.Номенклатура = СогласованиеЦен.Номенклатура
	|			И втСогласованиеЦен.ДоговорВзаиморасчетов = СогласованиеЦен.ДоговорВзаиморасчетов
	|			И втСогласованиеЦен.Контрагент = СогласованиеЦен.Контрагент
	|			И втСогласованиеЦен.РегистраторДата = СогласованиеЦен.Регистратор.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	втСогласованиеЦен.Номенклатура,
	|	втСогласованиеЦен.ДоговорВзаиморасчетов,
	|	втСогласованиеЦен.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	|ИЗ
	|	втСогласованиеЦенОбщий КАК втСогласованиеЦенОбщий
	|ГДЕ
	|	втСогласованиеЦенОбщий.Цена > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов
	|
	|УПОРЯДОЧИТЬ ПО
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов.Контрагент,
	|	втСогласованиеЦенОбщий.ДоговорВзаиморасчетов.Организация
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Контрагент", Контрагенты);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Подразделение", Подразделения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Договора.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура Договора(Команда)
	Если Не ТабДок.ВысотаТаблицы > 1 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет товара для проверки'; uk = 'Немає товару для перевірки'"));
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = ДоговораНаСервере();
	
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПриИзменении(Элемент)
	Элементы.ФормаДоговора.Видимость = АвтоДоговор И ЗначениеЗаполнено(Контрагенты);
КонецПроцедуры


&НаКлиенте
Процедура АкцияПриИзменении(Элемент)
	
	Если Акция Тогда
		Элементы.КонецПериода.Доступность = Истина;
	Иначе
		Элементы.КонецПериода.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры


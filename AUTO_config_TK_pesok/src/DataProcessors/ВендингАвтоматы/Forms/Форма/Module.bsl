

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьАвтоматы();
	ЗаполнитьДеревоТоваров();
	ПериодЗагрузки.Вариант = ВариантСтандартногоПериода.Сегодня;
	Пользователь = Пользователи.ТекущийПользователь();	
	Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Пользователь);
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.Подразделение.Видимость = ЭтоПолноправныйПользователь;
	Элементы.Документ.Видимость = ЭтоПолноправныйПользователь;
	Элементы.ГруппаЗагрузкаДанных.Видимость = ЭтоПолноправныйПользователь;
	Элементы.ОтборСклад.СписокВыбора.ЗагрузитьЗначения(МассивСкладов());
	
	Элементы.ГруппаПолезное.Видимость = ЭтоПолноправныйПользователь;
	Элементы.ГруппаВыгрузкаПраво.Видимость = ЭтоПолноправныйПользователь;
	Элементы.ГруппаПеремещениеВитрины.Видимость = ЭтоПолноправныйПользователь;
	ЗаполнитьПолезное();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоТоваров");
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовДеревоТоваров


&НаКлиенте
Процедура ДеревоТоваровПриАктивизацииЯчейки(Элемент)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.ДеревоТоваровПривязатьНоменклатуру.Видимость	= ЗначениеЗаполнено(Элемент.ТекущиеДанные.nameOnReceipt);
		Элементы.ДеревоТоваровСоздатьТовар.Видимость			= ЗначениеЗаполнено(Элемент.ТекущиеДанные.nameOnReceipt);
		Элементы.ДеревоТоваровСоздатьБренд.Видимость			= ЗначениеЗаполнено(Элемент.ТекущиеДанные.Бренд);
		Элементы.ДеревоТоваровОтвязатьНоменклатуру.Видимость	= ЗначениеЗаполнено(Элемент.ТекущиеДанные.nameOnReceipt);
		Элементы.ДеревоТоваровИзменитьТовар.Видимость			= ЗначениеЗаполнено(Элемент.ТекущиеДанные.nameOnReceipt);
		Элементы.ДеревоТоваровИзменитьБренд.Видимость			= ЗначениеЗаполнено(Элемент.ТекущиеДанные.Бренд);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьУзлыДерева(Команда)
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоТоваров",,Истина)
КонецПроцедуры

&НаКлиенте
Процедура СвернутьУзлыДерева(Команда)
	КоллекцияЭлементовДерева = ДеревоТоваров.ПолучитьЭлементы();
	//Свернуть дерево 
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоТоваров.Свернуть(ИдентификаторСтроки);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоТоваров(Команда)
	ЗаполнитьДеревоТоваров();
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоТоваров");
КонецПроцедуры

&НаКлиенте
Процедура ДеревоТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ДеревоТоваровimageUrl" Тогда
		ЗапуститьПриложение(Элемент.ТекущиеДанные.imageUrl);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьГруппу(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБренд(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТовар(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьГруппу(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьБренд(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТовар(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПривязатьНоменклатуру(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьНоменклатуру(Команда)
	СоздатьЭлемент(Команда.Имя,Истина);
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовАвтоматы


&НаКлиенте
Процедура АвтоматыПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Элементы.АвтоматыОтвязатьСклад.Видимость		= ЗначениеЗаполнено(ТекущиеДанные.Склад);
		Элементы.АвтоматыИзменитьВидАвтомата.Видимость	= ЗначениеЗаполнено(ТекущиеДанные.Склад);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАвтоматы(Команда)
	ЗаполнитьАвтоматы();
	Элементы.ОтборСклад.СписокВыбора.ЗагрузитьЗначения(МассивСкладов());
КонецПроцедуры

&НаКлиенте
Процедура СоздатьАвтомат(Команда)
	СоздатьЭлемент(Команда.Имя,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьАвтомат(Команда)
	СоздатьЭлемент(Команда.Имя,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПривязатьСклад(Команда)
	СоздатьЭлемент(Команда.Имя,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьСклад(Команда)
	СоздатьЭлемент(Команда.Имя,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидАвтомата(Команда)
	СоздатьЭлемент(Команда.Имя,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПланограмму(Команда)
	ЗапуститьФормированиеОтчета("Планограмма");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПродажи(Команда)
	ЗапуститьФормированиеОтчета("Продажи");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОстатки(Команда)
	ЗапуститьФормированиеОтчета("Остатки");
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовДокументы


&НаКлиенте 
Процедура ВыгрузкаПоступлениеТоваров(Команда)
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ПоступлениеТоваров");
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран склад отбора'; uk = 'Не вибрано склад відбору'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаПеремещениеТоваровПриход(Команда)
	
	Если ЗначениеЗаполнено(Документ) ТОгда
		ТабДок = ВыгрузкаПеремещениеТоваровНаСервере();
		Если ТабДок = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ТабДок.Показать("Перемещение Вендинг","Перемещение Вендинг");	
		
	Иначе
		Если ЗначениеЗаполнено(ОтборСклад) Тогда
			ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ПеремещениеТоваровПриход");
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Не выбран склад отбора'; uk = 'Не вибрано склад відбору'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаПеремещениеТоваровРасход(Команда)
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "ПеремещениеТоваровРасход");
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран склад отбора'; uk = 'Не вибрано склад відбору'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаИнвентаризация(Команда)
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		ОткрытьФормуПодтверждения("ВыгрузкаДокументов", "Инвентаризация");
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран склад отбора'; uk = 'Не вибрано склад відбору'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаЗакрытиеСмены(Команда)
	//ДопПараметры = Новый Структура;
	////ДопПараметры.Вставить("Проверка", флЗагрузитьЗКСНеЗагруженные);							
	//ДопПараметры.Вставить("Подразделение", Подразделение);
	//
	//ПараметрыФормы = Новый Структура("ДополнительныеПараметры", ДопПараметры);
	//
	//ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ЗакрытиеСмены", ПараметрыФормы);
	ОткрытьФормуПодтверждения("ЗагрузкаДокументов", "ЗакрытиеСмены");
КонецПроцедуры

&НаКлиенте
Процедура ПереоценитьОстатки(Команда)
	ЗапуститьФормированиеОтчета("Переоценка");
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовГрупповуха


&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	Если Не ТабДокумент.ВысотаТаблицы > 1 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для загрузки'; uk = 'Немає даних для завантаження'"));
		Возврат;
	КонецЕсли;
	Если ЧтоГрузим = "Планограмма" Тогда
		ЗаписатьПланограммуНаСервере();
	ИначеЕсли ЧтоГрузим = "Остаток" Тогда
		КорректировкаОстаткаНаСервере();
	ИначеЕсли ЧтоГрузим = "Товар" Тогда
		СоздатьТоварНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЧтоГрузимПриИзмененииНаСервере()
	ТабДокумент.Очистить();
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Загрузка");
	областьШапка = Макет.ПолучитьОбласть(ЧтоГрузим);
	ТабДокумент.Вывести(областьШапка);
КонецПроцедуры

&НаКлиенте
Процедура ЧтоГрузимПриИзменении(Элемент)
	ЧтоГрузимПриИзмененииНаСервере();
	Элементы.ЗагрузитьДанные.КнопкаПоУмолчанию = ЗначениеЗаполнено(ЧтоГрузим);
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции


&НаСервереБезКонтекста
Функция МассивВТаблицуЗначений(Массив,Команда)
	
	ТЗ = Новый ТаблицаЗначений;
	
	Если Команда = "product" Тогда
		ТЗ.Колонки.Добавить("id",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("uktzed",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(14)));
		ТЗ.Колонки.Добавить("parentId",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("article",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("nameOnReceipt",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(255)));
		ТЗ.Колонки.Добавить("nameOnScreen",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(255)));
		ТЗ.Колонки.Добавить("active",Новый ОписаниеТипов("Булево"));
		ТЗ.Колонки.Добавить("imageUrl",Новый ОписаниеТипов("Строка"));
		ТЗ.Колонки.Добавить("taxId",Новый ОписаниеТипов("Число"));
	ИначеЕсли Команда = "category" Тогда
		ТЗ.Колонки.Добавить("id",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("parentId",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("name",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(250)));
		ТЗ.Колонки.Добавить("active",Новый ОписаниеТипов("Булево"));
		ТЗ.Колонки.Добавить("imageUrl",Новый ОписаниеТипов("Строка"));
	ИначеЕсли Команда = "automat" Тогда
		ТЗ.Колонки.Добавить("id",Новый ОписаниеТипов("Число"));
		ТЗ.Колонки.Добавить("address",Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(255)));
		ТЗ.Колонки.Добавить("active",Новый ОписаниеТипов("Булево"));
		ТЗ.Колонки.Добавить("cellCount",Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Для Каждого Структура Из Массив Цикл
		НоваяСтрока = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
		//Если Команда = "product" Тогда
		//	НоваяСтрока.article = Формат(Структура.article,"ЧГ=0");
		//КонецЕсли;
	КонецЦикла;	
	
	Возврат ТЗ;
	
КонецФункции


&НаСервере
Процедура ЗаполнитьАвтоматы()
	
	Ответ = Обработки.ВендингАвтоматы.ПолучитьДанные_GET("automat");
	
	Если Ответ.КодСостояния > 399 Тогда
		Обработки.ВендингАвтоматы.Ошибка404(Ответ);
		Возврат;
	КонецЕсли;
	
	тзАвтоматы = МассивВТаблицуЗначений(Ответ.Данные,"automat");
	
	УстановитьПривилегированныйРежим(Истина);			
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тзАвтоматы.id КАК id,
	|	тзАвтоматы.address КАК address,
	|	тзАвтоматы.active КАК active,
	|	тзАвтоматы.cellCount КАК cellCount
	|ПОМЕСТИТЬ втАвтоматы
	|ИЗ
	|	&тзАвтоматы КАК тзАвтоматы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втАвтоматы.id КАК id,
	|	втАвтоматы.address КАК address,
	|	втАвтоматы.active КАК active,
	|	втАвтоматы.cellCount КАК cellCount,
	|	СкладыКомпании.Ссылка ЕСТЬ NULL КАК СкладНеНайден
	|ИЗ
	|	втАвтоматы КАК втАвтоматы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
	|		ПО втАвтоматы.id = СкладыКомпании.ИД_Автомата";
	
	Запрос.УстановитьПараметр("тзАвтоматы",тзАвтоматы);
	
	тзНайденных = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);			
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	тзАвтоматы.id КАК id,
	|	тзАвтоматы.address КАК address,
	|	тзАвтоматы.active КАК active,
	|	тзАвтоматы.cellCount КАК cellCount,
	|	тзАвтоматы.СкладНеНайден КАК СкладНеНайден
	|ПОМЕСТИТЬ втАвтоматы
	|ИЗ
	|	&тзАвтоматы КАК тзАвтоматы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втАвтоматы.id КАК id,
	|	втАвтоматы.address КАК address,
	|	втАвтоматы.active КАК active,
	|	втАвтоматы.cellCount КАК cellCount,
	|	СкладыКомпании.Ссылка КАК Склад
	|ПОМЕСТИТЬ втСоСкладом
	|ИЗ
	|	втАвтоматы КАК втАвтоматы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
	|		ПО втАвтоматы.id = СкладыКомпании.ИД_Автомата
	|ГДЕ
	|	(втАвтоматы.СкладНеНайден
	|			ИЛИ НЕ СкладыКомпании.Ссылка ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втСоСкладом.id КАК id,
	|	втСоСкладом.address КАК address,
	|	втСоСкладом.active КАК active,
	|	втСоСкладом.cellCount КАК cellCount,
	|	втСоСкладом.Склад КАК Склад,
	|	Дубли.Дублей = 1 КАК Ок
	|ИЗ
	|	втСоСкладом КАК втСоСкладом
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			втСоСкладом.id КАК id,
	|			КОЛИЧЕСТВО(втСоСкладом.id) КАК Дублей
	|		ИЗ
	|			втСоСкладом КАК втСоСкладом
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втСоСкладом.id) КАК Дубли
	|		ПО втСоСкладом.id = Дубли.id
	|
	|УПОРЯДОЧИТЬ ПО
	|	address,
	|	Склад
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("тзАвтоматы",тзНайденных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	ЗначениеВРеквизитФормы(ТЗ, "Автоматы");
	
	Элементы.ГруппаАвтоматы.Заголовок = "Автоматы ("+ТЗ.Количество()+")";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоТоваров()
	
	ОтветТовар = Обработки.ВендингАвтоматы.ПолучитьДанные_GET("product");
	
	Если ОтветТовар.КодСостояния > 399 Тогда
		Обработки.ВендингАвтоматы.Ошибка404(ОтветТовар);
		Возврат;
	КонецЕсли;
	
	ОтветГруппы = Обработки.ВендингАвтоматы.ПолучитьДанные_GET("category");
	
	Если ОтветГруппы.КодСостояния > 399 Тогда
		Обработки.ВендингАвтоматы.Ошибка404(ОтветГруппы);
		Возврат;
	КонецЕсли;
	
	СтруктураГрупп.Очистить();
	тзТовар = МассивВТаблицуЗначений(ОтветТовар.Данные,"product");
	тзГруппы = МассивВТаблицуЗначений(ОтветГруппы.Данные,"category");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	тзКатегории.id КАК id,
	|	тзКатегории.name КАК name,
	|	тзКатегории.parentId КАК parentId,
	|	тзКатегории.active КАК active,
	|	тзКатегории.imageUrl КАК imageUrl
	|ПОМЕСТИТЬ втКатегории
	|ИЗ
	|	&тзКатегории КАК тзКатегории
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втКатегории.id КАК id,
	|	втКатегории.name КАК name,
	|	втКатегории.active КАК active
	|ПОМЕСТИТЬ втГруппы
	|ИЗ
	|	втКатегории КАК втКатегории
	|ГДЕ
	|	втКатегории.parentId = 857
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГруппы.id КАК Группа_id,
	|	втГруппы.name КАК Группа_name,
	|	втГруппы.active КАК Группа_active,
	|	втБренды.id КАК Бренд_id,
	|	втБренды.name КАК Бренд_name,
	|	втБренды.parentId КАК Бренд_parentId,
	|	втБренды.active КАК Бренд_active,
	|	втБренды.imageUrl КАК Бренд_imageUrl
	|ПОМЕСТИТЬ втГруппыБренды
	|ИЗ
	|	втГруппы КАК втГруппы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКатегории КАК втБренды
	|		ПО втГруппы.id = втБренды.parentId
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	тзТовар.id КАК id,
	|	тзТовар.nameOnScreen КАК nameOnScreen,
	|	тзТовар.nameOnReceipt КАК nameOnReceipt,
	|	тзТовар.parentId КАК parentId,
	|	тзТовар.article КАК article,
	|	тзТовар.uktzed КАК uktzed,
	|	тзТовар.taxId КАК taxId,
	|	тзТовар.active КАК active,
	|	тзТовар.imageUrl КАК imageUrl
	|ПОМЕСТИТЬ втТовар
	|ИЗ
	|	&тзТовар КАК тзТовар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втГруппыБренды.Группа_name КАК Группа,
	|	втГруппыБренды.Группа_id КАК Группа_id,
	|	втГруппыБренды.Группа_active КАК Группа_active,
	|	втГруппыБренды.Бренд_name КАК Бренд,
	|	втГруппыБренды.Бренд_id КАК Бренд_id,
	|	втГруппыБренды.Бренд_parentId КАК Бренд_parentId,
	|	втГруппыБренды.Бренд_active КАК Бренд_active,
	|	втГруппыБренды.Бренд_imageUrl КАК Бренд_imageUrl,
	|	втТовар.id КАК id,
	|	втТовар.nameOnScreen КАК nameOnScreen,
	|	втТовар.nameOnReceipt КАК nameOnReceipt,
	|	втТовар.parentId КАК parentId,
	|	втТовар.article КАК article,
	|	втТовар.uktzed КАК uktzed,
	|	втТовар.taxId КАК taxId,
	|	втТовар.active КАК active,
	|	втТовар.imageUrl КАК imageUrl,
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	СтавкиНДС.Ссылка КАК СтавкаНДС,
	|	ИСТИНА КАК ОК_НДС,
	|	ИСТИНА КАК ОК_УКТВЭД,
	|	ИСТИНА КАК ОК_Арикул,
	|	ИСТИНА КАК ОК_Наименование,
	|	0 КАК Картинка
	|ИЗ
	|	втГруппыБренды КАК втГруппыБренды
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТовар КАК втТовар
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
	|			ПО втТовар.taxId = СтавкиНДС.Порядок
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|			ПО втТовар.id = Номенклатура.ИД_Товара
	|		ПО втГруппыБренды.Бренд_id = втТовар.parentId
	|
	|УПОРЯДОЧИТЬ ПО
	|	Группа,
	|	Бренд,
	|	nameOnScreen
	|ИТОГИ
	|	МАКСИМУМ(Группа_id),
	|	МАКСИМУМ(Группа_active),
	|	МАКСИМУМ(Бренд_id),
	|	МАКСИМУМ(Бренд_active),
	|	МАКСИМУМ(Бренд_imageUrl)
	|ПО
	|	Группа,
	|	Бренд";
	
	Запрос.УстановитьПараметр("тзТовар",тзТовар);
	Запрос.УстановитьПараметр("тзКатегории",тзГруппы);
	
	РезультатЗапроса = Запрос.Выполнить();	
	
	ДТ = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОК_НДС			= Истина;
	ОК_УКТВЭД		= Истина;
	ОК_Арикул		= Истина;
	ОК_Наименование	= Истина;
	ОК_Номенклатура	= Истина;
	
	Для Каждого строка ИЗ ДТ.Строки Цикл
		строка.nameOnScreen = строка.Группа;
		строка.id = строка.Группа_id;
		строка.active = строка.Группа_active;
		Для Каждого строкаСтроки ИЗ строка.Строки Цикл
			Если строкаСтроки.Бренд_id = NULL Тогда
				строка.Строки.Удалить(строкаСтроки);
				Продолжить;
			КонецЕсли;
			строкаСтроки.nameOnScreen = строкаСтроки.Бренд;
			строкаСтроки.id = строкаСтроки.Бренд_id;
			строкаСтроки.active = строкаСтроки.Бренд_active;
			строкаСтроки.imageUrl = строкаСтроки.Бренд_imageUrl;
			НоваяСтрока = СтруктураГрупп.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,строкаСтроки);
			Для Каждого строкаТовара ИЗ строкаСтроки.Строки Цикл
				Если строкаТовара.id = NULL Тогда
					строкаСтроки.Строки.Удалить(строкаТовара);
					Продолжить;
				КонецЕсли;
				Номенклатура = строкаТовара.Номенклатура;
				Если ЗначениеЗаполнено(Номенклатура) Тогда
					Если Номенклатура.ЭтоГруппа Тогда
						строкаТовара.Картинка = 3;	
					Иначе
						строкаТовара.ОК_НДС			= строкаТовара.taxId = Номенклатура.СтавкаНДС.Порядок;
						строкаТовара.ОК_УКТВЭД		= строкаТовара.uktzed = СтрЗаменить(Номенклатура.КодУКТВЭД.Код," ","");	// + ?(Номенклатура.СтранаПроисхождения.Код = "804","","Х");
						строкаТовара.ОК_Арикул		= Формат(строкаТовара.article,"ЧГ=0") = Номенклатура.Артикул;
						строкаТовара.ОК_Наименование= СокрЛП(строкаТовара.nameOnReceipt) = СокрЛП(Номенклатура.Наименование2);
						строкаТовара.Картинка		= НЕ (строкаТовара.ОК_НДС И строкаТовара.ОК_УКТВЭД И строкаТовара.ОК_Арикул И строкаТовара.ОК_Наименование);
					КонецЕсли;
				Иначе
					строкаТовара.Картинка = 2;
				КонецЕсли;
				ОК_НДС			= ОК_НДС			И строкаТовара.ОК_НДС;
				ОК_УКТВЭД		= ОК_УКТВЭД			И строкаТовара.ОК_УКТВЭД;
				ОК_Арикул		= ОК_Арикул			И строкаТовара.ОК_Арикул;
				ОК_Наименование	= ОК_Наименование	И строкаТовара.ОК_Наименование;
				ОК_Номенклатура	= ОК_Номенклатура	И (ЗначениеЗаполнено(Номенклатура) И НЕ Номенклатура.ЭтоГруппа);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДТ, "ДеревоТоваров");
	
	Элементы.ГруппаДеревоТоваров.Заголовок = "Дерево товаров ("+тзТовар.Количество()+")";
	
	Если ОК_НДС Тогда
		Элементы.ДеревоТоваровСтавкаНДС.КартинкаШапки = Новый Картинка ;
	Иначе
		Элементы.ДеревоТоваровСтавкаНДС.КартинкаШапки = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	Если ОК_УКТВЭД Тогда
		Элементы.ДеревоТоваровuktzed.КартинкаШапки = Новый Картинка;
	Иначе
		Элементы.ДеревоТоваровuktzed.КартинкаШапки = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	Если ОК_Арикул Тогда
		Элементы.ДеревоТоваровarticle.КартинкаШапки = Новый Картинка;
	Иначе
		Элементы.ДеревоТоваровarticle.КартинкаШапки = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	Если ОК_Наименование Тогда
		Элементы.ДеревоТоваровnameOnReceipt.КартинкаШапки = Новый Картинка;
	Иначе
		Элементы.ДеревоТоваровnameOnReceipt.КартинкаШапки = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	Если ОК_Номенклатура Тогда
		Элементы.ДеревоТоваровНоменклатура.КартинкаШапки = Новый Картинка;
	Иначе
		Элементы.ДеревоТоваровНоменклатура.КартинкаШапки = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлемент(Действие, Товар)
	
	
	Если Товар Тогда
		Данные = Элементы.ДеревоТоваров.ТекущиеДанные;
		
		_Параметры = Новый Структура("Действие,ГруппаИмя,ГруппаКод,БрендИмя,БрендКод,Бренд_imageUrl,ТоварИмя,ТоварКод,imageUrl,Номенклатура,СтруктураГрупп");
		_Параметры.СтруктураГрупп	= СтруктураГрупп;
		_Параметры.ГруппаИмя		= Данные.Группа;
		_Параметры.ГруппаКод		= Данные.Группа_id;
		_Параметры.БрендИмя			= Данные.Бренд;
		_Параметры.БрендКод			= Данные.Бренд_id;
		_Параметры.Бренд_imageUrl	= Данные.Бренд_imageUrl;
		_Параметры.ТоварИмя			= Данные.nameOnScreen;
		_Параметры.ТоварКод			= Данные.id;
		_Параметры.imageUrl			= Данные.imageUrl;
		_Параметры.Номенклатура		= Данные.Номенклатура;
	Иначе
		Данные = Элементы.Автоматы.ТекущиеДанные;
		
		_Параметры = Новый Структура("Действие,АвтоматАдрес,АвтоматКод,КоличествоЯчеек,Склад");
		_Параметры.АвтоматАдрес		= Данные.address;
		_Параметры.АвтоматКод		= Данные.id;
		_Параметры.КоличествоЯчеек	= Данные.cellCount;
		_Параметры.Склад			= Данные.Склад;
	КонецЕсли;
	
	_Параметры.Действие		= Действие;
	
	ОткрытьФорму("Обработка.ВендингАвтоматы.Форма.ФормаТовара", _Параметры, ЭтотОбъект,ЭтотОбъект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Функция ПреобразоватьТабличныйДокументВТаблицуЗначений(ТабДокумент) 
	ПоследняяСтрока = ТабДокумент.ВысотаТаблицы;
	ПоследняяКолонка = ТабДокумент.ШиринаТаблицы;
	ОбластьЯчеек = ТабДокумент.Область(1, 1, ПоследняяСтрока, ПоследняяКолонка); 
	// Создаем описание источника данных на основании области ячеек табличного документа.
	ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьЯчеек);  
	// Создаем объект для интеллектуального построения отчетов,
	// указываем источник данных и выполняем построение отчета.
	ПостроительОтчета = Новый ПостроительОтчета; 
	ПостроительОтчета.ИсточникДанных = ИсточникДанных;
	ПостроительОтчета.Выполнить();
	// Результат выгружаем в таблицу значений.
	ТабЗначений = ПостроительОтчета.Результат.Выгрузить();
	Возврат ТабЗначений
КонецФункции

&НаСервере
Процедура ЗагрузитьНаСервере()
	
	ТзТп = ПреобразоватьТабличныйДокументВТаблицуЗначений(ТабДокумент);
	спАртикулов = ТзТп.ВыгрузитьКолонку("Артикул");	
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Артикул КАК Артикул,
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Родитель КАК Родитель,
	|	Номенклатура.ТоварнаяМарка КАК ТоварнаяМарка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Артикул В(&Артикул)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Родитель,
	|	ТоварнаяМарка,
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Артикул", спАртикулов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	кАртикул = 1;
	кИД_Товара = 3;
	кИД_Бренда = 4;
	
	Для Стр = 2 По ТабДокумент.ВысотаТаблицы Цикл 
		
		Артикул = СокрЛП(ТабДокумент.Область(Стр, кАртикул).Текст);
		
		Если НЕ ЗначениеЗаполнено(Артикул) Тогда
			Продолжить;
		КонецЕсли;
		
		ТоварСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
		
		Если НЕ ЗначениеЗаполнено(ТоварСсылка) Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не найдена номенклатура с артикулом %2'; uk = 'Рядок %1. Не знайдено номенклатуру з артикулом %2'"),
			Стр,
			Артикул));
			Продолжить;
		КонецЕсли;
		
		ИД_Товара = СокрЛП(ТабДокумент.Область(Стр, кИД_Товара).Текст);
		ИД_Товара = СтрЗаменить(ИД_Товара, " ", "");
		
		Попытка
			ИД_Товара = Число(ИД_Товара);	
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не корректный id: %2'; uk = 'Рядок %1. Не коректний id: %2'"),
			Стр,
			ИД_Товара));
			Продолжить;
		КонецПопытки;
		
		ИД_Бренда = СокрЛП(ТабДокумент.Область(Стр, кИД_Бренда).Текст);
		ИД_Бренда = СтрЗаменить(ИД_Бренда, " ", "");
		
		
		Попытка
			ИД_Бренда = Число(ИД_Бренда);	
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не корректный id бренда: %2'; uk = 'Рядок %1. Не коректний id бренду: %2'"),
			Стр,
			ИД_Бренда));
			Продолжить;
		КонецПопытки;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Данные = Новый Структура;
		Данные.Вставить("id",ИД_Товара);
		Данные.Вставить("parentId",ИД_Бренда);
		Данные.Вставить("article",Число(ТоварСсылка.Артикул));
		Данные.Вставить("uktzed",СтрЗаменить(ТоварСсылка.КодУКТВЭД.Код," ",""));	// + ?(ТоварСсылка.СтранаПроисхождения.Код = "804","","Х"));
		Данные.Вставить("nameOnScreen",СокрЛП(ТоварСсылка.Наименование));
		Данные.Вставить("nameOnReceipt",СокрЛП(ТоварСсылка.Наименование2));
		Данные.Вставить("taxId",ТоварСсылка.СтавкаНДС.Порядок);
		//Данные.Вставить("imageUrl",Товар_imageUrl);
		Данные.Вставить("active",Истина);
		
		Попытка
			тт = Обработки.ВендингАвтоматы.ИзменитьЭлемент_PUT("product/"+ИД_Товара,Данные);
			ТоварОбъект = ТоварСсылка.ПолучитьОбъект();
			ТоварОбъект.ИД_Товара = ИД_Товара;
			ТоварОбъект.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
		КонецПопытки;
		
		
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю("ГОТОВО!");
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТоварНаСервере()
	
	ТзТп = ПреобразоватьТабличныйДокументВТаблицуЗначений(ТабДокумент);
	спАртикулов = ТзТп.ВыгрузитьКолонку("Артикул");	
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Артикул КАК Артикул,
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Родитель КАК Родитель,
	|	Номенклатура.ТоварнаяМарка КАК ТоварнаяМарка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Артикул В(&Артикул)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Родитель,
	|	ТоварнаяМарка,
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Артикул", спАртикулов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	кАртикул = 1;
	кИД_Товара = 3;
	кИД_Бренда = 4;
	к_imageUrl = 5;
	
	Для Стр = 2 По ТабДокумент.ВысотаТаблицы Цикл 
		
		Артикул = СокрЛП(ТабДокумент.Область(Стр, кАртикул).Текст);
		
		Если НЕ ЗначениеЗаполнено(Артикул) Тогда
			Продолжить;
		КонецЕсли;
		
		ТоварСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
		
		Если НЕ ЗначениеЗаполнено(ТоварСсылка) Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не найдена номенклатура с артикулом %2'; uk = 'Рядок %1. Не знайдено номенклатуру з артикулом %2'"),
			Стр,
			Артикул));
			Продолжить;
		КонецЕсли;
		
		ИД_Товара = СокрЛП(ТабДокумент.Область(Стр, кИД_Товара).Текст);
		ИД_Товара = СтрЗаменить(ИД_Товара, " ", "");
		
		Попытка
			ИД_Товара = Число(ИД_Товара);	
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не корректный id: %2'; uk = 'Рядок %1. Не коректний id: %2'"),
			Стр,
			ИД_Товара));
			Продолжить;
		КонецПопытки;
		
		ИД_Бренда = СокрЛП(ТабДокумент.Область(Стр, кИД_Бренда).Текст);
		ИД_Бренда = СтрЗаменить(ИД_Бренда, " ", "");
		
		
		Попытка
			ИД_Бренда = Число(ИД_Бренда);	
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не корректный id бренда: %2'; uk = 'Рядок %1. Не коректний id бренду: %2'"),
			Стр,
			ИД_Бренда));
			Продолжить;
		КонецПопытки;
		
		imageUrl = СокрЛП(ТабДокумент.Область(Стр, к_imageUrl).Текст);
		
		
		УстановитьПривилегированныйРежим(Истина);
		
		Данные = Новый Структура;
		//Данные.Вставить("id",ИД_Товара);
		Данные.Вставить("parentId",ИД_Бренда);
		Данные.Вставить("article",Число(ТоварСсылка.Артикул));
		Данные.Вставить("uktzed",СтрЗаменить(ТоварСсылка.КодУКТВЭД.Код," ",""));	// + ?(ТоварСсылка.СтранаПроисхождения.Код = "804","","Х"));
		Данные.Вставить("nameOnScreen",СокрЛП(ТоварСсылка.Наименование));
		Данные.Вставить("nameOnReceipt",СокрЛП(ТоварСсылка.Наименование2));
		Данные.Вставить("taxId",ТоварСсылка.СтавкаНДС.Порядок);
		Данные.Вставить("imageUrl",imageUrl);
		Данные.Вставить("active",Истина);
		
		Попытка
			тт = Обработки.ВендингАвтоматы.СоздатьЭлемент_POST("product",Данные);
			ТоварОбъект = ТоварСсылка.ПолучитьОбъект();
			ТоварОбъект.ИД_Товара = тт.id;
			ТоварОбъект.Записать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Сообщить(ОписаниеОшибки);
		КонецПопытки;
		
		
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю("ГОТОВО!");
	
КонецПроцедуры


&НаСервере
Процедура ЗаписатьПланограммуНаСервере()
	
	кАртикул = 1;
	кКодСклад = 3;
	кХарка = 4;
	к_Ячейка = 5;
	
	//Планограмма = Новый ТаблицаЗначений;
	//Планограмма.Колонки.Добавить("Период");
	//Планограмма.Колонки.Добавить("Склад");
	//Планограмма.Колонки.Добавить("Номенклатура");
	//Планограмма.Колонки.Добавить("Характеристика");
	//Планограмма.Колонки.Добавить("Ячейка");
	
	Для Стр = 2 По ТабДокумент.ВысотаТаблицы Цикл 
		
		КодСклада = СокрЛП(ТабДокумент.Область(Стр, кКодСклад).Текст);
		
		Если НЕ ЗначениеЗаполнено(КодСклада) Тогда
			Продолжить;
		КонецЕсли;
		
		СкладСсылка = Справочники.СкладыКомпании.НайтиПоКоду(КодСклада);
		
		Артикул = СокрЛП(ТабДокумент.Область(Стр, кАртикул).Текст);
		
		Если НЕ ЗначениеЗаполнено(Артикул) Тогда
			Продолжить;
		КонецЕсли;
		
		ТоварСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Артикул);
		
		Если НЕ ЗначениеЗаполнено(ТоварСсылка) Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не найдена номенклатура с артикулом %2'; uk = 'Рядок %1. Не знайдено номенклатуру з артикулом %2'"),
			Стр,
			Артикул));
			Продолжить;
		КонецЕсли;
		
		КодХарка = СокрЛП(ТабДокумент.Область(Стр, кХарка).Текст);
		
		Если ЗначениеЗаполнено(КодХарка) Тогда
			ХаркаСсылка = Справочники.ХарактеристикиНоменклатуры.НайтиПоКоду(КодХарка);
		Иначе
			ХаркаСсылка = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
		
		
		//ИД_Товара = СокрЛП(ТабДокумент.Область(Стр, кИД_Товара).Текст);
		//ИД_Товара = СтрЗаменить(ИД_Товара, " ", "");
		//
		//Попытка
		//	ИД_Товара = Число(ИД_Товара);	
		//Исключение
		//	ОбщегоНазначения.СообщитьПользователю(
		//	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		//	НСтр("ru = 'Строка %1. Не корректный id: %2'; uk = 'Рядок %1. Не коректний id: %2'"),
		//	Стр,
		//	ИД_Товара));
		//	Продолжить;
		//КонецПопытки;
		
		Ячейка = СокрЛП(ТабДокумент.Область(Стр, к_Ячейка).Текст);
		
		
		
		УстановитьПривилегированныйРежим(Истина);
		
		//Строка = Планограмма.Добавить();
		//Строка.Склад = СкладСсылка;
		//Строка.Номенклатура = ТоварСсылка;
		//Строка.Характеристика = ХаркаСсылка;
		//Строка.Ячейка = Ячейка;
		
		
		МенеджерЗаписи = РегистрыСведений.ПланограммаАвтомата.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущаяДата();
		МенеджерЗаписи.Склад = СкладСсылка;
		МенеджерЗаписи.Номенклатура = ТоварСсылка;
		МенеджерЗаписи.Характеристика = ХаркаСсылка;
		МенеджерЗаписи.Ячейка = Ячейка;
		МенеджерЗаписи.Записать();
		
		
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
	//Планограмма.ЗаполнитьЗначения(ТекущаяДата(),"Период");
	//
	//Рег = РегистрыСведений.ПланограммаАвтомата;
	//Набор = Рег.СоздатьНаборЗаписей();
	//// формирование таблицы значений
	//
	//// ...
	//
	//Набор.Загрузить(Планограмма);
	//
	//Попытка
	//	Набор.Записать();
	//Исключение
	//	ОписаниеОшибки = ОписаниеОшибки();
	//	Сообщить(ОписаниеОшибки);
	//КонецПопытки;
	
	
	ОбщегоНазначения.СообщитьПользователю("ГОТОВО!");
	
КонецПроцедуры


&НаСервере
Процедура КорректировкаОстаткаНаСервере()
	кАртикул = 1;
	кСклад = 3;
	кХарка = 4;
	кКолво = 5;
	
	Остатки = Новый ТаблицаЗначений;
	Остатки.Колонки.Добавить("Артикул");
	Остатки.Колонки.Добавить("Характеристика");
	Остатки.Колонки.Добавить("Количество");
	
	МассивДанных = Новый Массив;
	
	ТаблицаОстатков = Обработки.ВендингАвтоматы.ПолучитьТаблицаЗначений();
	
	Для Стр = 2 По ТабДокумент.ВысотаТаблицы Цикл 
		ИдСклада = СокрЛП(ТабДокумент.Область(Стр, кСклад).Текст);
		
		Если НЕ ЗначениеЗаполнено(ИдСклада) Тогда
			Продолжить;
		КонецЕсли;
		
		Артикул = СокрЛП(ТабДокумент.Область(Стр, кАртикул).Текст);
		
		Если НЕ ЗначениеЗаполнено(Артикул) Тогда
			Продолжить;
		КонецЕсли;
		
		Харка = СокрЛП(ТабДокумент.Область(Стр, кХарка).Текст);
		
		Колво = СокрЛП(ТабДокумент.Область(Стр, кКолво).Текст);
		
		Строка = Остатки.Добавить();
		Строка.Артикул =  Число(Артикул);
		Строка.Характеристика = Число(Харка)*100;
		Строка.Количество = Число(Колво);
	КонецЦикла;
	
	Ответ = Обработки.ВендингАвтоматы.ПолучитьДанные_GET("automat/"+Формат(ИдСклада,"ЧГ=0")+"/cell/");
	
	Если Ответ.КодСостояния > 399 Тогда
		Обработки.ВендингАвтоматы.Ошибка404(Ответ,"GET");
		Возврат;
	КонецЕсли;
	
	Для Каждого стрДанные Из Ответ.Данные Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОстатков.Добавить(),стрДанные);
	КонецЦикла;
	
	Для Каждого стрОстатки Из Остатки Цикл
		стрОстаток = ТаблицаОстатков.НайтиСтроки(Новый Структура("article,maxRetailPrice",стрОстатки.Артикул,стрОстатки.Характеристика));
		Если стрОстаток.Количество() > 0 Тогда
			ЯчейкаОстатков = стрОстаток[0];
			
			СтруктураСтроки = новый Структура;
			СтруктураСтроки.Вставить("number",ЯчейкаОстатков.number);
			СтруктураСтроки.Вставить("automatId",ИдСклада);
			СтруктураСтроки.Вставить("article",ЯчейкаОстатков.article);
			СтруктураСтроки.Вставить("maxRetailPrice",ЯчейкаОстатков.maxRetailPrice);
			СтруктураСтроки.Вставить("price",ЯчейкаОстатков.price);
			СтруктураСтроки.Вставить("remainingStock",ЯчейкаОстатков.remainingStock + стрОстатки.Количество);
			СтруктураСтроки.Вставить("order",ЯчейкаОстатков.order);
			СтруктураСтроки.Вставить("lotGuid","00000000-0000-0000-0000-000000000000");
			МассивДанных.Добавить(СтруктураСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Ответ = Обработки.ВендингАвтоматы.ИзменитьЭлемент_PATCH("automat/"+Формат(ИдСклада,"ЧГ=0")+"/cell/",МассивДанных);
	Если Ответ.КодСостояния > 399 Тогда
		Обработки.ВендингАвтоматы.Ошибка404(Ответ,"PATCH");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ошибка выгрузки данных'; uk = 'Помилка вивантаження даних'"));
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю("ГОТОВО!");
КонецПроцедуры






&НаСервере
Функция МассивСкладов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкладыКомпании.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.ИД_Автомата > 0
	|	И НЕ СкладыКомпании.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	МассивСкладов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивСкладов;
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуПодтверждения(РежимФормы, ТипДокумента, ПараметрыФормы = Неопределено)
	
	Если ПараметрыФормы = Неопределено Тогда 
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ПериодЗагрузки", 	ПериодЗагрузки);
	ПараметрыФормы.Вставить("ТипДокумент", 		ТипДокумента);
	ПараметрыФормы.Вставить("РежимФормы", 		РежимФормы);
	ПараметрыФормы.Вставить("Подразделение", 	Подразделение);
	
	ДобавитьПараметрыОтбора(ПараметрыФормы);
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ОповещениеОЗакрытииФормыПодтверждения", ЭтаФорма, ПараметрыФормы);
	
	ОткрытьФорму("Обработка.ВендингАвтоматы.Форма.ФормаПодтверждения", ПараметрыФормы, ЭтаФорма,,,, ОповещениеЗакрытия, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПараметрыОтбора(ПараметрыФормы)
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		МассивСкладов = Новый Массив();
		МассивСкладов.Добавить(ОтборСклад);
	Иначе
		МассивСкладов = МассивСкладов();
	КонецЕсли;
	ПараметрыФормы.Вставить("МассивСкладов",МассивСкладов);
	ПараметрыФормы.Вставить("ОтборПоСкладам",ЗначениеЗаполнено(ОтборСклад));
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗакрытииФормыПодтверждения(РезультатЗакрытия, ДопПараметры) Экспорт 
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда 
		СообщенияПользователю = Неопределено;
		Если РезультатЗакрытия.Свойство("СообщенияПользователю", СообщенияПользователю) Тогда 			
			Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
				Для Каждого Сообщение Из СообщенияПользователю Цикл 
					Если Не Найти(Сообщение.Текст, "{СтандартныеПодсистемы.ДлительныеОперации}") Тогда 
						Сообщение.Сообщить();
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры



&НаСервере
Функция ВыгрузкаПеремещениеТоваровНаСервере()
	Если Не ЗначениеЗаполнено(ОтборСклад.ВидАвтомата) Тогда
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'У склада (%1) %2 не установлен вид автомата'; uk = 'У складу (%1) %2 не встановлено тип автомата'"),
		СокрЛП(ОтборСклад.Код),
		ОтборСклад.Наименование));
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ Документ.Проведен Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	стрАвтомат = Автоматы.Выгрузить().Найти(ОтборСклад,"Склад");
	
	Если стрАвтомат = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МассивДанных = Новый Массив;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ФиксацияСверху=1;
	ТабДок.ИспользуемоеИмяФайла = "ПеремещениеВендинг";
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	
	областьШапка	= Макет.ПолучитьОбласть("Шапка");
	областьСтрока	= Макет.ПолучитьОбласть("Строка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Ссылка КАК Ссылка,
	|	ПеремещениеТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ПеремещениеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(ПланограммаАвтоматаСрезПоследних.Ячейка, 0) КАК Ячейка
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&НаДату, ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	|		ПО ПеремещениеТоваровТовары.Номенклатура = ЦеныСрезПоследних.Номенклатура
	|			И ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланограммаАвтомата.СрезПоследних КАК ПланограммаАвтоматаСрезПоследних
	|		ПО ПеремещениеТоваровТовары.Номенклатура = ПланограммаАвтоматаСрезПоследних.Номенклатура
	|			И ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры = ПланограммаАвтоматаСрезПоследних.Характеристика
	|			И ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель = ПланограммаАвтоматаСрезПоследних.Склад
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ячейка";
	
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.УстановитьПараметр("НаДату", КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ТипЦен", ОтборСклад.ТипЦенРозничнойТорговли);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ТабДок.Вывести(областьШапка);
	
	Пока Выборка.Следующий() Цикл
		
		//СтруктураСтроки = новый Структура;
		//СтруктураСтроки.Вставить("number",Выборка.НомерСтроки);
		//СтруктураСтроки.Вставить("automatId",стрАвтомат.id);
		//СтруктураСтроки.Вставить("article",Число(Выборка.Номенклатура.Артикул));
		//СтруктураСтроки.Вставить("maxRetailPrice",Выборка.Характеристика.ЗначениеМРЦ * 100);
		//СтруктураСтроки.Вставить("price",Выборка.Цена * 100);
		//СтруктураСтроки.Вставить("remainingStock",Выборка.КоличествоБазовое);
		//СтруктураСтроки.Вставить("lotGuid",Строка(Документ.УникальныйИдентификатор()));
		//МассивДанных.Добавить(СтруктураСтроки);
		
		областьСтрока.Параметры.Ячейка = Обработки.ВендингАвтоматы.ИмяЯчейки(ОтборСклад.ВидАвтомата.Рядов,ОтборСклад.ВидАвтомата.Ячеек,Выборка.Ячейка);
		областьСтрока.Параметры.product_id = Выборка.Номенклатура.ИД_Товара;
		областьСтрока.Параметры.cell = Выборка.Ячейка;
		областьСтрока.Параметры.title = Выборка.Номенклатура.Наименование2;
		областьСтрока.Параметры.quantity = Выборка.КоличествоБазовое;
		областьСтрока.Параметры.price = Выборка.Цена * 100;
		областьСтрока.Параметры.order = Выборка.НомерСтроки;
		областьСтрока.Параметры.maxRetailPrice = Выборка.Характеристика.ЗначениеМРЦ * 100;
		
		ТабДок.Вывести(областьСтрока);
		
		Если Выборка.Цена = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. У (%2) %3 нулевая цена'; uk = 'Рядок %1. У (%2) %3 нульова ціна'"),
			Выборка.НомерСтроки,
			Выборка.Номенклатура.Артикул,
			Выборка.Номенклатура));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции




&НаКлиенте
Процедура ЗапуститьФормированиеОтчета(ВидОтчета)
	
	ОчиститьСообщения();
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ВидОтчета",			ВидОтчета);
	
	Если ВидОтчета = "Планограмма" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Планограмма'; uk = 'Планограма'"));		
		ПараметрыЗагрузки.Вставить("ОтборСклад", 	Элементы.Автоматы.ТекущиеДанные.Склад);
	ИначеЕсли ВидОтчета = "Переоценка" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Переоценка остатков'; uk = 'Переоцінка залишків'"));		
	ИначеЕсли ВидОтчета = "Продажи" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Продажи за сегодня'; uk = 'Продажі за сьогодні'"));		
		ПараметрыЗагрузки.Вставить("ОтборСклад", 	Элементы.Автоматы.ТекущиеДанные.Склад);
	ИначеЕсли ВидОтчета = "Остатки" Тогда 
		ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Остатки на сейчас'; uk = 'Залишки на зараз'"));
		ПараметрыЗагрузки.Вставить("ОтборСклад", 	Элементы.Автоматы.ТекущиеДанные.Склад);
		//ИначеЕсли ВидОтчета = "РасхожденияЯкобс" Тогда 
		//	ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Расхождения по продажам автоматов Якобс'; uk = 'Розбіжності з продажу автоматів Якобс'"));
		//Иначе
		//	ПараметрыЗагрузки.Вставить("ПредставлениеВидаОтчета", НСтр("ru = 'Отчет'; uk = 'Звіт'"));			
	КонецЕсли;
	
	ПараметрыЗагрузки.Вставить("НачалоПериода", 	ПериодЗагрузки.ДатаНачала);
	ПараметрыЗагрузки.Вставить("КонецПериода", 		ПериодЗагрузки.ДатаОкончания);
	ПараметрыЗагрузки.Вставить("Подразделение", 	Подразделение);
	
	ДобавитьПараметрыОтбора(ПараметрыЗагрузки);
	
	ДлительнаяОперация = НачатьВыполнениеОперации("СформироватьОтчет", ПараметрыЗагрузки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Формируем отчет'; uk = 'Формуємо звіт'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.Вендинг";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	ПараметрыОжидания.Интервал = 2;	
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработкаРезультата", ЭтотОбъект, ПараметрыЗагрузки);	
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеОперации(Операция, ПараметрыОперации = Неопределено)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);				
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВендингАвтоматы."+Операция, ПараметрыОперации);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаРезультата(Операция, ПараметрыОтчета) Экспорт 
	
	Если Операция = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Операция.Статус = "Выполнено" Тогда		
		
		СообщениеЗавершения = НСтр("ru = 'Операция выполнена!'; uk = 'Операція виконана!'");		
		
		Если Операция.Свойство("АдресРезультата") Тогда 			
			
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);
			
			Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ТабличныйДокумент") Тогда 
				
				КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ПараметрыОтчета.ВидОтчета);
				ТекКоллекция = КоллекцияПечатныхФорм[0];
				ТекКоллекция.ТабличныйДокумент = Результат.ТабличныйДокумент;
				ТекКоллекция.Экземпляров = 1;
				ТекКоллекция.СинонимМакета = ПараметрыОтчета.ПредставлениеВидаОтчета;
				//ТекКоллекция.ИмяФайлаПечатнойФормы = "Продажи за+++";
				
				ПараметрыПечати = Новый Структура;
				ПараметрыПечати.Вставить("ВладелецФормы", ЭтаФорма);
				ПараметрыПечати.Вставить("ЗаголовокФормы", ПараметрыОтчета.ПредставлениеВидаОтчета);
				
				УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, Неопределено, ПараметрыПечати);
				
			КонецЕсли;			
			
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), , СообщениеЗавершения, БиблиотекаКартинок.Успешно32);			
		
	Иначе
		
		ПоказатьПредупреждение(, Операция.КраткоеПредставлениеОшибки);
		
	КонецЕсли;
	
	
КонецПроцедуры



&НаСервере
Процедура ЗаполнитьПолезное()
	МассивПолезного = Новый Массив;
	//МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","API","https://attica.customintegrator.net"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","API","https://api-vendiboard.digma.ua"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","Кабинет","https://cab.vendiboard.com/manager/"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","Камеры","https://cab.vendiboard.com/c0004sec/v04vb0001"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","Архив картинок","https://cab.vendiboard.com/assets/images/c0004/pics.zip"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение","Логи","https://cab.vendiboard.com/c0004_avtomaty/avtomaty-v04/"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Табло","https://attica.vendiboard.s-host.net"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Табло","https://attica.vendiboard.com/"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Остатки","https://cab.vendiboard.com/c0004_avtomaty/avtomaty-v04/v04vb0001",""));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Табло","https://attica-test.vendiboard.s-host.net","Тестовый"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Табло","https://attica-test.vendiboard.com/index.html","Тестовый"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","Остатки","https://cab.vendiboard.com/c0004_avtomaty/avtomaty-v04/v04vb0001-test","Тестовый"));
	МассивПолезного.Добавить(Новый Структура("Заголовок,Значение,Примечание","ХЗ","https://cab.vendiboard.com/api/connect_c0004/v04test/stock","Тестовый"));
	
	Для Каждого Строка Из МассивПолезного Цикл
		нСтрока = Полезное.Добавить();
		ЗаполнитьЗначенияСвойств(нСтрока,Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолезноеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ПолезноеЗначение" Тогда
		ЗапуститьПриложение(Элемент.ТекущиеДанные.Значение);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПереместитьВитринуНаСервере()
	ВидАвтомата = ОтборСклад.ВидАвтомата;
	ВендингАвтоматы = Обработки.ВендингАвтоматы;
	МассивДанных = Новый Массив;
	тзВитрина = Новый ТаблицаЗначений;
	тзВитрина.Колонки.Добавить("Период");
	тзВитрина.Колонки.Добавить("Склад");
	тзВитрина.Колонки.Добавить("Номенклатура");
	тзВитрина.Колонки.Добавить("Характеристика");
	тзВитрина.Колонки.Добавить("Витрина");
	
	Для Каждого строкаВитрина Из ТоварВитрины Цикл
		Если ЗначениеЗаполнено(строкаВитрина.number) И ЗначениеЗаполнено(строкаВитрина.automatId) И ЗначениеЗаполнено(строкаВитрина.article)
			И ЗначениеЗаполнено(строкаВитрина.price) И ЗначениеЗаполнено(строкаВитрина.order) И ЗначениеЗаполнено(строкаВитрина.lotGuid) Тогда
			СтруктураСтроки = Новый Структура("number,automatId,article,price,order,lotGuid,remainingStock,maxRetailPrice");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки,строкаВитрина);
			СтруктураСтроки.remainingStock = строкаВитрина.remainingStock + строкаВитрина.Колво;
			СтруктураСтроки.maxRetailPrice = строкаВитрина.maxRetailPrice * 100;
			МассивДанных.Добавить(СтруктураСтроки);
			стрВитрина = тзВитрина.Добавить();
			стрВитрина.Период = ТекущаяДата();
			стрВитрина.Склад = ОтборСклад;
			стрВитрина.Номенклатура = строкаВитрина.Номенклатура;
			стрВитрина.Характеристика = Справочники.ХарактеристикиНоменклатуры.ВернутьХарактеристикуПоЗначениюМРЦ(строкаВитрина.Номенклатура,строкаВитрина.maxRetailPrice, ,Истина);;
			стрВитрина.Витрина = Ложь;
		Иначе
			нСтр = ТоварВитрины.Индекс(строкаВитрина) + 1;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Не все поля заполнены'; uk = 'Рядок %1. Не все поля заповнені'"),
			нСтр));
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	
	Ответ = ВендингАвтоматы.ИзменитьЭлемент_PATCH("automat/"+Формат(ОтборСклад.ИД_Автомата,"ЧГ=0")+"/cell/",МассивДанных);
	Если Ответ.КодСостояния > 399 Тогда
		ВендингАвтоматы.Ошибка404(Ответ,"PATCH");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ошибка выгрузки данных'; uk = 'Помилка вивантаження даних'"));
	Иначе
		//НаборЗаписей = РегистрыСведений.ВитринаВендингАвтоматов.СоздатьНаборЗаписей();
		//НаборЗаписей.Загрузить(тзВитрина);
		//Попытка
		//	НаборЗаписей.Записать();
		//Исключение
		//	ОписаниеОшибки = ОписаниеОшибки();
		//	Сообщить(ОписаниеОшибки);
		//КонецПопытки;
		
		Для Каждого стрВитрина Из тзВитрина Цикл
			МенеджерЗаписи = РегистрыСведений.ВитринаВендингАвтоматов.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДата();
			МенеджерЗаписи.Склад = стрВитрина.Склад;
			МенеджерЗаписи.Номенклатура = стрВитрина.Номенклатура;
			МенеджерЗаписи.Характеристика = стрВитрина.Характеристика;
			МенеджерЗаписи.Витрина = стрВитрина.Витрина;
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'ГОТОВО'; uk = 'ГОТОВО'"));
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВитрину(Команда)
	ПереместитьВитринуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТоварВитриныЯчейкаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТоварВитрины.ТекущиеДанные;
	ТекущиеДанные.Колво = 1;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВитринуНаСервере()
	ВидАвтомата = ОтборСклад.ВидАвтомата;
	ВендингАвтоматы = Обработки.ВендингАвтоматы;
	
	ТаблицаОстатков = ВендингАвтоматы.ПолучитьТаблицаЗначений();
	Ответ = ВендингАвтоматы.ПолучитьДанные_GET("automat/"+Формат(ОтборСклад.ИД_Автомата,"ЧГ=0")+"/cell/");
	
	Если Ответ.КодСостояния > 399 Тогда
		ВендингАвтоматы.Ошибка404(Ответ,"GET");
		Возврат;
	КонецЕсли;
	
	ТаблицаОстатков.Очистить();                                                		
	
	Для Каждого строка Из Ответ.Данные Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОстатков.Добавить(),строка);	
	КонецЦикла;
	
	Для Каждого строкаВитрина Из ТоварВитрины Цикл
		стрОстаток = ТаблицаОстатков.Найти(строкаВитрина.number,"number");
		Если стрОстаток = Неопределено Тогда
			Продолжить
		Иначе
			строкаВитрина.Номенклатура	= Справочники.Номенклатура.НайтиПоРеквизиту("Артикул",Формат(стрОстаток.article,"ЧГ=0"));
			строкаВитрина.maxRetailPrice= стрОстаток.maxRetailPrice / 100;
			строкаВитрина.remainingStock= стрОстаток.remainingStock;
			строкаВитрина.ЯчейкаБ		= ВендингАвтоматы.ИмяЯчейки(ВидАвтомата.Рядов,ВидАвтомата.Ячеек,строкаВитрина.number);
			строкаВитрина.order			= стрОстаток.order;
			строкаВитрина.price			= стрОстаток.price;
			строкаВитрина.article		= стрОстаток.article;
			строкаВитрина.automatId		= ОтборСклад.ИД_Автомата;
			строкаВитрина.lotGuid		= "00000000-0000-0000-0000-000000000000";
		КонецЕсли;
	КонецЦикла;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВитрину(Команда)
	Если ЗначениеЗаполнено(ОтборСклад) Тогда
		ЗаполнитьВитринуНаСервере();
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран склад отбора'; uk = 'Не вибрано склад відбору'"));
	КонецЕсли;
КонецПроцедуры













#КонецОбласти

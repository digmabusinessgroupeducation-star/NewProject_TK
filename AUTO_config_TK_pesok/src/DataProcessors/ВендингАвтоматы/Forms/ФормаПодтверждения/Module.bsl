
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипДокумент", ТипДокумента);
	Параметры.Свойство("Подразделение", Подразделение);
	Параметры.Свойство("ПериодЗагрузки", ПериодЗагрузки);	
	Параметры.Свойство("РежимФормы", РежимФормы);
	//Параметры.Свойство("ДополнительныеПараметры", ДополнительныеПараметры);
	
	Параметры.Свойство("ОтборПоСкладам",ОтборПоСкладам); 

	//Если ОтборПоСкладам Тогда 		
		СписокСкладов.ЗагрузитьЗначения(Параметры.МассивСкладов);
	//КонецЕсли;
		
	ПрефиксЗаголовка = "";
	ПредставлениеДокумента = "";
	
	ТекстКнопки = "";
	
	СтатусыДокументов = Элементы.СтатусДокументов.СписокВыбора;
	СтатусыДокументов.Очистить();
	
	Если РежимФормы = "ЗагрузкаДокументов" Тогда 
		
		ПрефиксЗаголовка = НСтр("ru = 'Загрузка документа'; uk = 'Завантаження документу'");
		ТекстКнопки = НСтр("ru = 'Загрузить документ'; uk = 'Завантажити документ'");
		
		СтатусыДокументов.Добавить(0, НСтр("ru = 'Не загружен'; uk = 'Не завантажений'"));
		СтатусыДокументов.Добавить(1, НСтр("ru = 'Загружен'; uk = 'Завантажений'"));
		СтатусыДокументов.Добавить(2, НСтр("ru = 'Все'; uk = 'Всі'"));
		
	ИначеЕсли РежимФормы = "ВыгрузкаДокументов" Тогда 
		
		ПрефиксЗаголовка = НСтр("ru = 'Выгрузка документа'; uk = 'Вивантаження документу'");
		ТекстКнопки = НСтр("ru = 'Выгрузить документ'; uk = 'Вивантажити документ'");
		
		СтатусыДокументов.Добавить(0, НСтр("ru = 'Не выгружен'; uk = 'Не вивантажений'"));
		СтатусыДокументов.Добавить(1, НСтр("ru = 'Выгружен'; uk = 'Вивантажений'"));
		СтатусыДокументов.Добавить(2, НСтр("ru = 'Все'; uk = 'Всі'"));		
		
	КонецЕсли;
	
	
	Если ТипДокумента = "ЗакрытиеСмены" Тогда 				
		
		ПредставлениеДокумента = НСтр("ru = '""Закрытие смены""'; uk = '""Закриття зміни""'");				
		
	ИначеЕсли ТипДокумента = "Инвентаризация" Тогда		
		
		ПредставлениеДокумента = НСтр("ru = '""Инвентаризация""'; uk = '""Інвентаризація""'");
		
	ИначеЕсли ТипДокумента = "ПоступлениеТоваров" Тогда 		
		
		ПредставлениеДокумента = НСтр("ru = '""Поступление товаров""'; uk = '""Надходження товарів""'");				
		
	ИначеЕсли ТипДокумента = "ПеремещениеТоваровПриход" Тогда 
		
		ПредставлениеДокумента = НСтр("ru = '""Перемещение товаров приход""'; uk = '""Переміщення товарів надходження""'");		
		
	ИначеЕсли ТипДокумента = "ПеремещениеТоваровРасход" Тогда 
		
		ПредставлениеДокумента = НСтр("ru = '""Перемещение товаров расход""'; uk = '""Переміщення товарів витрата""'");		
		
	Иначе
		
		ПредставлениеДокумента = "";	
		
	КонецЕсли;
		
		
	Заголовок = ПрефиксЗаголовка + " " + ПредставлениеДокумента;
	Элементы.ЗагрузитьДокументы.Заголовок = ТекстКнопки;
	
	УстановитьВидимостьТаблицыДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗапуститьОбновлениеСпискаДокументов();
	//УстановитьВидимостьТаблицыДокументовНаКлиенте();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СтатусДокументовПриИзменении(Элемент)
	УстановитьВидимостьТаблицыДокументов();
	//УстановитьВидимостьТаблицыДокументовНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
	ЗакрСмены = ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ЗакрытиеСмены");
	Пометка = ТекущиеДанные.Пометка;
	Обработан = ТекущиеДанные.Обработан;
	Проведен = ТекущиеДанные.Проведен;
	ТекущиеДанные.Пометка = ?(ЗакрСмены,Пометка,Пометка И Проведен И НЕ Обработан);
	
	ПересчитатьОтмеченные();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекСтрока = Элемент.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = ТекСтрока.Документ;
	ТипДок = ТипЗнч(ДокументСсылка);
	
	Если ТипДок = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
		ОткрытьФорму("Документ.ПоступлениеТоваров.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));
	ИначеЕсли ТипДок = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		ОткрытьФорму("Документ.РеализацияТоваров.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		ОткрытьФорму("Документ.ПеремещениеТоваров.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));
	ИначеЕсли ТипДок = Тип("ДокументСсылка.Инвентаризация") Тогда 
		ОткрытьФорму("Документ.Инвентаризация.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ВозвратПоставщику") Тогда 
		ОткрытьФорму("Документ.ВозвратПоставщику.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));
	ИначеЕсли ТипДок = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда 
		ОткрытьФорму("Документ.ЗакрытиеСмены.ФормаОбъекта", Новый Структура("Ключ", ДокументСсылка));		
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТаблицаДокументовУстановитьФлажки(Команда)
	
	Для Каждого ДокЗагрузки Из Объект.ТаблицаДокументов Цикл 
		
		Если СтатусДокументов = 0 И НЕ ДокЗагрузки.Обработан Тогда
			 ДокЗагрузки.Пометка = Истина;
		ИначеЕсли СтатусДокументов = 1 И ДокЗагрузки.Обработан Тогда
			 ДокЗагрузки.Пометка = Истина;
		ИначеЕсли СтатусДокументов = 2 Тогда
			 ДокЗагрузки.Пометка = Истина;
		КонецЕсли;
			
	КонецЦикла;
	
	ПересчитатьОтмеченные();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовСнятьФлажки(Команда)
	
	Для Каждого ДокЗагрузки Из Объект.ТаблицаДокументов Цикл 
		
		Если СтатусДокументов = 0 И НЕ ДокЗагрузки.Обработан Тогда
			 ДокЗагрузки.Пометка = Ложь;
		ИначеЕсли СтатусДокументов = 1 И ДокЗагрузки.Обработан Тогда
			 ДокЗагрузки.Пометка = Ложь;
		ИначеЕсли СтатусДокументов = 2 Тогда
			 ДокЗагрузки.Пометка = Ложь;
		КонецЕсли;

	КонецЦикла;

	ПересчитатьОтмеченные();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДокументы(Команда)
	
	ЗапуститьОперациюНадДокументами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДокументы(Команда)
	
	ЗапуститьОбновлениеСпискаДокументов();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


#Область ДлительныеОперации

&НаСервере
Функция НачатьВыполнениеОперации(Операция, ПараметрыОперации = Неопределено)
	
	АдресТаблицаДокументов =  Объект.ТаблицаДокументов.Выгрузить();	
	АдресТаблицаСнятыхЗет  =  Объект.ТаблицаСнятыхЗетОтчетов.Выгрузить();	
	//АдресТаблицаТКС        =  Объект.ТаблицаТКС.Выгрузить();	
	АдресТаблицаТКС        =  Объект.ТаблицаДокументов.Выгрузить();	
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Если Операция = "ОбновитьСписокЗагрузки" Тогда 
				
		АдресТаблицаДокументов.Очистить();	
		АдресТаблицаСнятыхЗет.Очистить();
		АдресТаблицаТКС.Очистить();
		
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВендингАвтоматы.ЗагрузитьТаблицуДокументов", 
			ТипДокумента, ПараметрыОперации, АдресТаблицаДокументов, АдресТаблицаСнятыхЗет, АдресТаблицаТКС);
			
	ИначеЕсли Операция = "ОбновитьСписокВыгрузки" Тогда 
		
		АдресТаблицаДокументов.Очистить();	
		АдресТаблицаСнятыхЗет.Очистить();
		АдресТаблицаТКС.Очистить();			
		
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВендингАвтоматы.ЗаполнитьТаблицуДокументовДляВыгрузи", 
			ТипДокумента, ПараметрыОперации, АдресТаблицаДокументов);
		
	ИначеЕсли Операция = "СформироватьДокументы" Тогда 
				
		//Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.ВендингАвтоматы.ВыполнитьЗагрузкуДокументов", ТипДокумента, СтатусДокументов, Неопределено
		//	, АдресТаблицаДокументов, АдресТаблицаСнятыхЗет , АдресТаблицаТКС);
		
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВендингАвтоматы.ВыполнитьЗагрузкуДокументов", ТипДокумента, СтатусДокументов, ПараметрыОперации
			, АдресТаблицаДокументов, АдресТаблицаСнятыхЗет , АдресТаблицаТКС);
		
	ИначеЕсли Операция = "ВыгрузитьДокументы" Тогда
				
		//Возврат ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.ВендингАвтоматы.ВыполнитьВыгрузкуДокументов", ТипДокумента, ПараметрыОперации, АдресТаблицаДокументов);
		
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВендингАвтоматы.ВыполнитьВыгрузкуДокументов", ТипДокумента, ПараметрыОперации, АдресТаблицаДокументов);
		
	КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеСпискаДокументов()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Подразделение", 	Подразделение);
	ПараметрыЗагрузки.Вставить("НачалоПериода", 	ПериодЗагрузки.ДатаНачала);
	ПараметрыЗагрузки.Вставить("КонецПериода", 		ПериодЗагрузки.ДатаОкончания);
	ПараметрыЗагрузки.Вставить("ОтборПоСкладам", 	ОтборПоСкладам);
	ПараметрыЗагрузки.Вставить("МассивСкладов", 	СписокСкладов.ВыгрузитьЗначения());
	
	//Если ОтборПоСкладам И СписокСкладов.Количество() > 0 Тогда 
	//	ПараметрыЗагрузки.Вставить("МассивСкладов", СписокСкладов.ВыгрузитьЗначения());
	//КонецЕсли;
	
	//Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда 
	//	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыЗагрузки, ДополнительныеПараметры, Истина);
	//КонецЕсли;
	
	ПараметрыОжидания = ПолучитьПараметрыОжидания();		
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Получаем список документов'; uk = 'Отримуємо список документів'");		
	
	Если РежимФормы = "ЗагрузкаДокументов" Тогда 			
		ВидОперации = "ОбновитьСписокЗагрузки";
	ИначеЕсли РежимФормы = "ВыгрузкаДокументов" Тогда 		
		ВидОперации = "ОбновитьСписокВыгрузки";				
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный режим формы'; uk = 'Невідомий режим форми'") + РежимФормы;
	КонецЕсли;		
	
	ДлительнаяОперация = НачатьВыполнениеОперации(ВидОперации, ПараметрыЗагрузки);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, Новый Структура("ВидОперации", ВидОперации));	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОперациюНадДокументами()
	
	ПараметрыОперации = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда 				
		ПараметрыОперации = ДополнительныеПараметры;
	КонецЕсли;		
	
	ОчиститьСообщения();
	
	Если Объект.ТаблицаДокументов.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет отмеченных документов'; uk = 'Немає відмічених документів'"));
		Возврат;
	КонецЕсли;

	ПараметрыОжидания = ПолучитьПараметрыОжидания();	
	
	Если РежимФормы = "ЗагрузкаДокументов" Тогда 		
						
		ВидОперации = "СформироватьДокументы";
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загрузка документов'; uk = 'Завантаження документів'"); 		
		
	ИначеЕсли РежимФормы = "ВыгрузкаДокументов" Тогда 
		
		ВидОперации = "ВыгрузитьДокументы";
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выгрузка документов'; uk = 'Вивантаження документів'");
		
	КонецЕсли;
	
	ДлительнаяОперация = НачатьВыполнениеОперации(ВидОперации, ПараметрыОперации);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, Новый Структура("ВидОперации", ВидОперации));		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, ДопПараметры) Экспорт
	
	ВидОперации = "";
	ДопПараметры.Свойство("ВидОперации", ВидОперации);
	
	ОписаниеРезультата = ОбработатьРезультатОперации(Операция);
	
	ЕстьОшибки = ОписаниеРезультата.ЕстьОшибки;
	Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Ошибки", НСтр("ru = 'Обнаружена ошибка при выполнении операции'; uk = 'Виявлена помилка при виконанні операції'"));
	ОповещениеТекст = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеТекст", НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'"));
	ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеПояснение", НСтр(""));
	Картинка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Картинка", БиблиотекаКартинок.Информация32);	
	СообщенияПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "СообщенияПользователю", Неопределено);
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	
	Если Не ЕстьОшибки И (ВидОперации = "СформироватьДокументы" ИЛИ ВидОперации = "ВыгрузитьДокументы") Тогда 
		
		РезультатЗакрытия = Неопределено;
		Если ЗначениеЗаполнено(СообщенияПользователю) Тогда 
			РезультатЗакрытия = Новый Структура("СообщенияПользователю", СообщенияПользователю);
		КонецЕсли;
		Закрыть(РезультатЗакрытия);
		
	ИначеЕсли ЕстьОшибки И (ВидОперации = "СформироватьДокументы" ИЛИ ВидОперации = "ВыгрузитьДокументы") Тогда 		
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибки);
		ЗапуститьОбновлениеСпискаДокументов();				
		
	Иначе
		
		Если ЕстьОшибки Тогда 
			ПоказатьПредупреждение(, Ошибки,, НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'"));	
		КонецЕсли;
		
		ПересчитатьОтмеченные();		
	КонецЕсли;		
		
	   
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатОперации(Операция)
	
	ОповещениеТекст 	= "";
	ОповещениеПояснение = "";
	ЕстьОшибки 			= Ложь;
	Ошибки 				= "";
	Результат 			= Неопределено;
	Картинка			= Неопределено;
	СообщенияПользователю = Неопределено;
	
	Если Операция = Неопределено Тогда 
		
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		Картинка = БиблиотекаКартинок.Ошибка32;
		
	Иначе
		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда 		
			
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				
				Если Результат.Свойство("ТаблицаДокументов") Тогда 					
					Объект.ТаблицаДокументов.Загрузить(Результат.ТаблицаДокументов);							
				КонецЕсли;
				
				Если Результат.Свойство("ТаблицаСнятыхЗетОтчетов") Тогда 					
					Объект.ТаблицаСнятыхЗетОтчетов.Загрузить(Результат.ТаблицаСнятыхЗетОтчетов);					
				КонецЕсли;		
				
				Если Результат.Свойство("ТаблицаТКС") Тогда 					
					Объект.ТаблицаТКС.Загрузить(Результат.ТаблицаТКС);				
				КонецЕсли;
				
				Если Результат.Свойство("СообщениеЗавершения") Тогда 
					ОповещениеПояснение = Результат.СообщениеЗавершения;
				КонецЕсли;
				
				ЕстьОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ЕстьОшибки", Ложь);
				Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Ошибки", ""); 
			КонецЕсли;						
			
			Если Не ЕстьОшибки Тогда 			
				ОповещениеТекст = НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'");
				Картинка = БиблиотекаКартинок.Успешно32;
			Иначе
				ОповещениеТекст = НСтр("ru = 'Завершено с ошибками'; uk = 'Завершено з помилками'");
				Картинка = БиблиотекаКартинок.Предупреждение32;
			КонецЕсли;
			
			Если ПустаяСтрока(ОповещениеПояснение) Тогда 
				ОповещениеПояснение = НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'");
			КонецЕсли;
			
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 	
			
			ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");
			Картинка = БиблиотекаКартинок.Ошибка32;
			ЕстьОшибки = Истина;
			
			Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщениеЗавершения") Тогда 
				ОповещениеПояснение = Результат.СообщениеЗавершения;
			КонецЕсли;
			
			Если ПустаяСтрока(ОповещениеПояснение) Тогда 
				ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");
			КонецЕсли;
			
			Ошибки = Операция.КраткоеПредставлениеОшибки;
			
		Иначе			
			
			ЕстьОшибки = Истина;
		
		КонецЕсли;
		
		Если Операция.Свойство("Сообщения") И ТипЗнч(Операция.Сообщения) = Тип("ФиксированныйМассив") Тогда 
			Для Каждого Сообщение Из Операция.Сообщения Цикл 
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщенияПользователю") Тогда 
			СообщенияПользователю = Результат.СообщенияПользователю;
		КонецЕсли;		
	КонецЕсли;
	
	ОписаниеРезультата = Новый Структура;
	ОписаниеРезультата.Вставить("ЕстьОшибки", ЕстьОшибки);
	
	Если Не ПустаяСтрока(Ошибки) Тогда 
		ОписаниеРезультата.Вставить("Ошибки", Ошибки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Картинка) Тогда 
		ОписаниеРезультата.Вставить("Картинка", Картинка);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеТекст) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеТекст", ОповещениеТекст);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеПояснение) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеПояснение", ОповещениеПояснение);
	КонецЕсли;
	
	Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
		ОписаниеРезультата.Вставить("СообщенияПользователю", СообщенияПользователю);
	КонецЕсли;
	
	Возврат ОписаниеРезультата;
	
КонецФункции


#КонецОбласти



#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьПараметрыОжидания()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ВендингАвтоматы";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	
	
	Возврат ПараметрыОжидания;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьТаблицыДокументовНаКлиенте()
	
	СтруктураОтбора = Новый Структура;
	Если СтатусДокументов <> 2 Тогда
		СтруктураОтбора.Вставить("Обработан", СтатусДокументов = 1);
	КонецЕсли;
	
	Элементы.ТаблицаДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьТаблицыДокументов()
	
	Для Каждого УС Из УсловноеОформление.Элементы  Цикл 
		УС.Использование = Ложь;
	КонецЦикла;
	
	Если СтатусДокументов = 0 ИЛИ СтатусДокументов = 1 Тогда
		УО = УсловноеОформление.Элементы.Получить(СтатусДокументов);
		УО.Использование = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьОтмеченные()
	
	КоличествоОтмеченых = Объект.ТаблицаДокументов.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество();
	
	ТекстКнопки = "Кнопка";
	
	Если РежимФормы = "ЗагрузкаДокументов" Тогда 
		
		ТекстКнопки = НСтр("ru = 'Загрузить документы'; uk = 'Завантажити документи'");
		
	ИначеЕсли РежимФормы = "ВыгрузкаДокументов" Тогда 
		
		ТекстКнопки = НСтр("ru = 'Выгрузить документы'; uk = 'Вивантажити документи'");
		
	КонецЕсли;
	
	Если КоличествоОтмеченых > 0 Тогда 
		ТекстКнопки = ТекстКнопки + " (" + КоличествоОтмеченых + ")";
	КонецЕсли;
	
	Элементы.ЗагрузитьДокументы.Заголовок = ТекстКнопки;
	
КонецПроцедуры


#КонецОбласти
 
 
 

// Типы выгрузки, значения:
//0 - выгрузка по узлу обмена (только изменения)
//1 - полная выгрузка с отборами только по периоду и подразделению

// Доступные действия
// - Номенклатура
// - ТорговыеПлощадки
// - Продажи
// - Чеки


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УзелОбмена = ОбменPowerBi_ПовтИсп.УзелОбмена();
	
	МассивПодразделений = ОбменPowerBi_ПовтИсп.ПолучитьПодразделенияУзлаОбмена(УзелОбмена);			
	Элементы.Подразделение.СписокВыбора.ЗагрузитьЗначения(МассивПодразделений);
	
	Если МассивПодразделений.Количество() > 0 Тогда 
		Подразделение = МассивПодразделений[0];
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипВыгрузки = 0 Тогда 
		ПроверяемыеРеквизиты.Добавить("УзелОбмена");
	КонецЕсли;
	
	Если Действие = "ТорговыеПлощадки" Тогда
		
		Если ТипВыгрузки = 1 Тогда 
			ПроверяемыеРеквизиты.Добавить("Подразделение");
		КонецЕсли;		
		
	ИначеЕсли Действие = "Продажи" ИЛИ Действие = "Чеки" Тогда 		
		
		Если ТипВыгрузки = 1 Тогда 
			ПроверяемыеРеквизиты.Добавить("Период");
			ПроверяемыеРеквизиты.Добавить("Подразделение");
		КонецЕсли;		
		
	КонецЕсли;
		
	Действие = "";
		
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипВыгрузкиПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыгрузитьНоменклатуруНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВыгрузки = Новый Структура;
	
	Если ТипВыгрузки = 0 Тогда 
		ПараметрыВыгрузки.Вставить("УзелОбмена", УзелОбмена);
		
		Если НеУдалятьРегистрацию Тогда 
			ПараметрыВыгрузки.Вставить("НеУдалятьРегистрацию", НеУдалятьРегистрацию);
		КонецЕсли;
	КонецЕсли;
	
	ОбработкаОбъект.ВыполнитьВыгрузкуНоменклатуры(ПараметрыВыгрузки);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНоменклатуру(Команда)
	
	Действие = "Номенклатура";
	
	Если ПроверитьЗаполнение() Тогда 
		ВыгрузитьНоменклатуруНаСервере();	
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ВыгрузитьТорговыеПлощадкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВыгрузки = Новый Структура;
	
	Если ТипВыгрузки = 0 Тогда 
		ПараметрыВыгрузки.Вставить("УзелОбмена", УзелОбмена);
		
		Если НеУдалятьРегистрацию Тогда 
			ПараметрыВыгрузки.Вставить("НеУдалятьРегистрацию", НеУдалятьРегистрацию);
		КонецЕсли;	
	Иначе
		ПараметрыВыгрузки.Вставить("Подразделение", Подразделение);
	КонецЕсли;
	
	ОбработкаОбъект.ВыполнитьВыгрузкуОбъекты(ПараметрыВыгрузки);		
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьТорговыеПлощадки(Команда)
	
	ОчиститьСообщения();
	
	Действие = "ТорговыеПлощадки";
	
	Если ПроверитьЗаполнение() Тогда 
		ВыгрузитьТорговыеПлощадкиНаСервере();
	КонецЕсли;
		
КонецПроцедуры


&НаСервере
Процедура ВыгрузитьПродажиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВыгрузки = Новый Структура;
	
	Если ТипВыгрузки = 0 Тогда 
		ПараметрыВыгрузки.Вставить("УзелОбмена", УзелОбмена);
		Если НеУдалятьРегистрацию Тогда 
			ПараметрыВыгрузки.Вставить("НеУдалятьРегистрацию", НеУдалятьРегистрацию);
		КонецЕсли;		
	ИначеЕсли ТипВыгрузки = 1 Тогда 
		ПараметрыВыгрузки.Вставить("НачалоПериода", Период.ДатаНачала);
		ПараметрыВыгрузки.Вставить("КонецПериода", Период.ДатаОкончания);
		ПараметрыВыгрузки.Вставить("Подразделение", Подразделение);
	КонецЕсли;
	
	ОбработкаОбъект.ВыполнитьВыгрузкуПродажи(ПараметрыВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПродажи(Команда)
	
	ОчиститьСообщения();
	
	Действие = "Продажи";
	
	Если ПроверитьЗаполнение() Тогда 
		ВыгрузитьПродажиНаСервере();
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ВыгрузитьЧекиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВыгрузки = Новый Структура;
	
	Если ТипВыгрузки = 0 Тогда 
		ПараметрыВыгрузки.Вставить("УзелОбмена", УзелОбмена);
		Если НеУдалятьРегистрацию Тогда 
			ПараметрыВыгрузки.Вставить("НеУдалятьРегистрацию", НеУдалятьРегистрацию);
		КонецЕсли;		
	ИначеЕсли ТипВыгрузки = 1 Тогда 
		ПараметрыВыгрузки.Вставить("НачалоПериода", Период.ДатаНачала);
		ПараметрыВыгрузки.Вставить("КонецПериода", Период.ДатаОкончания);
		ПараметрыВыгрузки.Вставить("Подразделение", Подразделение);
	КонецЕсли;
	
	ОбработкаОбъект.ВыполнитьВыгрузкуЧеков(ПараметрыВыгрузки);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЧеки(Команда)
	
	ОчиститьСообщения();
	
	Действие = "Чеки";
	
	Если ПроверитьЗаполнение() Тогда 
		ВыгрузитьЧекиНаСервере();
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Период");
	МассивВсехРеквизитов.Добавить("Подразделение");
	
	МассивВидимыхРеквизитов = Новый Массив;
	
	Если ТипВыгрузки = 1 Тогда 
		МассивВидимыхРеквизитов.Добавить("Период");
		МассивВидимыхРеквизитов.Добавить("Подразделение");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивВидимыхРеквизитов);
	
КонецПроцедуры



#КонецОбласти
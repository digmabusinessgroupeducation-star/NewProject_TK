&НаКлиенте
Перем мУИДТекущейГруппы, мКлючиСвязейГрупп, Hex;
&НаКлиенте
Перем мКэшПоложенияУказателей;

&НаСервереБезКонтекста
Функция ПолучитьМассивHEX()
	
	// Строим словарь.
	МассивШестнадцатиричныхЗначений = Новый Массив(256);
	Словарь = "0123456789abcdef";
	Для Счт1 = 0 По 15 Цикл
		Для Счт2 = 0 По 15 Цикл
			МассивШестнадцатиричныхЗначений[Счт1 * 16 + Счт2] = Сред(Словарь, Счт1 + 1, 1) + Сред(Словарь, Счт2 + 1, 1)
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивШестнадцатиричныхЗначений;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ РольДоступна("ПолныеПрава") И НЕ РольДоступна("ТК_СПП") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КаринкаВыбраныЮзеры = БиблиотекаКартинок.Пользователь;
	КаринкаВсеЮзеры = БиблиотекаКартинок.ВариантыОтчетовДоступныеВсемПользователям;
	КартинкаПрогресс = БиблиотекаКартинок.ДлительнаяОперация48;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДоменПоУмолчанию()
	
	//определяем текущий домен
	Попытка 
		adsi = ПолучитьCOMОбъект("LDAP://RootDSE");
		Возврат adsi.Get("rootDomainNamingContext");
	Исключение
		Сообщить("Не удалось определить текущий домен!");
		Возврат "";
	КонецПопытки;
	
КонецФункции //ПолучитьДоменПоУмолчанию

&НаКлиенте
Процедура  ОпределитьКэшПоложенияУказателейпоУмолчанию()
	
	мКэшПоложенияУказателей = Новый Структура("Группа,Пользователь,Иерархия,Пометка,Принудительно",Неопределено,Неопределено,Иерархия,Неопределено,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	Hex = ПолучитьМассивHEX();
	
	мКлючиСвязейГрупп = Новый Соответствие;
	LDAPText = Объект.LDAP_HDR + ПолучитьДоменПоУмолчанию();  //?? некрасиво..
	
	Элементы.ТаблицаПользователейОтборИерархия.Пометка = Иерархия;
	Элементы.ТаблицаПользователейСПочтой.Пометка = ЕстьПочта;
	
	Если Иерархия ИЛИ ЕстьПочта Тогда
		УстановитьОтборИерархия();
	КонецЕсли;
	
	ОпределитьКэшПоложенияУказателейпоУмолчанию();
	
	//ПолучитьСписокПользователейАД();
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПолучитьСписокПользователейАД();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокПользователейАД()
	
	ОпределитьКэшПоложенияУказателейпоУмолчанию();
	
	СписокПользователей.Очистить();
	ЗаполнитьТаблицуПользователей();
	
	Если Элементы.ДеревоAD.ТекущаяСтрока = Неопределено	Тогда
		Попытка
			Элементы.ДеревоAD.ТекущаяСтрока = ДеревоAD.ПолучитьЭлементы()[0].ПолучитьЭлементы()[0].ПолучитьИдентификатор();
			Элементы.ДеревоAD.Развернуть(Элементы.ДеревоAD.ТекущаяСтрока,Ложь);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	Элементы.ТаблицаПользователейТолькоВыбранные.Видимость = ТаблицаПользователей.Количество()>0;	
	
КонецПроцедуры
	

&НаКлиенте
Процедура ЗаполнитьТаблицуПользователей()//ТаблицаПользователей, LDAPText, Рекурсивно = Ложь, СтатусВыполнения = "") Экспорт             
	
	ТВ = ТекущаяДата();
	мКлючиСвязейГрупп.Очистить();
	
	Если Иерархия И НЕ Элементы.ДеревоAD.ТекущиеДанные = Неопределено Тогда
		мУИДТекущейГруппы = Элементы.ДеревоAD.ТекущиеДанные.UID;
	Иначе 
		мУИДТекущейГруппы = Неопределено;
	КонецЕсли;
	
	ТаблицаПользователей.Очистить();
	ЭлементыДерева = ДеревоAD.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Коннектор = Новый COMОбъект("ADODB.Connection");
	Коннектор.Provider = "ADSDSOObject";
	Коннектор.CursorLocation = 2;
	Коннектор.Mode = 1;
	Коннектор.Open("ADs Provider");
	
	ОбъектГруппы = ПолучитьCOMОбъект(LDAPText);
	
	СтрокаГрупп = ЭлементыДерева.Добавить();
	СтрокаГрупп.UID = ОбъектГруппы.GUID;
	СтрокаГрупп.name = ОбъектГруппы.name;
	СтрокаГрупп.Path = ОбъектГруппы.ADsPath;
	мКлючиСвязейГрупп.Вставить(ОбъектГруппы.GUID,СтрокаГрупп.ПолучитьИдентификатор());
	Если ПолучитьДанныеГруппыРекурсивно(Коннектор,ОбъектГруппы.ADsPath,СтрокаГрупп,0, Ложь) < 0 Тогда
		ЭлементыДерева.Удалить(СтрокаГрупп);
	КонецЕсли;			
	
	Коннектор.Close();
	
	Если ТипЗнч(мУИДТекущейГруппы) = Тип("Число") Тогда
		Элементы.ДеревоAD.ТекущаяСтрока = мУИДТекущейГруппы;
	КонецЕсли;
	
	ТаблицаПользователей.Сортировать("Имя");
	
	//Состояние("Обработка данных по группам active directory...");
	ПоказатьОповещениеПользователя("Работает сервер...",,"Обработка данных по группам active directory...");
	
	ЗаполнитьГруппы(СтрокаГрупп.ПолучитьИдентификатор());
	ПоказатьОповещениеПользователя("Работает сервер...",,"Обработка данных по юзерам active directory...");
	ЗаполнитьПользователей();
	
	ВремяВыполнения = ТекущаяДата()-ТВ;
	ПоказатьОповещениеПользователя("ОК",,"Выполнено за (сек.): "+Формат(ВремяВыполнения,"ЧН=; ЧГ="),БиблиотекаКартинок.Информация);
	
	
КонецПроцедуры  //ЗаполнитьТаблицуПользователей

&НаСервере
Процедура ЗаполнитьГруппы(СтрокаДереваИдентификатор)
	
	СтрокаДереваИсходная = ДеревоAD.НайтиПоИдентификатору(СтрокаДереваИдентификатор);
	Если СтрокаДереваИсходная.Удаленные Тогда
		Возврат;
	КонецЕсли;
	
	Подстроки = СтрокаДереваИсходная.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из Подстроки Цикл
		
		СтрДанныеГруппы = Новый Структура("UID,Наименование",СтрокаДерева.UID,СтрокаДерева.name);
		
		СтрДанныеГруппы.Вставить("ГруппаПользователейРодитель",СтрокаДереваИсходная.ГруппаПользователей);
		СтрДанныеГруппы.Вставить("ПодразделениеРодитель",СтрокаДереваИсходная.Подразделение);
		
		Если УстановитьДанныеГруппыПользователей(СтрДанныеГруппы) Тогда
			СтрокаДерева.ГруппаПользователей = СтрДанныеГруппы.ГруппаПользователей;
			//СтрокаДерева.Подразделение = СтрДанныеГруппы.Подразделение;
			ЗаполнитьГруппы(СтрокаДерева.ПолучитьИдентификатор());
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользователей()
	
	Запрос = Новый Запрос();
	Запрос.Текст =  "ВЫБРАТЬ
	                |	Пользователи.Ссылка КАК Пользователь,
	                |	Пользователи.AD_UID КАК UID
	                |ИЗ
	                |	Справочник.Пользователи КАК Пользователи
	                |ГДЕ
	                |	Пользователи.AD_UID В(&мИдентификаторы)";
	
	Запрос.УстановитьПараметр("мИдентификаторы",ТаблицаПользователей.Выгрузить(,"UID").ВыгрузитьКолонку("UID"));
	
	Отбор = Новый Структура("UID",);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Отбор.UID = Выборка.UID;
		ТаблицаПользователей.НайтиСтроки(Отбор)[0].Пользователь = Выборка.Пользователь;
	КонецЦикла
	
	
КонецПроцедуры

&НаКлиенте	
Функция ПолучитьДанныеГруппыРекурсивно(Коннектор,КорневойУзел,СтрокаДерева,Знач Уровень, ФлагОтключен)
	Перем ЧислоЗаписей;
	
	Если Уровень = 1 Тогда
		Если Найти(КорневойУзел,"Ассоциация") > 0 Тогда
			Отключен = Ложь;
		ИначеЕсли Найти(КорневойУзел,"DisabledAccounts") > 0 Тогда
			Отключен = Истина;
		Иначе 
			Возврат 0; //остальные группы не нужны, пропускаем
		КонецЕсли;
	Иначе 
		Отключен = ФлагОтключен; 
	КонецЕсли;
	
	ДобавитьПользователейГруппы(Коннектор,КорневойУзел,СтрокаДерева,Отключен);
	
	Текст = "<" + СокрЛП(КорневойУзел) + ">;(&(objectCategory=organizationalUnit));ADsPath, Name, objectGUID;" "onelevel";
	Попытка
		НаборЗаписейПодразделения = Коннектор.Execute(текст);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке());
		НаборЗаписей = Неопределено;
		Возврат -1;
	КонецПопытки;
	
	Строки = СтрокаДерева.ПолучитьЭлементы();
	Сч = 0; СчП = 0;
	Пока НЕ НаборЗаписейПодразделения.EOF Цикл
		
		//Состояние(НаборЗаписейПодразделения.Fields("Name").Value);
		//ПоказатьОповещениеПользователя("Чтение",,НаборЗаписейПодразделения.Fields("Name").Value);
		
		НоваяСтрока = Строки.Добавить();
		НоваяСтрока.UID = ПолучитьGUID(НаборЗаписейПодразделения.Fields("objectGUID").Value.Выгрузить());
		НоваяСтрока.name = НаборЗаписейПодразделения.Fields("Name").Value;
		НоваяСтрока.Path = НаборЗаписейПодразделения.Fields("ADsPath").Value;
		НоваяСтрока.Удаленные = Отключен;
		ПолучитьДанныеГруппыРекурсивно(Коннектор,НаборЗаписейПодразделения.Fields("ADsPath").Value,НоваяСтрока,Уровень+1,Отключен);
		
		Если НоваяСтрока.КоличествоПользователей = 0 Тогда
			Строки.Удалить(НоваяСтрока);
		Иначе 
			СчП = СчП + НоваяСтрока.КоличествоПользователей;
			Если НЕ мУИДТекущейГруппы = Неопределено И ТипЗнч(мУИДТекущейГруппы) = Тип("Строка") И мУИДТекущейГруппы = НоваяСтрока.UID Тогда
				мУИДТекущейГруппы = НоваяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			мКлючиСвязейГрупп.Вставить(НоваяСтрока.UID,НоваяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
		
		Попытка
			НаборЗаписейПодразделения.MoveNext();
			Сч = Сч + 1;
		Исключение
			СтатусВыполнения = "Данные получены не полностью (" + Сч + " записей). Ограничьте размер выборки.";
			Сообщить(СтатусВыполнения);
		КонецПопытки;
	КонецЦикла;
	
	НаборЗаписейПодразделения.Close();
	
	СтрокаДерева.КоличествоПользователей = СтрокаДерева.КоличествоПользователей +СчП;
	
	Возврат СтрокаДерева.КоличествоПользователей;
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьДанныеГруппыПользователей(СтруктураДанные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГруппыПользователей.Ссылка КАК ГруппаПользователей,
	               |	ВЫБОР
	               |		КОГДА НЕ ГруппыПользователей.Наименование = &Наименование
	               |				ИЛИ НЕ ГруппыПользователей.Родитель = &ГруппаПользователейРодитель
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК ГруппаПользователейМодиф
	               |ИЗ
	               |	Справочник.ГруппыПользователей КАК ГруппыПользователей
	               |ГДЕ
	               |	ГруппыПользователей.AD_UID = &UID";
	
	Для Каждого КлючИЗначение Из СтруктураДанные Цикл 
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ,КлючИЗначение.Значение)
	КонецЦикла;
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда		
		
		Если Не Выборка.ГруппаПользователейМодиф Тогда
			СтруктураДанные.Вставить("ГруппаПользователей", Выборка.ГруппаПользователей);
			Возврат Истина;
		КонецЕсли;
		
		ГруппаПользователейОбъект = Выборка.ГруппаПользователей.ПолучитьОбъект();
	Иначе
		ГруппаПользователейОбъект = Справочники.ГруппыПользователей.СоздатьЭлемент();
	КонецЕсли;
	
	ГруппаПользователейОбъект.Наименование = СтруктураДанные.Наименование;
	ГруппаПользователейОбъект.AD_UID = СтруктураДанные.UID;
	ГруппаПользователейОбъект.Родитель = СтруктураДанные.ГруппаПользователейРодитель;
	
	Попытка
		ГруппаПользователейОбъект.Записать();
	Исключение
		Сообщить(ОписаниеОшибки(),СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецПопытки;		
	
	СтруктураДанные.Вставить("ГруппаПользователей",ГруппаПользователейОбъект.Ссылка);
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ПолучитьGUID(Массив)
	
	Рез = "";
	
	Для инд = 0 По 15 Цикл
		Рез = Рез+Hex[Массив[инд]];
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции

&НаКлиенте
Функция ДобавитьПользователейГруппы(Коннектор,Узел,СтрокаДерева,флОтключен)
	
	ТекстЗапроса = "<" + Узел + ">;(&(objectCategory=person)(objectClass=user)"+?(ЕстьПочта,"(mail=*)","")
	+"(!userAccountControl:1.2.840.113556.1.4.803:=2));ADsPath,mail,sAMAccountName,objectGUID,Description,userAccountControl,DisplayName,LastLogontimeStamp,cn,sn ; onelevel";  //source,actconn,cursortyp,locktyp,opt

	//ТекстЗапроса = "<" + Узел + ">;(&(objectCategory=person)(objectClass=user)"+?(ЕстьПочта,"(mail=*)","")
	//+"(!userAccountControl:1.2.840.113556.1.4.803:=2));* ; onelevel";  //source,actconn,cursortyp,locktyp,opt

	Попытка
		НаборЗаписейЮзеры = Коннектор.Execute(ТекстЗапроса);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке());
		//ркОбщийМодульКлиентСервер.СообщитьИнформациюОбОшибке(ИнформацияОбОшибке());
		НаборЗаписейЮзеры = Неопределено;
		Возврат 0;
	КонецПопытки;
	Сч = 0;
	Пока НЕ НаборЗаписейЮзеры.EOF Цикл
		//ОбъектЮзер = ПолучитьCOMОбъект(НаборЗаписейЮзеры.Fields("ADsPath").Value);
		НоваяСтрока = ТаблицаПользователей.Добавить();
		
		НоваяСтрока.Email = нрег(НаборЗаписейЮзеры.Fields("mail").Value);
		НоваяСтрока.UID = ПолучитьGUID(НаборЗаписейЮзеры.Fields("objectGUID").Value.Выгрузить());
		НоваяСтрока.КлючСвязи = СтрокаДерева.UID;
		НоваяСтрока.Аккаунт = НаборЗаписейЮзеры.Fields("sAMAccountName").Value;
		НоваяСтрока.ЕстьПочта = Не ПустаяСтрока(НоваяСтрока.Email);
		НоваяСтрока.Отключен = флОтключен;
		абс = НаборЗаписейЮзеры.Fields("LastLogontimeStamp").Value;
		Если НЕ НаборЗаписейЮзеры.Fields("Description").Value = Null Тогда
			массивСтроковый = НаборЗаписейЮзеры.Fields("Description").Value;
			счСтр = 0;
			Для каждого Строка Из массивСтроковый Цикл
				разделитель = ?(счСтр>0,Символы.ПС,"");
				НоваяСтрока.Описание = НоваяСтрока.Описание + разделитель + Строка;
				счСтр = счСтр + 1;
			КонецЦикла;
		КонецЕсли;			
		
		НоваяСтрока.Имя = НаборЗаписейЮзеры.Fields("DisplayName").Value;
		
		НоваяСтрока.КороткоеИмя = ПолучитьКороткоеИмя(НаборЗаписейЮзеры.Fields("DisplayName").Value);
		
		Попытка
			НаборЗаписейЮзеры.MoveNext();
			Сч = Сч + 1;
		Исключение
			СтатусВыполнения = "Данные получены не полностью (" + Сч + " записей). Ограничьте размер выборки.";
			Сообщить(СтатусВыполнения);
			Прервать;
		КонецПопытки;
		
	КонецЦикла;
	
	СтрокаДерева.КоличествоПользователей = СтрокаДерева.КоличествоПользователей+Сч;
	
	НаборЗаписейЮзеры.Close();
	
	Возврат Сч;
	
КонецФункции

&НаКлиенте
Функция ПолучитьКороткоеИмя(ПолноеИмя)
	
	Массив = Новый Массив;
	ИсходнаяСтрока = СокрЛП(ПолноеИмя);
	Массив.Добавить(ИсходнаяСтрока);
	
	ЕстьФамилия = Ложь;
	
	ИндексРазделителя = Найти(ИсходнаяСтрока," ");
	Пока ИндексРазделителя > 0 ИЛИ НЕ ПустаяСтрока(ИсходнаяСтрока) Цикл
		Если ИндексРазделителя > 0 Тогда
			Подстрока = СокрЛ(Лев(ИсходнаяСтрока,ИндексРазделителя-1));
		Иначе 
			Подстрока = ИсходнаяСтрока;
		КонецЕсли;
		
		Если НЕ ЕстьФамилия Тогда
			Массив[0] = Подстрока+" ";
			ЕстьФамилия = Истина;
		Иначе 
			Массив.Добавить(Врег(Лев(Подстрока,1)));
		КонецЕсли;
		Если ИндексРазделителя > 0 Тогда
			ИсходнаяСтрока = СокрЛ(Прав(ИсходнаяСтрока,СтрДлина(ИсходнаяСтрока)-ИндексРазделителя));
		Иначе 
			Прервать;
		КонецЕсли;
		ИндексРазделителя = Найти(ИсходнаяСтрока," ");
	КонецЦикла;
	
	Если Массив.Количество() > 1 Тогда
	
		РезСтрока = ""; Разделитель = "";
		Для Каждого Подстрока Из Массив Цикл
			РезСтрока = РезСтрока + Подстрока+Разделитель;
			Разделитель = ".";
		КонецЦикла;
		
	Иначе
		РезСтрока = СокрЛП(Массив[0]);
	КонецЕсли;

	Возврат РезСтрока;
	
КонецФункции

&НаКлиенте
Процедура ОтборИерархия(Команда)
	
	Иерархия = НЕ Иерархия;
	Элементы.ТаблицаПользователейОтборИерархия.Пометка = Иерархия;
	УстановитьОтборИерархия();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборИерархия()
	
	Если НЕ Иерархия Тогда
		ОтборСтрок = Неопределено;
	Иначе 
		КлючСвязи = ?(НЕ Элементы.ДеревоAD.ТекущиеДанные = Неопределено,Элементы.ДеревоAD.ТекущиеДанные.UID,0);
		ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи",КлючСвязи);
	КонецЕсли;		
	
	Элементы.ТаблицаПользователей.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоADПриАктивизацииСтроки(Элемент)
	
	Если Иерархия Тогда
		УстановитьОтборИерархия();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СПочтой(Команда)
	
	ЕстьПочта = НЕ ЕстьПочта;
	Элементы.ТаблицаПользователейСПочтой.Пометка = ЕстьПочта;
	ЗаполнитьТаблицуПользователей();
	СписокПользователей.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Иерархия ИЛИ ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Идент = мКлючиСвязейГрупп.Получить(Элемент.ТекущиеДанные.КлючСвязи);
	Если НЕ Идент = Неопределено Тогда
		Элементы.ДеревоAD.ТекущаяСтрока = Идент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейВыбранПриИзменении(Элемент)
	
	ТД = Элементы.ТаблицаПользователей.ТекущиеДанные;
	УИД = ТД.UID;
	Имя = ТД.Имя;
	Если ТД.Выбран Тогда
		СписокПользователей.Добавить(Уид,Имя);
	Иначе 
		СписокПользователей.Удалить(СписокПользователей.НайтиПоЗначению(Уид));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейПередУдалением(Элемент, Отказ)
	
	Для Каждого ЭлементВыд Из Элемент.ВыделенныеСтроки Цикл
		Эл = СписокПользователей.НайтиПоИдентификатору(ЭлементВыд);
		мСтроки = ТаблицаПользователей.НайтиСтроки(Новый Структура("UID,Выбран",Эл.Значение,Истина));
		Если мСтроки.Количество() > 0 Тогда
			мСтроки[0].Выбран = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователей(Команда)
 	Перем объектРВ;
	
	Если СписокПользователей.Количество() > 0 Тогда
		Элементы.ГруппаПрогресс.Видимость = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("СоздатьПользователейЗавершение", ЭтаФорма), "Вы действиетльно хотите создать (обновить) этих пользователей?",РежимДиалогаВопрос.ДаНет,10,КодВозвратаДиалога.Нет,Нстр("ru = 'Вопросец'; uk = 'Питаннячко'"),КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПользователейЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если СоздатьПользователейСервер(мКлючиСвязейГрупп) Тогда
			мКлючиСвязейГрупп.Вставить("Принудительно",Истина);
			//ТолькоВыбранные();
			Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаАД;
			ПоказатьОповещениеПользователя("ВНИМАНИЕ!!!",,"Не забудьте установить группы доступа новым пользователям!", БиблиотекаКартинок.Информация32);
		КонецЕсли;
	КонецЕсли;

	Элементы.ГруппаПрогресс.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Функция СоздатьПользователейСервер(КлючиСвязейГрупп)
	
	ТЗПользователи = ДанныеФормыВЗначение(ТаблицаПользователей,Тип("ТаблицаЗначений"));
	ДанныеПользователя = Новый Структура;
	Для Каждого Колонка Из ТЗПользователи.Колонки Цикл
		ДанныеПользователя.Вставить(Колонка.Имя,Неопределено);
	КонецЦикла;
	мПользователиПоСтрокам = Новый Соответствие;
	ЕстьОшибки = Ложь;
	НачатьТранзакцию();
	мПользователи = Новый Массив;
	Для каждого СпЭлемент Из СписокПользователей Цикл
		
		//СтрокаТЗ = ТЗПользователи.Найти(СпЭлемент.Значение,"UID");
		СтрокаТЗ = ТаблицаПользователей.НайтиСтроки(Новый Структура("UID",СпЭлемент.Значение))[0];
		ЗаполнитьЗначенияСвойств(ДанныеПользователя,СтрокаТЗ);
			
		Идент = КлючиСвязейГрупп.Получить(СтрокаТЗ.КлючСвязи);
		
		СтрокаГруппы = ДеревоAD.НайтиПоИдентификатору(Идент);
		ДанныеПользователя.Вставить("ГруппаПользователей",СтрокаГруппы.ГруппаПользователей);
		
		Если НЕ СоздатьОбновитьПользователя(ДанныеПользователя) = Истина Тогда
			ЕстьОшибки = Истина;
		Иначе 
			мПользователиПоСтрокам.Вставить(СтрокаТЗ.ПолучитьИдентификатор(),ДанныеПользователя.Пользователь);
			мПользователи.Добавить(ДанныеПользователя.Пользователь);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		ОтменитьТранзакцию();
	Иначе

		СпрВсеГруппаДоступа = Справочники.ГруппыДоступа.НайтиПоНаименованию("Користувачі",Истина);
		Если НЕ СпрВсеГруппаДоступа.Пустая() И мПользователи.Количество() > 0 Тогда
			ЗаписатьГруппуВсе = Ложь;
			
			СпрВсеОбъект = СпрВсеГруппаДоступа.ПолучитьОбъект();
			Для Каждого тПользователь Из мПользователи Цикл
				Менеджер = РегистрыСведений.СоставыГруппПользователей.СоздатьМенеджерЗаписи();
				Менеджер.ГруппаПользователей = Справочники.ГруппыПользователей.ВсеПользователи;
				Менеджер.Пользователь = тПользователь;
				Менеджер.Используется = Истина;
				Менеджер.Записать(Истина);
				ЗаписатьГруппуВсе = Истина;
			КонецЦикла;
			Если ЗаписатьГруппуВсе Тогда
				СпрВсеОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
		//ОтменитьТранзакцию();
		ЗафиксироватьТранзакцию();
		СписокПользователей.Очистить();
		Для Каждого КлючИЗначение Из мПользователиПоСтрокам Цикл
			ТекСтр = ТаблицаПользователей.НайтиПоИдентификатору(КлючИЗначение.Ключ);
			Если ТекСтр <> Неопределено Тогда 
				ТекСтр.Пользователь = КлючИЗначение.Значение;
				ТекСтр.Выбран = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;		
	
	Возврат НЕ ЕстьОшибки;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьОбновитьПользователя(ДанныеПоПользователю)
	
	Логин = Нрег(СокрЛП(ДанныеПоПользователю.Аккаунт));
	ЭтоНовый = Ложь;
	
	Если НЕ ДанныеПоПользователю.Пользователь.Пустая() Тогда
		
		ОбъектП = ДанныеПоПользователю.Пользователь.ПолучитьОбъект();
		
	Иначе 
		
		ОбъектП = Справочники.Пользователи.СоздатьЭлемент();
		ОбъектП.AD_UID = ДанныеПоПользователю.UID;
		GUID = ДанныеПоПользователю.UID;
		СтрУИД = Прав(GUID, 8) + "-" + Сред(GUID, 21, 4) + "-" + Сред(GUID, 17, 4) + "-" + Лев(GUID, 4)  + "-" + Сред(GUID, 5, 12);
		
		ОбъектП.УстановитьСсылкуНового(Справочники.Пользователи.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрУИД)));
		ЭтоНовый = Истина;		
		
	КонецЕсли;
	
	ОбъектП.Наименование = ДанныеПоПользователю.Имя;
	
	ЮзерБД = Неопределено;	
	Если Не ЭтоНовый И ЗначениеЗаполнено(ОбъектП.ИдентификаторПользователяИБ) Тогда		
		Попытка
			ЮзерБД = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ОбъектП.ИдентификаторПользователяИБ);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если ЮзерБД = Неопределено Тогда 	
		ЮзерБД = ПользователиИнформационнойБазы.СоздатьПользователя();
	КонецЕсли;
	
	ЮзерБД.АутентификацияОС = Истина;
	ЮзерБД.ПользовательОС = "\\SPACE\"+Логин;
	ЮзерБД.Имя = ?(ПустаяСтрока(ДанныеПоПользователю.КороткоеИмя),ДанныеПоПользователю.Имя,ДанныеПоПользователю.КороткоеИмя);
	ЮзерБД.ПолноеИмя = ДанныеПоПользователю.Имя;
	ЮзерБД.АутентификацияСтандартная = Ложь;
	ЮзерБД.ПоказыватьВСпискеВыбора = Ложь;
		
	ЮзерБД.Язык = Метаданные.Языки.Русский;
	
	ЮзерБД.Записать();
	
	ОбъектП.ИдентификаторПользователяИБ = ЮзерБД.УникальныйИдентификатор;
		
	Если НЕ ПустаяСтрока(ДанныеПоПользователю.Описание) И Найти(ОбъектП.Комментарий,ДанныеПоПользователю.Описание) = 0 Тогда
		ОбъектП.Комментарий = ДанныеПоПользователю.Описание;
	КонецЕсли;
	
	ОбъектП.ОбменДанными.Загрузка = Истина;
	ОбъектП.Записать();
	
	ДанныеПоПользователю.Вставить("Пользователь",ОбъектП.Ссылка);
	МенеджерРС = РегистрыСведений.СведенияОПользователях.СоздатьМенеджерЗаписи();
	МенеджерРС.Пользователь = ОбъектП.Ссылка;
	МенеджерРС.Прочитать();
 	Если НЕ МенеджерРС.Выбран() Тогда
		МенеджерРС.Пользователь = ОбъектП.Ссылка;
		//МенеджерРС.Подразделени = ДанныеПоПользователю.Подразделение;
		МенеджерРС.Записать(Истина);
	КонецЕсли;
	
	Если ДанныеПоПользователю.ГруппаПользователей.Состав.Найти(ОбъектП.Ссылка,"Пользователь") = Неопределено Тогда
		ОбъектГ = ДанныеПоПользователю.ГруппаПользователей.ПолучитьОбъект();
		НоваяСтрока = ОбъектГ.Состав.Добавить();
		НоваяСтрока.Пользователь = ОбъектП.Ссылка;
		ОбъектГ.Записать();
	КонецЕсли;
	
	естьМыло = Ложь; естьУчетнаяЗапись = Ложь;
	Если ДанныеПоПользователю.ЕстьПочта Тогда
		Мыло = НРег(СокрЛП(ДанныеПоПользователю.Email));
		ОтборКИ = Новый Структура("Тип,Вид",Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		мСтроки = ОбъектП.КонтактнаяИнформация.НайтиСтроки(ОтборКИ);
		Для Каждого тСтрока ИЗ мСтроки Цикл
			Если НРег(СокрЛП(тСтрока.АдресЭП)) = Мыло Тогда
				естьМыло = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ естьМыло Тогда
			НоваяСтрока = ОбъектП.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ОтборКИ);
			НоваяСтрока.АдресЭП = Мыло;
			НоваяСтрока.Представление = Мыло;
			естьМыло = Истина;
			НоваяСтрока.ДоменноеИмяСервера = ПолучитьДоменноеИмяСервера(Мыло);
			ОбъектП.Записать();
		КонецЕсли;
		
		Если естьМыло Тогда
			УЗ = Справочники.УчетныеЗаписиЭлектроннойПочты.НайтиПоРеквизиту("АдресЭлектроннойПочты",Мыло);
			УЗтребуетсяЗаполнить = Ложь;
			Если УЗ.Пустая() Тогда
				ОбъектУЗ = Справочники.УчетныеЗаписиЭлектроннойПочты.СоздатьЭлемент();
				ОбъектУЗ.АдресЭлектроннойПочты = Мыло;
				ОбъектУЗ.ВладелецУчетнойЗаписи = ОбъектП.Ссылка;
				УЗтребуетсяЗаполнить = Истина;
			Иначе 
				ОбъектУЗ = Уз.ПолучитьОбъект();
				Если ОбъектУЗ.ВладелецУчетнойЗаписи <> ОбъектП.Ссылка Тогда
					УЗтребуетсяЗаполнить = Истина;
					ОбъектУЗ.ВладелецУчетнойЗаписи = ОбъектП.Ссылка;
				КонецЕсли
			КонецЕсли;
			
			Если УЗтребуетсяЗаполнить Тогда
				ОбъектУЗ.Наименование = Мыло;
				ОбъектУЗ.ИмяПользователя = ?(ПустаяСтрока(ДанныеПоПользователю.КороткоеИмя),ДанныеПоПользователю.Имя,ДанныеПоПользователю.КороткоеИмя);
				//ОбъектУЗ.SMTPАутентификац = Перечисления.ВариантыSMTPАутентифика;
				//ОбъектУЗ.ВариантИспользования = Перечисления.ВариантыИспользованияПочты.Легкая;
				ОбъектУЗ.ВремяОжидания = 30;
				//ОбъектУЗ.ИспользоватьSSLPOP3 = Ложь;
				//ОбъектУЗ.ИспользоватьSSLSMTP = Ложь;
				ОбъектУЗ.ИспользоватьДляОтправки = Истина;
				ОбъектУЗ.ИспользоватьДляПолучения = Ложь;
				//ОбъектУЗ.СпособSMTPАутентификации = Перечисления.СпособыSMTPАутентификации.Plain;
				ОбъектУЗ.СерверИсходящейПочты = "smtp.space.digma";
				//ОбъектУЗ.ПортSMTP = 25;
				ОбъектУЗ.ПользовательSMTP = Логин;
				ОбъектУЗ.ПериодХраненияСообщенийНаСервере = 0;
				//ОбъектУЗ.НеИспользоватьВнутреннююМаршрутизациюДляВходящихПисем = Ложь;
				ОбъектУЗ.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоменноеИмяСервера(АдресПочты)
	
	Индекс = Найти(АдресПочты,"@");
	Если Индекс > 1 Тогда
		
		Возврат Сред(АдресПочты,Индекс+1,СтрДлина(АдресПочты)-Индекс);
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКарточкуПользователя(Команда)
	
	ПараметрыОткрытия = Новый Структура("ЗакрыватьПриЗакрытииВладельца,Ключ",Истина,Элементы.ТаблицаПользователей.ТекущиеДанные.Пользователь);
	ФормаПользователь = ОткрытьФорму("Справочник.Пользователи.ФормаОбъекта",ПараметрыОткрытия,ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПользователейПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		УстДоступность = Ложь;
	Иначе 
		УстДоступность = ЗначениеЗаполнено(Элемент.ТекущиеДанные.Пользователь);
	КонецЕсли;
	
	Элементы.ТаблицаПользователейКонтекстноеМенюОткрытьКарточкуПользователя.Доступность = УстДоступность;
	Элементы.ТаблицаПользователейОткрытьКарточкуПользователя.Доступность = УстДоступность;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоВыбранные()
	
	Если мКэшПоложенияУказателей.Принудительно Тогда
		Пометка = Истина;
	ИначеЕсли мКэшПоложенияУказателей.Пометка = Неопределено Тогда
		Пометка = Элементы.ТаблицаПользователейТолькоВыбранные.Пометка;
	Иначе 
		Пометка = НЕ мКэшПоложенияУказателей.Пометка;
		Элементы.ТаблицаПользователейТолькоВыбранные.Пометка = Пометка;
	КонецЕсли;
	
	Если Пометка Тогда
		ОтборСтрок = Новый ФиксированнаяСтруктура("Выбран",Истина);
		мКэшПоложенияУказателей.Иерархия = Иерархия;
		мКэшПоложенияУказателей.Группа = Элементы.ДеревоAD.ТекущаяСтрока;
		мКэшПоложенияУказателей.Пользователь = Элементы.ТаблицаПользователей.ТекущаяСтрока;
		Элементы.ТаблицаПользователейТолькоВыбранные.Заголовок = "Все пользователи";
		Элементы.ТаблицаПользователейТолькоВыбранные.Картинка = КаринкаВсеЮзеры;
		мКэшПоложенияУказателей.Пометка = Истина;
		Иерархия = Ложь;
		Элементы.ТаблицаПользователейОтборИерархия.Доступность = Ложь;
		Элементы.ТаблицаПользователейСПочтой.Доступность = Ложь;
		Элементы.ТаблицаПользователейОбновить.Доступность = Ложь;
	Иначе 
		ОтборСтрок = Неопределено;
		Элементы.ТаблицаПользователейТолькоВыбранные.Заголовок = "Только выбранные";
		Элементы.ТаблицаПользователейТолькоВыбранные.Картинка = КаринкаВыбраныЮзеры;
		мКэшПоложенияУказателей.Пометка = Ложь;
		Элементы.ТаблицаПользователейОтборИерархия.Доступность = Истина;
		Элементы.ТаблицаПользователейСПочтой.Доступность = Истина;
		Элементы.ТаблицаПользователейОбновить.Доступность = Истина;
		Иерархия = мКэшПоложенияУказателей.Иерархия;
	КонецЕсли;
	мКэшПоложенияУказателей.Принудительно = Ложь;
	Элементы.ТаблицаПользователей.ОтборСтрок = ОтборСтрок;
	Элементы.ТаблицаПользователей.Обновить();
	
КонецПроцедуры


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПланОбмена = ПланыОбмена.ОбменConsolidation.НайтиПоКоду("CON");
	
	СписокДействий.Добавить("ПоступлениеТоваров", НСтр("ru = 'Поступление товаров'"));
	СписокДействий.Добавить("ВозвратПоставщику", НСтр("ru = 'Возврат поставщику'"));
	СписокДействий.Добавить("РеализацияТоваров", НСтр("ru = 'Реализация товаров'"));
	СписокДействий.Добавить("ВозвратОтПокупателя", НСтр("ru = 'Возврат от покупателя'"));
	СписокДействий.Добавить("ТК_ЗакрытиеСменыЗКС", НСтр("ru = 'Закрытие смены ЗКС'"));
	СписокДействий.Добавить("ЗакрытиеСмены", НСтр("ru = 'Закрытие смены'"));
	СписокДействий.Добавить("ПеремещениеТоваров", НСтр("ru = 'Перемещение товаров'"));
	СписокДействий.Добавить("Инвентаризация", НСтр("ru = 'Инвентаризация'"));
	//СписокДействий.Добавить("ВыпускПродукции", НСтр("ru = 'Выпуск продукции'"));
	СписокДействий.Добавить("СписаниеТоваров", НСтр("ru = 'Списание товаров'"));
	//СписокДействий.Добавить("ПриходныйКассовыйОрдер", НСтр("ru = 'Приходный кассовый ордер'"));
	//СписокДействий.Добавить("РасходныйКассовыйОрдер", НСтр("ru = 'Расходный кассовый ордер'"));
	СписокДействий.Добавить("КорректировкаДолга", НСтр("ru = 'Корректировка долга'"));
	СписокДействий.Добавить("Взаимозачет", НСтр("ru = 'Взаимозачет'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПересчитатьДействия();
	УстановитьВидимостьЭлементов();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура СписокДействийПометкаПриИзменении(Элемент)
	
	ПересчитатьДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	СписокДействий.ЗаполнитьПометки(Истина);
	ПересчитатьДействия();

КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	СписокДействий.ЗаполнитьПометки(Ложь);
	ПересчитатьДействия();

КонецПроцедуры

&НаКлиенте
Процедура СписокТиповРезультатовПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.СписокТиповРезультатов.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура;
	Если ТекДанные.Значение <> "Все" Тогда 
		СтруктураОтбора.Вставить("ТипДокумента", ТекДанные.Значение);
	КонецЕсли;
	
	Элементы.ТаблицаДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовФлагПриИзменении(Элемент)
	ПересчитатьДокументы();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьПроверку(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	
	Для Каждого Стр Из СписокДействий Цикл 
		Если Стр.Пометка Тогда 
			СтруктураДействий.Вставить(Стр.Значение, Стр.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураДействий.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выбранных действий!'"));
		Возврат;
	КонецЕсли;

	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("НачалоПериода", Период.ДатаНачала);
	ПараметрыОперации.Вставить("КонецПериода", Период.ДатаОкончания);
	ПараметрыОперации.Вставить("Действия", СтруктураДействий);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ОбработкаСверкиSQL";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	
	
	ДлительнаяОперация = НачатьВыполнениеОперации(ПараметрыОперации);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект, ПараметрыОперации);		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьИзменения(Команда)
	
	КоличествоНаВыгрузку = ТаблицаДокументов.НайтиСтроки(Новый Структура("Флаг", Истина)).Количество();
	
	ЗарегистрироватьИзмененияНаСервере();
	УстановитьВидимостьЭлементов();
	
	ПоказатьОповещениеПользователя(
		Строка(ПланОбмена),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Зарегистрировано %1 документов'; uk = 'Зареєстровано %1 документів'"),
			КоличествоНаВыгрузку));
			
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьИзмененияНаСервере()
	
	тзДокументы = ТаблицаДокументов.Выгрузить(Новый Структура("Флаг", Истина));	
	ТаблицаДокументов.Очистить();
	
	Для Каждого Стр Из тзДокументы Цикл 
		ПланыОбмена.ЗарегистрироватьИзменения(ПланОбмена, Стр.Документ);
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НачатьВыполнениеОперации(Знач ПараметрыОперации)
	
	СписокТиповРезультатов.Очистить();
	ТаблицаДокументов.Очистить();
	
	тзДокументов = ТаблицаДокументов.Выгрузить();
	
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ТК_ОбработкаСверкиSQL.ВыполнитьСверкуДокументов", "CON", ПараметрыОперации, тзДокументов);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, ДопПараметры) Экспорт
	
	ОписаниеРезультата = ОбработатьРезультатОперации(Операция, ДопПараметры);
	
	ЕстьОшибки = ОписаниеРезультата.ЕстьОшибки;
	Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Ошибки", НСтр("ru = 'Обнаружена ошибка при выполнении операции'; uk = 'Виявлена помилка при виконанні операції'"));
	ОповещениеТекст = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеТекст", НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'"));
	ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "ОповещениеПояснение", НСтр(""));
	Картинка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеРезультата, "Картинка", БиблиотекаКартинок.Информация32);	
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);	
	
	Если ЕстьОшибки И Не ПустаяСтрока(Ошибки) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибки);
	КонецЕсли;
	
	УстановитьВидимостьЭлементов();
	
	Если Элементы.СтраницаРезультат.Видимость Тогда 
		ПересчитатьДокументы();
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатОперации(Операция, ДопПараметры)

	ОповещениеТекст 	= "";
	ОповещениеПояснение = "";
	ЕстьОшибки 			= Ложь;
	Ошибки 				= "";
	Результат 			= Неопределено;
	Картинка			= Неопределено;
	СообщенияПользователю = Неопределено;
	
	Если Операция = Неопределено Тогда 
		
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		Картинка = БиблиотекаКартинок.Ошибка32;
		
	Иначе
		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда 		
			
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				Если Результат.Свойство("ТаблицаДокументов") Тогда 
					ТаблицаДокументов.Загрузить(Результат.ТаблицаДокументов);
					ОбновитьСписокТиповРезультатов();	
				КонецЕсли;												
				
				ОповещениеПояснение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СообщениеЗавершения", "");
				
				ЕстьОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ЕстьОшибки", Ложь);
				Ошибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Ошибки", ""); 
			КонецЕсли;						
			
			Если Не ЕстьОшибки Тогда 			
				ОповещениеТекст = НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'");
				Картинка = БиблиотекаКартинок.Успешно32;
			Иначе
				ОповещениеТекст = НСтр("ru = 'Завершено с ошибками'; uk = 'Завершено з помилками'");
				Картинка = БиблиотекаКартинок.Предупреждение32;
			КонецЕсли;
			
			Если ПустаяСтрока(ОповещениеПояснение) Тогда 
				ОповещениеПояснение = НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'");
			КонецЕсли;
			
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 	
			
			ОповещениеТекст = НСтр("ru = 'Ошибка операции'; uk = 'Помилка операції'");
			Картинка = БиблиотекаКартинок.Ошибка32;
			ЕстьОшибки = Истина;
			
			Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщениеЗавершения") Тогда 
				ОповещениеПояснение = Результат.СообщениеЗавершения;
			КонецЕсли;
			
			Если ПустаяСтрока(ОповещениеПояснение) Тогда 
				ОповещениеПояснение = НСтр("ru = 'Операция не выполнена'; uk = 'Операція не виконана'");
			КонецЕсли;
			
			Ошибки = Операция.КраткоеПредставлениеОшибки;
			
		Иначе			
			
			ЕстьОшибки = Истина;
		
		КонецЕсли;
		
		Если Операция.Свойство("Сообщения") И ТипЗнч(Операция.Сообщения) = Тип("ФиксированныйМассив") Тогда 
			Для Каждого Сообщение Из Операция.Сообщения Цикл 
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("СообщенияПользователю") Тогда 
			СообщенияПользователю = Результат.СообщенияПользователю;
		КонецЕсли;		
	КонецЕсли;
	
	ОписаниеРезультата = Новый Структура;
	ОписаниеРезультата.Вставить("ЕстьОшибки", ЕстьОшибки);
	
	Если Не ПустаяСтрока(Ошибки) Тогда 
		ОписаниеРезультата.Вставить("Ошибки", Ошибки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Картинка) Тогда 
		ОписаниеРезультата.Вставить("Картинка", Картинка);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеТекст) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеТекст", ОповещениеТекст);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОповещениеПояснение) Тогда 
		ОписаниеРезультата.Вставить("ОповещениеПояснение", ОповещениеПояснение);
	КонецЕсли;
	
	Если ТипЗнч(СообщенияПользователю) = Тип("ФиксированныйМассив") Тогда 
		ОписаниеРезультата.Вставить("СообщенияПользователю", СообщенияПользователю);
	КонецЕсли;
	
	Возврат ОписаниеРезультата;	
КонецФункции

&НаСервере
Процедура ОбновитьСписокТиповРезультатов()
	
	тзТипыДокументов = ТаблицаДокументов.Выгрузить(, "ТипДокумента");
	тзТипыДокументов.Свернуть("ТипДокумента");
	
	СписокТиповРезультатов.Очистить();
	
	СписокТиповРезультатов.Добавить("Все");
	Для Каждого Стр Из тзТипыДокументов Цикл 
		СписокТиповРезультатов.Добавить(Стр.ТипДокумента);	
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.СтраницаРезультат.Видимость = ТаблицаДокументов.Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДействия()
	
	КоличествоОтметок = 0;
	Для Каждого Стр Из СписокДействий Цикл 
		Если Стр.Пометка Тогда 
			КоличествоОтметок = КоличествоОтметок + 1;
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовокСтраницы = НСтр("ru = 'Установки'; uk = 'Налаштування'") + ?(КоличествоОтметок > 0, " (" + КоличествоОтметок + ")", "");
	
	Элементы.СтраницаУстановки.Заголовок = ЗаголовокСтраницы;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокументы()
	
	ОбщееКоличество = ТаблицаДокументов.НайтиСтроки(Новый Структура("Флаг", Истина)).Количество();
	
	Для Каждого Стр Из СписокТиповРезультатов Цикл 
		
		текКоличество = 0;
		Представление = Стр.Значение;
		
		Если Стр.Значение = "Все" Тогда 
			текКоличество = ОбщееКоличество;
		Иначе
			текКоличество = ТаблицаДокументов.НайтиСтроки(Новый Структура("Флаг, ТипДокумента", Истина, Стр.Значение)).Количество();
		КонецЕсли;
		            
		Стр.Представление = Представление + ?(текКоличество > 0, " (" + текКоличество + ")", "");
	КонецЦикла;
	
	Элементы.СтраницаРезультат.Заголовок = НСтр("ru = 'Результат'; uk = 'Результат'") + ?(ОбщееКоличество > 0, " ("+ОбщееКоличество+")", "");
	
КонецПроцедуры


#КонецОбласти
 
 
 
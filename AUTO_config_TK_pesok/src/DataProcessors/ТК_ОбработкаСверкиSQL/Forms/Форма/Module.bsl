
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МассивОбъектов = Новый Массив;
		
	МассивОбъектов.Добавить("ПоступлениеТоваров");	
	МассивОбъектов.Добавить("ВозвратПоставщику");	
	МассивОбъектов.Добавить("РеализацияТоваров");
	МассивОбъектов.Добавить("ЗакрытиеСмены");
	МассивОбъектов.Добавить("ВозвратОтПокупателя");
	МассивОбъектов.Добавить("ПеремещениеТоваров");
	МассивОбъектов.Добавить("Инвентаризация");	
	МассивОбъектов.Добавить("ЗаказПоставщику");		
	МассивОбъектов.Добавить("ИзменениеАссортимента");
	МассивОбъектов.Добавить("СписаниеТоваров");
	МассивОбъектов.Добавить("ВыпускПродукции");	
	МассивОбъектов.Добавить("ИзменениеЦен");
	МассивОбъектов.Добавить("ПротоколСогласованияЦен");
	МассивОбъектов.Добавить("ПересортицаТоваров");
	МассивОбъектов.Добавить("ПриходныйКассовыйОрдер");
	МассивОбъектов.Добавить("РасходныйКассовыйОрдер");
	МассивОбъектов.Добавить("ПоступлениеБезналичныхДенежныхСредств");
	МассивОбъектов.Добавить("СписаниеБезналичныхДенежныхСредств");	
	МассивОбъектов.Добавить("КорректировкаДолга");
	МассивОбъектов.Добавить("ТК_ЗакрытиеСменыЗКС");

	
	СписокПроверок.ЗагрузитьЗначения(МассивОбъектов);
	Элементы.ОбъектМетаданных.СписокВыбора.ЗагрузитьЗначения(МассивОбъектов);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьЗначенияФлажкам(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьЗначенияФлажкам(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверокПометкаПриИзменении(Элемент)
	ПересчитатьДействия();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьПроверку(Команда)
	
	СтруктураДействий = Новый Структура;
	Для Каждого Стр Из СписокПроверок Цикл 
		Если Стр.Пометка Тогда 
			СтруктураДействий.Вставить(Стр.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураДействий.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, "Нет выбранных действий!");
		Возврат;
	КонецЕсли;
		
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("НачДата", Период.ДатаНачала);
	ПараметрыВыполнения.Вставить("КонДата", Период.ДатаОкончания);
	ПараметрыВыполнения.Вставить("Действия", СтруктураДействий);	
		
	Результат = ЗапуститьПроверкуНаСервере(ПараметрыВыполнения);
	
	Действия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Действия", Новый Структура);	
	
	ПоказатьОповещениеПользователя("Выполнено " + Действия.Количество() + " проверок(и/а)");
	
	Для Каждого СтрРез Из Действия Цикл 		
		Ключ = СтрРез.Ключ;
		Если ЗначениеЗаполнено(СтрРез.Значение) Тогда 
			Значение = СтрРез.Значение;
		Иначе
			Значение = "Нет данных";
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Строка(Ключ) + ": " + Значение);		
	КонецЦикла;
	
	//Вывод на печать результатов проверок
	СтруктураОтчетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Отчеты", Новый Структура);
	СтрокаСозданияКоллекции = "";
	
	Если СтруктураОтчетов.Количество() > 0 Тогда 		
		Для Каждого Стр из СтруктураОтчетов Цикл 
			СтрокаСозданияКоллекции = СтрокаСозданияКоллекции + ?(Не ПустаяСтрока(СтрокаСозданияКоллекции), ", ", "") + Стр.Ключ;
		КонецЦикла;
	
		КоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(СтрокаСозданияКоллекции);
		
		Ит = 0;
		Для Каждого Стр Из СтруктураОтчетов Цикл 		
			
			Структура = Стр.Значение;		
			ТекКоллекция = КоллекцияПечатныхФорм[Ит];		
			ЗаполнитьЗначенияСвойств(ТекКоллекция, Структура);
			
			Ит = Ит + 1;
		КонецЦикла;		
		
		ПараметрыПечати = Новый Структура;
	
		ПараметрыПечати.Вставить("ВладелецФормы", ЭтаФорма);
		ПараметрыПечати.Вставить("ЗаголовокФормы", НСтр("ru = 'Расхождения между базами'; uk = 'Розбіжності між базами'"));
	
		УправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, , ПараметрыПечати);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьПроверкуНаСервере(Знач ПараметрыВыполнения)
	
	Возврат Обработки.ТК_ОбработкаСверкиSQL.ВыполнитьСверку(ПараметрыВыполнения);
	
КонецФункции

&НаСервере
Процедура ОбработатьГУИДыНаСервере()
	
	Данные = ТекстИсточник.ПолучитьТекст();
	ДопКод = ТекстДляВыполнения.ПолучитьТекст();
	
	УзелОбмена = ПланыОбмена.ОбменТК_Center.НайтиПоКоду("GL");
	МассивУзлов = Новый Массив;
	МассивУзлов.Добавить(УзелОбмена);	
	
	ТаблицаДокументов.Очистить();
	
	ВсегоСтрок = СтрЧислоСтрок(Данные);
	
	Для Ит = 1 По ВсегоСтрок Цикл 
		Значение = СокрЛП(СтрПолучитьСтроку(Данные, Ит));
		Если ПустаяСтрока(Значение) Тогда 
			Продолжить;
		КонецЕсли;
		
		Док = Неопределено;
		Сообщение = "";		
		
		Попытка
			Если ПреобразоватьГУИД Тогда 
				СтрГУИД = СтрЗаменить(Значение, "0x", "");
				СтрГУИД = СтрЗаменить(СтрГУИД, "{", "");
				СтрГУИД = СтрЗаменить(СтрГУИД, "}", "");
				НовСтр =  Прав(СтрГУИД, 8) + "-" + Сред(СтрГУИД, 21, 4) + "-" + Сред(СтрГУИД, 17, 4) + "-" + Лев(СтрГУИД, 4) + "-" + Сред(СтрГУИД, 5, 12);				
				ГУИД = Новый УникальныйИдентификатор(НРег(НовСтр));
			Иначе
				ГУИД = Новый УникальныйИдентификатор(НРег(Значение));
			КонецЕсли;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			Продолжить;
		КонецПопытки;
		
		Док = Документы[ОбъектМетаданных].ПолучитьСсылку(ГУИД);
				
		Если Не ПустаяСтрока(ДопКод) Тогда  
			Попытка
				Выполнить ДопКод;
			Исключение
			КонецПопытки;
		КонецЕсли;

		Если ВыводитьСообщение Тогда 
			ОбщегоНазначения.СообщитьПользователю(Строка(Док) + ?(Не ПустаяСтрока(Сообщение), ", " + Сообщение, ""), ?(ОбщегоНазначения.СсылкаСуществует(Док), Док, Неопределено));
		КонецЕсли;
		
		Если Док <> Неопределено Тогда 
			Попытка
				НовСтрока = ТаблицаДокументов.Добавить();
				НовСтрока.Выбор = Истина;
				НовСтрока.Документ = Док;
				НовСтрока.ТипЗначения = Строка(ТипЗнч(Док));
				НовСтрока.Комментарий = Значение;
			Исключение
			КонецПопытки;
			
			Если РегистрироватьНаВыгрузку Тогда 
				ПланыОбмена.ЗарегистрироватьИзменения(МассивУзлов, Док);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьГУИДы(Команда)
	Если Не ПустаяСтрока(ОбъектМетаданных) Тогда 	
		ОбработатьГУИДыНаСервере();
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЗначенияФлажкам(Значение)
	СписокПроверок.ЗаполнитьПометки(Значение);
	ПересчитатьДействия();
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДействия()
	
	КоличествоОтметок = 0;
	Для Каждого Стр Из СписокПроверок Цикл 
		Если Стр.Пометка Тогда 
			КоличествоОтметок = КоличествоОтметок + 1;
		КонецЕсли;
	КонецЦикла;
	
	ЗаголовокСтраницы = "Установки" + ?(КоличествоОтметок > 0, " (" + КоличествоОтметок + ")", "");
	
	Элементы.СтраницаУстановки.Заголовок = ЗаголовокСтраницы;
	
КонецПроцедуры


#КонецОбласти
 
 
 
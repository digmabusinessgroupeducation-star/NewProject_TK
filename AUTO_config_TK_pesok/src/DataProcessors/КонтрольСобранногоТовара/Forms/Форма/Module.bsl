
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	//Инициализация
	текСтатус = "ОжиданиеСканирования";
	Объект.Комплектовщик = Пользователи.ТекущийПользователь();
	текМестоРазмещения = Перечисления.МестаРазмещения.A;
	КоличествоМест = 1;
	Пользователь = Пользователи.ТекущийПользователь();
	ИспользуютсяОбъединенныеРезервы = ПолучитьФункциональнуюОпцию("ИспользоватьОбъединениеРезервированиеЗаказов");
	
	КонтрольВремени = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"ПерсонализироватьКонтрольСобраногоТовара",Ложь);
	
	ДопПараметры = Новый Структура;
	
	//Отчет отгрузка склада
	ВариантОтчета = ПолучитьВариантОтчетаОтгрузкиСклада();
	Элементы.ОтчетОтгрузкиСклада.Видимость = ВариантОтчета <> Неопределено;
	
	//ЗапретПечати
	ЗапретПечати_Статус = Истина;
	
	СписокСкладов = Новый СписокЗначений;
	СписокСкладов.Добавить(Справочники.СкладыКомпании.НайтиПоКоду("AC000019")); //м.Харків, вул. Суздальські Ряди, буд 12/2
	СписокСкладов.Добавить(Справочники.СкладыКомпании.НайтиПоКоду("ФС000004")); //РЦ Суздальские ряды,12 - Снеки Аттика
	
	ЗапретПечати_Параметры = Новый Структура;
	ЗапретПечати_Параметры.Вставить("ЗапрещенаПечать", СписокСкладов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");								
		//ОповещениеПриПодключении = НСтр("ru = 'Отсканируйте накладную'; uk = 'Отскануйте накладну'");
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПоТипу(Неопределено, УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	Если КонтрольВремени Тогда
		Элементы.КоличествоМест.Видимость = КонтрольВремени;
		Элементы.Переместить.Видимость = КонтрольВремени;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекКод = Параметр[0];
			Иначе
				ТекКод = Параметр[1][1];
			КонецЕсли;
			
			ОбработатьПолученныйШК(ТекКод);
			Возврат;
			
		ИначеЕсли ИмяСобытия = "ScanDataBase64" Тогда
			
			ШтрихкодBase64 = "";
			СимволыШтрихкодBase64 = Base64Значение(Параметр[0]); // Декодируем строку
			// Преобразуем в строку символов
			МассивСимволов = СтрРазделить(СимволыШтрихкодBase64, " ");
			Для Каждого СимволШтрихкода Из МассивСимволов Цикл
				Если СимволШтрихкода = "1D" Тогда // Код символа GS в HEX
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияМаркировкаКлиентСервер.ЭкранированныйСимволGS1();
				Иначе
					ШтрихкодBase64 = ШтрихкодBase64 + МенеджерОборудованияКлиентСервер.ПреобразоватьHEXВСтроку(СимволШтрихкода);
				КонецЕсли;
			КонецЦикла;
			ОбработатьПолученныйШК(ШтрихкодBase64);
			Возврат;

			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	ДобавитьЛог(ЛогДействий, "Сканер", "Не известное событие <" + ИмяСобытия + ">, Источник <" + Источник + ">");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	//ЗаписатьЛог();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить("СканерШтрихкода");
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоТипу(, УникальныйИдентификатор, ПоддерживаемыеТипыВО);	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура фКоличествоПровереноПриИзменении(Элемент)
	Элементы.фПодтвердитьКорректировкуКоличества.Доступность = КоличествоПроверено <= КоличествоВНакладной;
	ЭтаФорма.ТекущийЭлемент = Элементы.фПодтвердитьКорректировкуКоличества;
	//Элементы.фПодтвердитьКорректировкуКоличества.акти
	//фПодтвердитьКорректировкуКоличества
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ТоварыПередНачаломИзменения(Элемент, Отказ)	
	Отказ = Истина;
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ВидСканирования = Новый Структура("Производитель, КраткийКодБлока", "", Ложь);
	
	мСтрок = КешТоваров.НайтиСтроки(Новый Структура("Номенклатура", ТекущаяСтрока.Номенклатура));			
	Если мСтрок.Количество() > 0 Тогда 
		ЗаполнитьЗначенияСвойств(ВидСканирования, мСтрок[0]);
	КонецЕсли;
	
	Если ВидСканирования.Производитель = "PMU" ИЛИ ВидСканирования.Производитель = "BAT" Тогда 
		ДобавитьЛог(ЛогДействий, "Ручной ввод", "Отказ Ручного выбора в ТЧ ""Товары""", Строка(ТекущаяСтрока.Номенклатура)+?(ЗначениеЗаполнено(ТекущаяСтрока.ХарактеристикаНоменклатуры),", " + Строка(ТекущаяСтрока.ХарактеристикаНоменклатуры), ""));		
		
		ПоказатьПредупреждение(,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Товар ""%1"" запрещено редактировать вручную!
                      |%2'; uk = 'Товар ""%1"" заборонено редагувати вручну!
                      |%2'"),
				ТекущаяСтрока.Номенклатура,
				?(Не ВидСканирования.КраткийКодБлока, НСтр("ru = 'Просканируйте QR-код на блоке!'; uk = 'Проскануйте QR-код на блоці!'"), НСтр("ru = 'Просканируйте двумерный код на блоке,
                                                                                                                                                |затем просканируйте краткий QR-код.'; uk = 'Проскануйте двумірний код на блоці,
                                                                                                                                                |потім проскануйте короткий QR-код.'"))));			
	Иначе
		ДобавитьЛог(ЛогДействий, "Ручной ввод", "Ручной выбор в ТЧ ""Товары""", Строка(ТекущаяСтрока.Номенклатура)+?(ЗначениеЗаполнено(ТекущаяСтрока.ХарактеристикаНоменклатуры),", " + Строка(ТекущаяСтрока.ХарактеристикаНоменклатуры), ""));
		
		текСтатус = "ПроверкаКоличества";
		РучнойВвод = Истина;
		ИндексСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
		
		УстановитьСтраницуПоСтатусу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура текСписокМРЦПередНачаломИзменения(Элемент, Отказ)
	ПодтвердитьМРЦ(Неопределено);
	Отказ = Истина;
КонецПроцедуры


#КонецОбласти

&НаСервереБезКонтекста
Процедура ОтправитьНакладную(ДокументРасход)
	
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыбраннаяПечФорма  = УправлениеСвойствами.ЗначениеСвойства(ДокументРасход.Контрагент,"ПечатнаяФормаНаПочту");
	
	Если НЕ ЗначениеЗаполнено(ВыбраннаяПечФорма) Тогда
		Возврат;
	КонецЕсли;
	Попытка
		ВидКИ = УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("EmailКонтрагента");
	Исключение
		Возврат;
	КонецПопытки;
	
	ЭлАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ДокументРасход.Контрагент,ВидКИ);
	
	Если ПустаяСтрока(ЭлАдрес) Тогда
		//ДобавитьЛог(ЛогДействий, "ОтправкаНакладной", "Адрес не заполнен", Строка(ДокументРасход.Контрагент));
		Возврат;
	КонецЕсли;
	
	
	МассивДанных = Новый Массив;        
	МассивДанных.Добавить(Метаданные.Документы.РеализацияТоваров);
	    
	КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("ФормаСписка", МассивДанных);
	
	мОбъектыПечати = Новый Массив();
	мОбъектыПечати.Добавить(ДокументРасход);

	НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
	НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.XLSX);
	
	ПечатнаяФорма = ВРег(ВыбраннаяПечФорма.Наименование);
	ДокументНапечатан = Ложь;
	Для Каждого Команда Из КомандыПечати Цикл 
		
		Если ВРег(Команда.Представление) = ПечатнаяФорма Тогда
			тзПечатныеФормы = УправлениеПечатью.НапечататьВФайл(Команда,мОбъектыПечати,НастройкиСохранения);  
			ДокументНапечатан = Истина;
			Прервать;
		КонецЕсли;    
	КонецЦикла;

	
	Если ДокументНапечатан Тогда
		Если тзПечатныеФормы.Количество() = 0 Тогда Возврат; КонецЕсли;
		
		ДД = тзПечатныеФормы[0].ДвоичныеДанные;  
		ИмяФайла = тзПечатныеФормы[0].ИмяФайла;
		АдресВХ =    ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор());
		МассивВложения = Новый Массив;
		СтруктураВложения = Новый Структура;
		СтруктураВложения.Вставить("Представление",ИмяФайла);
		СтруктураВложения.Вставить("АдресВоВременномХранилище",АдресВХ);
		СтруктураВложения.Вставить("Кодировка","");
		СтруктураВложения.Вставить("Идентификатор","");
		МассивВложения.Добавить(СтруктураВложения);
		
		ПараметрыПисьма = Новый Структура;
		ПараметрыПисьма.Вставить("Кому",ЭлАдрес);
		ПараметрыПисьма.Вставить("Тема","Накладная");
		ПараметрыПисьма.Вставить("Тело",ИмяФайла);
		ПараметрыПисьма.Вставить("Вложения",МассивВложения);
		
		Письмо =	РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
		РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);

	КонецЕсли;
	
	
	
	
КонецПроцедуры



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКомандуПечати(ДокументСсылка, РеглУчет, НаПринтер = Ложь)
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	ИмяМенеджераПечати = "";
	СтрокаПечФорм = "";
	
	Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		Если РеглУчет Тогда 
			СтрокаПечФорм = "РасходнаяНакладнаяУКТВЭД,РасходнаяНакладнаяУКТВЭД";
		Иначе
			СтрокаПечФорм = "РасходнаяНакладнаяУКТВЭД,РасходнаяНакладнаяБезШапкиОтгрузка";	
		КонецЕсли;		
		ИмяМенеджераПечати = "Документ.РеализацияТоваров";	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		СтрокаПечФорм = "ПеремещениеТоваров,ПеремещениеТоваров";
		ИмяМенеджераПечати = "Документ.ПеремещениеТоваров";	
	КонецЕсли;
	
	Если НаПринтер Тогда 
		ТК_УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(ИмяМенеджераПечати, СтрокаПечФорм, ДокументСсылка, Новый Структура());	
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяМенеджераПечати, СтрокаПечФорм, ДокументСсылка, ЭтаФорма, Новый Структура());	
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		ОтправитьНакладную(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечать(Команда)		
	
	Если текСтатус <> "ПечатьНакладной" ИЛИ ЗапретПечати_Статус Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоОбъединенныйРезерв Тогда 
		Для Каждого СтрРасход Из ДокументыРасход Цикл 
			ВыполнитьКомандуПечати(СтрРасход.Документ, СтрРасход.РегламентированныйУчет, Ложь);
		КонецЦикла;
	Иначе
		ВыполнитьКомандуПечати(ДокументРасход, ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "РегламентированныйУчет", Ложь), Ложь);
	КонецЕсли;
	
	Элементы.Декорация1.Заголовок = НСтр("ru = 'Просканируйте накладную!'; uk = 'Проскануйте накладну!'");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьНаПринтер(Команда)
	
	Если текСтатус <> "ПечатьНакладной" ИЛИ ЗапретПечати_Статус Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтоОбъединенныйРезерв Тогда 
		Для Каждого СтрРасход Из ДокументыРасход Цикл 
			ВыполнитьКомандуПечати(СтрРасход.Документ, СтрРасход.РегламентированныйУчет, Истина);
		КонецЦикла;
	Иначе
		ВыполнитьКомандуПечати(ДокументРасход, ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопПараметры, "РегламентированныйУчет", Ложь), Истина);
	КонецЕсли;

	Элементы.Декорация1.Заголовок = НСтр("ru = 'Просканируйте накладную!'; uk = 'Проскануйте накладну!'");
	
КонецПроцедуры




&НаКлиенте
Процедура Отладка_РучноеСканирование(Команда)
	
	Если Не ПустаяСтрока(ПолеСканировки) Тогда 
		ТекКод = СокрЛП(ПолеСканировки);						
		ДобавитьЛог(ЛогДействий, "Сканер", "Ручной ввод штрих-кода: " + ТекКод);		
		
		ОбработатьПолученныйШК(ТекКод);				
		
		ПолеСканировки = "";
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Поле сканировки пустое'; uk = 'Поле сканування не заповнене'"), 5);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРаботуРабочегоМеста(Команда)
	
	Если текСтатус <> "ОжиданиеСканирования" Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаписатьЛог();
	ЗавершитьРаботуСистемы(Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтменаНакладной(Команда)
	ВсеОк = Истина;
	ОтменаПроверкиНаСервере(Всеок);
	
	Если ВсеОк Тогда 
		текСтатус = "ОжиданиеСканирования";		
		УстановитьСтраницуПоСтатусу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменаПроверкиНаСервере(ВсеОк)
	
	НаборЗаписей = РегистрыСведений.ЗапрещеннаяОтгрузкаФилипМоррис.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.РезервированиеЗаказовПокупателя.Установить(Объект.Резерв);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество()>0 Тогда
		Для Каждого Запись Из НаборЗаписей Цикл 
			Запись.ПризнакОбработки = Перечисления.СтатусыДокументовЕДИ.Новый;
		КонецЦикла;
		Попытка
			НаборЗаписей.Записать();
		Исключение
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ДобавитьЛог(ЛогДействий, "Отмена обработки накладной", "Ошибка при Нажатии кнопки отмены", Строка(Объект.Резерв));
				ЗаписатьЛог();
				ВсеОк = Ложь;
				Возврат;
			КонецПопытки;
		КонецПопытки;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "Отмена обработки накладной", "Нажатие клавиши ""Отмена"" на странице подбора товара", Строка(Объект.Резерв));
	
	ЗакончитьСборкуРезерва(); // Vov41k 12.12.2023
	
	ЗаписатьЛог();

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНакладную(Команда)
	
	Если текСтатус = "НакладнаяНайдена" Тогда		
		ПроверитьНакладнуюНаСервере();
		
		текСтатус = "ПроверкаНакладной";
		УстановитьСтраницуПоСтатусу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНакладнуюНаСервере()	
	
	ДокументКорректен = Истина;	
	
	ДобавитьЛог(ЛогДействий, "Проверка", "Нажатие кнопки ""Проверить""", Строка(Объект.Резерв));
	
	РасхожденияКоличества.Очистить();
	
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.КоличествоПроверено <> Стр.Количество Тогда 
			ДокументКорректен = Ложь;	
			НовСтрока = РасхожденияКоличества.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Стр);
			НовСтрока.Разница = НовСтрока.КоличествоПроверено - НовСтрока.Количество;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьМРЦ(Команда)
	ВыбранноеМРЦ = Элементы.текСписокМРЦ.ТекущиеДанные;
	    
	Если ВыбранноеМРЦ <> Неопределено Тогда 
		ПодтвердитьВыбранноеМРЦ(ВыбранноеМРЦ.Значение);
		ОбработатьВводСтроки();
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите требуемое МРЦ и нажмите кнопку ""Выбрать""'; uk = 'Оберіть потрібне МРЦ та нажміть клавішу ""Обрати""'"), 8, НСтр("ru = 'Внимание!'; uk = 'Увага!'"));
	КонецЕсли;
	
	УстановитьСтраницуПоСтатусу();
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВыбранноеМРЦ(МРЦ)
	текДействие = "Выбор МРЦ";
	ДобавитьЛог(ЛогДействий, текДействие, "Выбрано МРЦ пользователем", Строка(МРЦ));
	
	нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", текНоменклатура, МРЦ));
	Если нСтроки.Количество() > 0 Тогда 
		НайденнаяСтрока = нСтроки[0].ПолучитьИдентификатор();
	Иначе 
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка! Не найдено в тч Товары строки с указанными данными", Строка(текНоменклатура) + ", " + Строка(МРЦ));
		ТекстОшибки = НСтр("ru = 'Ошибка! Не найдена строка документа: ""%Товар%""'; uk = 'Помилка! Не знайдена строка документу: ""%Товар%""'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Товар%", Строка(текНоменклатура) + ", " + Строка(МРЦ));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	текСписокМРЦ.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьМРЦ(Команда)
	ДобавитьЛог(ЛогДействий, "Отмена выбора МРЦ", "Нажатие клавиши ""Отмена"" на странице выбора МРЦ", Строка(текНоменклатура));
	
	текСтатус = "НакладнаяНайдена";
	текСтруктураШК = Неопределено;
	текНоменклатура = Неопределено;
	текСписокМРЦ.Очистить();
	
	УстановитьСтраницуПоСтатусу();
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьКорректировкуКоличества(Команда)
	
	Если текСтатус <> "ПроверкаКоличества" Тогда 
		Возврат;
	КонецЕсли;
	
	текДействие = ?(РучнойВвод, "Ручной ввод", "Сканирование QR-кода");
	
	Если КоличествоПроверено <= КоличествоВНакладной Тогда 		
		
		ТекСтрока = Объект.Товары.НайтиПоИдентификатору(ИндексСтроки);
		ВидСканирования = Новый Структура("Производитель", "");		
		мСтрок = КешТоваров.НайтиСтроки(Новый Структура("Номенклатура", ТекСтрока.Номенклатура));
		Если мСтрок.Количество() > 0 Тогда 
			ЗаполнитьЗначенияСвойств(ВидСканирования, мСтрок[0]);
		КонецЕсли;
		
		Если (ВидСканирования.Производитель = "PMU" ИЛИ ВидСканирования.Производитель = "BAT") И РучнойВвод И КоличествоПроверено > 0 Тогда
			Возврат;
		КонецЕсли;
		
		ДобавитьЛог(ЛогДействий, текДействие, "Подтверждение изменения количества", "Было=" + ТекСтрока.КоличествоПроверено + ", Стало=" + КоличествоПроверено);				
		
		ТекСтрока.КоличествоПроверено = КоличествоПроверено;						
				
		Если ВидСканирования.Производитель = "PMU" ИЛИ ВидСканирования.Производитель = "BAT" Тогда
			Элементы.ГруппаQRкодФМ.Видимость = Ложь;
			ЗапомнитьОтсканированныеБлоки(ТекСтрока.Организация);
		КонецЕсли;
		
		текСтатус = "НакладнаяНайдена";		
		УстановитьСтраницуПоСтатусу();
		
		НайденнаяСтрока = ИндексСтроки;		
		ПодключитьОбработчикОжидания("АктивироватьНайденнуюСтроку", 0.2, Истина);
	Иначе		
		ТекстСообщения = НСтр("ru = 'Введено количество больше чем указано в накладной'; uk = 'Введена кількість більша за вказану у накладній'");		
		ПоказатьПредупреждение(, ТекстСообщения,, НСтр("ru = 'Введено неправильное количество'; uk = 'Введена невірна кількість'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПроверкиКоличество(Команда)
	
	НайденнаяСтрока = Неопределено;
	текНоменклатура = Неопределено;
	текСтруктураШК = Неопределено;
	
	текСтатус = "НакладнаяНайдена";		
	УстановитьСтраницуПоСтатусу();

	ПодключитьОбработчикОжидания("АктивироватьНайденнуюСтроку", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаПроверки(Команда)

	Если текСтатус <> "ПроверкаНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "Отмена", "Нажатием кнопки ""Отмена"" на странице проверки накладной");
	
	текСтатус = "НакладнаяНайдена";
	УстановитьСтраницуПоСтатусу();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНакладную(Команда)
	
	Если текСтатус <> "ПроверкаНакладной" Тогда 
		Возврат;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, "Формирование расхода", "Нажатие кнопки ""Сформировать (F7)""", Строка(Объект.Резерв));		
	
	ОчиститьСообщения();
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПроверка Тогда 
		Если ДокументКорректен Тогда 
			Если ЭтоОбъединенныйРезерв Тогда 
				СформироватьРасходныеДокументыДляОбРезерва();	
			Иначе
				СформироватьНакладнуюНаСервере();
			КонецЕсли;
			
			УстановитьСтраницуПоСтатусу();
		Иначе
			ОповещениеЗавершения = Новый ОписаниеОповещения("СформироватьНакладнуюСРасхождениемПродолжение", ЭтаФорма);
			ПоказатьВопрос(ОповещениеЗавершения, НСтр("ru = 'Обнаружены расхождения в документе.
                                                       |Сформировать документ с расхождениями?'; uk = 'Знайдені розбіжності у документі.
                                                       |Сформувати документ з розбіжностями?'"), РежимДиалогаВопрос.ДаНет, 10, КодВозвратаДиалога.Нет);
		КонецЕсли;
	Иначе
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Ложное срабатывание кнопки ""Сформировать (F7)"". Текущая страница = " + Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНакладнуюСРасхождениемПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Подтверждение формирования расхода с расхождениями", Строка(Объект.Резерв));		
		
		Если ЭтоОбъединенныйРезерв Тогда 
			СформироватьРасходныеДокументыДляОбРезерва();	
		Иначе
			СформироватьНакладнуюНаСервере();
		КонецЕсли;

		УстановитьСтраницуПоСтатусу();
	Иначе
		ДобавитьЛог(ЛогДействий, "Формирование расхода", "Отказ от формирования расхода с расхождениями", Строка(Объект.Резерв));		
	КонецЕсли;		
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРасходныеДокументыДляОбРезерва()
	
	УстановитьПривилегированныйРежим(Истина);
	
	текДействие = "Формирование расхода (Об. резерв)";		
	
	Если Объект.Товары.Итог("КоличествоПроверено") = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "В документе нет провереных строк. Расход не сформирован");
		Возврат;
	КонецЕсли;		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗаказПокупателя.ХозОперация КАК ХозОперация,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПокупателя.ТипЦен КАК ТипЦен,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	ЗаказПокупателя.Ссылка КАК Ссылка,
	               |	ЗаказПокупателя.Дата КАК Дата
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка В(&МассивЗаказов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата";

	тзЗаказы = ЗаказыПокупателя.Выгрузить();	
	Запрос.УстановитьПараметр("МассивЗаказов", тзЗаказы.ВыгрузитьКолонку("Документ"));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка получения реквизтов ");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаказа = Результат.Выбрать();
	ВсеОк = Истина;
	
	НачатьТранзакцию();
	
	Пока РеквизитыЗаказа.Следующий() Цикл 
		
		Попытка
			текДокументРасход = Неопределено;
			
			Если ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(РеквизитыЗаказа.Контрагент) Тогда		
				ВсеОк = СформироватьПеремещениеТоваров(текДокументРасход, РеквизитыЗаказа); 
			Иначе
				ВсеОк = СформироватьРеализациюТоваров(текДокументРасход, РеквизитыЗаказа);
			КонецЕсли;
			
			Если ВсеОк И ЗначениеЗаполнено(текДокументРасход) Тогда 
				НоваяСтрока = ДокументыРасход.Добавить();
				НоваяСтрока.Документ = текДокументРасход;
				НоваяСтрока.РегламентированныйУчет = текДокументРасход.РегламентированныйУчет;
			КонецЕсли;
		Исключение
			ВсеОк = Ложь;
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка создания накладных: " + ОписаниеОшибки());
		КонецПопытки;
			
		Если Не ВсеОк Тогда 
			Прервать;
		КонецЕсли;		
	КонецЦикла; 	
	
	Если ДокументыРасход.Количество() = 0 Тогда 
		ВсеОк = Ложь;
		ТекстОшибки = НСтр("ru = 'Отгрузка не сформирована! Нету просканированного товара!'; uk = 'Відвантаження не сформоване! Немає відсканованого товару!'");
		
		ДобавитьЛог(ЛогДействий, текДействие, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Если ВсеОк Тогда 		
		ЗаполнитьДанные_ЗапрещеннымиДанными();
		ВсеОк = ЗаписатьДанныеСканирования() И ЗаписатьЗапрещенныеДанные();		
	КонецЕсли;
	
	Если ВсеОк Тогда 
		ЗафиксироватьТранзакцию();
				
		Если КонтрольВремени Тогда 
			ЗавершитьКонтроль();
		КонецЕсли;
		
		ЗакончитьСборкуРезерва(); // Vov41k 12.12.2023
		
		ЗаписатьЛог();
		
	  	текСтатус = "ПечатьНакладной";		
		ОбновитьЭлементыФормыДляПечати();				
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНакладнуюНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);

	текДействие = "Формирование расхода";		
	
	Если Объект.Товары.Итог("КоличествоПроверено") = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "В документе нет провереных строк. Расход не сформирован");
		Возврат;
	КонецЕсли;		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗаказПокупателя.ХозОперация КАК ХозОперация,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПокупателя.ТипЦен КАК ТипЦен,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	ЗаказПокупателя.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("ЗаказПокупателя", ЗаказПокупателя);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка получения реквизтов ");
		Возврат;
	КонецЕсли;
	
	РеквизитыЗаказа = Результат.Выбрать();
	РеквизитыЗаказа.Следующий();
	
	ДопПараметры.Вставить("РегламентированныйУчет", РеквизитыЗаказа.РегламентированныйУчет);
	ДокументРасход = Неопределено;
	
	НачатьТранзакцию();
	
	Если ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(РеквизитыЗаказа.Контрагент) Тогда		
		ВсеОк = СформироватьПеремещениеТоваров(ДокументРасход, РеквизитыЗаказа); 
	Иначе
		ВсеОк = СформироватьРеализациюТоваров(ДокументРасход, РеквизитыЗаказа);
	КонецЕсли;
		
	Если ВсеОК Тогда 
		ЗаполнитьДанные_ЗапрещеннымиДанными();
		ВсеОк = ЗаписатьДанныеСканирования() И ЗаписатьЗапрещенныеДанные(); 	
	КонецЕсли;
	
	Если ВсеОк Тогда 
		ЗафиксироватьТранзакцию();		
			
		Если КонтрольВремени Тогда 
			ЗавершитьКонтроль();
		КонецЕсли;
		
		ЗакончитьСборкуРезерва(); // Vov41k 12.12.2023
		
		текСтатус = "ПечатьНакладной";
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
		
	ЗаписатьЛог();
	
КонецПроцедуры

&НаСервере
Функция СформироватьПеремещениеТоваров(Документ, РеквизитыОснование)
	
	ВсеОк = Истина;
	текДействие = "Формирование перемещения";
	ВидПодчиненного = "ПеремещениеТоваров";
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документа перемещение");
	
	тчТовары = ПолучитьТаблицуТоваровПоЗаказу(РеквизитыОснование.Ссылка);
	
	Если тчТовары.Количество() = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Нет заполненной номенклатуры для создания отгрузки по заказу", Строка(РеквизитыОснование.Ссылка));
		Документ = Неопределено;
		Возврат ВсеОк;
	КонецЕсли;	
	
	ВсеОк = ПереместитьНоменклатуруИзМестРазмещения(РеквизитыОснование, тчТовары);
	
	Если Не ВсеОк Тогда 		
		Документ = Неопределено;
		Возврат ВсеОк;
	КонецЕсли;
	
	
	ДокПеремещение = Документы.ПеремещениеТоваров.СоздатьДокумент();
	ДокПеремещение.Дата = ТекущаяДата();
	ДокПеремещение.Автор = Пользователи.ТекущийПользователь();
	ДокПеремещение.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
	ДокПеремещение.ДокументОснование = РеквизитыОснование.Ссылка;
	
	СписокСвойств = "РегламентированныйУчет, Организация, ПодразделениеКомпании, ТорговаяПлощадка, ТорговаяПлощадкаПолучатель, СкладКомпании, ВалютаДокумента, КурсДокумента";
	ЗаполнитьЗначенияСвойств(ДокПеремещение, РеквизитыОснование, СписокСвойств);	
	ДокПеремещение.СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ДокПеремещение.ТорговаяПлощадкаПолучатель);
	ДокПеремещение.ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(ДокПеремещение.СкладКомпанииПолучатель);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");	
	СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокПеремещение)));//РеквизитыОснование.Ссылка)));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ЦеныБезХарактеристики", Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ДокПеремещение.СкладКомпанииПолучатель, ДокПеремещение.Дата));		
	
	//Для Каждого стрТовар Из Объект.Товары Цикл 		
	//	Если стрТовар.КоличествоПроверено = 0 
	//		ИЛИ (ЭтоОбъединенныйРезерв И стрТовар.ЗаказПокупателя <> РеквизитыОснование.Ссылка) Тогда 
	//		
	//		Продолжить;
	//	КонецЕсли;
	//			
	//	новСтрока = ДокПеремещение.Товары.Добавить();		
	//	ЗаполнитьЗначенияСвойств(новСтрока, стрТовар, "Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения");
	//	новСтрока.Количество = стрТовар.КоличествоПроверено;
	//	
	//	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(новСтрока, СтруктураДействий, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения());	
	//КонецЦикла;
	
	ДокПеремещение.Товары.Загрузить(тчТовары);
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ДокПеремещение.Товары, СтруктураДействий, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения());
	
	ДокПеремещение.СуммаДокумента = ДокПеремещение.Товары.Итог("СуммаВсего");			
	
	ДокПеремещение.УстановитьНовыйНомер();
	
	Если ПолучитьФункциональнуюОпцию("КонтролерНеПроводитПеремещение") Тогда
		режЗаписи = РежимЗаписиДокумента.Запись;
		режПроведения = РежимПроведенияДокумента.Неоперативный;

	Иначе
		режЗаписи = РежимЗаписиДокумента.Проведение;
		режПроведения = РежимПроведенияДокумента.Оперативный;
	КонецЕсли;
	
	СтруктураСостояния = Новый Структура("Состояние,ДатаПоследнегоИзменения,АвторИзменения,ТорговаяПлощадка",
		Перечисления.СтатусыСборкиРезервов.Отгружен,ТекущаяДатаСеанса(),Справочники.ФизическиеЛица.ПустаяСсылка(),ДокПеремещение.ТорговаяПлощадка);

	Попытка
		ДокПеремещение.Записать(режЗаписи, режПроведения);
		ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ ""Перемещение в филал""", Строка(ДокПеремещение.Ссылка));
		
		   //Изменить состояние документа
		РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(ДокПеремещение.Ссылка,СтруктураСостояния);
		РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(дНакладная,СтруктураСостояния);
		РегистрыСведений.ТК_СостояниеДокументовИзменения.ЗаписатьСостояниеДокумента(ДокПеремещение.Ссылка,СтруктураСостояния);
		Если ЗначениеЗаполнено(ДокПеремещение.ДокументОснование) И ТипЗнч(ДокПеремещение.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			//Изменить состояние заказа
			РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(ДокПеремещение.ДокументОснование,СтруктураСостояния);
			
		КонецЕсли;

		
	Исключение
		ВсеОк = Ложь;
		//ДокПеремещение.Записать(РежимЗаписиДокумента.Запись);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка проведения документа ""Перемещение в филал"". Ошибки: " + ОписаниеОшибки()); 
	КонецПопытки;
		
	Если ВсеОк Тогда 			
		ДокПриход = Документы.ПеремещениеТоваров.СоздатьДокумент();			
		ДокПриход.Заполнить(ДокПеремещение.Ссылка);			
		ДокПриход.Дата = ТекущаяДата();

		Попытка			
			ДокПриход.Записать(РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Оперативный);
			ДобавитьЛог(ЛогДействий, текДействие, "Записан документ ""Перемещение из филиала""", Строка(ДокПриход.Ссылка)); 
			 //Изменить состояние документа
			РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(ДокПриход.Ссылка,СтруктураСостояния);
			РегистрыСведений.ТК_СостояниеДокументовИзменения.ЗаписатьСостояниеДокумента(ДокПриход.Ссылка,СтруктураСостояния);

		Исключение
			ВсеОк = Ложь;
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка записи перемещения из филиала: " + ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;		
	КонецЕсли;
	
	Если ВсеОк Тогда 
		Документ = ДокПеремещение.Ссылка;
	КонецЕсли;
	
	Возврат ВсеОк;
КонецФункции

&НаСервере
Функция СформироватьРеализациюТоваров(Документ, РеквизитыОснование)	
	
	ВсеОк = Истина;
	текДействие = "Формирование реализации";
	ВидПодчиненного = "РеализацияТоваров";
	
	ДобавитьЛог(ЛогДействий, текДействие, "Идет процесс заполнения документа реализации");
	
	тчТовары = ПолучитьТаблицуТоваровПоЗаказу(РеквизитыОснование.Ссылка);
	
	Если тчТовары.Количество() = 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Нет заполненной номенклатуры для создания отгрузки по заказу", Строка(РеквизитыОснование.Ссылка));
		Документ = Неопределено;
		Возврат ВсеОк;
	КонецЕсли;

	ВсеОк = ПереместитьНоменклатуруИзМестРазмещения(РеквизитыОснование, тчТовары);
	
	Если Не ВсеОк Тогда 		
		Документ = Неопределено;
		Возврат ВсеОк;
	КонецЕсли;
	
	Попытка
		ДокРеализация = Документы.РеализацияТоваров.СоздатьДокумент();
		ДокРеализация.Дата = ТекущаяДата();
		ДокРеализация.Автор = Пользователи.ТекущийПользователь();
		ДокРеализация.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.РеализацияТоваров");
		ДокРеализация.ДокументОснование = РеквизитыОснование.Ссылка;
		
		СписокСвойств = "РегламентированныйУчет, Организация, ПодразделениеКомпании, ТорговаяПлощадка, СкладКомпании, Контрагент, ДоговорВзаиморасчетов, ВалютаДокумента, КурсДокумента, ТочкаДоставки";
		ЗаполнитьЗначенияСвойств(ДокРеализация, РеквизитыОснование, СписокСвойств);	
	//	ДокРеализация.ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(ДокРеализация.СкладКомпании);
		//
		//СтруктураДействий = Новый Структура;
		//СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");	
		//СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокРеализация))); //РеквизитыОснование.Ссылка)));
		//СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		//СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		//СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		//СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		//СтруктураДействий.Вставить("ПересчитатьСумму");
		
		
		Для Каждого стрТовар Из тчТовары Цикл 	
			новСтрока = ДокРеализация.Товары.Добавить();		
			ЗаполнитьЗначенияСвойств(новСтрока, стрТовар, "Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество");
			//новСтрока.Количество = стрТовар.КоличествоПроверено;			
			//ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(новСтрока, СтруктураДействий, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения());	
		КонецЦикла;
		
		
		тзТовары = ДокРеализация.Товары.Выгрузить();
		тзТовары.Колонки.Добавить("ХарактеристикиИспользуются");
		тзТовары.Колонки.Добавить("ГруппаЦенообразования");
		тзТовары.Колонки.Добавить("ТипЦен");

		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",ДокРеализация.Организация,ДокРеализация.ТорговаяПлощадка,"Реализация"));
		СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокРеализация))); //РеквизитыОснование.Ссылка)));
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСумму");

		///Цены из договора по группе ценообразования
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",ДокРеализация.РегламентированныйУчет);

		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(ДокРеализация));
		
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(тзТовары,СтруктураДействий,Неопределено);
	
		ДокРеализация.ТипЦен = тзТовары[0].ТипЦен;
		ДокРеализация.Товары.Загрузить(тзТовары);
		
		ОбработкаТабличнойЧастиСервер.СортироватьТЧ(ДокРеализация.Товары);
			
		ДокРеализация.СуммаДокумента = ДокРеализация.Товары.Итог("СуммаВсего");
		
		
		ДокРеализация.УстановитьНовыйНомер();
		
		ВсеОк = ДокРеализация.ПроверитьЗаполнение();
		Если ВсеОк Тогда 		
			Попытка
				ДокРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);		
				ДобавитьЛог(ЛогДействий, текДействие, "Проведен документ расхода", Строка(ДокРеализация.Ссылка)); 
				
				Документ = ДокРеализация.Ссылка;
				
				СтруктураСостояния = Новый Структура("Состояние,ДатаПоследнегоИзменения,АвторИзменения,ТорговаяПлощадка",
					Перечисления.СтатусыСборкиРезервов.Проведен,ТекущаяДатаСеанса(),Справочники.ФизическиеЛица.ПустаяСсылка(),ДокРеализация.ТорговаяПлощадка);
				РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(дНакладная,СтруктураСостояния);

			Исключение
				ВсеОк = Ложь;
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
				ДобавитьЛог(ЛогДействий, текДействие, "Ошибка проведения документа ""Реализация товаров"". Ошибки: " + ОписаниеОшибки()); 
			КонецПопытки;
		Иначе			
			ДобавитьЛог(ЛогДействий, текДействие, "Документ реализация не может быть проведен. Не заполнены основные реквизиты");	
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Документ реализация не может быть проведен. Не заполнены основные реквизиты'; uk = 'Документ реалізації не може бути проведений. Не заповнені головні реквізити'"));
		КонецЕсли;
	Исключение
		ВсеОк = Ложь;		
		Ошибки = ОписаниеОшибки();
		
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка проведения реализации: " + Ошибки);
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Возникла ошибка при проведении документа Реализация товаров. Ошибка: %1'; uk = 'Виникла помилка при проведенні документу Реалізація товарів. Помилка: %1'"),
				Ошибки));
	КонецПопытки;
	
	Возврат ВсеОк;

КонецФункции


&НаКлиенте
Процедура Остатки(Команда)

	////
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура);
	ПараметрыФормы.Отбор.Вставить("Период", КонецДня(ТекущаяДата()));
	//ПараметрыФормы.Отбор.Вставить("СкладКомпании", ПоулчитьМассивСкладовКонтролера());

	//
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ОстаткиКонтролер");
	
	ОткрытьФорму(
		"Отчет.ОстаткиТоваров.Форма", 
		ПараметрыФормы, 
		);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетДвижениеТоваров(Команда)

	ТекДата = ТекущаяДата();
	
	//Период = Новый СтандартныйПериод;
	//Период.ДатаНачала = НачалоДня(ТекДата - 7*60*60*24);
	//Период.ДатаОкончания = КонецДня(ТекДата);
	//
	ПараметрыФормы = Новый Структура; //("Отбор", Новый Структура);
	//ПараметрыФормы.Отбор.Вставить("Период", Период);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Ложь);
	ПараметрыФормы.Вставить("КлючВарианта", "ДвижениеПоСкладамКонтролер");
	
	ОткрытьФорму(
		"Отчет.ОстаткиИОборотыТоваров.Форма", 
		ПараметрыФормы, 
		);	
		
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОтгрузкиСклада(Команда)
	
	ВариантОтчета = ПолучитьВариантОтчетаОтгрузкиСклада();
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, ВариантОтчета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВариантОтчетаОтгрузкиСклада()
	
	ВариантОтчета = Неопределено;
	
	Попытка
		Отчет = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", "ОтчетОтгрузкиСклада_Контролер");
		Если Отчет <> Неопределено И Не Отчет.Пустая() Тогда
			ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, "ОтгрузкиСклада_Контролер");				
		КонецЕсли;
	Исключение
		//Значит нету прав на запуск
	КонецПопытки;
	
	Возврат ВариантОтчета;
КонецФункции

&НаКлиенте
Процедура ОтчетОстаткиСРезервом(Команда)
	
	ВариантОтчета = ПолучитьВариантОтчетаОстаткиСРезервом();
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, ВариантОтчета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВариантОтчетаОстаткиСРезервом()
	
	ВариантОтчета = Неопределено;
	
	Попытка
		Отчет = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", "ОстаткиКонтроллер");
		Если Отчет <> Неопределено И Не Отчет.Пустая() Тогда
			ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, "5e026993-67d6-4b6a-a243-7781b9265918");				
		КонецЕсли;
	Исключение
		//Значит нету прав на запуск
	КонецПопытки;
	
	Возврат ВариантОтчета;
КонецФункции

&НаКлиенте
Процедура ОтчетОчередьСборки(Команда)
	ВариантОтчета = ПолучитьВариантОтчетаОчередьСборки();
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, ВариантОтчета);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВариантОтчетаОчередьСборки()
	
	ВариантОтчета = Неопределено;
	
	Попытка
		Отчет = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", "ОчередьСборки");
		Если Отчет <> Неопределено И Не Отчет.Пустая() Тогда
			ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, "Основной");				
		КонецЕсли;
	Исключение
		//Значит нету прав на запуск
	КонецПопытки;
	
	Возврат ВариантОтчета;
КонецФункции


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСтраницуПоСтатусу()
	
	МассивВсехЭлементов = Новый Массив;
	МассивВидимыхЭлементов = Новый Массив;
	
	ИзменитьДоступностьСтраниц = Ложь;
	ИмяТекущейСтраницы = "";
	
	стрСканировки 			= Элементы.СтраницаСканировки;
	стрНакладнаяНайдена 	= Элементы.СтраницаНакладнаяНайдена;
	стрПроверкаКоличество 	= Элементы.СтраницаПроверкаКоличество;
	стрВыборМРЦ 			= Элементы.СтраницаВыбораМРЦ;
	стрПроверкаНакладной	= Элементы.СтраницаПроверка;
	стрПечать				= Элементы.СтраницаПечать;
	
	Если текСтатус = "ОжиданиеСканирования" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрСканировки Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрСканировки;
			
			ИмяТекущейСтраницы = стрСканировки.Имя;
			ИзменитьДоступностьСтраниц = Истина;
			ОчиститьДанныеДокумента();
		КонецЕсли;
	ИначеЕсли текСтатус = "НакладнаяНайдена" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрНакладнаяНайдена Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрНакладнаяНайдена;
			
			ИмяТекущейСтраницы = стрНакладнаяНайдена.Имя;
			ИзменитьДоступностьСтраниц = Истина;
			
			МассивВсехЭлементов.Добавить("ТоварыЗаказПокупателя");
			
			Если ЭтоОбъединенныйРезерв Тогда 
				МассивВидимыхЭлементов.Добавить("ТоварыЗаказПокупателя");
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли текСтатус = "ПроверкаКоличества" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрПроверкаКоличество Тогда 
			ОбновитьЭлементыУстановкиКоличества();
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрПроверкаКоличество;
			ЭтаФорма.ТекущийЭлемент = Элементы.фКоличествоПроверено;
			
			ИмяТекущейСтраницы = стрПроверкаКоличество.Имя;
			ИзменитьДоступностьСтраниц = Истина;
		КонецЕсли;
	ИначеЕсли текСтатус = "ВыборМРЦ" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрВыборМРЦ Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрВыборМРЦ;
			
			ИмяТекущейСтраницы = стрВыборМРЦ.Имя;
			ИзменитьДоступностьСтраниц = Истина;
		КонецЕсли;
	ИначеЕсли текСтатус = "ПроверкаНакладной" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрПроверкаНакладной Тогда 
			ОбновитьЭлементыПроверкиНакладной();
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрПроверкаНакладной;
			
			ИмяТекущейСтраницы = стрПроверкаНакладной.Имя;
			ИзменитьДоступностьСтраниц = Истина;
		КонецЕсли;
	ИначеЕсли текСтатус = "ПечатьНакладной" Тогда 
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> стрПечать Тогда 
			
			//ЗапретПечати
			Если ЗапретПечати_Параметры.ЗапрещенаПечать.НайтиПоЗначению(СкладРезервирования) <> Неопределено Тогда 
				ЗапретПечати_Статус = Истина;
			Иначе
				ЗапретПечати_Статус = Ложь;	
			КонецЕсли;			
						
			Элементы.ГруппаСтраницы.ТекущаяСтраница = стрПечать;
			Элементы.Декорация1.Заголовок = НСтр("ru = 'Распечатайте накладную'; uk = 'Роздрукуйте накладну'");
			//Элементы.Печать.Доступность 		 = Ложь;
			//Элементы.ПечатьНаПринтер.Доступность = Ложь;

			ИмяТекущейСтраницы = стрПечать.Имя;
			ИзменитьДоступностьСтраниц = Истина;
			
			ОчиститьДанныеДокумента();
			
			МассивВсехЭлементов.Добавить("ГруппаСвязанныеДокументы");
			МассивВсехЭлементов.Добавить("ГруппаСвязанныеДокументыОбРез");
			
			//ЗапретПечати
			МассивВсехЭлементов.Добавить("Печать");
			МассивВсехЭлементов.Добавить("ПечатьНаПринтер");
			МассивВсехЭлементов.Добавить("Печать1");
			МассивВсехЭлементов.Добавить("ПечатьНаПринтер1");
					
			Если Не ЗапретПечати_Статус Тогда 
				МассивВидимыхЭлементов.Добавить("Печать");
				МассивВидимыхЭлементов.Добавить("ПечатьНаПринтер");
				МассивВидимыхЭлементов.Добавить("Печать1");
				МассивВидимыхЭлементов.Добавить("ПечатьНаПринтер1");
			КонецЕсли;
			
			Если ЭтоОбъединенныйРезерв Тогда 
				МассивВидимыхЭлементов.Добавить("ГруппаСвязанныеДокументыОбРез");
			Иначе
				МассивВидимыхЭлементов.Добавить("ГруппаСвязанныеДокументы");
			КонецЕсли; 
		КонецЕсли;		
	КонецЕсли;

	Если ИзменитьДоступностьСтраниц И Не ПустаяСтрока(ИмяТекущейСтраницы) Тогда
		МассивВсехЭлементов.Добавить("СтраницаСканировки");
		МассивВсехЭлементов.Добавить("СтраницаНакладнаяНайдена");
		МассивВсехЭлементов.Добавить("СтраницаПроверкаКоличество");
		МассивВсехЭлементов.Добавить("СтраницаВыбораМРЦ");
		МассивВсехЭлементов.Добавить("СтраницаПроверка");
		МассивВсехЭлементов.Добавить("СтраницаПечать");
		
		МассивВидимыхЭлементов.Добавить(ИмяТекущейСтраницы);			
	КонецЕсли;
	
	
	Если МассивВидимыхЭлементов.Количество() > 0 Тогда 
		ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехЭлементов, МассивВидимыхЭлементов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныйШК(ТекКод)
	

	ДанныеСканирования = ПроверитьИД(СокрЛП(ТекКод));
	
	Если текСтатус = "ОжиданиеСканирования" ИЛИ текСтатус = "ПечатьНакладной" Тогда 		
		
		ОбработатьПоискНакладной(ДанныеСканирования);		
		ПоказатьОшибкуСканировния();
				
	ИначеЕсли текСтатус = "НакладнаяНайдена" Тогда
		
		ОбработатьПоискТовараКлиент(ДанныеСканирования);
		
	ИначеЕсли текСтатус = "ПроверкаКоличества" Тогда 
		
		ОбработкаСканированиеQR_ФМ_БАТ(ДанныеСканирования);
		
	КонецЕсли;
	
	УстановитьСтраницуПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСканированиеQR_ФМ_БАТ(Знач ДанныеСканирования)
	
	Если текСтруктураШК = Неопределено ИЛИ Не текСтруктураШК.Свойство("ОжиданиеСканированияКраткогоШКБлока") Тогда 
		Возврат;
	КонецЕсли;
		
	текДействие = "Сканирование QR-кода";
	ДобавитьЛог(ЛогДействий, текДействие, "Сканирование на странице проверки количества");

	КраткийШКБлока = СтрДлина(ДанныеСканирования) = 12 ИЛИ СтрДлина(ДанныеСканирования) = 32;	
	
	Если Не КраткийШКБлока Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Просканирован не краткий QR-код", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ДанныеСканирования));
		
		ПоказатьПредупреждение(, НСтр("ru = 'Просканирован неверный штрих-код.
                                       |Ожидается сканирование краткого QR-кода'; uk = 'Відсканований невірний штрих-код.
                                       |Очікується сканування короткого QR-коду'"),, НСтр("ru = 'Ошибка'; uk = 'Помилка'"));
		Возврат;		
	КонецЕсли;
	
	Результат = Новый Структура;
	Если Не ПроверитьШтрихКодБлока(ДанныеСканирования, Результат, текДействие) Тогда 		
		ПоказатьПредупреждение(, Результат.Ошибка,, НСтр("ru = 'Ошибка'; uk = 'Помилка'"));
		Возврат;
	КонецЕсли;

	текСтруктураШК.Вставить("ЭтоШКБлока", Истина);
	текСтруктураШК.Вставить("СтрокаПоиска", ДанныеСканирования); 
	
	текСтруктураШК.Удалить("ОжиданиеСканированияКраткогоШКБлока");	
	
	Элементы.ГруппаQRкодФМ.Видимость = Ложь;
	
	текСтрока = Объект.Товары.НайтиПоИдентификатору(ИндексСтроки);	
	КоличествоПроверено = КоличествоПроверено + Цел(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(текСтруктураШК, "Коэффициент", 1) * 1 / ?(текСтрока.Коэффициент = 0, 1, текСтрока.Коэффициент));
	
	фКоличествоПровереноПриИзменении(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПоискНакладной(тИдДокумента)
		
	УстановитьПривилегированныйРежим(Истина);

	текДействие = "Сканирование документа";
	ШтрихкодФМ = Ложь;
	Попачка = Ложь;
	ЭтоОбъединенныйРезерв = Ложь;
	
	ЗаказыПокупателя.Очистить();
	ДокументыРасход.Очистить();
	
	Если КонтрольВремени Тогда
		Если НЕ ЗначениеЗаполнено(Контроллер) Тогда
			текСотрудник = НайтиСотрудника(тИдДокумента);
			Если НЕ текСотрудник Тогда 
				ОшибкаСканирования = "Отсканируйте штрих-код контроллера!!!";
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьЛог(ЛогДействий, текДействие, "Сканирование на странице поиска накладной", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
	
	Если Не СтрДлина(тИдДокумента) = 36 Тогда 
		Если Найти(тИдДокумента, "*") > 0 Тогда 
			ШтрихкодФМ = Истина;
		Иначе
			ДобавитьЛог(ЛогДействий, текДействие, "Не корректный ID накладной", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
			
			ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Не корректний ID накладной <%1>'; uk = 'Некоректний ID накладної <%1>'"),
									СокрЛП(тИдДокумента));
			Возврат;
		КонецЕсли;		
	КонецЕсли;
	
	Если ШтрихкодФМ Тогда 		
		СтруктураШКФМ = ОбщегоНазначенияТККлиентСервер.Парсить(тИдДокумента, "*");
		
		Попытка
			сНомерНакладной = СтруктураШКФМ.Парам1;			
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			               |	РезервированиеЗаказовПокупателя.Ссылка КАК Ссылка,
			               |	РезервированиеЗаказовПокупателя.Контрагент.КодWERA КАК КодКонтрагентаФМ
			               |ИЗ
			               |	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
			               |ГДЕ
			               |	РезервированиеЗаказовПокупателя.Номер = &Номер
			               |	И РезервированиеЗаказовПокупателя.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
			
			Запрос.УстановитьПараметр("КонецПериода", КонецДня(ТекущаяДата()));
			Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоДня(ТекущаяДата()),-1));
			Запрос.УстановитьПараметр("Номер", сНомерНакладной);
			
			Выборка = Запрос.Выполнить().Выбрать();		
			
			Если Выборка.Следующий() И Выборка.Количество() = 1 И Выборка.КодКонтрагентаФМ = Число(СтруктураШКФМ.Парам2) Тогда 
				ДокСсылка = Выборка.Ссылка;
			Иначе
				ДобавитьЛог(ЛогДействий, текДействие, "Ошибка поиска накладной по ШК ФМ", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
				Возврат;								
			КонецЕсли;			
		Исключение
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка поиска накладной по ШК ФМ:" + ОписаниеОшибки(), ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(тИдДокумента));
			Возврат;
		КонецПопытки;
	Иначе				
		новИдДокумента = ПроверитьИД(тИдДокумента);
		Попытка
			ГУИДДокумента = Новый УникальныйИдентификатор(новИдДокумента);
		Исключение
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка поиска накладной по ИД", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));			
			Возврат;
		КонецПопытки;
		
		ДокСсылка = Документы.РезервированиеЗаказовПокупателя.ПолучитьСсылку(ГУИДДокумента);
		Если Не ОбщегоНазначения.СсылкаСуществует(ДокСсылка) Тогда 
			ДобавитьЛог(ЛогДействий, текДействие, "Документ Резервирование не найден", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	  
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РезервированиеЗаказовПокупателя.Ссылка КАК Ссылка,
	               |	РезервированиеЗаказовПокупателя.Дата КАК Дата,
	               |	РезервированиеЗаказовПокупателя.Номер КАК Номер,
	               |	РезервированиеЗаказовПокупателя.Проведен КАК Проведен,
	               |	РезервированиеЗаказовПокупателя.ПометкаУдаления КАК ПометкаУдаления,
	               |	РезервированиеЗаказовПокупателя.Контрагент КАК Контрагент,
	               |	РезервированиеЗаказовПокупателя.ДокументОснование КАК ДокументОснование,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки.ПорядокСигаретный КАК ПорядокСигаретный,
	               |	РезервированиеЗаказовПокупателя.Представление КАК Представление,
	               |	РезервированиеЗаказовПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РезервированиеЗаказовПокупателя.СкладКомпании КАК СкладКомпании
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателя.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	               |	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество,
	               |	ВЫБОР
	               |		КОГДА &ИспользуютсяОбъединенныеРезервы
	               |			ТОГДА РезервированиеЗаказовПокупателяТовары.ЗаказПокупателя
	               |		ИНАЧЕ РезервированиеЗаказовПокупателяТовары.Ссылка.ДокументОснование
	               |	КОНЕЦ КАК ЗаказПокупателя,
	               |	ВЫБОР
	               |		КОГДА &ИспользуютсяОбъединенныеРезервы
	               |			ТОГДА РезервированиеЗаказовПокупателяТовары.ЗаказПокупателя.Организация
	               |		ИНАЧЕ РезервированиеЗаказовПокупателяТовары.Ссылка.ДокументОснование.Организация
	               |	КОНЕЦ КАК Организация,
	               |	ВЫБОР
	               |		КОГДА РезервированиеЗаказовПокупателяТовары.МестоРазмещения <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
	               |			ТОГДА РезервированиеЗаказовПокупателяТовары.МестоРазмещения
	               |		ИНАЧЕ РезервированиеЗаказовПокупателяТовары.Ссылка.СкладКомпании
	               |	КОНЕЦ КАК МестоРазмещения
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.Документ КАК Документ,
	               |	ВложенныйЗапрос.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЕСТЬNULL(ДополнительныеСведения.Значение, ЛОЖЬ) КАК Попачка
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ,
	               |		РезервированиеЗаказовПокупателя.ДокументОснование.РегламентированныйУчет КАК РегламентированныйУчет
	               |	ИЗ
	               |		Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	               |	ГДЕ
	               |		РезервированиеЗаказовПокупателя.Ссылка = &Ссылка
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя,
	               |		РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя.РегламентированныйУчет
	               |	ИЗ
	               |		Документ.РезервированиеЗаказовПокупателя.РаспределениеОтгрузки КАК РезервированиеЗаказовПокупателяРаспределениеОтгрузки
	               |	ГДЕ
	               |		РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка = &Ссылка
	               |		И &ИспользуютсяОбъединенныеРезервы) КАК ВложенныйЗапрос
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ПО ВложенныйЗапрос.Документ = ДополнительныеСведения.Объект
	               |			И (ДополнительныеСведения.Свойство.Имя = ""Попачечно_b9a6d6a63dc74ae1a8f7d6a75ea25654"")
	               |ГДЕ
	               |	НЕ ВложенныйЗапрос.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	               |	И НЕ ВложенныйЗапрос.Документ ЕСТЬ NULL
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Документ,
	               |	ВложенныйЗапрос.РегламентированныйУчет,
	               |	ЕСТЬNULL(ДополнительныеСведения.Значение, ЛОЖЬ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ТК_ДокументыВОбработке.Документ КАК Документ,
	               |	ТК_ДокументыВОбработке.ДатаВремя КАК ДатаВремя,
	               |	ТК_ДокументыВОбработке.Пользователь КАК Пользователь
	               |ИЗ
	               |	РегистрСведений.ТК_ДокументыВОбработке КАК ТК_ДокументыВОбработке
	               |ГДЕ
	               |	ТК_ДокументыВОбработке.Документ = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокСсылка);
	Запрос.УстановитьПараметр("ИспользуютсяОбъединенныеРезервы", ИспользуютсяОбъединенныеРезервы);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РеквизитыРезерва = МассивРезультатов[0].Выбрать();
	тчТовары = МассивРезультатов[1].Выгрузить();
	тзЗаказы = МассивРезультатов[2].Выгрузить();
	
	вбИспользование = МассивРезультатов[3].Выбрать(); //Vov41k 12.12.2023
	
	Если Не РеквизитыРезерва.Следующий() Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Не удалось получить реквизиты Документа резервирования запросом", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));
		Возврат;
	КонецЕсли;
	
	Если вбИспользование.Следующий() Тогда 
		
		Если вбИспользование.Пользователь <> Пользователи.ТекущийПользователь() Тогда 
			ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Документ ""%1"" уже обрабатывается пользователем %2 с %3'; uk = 'Документ ""%1"" вже оброблюється користувачем %2 з %3'"),
									ДокСсылка,
									вбИспользование.Пользователь,
									вбИспользование.ДатаВремя);
			
			ДобавитьЛог(ЛогДействий, текДействие, ОшибкаСканирования, Строка(ДокСсылка));			
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если РеквизитыРезерва.ПометкаУдаления Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Документ Резервирование помечен на удаление", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Накладная: %1 помечена на удаление!'; uk = 'Накладна: %1 помічена на видалення!'"),
								РеквизитыРезерва.Представление);
		Возврат;
	КонецЕсли;
	
	Если тзЗаказы.Количество() = 0 Тогда
		ДобавитьЛог(ЛогДействий, текДействие, "Для документа " + ДокСсылка + " не найдены связанные заказы", Строка(ДокСсылка));
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Для документа ""%1"" не найдены связанные заказы'; uk = 'Для документу ""%1"" не знайдені зв''язані замовлення'"),
						РеквизитыРезерва.Представление);

		Возврат;	
	КонецЕсли;
	
	ЗаказыПокупателя.Загрузить(тзЗаказы);
	МассивЗаказов = тзЗаказы.ВыгрузитьКолонку("Документ");
	
	Если ИспользуютсяОбъединенныеРезервы И МассивЗаказов.Количество() > 1 Тогда
		ЭтоОбъединенныйРезерв = Истина;
	Иначе
		ЭтоОбъединенныйРезерв = Ложь;
		ЗаказПокупателя = МассивЗаказов[0];		
	КонецЕсли;
	
	Попачка = тзЗаказы[0].Попачка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Проведен
	               |	И РеализацияТоваров.ДокументОснование В(&МассивЗаказов)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПеремещениеТоваров.Ссылка
	               |ИЗ
	               |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	               |ГДЕ
	               |	ПеремещениеТоваров.Проведен
	               |	И ПеремещениеТоваров.ДокументОснование В(&МассивЗаказов)";
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда		
		ДобавитьЛог(ЛогДействий, текДействие, "Документ уже отгружен", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));		
		
		ОшибкаСканирования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Накладная: %1 Отгружена!'; uk = 'Накладна: %1 Відвантажена!'"),
								РеквизитыРезерва.Представление);
		Возврат;		
	КонецЕсли;

	ДобавитьЛог(ЛогДействий, текДействие, "Начало проверки документа", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(новИдДокумента));	
	
	Объект.Товары.Загрузить(тчТовары);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(тчТовары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	тчТовары.Количество КАК Количество,
	               |	тчТовары.Коэффициент КАК Коэффициент
	               |ПОМЕСТИТЬ вт_Товары
	               |ИЗ
	               |	&тчТовары КАК тчТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	вт_Товары.Количество КАК Количество,
	               |	вт_Товары.Коэффициент КАК Коэффициент,
	               |	ВЫБОР
	               |		КОГДА ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмеренияУпаковки.Коэффициент > вт_Товары.Количество * вт_Товары.Коэффициент
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Попачка
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	вт_Товары КАК вт_Товары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	               |	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	               |	ШтрихкодыНоменклатуры.Упаковка КАК ЕдиницаИзмерения,
	               |	ШтрихкодыНоменклатуры.Упаковка.Коэффициент КАК Коэффициент,
	               |	ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаФМ) КАК ТоварФМ,
	               |	ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаБАТ) КАК ТоварБАТ,
	               |	ВЫБОР
	               |		КОГДА ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаФМ)
	               |			ТОГДА ""PMU""
	               |		КОГДА ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаБАТ)
	               |			ТОГДА ""BAT""
	               |		КОГДА ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаJTI)
	               |			ТОГДА ""JTI""
	               |		ИНАЧЕ """"
	               |	КОНЕЦ КАК Производитель
	               |ПОМЕСТИТЬ ВТ_ШтрихКоды
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	ШтрихкодыНоменклатуры.Номенклатура В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				втТовары.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				втТовары КАК втТовары)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ИдентификаторыITGНоменклатуры.Номенклатура,
	               |	ИдентификаторыITGНоменклатуры.Код,
	               |	ИдентификаторыITGНоменклатуры.Номенклатура.ЕдиницаИзмеренияУпаковки,
	               |	ИдентификаторыITGНоменклатуры.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент,
	               |	ВЫРАЗИТЬ(ИдентификаторыITGНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаФМ),
	               |	ВЫРАЗИТЬ(ИдентификаторыITGНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаБАТ),
	               |	ВЫБОР
	               |		КОГДА ВЫРАЗИТЬ(ИдентификаторыITGНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаФМ)
	               |			ТОГДА ""PMU""
	               |		КОГДА ВЫРАЗИТЬ(ИдентификаторыITGНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаБАТ)
	               |			ТОГДА ""BAT""
	               |		КОГДА ВЫРАЗИТЬ(ИдентификаторыITGНоменклатуры.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМаркаJTI)
	               |			ТОГДА ""JTI""
	               |		ИНАЧЕ """"
	               |	КОНЕЦ
	               |ИЗ
	               |	РегистрСведений.ИдентификаторыITGНоменклатуры КАК ИдентификаторыITGНоменклатуры
	               |ГДЕ
	               |	ИдентификаторыITGНоменклатуры.Номенклатура В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				втТовары.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				втТовары КАК втТовары)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ЕСТЬNULL(ДополнительныеСведения.Значение, ЛОЖЬ) КАК ИсключениеФМ
	               |ПОМЕСТИТЬ Исключения
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
	               |	И ДополнительныеСведения.Объект В
	               |			(ВЫБРАТЬ
	               |				ВТ_ШтрихКоды.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				ВТ_ШтрихКоды КАК ВТ_ШтрихКоды
	               |			ГДЕ
	               |				(ВТ_ШтрихКоды.Производитель = ""PMU""
	               |					ИЛИ ВТ_ШтрихКоды.Производитель = ""BAT"")
	               |			СГРУППИРОВАТЬ ПО
	               |				ВТ_ШтрихКоды.Номенклатура)
	               |	И ДополнительныеСведения.Свойство.Имя = ""ИсключенияФМ""
	               |	И ДополнительныеСведения.Значение = ИСТИНА
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ИСТИНА КАК КраткийКодБлока
	               |ПОМЕСТИТЬ КраткийКод
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
	               |	И ДополнительныеСведения.Объект В
	               |			(ВЫБРАТЬ
	               |				ВТ_ШтрихКоды.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				ВТ_ШтрихКоды КАК ВТ_ШтрихКоды
	               |			ГДЕ
	               |				(ВТ_ШтрихКоды.Производитель = ""PMU""
	               |					ИЛИ ВТ_ШтрихКоды.Производитель = ""BAT"")
	               |			СГРУППИРОВАТЬ ПО
	               |				ВТ_ШтрихКоды.Номенклатура)
	               |	И ДополнительныеСведения.Свойство.Имя = ""КраткийQRКодБлока""
	               |	И ДополнительныеСведения.Значение = ИСТИНА
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ШтрихКоды.Штрихкод КАК Штрихкод,
	               |	ВТ_ШтрихКоды.Номенклатура КАК Номенклатура,
	               |	ВТ_ШтрихКоды.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВТ_ШтрихКоды.Коэффициент КАК Коэффициент,
	               |	ЕСТЬNULL(КраткийКод.КраткийКодБлока, ЛОЖЬ) КАК КраткийКодБлока,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(Исключения.ИсключениеФМ, ЛОЖЬ) = ЛОЖЬ
	               |			ТОГДА ВЫБОР
	               |					КОГДА втТовары.Попачка
	               |						ТОГДА """"
	               |					ИНАЧЕ ВТ_ШтрихКоды.Производитель
	               |				КОНЕЦ
	               |		ИНАЧЕ """"
	               |	КОНЕЦ КАК Производитель
	               |ИЗ
	               |	ВТ_ШтрихКоды КАК ВТ_ШтрихКоды
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Исключения КАК Исключения
	               |		ПО ВТ_ШтрихКоды.Номенклатура = Исключения.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КраткийКод КАК КраткийКод
	               |		ПО ВТ_ШтрихКоды.Номенклатура = КраткийКод.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втТовары КАК втТовары
	               |		ПО ВТ_ШтрихКоды.Номенклатура = втТовары.Номенклатура";
	
	//Запрос.УстановитьПараметр("МассивНоменклатуры", тчТовары.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("тчТовары", тчТовары);
	Запрос.УстановитьПараметр("ТоварнаяМаркаФМ", Справочники.ТоварныеМарки.НайтиПоКоду("Т80"));		// группа товарных марок Филип Моррис
	Запрос.УстановитьПараметр("ТоварнаяМаркаБАТ", Справочники.ТоварныеМарки.НайтиПоКоду("Т10"));	// группа товарных марок Бритиш Американ Тобакко
	Запрос.УстановитьПараметр("ТоварнаяМаркаJTI", Справочники.ТоварныеМарки.НайтиПоКоду("Т20"));	// группа товарных марок Бритиш Американ Тобакко
	
	ШКТоваров = Запрос.Выполнить().Выгрузить();		
	КешТоваров.Загрузить(ШКТоваров);
	
	Если КонтрольВремени Тогда 
		Собрана = ПроверитьСборкуНакладной(ДокСсылка);
		Если НЕ Собрана Тогда 
			ОшибкаСканирования = "Накладная НЕ собрана!!!";
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Объект.Резерв = ДокСсылка;	
	текСтатус = "НакладнаяНайдена";
	
	ОграничениеОтгрузокФМ = ПроверитьОграничениеОтгрузокФМ(РеквизитыРезерва.ТочкаДоставки);
	Если ОграничениеОтгрузокФМ Тогда 
		КешЗапрещенныхТоваров.Загрузить(ПолучитьЗапрещенныйТовар());
	КонецЕсли;
	
	дНакладная = ДокСсылка;
	дКонтрагент = РеквизитыРезерва.Контрагент;
	дАдресДоставки = РеквизитыРезерва.ТочкаДоставки;
	ТорговаяПлощадка = РеквизитыРезерва.ТорговаяПлощадка;
	СкладРезервирования = РеквизитыРезерва.СкладКомпании;
	
	Если КонтрольВремени Тогда
		ЗаписатьВыдачу();
	КонецЕсли;
	
	НачатьСборкуРезерва(); // Vov41k 12.12.2023
	
	ДопПараметры = Новый Структура;
	
	Попытка
		дМаршрут = Цел(РеквизитыРезерва.ПорядокСигаретный / 100);
	Исключение 
		дМаршрут = 0;	
	КонецПопытки;	
	
	ДобавитьЛог(ЛогДействий, текДействие, "Найдена накладная", РеквизитыРезерва.Представление);		
КонецПроцедуры


#Область ПоискНоменклатуры

&НаКлиенте
Процедура ОбработатьПоискТовараКлиент(Знач ДанныеСканирования)
	
	Результат = ЗаполнитьСтруктуруОтсканированногоТовара(ДанныеСканирования);		
	Результат.Свойство("СтруктураШК", текСтруктураШК);
	
	Если текСтруктураШК <> Неопределено Тогда
		ОбработкаНайденойНоменклатуры(Результат);
	КонецЕсли;
	
	Если Результат.Свойство("Ошибка") Тогда 
		
		ПоказатьПредупреждение(, Результат.Ошибка,, НСтр("ru = 'Ошибка'; uk = 'Помилка'"));
		
	ИначеЕсли Результат.Свойство("ОжиданиеСканированияКраткогоШКБлока") И НайденнаяСтрока <> Неопределено Тогда  			
				
		текСтатус = "ПроверкаКоличества";				
		РучнойВвод = Ложь;
		ИндексСтроки = НайденнаяСтрока;
		
	Иначе
		
		ОбработатьВводСтроки();
		
	КонецЕсли;		

	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьСтруктуруОтсканированногоТовара(Знач СтрокаПоиска)
	
	текДействие = "Сканирование товара";	
	ДобавитьЛог(ЛогДействий, текДействие, "Сканирование на странице поиска товара");			
	
	Результат = Новый Структура;
	
	текШтрихКод = СтрокаПоиска;
	КраткийШКБлока 	= Ложь; //Набойченко М.20555 //СтрДлина(СтрокаПоиска) = 12;
	ЭтоШККороба 	= СтрДлина(СтрокаПоиска) > 60 И Лев(СтрокаПоиска, 3) = "010";
	ЭтоШКБлока 		= Не ЭтоШККороба И (КраткийШКБлока ИЛИ (СтрДлина(СтрокаПоиска) > 16 И (Лев(СтрокаПоиска, 3) = "010" ИЛИ Лев(СтрокаПоиска, 3) = "343")));	//тут
	СтруктураШК 	= Неопределено;
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Получена пустая строка");				
		Результат.Вставить("Ошибка", НСтр("ru = 'Просканирована пустая строка'; uk = 'Проскановано порожню строку'"));		
		Возврат Результат;
	КонецЕсли;	
	
	Если КраткийШКБлока Тогда 		
		ДобавитьЛог(ЛогДействий, текДействие, "Просканирован краткий QR-код", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));		
		Результат.Вставить("Ошибка", НСтр("ru = 'Просканирован краткий QR-код'; uk = 'Просканирован краткий QR-код'")); //Заполнить текст ошибки		
		Возврат Результат;
	КонецЕсли;
	
	Если ЭтоШКБлока ИЛИ ЭтоШККороба Тогда 		
		//Проверим штрих-код на повторное сканирование		
		Если Не ПроверитьШтрихКодБлока(СтрокаПоиска, Результат, текДействие) Тогда 
			Возврат Результат;
		КонецЕсли;		
		
		//тут
		Если Лев(СтрокаПоиска, 3) = "010" Тогда
			текШтрихКод = Сред(СтрокаПоиска, 4, 13);	//было
		Иначе
			текШтрихКод = Сред(СтрокаПоиска, 4, 8);
			Пока Лев(текШтрихКод, 1) = "0" Цикл
				текШтрихКод = Прав(текШтрихКод,СтрДлина(текШтрихКод) - 1);
			КонецЦикла;
		КонецЕсли;	
		//тут	
	КонецЕсли;	
	
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(текШтрихКод) Тогда					
		
		СтруктураШК = ЗаполнитьСтруктуруШКИзКеша(текШтрихКод);
		
	КонецЕсли;
	
	
	Если СтруктураШК <> Неопределено Тогда 			
		
		СтруктураШК.Вставить("ЭтоШКБлока", ЭтоШКБлока ИЛИ ЭтоШККороба);
		СтруктураШК.Вставить("СтрокаПоиска", СтрокаПоиска);
		
		Если СтруктураШК.Производитель = "PMU" Тогда
			
			Если ЭтоШКБлока Тогда 
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован блок ФМ по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));					
				КодМРЦ = Прав(СтрокаПоиска, 11);
				
				Если Не ПустаяСтрока(КодМРЦ) Тогда 
					текМРЦ = ПолучитьМРЦПоКоду(СтруктураШК.Номенклатура, КодМРЦ);
					Если ЗначениеЗаполнено(текМРЦ) Тогда 
						СтруктураШК.Вставить("ХарактеристикаНоменклатуры", текМРЦ);
					КонецЕсли;
				КонецЕсли;	
				
			ИначеЕсли СтруктураШК.КраткийКодБлока Тогда 
				
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован штрих-код блока ФМ. Ожидание сканирования краткого QR-кода", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));
				
				СтруктураШК.Вставить("ОжиданиеСканированияКраткогоШКБлока");
				Результат.Вставить("ОжиданиеСканированияКраткогоШКБлока");
				
			ИначеЕсли Попачка Тогда
				
			Иначе
				ДобавитьЛог(ЛогДействий, текДействие, "Позиция ФМ просканирован штрих-код пачки вместо QR-кода блока!",  ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));
				
				Результат.Вставить("Ошибка", 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для товара ""%1""
                      |Необходимо сканироваь QR-код блока!'; uk = 'Для товару ""%1""
                      |Необхідно сканувати QR-код блоку!'"),
				СтруктураШК.Номенклатура));						
				
				СтруктураШК = Неопределено;
				
				Возврат Результат;																						
			КонецЕсли;
			
		ИначеЕсли СтруктураШК.Производитель = "BAT" Тогда
			
			Если ЭтоШКБлока Тогда 
				
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован блок БАТ по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));					
				КодМРЦ = Прав(СтрокаПоиска, 8);									
				
				Если Не ПустаяСтрока(КодМРЦ) Тогда 
					текМРЦ = ПолучитьМРЦПоКоду(СтруктураШК.Номенклатура, КодМРЦ);
					Если ЗначениеЗаполнено(текМРЦ) Тогда 
						СтруктураШК.Вставить("ХарактеристикаНоменклатуры", текМРЦ);
					КонецЕсли;
				КонецЕсли;	
				
			ИначеЕсли ЭтоШККороба Тогда 
				
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован короб БАТ по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));										
				
				КодМРЦ = "";				
				ПозицияМРЦ = СтрНайти(СтрокаПоиска, "240", НаправлениеПоиска.СНачала, 14) + 3; 
				
				Если ПозицияМРЦ > 3 Тогда 
					КодМРЦ = Сред(СтрокаПоиска, ПозицияМРЦ, 8);
				КонецЕсли;
				
				Если Не ПустаяСтрока(КодМРЦ) Тогда 
					текМРЦ = ПолучитьМРЦПоКоду(СтруктураШК.Номенклатура, КодМРЦ);
					Если ЗначениеЗаполнено(текМРЦ) Тогда 
						СтруктураШК.Вставить("ХарактеристикаНоменклатуры", текМРЦ);
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли СтруктураШК.КраткийКодБлока Тогда 
				
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован штрих-код блока БАТ. Ожидание сканирования краткого QR-кода", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));
				
				СтруктураШК.Вставить("ОжиданиеСканированияКраткогоШКБлока");
				Результат.Вставить("ОжиданиеСканированияКраткогоШКБлока");
			ИначеЕсли Попачка Тогда
	
			Иначе
				ДобавитьЛог(ЛогДействий, текДействие, "Позиция БАТ просканирован штрих-код пачки вместо QR-кода блока!",  ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));
				
				Результат.Вставить("Ошибка", 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для товара ""%1""
                      |Необходимо сканироваь QR-код блока!'; uk = 'Для товару ""%1""
                      |Необхідно сканувати QR-код блоку!'"),
				СтруктураШК.Номенклатура));						
				
				СтруктураШК = Неопределено;
				
				Возврат Результат;																						
			КонецЕсли;			
			
		ИначеЕсли СтруктураШК.Производитель = "JTI" Тогда
			
			Если ЭтоШКБлока Тогда 				
				ДобавитьЛог(ЛогДействий, текДействие, "Отсканирован блок JTI по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));					
				КодМРЦ = Прав(СтрокаПоиска, 8);									
				
				Если Не ПустаяСтрока(КодМРЦ) Тогда 
					текМРЦ = ПолучитьМРЦПоКоду(СтруктураШК.Номенклатура, КодМРЦ);
					Если ЗначениеЗаполнено(текМРЦ) Тогда 
						СтруктураШК.Вставить("ХарактеристикаНоменклатуры", текМРЦ);
					КонецЕсли;
				КонецЕсли;
			Иначе
				ДобавитьЛог(ЛогДействий, текДействие, "Найден товар JTI по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(текШтрихКод));
			КонецЕсли;
			
		Иначе
			ДобавитьЛог(ЛогДействий, текДействие, "Найден товар по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(текШтрихКод));
		КонецЕсли;																
		
		Результат.Вставить("СтруктураШК", СтруктураШК);

	Иначе 		
		
		ДобавитьЛог(ЛогДействий, текДействие, "Не найден товар по штрих-коду", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СтрокаПоиска));
		
		Результат.Вставить("Ошибка", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Номенклатура не найдена с штрих-кодом <%1>'; uk = 'Номенклатура не знайдена із штрих-кодом <%1>'"),
										СтрокаПоиска));	
	КонецЕсли;
	
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаНайденойНоменклатуры(ДопПараметры)
	
	текДействие = "Обработка выбора";
	текНоменклатура = текСтруктураШК.Номенклатура;
	ПредставлениеНоменклатуры = Строка(текНоменклатура);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Номенклатура", текНоменклатура);	
	Если текСтруктураШК.Свойство("ХарактеристикаНоменклатуры") Тогда 
		СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", текСтруктураШК.ХарактеристикаНоменклатуры);
		ПредставлениеНоменклатуры = ПредставлениеНоменклатуры + ", " + текСтруктураШК.ХарактеристикаНоменклатуры;
	КонецЕсли;
	
	нСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	мСвободныеСтроки = Новый Массив;
	
	Для Каждого Стр Из нСтроки Цикл 
		Если Стр.КоличествоПроверено < Стр.Количество Тогда 
			мСвободныеСтроки.Добавить(Стр);
		КонецЕсли;
	КонецЦикла;
	
	колвоСтрок = нСтроки.Количество();
	колвоСвободныхСтрок = мСвободныеСтроки.Количество();
			
	Если колвоСвободныхСтрок = 1 Тогда 	
		
		ДобавитьЛог(ЛогДействий, текДействие, "Найдена одна строка с товаром", ПредставлениеНоменклатуры);		
		НайденнаяСтрока = мСвободныеСтроки[0].ПолучитьИдентификатор();
		
	ИначеЕсли колвоСвободныхСтрок > 1 Тогда  //ОбъединенныеРезервы
		
		ДобавитьЛог(ЛогДействий, текДействие, "Найдено " + колвоСвободныхСтрок + " строк с товаром", ПредставлениеНоменклатуры);
				
		МассивХарактеристик = Новый Массив;
		Для Каждого стрМРЦ Из мСвободныеСтроки Цикл 		
			Если ЗначениеЗаполнено(стрМРЦ.ХарактеристикаНоменклатуры) Тогда 
				МассивХарактеристик.Добавить(стрМРЦ.ХарактеристикаНоменклатуры);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивХарактеристик.Количество() > 1 Тогда 
			МассивХарактеристик = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивХарактеристик);
		КонецЕсли;
		
		Если МассивХарактеристик.Количество() > 1 Тогда
			текСписокМРЦ.Очистить();
			Для Каждого текМРЦ Из МассивХарактеристик Цикл 		
				текСписокМРЦ.Добавить(текМРЦ, Строка(текМРЦ));			
			КонецЦикла;		
			
			текСтатус = "ВыборМРЦ";		
		Иначе
			НайденнаяСтрока = мСвободныеСтроки[0].ПолучитьИдентификатор();	
		КонецЕсли;
		
	Иначе		
		
		Если колвоСтрок = 0 Тогда  
			
			ДобавитьЛог(ЛогДействий, текДействие, "Отсканированная номенклатура отсутствует в накладной", ПредставлениеНоменклатуры);			
			ДопПараметры.Вставить("Ошибка", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Товар: ""%1"" отсутствует в проверяемой накладной!'; uk = 'Товар: ""%1"" відсутній у перевіряємій накладній!'"),
												ПредставлениеНоменклатуры));									
		ИначеЕсли колвоСтрок = 1 Тогда 
			
			текСтрока = нСтроки[0];
			ДобавитьЛог(ЛогДействий, текДействие, "Уже отсканировано "+Строка(текСтрока.КоличествоПроверено)+ " "+Строка(текСтрока.ЕдиницаИзмерения)+" из зарезервированных "+Строка(текСтрока.Количество)+" "+Строка(текСтрока.ЕдиницаИзмерения)+". Номенклатура не добавлена", ПредставлениеНоменклатуры);
			ДопПараметры.Вставить("Ошибка", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'По товару ""%1"" уже отсканировано %2 %3 из зарезервированных %4 %3.
												|Запрещено отгружать больше зарезервированного количества.'; uk = 'По товару ""%1"" вже відскановано %2 %3 із зарезервованих %4 %3.
												|Заборонено відвантажувати більше ніж зарезервовано.'"),
												ПредставлениеНоменклатуры,
												текСтрока.КоличествоПроверено,
												Строка(текСтрока.ЕдиницаИзмерения),
												текСтрока.Количество));				
									
		Иначе	
		
			ДобавитьЛог(ЛогДействий, текДействие, "Найдено " + колвоСтрок + " строк. Все строки отсканированы согласно резерву", ПредставлениеНоменклатуры);	
			ДопПараметры.Вставить("Ошибка", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'По товару ""%1"" найдено %2 строк(а). Все строки отсканированы согласно резерву.
                                                      |Товар не добавлен.'; uk = 'По товару ""%1"" знайдено %2 строк. Всі строки відскановані згідно резерву.
                                                      |Товар не доданий.'"),
												ПредставлениеНоменклатуры,
												колвоСтрок));
												
		КонецЕсли;
	
		текСтруктураШК = Неопределено;											
		текНоменклатура = Неопределено;
		НайденнаяСтрока = Неопределено;

	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьСтруктуруШКИзКеша(Знач ШтрихКод)
	
	Перем нСтрокаТовара;
	
	СтруктураШК = Новый Структура("Номенклатура, ЕдиницаИзмерения, Коэффициент, КраткийКодБлока, Производитель", , , , Ложь, "");	
	
	Попытка
		нСтроки = КешТоваров.НайтиСтроки(Новый Структура("Штрихкод", ШтрихКод));
		Если нСтроки.Количество() > 0 Тогда
			нСтрокаТовара = нСтроки[0];
		КонецЕсли;
	Исключение
		ДобавитьЛог(ЛогДействий, "ПолучитьДанныеИзКэшаТоваров","Ошибка получения данны из КешТоваров", "Ошибка: " + ОписаниеОшибки() + "; Свойство=""Штрх-код"", Значение=""" + Строка(ШтрихКод) + """");
	КонецПопытки;
		
	Если нСтрокаТовара = Неопределено Тогда 
		Результат = ПолучитьДанныеПоТовару(ШтрихКод);
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда 
			ЗаполнитьЗначенияСвойств(СтруктураШК, Результат, "Номенклатура, ЕдиницаИзмерения, Коэффициент");
		Иначе
			СтруктураШК = Неопределено;
		КонецЕсли;
	Иначе
		ЗаполнитьЗначенияСвойств(СтруктураШК, нСтрокаТовара);
	КонецЕсли;
	
	Возврат СтруктураШК;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВводСтроки()
	
	Если НайденнаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Попытка
		нСтрока = Объект.Товары.НайтиПоИдентификатору(НайденнаяСтрока);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки());		
		Возврат;		
	КонецПопытки;
	
	Если нСтрока.Коэффициент = текСтруктураШК.Коэффициент 
		ИЛИ (нСтрока.Коэффициент > текСтруктураШК.Коэффициент И Не (текСтруктураШК.Производитель = "PMU" ИЛИ текСтруктураШК.Производитель = "BAT")) Тогда 
		
		нСтрока.КоличествоПроверено = нСтрока.КоличествоПроверено + 1;
	Иначе 
		КолВоНакл = Цел(текСтруктураШК.Коэффициент / ?(нСтрока.Коэффициент <> 0, нСтрока.Коэффициент, 1));
		Если КолВоНакл < 1 Тогда
			КолВоНакл = 1;
		КонецЕсли;
		
		нСтрока.КоличествоПроверено  = нСтрока.КоличествоПроверено + КолВоНакл;			
	КонецЕсли;
	
	Если нСтрока.КоличествоПроверено > нСтрока.Количество Тогда 
		текСтатус = "ПроверкаКоличества";				
		ИндексСтроки = нСтрока.ПолучитьИдентификатор();		
	Иначе
		текСтатус = "НакладнаяНайдена";
		ЗапомнитьОтсканированныеБлоки(нСтрока.Организация);
	КонецЕсли;
	
	Если текСтатус <> "ПроверкаКоличества" Тогда 
		ПодключитьОбработчикОжидания("АктивироватьНайденнуюСтроку", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьОтсканированныеБлоки(Организация)
	
	Если текСтруктураШК = Неопределено Тогда 			
		Возврат;
	КонецЕсли;			
	
	ЭтоШКБлока = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(текСтруктураШК, "ЭтоШКБлока", Ложь);
	СтрокаПоиска = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(текСтруктураШК, "СтрокаПоиска", "");
	
	Если Не ПустаяСтрока(СтрокаПоиска) И ЭтоШКБлока Тогда 
		Если текСтруктураШК.Производитель = "PMU" Тогда 
			
			Если ОграничениеОтгрузокФМ И КешЗапрещенныхТоваров.НайтиСтроки(Новый Структура("Номенклатура", текСтруктураШК.Номенклатура)).Количество() > 0 Тогда 
				нСтрокаЗапрет = Объект.ЗапрещенныйФМ.Добавить();
				нСтрокаЗапрет.Номенклатура = текСтруктураШК.Номенклатура;
				нСтрокаЗапрет.ШтрихКод = СтрокаПоиска;
			Иначе
				нСтрокаФМ = ШтрихКода.Добавить();
				нСтрокаФМ.ДанныеШтрихКода = СтрокаПоиска;
				нСтрокаФМ.Производитель = "PMU";
				нСтрокаФМ.Организация = Организация;        
			КонецЕсли;	
			
		ИначеЕсли текСтруктураШК.Производитель = "BAT" Тогда 
						
			нСтрокаБАТ = ШтрихКода.Добавить();
			нСтрокаБАТ.ДанныеШтрихКода = СтрокаПоиска;				
			нСтрокаБАТ.Производитель = "BAT";
			нСтрокаБАТ.Организация = Организация;
			
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура АктивироватьНайденнуюСтроку()
	
	Если НайденнаяСтрока <> Неопределено Тогда 
		Если ЭтотОбъект.ТекущийЭлемент <> ЭтотОбъект.Элементы.Товары Тогда 
			ЭтотОбъект.ТекущийЭлемент = ЭтотОбъект.Элементы.Товары;
		КонецЕсли;
		
		ЭтотОбъект.Элементы.Товары.ТекущаяСтрока = НайденнаяСтрока;
		НайденнаяСтрока = Неопределено;
		текНоменклатура = Неопределено;
		текСтруктураШК = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьШтрихКодБлока(Знач ШтрихКод, Результат, текДействие)
	
	//СтруктураОтбора = Новый Структура("ДанныеШтрихКода", ШтрихКод);	
	//Если Объект.ШтрихКодаФМ.НайтиСтроки(СтруктураОтбора).Количество() > 0
	//	ИЛИ Объект.ШтрихКодаБАТ.НайтиСтроки(СтруктураОтбора).Количество() > 0 Тогда 
	
	Если ШтрихКода.НайтиСтроки(Новый Структура("ДанныеШтрихКода", ШтрихКод)).Количество() > 0 Тогда 
		ДобавитьЛог(ЛогДействий, текДействие, "Повторное сканирование", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ШтрихКод));
		
		Результат.Вставить("Ошибка", 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Штрих-код: %1
                      |Уже отсканирован, возьмите другой товар.'; uk = 'Штрих-код: %1
                      |Вже відсканований, візьміть інший товар.'"),
				ШтрихКод));	
		
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ПроверитьУникальностьШтрихКода(ШтрихКод, ТорговаяПлощадка) И НЕ Попачка Тогда 
		
		ДобавитьЛог(ЛогДействий, текДействие, "Повторно просканирован штрих-код блока", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(ШтрихКод));
		
		Результат.Вставить("Ошибка", 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Штрих-код ""%1"" уже сканировался ранее! Возьмите другой блок!'; uk = 'Штрих-код ""%1"" вже сканувався раніше. Візьміть інший блок!'"),
				ШтрихКод));
		
		Возврат Ложь;
	КонецЕсли;
				
	Возврат Истина;		
КонецФункции


#КонецОбласти


#Область Логгирование

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьЛог(ЛогДействий, Действие = "", Информация = "", Данные = "")	

	ЗаписьЛогаСтрока = "";	
	
	СтруктураСтроки = Новый Структура("Действие, Инфо, Данные", Действие, Информация, Данные);
	Для Каждого Стр Из СтруктураСтроки Цикл 
		Если Не ПустаяСтрока(Стр.Значение) Тогда 
			текПараметр = """" + Стр.Ключ + """=>""" + Стр.Значение + """";
			ЗаписьЛогаСтрока = ?(Не ПустаяСтрока(ЗаписьЛогаСтрока), ЗаписьЛогаСтрока + ",", "") + текПараметр;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПустаяСтрока(ЗаписьЛогаСтрока) Тогда 
		ЛогДействий.ДобавитьСтроку(Формат(ТекущаяДата(), "ДФ=yyyy-MM-dd_HH:mm:ss") + "|" + ЗаписьЛогаСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЛог()
	
	УстановитьПривилегированныйРежим(Истина);

	Если ЛогДействий.КоличествоСтрок() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаЗаписиЛога = ТекущаяДата();
	
	НаборЗаписей = РегистрыСведений.ДействияОперацииКонтроля.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДатаРегистрации.Установить(ДатаЗаписиЛога);
	НаборЗаписей.Отбор.Пользователь.Установить(Объект.Комплектовщик);
	НаборЗаписей.Отбор.ДокументПроверки.Установить(Объект.Резерв);
		
	НоваяЗапиь = НаборЗаписей.Добавить();
	НоваяЗапиь.ДатаРегистрации	= ДатаЗаписиЛога;
	НоваяЗапиь.Пользователь     = Объект.Комплектовщик;
	НоваяЗапиь.ДокументПроверки = Объект.Резерв;
	НоваяЗапиь.Действия 		= ЛогДействий.ПолучитьТекст();	
	НаборЗаписей.Записать();
	
	ЛогДействий.Очистить();

КонецПроцедуры


#КонецОбласти


#Область ОбработкаОшибокСканирования

&НаКлиенте
Процедура ПоказатьОшибкуСканировния()		
	Если ПустаяСтрока(ОшибкаСканирования) Тогда 
		Возврат;
	КонецЕсли;	
			
	Элементы.ДекорацияНадписьОшибкаСканирования.Заголовок = ОшибкаСканирования;
	Элементы.ГруппаОшибкиСканирования.Видимость = Истина;
	
	текСтатус = "ОжиданиеСканирования";
	УстановитьСтраницуПоСтатусу();
	
	ПодключитьОбработчикОжидания("ОчиститьОшибкиСканирования", 5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОшибкиСканирования()	
	ГруппаОшибкиСканирования = Элементы.ГруппаОшибкиСканирования;
	
	Если ГруппаОшибкиСканирования.Видимость Тогда 
		ГруппаОшибкиСканирования.Видимость = Ложь;
	КонецЕсли;
	
	ОшибкаСканирования = "";	
КонецПроцедуры

#КонецОбласти


#Область ОбработкаОшибокСканирования

&НаСервере
Процедура НачатьСборкуРезерва()
	
	текДействие = "Контроль использования";
	
	МенеджерЗаписи = РегистрыСведений.ТК_ДокументыВОбработке.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = дНакладная;	
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда 	
		МенеджерЗаписи.Документ 	= дНакладная;
		МенеджерЗаписи.ДатаВремя 	= ТекущаяДатаСеанса();
		МенеджерЗаписи.Пользователь = Пользователи.ТекущийПользователь();	
		
		Попытка
			МенеджерЗаписи.Записать(Истина);
			ДобавитьЛог(ЛогДействий, текДействие, "Начало сборки резерва", Строка(дНакладная));		
		Исключение
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка записи: " + ОписаниеОшибки(), Строка(дНакладная));		
		КонецПопытки;
	Иначе
		ДобавитьЛог(ЛогДействий, текДействие, "Ранее уже начинали собирать накладную", Строка(дНакладная));		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗакончитьСборкуРезерва()
	
	УстановитьПривилегированныйРежим(Истина);
	
	текДействие = "Контроль использования";
	
	МенеджерЗаписи = РегистрыСведений.ТК_ДокументыВОбработке.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = дНакладная;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда 
		Попытка
			МенеджерЗаписи.Удалить();	
			ДобавитьЛог(ЛогДействий, текДействие, "Конец сборки резерва", Строка(дНакладная));			
		Исключение
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибка удаления записи использования: " + ОписаниеОшибки(), Строка(дНакладная));		
		КонецПопытки;
	Иначе
		ДобавитьЛог(ЛогДействий, текДействие, "Ошибка! Не найдена запись использования резерва", Строка(дНакладная));			
	КонецЕсли;
	
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЭлементыФормыДляПечати()
	
	СоздатьЭлементыФормы(ЗаказыПокупателя, "ЗаказыПокупателя", Элементы.ГруппаЗаказыПокупателя);
	СоздатьЭлементыФормы(ДокументыРасход, "ДокументыРасход", Элементы.ГруппаДокументыРасход);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыФормы(ТаблицаЗначений, ИмяСпискаЗначений, Группа)
	
	КоличествоЭлементов = Группа.ПодчиненныеЭлементы.Количество();
	КоличествоСписок = ТаблицаЗначений.Количество();
	
	МассивЭлементовНаУдаление = Новый Массив;
	
	Для Ит = 0 По Макс(КоличествоЭлементов, КоличествоСписок)-1 Цикл 
		текЭлемент = Неопределено;
		Если Ит <= КоличествоЭлементов-1 Тогда 
			текЭлемент = Группа.ПодчиненныеЭлементы[Ит];
		КонецЕсли;
		
		текДанные = Неопределено;
		Если Ит <= КоличествоСписок-1 Тогда 
			текДанные = ТаблицаЗначений[Ит].Документ;
		КонецЕсли;
		
		Если текДанные <> Неопределено Тогда 
			Если текЭлемент = Неопределено Тогда 
				текЭлемент = Элементы.Добавить(ИмяСпискаЗначений + "_" + Ит, Тип("ПолеФормы"), Группа);
				текЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
				текЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				текЭлемент.Гиперссылка = Истина;
				текЭлемент.Шрифт = Новый Шрифт(текЭлемент.Шрифт,,14);
				
				текЭлемент.ПутьКДанным = ИмяСпискаЗначений + "[" + Ит + "].Документ";
			КонецЕсли;			                            
			
		Иначе
			Если текЭлемент <> Неопределено Тогда 
				МассивЭлементовНаУдаление.Добавить(текЭлемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если МассивЭлементовНаУдаление.Количество() > 0 Тогда 
		Для Каждого Стр Из МассивЭлементовНаУдаление Цикл 
			Элементы.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьИД(Знач ИД)
	тНовИД = "";
	  
	Символица = "АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЬЮЯЫЭ";
	Наим=СокрЛП(ВРег(ИД));
	Для й=1 По СтрДлина(Наим) Цикл
		тСимвол = Сред(Наим,й,1);
		Если Найти(Символица,тСимвол)>0 Тогда
			
			Если тСимвол ="А" Тогда 
				тНовИД = тНовИД + "f";
			ИначеЕсли тСимвол="Б" Тогда 
				тНовИД=тНовИД+",";
			ИначеЕсли тСимвол="В" Тогда 
				тНовИД=тНовИД+"d";
			ИначеЕсли тСимвол="Г" Тогда 
				тНовИД=тНовИД+"u";
			ИначеЕсли тСимвол="Д" Тогда 
				тНовИД=тНовИД+"l";
			ИначеЕсли тСимвол="Е" Тогда 
				тНовИД=тНовИД+"t";
			ИначеЕсли тСимвол="Ё" Тогда 
				тНовИД=тНовИД+"`";
			ИначеЕсли тСимвол="Ж" Тогда 
				тНовИД=тНовИД+";";
			ИначеЕсли тСимвол="З" Тогда 
				тНовИД=тНовИД+"p";
			ИначеЕсли тСимвол="И" Тогда 
				тНовИД=тНовИД+"b";
			ИначеЕсли тСимвол="Й" Тогда 
				тНовИД=тНовИД+"q";
			ИначеЕсли тСимвол="К" Тогда 
				тНовИД=тНовИД+"r";
			ИначеЕсли тСимвол="Л" Тогда 
				тНовИД=тНовИД+"k";
			ИначеЕсли тСимвол="М" Тогда 
				тНовИД=тНовИД+"v";
			ИначеЕсли тСимвол="Н" Тогда 
				тНовИД=тНовИД+"y";
			ИначеЕсли тСимвол="О" Тогда 
				тНовИД=тНовИД+"j";
			ИначеЕсли тСимвол="П" Тогда 
				тНовИД=тНовИД+"g";
			ИначеЕсли тСимвол="Р" Тогда 
				тНовИД=тНовИД+"h";
			ИначеЕсли тСимвол="С" Тогда 
				тНовИД=тНовИД+"c";
			ИначеЕсли тСимвол="Т" Тогда 
				тНовИД=тНовИД+"n";
			ИначеЕсли тСимвол="У" Тогда 
				тНовИД=тНовИД+"e";
			ИначеЕсли тСимвол="Ф" Тогда 
				тНовИД=тНовИД+"a";
			ИначеЕсли тСимвол="Х" Тогда 
				тНовИД=тНовИД+"[";
			ИначеЕсли тСимвол="Ц" Тогда 
				тНовИД=тНовИД+"w";
			ИначеЕсли тСимвол="Ч" Тогда 
				тНовИД=тНовИД+"x";
			ИначеЕсли тСимвол="Ш" Тогда 
				тНовИД=тНовИД+"i";
			ИначеЕсли тСимвол="Щ" Тогда 
				тНовИД=тНовИД+"o";
			ИначеЕсли тСимвол="Ъ" Тогда 
				тНовИД=тНовИД+"]";
			ИначеЕсли тСимвол="Ь" Тогда 
				тНовИД=тНовИД+"m";
			ИначеЕсли тСимвол="Ю" Тогда 
				тНовИД=тНовИД+".";
			ИначеЕсли тСимвол="Я" Тогда 
				тНовИД=тНовИД+"z";
			ИначеЕсли тСимвол="Ы" Тогда 
				тНовИД=тНовИД+"s";
			ИначеЕсли тСимвол="Э" Тогда 
				тНовИД=тНовИД+"'";
			КонецЕсли;	
			
			
		Иначе
			тНовИД = тНовИД + тСимвол;
		КонецЕсли;
		
	КонецЦикла;
	
	НовИД = НРег(тНовИД);
	Возврат ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(НовИД);
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьОграничениеОтгрузокФМ(Знач ТочкаДоставки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.ТочкиДоставки
	               |	И ДополнительныеСведения.Объект = &ТочкаДоставки
	               |	И ДополнительныеСведения.Свойство.Имя = ""РазрешеннаяТочкаФМ""
	               |	И ДополнительныеСведения.Значение = ИСТИНА";
	Запрос.УстановитьПараметр("ТочкаДоставки", ТочкаДоставки);
	
	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьУникальностьШтрихКода(Знач ДанныеСканирования, ТорговаяПлощадка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ТК_ОтсканированныеQRкоды.Дата КАК Дата
	               |ИЗ
	               |	РегистрСведений.ТК_ОтсканированныеQRкоды КАК ТК_ОтсканированныеQRкоды
	               |ГДЕ
	               |	ТК_ОтсканированныеQRкоды.ДанныеСканирования = &ДанныеСканирования
	               |	И ТК_ОтсканированныеQRкоды.ТорговаяПлощадка = &ТорговаяПлощадка";
	
	Запрос.УстановитьПараметр("ДанныеСканирования", ДанныеСканирования);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	Возврат Запрос.Выполнить().Пустой();
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМРЦПоКоду(Знач Номенклатура, Знач КодМРЦ)
	
	Перем РазмерМРЦ, ХарактеристикаМРЦ;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ТК_ИдентификаторыМРЦ.МРЦ КАК МРЦ
	               |ИЗ
	               |	РегистрСведений.ТК_ИдентификаторыМРЦ КАК ТК_ИдентификаторыМРЦ
	               |ГДЕ
	               |	ТК_ИдентификаторыМРЦ.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодМРЦ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		РазмерМРЦ = Выборка.МРЦ;
	КонецЕсли;
	
	Если РазмерМРЦ <> Неопределено Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		               |ГДЕ
		               |	ХарактеристикиНоменклатуры.Владелец = &Владелец
		               |	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
		               |	И ХарактеристикиНоменклатуры.Наименование ПОДОБНО &НаименованиеМРЦ
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ХарактеристикиНоменклатуры.Сортировка УБЫВ";
		
		Запрос.УстановитьПараметр("Владелец", Номенклатура);
		Запрос.УстановитьПараметр("НаименованиеМРЦ", "МРЦ%" + Формат(РазмерМРЦ, "ЧДЦ=2; ЧРД=%"));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ХарактеристикаМРЦ = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ХарактеристикаМРЦ;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗапрещенныйТовар()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Номенклатура
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
	               |	И ДополнительныеСведения.Свойство.Имя = ""ЗапретОтгрузкиФМ""
	               |	И ДополнительныеСведения.Значение = ИСТИНА";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоТовару(Знач ШтрихКод)
	УстановитьПривилегированныйРежим(Истина);	
	//тут
	Если СтрДлина(ШтрихКод) = 13 Тогда
		Возврат Справочники.Номенклатура.НайтиНоменклатуруПоШтрихКоду(ШтрихКод);	//было
	Иначе
		Возврат ПолучитьДанныеПоТоваруФМУ_Империала(ШтрихКод);
	КонецЕсли;
	//тут
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоТоваруФМУ_Империала(Знач ШтрихКод)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ИдентификаторыITGНоменклатуры.Код КАК Штрихкод,
	|	ИдентификаторыITGНоменклатуры.Номенклатура КАК Номенклатура,
	|	ИдентификаторыITGНоменклатуры.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмерения,
	|	ИдентификаторыITGНоменклатуры.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК Коэффициент
	|ИЗ
	|	РегистрСведений.ИдентификаторыITGНоменклатуры КАК ИдентификаторыITGНоменклатуры
	|ГДЕ
	|	ИдентификаторыITGНоменклатуры.Код = &Штрихкод";
	
	Запрос.УстановитьПараметр("ШтрихКод", ШтрихКод);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	СтруктураПоШтрихКоду = Новый Структура("Номенклатура, ЕдиницаИзмерения, Коэффициент",
	Выборка.Номенклатура,
	Выборка.ЕдиницаИзмерения,
	Выборка.Коэффициент);
	
	Возврат СтруктураПоШтрихКоду;
КонецФункции


&НаКлиенте
Процедура ОбновитьЭлементыУстановкиКоличества()
	
	Попытка	
		нСтрока = Объект.Товары.НайтиПоИдентификатору(ИндексСтроки);	
		
		ВидСканирования = Новый Структура("Производитель, КраткийКодБлока", "", Ложь);
		
		Если текСтруктураШК <> Неопределено Тогда 			
			ЗаполнитьЗначенияСвойств(ВидСканирования, текСтруктураШК);
		Иначе
			нКешТоваров = КешТоваров.НайтиСтроки(Новый Структура("Номенклатура", нСтрока.Номенклатура));
			
			Если нКешТоваров.Количество() > 0 Тогда 
				ЗаполнитьЗначенияСвойств(ВидСканирования, нКешТоваров[0]);
			КонецЕсли;
		КонецЕсли;
		
		стрПредставление = Строка(нСтрока.Номенклатура) + Символы.ПС;
		Если ЗначениеЗаполнено(нСтрока.ХарактеристикаНоменклатуры) Тогда 
			стрПредставление = стрПредставление + Строка(нСтрока.ХарактеристикаНоменклатуры) + Символы.ПС;
		КонецЕсли;
		стрПредставление = стрПредставление + Строка(нСтрока.ЕдиницаИзмерения) + " Х " + Строка(нСтрока.Коэффициент);
		
		Элементы.ОписаниеТовара.Заголовок = стрПредставление;
		КоличествоВНакладной = нСтрока.Количество;
		КоличествоПроверено = нСтрока.КоличествоПроверено;		
		
		ПроверяемыйПроизводитель = ВидСканирования.Производитель = "PMU" ИЛИ ВидСканирования.Производитель = "BAT";
		
		Элементы.фКоличествоПроверено.Доступность = РучнойВвод ИЛИ Не ПроверяемыйПроизводитель;
		Элементы.фПодтвердитьКорректировкуКоличества.Доступность = КоличествоПроверено <= КоличествоВНакладной;
		Элементы.ГруппаQRкодФМ.Видимость = ПроверяемыйПроизводитель;
	Исключение
		ДобавитьЛог(ЛогДействий, "Обновление элементов Установки количества", "Ошибка: " + ОписаниеОшибки(), "ИндекСтроки=" + Строка(ИндексСтроки));
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыПроверкиНакладной()
	
	НадписьРезПроверки = Элементы.ДекорацияРезультатПроверки;
	
	Если ДокументКорректен Тогда 
		НадписьРезПроверки.Заголовок = НСтр("ru = 'Расхождений не выявлено'; uk = 'Розбіжность не виявлені'");
	Иначе
		НадписьРезПроверки.Заголовок = НСтр("ru = 'Внимание!
                                       |В документе есть расхождения'; uk = 'Увага!
                                       |У документі є розбіжності'");
	КонецЕсли;
	
	Элементы.РасхожденияКоличества.Видимость = Не ДокументКорректен;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанныеСканирования()
	Перем МассивШтрихКодовФМ, МассивШтрихКодовБАТ, МассивОбщий;
	
	ВсеОк = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивОбщий	= ШтрихКода.Выгрузить(, "ДанныеШтрихКода");
	МассивОбщий.Свернуть("ДанныеШтрихКода");
	
	// штрих-кода Филипп Моррис
	стрШК_ФМ = ШтрихКода.НайтиСтроки(Новый Структура("Производитель", "PMU"));
	
	Если стрШК_ФМ.Количество() > 0 Тогда 		
		ДобавитьЛог(ЛогДействий, "ФМ", "Начало записи данных ФМ");
		
		тзШтрихКода = ШтрихКода.Выгрузить(стрШК_ФМ);
		тзШтрихКода.Свернуть("ДанныеШтрихКода, Организация");		
		
		мОрганизации = тзШтрихКода.ВыгрузитьКолонку("Организация");
		мОрганизации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мОрганизации);
		
		Для Каждого Организация Из мОрганизации Цикл 
			МассивШтрихКодовФМ = Новый Массив;
			
			мСтрШК = тзШтрихКода.НайтиСтроки(Новый Структура("Организация", Организация));			
			Для Каждого стрШК Из мСтрШК Цикл 
				Если ЗначениеЗаполнено(стрШК.ДанныеШтрихКода) Тогда
					МассивШтрихКодовФМ.Добавить(стрШК.ДанныеШтрихКода);
				КонецЕсли;
			КонецЦикла;
			
			Если МассивШтрихКодовФМ.Количество() > 0 Тогда 
				Запись = Новый ЗаписьJSON;
				Запись.УстановитьСтроку();
				ЗаписатьJSON(Запись, МассивШтрихКодовФМ);
				
				СтрокаJSON = Запись.Закрыть();
				
				НаборЗаписей = РегистрыСведений.ДанныеДляФилипМоррис.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.РезервированиеЗаказовПокупателя.Установить(Объект.Резерв);
				НаборЗаписей.Отбор.Организация.Установить(Организация);
				
				НоваяСтрока = НаборЗаписей.Добавить();
				НоваяСтрока.Статус = Перечисления.СтатусыОтправкиФМ.Новый;
				НоваяСтрока.ДатаСканирования = ТекущаяДата();
				НоваяСтрока.РезервированиеЗаказовПокупателя = Объект.Резерв;
				НоваяСтрока.Пользователь = Объект.Комплектовщик;
				НоваяСтрока.ДанныеСканирования = Новый ХранилищеЗначения(СтрокаJSON,Новый СжатиеДанных(9));
				НоваяСтрока.Организация = Организация;
				
				Попытка
					НаборЗаписей.Записать();
				Исключение
					Попытка
						НаборЗаписей.Записать();	
					Исключение
						ВсеОК = Ложь;
					КонецПопытки;
				КонецПопытки;
				
				Если ВсеОк Тогда 
					ДобавитьЛог(ЛогДействий, "ФМ", "Данные по ФМ записаны");
				Иначе
					ДобавитьЛог(ЛогДействий, "ФМ", "Ошибка записи в регистр ""ДанныеДляФилипМоррис"": " + ОписаниеОшибки());
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	
	// штрих-кода БАТ
	стрШК_БАТ = ШтрихКода.НайтиСтроки(Новый Структура("Производитель", "BAT"));
	
	Если стрШК_БАТ.Количество()> 0 Тогда
		ДобавитьЛог(ЛогДействий, "БАТ", "Начало записи данных БАТ");
		
		тзШтрихКода = ШтрихКода.Выгрузить(стрШК_БАТ);
		тзШтрихКода.Свернуть("ДанныеШтрихКода, Организация");		
		
		мОрганизации = тзШтрихКода.ВыгрузитьКолонку("Организация");
		мОрганизации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(мОрганизации);
		
		Для Каждого Организация Из мОрганизации Цикл 
			МассивШтрихКодовБАТ = Новый Массив;		
			
			мСтрШК = тзШтрихКода.НайтиСтроки(Новый Структура("Организация", Организация));			
			Для Каждого стрШК Из мСтрШК Цикл 
				Если ЗначениеЗаполнено(стрШК.ДанныеШтрихКода) Тогда
					МассивШтрихКодовБАТ.Добавить(стрШК.ДанныеШтрихКода);
				КонецЕсли;
			КонецЦикла;
			
			Если МассивШтрихКодовБАТ.Количество() > 0 Тогда 
				Запись = Новый ЗаписьJSON;
				Запись.УстановитьСтроку();
				ЗаписатьJSON(Запись, МассивШтрихКодовБАТ);
				
				СтрокаJSON = Запись.Закрыть();
				
				НаборЗаписей = РегистрыСведений.ДанныеДляБАТ.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.РезервированиеЗаказовПокупателя.Установить(Объект.Резерв);
				НаборЗаписей.Отбор.Организация.Установить(Организация);
				
				НоваяСтрока = НаборЗаписей.Добавить();
				НоваяСтрока.Статус = Перечисления.СтатусыОтправкиФМ.Новый;
				НоваяСтрока.ДатаСканирования = ТекущаяДата();
				НоваяСтрока.РезервированиеЗаказовПокупателя = Объект.Резерв;
				НоваяСтрока.Пользователь = Объект.Комплектовщик;
				НоваяСтрока.ДанныеСканирования = Новый ХранилищеЗначения(СтрокаJSON,Новый СжатиеДанных(9));
				НоваяСтрока.Организация = Организация;
				
				Попытка
					НаборЗаписей.Записать();
				Исключение
					Попытка
						НаборЗаписей.Записать();	
					Исключение
						ВсеОК = Ложь;
					КонецПопытки;
				КонецПопытки;
				
				Если ВсеОк Тогда 
					ДобавитьЛог(ЛогДействий, "БАТ", "Данные по БАТ записаны");
				Иначе
					ДобавитьЛог(ЛогДействий, "БАТ", "Ошибка записи в регистр ""ДанныеДляБАТ"": " + ОписаниеОшибки());	
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	НомерРезерва = дНакладная.Номер;
	тДата = ТекущаяДата();
	
	Если МассивОбщий.Количество() > 0 Тогда 
		Для Каждого текШК Из МассивОбщий Цикл 
			Попытка
				МенеджерЗаписи = РегистрыСведений.ТК_ОтсканированныеQRкоды.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.ДанныеСканирования = текШК.ДанныеШтрихКода;
				МенеджерЗаписи.ТорговаяПлощадка = ТорговаяПлощадка;
				МенеджерЗаписи.Дата = тДата;
				МенеджерЗаписи.НомерРезерва = НомерРезерва;
				
				МенеджерЗаписи.Записать(Истина);
			Исключение
				ВсеОк  = Ложь;				
				ДобавитьЛог(ЛогДействий, "Запись отсканированных QR-кодов", "Ошибка записи кода <"+текШК+"> в регистр ""ТК_ОтсканированныеQRкоды"" для: " + ОписаниеОшибки());	
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВсеОк;
КонецФункции

&НаСервере
Функция  ЗаписатьЗапрещенныеДанные()
	ВсеОК = Истина;
	
	Если Объект.ЗапрещенныйФМ.Количество() = 0 Тогда
		Возврат ВсеОК;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	ДобавитьЛог(ЛогДействий, "ФМ", "Начало записи Запрещенных данных ФМ");
	
	Для Каждого строка из Объект.ЗапрещенныйФМ Цикл
		НовЗапись = РегистрыСведений.ЗапрещеннаяОтгрузкаФилипМоррис.СоздатьМенеджерЗаписи();
		НовЗапись.Штрихкод = строка.ШтрихКод;
		НовЗапись.ПризнакОбработки = Перечисления.СтатусыДокументовEDI.Создан;
		НовЗапись.ДатаСканирования = ТекущаяДата();
		НовЗапись.Номенклатура = строка.Номенклатура;
		НовЗапись.ТочкаДоставки = дАдресДоставки;
		Попытка
			НовЗапись.Записать();
			ДобавитьЛог(ЛогДействий, "ФМ", "Запрещенных данные по ФМ записаны");
		Исключение
			Попытка
				НовЗапись.Записать();	
			Исключение
				ВсеОК = Ложь;
				ДобавитьЛог(ЛогДействий, "ФМ", "Ошибка записи Запрещенных данных ФМ: " + ОписаниеОшибки());
			КонецПопытки;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ВсеОК;
КонецФункции

&НаСервере
Процедура ЗаполнитьДанные_ЗапрещеннымиДанными();
	УстановитьПривилегированныйРежим(Истина);
	
	//Доделать...
	Если Ложь И ЗначениеЗаполнено(дАдресДоставки) И ПроверитьОграничениеОтгрузокФМ(дАдресДоставки) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДополнительныеСведения.Объект КАК Объект,
		               |	ДополнительныеСведения.Значение КАК Значение
		               |ПОМЕСТИТЬ ДопСвойства
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Объект ССЫЛКА Справочник.ТочкиДоставки
		               |	И ДополнительныеСведения.Свойство.Имя = ""ПереадресацияОтгрузкиФМ""
		               |	И ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК Справочник.СкладыКомпании).ТочкаДоставки = &ТочкаДоставки
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Объект
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.ПризнакОбработки КАК ПризнакОбработки,
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.Штрихкод КАК Штрихкод,
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя,
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.ДатаСканирования КАК ДатаСканирования,
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.Номенклатура КАК Номенклатура,
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.ТочкаДоставки КАК ТочкаДоставки,
		               |	ДопСвойства.Объект КАК Объект,
		               |	ДопСвойства.Значение КАК Значение
		               |ИЗ
		               |	ДопСвойства КАК ДопСвойства
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗапрещеннаяОтгрузкаФилипМоррис КАК ЗапрещеннаяОтгрузкаФилипМоррис
		               |		ПО ДопСвойства.Объект = ЗапрещеннаяОтгрузкаФилипМоррис.ТочкаДоставки
		               |ГДЕ
		               |	ЗапрещеннаяОтгрузкаФилипМоррис.ПризнакОбработки = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовEDI.Создан)";
		
		Запрос.УстановитьПараметр("ТочкаДоставки", дАдресДоставки);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
		
		НаборЗаписей = РегистрыСведений.ЗапрещеннаяОтгрузкаФилипМоррис.СоздатьНаборЗаписей();
		
		Ошибки = "";		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НачатьТранзакцию();
			//нСтрокаФМ = Объект.ШтрихКодаФМ.Добавить();
			//нСтрокаФМ.ДанныеШтрихКода = ВыборкаДетальныеЗаписи.Штрихкод;
			НаборЗаписей.Отбор.Штрихкод.Установить(ВыборкаДетальныеЗаписи.Штрихкод);
			НаборЗаписей.Прочитать();
			НаборЗаписей[0].ПризнакОбработки = Перечисления.СтатусыДокументовEDI.Создан;
			НаборЗаписей[0].РезервированиеЗаказовПокупателя = Объект.Резерв;
			Попытка
				НаборЗаписей.Записать();
				ЗафиксироватьТранзакцию();
			Исключение
				Попытка
					НаборЗаписей.Записать();
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					Ошибки = Ошибки + "Ошибка: " + ОписаниеОшибки() + Символы.ПС;
				КонецПопытки;
			КонецПопытки;
		КонецЦикла;
		
		Если Не ПустаяСтрока(Ошибки) Тогда 
			ДобавитьЛог(ЛогДействий, "Запись в регистр ""ЗапрещеннаяОтгрузкаФилипМоррис""", "Ошибки записи:" + Символы.ПС + Ошибки);
		Иначе
			ДобавитьЛог(ЛогДействий, "Запись в регистр ""ЗапрещеннаяОтгрузкаФилипМоррис""", "Данные записаны");
		КонецЕсли;		
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоулчитьМассивСкладовКонтролера()
	//
	Массив = Новый Массив;
	
	текПользователь = Пользователи.ТекущийПользователь();
	ТогрПлощадка = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(текПользователь,"ОсновнаяТорговаяПлощадка");	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СкладыКомпании.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И СкладыКомпании.ПометкаУдаления = ЛОЖЬ
		|	И СкладыКомпании.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТогрПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Массив.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Возврат Массив;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьДанныеДокумента()
	
	Объект.Товары.Очистить();
	Объект.ЗапрещенныйФМ.Очистить();
	//Объект.ШтрихКодаБАТ.Очистить();
	//Объект.ШтрихКодаФМ.Очистить();
	ШтрихКода.Очистить();
	
	КешТоваров.Очистить();
	КешЗапрещенныхТоваров.Очистить();
	РасхожденияКоличества.Очистить();
	
	//ДопПараметры = Новый Структура;
	
КонецПроцедуры

&НаСервере
Функция НайтиСотрудника(Код)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СборщикиРезервов.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.СборщикиРезервов КАК СборщикиРезервов
		|ГДЕ
		|	СборщикиРезервов.Сотрудник.Код = &Код
		|	И СборщикиРезервов.Должность = &Должность";
	
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Должность", Справочники.Должности.НайтиПоРеквизиту("КодКП","9311"));

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Контроллер = ВыборкаДетальныеЗаписи.Сотрудник;
		ОшибкаСканирования = "Установлен контролер "+Контроллер;
		Возврат Истина;
	Иначе
		ОшибкаСканирования = "Сотрудник не является контролером!!!";
		Возврат Ложь;
	КонецЕсли;;
	
	
КонецФункции

&НаСервере
Процедура ЗаписатьВыдачу()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СборщикиРезервов.Сотрудник КАК Сотрудник,
		|	СборщикиРезервов.Упаковщик КАК Упаковщик
		|ИЗ
		|	РегистрСведений.СборщикиРезервов КАК СборщикиРезервов
		|ГДЕ
		|	СборщикиРезервов.Склад = &Склад
		|	И СборщикиРезервов.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Склад", 	   дНакладная.СкладКомпании);
	Запрос.УстановитьПараметр("Сотрудник", Контроллер);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() тогда
		Упаковщик = ВыборкаДетальныеЗаписи.Упаковщик;
	Иначе
		Упаковщик = Справочники.ФизическиеЛица.ПустаяСсылка();
		ОбщегоНазначения.СообщитьПользователю("НЕ назначена пара упаковщик-контроллер для текущего контроллера" + Контроллер);
	КонецЕсли;
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = Объект.Резерв;
	ЗаписьВОчереди.Статус = Перечисления.СтатусыСборкиРезервов.Собран;
	ЗаписьВОчереди.Прочитать();
	Если ЗаписьВОчереди.Выбран() Тогда
		ЗаписьВОчереди.СотрудникКонтроля = Контроллер;
		ЗаписьВОчереди.НачалоКонтроля = ТекущаяДата();
		ЗаписьВОчереди.Упаковщик = Упаковщик;
		ЗаписьВОчереди.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьКонтроль()
	
	ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
	ЗаписьВОчереди.Документ = дНакладная;
	ЗаписьВОчереди.Прочитать();
	Если ЗаписьВОчереди.Выбран() Тогда
		ЗаписьВОчереди.Статус = Перечисления.СтатусыСборкиРезервов.Отгружен;
		ЗаписьВОчереди.КонецКонтроля = ТекущаяДата();
		ЗаписьВОчереди.Записать(Истина);
	КонецЕсли;
	
	Контроллер = Справочники.ФизическиеЛица.ПустаяСсылка();
КонецПроцедуры

&НаСервере
Функция ПроверитьСборкуНакладной(Резерв)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_ОчередьНаСборкуРезервов.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.ТК_ОчередьНаСборкуРезервов КАК ТК_ОчередьНаСборкуРезервов
		|ГДЕ
		|	ТК_ОчередьНаСборкуРезервов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборкиРезервов.Собран)
		|	И ТК_ОчередьНаСборкуРезервов.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Резерв);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;;
	
КонецФункции

&НаСервере
Процедура ПереместитьНаСервере()
	
	НомерМаршрута = Цел(дНакладная.ТочкаДоставки.ПорядокСигаретный/100);
	Место = НайтиМесто(НомерМаршрута);
	
	Если Место.Пустая() Тогда 
		ОбщегоНазначения.СообщитьПользователю("НЕ найдено место хранения для данного маршрута!!!");
	Иначе
		
		ЗаписьВОчереди = РегистрыСведений.РазмещениеСобраногоТовара.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ = ДокументРасход;
		ЗаписьВОчереди.Период = ТекущаяДата();
		
		ЗаписьВОчереди.МестоРазмещения = Место;
		ЗаписьВОчереди.КоличествоМест = КоличествоМест;
		ЗаписьВОчереди.Записать(Истина);
		
		ОбщегоНазначения.СообщитьПользователю("Товар перемещено на ячейку "+текМестоРазмещения+". Количество мест "+КоличествоМест);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция НайтиМесто(Маршрут)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_МестаРазмещения.МестоРазмещения КАК МестоРазмещения
		|ИЗ
		|	Справочник.ТК_МестаРазмещения КАК ТК_МестаРазмещения
		|ГДЕ
		|	ТК_МестаРазмещения.Маршрут = &Маршрут
		|	И ТК_МестаРазмещения.ДеньНедели = &ДеньНедели";
	
	Запрос.УстановитьПараметр("ДеньНедели", ДеньНедели(ТекущаяДата()));
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.МестоРазмещения;
	Иначе
		Возврат Справочники.ТК_МестаРазмещения.ПустаяСсылка();		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Переместить(Команда)
	
	Если КоличествоМест = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Установите количество мест больше 0");
		Возврат;
	КонецЕсли;
		
	ПереместитьНаСервере();
	//ЗавершитьКонтроль();
	
	//Элементы.Переместить.Доступность 	 = Ложь;
	//Элементы.Печать.Доступность 		 = Истина;
	//Элементы.ПечатьНаПринтер.Доступность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуТоваровПоЗаказу(Заказ)
	
	тчТовары = Новый ТаблицаЗначений;
	тчТовары.Колонки.Добавить("Номенклатура");
	тчТовары.Колонки.Добавить("ХарактеристикаНоменклатуры");
	тчТовары.Колонки.Добавить("ЕдиницаИзмерения"); 
	тчТовары.Колонки.Добавить("Количество");
	тчТовары.Колонки.Добавить("МестоРазмещения");
	
	Если ЭтоОбъединенныйРезерв Тогда 
		
		мСтроки = Объект.Товары.НайтиСтроки(Новый Структура ("ЗаказПокупателя", Заказ));	
		Для Каждого СтрТовар Из мСтроки Цикл 
			Если стрТовар.КоличествоПроверено = 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			новСтрока = тчТовары.Добавить();
			ЗаполнитьЗначенияСвойств(новСтрока, СтрТовар);
			новСтрока.Количество = стрТовар.КоличествоПроверено;
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрТовар Из Объект.Товары Цикл
			Если стрТовар.КоличествоПроверено = 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			новСтрока = тчТовары.Добавить();
			ЗаполнитьЗначенияСвойств(новСтрока, СтрТовар);
			новСтрока.Количество = стрТовар.КоличествоПроверено;							
		КонецЦикла;
		
	КонецЕсли;

	Возврат тчТовары;
	
КонецФункции

&НаСервере
Функция ПереместитьНоменклатуруИзМестРазмещения(Реквизиты, тчТовары)
	
	ВсеОк = Истина;
		
	ОснСклад = Реквизиты.СкладКомпании;
	
	МестаРазмещения = тчТовары.ВыгрузитьКолонку("МестоРазмещения");	
	МестаРазмещения = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МестаРазмещения);
		
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МестаРазмещения, ОснСклад);
	
	Если МестаРазмещения.Количество() = 0 Тогда 
		Возврат ВсеОк;
	КонецЕсли;
	
	Для Каждого Склад Из МестаРазмещения Цикл 
		текДействие = "Формирование перемещений со склада """ + Склад + """";
		ДобавитьЛог(ЛогДействий, текДействие, "Заполнение перемещения");
		
		ТоварыПеремещение = тчТовары.Скопировать(Новый Структура("МестоРазмещения", Склад));
		
		ДокПеремещение = Документы.ПеремещениеТоваров.СоздатьДокумент();
		ДокПеремещение.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров;
		ДокПеремещение.Дата = ТекущаяДатаСеанса()-10; //Установить дату
		ДокПеремещение.КурсДокумента = 1;
		ДокПеремещение.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Документ создан автоматически. Перемещение из общего склада %1'; uk = 'Документ створений автоматично. Переміщення із загального складу %1'"),
										ТекущаяДатаСеанса());
		
		ЗаполнитьЗначенияСвойств(ДокПеремещение, Реквизиты, "Организация, ПодразделениеКомпании, ВалютаДокумента, ТипЦен");
		
		ДокПеремещение.СкладКомпании = Склад;
		ДокПеремещение.СкладКомпанииПолучатель = ОснСклад;
		
		ДокПеремещение.ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Склад);
		ДокПеремещение.ТорговаяПлощадкаПолучатель = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(ОснСклад);
		
		ДокПеремещение.ДокументОснование = Реквизиты.Ссылка;
		ДокПеремещение.ЗакрытиеЗаказовПокупателей = 2;
		ДокПеремещение.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		
		
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");

		СтруктураПолейДляЗаполненияЦен = Новый Структура;		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокПеремещение));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);			
		Если Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ДокПеремещение.СкладКомпанииПолучатель, ДокПеремещение.Дата) Тогда
			СтруктураДействий.Вставить("ЦеныБезХарактеристики", Истина);
		КонецЕсли;                                                                         
		
		ДокПеремещение.Товары.Загрузить(ТоварыПеремещение);		
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ДокПеремещение.Товары, СтруктураДействий, Неопределено);
		
		Попытка
			ДокПеремещение.Записать(РежимЗаписиДокумента.Проведение);
			ДобавитьЛог(ЛогДействий, текДействие, "Проведено перемещение", Строка(ДокПеремещение.Ссылка));
		Исключение
			ВсеОк = Ложь;
			Ошибки = ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
			ДобавитьЛог(ЛогДействий, текДействие, "Ошибки проведения перемещения:", Ошибки);
		КонецПопытки;
	КонецЦикла;
	
	Возврат ВсеОк;
	
КонецФункции

&НаСервере
Процедура УведомлениеНаСервере()
	
		ЭлАдрес = "zhuravel.denis1999@gmail.com";
		
		МассивДанных = Новый Массив;        
		МассивДанных.Добавить(Метаданные.Документы.РеализацияТоваров);
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("ФормаСписка", МассивДанных);
		
		мОбъектыПечати = ПолучитьМассивДокументов();
		Если мОбъектыПечати.Количество() = 0 Тогда
			Возврат;	
		КонецЕсли; 
		
		//УправлениеПечатью.вывести
		НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
		НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.XLSX);
		НастройкиСохранения.УпаковатьВАрхив = Истина;
		
		ПечатнаяФорма = ВРег("Расходная накладная (Без шапки)");
		ДокументНапечатан = Ложь;
		
		Для Каждого Команда Из КомандыПечати Цикл 
			
			Если ВРег(Команда.Представление) = ПечатнаяФорма Тогда
				Результат = Новый ТаблицаЗначений;
				Результат.Колонки.Добавить("ИмяФайла", Новый ОписаниеТипов("Строка"));
				Результат.Колонки.Добавить("ДвоичныеДанные", Новый ОписаниеТипов("ДвоичныеДанные"));
				
				СписокКоманд = КомандыПечати;
				Если ТипЗнч(КомандыПечати) <> Тип("Массив") Тогда
					СписокКоманд = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КомандыПечати);
				КонецЕсли;
				
				тзПечатныеФормы = ВыполнитьКомандуПечатиВФайл(Команда,НастройкиСохранения, мОбъектыПечати,Результат); 
				
				ДокументНапечатан = Истина;
				Прервать;
			КонецЕсли;    
		КонецЦикла;
		
		Если ДокументНапечатан Тогда
			Если тзПечатныеФормы.Количество() = 0 Тогда Возврат; КонецЕсли;
			
			ДД = тзПечатныеФормы[0].ДвоичныеДанные;  
			ИмяФайла = тзПечатныеФормы[0].ИмяФайла;
			АдресВХ =    ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор());
			МассивВложения = Новый Массив;
			
			МассивВложения = Новый Массив;
			СтруктураВложения = Новый Структура;
			СтруктураВложения.Вставить("Представление",ИмяФайла);
			СтруктураВложения.Вставить("АдресВоВременномХранилище",АдресВХ);
			СтруктураВложения.Вставить("Кодировка","");
			СтруктураВложения.Вставить("Идентификатор","");
			МассивВложения.Добавить(СтруктураВложения);
			
			ПараметрыПисьма = Новый Структура;
			ПараметрыПисьма.Вставить("Кому",ЭлАдрес);
			ПараметрыПисьма.Вставить("Тема","Накладная");
			ПараметрыПисьма.Вставить("Тело",ИмяФайла);
			ПараметрыПисьма.Вставить("Вложения",МассивВложения);
			
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
			РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);
			
		КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Уведомление(Команда)
	УведомлениеНаСервере();
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивДокументов()
	
	НачалоПериода = НачалоДня(ТекущаяДатаСеанса());
	КонецПериода  = ТекущаяДатаСеанса();
	//СкладКомпании = Справочники.СкладыКомпании.НайтиПоКоду("AC000021");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТоваров.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ГДЕ
		|	РеализацияТоваров.Проведен
		|	И РеализацияТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РеализацияТоваров.СкладКомпании.Код = ""AC000021""
		|	И РеализацияТоваров.ТочкаДоставки.ПорядокСигаретный = 99900";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	//Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		МассивДокументов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		Возврат МассивДокументов;
	КонецЕсли; 
	
КонецФункции  

&НаСервере
Функция ВыполнитьКомандуПечатиВФайл(КомандаПечати, НастройкиСохранения, СписокОбъектов, Результат)
	ТабДок = Новый ТабличныйДокумент;
	
	ДанныеПечати = Неопределено;
	
	ДанныеПечати = УправлениеПечатью.СформироватьПечатныеФормы(КомандаПечати.МенеджерПечати, КомандаПечати.Идентификатор,
	СписокОбъектов, КомандаПечати.ДополнительныеПараметры);
	
	КоллекцияПечатныхФорм = ДанныеПечати.КоллекцияПечатныхФорм;
	ОбъектыПечати = ДанныеПечати.ОбъектыПечати;
	
	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	
	Для Каждого ПечатнаяФорма Из КоллекцияПечатныхФорм Цикл
		
		ПечатныеФормыПоОбъектам = УправлениеПечатью.ПечатныеФормыПоОбъектам(ПечатнаяФорма.ТабличныйДокумент, ОбъектыПечати);
		Для Каждого СоответствиеОбъектаПечатнойФорме Из ПечатныеФормыПоОбъектам Цикл
			
			ОбъектПечати = СоответствиеОбъектаПечатнойФорме.Ключ;
			ТабличныйДокумент = СоответствиеОбъектаПечатнойФорме.Значение;
			
			Если ТабличныйДокумент.ВысотаТаблицы = 0 Тогда
				Продолжить;
			КонецЕсли;
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Табдок.Присоединить(ТабличныйДокумент);
		КонецЦикла;
	КонецЦикла;

	Файл = Результат.Добавить();
	Файл.ИмяФайла = "Отгрузки "+ТекущаяДатаСеанса()+".XLSX";
	Файл.ДвоичныеДанные = ТабличныйДокументВДвоичныеДанные(Табдок, ТипФайлаТабличногоДокумента.XLSX);

	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТабличныйДокументВДвоичныеДанные(ТабличныйДокумент, Формат)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ТабличныйДокумент.Записать(ИмяВременногоФайла, Формат);
	
	Если Формат = ТипФайлаТабличногоДокумента.HTML Тогда
		УправлениеПечатью.ВставитьКартинкиВHTML(ИмяВременногоФайла);
	КонецЕсли;
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	Возврат ДвоичныеДанные;
	
КонецФункции


#КонецОбласти


#КонецОбласти
 
 
 

///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Период.Вариант = ВариантСтандартногоПериода.ДоКонцаЭтойНедели;
	Объект.УзелПланаОбмена = ПланыОбмена.ОбменFirestore.НайтиПоКоду("DBG");
	
	Элементы.ГруппаОбмен.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ГруппаКоллекция.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ЗаявкиСоздатьЗаявку.Видимость = Пользователи.РолиДоступны("ТК_СозданиеЗаявокСервисДеск");
	Элементы.ЗаявкиУдалитьЗаявку.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормыСтраницыЗаявки


&НаКлиенте
Процедура ОткрытьЗаявку(Команда)
	ОткрытьФорму("Обработка.СервисДеск.Форма.ФормаЗаявкиДемо",, ЭтотОбъект,ЭтотОбъект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗаявки(Команда)
	Заявки.Очистить();
	ПолучитьЗаявкиНаСервере();
	Заявки.Сортировать("Дата");
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаявку(Команда)
	ДополнительныеПараметры = Новый Структура("НоваяЗаявка, ДанныеЗаявки", Истина, Новый Структура());
	ОткрытьФормуЗаявки(ДополнительныеПараметры);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаявку(Команда)
	ТекущиеДанные = Элементы.Заявки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОК = УдалитьЗаявкуНаСервере(ТекущиеДанные.ID);
		Сообщить(Формат(ОК,"БЛ=ОШИБКА; БИ=ГОТОВО"));
		Если ОК ТОгда
			Заявки.Очистить();
			ПолучитьЗаявкиНаСервере();
			Заявки.Сортировать("Дата");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаявки


&НаКлиенте
Процедура ЗаявкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ДополнительныеПараметры = Новый Структура("ID,Номер,Дата,Статус,ДатаВыполнения,ИндексСтатуса,Срочная,Услуга,Точка,Категория,Инициатор,Исполнитель,СрокВыполнения,Описание,КомментарийВыполнения,Фото,ФотоВыполнения,АвтоЗакрытая,Куратор,Роль");
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры,Элемент.ТекущиеДанные);
	ОткрытьФормуЗаявки(Новый Структура("ДанныеЗаявки", ДополнительныеПараметры));
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормыСтраницыОбмен


&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	ВыгрузитьДанныеНаСервере();
	Данные.Очистить();
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКалендарь(Команда)
	ВыгрузитьКалендарьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	Данные.Очистить();
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКоллекцию(Команда)
	ПолучитьКоллекциюНаСервере();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаявки


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ЗаполнитьДанные(УзелПланаОбмена)
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	Состав = УзелПланаОбмена.Метаданные().Состав;
	МассивМетаданных = Новый Массив;
	
	Для каждого Элемент Из Состав Цикл
		МассивМетаданных.Добавить(Элемент.Метаданные.ПолноеИмя());
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокУзлов", УзелПланаОбмена);
	
	Текст = "";
	Для Каждого Элемент Из МассивМетаданных Цикл
		Если ПустаяСтрока(Элемент) Тогда
			Продолжить;
		КонецЕсли;
		
		Текст = Текст + ?(Текст = "", "", "ОБЪЕДИНИТЬ ВСЕ") + " 
		|ВЫБРАТЬ 
		|	""" + Элемент + """ КАК МетаПолноеИмя,
		|	Узел                КАК УзелОбмена,
		|	КОЛИЧЕСТВО(*)       КАК КоличествоИзменений
		|ИЗ
		|	" + Элемент + ".Изменения
		|ГДЕ
		|	Узел В (&СписокУзлов)
		|СГРУППИРОВАТЬ ПО
		|	Узел
		|";
	КонецЦикла;
	
	Если Текст <> "" Тогда
		Запрос.Текст = Текст;
		Результат = Запрос.Выполнить().Выгрузить();
		Для каждого Элемент Из Состав Цикл
			стрДанных = Данные.Добавить();
			стрДанных.Наименование = Элемент.Метаданные;
			стрДанных.Тип = ЭтоЧто(Элемент.Метаданные);
			стрДанных.МетаПолноеИмя = Элемент.Метаданные.ПолноеИмя();
			стрРезультата = Результат.Найти(Элемент.Метаданные.ПолноеИмя(),"МетаПолноеИмя");
			Если стрРезультата <> Неопределено Тогда 
				стрДанных.Пометка = Истина;
				стрДанных.Количество = стрРезультата.КоличествоИзменений;
			КонецЕсли;
		КонецЦикла;
		Данные.Сортировать("Наименование");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗаявкиНаСервере(nextPageToken = "")
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
	АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("GET", ПараметрыДляЗапроса.Project_ID, "tasks",,nextPageToken);
	
	ОтветHTTP = Обработки.СервисДеск.ПолучитьДокументы_GET(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		СтруктураОтвета = Обработки.СервисДеск.Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
		
		Если СтруктураОтвета.Свойство("documents") Тогда
			Обработки.СервисДеск.ЗаполнитьТаблицуДокументами(СтруктураОтвета.documents, Заявки, Ложь);
			
			Если СтруктураОтвета.Свойство("nextPageToken") Тогда
				ПолучитьЗаявкиНаСервере(СтруктураОтвета.nextPageToken);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СоединениеFire = Неопределено;
	Элементы.ГруппаЗаявки.Заголовок = "Заявки ("+Заявки.Количество()+")";
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДанныеНаСервере()
	МодукльОбъекта = РеквизитФормыВЗначение("Объект");
	ОК = МодукльОбъекта.ВыгрузитьДанныеВFirestore(Данные.Выгрузить());
	
	Сообщить(ОК);
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьКалендарьНаСервере()
	МодукльОбъекта = РеквизитФормыВЗначение("Объект");
	ОК = МодукльОбъекта.ВыгрузитьКалендарьВFirestore(Период.ДатаНачала, Период.ДатаОкончания);
	
	Сообщить(ОК);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаявки(ДополнительныеПараметры)
	ОткрытьФорму("Обработка.СервисДеск.Форма.ФормаЗаявки", ДополнительныеПараметры, ЭтотОбъект,ЭтотОбъект,,,Новый ОписаниеОповещения("ВыполнитьПослеЗакрытия", ЭтотОбъект),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытия(Обновить,ДополнительныеПараметры)	Экспорт
	Если Обновить = Истина Тогда
		Заявки.Очистить();
		ПолучитьЗаявкиНаСервере();
		Заявки.Сортировать("Дата");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УдалитьЗаявкуНаСервере(documentId)
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
	АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("DELETE", ПараметрыДляЗапроса.Project_ID, "tasks", documentId);
	
	ОтветHTTP = Обработки.СервисДеск.УдалитьДокумент_DELETE(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
	
	СоединениеFire = Неопределено;
	Возврат ОтветHTTP.КодСостояния = 200;
КонецФункции

&НаСервере
Процедура ПолучитьКоллекциюНаСервере(nextPageToken="", ТЗ = Неопределено)
	Если ТЗ = Неопределено Тогда
		ТЗ = Новый ТаблицаЗначений;
	КонецЕсли;
	
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
	АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("GET", ПараметрыДляЗапроса.Project_ID, Коллекции,,nextPageToken);
	
	ОтветHTTP = Обработки.СервисДеск.ПолучитьДокументы_GET(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		СтруктураОтвета = Обработки.СервисДеск.Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
		
		Если СтруктураОтвета.Свойство("documents") Тогда
			Обработки.СервисДеск.ПолучитьКоллекцию(СтруктураОтвета.documents, ТЗ);
			
			Если СтруктураОтвета.Свойство("nextPageToken") Тогда
				ПолучитьКоллекциюНаСервере(СтруктураОтвета.nextPageToken, ТЗ);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СоединениеFire = Неопределено;
КонецПроцедуры


#КонецОбласти


#Область ВспомогательныеПроцедурыИФункции


&НаСервереБезКонтекста
Функция ЭтоЧто(ОбъектМетаданных)
	Если ОбщегоНазначения.ЭтоСправочник(ОбъектМетаданных) Тогда
		Возврат 3;
	ИначеЕсли ОбщегоНазначения.ЭтоДокумент(ОбъектМетаданных) Тогда
		Возврат 5;
	ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(ОбъектМетаданных) Тогда
		Возврат 9;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции






#КонецОбласти














#Область ОбработчикиСобытийФормы


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	ОчиститьСообщения();
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие); 
	Диалог.МножественныйВыбор = Истина;
	Диалог.Заголовок = "Выберите Каталог с файлами";
	Диалог.Фильтр = "Новый формат (*.xlsx)|*.xlsx|Старый формат (*.xls)|*.xls";

	ОповещениеЗавершения = Новый ОписаниеОповещения("ЗавершениеПомещенияФайлов", ЭтаФорма, Неопределено);
	
	НачатьПомещениеФайлов(ОповещениеЗавершения, Диалог, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПомещенияФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПомещенныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	МассивXSL = Новый Массив;
	Для Каждого нФайл Из ПомещенныеФайлы Цикл 
			МассивXSL.Добавить(нФайл);
	КонецЦикла;
	
	Если МассивXSL.Количество() > 0 Тогда 
		ЗагрузитьФайлыНаСервере(МассивXSL);
	Иначе 
		ТекстСообщения = НСтр("ru = 'Нет выбранных файлов формата xls'; uk = 'Немає обраних файлів формату xls'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
КонецПроцедуры	


&НаСервере
Процедура ЗагрузитьФайлыНаСервере(МассивXSL)
	
	ДеревоДокументов.ПолучитьЭлементы().Очистить();	
	Дерево = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));	
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТЗ.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмерения"));
	ТЗ.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15,2)));

	Для Каждого ОписаниеФайла Из МассивXSL Цикл
		
		врФайл = ПолучитьИзВременногоХранилища(ОписаниеФайла.Хранение);
		имяВрФайла = ПолучитьИмяВременногоФайла(".xlsx");
		врФайл.Записать(имяВрФайла);		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.Прочитать(имяВрФайла, СпособЧтенияЗначенийТабличногоДокумента.Текст);	
		Тз.Очистить();
		
		Для НомерСтроки=2 По ТабличныйДокумент.ВысотаТаблицы Цикл
			
			КодТедис = СокрЛП(ЗначениеКолонки(ТабличныйДокумент, НомерСтроки, 1));
			Штрихкод = СокрЛП(ЗначениеКолонки(ТабличныйДокумент, НомерСтроки, 2)); 
			
			Если ПустаяСтрока(КодТедис) Тогда 
				Продолжить; 
			КонецЕсли; 						
			
			КодТовара = СтрЗаменить(КодТедис, " ", "");
			
			ТекстОшибки = "";
			
			ТоварСсылка = ПоискНоменклатуры(КодТовара); 
			
			Если ТоварСсылка.Номенклатура.Пустая() Тогда 
				
				ТоварСсылка = Справочники.Номенклатура.НайтиНоменклатуруПоШтрихКоду(Штрихкод);
				
				Если ТоварСсылка = Неопределено Тогда 
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Строка %1. Товар с кодом %2 - не найден! В документе %3'; uk = 'Строка %1. Товар з кодом %2 - не знайдений! В документе %3'"),
					НомерСтроки,
					КодТовара,
					ОписаниеФайла.Имя);
				КонецЕсли;
				
			ИначеЕсли ТоварСсылка.Номенклатура.ПометкаУдаления Тогда 
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Строка %1. Товар  [%2] %3 - помечен на удаление! В документе %4'; uk = 'Строка %1. Товар  [%2] %3 - відмічений на видалення! В документі %4'"),
				НомерСтроки,
				КодТовара,
				ТоварСсылка,
				ОписаниеФайла.Имя);
				
			КонецЕсли;
			
			Если Не ПустаяСтрока(ТекстОшибки) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				ВсеОк = Ложь;
				Продолжить;
			КонецЕсли;
			
			КоличествоСтрока = СокрЛП(ЗначениеКолонки(ТабличныйДокумент, НомерСтроки, 5));		
			Количество = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(КоличествоСтрока);
			
			Если Количество = 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			стрТовары = ТЗ.Добавить();
			стрТовары.Номенклатура = ТоварСсылка.Номенклатура;
			стрТовары.ЕдиницаИзмерения = ТоварСсылка.Номенклатура.ЕдиницаИзмеренияУпаковки;
			стрТовары.Количество = Количество;
			
		КонецЦикла;
		
		Если Тз.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ "+ОписаниеФайла.Имя+" имеет неправильную структуру!!!");
			Продолжить;
		КонецЕсли;
		
		СтруктураДокумента = Новый Структура;
		СтруктураДокумента.Вставить("ИмяФайла",ОписаниеФайла.Имя);
		СтруктураДокумента.Вставить("Товары", ТЗ);

		ЗаполнитьДеревоДокументов(СтруктураДокумента, Дерево)
		
	КонецЦикла;
	
	ЗначениеВДанныеФормы(Дерево, ДеревоДокументов);
	
КонецПроцедуры

&НаСервере
Функция ПоискНоменклатуры(КодТедис) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДополнительныеСведения.Объект КАК Объект
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Значение = &Значение
		|	И ДополнительныеСведения.Свойство.Имя = ""КодТедис""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура";
	
	Запрос.УстановитьПараметр("Значение", КодТедис);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Новый Структура("Номенклатура",Справочники.Номенклатура.ПустаяСсылка());
	Иначе
		ВыборкаДетальныеЗаписи.Следующий();
		Возврат Новый Структура("Номенклатура", ВыборкаДетальныеЗаписи.Объект);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ЗначениеКолонки(ТабличныйДокумент, НомерСтроки, НомерКолонки)

	Область = ТабличныйДокумент.Область("R" + Строка(НомерСтроки) + "C" + Строка(НомерКолонки));
	Возврат Область.Текст;

КонецФункции // ЗначениеКолонки()

&НаСервере
Процедура ЗаполнитьДеревоДокументов(СтруктураДокумента, ДеревоДокументов)
	
	СтрокаДерева = ДеревоДокументов.Строки.Добавить();
	СтрокаДерева.ДокументНоменклатура = "Документ "+ СтруктураДокумента.ИмяФайла;
	СтрокаДерева.ИмяФайла       = СтруктураДокумента.ИмяФайла;
	СтрокаДерева.Выбор = Истина;
	 	
	Для Каждого СтрокаТовары Из СтруктураДокумента.Товары Цикл 
		СтрокаДереваТовары = СтрокаДерева.Строки.Добавить();
		
		СтрокаДереваТовары.ДокументНоменклатура = СтрокаТовары.Номенклатура;   
		СтрокаДереваТовары.ЕдиницаИзмерения = СтрокаТовары.ЕдиницаИзмерения;
		СтрокаДереваТовары.Количество = СтрокаТовары.Количество;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументы(Команда)
	
	Если ПроверитьЗаполнение() И ДеревоДокументов.ПолучитьЭлементы().Количество() > 0 Тогда 
		СформироватьДокументыНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДокументыНаСервере()
	
	Дерево = РеквизитФормыВЗначение("ДеревоДокументов", Тип("ДеревоЗначений"));		
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СкладКомпании", СкладКомпании);
	ДополнительныеПараметры.Вставить("Организация", Организация);
	
	Результат = Обработки.ТК_ЗагрузкаЗаказов.СформироватьДокументы(Дерево, ДополнительныеПараметры);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда 
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	Если Результат.Свойство("ДеревоДокументов", Дерево) Тогда 
		ЗначениеВДанныеФормы(Дерево, ДеревоДокументов);
	КонецЕсли;	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	ОбойтиДеревоНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбойтиДеревоНаСервере()
	
	ДеревоЗнач = РеквизитФормыВЗначение("ДеревоДокументов");
	
	Для Каждого СтрДерева Из ДеревоЗнач.Строки Цикл
		СтрДерева.Выбор = Ложь;
	КонецЦикла;
	
	ЗначениеВДанныеФормы(ДеревоЗнач, ДеревоДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыделеные(Команда)
	Для Каждого СтрДерева Из Элементы.ДеревоДокументов.ВыделенныеСтроки Цикл
		 ЭлементДерева = Элементы.ДеревоДокументов.ДанныеСтроки(СтрДерева);
		 ЭлементДерева.Выбор = Истина;
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти



#Область СлужебныеПроцедурыИФункции


#КонецОбласти


#Область ПрочиеПроцедурыИФункции


#КонецОбласти
 

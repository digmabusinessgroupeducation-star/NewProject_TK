

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	
	Если Параметры.Свойство("ИзменениеЦен") И ЗначениеЗаполнено(Параметры.ИзменениеЦен) Тогда
		ДокОснование = Параметры.ИзменениеЦен;
		Если Параметры.Свойство("Округление") И ЗначениеЗаполнено(Параметры.Округление) И 
			Параметры.Свойство("НачалоДействия") И ЗначениеЗаполнено(Параметры.НачалоДействия)	Тогда	
			ОкруглитьДо = Параметры.Округление;
			ДатаНачалаДействия = Параметры.НачалоДействия;
		КонецЕсли;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
		
	БазовыйТипЦен = ДокОснование.ТипЦен;
	
	ПроводитьДокументы = Истина;
	
	Если ДокОснование.Пустая()
		Или БазовыйТипЦен.Пустая() Тогда 
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТипыЦен.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ТипыЦен КАК ТипыЦен
	               |ГДЕ
	               |	ТипыЦен.БазовыйТипЦен = &БазовыйТипЦен
	               |	И НЕ ТипыЦен.ПометкаУдаления
	               |	И НЕ ТипыЦен.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("БазовыйТипЦен", БазовыйТипЦен);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для типа цен ""%1"" нет подчиненных типов цен'; uk = 'Для типа ціни ""%1"" немає підпорядкованих типів цін'"),
				БазовыйТипЦен),
			ДокОснование,
			,
			,
			Отказ);
		
		Возврат;
	КонецЕсли;
	
	тзТипыЦен = Результат.Выгрузить();
	
	СписокТиповЦен.ЗагрузитьЗначения(тзТипыЦен.ВыгрузитьКолонку("Ссылка"));
	СписокТиповЦен.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

#КонецОбласти

 
#Область ОбработчикиСобытийЭлементовФормы



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	МассивТиповЦен = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокТиповЦен);
	
	Если МассивТиповЦен.Количество() = 0 Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выбранных типов цен'; uk = 'Немає обраних типів цін'"));
		Возврат;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОкруглитьДо) и ЗначениеЗаполнено(ДатаНачалаДействия) Тогда
		ПараметрыЫ = Новый Структура("ОкруглятьДо, ДатаНачалаДействия", ОкруглитьДо, ДатаНачалаДействия);
	Иначе
		ПараметрыЫ = Неопределено
	КонецЕсли;
	
	Статистика = Новый Структура("Создано, СОшибками", 0, 0);	
	СоздатьДокументыНаСервере(ДокОснование, МассивТиповЦен, ПроводитьДокументы, Статистика,ПараметрыЫ);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Успешное завершение'; uk = 'Успішне завершення'"), 
		, 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Создано изменений цен %1
                  | из них с ошибками - %2'; uk = 'Створено змін цін %1
                  | з них з помилками - %2'"),
			Статистика.Создано,
			Статистика.СОшибками), 
		БиблиотекаКартинок.Успешно32);			
		
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьДокументыНаСервере(ИзменениеЦен, МассивТиповЦен, ПроводитьДокументы, Статистика, иПараметры)
	Обработки.ТК_ВводНаОснованииИзменениеЦен.ЗаполнитьНаОснованииИзмененияЦен(ИзменениеЦен, МассивТиповЦен, ПроводитьДокументы, Статистика, иПараметры);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции



#КонецОбласти


#Область ПрочиеПроцедурыИФункции



#КонецОбласти

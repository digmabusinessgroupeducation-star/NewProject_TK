
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.УзелПланаОбмена = ПланыОбмена.ОбменОбучение.НайтиПоКоду("DBG");
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Элементы.ГруппаОбмен.Видимость = ЭтоПолноправныйПользователь;
	Элементы.ГруппаКоллекция.Видимость = ЭтоПолноправныйПользователь;
	
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормыСтраницыЗаявки


&НаКлиенте
Процедура ПолучитьТесты(Команда)
	Объект.Тесты.Очистить();
	ПолучитьТестыНаСервере();
	Объект.Тесты.Сортировать("Дата");
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаявки


&НаКлиенте
Процедура ТестВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ДополнительныеПараметры = Новый Структура("ID,Номер,Дата,Статус,ДатаВыполнения,ИндексСтатуса,Срочная,Услуга,Точка,Категория,Инициатор,Исполнитель,СрокВыполнения,Описание,КомментарийВыполнения,Фото,ФотоВыполнения,АвтоЗакрытая,,");
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры,Элемент.ТекущиеДанные);
	ОткрытьФормуТеста(Новый Структура("ДанныеЗаявки", ДополнительныеПараметры));
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормыСтраницыОбмен


&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	ВыгрузитьДанныеНаСервере();
	Данные.Очистить();
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	Данные.Очистить();
	ЗаполнитьДанные(Объект.УзелПланаОбмена);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКоллекцию(Команда)
	ПолучитьКоллекциюНаСервере();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыЗаявки


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ЗаполнитьДанные(УзелПланаОбмена)
	
	Состав = УзелПланаОбмена.Метаданные().Состав;
	МассивМетаданных = Новый Массив;
	
	Для каждого Элемент Из Состав Цикл
		МассивМетаданных.Добавить(Элемент.Метаданные.ПолноеИмя());
	КонецЦикла;
	
	СоответствиеКоллекций = Новый Соответствие;
	СоответствиеКоллекций.Вставить("Справочник.ТестовыеВопросы",1);
	СоответствиеКоллекций.Вставить("Справочник.Лекции",			2);
	СоответствиеКоллекций.Вставить("Справочник.Курсы",			3);
	СоответствиеКоллекций.Вставить("Справочник.Дисциплины",		4);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокУзлов", УзелПланаОбмена);
	
	Текст = "";
	Для Каждого Элемент Из МассивМетаданных Цикл
		Если ПустаяСтрока(Элемент) Тогда
			Продолжить;
		КонецЕсли;
		
		Текст = Текст + ?(Текст = "", "", "ОБЪЕДИНИТЬ ВСЕ") + " 
		|ВЫБРАТЬ 
		|	""" + Элемент + """ КАК МетаПолноеИмя,
		|	Узел                КАК УзелОбмена,
		|	КОЛИЧЕСТВО(*)       КАК КоличествоИзменений
		|ИЗ
		|	" + Элемент + ".Изменения
		|ГДЕ
		|	Узел В (&СписокУзлов)
		|СГРУППИРОВАТЬ ПО
		|	Узел
		|";
	КонецЦикла;
	
	Если Текст <> "" Тогда
		Запрос.Текст = Текст;
		Результат = Запрос.Выполнить().Выгрузить();
		Для каждого Элемент Из Состав Цикл
			стрДанных = Данные.Добавить();
			стрДанных.Наименование = Элемент.Метаданные;
			стрДанных.МетаПолноеИмя = Элемент.Метаданные.ПолноеИмя();
			стрДанных.Порядок = СоответствиеКоллекций.Получить(Элемент.Метаданные.ПолноеИмя());
			стрРезультата = Результат.Найти(Элемент.Метаданные.ПолноеИмя(),"МетаПолноеИмя");
			Если стрРезультата <> Неопределено Тогда 
				стрДанных.Пометка = Истина;
				стрДанных.Количество = стрРезультата.КоличествоИзменений;
			КонецЕсли;
		КонецЦикла;
		Данные.Сортировать("Порядок");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТестыНаСервере(nextPageToken = "")
	Тесты = Объект.Тесты;
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	СоединениеFire = Обработки.Обучение.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
	АдресРесурса = Обработки.Обучение.ПодготовитьАдресРесурса("GET", ПараметрыДляЗапроса.Project_ID, "tasks",,nextPageToken);
	
	ОтветHTTP = Обработки.Обучение.ПолучитьДокументы_GET(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		СтруктураОтвета = Обработки.Обучение.Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
		
		Если СтруктураОтвета.Свойство("documents") Тогда
			Обработки.Обучение.ЗаполнитьТаблицуДокументами(СтруктураОтвета.documents, Тесты, Ложь);
			
			Если СтруктураОтвета.Свойство("nextPageToken") Тогда
				ПолучитьТестыНаСервере(СтруктураОтвета.nextPageToken);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СоединениеFire = Неопределено;
	//Элементы.ГруппаЗаявки.Заголовок = "Заявки ("+Тесты.Количество()+")";
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьДанныеНаСервере()
	МодукльОбъекта = РеквизитФормыВЗначение("Объект");
	ОК = МодукльОбъекта.ВыгрузитьДанныеВFirestore(Данные.Выгрузить());
	
	Сообщить(ОК);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТеста(ДополнительныеПараметры)
	ОткрытьФорму("Обработка.Обучение.Форма.ФормаЗаявки", ДополнительныеПараметры, ЭтотОбъект,ЭтотОбъект,,,Новый ОписаниеОповещения("ВыполнитьПослеЗакрытия", ЭтотОбъект),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытия(Обновить,ДополнительныеПараметры)	Экспорт
	Если Обновить = Истина Тогда
		Объект.Тесты.Очистить();
		ПолучитьТестыНаСервере();
		Объект.Тесты.Сортировать("Дата");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьКоллекциюНаСервере(nextPageToken="", ТЗ = Неопределено)
	Если ТЗ = Неопределено Тогда
		ТЗ = Новый ТаблицаЗначений;
	КонецЕсли;
	
	ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
	ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, Объект.УзелПланаОбмена);
	СоединениеFire = Обработки.Обучение.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
	АдресРесурса = Обработки.Обучение.ПодготовитьАдресРесурса("GET", ПараметрыДляЗапроса.Project_ID, ИмяКоллекции,,nextPageToken);
	
	ОтветHTTP = Обработки.Обучение.ПолучитьДокументы_GET(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		СтруктураОтвета = Обработки.Обучение.Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
		
		Если СтруктураОтвета.Свойство("documents") Тогда
			Обработки.Обучение.ПолучитьКоллекцию(СтруктураОтвета.documents, ТЗ);
			
			Если СтруктураОтвета.Свойство("nextPageToken") Тогда
				ПолучитьКоллекциюНаСервере(СтруктураОтвета.nextPageToken, ТЗ);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	СоединениеFire = Неопределено;
КонецПроцедуры


#КонецОбласти


#Область ВспомогательныеПроцедурыИФункции







#КонецОбласти













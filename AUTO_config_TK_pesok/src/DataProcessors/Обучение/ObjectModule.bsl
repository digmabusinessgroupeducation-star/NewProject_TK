
///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, Тихий.
// Все права защищены.)) 
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область СлужебныйПрограммныйИнтерфейс
	
	
	Функция ВыгрузитьДанныеВFirestore(Данные)	Экспорт
		
		ВсеОК = Истина;
		ТаблицаКураторов = Новый ТаблицаЗначений;
		ТаблицаЗагруженных = Новый ТаблицаЗначений;
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		
		ТекстОшибок = "";
		РезВО = РегистрыСведений.РезультатыВыполненияОбработок;	
		СтрЗР = РезВО.СформироватьСтруктуруЗаписиРезультатов();
		СтрЗР.ИдентификаторОбработки = "ВыгрузкаДанныхВFirestore";
		СтрЗР.КлючОбработки = "Выгрузка данных по обучению";
		
		ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
		ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, УзелПланаОбмена);
		СоединениеFire = Обработки.Обучение.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		
		СоответствиеКоллекций = Новый Соответствие;
		СоответствиеКоллекций.Вставить("Справочник.ФизическиеЛица","edu_sotrudniki");
		СоответствиеКоллекций.Вставить("Справочник.ТестовыеВопросы","edu_questions");
		СоответствиеКоллекций.Вставить("Справочник.Лекции","edu_lectures");
		СоответствиеКоллекций.Вставить("Справочник.Курсы","edu_courses");
		СоответствиеКоллекций.Вставить("Справочник.Дисциплины","edu_disciplines");
		
		Для Каждого стрМета Из Данные Цикл
			Если Не стрМета.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Если стрМета.МетаПолноеИмя = "Справочник.ФизическиеЛица" Тогда
				ПолучитьТаблицаКураторов(ТаблицаКураторов);
				ПолучитьТаблицаЗагруженных(ТаблицаЗагруженных, ПараметрыДляЗапроса);
				ПолучитьТаблицаСотрудников(ТаблицаКураторов, ТаблицаЗагруженных, ТаблицаСотрудников);
				СтруктураСотрудника = Новый Структура;
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	МетаданныеИзменения.Ссылка КАК Ссылка
			|ИЗ
			|	" + стрМета.МетаПолноеИмя + ".Изменения КАК МетаданныеИзменения
			|ГДЕ
			|	Узел = &Узел";
			
			Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			МассивЭлементов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			ПланыОбмена.ВыбратьИзменения(УзелПланаОбмена, 1, МассивЭлементов);
			
			Коллекция = СоответствиеКоллекций.Получить(стрМета.МетаПолноеИмя);
			
			кэ = 0;
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если НужноВыгружать(Выборка.Ссылка, стрМета.МетаПолноеИмя, ТаблицаКураторов, ТаблицаСотрудников, СтруктураСотрудника) Тогда
					document_ID	= Строка(Выборка.Ссылка.УникальныйИдентификатор());
					
					АдресРесурса= Обработки.Обучение.ПодготовитьАдресРесурса("PATCH", ПараметрыДляЗапроса.Project_ID, Коллекция, document_ID);
					ТелоЗапроса = Обработки.Обучение.ПодготовитьТелоЗапроса(ПараметрыДляЗапроса, СоединениеFire, Коллекция, ?(стрМета.МетаПолноеИмя = "Справочник.ФизическиеЛица",СтруктураСотрудника,Выборка.Ссылка));
					
					ОК = Обработки.Обучение.ИзменитьДокументы_PATCH(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса, ТелоЗапроса);
					ВсеОК = ВсеОК И ОК;	
					
					Если ОК Тогда
						кэ = 1 + кэ;
					Иначе
						ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, Выборка.Ссылка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			РезВО.ДобавитьКомментарий("Выгружено " + кэ + " " + стрМета.МетаПолноеИмя, СтрЗР);
			
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелПланаОбмена,1);
			
		КонецЦикла;
		
		СоединениеFire = Неопределено;
		
		РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР,НЕ ЗначениеЗаполнено(ТекстОшибок));
		
		Возврат ВсеОК;
		
	КонецФункции
	
	Функция ЗарегистрироватьНаВыгрузкуСотрудниковПоОбучению()	Экспорт
		ВсеОК = Истина;
		ТаблицаКураторов = Новый ТаблицаЗначений;
		ТаблицаЗагруженных = Новый ТаблицаЗначений;
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		
		РезВО = РегистрыСведений.РезультатыВыполненияОбработок;	
		СтрЗР = РезВО.СформироватьСтруктуруЗаписиРезультатов();
		СтрЗР.ИдентификаторОбработки = "ВыгрузкаДанныхВFirestore";
		СтрЗР.КлючОбработки = "Регистрация сотрудников по обучению";
		
		ПараметрыДляЗапроса = Новый Структура("Service_CloudFirestoreAPI, Service_IdentityToolkitAPI, Project_ID, Web_API_Key, UserUID");
		ЗаполнитьЗначенияСвойств(ПараметрыДляЗапроса, УзелПланаОбмена);
		СоединениеFire = Обработки.Обучение.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		
		ПолучитьТаблицаКураторов(ТаблицаКураторов);
		ПолучитьТаблицаЗагруженных(ТаблицаЗагруженных, ПараметрыДляЗапроса);
		ПолучитьТаблицаСотрудников(ТаблицаКураторов, ТаблицаЗагруженных, ТаблицаСотрудников);
		СтруктураСотрудника = Новый Структура;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.Сотрудник КАК Сотрудник,
		|	ТЗ.ИНН КАК ИНН,
		|	ТЗ.Работает КАК Работает,
		|	ТЗ.Куратор КАК Куратор,
		|	ТЗ.Продавец КАК Продавец,
		|	ТЗ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|	ТЗ.КодТочки КАК КодТочки,
		|	ТЗ.Должность КАК Должность,
		|	ТЗ.ПричинаИзмененияСостояния КАК ПричинаИзмененияСостояния,
		|	ВЫРАЗИТЬ(ТЗ.ТелефонЗУП КАК СТРОКА(15)) КАК ТелефонЗУП,
		|	ТЗ.Период КАК Период,
		|	ТЗ.kurator КАК kurator,
		|	ТЗ.prodavets КАК prodavets,
		|	ВЫРАЗИТЬ(ТЗ.telephone КАК СТРОКА(15)) КАК telephone,
		|	ТЗ.type КАК type,
		|	ТЗ.working КАК working
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Сотрудник КАК Сотрудник,
		|	ВТ.ИНН КАК ИНН,
		|	ВТ.Работает КАК Работает,
		|	ВТ.Куратор КАК Куратор,
		|	ВТ.Продавец КАК Продавец,
		|	ВТ.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|	ВТ.КодТочки КАК КодТочки,
		|	ВТ.Должность КАК Должность,
		|	ВТ.ПричинаИзмененияСостояния КАК ПричинаИзмененияСостояния,
		|	ВТ.ТелефонЗУП КАК ТелефонЗУП,
		|	ВТ.Период КАК Период,
		|	ВТ.kurator КАК kurator,
		|	ВТ.prodavets КАК prodavets,
		|	ВТ.telephone КАК telephone,
		|	ВТ.type КАК type,
		|	ВТ.working КАК working,
		|	ВТ.type ЕСТЬ NULL
		|		ИЛИ ВТ.Работает <> ЕСТЬNULL(ВТ.working, НЕОПРЕДЕЛЕНО)
		|		ИЛИ ВТ.Куратор <> ЕСТЬNULL(ВТ.kurator, НЕОПРЕДЕЛЕНО)
		|		ИЛИ ВТ.Продавец <> ЕСТЬNULL(ВТ.prodavets, НЕОПРЕДЕЛЕНО)
		|		ИЛИ ЕСТЬNULL(ВТ.ТелефонЗУП, """") <> ЕСТЬNULL(ВТ.telephone, НЕОПРЕДЕЛЕНО) КАК Выгружать
		|ИЗ
		|	ВТ КАК ВТ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Выгружать УБЫВ,
		|	Работает УБЫВ,
		|	Продавец";
		
		Запрос.УстановитьПараметр("ТЗ", ТаблицаСотрудников);
		
		РезультатЗапроса = Запрос.Выполнить();	//	РезультатЗапроса.Выгрузить()
		
		к = 0;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Выгружать Тогда
				к = 1 + к;
				СотрудникСсылка = Справочники.ФизическиеЛица.НайтиПоКоду(СокрЛП(Выборка.ИНН));
				Если СотрудникСсылка.Пустая() Тогда
					ВсеОК = Ложь;
					РезВО.СообщитьОбОшибке(СтрШаблон("По ИНН (%1) не найден сотрудник: %2 ",СокрЛП(Выборка.ИНН),СокрЛП(Выборка.Сотрудник)),СтрЗР);
				Иначе
					ПланыОбмена.ЗарегистрироватьИзменения(УзелПланаОбмена, СотрудникСсылка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		РезВО.ДобавитьКомментарий("Зарегистрировано " + к + " сотрудников", СтрЗР);
		РезВО.ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтрЗР, ВсеОК);
		
		Возврат ВсеОК;
		
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область СлужебныеПроцедурыИФункции
	
	
	Функция НужноВыгружать(Ссылка, МетаПолноеИмя, ТаблицаКураторов, ТаблицаСотрудников, СтруктураСотрудника)
		Если МетаПолноеИмя = "Справочник.ФизическиеЛица" Тогда
			Если НЕ Ссылка.ЭтоГруппа Тогда;
				ИНН = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"Код"));
				Если ИНН = "N00016383" Тогда Возврат Ложь; КонецЕсли;	//	!Служебный пользователь
				
				стрСотрудник	= ТаблицаСотрудников.Найти(ИНН, "ИНН");
				ПризракТК		= стрСотрудник = Неопределено;
				Если ПризракТК Тогда Возврат Ложь; КонецЕсли;	//	Новый Не Работает
				
				НетВБазе	= Не ЗначениеЗаполнено(стрСотрудник.type);
				Работает	= стрСотрудник.Работает;
				ЭтоКуратор	= ТаблицаКураторов.Найти(Ссылка, "Куратор") <> Неопределено;	
				
				Если ЭтоКуратор Тогда
					ЭтоПродавец	= Ложь;
				Иначе
					Если Работает Тогда
						ЭтоПродавец	= стрСотрудник.Продавец;
					Иначе
						ЭтоПродавец	= стрСотрудник.prodavets;
					КонецЕсли;
				КонецЕсли;
				
				Если Работает Тогда
					Телефон	= стрСотрудник.ТелефонЗУП;
				Иначе
					Телефон	= стрСотрудник.telephone;
				КонецЕсли;
				
				ФИО = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"Наименование"));
				мПодразделений = ПодразделениеСотрудника(стрСотрудник, Ссылка, ЭтоКуратор, ТаблицаКураторов);
				
				Нужон = НетВБазе ИЛИ ЭтоКуратор <> стрСотрудник.kurator ИЛИ ЭтоПродавец <> стрСотрудник.prodavets
				ИЛИ Телефон <> стрСотрудник.telephone ИЛИ Работает <> стрСотрудник.working;
				Если Нужон Тогда				
					СтруктураСотрудника.Вставить("Код",			ИНН);
					СтруктураСотрудника.Вставить("ФИО",			ФИО);
					СтруктураСотрудника.Вставить("Куратор",		ЭтоКуратор);
					СтруктураСотрудника.Вставить("Продавец",	ЭтоПродавец);
					СтруктураСотрудника.Вставить("Телефон",		Телефон);
					СтруктураСотрудника.Вставить("Тип",			0);
					СтруктураСотрудника.Вставить("Работает",	Работает);
					СтруктураСотрудника.Вставить("Подразделения",мПодразделений);
					СтруктураСотрудника.Вставить("Фото",		"");
				КонецЕсли;
				Возврат Нужон;
			Иначе
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Возврат НЕ Ссылка.ЭтоГруппа;
		КонецЕсли;
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область ВспомогательныеПроцедурыИФункции
	
	Процедура ПолучитьТаблицаКураторов(ТаблицаКураторов)
		
		Если ЗначениеЗаполнено(ТаблицаКураторов) Тогда Возврат; КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение КАК Куратор,
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение.Код КАК ИНН,
		|	МАКСИМУМ(ТорговыеПлощадкиДополнительныеРеквизиты.Ссылка.ПодразделениеКомпании) КАК Подразделение
		|ИЗ
		|	Справочник.ТорговыеПлощадки.ДополнительныеРеквизиты КАК ТорговыеПлощадкиДополнительныеРеквизиты
		|ГДЕ
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Свойство.Имя = ""КураторПлощадки""
		|
		|СГРУППИРОВАТЬ ПО
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение,
		|	ТорговыеПлощадкиДополнительныеРеквизиты.Значение.Код";
		
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаКураторов = РезультатЗапроса.Выгрузить();
		
	КонецПроцедуры
	
	Процедура ПолучитьТаблицаЗагруженных(ТаблицаЗагруженных, ПараметрыДляЗапроса)
		
		Если ЗначениеЗаполнено(ТаблицаЗагруженных) Тогда Возврат; КонецЕсли;
		
		ПолучитьКоллекциюНаСервере(ПараметрыДляЗапроса, ТаблицаЗагруженных);
		
		
	КонецПроцедуры
	
	Процедура ПолучитьТаблицаСотрудников(ТаблицаКураторов, ТаблицаЗагруженных, ТаблицаСотрудников)
		
		Если ЗначениеЗаполнено(ТаблицаСотрудников) Тогда Возврат; КонецЕсли;
		
		стрСписокИнн = Обработки.Обучение.В_JSON(ТаблицаКураторов.ВыгрузитьКолонку("ИНН"));
		стрТаблицаЗагруженных = Обработки.Обучение.В_JSON(ТаблицаЗагруженных, Истина);
		
		Определение = Новый WSОпределения("https://orders-1c.digma.ua/zuUU/ws/cwd2.1cws?wsdl","ObmenSSL","bynthghbpf",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));	
		WS = Новый WSПрокси(Определение,"http://www.digma.ua","CommunicationWithDatabases","CommunicationWithDatabasesSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
		WS.Пользователь="ObmenSSL";
		WS.Пароль ="bynthghbpf";
		
		Рез = WS.WorkingEmployees(стрСписокИнн, стрТаблицаЗагруженных);
		
		ТаблицаСотрудников = Обработки.Обучение.Из_JSON(Рез, Истина);
		
	КонецПроцедуры
	
	Процедура ПолучитьКоллекциюНаСервере(ПараметрыДляЗапроса, ТаблицаЗагруженных, nextPageToken="")
		СоединениеFire = Обработки.Обучение.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		АдресРесурса = Обработки.Обучение.ПодготовитьАдресРесурса("GET", ПараметрыДляЗапроса.Project_ID, "edu_sotrudniki",,nextPageToken);
		
		ОтветHTTP = Обработки.Обучение.ПолучитьДокументы_GET(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса);
		
		Если ОтветHTTP.КодСостояния = 200 Тогда
			СтруктураОтвета = Обработки.Обучение.Из_JSON(ОтветHTTP.ПолучитьТелоКакСтроку());
			Если СтруктураОтвета.Свойство("documents") Тогда
				мДокументы = СтруктураОтвета.documents;
				Для Каждого Документ Из мДокументы Цикл
					Поля = Документ.fields;
					Если ТаблицаЗагруженных.Количество() = 0 Тогда
						//ТаблицаЗагруженных.Колонки.Добавить("url",Новый ОписаниеТипов("Строка"));
						Для Каждого Поле из Поля Цикл
							Для Каждого ПолеПоля из Поле.Значение Цикл
								ОписаниеТипов = ТипЗнч(ПолеПоля.Значение);	
							КонецЦикла; 
							ТаблицаЗагруженных.Колонки.Добавить(Поле.Ключ,Новый ОписаниеТипов(Строка(ОписаниеТипов)));
						КонецЦикла;
					КонецЕсли;
					нСтрока = ТаблицаЗагруженных.Добавить();
					Для Каждого Поле из Поля Цикл
						Для Каждого ПолеПоля из Поле.Значение Цикл
							нСтрока[Поле.Ключ] = ПолеПоля.Значение;	
						КонецЦикла; 
					КонецЦикла; 
				КонецЦикла;
				
				Если СтруктураОтвета.Свойство("nextPageToken") Тогда
					ПолучитьКоллекциюНаСервере(ПараметрыДляЗапроса, ТаблицаЗагруженных, СтруктураОтвета.nextPageToken);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		СоединениеFire = Неопределено;
	КонецПроцедуры
	
	Функция ПодразделениеСотрудника(стрСотрудник, Сотрудник, ЭтоКуратор, ТаблицаКураторов)
		мПодразделений = Новый Массив;
		Если ЭтоКуратор Тогда
			мСтрокПодразделений = ТаблицаКураторов.НайтиСтроки(Новый Структура("Куратор",Сотрудник));
			Для Каждого стрПодразделение Из мСтрокПодразделений Цикл
				мПодразделений.Добавить(стрПодразделение.Подразделение);
			КонецЦикла;
		Иначе
			КодТочки = СокрЛП(стрСотрудник.КодТочки);
			Точка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду(КодТочки);
			Если Точка <> Неопределено Тогда
				Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Точка, "ПодразделениеКомпании");
				мПодразделений.Добавить(Подразделение);	
			КонецЕсли;
		КонецЕсли;
		
		Возврат мПодразделений;
	КонецФункции
	
	
	
	#КонецОбласти
	
	
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли 
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция НеобходимаРегистрацияНаВыгрузку()
	
	ИсключаемыеГруппыТоваров = ОбменФронтПовтИсп.МассивНеАтивногоТовара();
	Рез = ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Родитель КАК Родитель,
		|	Номенклатура.Родитель.Родитель КАК РодительРодитель,
		|	Номенклатура.Родитель.Родитель.Родитель КАК РодительРодительРодитель,
		|	Номенклатура.Родитель.Родитель.Родитель.Родитель КАК РодительРодительРодительРодитель,
		|	Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель КАК РодительРодительРодительРодительРодитель
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Рез;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	КоличествоКолонок =  РезультатЗапроса.Колонки.Количество() - 1;
	псНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
	Для Каждого  мГруппа Из ИсключаемыеГруппыТоваров Цикл 
		Для НомерКолонки = 0 По КоличествоКолонок Цикл 
			ТекущийЭлементГруппы = Выборка[НомерКолонки]; 
			
			Если ТекущийЭлементГруппы = псНоменклатура Тогда
				Прервать;
			ИначеЕсли ТекущийЭлементГруппы = мГруппа Тогда
				Рез = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Рез;
	
КонецФункции
	
	
Процедура УстановитьАктивностьТовара()
	
	Если НЕ ЗначениеЗаполнено(Родитель) Тогда
		НеАктивный = Истина;
		Возврат;
	КонецЕсли;
	
	ТекРодитель = Родитель;
	ИсключаемыеГруппыТоваров = ОбменФронтПовтИсп.МассивНеАтивногоТовара();
	
	АктивностьТовара = Истина;
	Пока ЗначениеЗаполнено(ТекРодитель) Цикл 
		МожноПрервать = Ложь;
		Для Каждого  мГруппа Из ИсключаемыеГруппыТоваров Цикл 
			
			Если мГруппа = ТекРодитель Тогда
				АктивностьТовара = Ложь;
				МожноПрервать = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если МожноПрервать Тогда
			Прервать;
		КонецЕсли;
		ТекРодитель = ТекРодитель.Родитель;
		
	КонецЦикла;
	
	НеАктивный = Не АктивностьТовара;
	
	
КонецПроцедуры

Процедура ОбработатьДляОпенСтор()
	
	//Если перемещают из группы неиспользуемые товары зарегестрируем цены для выгрузки OS
	Если НЕ ЭтоНовый() И НеобходимаРегистрацияНаВыгрузку()  Тогда
		ДополнительныеСвойства.Вставить("РегистрацияНеИспользуемыйТовар",Истина);
	КонецЕсли;

	// Установка фл. не активности товара, не активный товар не выгружается в OS
	Если НЕ ЭтоГруппа Тогда
		УстановитьАктивностьТовара();
	КонецЕсли;
	
КонецПроцедуры



Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	Наименование       = ОбщегоНазначенияТККлиентСервер.УдалитьСпецСимволыСтроки(Наименование);
	Если Не ЭтоГруппа  Тогда
		ОсновнойШтрихКод	= СокрЛП(ОсновнойШтрихКод);
		НаименованиеПолное 	= ОбщегоНазначенияТККлиентСервер.УдалитьСпецСимволыСтроки(НаименованиеПолное);
		НаименованиеRB     	= ОбщегоНазначенияТККлиентСервер.УдалитьСпецСимволыСтроки(НаименованиеRB);
	КонецЕсли;
	
	ПроверитьУникальностьАртикула(Отказ);
	
	Если Не Отказ Тогда 
		ОбработатьДляОпенСтор();
	КонецЕсли;		
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(ОсновнойШтрихКод) Тогда
		
		НаборШК = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
		НаборШК.Отбор.Штрихкод.Установить(ОсновнойШтрихКод);
		НаборШК.Прочитать();
		
		Если НаборШК.Количество() = 0 Тогда
			НоваяЗапись = НаборШК.Добавить();
			НоваяЗапись.Номенклатура = Ссылка;
			НоваяЗапись.Штрихкод = ОсновнойШтрихКод;
			НоваяЗапись.Упаковка = ОсновнаяЕдиницаИзмерения;
			НаборШК.Записать();	
		КонецЕсли;		
		
	КонецЕсли;
	
	
	
	
КонецПроцедуры



Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЭтоГруппа Тогда 
		ОсновнойШтрихКод = "";
	КонецЕсли;

КонецПроцедуры


Процедура ПриКопировании(ОбъектКопирования)
	ОсновнойШтрихКод = "";
	ОсновнойАртикул = "";
	Артикул = "";
	ПустаяЕдиницыИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	ОсновнаяЕдиницаИзмерения = ПустаяЕдиницыИзмерения;
	ЕдиницаИзмеренияУпаковки = ПустаяЕдиницыИзмерения;
	ЕдиницаИзмеренияЯщиков   = ПустаяЕдиницыИзмерения;
	ИД_Товара = 0;
КонецПроцедуры




Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	МассивУдалить = Новый Массив;
	Если ЭтоГруппа Тогда
		МассивУдалить.Добавить("СтавкаНДС");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивУдалить);

	
КонецПроцедуры


Процедура ПроверитьУникальностьАртикула(Отказ) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Артикул = &Артикул
	               |	И Номенклатура.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		Отказ = Истина;
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Артикул ""%1"" уже используется в номенклатуре ""%2""'; uk = 'Артикул ""%1"" вже використовується в номенклатурі ""%2""'"),
				Артикул,
				Выборка.Ссылка),
			Выборка.Ссылка);
	КонецЕсли;
	
КонецПроцедуры


#КонецЕсли


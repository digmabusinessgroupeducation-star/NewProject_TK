#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


// Проверяет могут ли переданные характеристика и упавка являться характеристикой и упаковой переданной
//номенклатуры. //
//		Параметры:
//			Номенклатура - СправочникСсылка.Номенклатура - номенклатура-владелец характеристик и упаковок
//			Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - проверяемая характеристика
//			ЕденицаИзмерений - СправочникСсылка.ЕдиницыИзмерения - проверяемая упаковка
//
//		Возвращаемое значение:
//			Структура - стуктура с полями
//				Характеристика - если проверяемая характеристика подходит, то записывается она, если нет - пустая ссылка
//				ЕденицаИзмерений - если провуеряемая упаковка подходит, то записывается она, если нет - пустая ссылка
//				ХарактеристикиИспользуются - равно ИСТИНА, если по номенклатуре ведется учет характеристик
//
Функция ХарактеристикаИУпаковкаПринадлежатВладельцу(Номенклатура, Характеристика, ЕдиницаИзмерения) Экспорт
	
	СтруктураВозврата = Новый Структура("ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Коэффициент, ХарактеристикиИспользуются,ЗначениеМРЦ");
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		СтруктураВозврата.Коэффициент				 = 0;
		СтруктураВозврата.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		СтруктураВозврата.ЕдиницаИзмерения           = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
		СтруктураВозврата.ХарактеристикиИспользуются = Ложь;
		СтруктураВозврата.ЗначениеМРЦ = 0;

		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Характеристика) И (Номенклатура<>Характеристика.Владелец) Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	СтруктураВозврата.ХарактеристикаНоменклатуры = Характеристика;	
	Если ЗначениеЗаполнено(Характеристика) Тогда
		СтруктураВозврата.ЗначениеМРЦ = Характеристика.ЗначениеМРЦ;
	Иначе
		СтруктураВозврата.ЗначениеМРЦ = 0;
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(спрНоменклатура.ОсновнаяЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(спрНоменклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК Коэффициент,
	|	спрНоменклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|ГДЕ
	|	спрНоменклатура.Ссылка = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	
	Возврат СтруктураВозврата;


КонецФункции

// Возвращает значения реквизитов номеклатуры, если номенклатура не передана - возвращаются значения по умолчанию
//
//	Параметры:
//		Номенклатура - СправочникСсылка.Номенклатура - номенклатура, чьи реквизиты нужно вернуть
//	Возвращаемое значение:
//		Структура - поля СтавкаНДС, ЕдиницаИзмерения
//
Функция ЗначенияРеквизитовНоменклатуры(Номенклатура) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтавкаНДС = Выборка.СтавкаНДС;
		ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
	Иначе
		СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС;
		ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("СтавкаНДС, ЕдиницаИзмерения",
		СтавкаНДС,
		ЕдиницаИзмерения,
	);
		
	
	Возврат СтруктураРеквизитов;

КонецФункции // ЗначенияРеквизитовНоменклатуры()

// Возвращает структуру с заголовками элементов.
// Параметры:
//	Объект - СправочникОбъект.Номенклатура - элемент справочника номенклатуры
//	СтруктураВидимостиЭлементов - Структура - струтура видимости элементов
//
// Возвращаемое значение:
//	Структура - струтура заголовков элементов. Ключ - имя элемента, значения - заголовок
//
Функция СтруктураЗаголовковЭлементовФормы(Объект, СтруктураВидимостиЭлементов) Экспорт
	
	СтруктураЗаголовков = Новый Структура;
	
	
	#Область ЗаголовокиГиперссылок
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура,
	|	ШтрихкодыНоменклатуры.Характеристика,
	|	ШтрихкодыНоменклатуры.Упаковка
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
	|";
	
	Запрос.УстановитьПараметр("Номенклатура", Объект.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборки = Запрос.ВыполнитьПакет();
	
	
	Если РазделВиден("ГиперссылкаПерейтиШтрихкодыНоменклатуры", СтруктураВидимостиЭлементов) Тогда
		Количество = Выборки[0].Выбрать().Количество();
		ЗаголовокГиперссылки = Новый ФорматированнаяСтрока(НСтр("ru='Штрихкоды';uk='Штрихкоди'") + " (" + Количество + ")",,,,"ШтрихкодыНоменклатурыКоличество");
		СтруктураЗаголовков.Вставить("ГиперссылкаПерейтиШтрихкодыНоменклатуры", ЗаголовокГиперссылки);
	КонецЕсли;
	
	
	#КонецОбласти
	
	Возврат СтруктураЗаголовков;

КонецФункции

// Возвращает структуру с видимостью элементов.
// Параметры:
//	Объект - СправочникОбъект.Номенклатура - элемент справочника номенклатуры
//	РежимВидимостиПоказатьТолькоВажные - Булево - признак отображения основных реквизитов
//
// Возвращаемое значение:
//	Структура - струтура видимости элементов. Ключ - имя элемента, значения - видимость
//
Функция СтруктураВидимостиЭлементовФормы(Объект, РежимВидимостиПоказатьТолькоВажные = Ложь) Экспорт
	
	СтруктураВидимости = Новый Структура;
	
	ГиперссылкаПерейтиШтрихкодыНоменклатурыВидимость = ПравоДоступа("Просмотр",
			Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры);
		
	СтруктураВидимости.Вставить("ГиперссылкаПерейтиШтрихкодыНоменклатуры", 
		ГиперссылкаПерейтиШтрихкодыНоменклатурыВидимость);
	
	Возврат СтруктураВидимости;
	
КонецФункции

// Проверяет используются ли характеристики для переденной номенклатуры
//	Параметры:
//		Номенклатура - СправочникСсылка.Номенклатура - проверяемая номенклатура
//	Возвращаемое значение:
//		Булево - ИСТИНА, если характеристики используются
//
Функция ХарактеристикиИспользуются(Номенклатура) Экспорт
	
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Номенклатура.ИспользованиеХарактеристик;
	
КонецФункции

// Возвращает структуру данных номенклатуры по переданному штрих-коду номенклатуры
//		Параметры:
//			ШтрихКод - Строка - штрих-код товара строкового типа
//
//		Возвращаемое значение:
//			Структура - стуктура с полями
//				Номенклатура - товар, которому принадлежит переданный штрих-код
//				ЕдиницаИзмерения - единица измерения, по штрих-коду
//				Коэффициент - коэффициент единицы измерения по штрих-коду
//
Функция НайтиНоменклатуруПоШтрихКоду(ШтрихКод) Экспорт 
	
	Если ТипЗнч(ШтрихКод) <> Тип("Строка") ИЛИ ПустаяСтрока(ШтрихКод) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	               |	ШтрихкодыНоменклатуры.Упаковка КАК ЕдиницаИзмерения,
	               |	ВЫРАЗИТЬ(ШтрихкодыНоменклатуры.Упаковка КАК Справочник.ЕдиницыИзмерения).Коэффициент КАК Коэффициент
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	ШтрихкодыНоменклатуры.Штрихкод = &Штрихкод";
	
	Запрос.УстановитьПараметр("ШтрихКод", ШтрихКод);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	СтруктураПоШтрихКоду = Новый Структура("Номенклатура, ЕдиницаИзмерения, Коэффициент",
		Выборка.Номенклатура,
		Выборка.ЕдиницаИзмерения,
		Выборка.Коэффициент);
	
	Возврат СтруктураПоШтрихКоду;
КонецФункции

Функция НайтиНоменклатуруПоАртикулу(Артикул)Экспорт
	Если ТипЗнч(Артикул) <> Тип("Строка") ИЛИ ПустаяСтрока(Артикул) Тогда
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Артикул = &Артикул";
	
	Запрос.УстановитьПараметр("Артикул", Артикул);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
КонецФункции

Функция НоменклатураВыигрышЛотерея()Экспорт
	СсылкаЛотерея = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор("93f8c413-3959-11ef-bbdd-005056bc2b7a"));
	Возврат СсылкаЛотерея;
КонецФункции

Функция ПолучитьСтавкуНДС(Номенклатура,Организация = Неопределено,ТорговаяПлощадка = Неопределено,ПроизводительИмпортер=Ложь)Экспорт
	
		
	Если ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СписокНоменклатуры	= Новый Массив;
	СписокНоменклатуры.Добавить(Номенклатура);
	СтруктураСтавкаНДС = РегистрыСведений.ТК_СтавкиНДС_Номенклатуры.ПолучитьСтавкиНДС_СпискаНоменклатуры(Организация,СписокНоменклатуры,ТорговаяПлощадка).Получить(Номенклатура);
	
	Если ПроизводительИмпортер Тогда
		СтавкаНДС = СтруктураСтавкаНДС.СтавкаНДСВходящегоДокумента;
	Иначе
		СтавкаНДС = СтруктураСтавкаНДС.СтавкаНДС;
	КонецЕсли;
	
	Возврат СтавкаНДС;
	
КонецФункции

//Возвращает булево, является ли товар социальным
Функция ТоварСоциальный(Номенклатура) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Свойство.Имя = ""СоциальныйТовар""
		|	И ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
		|	И ДополнительныеСведения.Объект = &Объект";
	
	Запрос.УстановитьПараметр("Объект", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Значение ;
	КонецЕсли;
		
КонецФункции


// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Код");
	БлокируемыеРеквизиты.Добавить("Артикул");

	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов



#КонецОбласти


Функция РазделВиден(ИмяРаздела, СтруктураВидимости)
	
	Видимость = Истина;
	Если СтруктураВидимости.Свойство(ИмяРаздела) Тогда 
		Видимость = СтруктураВидимости[ИмяРаздела];	
	КонецЕсли;
	
	Возврат Видимость;
	
КонецФункции


#КонецЕсли

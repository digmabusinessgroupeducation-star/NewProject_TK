
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Заголовок 			= Параметры.Заголовок;
	Метод 						= Параметры.Метод;
	НазваниеПроцедуры 			= Параметры.НазваниеПроцедуры;
	СопроводитьКомментариями 	= 1;
	ЗаполнитьТаблицуПараметров();

КонецПроцедуры


&НаСервере
Процедура ЗаполнитьТаблицуПараметров()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Метод", Метод);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТелеграмМетоды.Параметр1С КАК Параметр1С,
	|	ТелеграмМетоды.ОбязательныйДляЗаполнения КАК ОбязательныйДляЗаполнения,
	|	ТелеграмМетоды.Тип КАК Тип,
	|	ТелеграмМетоды.Описание КАК Описание,
	|	ТелеграмМетоды.ОбязательныйДляЗаполнения КАК Добавить
	|ИЗ
	|	РегистрСведений.ТелеграмМетоды КАК ТелеграмМетоды
	|ГДЕ
	|	ТелеграмМетоды.Метод = &Метод
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмМетоды.Сортировка";
	
	ТаблицаПараметров.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры


&НаКлиенте
Функция ФорматироватьВКомментарий(Описание)
	
	Если ЗначениеЗаполнено(Описание) Тогда
		Ответ = СтрЗаменить(Описание, Символы.ПС, Символы.ПС + "// ");
		Ответ = "// " + Ответ;
		Ответ = Символы.ПС + Ответ + Символы.ПС;
	Иначе
		Ответ = "";
	КонецЕсли;
	Возврат Ответ;
	
КонецФункции


&НаКлиенте
Процедура Перенести(Команда)
	
	ВходнойФайлСтруктура = 
	"ВходнойФайлСтруктура = Новый Структура;
	|ВходнойФайлСтруктура.Вставить(""ВидИсточника"", [""Файл"", ""АдресВХранилище"", ""ДвоичныеДанные"", ""СерверТелеграм""]);
	|ВходнойФайлСтруктура.Вставить(""Источник"", [Полное имя файла, или ссылка на значение типа ДвоичныеДанные во временном хранилище, или значение типа ДвоичныеДанные, или ссылка на сервер Телеграм]);
	|ВходнойФайлСтруктура.Вставить(""ИмяФайла"", <Строка>);
	|
	|";
	
	Ответ = "Сообщение = Новый Структура;";
	Ответ = Ответ + Символы.ПС;
	
	ПоляПоиска = Новый Структура("Добавить", Истина);
	СтрокиПеренести = ТаблицаПараметров.НайтиСтроки(ПоляПоиска);
	Для Каждого СтрокаПеренести Из СтрокиПеренести Цикл
		
		Если СтрокаПеренести.Параметр1С = "ВходнойФайлСтруктура" Тогда
			Если СопроводитьКомментариями > 0 Тогда
				Ответ = Ответ + ФорматироватьВКомментарий(СтрокаПеренести.Описание);
			КонецЕсли;
			Ответ = Ответ + "Сообщение.Вставить(""ВходнойФайлСтруктура"", ВходнойФайлСтруктура);" + Символы.ПС;
			Ответ = ВходнойФайлСтруктура + Ответ;
		Иначе
			Если СопроводитьКомментариями > 0 Тогда
				Ответ = Ответ + ФорматироватьВКомментарий(СтрокаПеренести.Описание);
			КонецЕсли;
			Ответ = Ответ + СтрШаблон("Сообщение.Вставить(""%1"", <%2>);", СтрокаПеренести.Параметр1С, СтрокаПеренести.Тип) + Символы.ПС;
		КонецЕсли;
		
	КонецЦикла;
	
	Ответ = Ответ + Символы.ПС + СтрШаблон("%1(Сообщение, НакопленныеДанные);", НазваниеПроцедуры);
	
	Если СопроводитьКомментариями = 2 Тогда // Максимальный
		
		МаксимальныйКомментарий = 
		"// При формировании сообщения доступна структура НакопленныеДанные
		|// Ключи структуры:
		|// НакопленныеДанные.ДанныеБота.Бот — текущий бот, <СправочникСсылка.ТелеграмБоты>
		|// НакопленныеДанные.ДанныеБота.Жетон — жетон бота, <строка>
		|// НакопленныеДанные.ДанныеБота.Имя — представление бота, <Строка>
		|// НакопленныеДанные.ДанныеБота.Смещение — последний обработанный ботом идентификатор обновления, <Число>
		|// НакопленныеДанные.ИдентификаторОбновления — идентификатор текущего обновления, <число>
		|// НакопленныеДанные.ИдентификаторСообщения — идентификатр сообщения, <число>
		|// НакопленныеДанные.ИдентификаторЧата — идентификатор чата, <число>
		|// НакопленныеДанные.ВидВходящегоОбновления - вид входящего обновления, определямый на основании содержания обновления, <Перечисления.ТелеграмВидыВходящихОбновлений> (чаще всего Сообщение)
		|// НакопленныеДанные.Обновление — обрабатываемое обновление https://core.telegram.org/bots/api#update, <ОбъектXDTO>
		|// НакопленныеДанные.Сообщение — сообщение, https://core.telegram.org/bots/api#message, <ОбъектXDTO> или НЕОПРЕДЕЛЕНО
		|// НакопленныеДанные.ТекстСообщения  — текст сообщения, <строка> или НЕОПРЕДЕЛЕНО
		|// НакопленныеДанные.НастройкиПользователя - структура с настройками пользователя из регистра сведений ТелеграмНастройкиПользователя
		|// НакопленныеДанные.ОтветКонтекстнойКлавиатуры — ответ контекстной клавиатуры, если ВидВходящегоОбновления = Перечисления.ТелеграмВидыВходящихОбновлений.ОтветКонтекстнойКлавиатуры, <строка> или НЕОПРЕДЕЛЕНО
		|// 
		|";
		
		Ответ = МаксимальныйКомментарий + Ответ;
		
	КонецЕсли;
	
	ЭтаФорма.Закрыть(Ответ);
	
КонецПроцедуры
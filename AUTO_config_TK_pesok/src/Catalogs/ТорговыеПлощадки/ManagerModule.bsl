


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Ссылка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;

	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Код");
	БлокируемыеРеквизиты.Добавить("ПодразделениеКомпании");

	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти



#Область ОбновлениеИнформационнойБазы

Процедура СоздатьВидыКонтактнойИнформацци() Экспорт
	
		// Контактная информация справочника "ТорговыеПлощадки"
	
	ГруппаКонтактнойИнформации = УправлениеКонтактнойИнформацией.ПараметрыГруппыВидаКонтактнойИнформации();
	ГруппаКонтактнойИнформации.Имя          = "СправочникТорговыеПлощадки";
	ГруппаКонтактнойИнформации.Наименование = НСтр("ru = 'Контактная информация справочника ""Торговые площадки""';uk='Контактна інформація довідника ""Торгові майданчики""'");
	
	Группа = УправлениеКонтактнойИнформацией.УстановитьСвойстваГруппыВидаКонтактнойИнформации(ГруппаКонтактнойИнформации);
	
	
	Вид = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	Вид.Имя          = "ФактическийАдресОбъекта";
	Вид.Группа       = Группа;
	Вид.Наименование = НСтр("ru = 'Фактический адрес Торговой площадки';uk='Фактична адреса Торгової площадки'");
	Вид.Порядок      = 1;
	Вид.МожноИзменятьСпособРедактирования = Истина;
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(Вид);
	

	
КонецПроцедуры


#КонецОбласти


Функция ОрганизацияТорговойПлощадки(ТорговыеПлощадки, НаДату = Неопределено) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация
	               |ИЗ
	               |	РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(&НаДату, ТорговаяПлощадка = &ТорговаяПлощадка) КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних";
	
	Запрос.УстановитьПараметр("НаДату", ?(НаДату <> Неопределено, НаДату, Неопределено));
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговыеПлощадки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Организация = Выборка.Организация;
	Иначе
		Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
	Возврат Организация;
	
КонецФункции

Функция НайтиТорговуюПлощадкуПоGlobal_ID(Знач ИД, Знач Подразделение = Неопределено)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ТорговыеПлощадки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|ГДЕ
	|	ТорговыеПлощадки.Global_ID = &Global_ID
	|	И ВЫБОР
	|			КОГДА &ПодразделениеКомпании = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ТорговыеПлощадки.ПодразделениеКомпании = &ПодразделениеКомпании
	|		КОНЕЦ
	|	И ТорговыеПлощадки.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Global_ID", ИД);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	 
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
	
	
КонецФункции

Функция НайтиТорговуюПлощадкуПоGLN(Знач GLN)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ТорговыеПлощадки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|ГДЕ
	|	ТорговыеПлощадки.GLN = &GLN
	|	И ТорговыеПлощадки.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("GLN", GLN);

	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	 
	Возврат ВыборкаДетальныеЗаписи.Ссылка;

КонецФункции

Функция НайтиТорговуюПлощадкуПоКоду(Знач ИД)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ТорговыеПлощадки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|ГДЕ
	|	ТорговыеПлощадки.Код = &ИД";
	
	Запрос.УстановитьПараметр("ИД", ИД);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	 
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
	
	
	
КонецФункции

Функция ЭтоТекущиеОстаткиТП(ТорговаяПлощадка)Экспорт 
	Возврат ТорговаяПлощадка.РегистрацияОС_ТекущиеОстатки;
КонецФункции

Процедура ПолучитьМассивИерархииТорговыхПлощадок(МассивТП,ТорговаяПлощадка)Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Ссылка КАК Ссылка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТорговыеПлощадки.Ссылка КАК Ссылка
		|	ИЗ
		|		Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		|	ГДЕ
		|		ТорговыеПлощадки.Ссылка В ИЕРАРХИИ(&ТорговаяПлощадка)) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_НастройкиТСД КАК ТК_НастройкиТСД
		|		ПО ВложенныйЗапрос.Ссылка = ТК_НастройкиТСД.ТорговаяПлощадка
		|			И (ТК_НастройкиТСД.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиТСД.ЗапретНаВыборТорговойПлощадки))
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ЕСТЬNULL(ТК_НастройкиТСД.Значение, ЛОЖЬ) = ИСТИНА
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивТП.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	
КонецПроцедуры

Функция ПодразделениеОпенстор(ТорговаяПлощадка)Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТорговыеПлощадки.ПодразделениеКомпании КАК ПодразделениеКомпании
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|ГДЕ
	|	ТорговыеПлощадки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.ПодразделенияКомпании.ПустаяСсылка();
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	Подразделение = ВыборкаДетальныеЗаписи.ПодразделениеКомпании;
	
	Если Справочники.ПодразделенияКомпании.ЭтоАттика(Подразделение) Тогда
		Подразделение  = Справочники.ПодразделенияКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор("aca6e0b4-d29a-11e6-80d2-0050569272d9"));
	КонецЕсли;
	
	Возврат Подразделение;
	
КонецФункции




#КонецОбласти

#КонецЕсли

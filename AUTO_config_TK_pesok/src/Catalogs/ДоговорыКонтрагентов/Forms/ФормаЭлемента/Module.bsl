
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "СрокиОплатПоДоговорам", Объект.Ссылка);
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьНаборЗаписейИстории(ЭтаФорма, "СрокиОплатПоДоговорам", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "УсловияАрендыПоДоговорам", Объект.Ссылка);
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьНаборЗаписейИстории(ЭтаФорма, "УсловияАрендыПоДоговорам", Объект.Ссылка);

	ОбновитьПолеСрокиОплатПоДоговорамПериодСтрокой(ЭтаФорма);
	ОбновитьПолеУсловияАрендыПоДоговорамПериодСтрокой(ЭтаФорма);

	ЦеныПоДоговору.Параметры.УстановитьЗначениеПараметра("ДоговорКонтрагента",Объект.Ссылка);
	
	Если РольДоступна("ТК_ЧтениеУсловияАренды") или РольДоступна("ТК_ДобавлениеИзменениеУсловияАренды") или РольДоступна("ПолныеПрава") Тогда
		Элементы.ГруппаАренда.Видимость = Истина;
	Иначе
		Элементы.ГруппаАренда.Видимость = Ложь;
	КонецЕсли; 
	
	Если РольДоступна("ТК_ПравоКонроляЗадолжености") Тогда
		Элементы.ОтключитьКонтрольЗадолжености.Видимость = Истина;
	Иначе		
		Элементы.ОтключитьКонтрольЗадолжености.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаполнитьПредставлениеЗадолженности();	
	ЗаполнитьПараметрВидДоговораОсн();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПоказатьПодтверждениеЗакрытияФормыСФайлами(ЭтотОбъект, Отказ, ЗавершениеРаботы, Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);	
	// Конец ТК_УправлениеДоступом
	
	ЗаполнитьПредставлениеЗадолженности();
	ЗаполнитьПараметрВидДоговораОсн();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
//	ЗапроситьРежимИзмененияПараметровОплат(СрокиОплатПоДоговорам.Период, Отказ);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьДанныеПоСрокамОплат(ТекущийОбъект);
	ЗаписатьДанныеПоУсловиямАренды(ТекущийОбъект);
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовШапкиФормы

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
				ПараметрыПеретаскивания, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
				ПараметрыПеретаскивания, СтандартнаяОбработка);

КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

&НаКлиенте
Процедура ЗадолженностьНажатие(Элемент)
	
	Если Параметры.Ключ.Пустая() Тогда 
		Возврат;
	КонецЕсли;
	
	ИмяОтчета = ПолучитьИмяОтчетаВзаиморасчетов(ВидДоговораОсн);
	
	Если Не ЗначениеЗаполнено(ИмяОтчета) Тогда 
		Возврат;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура);
	ПараметрыФормы.Отбор.Вставить("ДоговорВзаиморасчетов", Объект.Ссылка);
	ПараметрыФормы.Вставить("Заголовок", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Движение по сделкам %1'; uk = 'Рух по угодам %1'"),
												Объект.Ссылка));	
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ДвиженияПоСделкамДоговоров");
	
	ОткрытьФорму(
		"Отчет."+ИмяОтчета+".Форма", 
		ПараметрыФормы,
		ЭтаФорма,
		Истина,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	ЗаполнитьПараметрВидДоговораОсн();
	СформироватьРабочееНаименованиеДоговора();
КонецПроцедуры

&НаКлиенте
Процедура ТипОплатыПоДоговоруТипОплатыПриИзменении(Элемент)

КонецПроцедуры


&НаКлиенте
Процедура СрокиОплатПоДоговорамПериодСтрокойПриИзменении(Элемент)
	ВводПериодаПриИзменении(ЭтаФорма,
								   "СрокиОплатПоДоговорам.Период",
								   "СрокиОплатПоДоговорамПериодСтрокой",
								   Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокиОплатПоДоговорамПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ВводПериодаНачалоВыбора(
		ЭтаФорма,
		ЭтаФорма,
		"СрокиОплатПоДоговорам.Период",
		"СрокиОплатПоДоговорамПериодСтрокой");

КонецПроцедуры


&НаКлиенте
Процедура ВводПериодаПриИзменении(РедактируемыйОбъект, ПутьРеквизита, ПутьРеквизитаПредставления, Модифицированность = Ложь, Аренда= Ложь)
	
	ЗначениеПредставления = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления);
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита);
	
	ДатаКакМесяцПодобратьДатуПоТексту(ЗначениеПредставления, Значение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект,
																				ПутьРеквизитаПредставления,
																				ПолучитьПредставлениеДаты(Значение));
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	
	Модифицированность = Истина;
	
	Если Аренда Тогда
		ЗапроситьРежимИзмененияПараметровАренды(УсловияАрендыПоДоговорам.Период, Ложь);
	Иначе 
		ЗапроситьРежимИзмененияПараметровОплат(СрокиОплатПоДоговорам.Период, Ложь);
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиенте
Процедура ВводПериодаНачалоВыбора(Форма,
											РедактируемыйОбъект,
											ПутьРеквизита,
											ПутьРеквизитаПредставления,
											ИзменитьМодифицированность = Истина,
											ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("РедактируемыйОбъект", РедактируемыйОбъект);
	ДополнительныеПараметры.Вставить("ПутьРеквизита", ПутьРеквизита);
	ДополнительныеПараметры.Вставить("ПутьРеквизитаПредставления", ПутьРеквизитаПредставления);
	ДополнительныеПараметры.Вставить("ИзменитьМодифицированность", ИзменитьМодифицированность);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ПоясняющийТекст = НСтр("ru='Выберите дату начала действия';uk='Виберіть дату початку дії'") + " ";
	
	Оповещение = Новый ОписаниеОповещения("ВводПериодаНачалоВыбораЗавершение",ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
	"ОбщаяФорма.ВыборДаты",
	Новый Структура("ПоясняющийТекст, НачальноеЗначение", ПоясняющийТекст),
 	,,,,
	Оповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		
КонецПроцедуры

&НаКлиенте
Процедура ВводПериодаНачалоВыбораА(Форма,
											РедактируемыйОбъект,
											ПутьРеквизита,
											ПутьРеквизитаПредставления,
											ИзменитьМодифицированность = Истина,
											ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("РедактируемыйОбъект", РедактируемыйОбъект);
	ДополнительныеПараметры.Вставить("ПутьРеквизита", ПутьРеквизита);
	ДополнительныеПараметры.Вставить("ПутьРеквизитаПредставления", ПутьРеквизитаПредставления);
	ДополнительныеПараметры.Вставить("ИзменитьМодифицированность", ИзменитьМодифицированность);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ПоясняющийТекст = НСтр("ru='Выберите дату начала действия';uk='Виберіть дату початку дії'") + " ";
	
	Оповещение = Новый ОписаниеОповещения("ВводПериодаНачалоВыбораЗавершениеА",ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
	"ОбщаяФорма.ВыборДаты",
	Новый Структура("ПоясняющийТекст, НачальноеЗначение", ПоясняющийТекст),
 	,,,,
	Оповещение,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		
КонецПроцедуры

&НаКлиенте
Процедура ВводПериодаНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	РедактируемыйОбъект = ДополнительныеПараметры.РедактируемыйОбъект;
	ПутьРеквизита = ДополнительныеПараметры.ПутьРеквизита;
	ПутьРеквизитаПредставления = ДополнительныеПараметры.ПутьРеквизитаПредставления;
	ИзменитьМодифицированность = ДополнительныеПараметры.ИзменитьМодифицированность;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Значение = ВыбранноеЗначение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	Представление = ПолучитьПредставлениеДаты(Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления, Представление);
	
	Если ИзменитьМодифицированность Тогда 
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	КонецЕсли;
	ЗапроситьРежимИзмененияПараметровОплат(СрокиОплатПоДоговорам.Период, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ВводПериодаНачалоВыбораЗавершениеА(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	РедактируемыйОбъект = ДополнительныеПараметры.РедактируемыйОбъект;
	ПутьРеквизита = ДополнительныеПараметры.ПутьРеквизита;
	ПутьРеквизитаПредставления = ДополнительныеПараметры.ПутьРеквизитаПредставления;
	ИзменитьМодифицированность = ДополнительныеПараметры.ИзменитьМодифицированность;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Значение = ВыбранноеЗначение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизита, Значение);
	Представление = ПолучитьПредставлениеДаты(Значение);
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(РедактируемыйОбъект, ПутьРеквизитаПредставления, Представление);
	
	Если ИзменитьМодифицированность Тогда 
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	КонецЕсли;
	ЗапроситьРежимИзмененияПараметровАренды(УсловияАрендыПоДоговорам.Период, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаДействияПриИзменении(Элемент)
	СформироватьРабочееНаименованиеДоговора();
	СформироватьРабочееНаименованиеДоговораДляПечати();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Объект.ДатаНачалаДействия = Объект.Дата;
	СформироватьРабочееНаименованиеДоговора();
	СформироватьРабочееНаименованиеДоговораДляПечати();
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	СформироватьРабочееНаименованиеДоговора();
	СформироватьРабочееНаименованиеДоговораДляПечати();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияДействияПриИзменении(Элемент)
	СформироватьРабочееНаименованиеДоговора();
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)

	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);

КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОбщегоНазначенияТККлиент.РедактироватьПериод(Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачалаДействия", "ДатаОкончанияДействия"));
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИзменений(Команда)
	ТолькоПросмотрИстории = Ложь;
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию("СрокиОплатПоДоговорам",Объект.Ссылка,	ЭтаФорма,
	ТолькоПросмотрИстории);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияИзменений1(Команда)
	
	ТолькоПросмотрИстории = Ложь;
	РедактированиеПериодическихСведенийКлиент.ОткрытьИсторию("УсловияАрендыПоДоговорам",Объект.Ссылка,	ЭтаФорма,
	ТолькоПросмотрИстории);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКонтрольЗадолжености(Команда)
	
	УстановитьКонтрольЗадолжености();
	ЭтаФорма.Закрыть();
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента", Новый Структура("Ключ", Объект.Ссылка));
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Функция СписокМесяцевПоСтроке(Текст)
	
	СписокМесяцев  = Новый СписокЗначений;
	Месяцы         = Новый Соответствие;
	МесяцыВозврата = Новый Массив;
	
	Для Счетчик = 1 По 12 Цикл
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='ММММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
		Представление = Формат(Дата(2000, Счетчик, 1), "ДФ='МММ'");
		СписокМесяцев.Добавить(Счетчик, Представление);
	КонецЦикла;
	
	Для Каждого ЭлементСписка Из СписокМесяцев Цикл
		Если ВРег(Текст) = ВРег(Лев(ЭлементСписка.Представление, СтрДлина(Текст))) Тогда
			Месяцы[ЭлементСписка.Значение] = 0;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из Месяцы Цикл
		МесяцыВозврата.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Возврат МесяцыВозврата;
	
КонецФункции

&НаКлиенте
Функция ДатаКакМесяцПодобратьДатуПоТексту(Текст, ДатаПоТексту = НеОпределено)
	
	СписокВозврата = Новый СписокЗначений;
	ТекущийГод = Год(ДатаСеанса());
	
	Если ПустаяСтрока(Текст) Тогда
		ДатаПоТексту = Дата(1, 1, 1);
		Возврат СписокВозврата;
	КонецЕсли;
	
	Если Найти(Текст, ".") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ".");
	ИначеЕсли Найти(Текст, ",") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, ",");
	ИначеЕсли Найти(Текст, "-") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "-");
	ИначеЕсли Найти(Текст, "/") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "/");
	ИначеЕсли Найти(Текст, "\") <> 0 Тогда
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "\");
	Иначе
		Подстроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ");
	КонецЕсли;
	
	Если Подстроки.Количество() = 1 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Текст) Тогда
			МесяцЧислом = Число(Текст);
			Если МесяцЧислом >= 1 и МесяцЧислом <=12 Тогда
				ДатаПоТексту = Дата(ТекущийГод, МесяцЧислом, 1);
				Если СтрДлина(Текст) = 1 Тогда
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='М/гг'"));
				Иначе
					СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММ/гг'"));
				КонецЕсли;
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
		Иначе
			СписокМесяцев = СписокМесяцевПоСтроке(Текст);
			Для Каждого Месяц Из СписокМесяцев Цикл
				ДатаПоТексту = Дата(ТекущийГод, Месяц, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Подстроки.Количество() = 2 Тогда
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[1]) Тогда
			
			Если ПустаяСтрока(Подстроки[1]) Тогда
				ГодЧислом = 0;
				Подстроки[1] = "0";
				ТекстВозврата = Текст + "0";
			Иначе
				ГодЧислом = Число(Подстроки[1]);
				ТекстВозврата = "";
			КонецЕсли;
			
			Если ГодЧислом > 3000 Тогда
				Возврат СписокВозврата;
			КонецЕсли;
			
			Если СтрДлина(Подстроки[1]) <= 1 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 3) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 2 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 2) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 3 Тогда
				ГодЧислом = Число(Лев(Формат(ТекущийГод, "ЧГ="), 1) + Подстроки[1]);
			ИначеЕсли СтрДлина(Подстроки[1]) = 4 Тогда
				ГодЧислом = Число(Подстроки[1]);
			КонецЕсли;                    
			
		Иначе
			
			Возврат СписокВозврата;
			
		КонецЕсли;                
		Если ЗначениеЗаполнено(Подстроки[0]) И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Подстроки[0]) Тогда
			
			МесяцЧислом = Число(Подстроки[0]);
			Если МесяцЧислом >= 1 и МесяцЧислом <= 12 Тогда
				ДатаПоТексту = Дата(ГодЧислом, МесяцЧислом, 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Возврат СписокВозврата;
			КонецЕсли;                
			
		Иначе
			
			СписокМесяцев = СписокМесяцевПоСтроке(Подстроки[0]);
			
			Если СписокМесяцев.Количество() = 1 Тогда
				ДатаПоТексту = Дата(ГодЧислом, СписокМесяцев[0], 1);
				СписокВозврата.Добавить(Формат(ДатаПоТексту, "ДФ='ММММ гггг'"));
			Иначе
				Для Каждого Месяц Из СписокМесяцев Цикл
					ДатаПоТексту = Дата(ГодЧислом, Месяц, 1);
					СписокВозврата.Добавить(Формат(Дата(ГодЧислом, Месяц, 1), "ДФ='ММММ гггг'"));
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат СписокВозврата;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеДаты(Знач ДатаНачала)
	
	Возврат Формат(ДатаНачала, "ДФ=dd.MM.yyyy");
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОтредактированаИстория" И Объект.Ссылка = Источник
		И Параметр.ИмяРегистра = "СрокиОплатПоДоговорам" Тогда
		
		Если СрокиОплатПоДоговорамНаборЗаписейПрочитан Тогда
			НастройкиСрокаОплатВведены = Истина;
			РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
			ЭтаФорма,
			Объект.Ссылка,
			ИмяСобытия,
			Параметр,
			Источник);
			
			ОбновитьПолеСрокиОплатПоДоговорамПериодСтрокой(ЭтаФорма);
			
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОтредактированаИстория" И Объект.Ссылка = Источник
		И Параметр.ИмяРегистра = "УсловияАрендыПоДоговорам" Тогда
		
		Если УсловияАрендыПоДоговорамНаборЗаписейПрочитан Тогда
			НастройкиУсловийАрендыВведены = Истина;
			РедактированиеПериодическихСведенийКлиент.ОбработкаОповещения(
			ЭтаФорма,
			Объект.Ссылка,
			ИмяСобытия,
			Параметр,
			Источник);
			
			ОбновитьПолеУсловияАрендыПоДоговорамПериодСтрокой(ЭтаФорма);
			
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	

КонецПроцедуры

&НаСервере
Функция ЗаполнитьПредставлениеЗадолженности()
	
	Элементы.Задолженность.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Текущий долг составляет: %1 %2'; uk = 'Поточний борг складає: %1 %2'"),
											Формат(ВзаиморасчетыСервер.ПолучитьЗадолженностьПоДоговору(Объект.Ссылка), "ЧДЦ=2; ЧН="),
											Объект.ВалютаВзаиморасчетов);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПараметрВидДоговораОсн()
	
	ВидДоговораОсн = Объект.ВидДоговора.ВидДоговора;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРабочееНаименованиеДоговора()
	
	текВидДоговора = ?(ЗначениеЗаполнено(Объект.ВидДоговора), НРег(СокрЛП(Объект.ВидДоговора)), НСтр("ru = 'Договор'; uk = 'Договір'"));	
	текНомер = СокрЛП(Объект.Номер);
	
	текПериод = "";
	Если ЗначениеЗаполнено(Объект.ДатаНачалаДействия) Тогда 
		текПериод = НСтр("ru = 'от'; uk = 'від'") + " " + Формат(Объект.ДатаНачалаДействия, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ДатаОкончанияДействия) Тогда 
		текПериод = текПериод + ?(Не ПустаяСтрока(текПериод), " ", "") + НСтр("ru = 'по'; uk = 'до'") + " " + Формат(Объект.ДатаОкончанияДействия, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	Объект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"%1 %2 %3",
							текВидДоговора,
							текНомер,
							текПериод);
							
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРабочееНаименованиеДоговораДляПечати()
	
	текВидДоговора = ?(ЗначениеЗаполнено(Объект.ВидДоговора), НРег(СокрЛП(Объект.ВидДоговора)), НСтр("ru = 'Договор'; uk = 'Договір'"));	
	текНомер = СокрЛП(Объект.Номер);
	
	текПериод = "";
	Если ЗначениеЗаполнено(Объект.ДатаНачалаДействия) Тогда 
		текПериод = НСтр("ru = 'от'; uk = 'від'") + " " + Формат(Объект.ДатаНачалаДействия, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	Объект.НаименованиеДляПечати = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									"Договор %1 %2 %3",
									текВидДоговора,
									текНомер,
									текПериод);
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяОтчетаВзаиморасчетов(Знач ВидДоговора)
	
	ИмяОтчета = Неопределено;	
	ПолныеПрава = Пользователи.ЭтоПолноправныйПользователь();
	
	Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоров.Покупка")
		И (ПолныеПрава ИЛИ РольДоступна("ТК_ИспользваниеОтчетаВзаиморасчетыСПоставщиками")) Тогда 
		
		ИмяОтчета = "ВзаиморасчетыСПоставщиками";	
		
	ИначеЕсли ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоров.Продажа")
		И (ПолныеПрава ИЛИ РольДоступна("ТК_ИспользваниеОтчетаВзаиморасчетыСПокупателями")) Тогда 
		
		ИмяОтчета = "ВзаиморасчетыСПокупателями";
		
	КонецЕсли;
		
	Возврат ИмяОтчета;
	
КонецФункции

&НаСервере
Функция ДатаСеанса()
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ТекущаяДатаСеанса();
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
КонецФункции

&НаСервере
Процедура ПрочитатьНаборЗаписейПериодическихСведений(ИмяРегистра, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, ИмяРегистра, ВедущийОбъект);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗапроситьРежимИзмененияПараметровОплат(ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ЗапроситьРежимИзмененияСрокиОплатПоДоговоруЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстКнопкиДа = НСтр("ru='Изменить параметры оплаты';uk='Змінити параметри оплати'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru =  'При редактировании изменились параметры оплаты по договору.
					|Если просто исправлены прежние данные (они были ошибочны), нажмите ""Исправлена ошибка"".
					|Если параметры оплаты изменились с %1, нажмите ""Изменить параметры оплаты""'"), 
		Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"));
	
	ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "СрокиОплатПоДоговорам", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияПараметровАренды(ДатаИзменения, Отказ, ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура("ОповещениеЗавершения", ОповещениеЗавершения);
	Оповещение = Новый ОписаниеОповещения("ЗапроситьРежимИзмененияУсловияАрендыПоДоговорамЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстКнопкиДа = НСтр("ru='Изменить условия аренды';uk='Змінити дані оренди'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru =  'При редактировании изменились параметры оплаты по договору.
					|Если просто исправлены прежние данные (они были ошибочны), нажмите ""Исправлена ошибка"".
					|Если параметры оплаты изменились с %1, нажмите ""Изменить параметры оплаты""'"), 
		Формат(ДатаИзменения, "ДФ='д ММММ гггг ""г""'"));
	
	ЗапроситьРежимИзмененияРегистра(ЭтаФорма, "УсловияАрендыПоДоговорам", ТекстВопроса, ТекстКнопкиДа, Отказ, Оповещение);
	
КонецПроцедуры
&НаКлиенте
Процедура ЗапроситьРежимИзмененияРегистра(Форма, ИмяРегистра, ТекстЗапроса, ТекстКнопкиДа, Отказ, ОповещениеЗавершения = Неопределено)
	// Требуется запрашивать пользователя об изменении только если еще не принято решение, что запись - новая
	Если Форма[ИмяРегистра + "НоваяЗапись"] = Истина Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	// Требуется запрашивать пользователя об изменении только если задана дата записи
	Если Не ЗначениеЗаполнено(Форма[ИмяРегистра].Период) Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// Требуется запрашивать пользователя об изменении только если была считана прежняя запись
	Если Не ЗначениеЗаполнено(Форма[ИмяРегистра + "Прежняя"].Период) Тогда
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	//ИзменилсяПериод = (Форма[ИмяРегистра].Период <> Форма[ИмяРегистра + "Прежняя"].Период);
	ИзменилисьДанные = Ложь;
	Для Каждого Поле Из Форма[ИмяРегистра + "Прежняя"] Цикл
		//Если Поле.Ключ = "Период" Тогда
		//	Продолжить;
		//КонецЕсли;
		ИзменилисьДанные = Форма[ИмяРегистра][Поле.Ключ] <> Поле.Значение;
		Если ИзменилисьДанные Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Требуется запрашивать пользователя об изменении - изменили и текущие данные, и дату записи
	Если ИзменилисьДанные  Тогда
		
		Кнопки = Новый СписокЗначений();
		Кнопки.Добавить(КодВозвратаДиалога.Нет,  	НСтр("ru='Исправлена ошибка';uk='Виправлена помилка'"));
		Кнопки.Добавить(КодВозвратаДиалога.Да, 		ТекстКнопкиДа);
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, 	НСтр("ru='Отмена';uk='Відмінити'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма",                Форма);
		ДополнительныеПараметры.Вставить("Отказ",                Отказ);
		ДополнительныеПараметры.Вставить("ИмяРегистра",          ИмяРегистра);
		ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ЗапроситьРежимИзмененияРегистраОбработкаОтвета", ЭтотОбъект, ДополнительныеПараметры), 
			ТекстЗапроса, 
			Кнопки, ,
			КодВозвратаДиалога.Отмена);
		
		Возврат;
					
	Иначе
		
		Если ОповещениеЗавершения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияРегистраОбработкаОтвета(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Форма                = ДополнительныеПараметры.Форма;
	Отказ                = ДополнительныеПараметры.Отказ;
	ИмяРегистра          = ДополнительныеПараметры.ИмяРегистра;
	ОповещениеЗавершения = ДополнительныеПараметры.ОповещениеЗавершения;	
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		Форма[ИмяРегистра + "НоваяЗапись"] = Истина;
	КонецЕсли;
	
	Если ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияСрокиОплатПоДоговоруЗавершение(Отказ, ДополнительныеПараметры) Экспорт 
	
	Если НЕ Отказ Тогда
		НастройкиСрокаОплатВведены = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРежимИзмененияУсловияАрендыПоДоговорамЗавершение(Отказ, ДополнительныеПараметры) Экспорт 
	
	Если НЕ Отказ Тогда
		НастройкиУсловийАрендыВведены = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеПоСрокамОплат(ТекущийОбъект)
	
	Если НЕ НастройкиСрокаОплатВведены Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.СрокиОплатПоДоговорам) Тогда
		
		Если Не ЗначениеЗаполнено(СрокиОплатПоДоговорам.ДоговорКонтрагента) Тогда
			СрокиОплатПоДоговорам.ДоговорКонтрагента = ТекущийОбъект.Ссылка;
		КонецЕсли;
		
		РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма,
																					"СрокиОплатПоДоговорам",
																					ТекущийОбъект.Ссылка);
		РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма,
																					"СрокиОплатПоДоговорам",
																					ТекущийОбъект.Ссылка);
		Если Не ЗначениеЗаполнено(СрокиОплатПоДоговорам.Период) Тогда
			СрокиОплатПоДоговорам.Период = Дата(0001,1,1);
		КонецЕсли;
		ОбновитьПолеСрокиОплатПоДоговорамПериодСтрокой(ЭтаФорма);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеПоУсловиямАренды(ТекущийОбъект)
	
	Если НЕ НастройкиУсловийАрендыВведены Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.УсловияАрендыПоДоговорам) Тогда
		
		Если Не ЗначениеЗаполнено(УсловияАрендыПоДоговорам.ДоговорКонтрагента) Тогда
			УсловияАрендыПоДоговорам.ДоговорКонтрагента = ТекущийОбъект.Ссылка;
		КонецЕсли;
		
		РедактированиеПериодическихСведений.ЗаписатьЗаписьПослеРедактированияВФорме(ЭтаФорма,
																					"УсловияАрендыПоДоговорам",
																					ТекущийОбъект.Ссылка);
		РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма,
																					"УсловияАрендыПоДоговорам",
																					ТекущийОбъект.Ссылка);
		Если Не ЗначениеЗаполнено(УсловияАрендыПоДоговорам.Период) Тогда
			УсловияАрендыПоДоговорам.Период = Дата(0001,1,1);
		КонецЕсли;
		ОбновитьПолеУсловияАрендыПоДоговорамПериодСтрокой(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолеСрокиОплатПоДоговорамПериодСтрокой(Форма)
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "СрокиОплатПоДоговорам.Период");
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма,"СрокиОплатПоДоговорамПериодСтрокой", ПолучитьПредставлениеДаты(Значение));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПолеУсловияАрендыПоДоговорамПериодСтрокой(Форма)
	Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "УсловияАрендыПоДоговорам.Период");
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Форма,"УсловияАрендыПоДоговорамПериодСтрокой", ПолучитьПредставлениеДаты(Значение));
КонецПроцедуры

&НаКлиенте
Процедура УсловияАрендыПоДоговорамПериодСтрокойПриИзменении(Элемент)
	ВводПериодаПриИзменении(ЭтаФорма,
								   "УсловияАрендыПоДоговорам.Период",
								   "УсловияАрендыПоДоговорамПериодСтрокой",
								   Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияАрендыПоДоговорамПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВводПериодаНачалоВыбораА(
		ЭтаФорма,
		ЭтаФорма,
		"УсловияАрендыПоДоговорам.Период",
		"УсловияАрендыПоДоговорамПериодСтрокой");
КонецПроцедуры


&НаСервере
Процедура УстановитьКонтрольЗадолжености()
	УстановитьПривилегированныйРежим(Истина);
	
	Объект.НеПроверятьПросроченнуюЗадолженность = НЕ Объект.НеПроверятьПросроченнуюЗадолженность;
	ЭтотОбъект.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры 




#КонецОбласти















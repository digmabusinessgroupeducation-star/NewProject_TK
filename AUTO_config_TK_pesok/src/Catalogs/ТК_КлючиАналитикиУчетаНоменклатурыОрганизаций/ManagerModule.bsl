#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает пометку на удаление для найденных элементов справочника.
//
// Параметры:
//	СтруктураПараметров - Структура - Параметры выбора элементов справочника
//	ПометкаУдаления - Булево - Признак установки пометки на удаление
//
Процедура УстановитьПометкуУдаления(СтруктураПараметров, ПометкаУдаления) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления <> &ПометкаУдаления
	|";
	Запрос = Новый Запрос;
	
	Если СтруктураПараметров.Свойство("Организация") Тогда

		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Организация = &Организация";

	КонецЕсли;

	
	Если СтруктураПараметров.Свойство("Номенклатура") Тогда

		Запрос.УстановитьПараметр("Номенклатура", СтруктураПараметров.Номенклатура);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Номенклатура = &Номенклатура";

	КонецЕсли;
	Если СтруктураПараметров.Свойство("ХарактеристикаНоменклатуры") Тогда

		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтруктураПараметров.ХарактеристикаНоменклатуры);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры";

	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);

	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаменаДублейКлючейАналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	Справочник.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций КАК ДанныеСправочника
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций КАК ДанныеРегистра
	|	ПО
	|		ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций КАК Аналитика
	|	ПО
	|		ДанныеСправочника.Организация		= Аналитика.Организация
	|		И ДанныеСправочника.Номенклатура	= Аналитика.Номенклатура
	|		И ДанныеСправочника.ХарактеристикаНоменклатуры			= Аналитика.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL
	|");
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			
			Если Не Выборка.ПометкаУдаления Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Попытка
					СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				Исключение
				КонецПопытки;
			КонецЕсли;

		КонецЦикла;
		ОбщегоНазначенияТК.ЗаменитьСсылки(СоответствиеАналитик);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
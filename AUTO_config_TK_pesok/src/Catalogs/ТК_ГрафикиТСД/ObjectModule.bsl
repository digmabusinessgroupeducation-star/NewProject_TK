

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
	
	Если ЭтоНовый() Тогда
		Автор 				  = Пользователи.ТекущийПользователь();

		ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивУдалить = Новый Массив;
	Если Периодичность = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.НаДату Тогда
		
		МассивУдалить.Добавить("ДниНедели");
		МассивУдалить.Добавить("ДатаЧисло");
	ИначеЕсли Периодичность = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.НаДатуМесяца Тогда
		МассивУдалить.Добавить("Дата");
		МассивУдалить.Добавить("ДниНедели");
	Иначе
		МассивУдалить.Добавить("Дата");
		МассивУдалить.Добавить("ДатаЧисло");


	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивУдалить);

	
	
	Если Периодичность = Перечисления.ТК_ВидыПериодичностиГрафикаТСД.НаДатуМесяца Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_ГрафикиТСДТорговыеОбъекты.Ссылка КАК График,
		|	ТК_ГрафикиТСДТорговыеОбъекты.ТорговаяПлощадка КАК ТорговаяПлощадка
		|ИЗ
		|	Справочник.ТК_ГрафикиТСД.ТорговыеОбъекты КАК ТК_ГрафикиТСДТорговыеОбъекты
		|ГДЕ
		|	ТК_ГрафикиТСДТорговыеОбъекты.ТорговаяПлощадка В(&МассивТП)
		|	И НЕ ТК_ГрафикиТСДТорговыеОбъекты.Ссылка = &ЭтотГрафик
		|	И ТК_ГрафикиТСДТорговыеОбъекты.Ссылка.Активный
		|	И ТК_ГрафикиТСДТорговыеОбъекты.Ссылка.ВидГрафика = &ВидГрафика
		|	И ТК_ГрафикиТСДТорговыеОбъекты.Ссылка.ПометкаУдаления = ЛОЖЬ";
		
		Запрос.УстановитьПараметр("МассивТП", ТорговыеОбъекты.ВыгрузитьКолонку("ТорговаяПлощадка"));
		Запрос.УстановитьПараметр("ЭтотГрафик", Ссылка);
		Запрос.УстановитьПараметр("ВидГрафика", ВидГрафика);

		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(Строка(ВыборкаДетальныеЗаписи.ТорговаяПлощадка) +" уже включен в график "+Строка(ВыборкаДетальныеЗаписи.График));
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВидГрафика = Перечисления.ТК_ВидыЗадачТСД.ВерификацияЦенников Тогда
		
		Если ТоварныеГруппы.Количество() = 0 Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Група товаров на заполнена");
		Иначе
			Для Каждого стрГруппа Из ТоварныеГруппы Цикл 
				Если Не ЗначениеЗаполнено(стрГруппа.Номенклатура) Тогда
					Отказ = Истина;
					ОбщегоНазначения.СообщитьПользователю("Не заполнена группа товаров "+Строка(стрГруппа.НомерСтроки));
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	
КонецПроцедуры
	
#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьСписокВидовЦен();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВидовЦен()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВидыЦен.Ссылка КАК Значение,
	|	ВидыЦен.Наименование КАК Представление,
	|	ЛОЖЬ КАК Пометка
	|ИЗ
	|	Справочник.ТипыЦен КАК ВидыЦен
	|ГДЕ
	|	НЕ ВидыЦен.Ссылка В (&ВидыЦен)
	|	И ВидыЦен.ЭтоГруппа = ЛОЖЬ
	|	И ВидыЦен.ПометкаУдаления = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЦен.Ссылка,
	|	ВидыЦен.Наименование,
	|	ИСТИНА
	|ИЗ
	|	Справочник.ТипыЦен КАК ВидыЦен
	|ГДЕ
	|	ВидыЦен.Ссылка В(&ВидыЦен)
	|	И ВидыЦен.ЭтоГруппа = ЛОЖЬ
	|	И ВидыЦен.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление";
	
	Запрос.УстановитьПараметр("ВидыЦен", Параметры.СписокВидовЦен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовыйЭлемент = СписокВидовЦен.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаОК(Команда)
	ВыбранныеВидыЦен = Новый СписокЗначений;
	
	Для каждого ЭлементСЗ Из СписокВидовЦен Цикл
		
		Если ЭлементСЗ.Пометка Тогда
			
			НовыйЭлемент = ВыбранныеВидыЦен.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементСЗ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Закрыть(ВыбранныеВидыЦен);
	
КонецПроцедуры


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	Функция ВернутьХарактеристикуПоЗначениюМРЦ(Номенклатура,ЗначениеМРЦ,СоздаватьМРЦ=Ложь,ИсключитьУдаленные=Ложь)Экспорт
		
		Если ЗначениеМРЦ = 0 Тогда
			Возврат Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Владелец = &Владелец
		|	И ХарактеристикиНоменклатуры.ЗначениеМРЦ = &ЗначениеМРЦ";
		
		Если ИсключитьУдаленные Тогда
			Запрос.Текст = Запрос.Текст + Символы.ПС + "	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Владелец", Номенклатура);
		Запрос.УстановитьПараметр("ЗначениеМРЦ", ЗначениеМРЦ);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Если СоздаватьМРЦ Тогда
				Результат = ТК_ОбменCentr.ОтправитьЗапросНаСозданиеМРЦ(Номенклатура,ЗначениеМРЦ,Неопределено);
				Если Результат.Успешно Тогда 					
					Характеристика = Результат.Характеристика;
				Иначе
					Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				КонецЕсли;
				Возврат  Характеристика
			Иначе
				Возврат Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
		
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецФункции
	
	Функция ВернутьПоследнееАктивноеМРЦ(Номенклатура)Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Владелец = &Владелец
		|	И ХарактеристикиНоменклатуры.НеАктивная = ЛОЖЬ
		|	И ХарактеристикиНоменклатуры.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ХарактеристикиНоменклатуры.Сортировка УБЫВ";
		
		Запрос.УстановитьПараметр("Владелец", Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда 
			Возврат Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецФункции
	
	
	Функция ЗначениеМРЦ(Характеристика)Экспорт
		
		Если ЗначениеЗаполнено(Характеристика) Тогда
			МРЦ = Характеристика.ЗначениеМРЦ;
		Иначе
			МРЦ = 0;
		КонецЕсли;
		
		Возврат МРЦ;
		
	КонецФункции
	
	
#КонецЕсли

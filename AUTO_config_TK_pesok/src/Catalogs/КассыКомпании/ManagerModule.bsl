#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция определяет реквизиты выбранной кассы.
//
// Параметры:
//  Касса - СправочникСсылка.Кассы - Ссылка на кассу
//
// Возвращаемое значение:
//	Структура - Реквизиты выбранной кассы
//
Функция ПолучитьРеквизитыКассыКомпании(КассаКомпании) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КассыКомпании.Код КАК Код,
	                      |	КассыКомпании.Организация КАК Организация,
	                      |	КассыКомпании.Подразделение КАК Подразделение,
	                      |	КассыКомпании.Объект КАК Объект,
	                      |	КассыКомпании.ВалютаДенежныхСредств КАК Валюта,
	                      |	КассыКомпании.МноговалютнаяКасса КАК МноговалютнаяКасса,
	                      |	КассыКомпании.Кассир КАК Кассир,
	                      |	КассыКомпании.РегламентированныйУчет КАК РегламентированныйУчет,
	                      |	КассыКомпании.Лимит КАК Лимит
	                      |ИЗ
	                      |	Справочник.КассыКомпании КАК КассыКомпании
	                      |ГДЕ
	                      |	КассыКомпании.Ссылка = &КассаКомпании");
	
	Запрос.УстановитьПараметр("КассаКомпании", КассаКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Код 			= Выборка.Код;
		Организация 	= Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(Выборка.Объект,ТекущаяДатаСеанса());
		Подразделение	= Выборка.Подразделение;
		Объект			= Выборка.Объект;
		Валюта			= Выборка.Валюта;
		МноговалютнаяКасса = Выборка.МноговалютнаяКасса;
		Кассир			= Выборка.Кассир;
		РегламентированныйУчет = Выборка.РегламентированныйУчет;
		Лимит			= Выборка.Лимит;
	Иначе
		Код 			= "";
		Организация 	= Справочники.Организации.ПустаяСсылка();
		Подразделение	= Справочники.ПодразделенияКомпании.ПустаяСсылка();
		Объект			= Справочники.ТорговыеПлощадки.ПустаяСсылка();
		Валюта		    = Справочники.Валюты.ПустаяСсылка();
		МноговалютнаяКасса = Ложь;
		Кассир			= Справочники.ФизическиеЛица.ПустаяСсылка();
		РегламентированныйУчет = Ложь;
		Лимит			= 0;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Код, Организация, Подразделение, Объект, Валюта, МноговалютнаяКасса, Кассир, РегламентированныйУчет, Лимит",
		Код,
		Организация,
		Подразделение,
		Объект,
		Валюта,
		МноговалютнаяКасса,
		Кассир,
		РегламентированныйУчет,
		Лимит);
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли; 
	
	Возврат СтруктураРеквизитов;

КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(Объект)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиСобытий


#КонецОбласти



#КонецЕсли
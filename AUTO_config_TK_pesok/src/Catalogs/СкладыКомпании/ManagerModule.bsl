#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ЭтоГруппа
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ 
	|			ЗначениеРазрешено(Подразделение)
	|			И ЗначениеРазрешено(ТорговаяПлощадка)
	|	КОНЕЦ";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Код");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов


#КонецОбласти


Функция ПолучитьСкладПоКлючуФоновогоЗадания(КлючФоновогоЗадания) Экспорт

	ПозицияРазделителя = Найти(КлючФоновогоЗадания, "UID#");
	
	УникальныйИдентификаторСклада = Сред(КлючФоновогоЗадания, ПозицияРазделителя + 4);
	
	Возврат Справочники.СкладыКомпании.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторСклада));

КонецФункции // ПолучитьСкладПоКлючуФоновогоЗадания()

Функция ПолучитьКлючФоновогоЗаданияПоСкладу(Склад) Экспорт

	Возврат СокрЛП(Склад.Наименование) + ", UID#" + Склад.УникальныйИдентификатор();	

КонецФункции // ПолучитьКлючФоновогоЗаданияПоСкладу()

Функция ТипЦенСебестоимости(СкладыКомпании) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.ТипЦенСебестоимости
	|ИЗ
	|	Справочник.СкладыКомпании КАК Склады
	|ГДЕ
	|	Склады.Ссылка = &СкладыКомпании";
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТипЦены = Выборка.ТипЦенСебестоимости;
	Иначе
		ТипЦены = ПредопределенноеЗначение("Справочник.ТипыЦен.ПустаяСсылка");
	КонецЕсли;
	
	Возврат ТипЦены;
КонецФункции

Функция ТипЦенРозничный(СкладыКомпании) Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.ТипЦенРозничнойТорговли КАК ТипЦенРозничнойТорговли
	|ИЗ
	|	Справочник.СкладыКомпании КАК Склады
	|ГДЕ
	|	Склады.Ссылка = &СкладыКомпании";
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТипЦены = Выборка.ТипЦенРозничнойТорговли;
	Иначе
		ТипЦены = ПредопределенноеЗначение("Справочник.ТипыЦен.ПустаяСсылка");
	КонецЕсли;
	
	Возврат ТипЦены;	
КонецФункции

Функция ТорговаяПлощадкаСклада(СкладыКомпании) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.ТорговаяПлощадка
	|ИЗ
	|	Справочник.СкладыКомпании КАК Склады
	|ГДЕ
	|	Склады.Ссылка = &СкладыКомпании";
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТорговаяПлощадка = Выборка.ТорговаяПлощадка;
	Иначе
		ТорговаяПлощадка = ПредопределенноеЗначение("Справочник.ТорговыеПлощадки.ПустаяСсылка");
	КонецЕсли;
	
	Возврат ТорговаяПлощадка;
КонецФункции

Функция ОсновнойСкладТорговойПлощадки(ТорговаяПлощадка)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СкладыКомпании.Ссылка КАК СкладСсылка,
		|	ВЫБОР
		|		КОГДА СкладыКомпании.ПометкаУдаления
		|			ТОГДА 9
		|		КОГДА СкладыКомпании.НеАктивен
		|			ТОГДА 8
		|		КОГДА СкладыКомпании.ОсновнойСкладТорговойПлощадки
		|			ТОГДА 0
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
		|			ТОГДА 1
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон)
		|			ТОГДА 2
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ОсновнойСклад)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И НЕ СкладыКомпании.ПометкаУдаления
		|	И НЕ СкладыКомпании.НеАктивен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	СкладСсылка = ВыборкаДетальныеЗаписи.СкладСсылка;
	
	Возврат СкладСсылка;
	
	
КонецФункции

Функция ТНПСкладТорговойПлощадки(ТорговаяПлощадка)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СкладыКомпании.Ссылка КАК СкладСсылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И НЕ СкладыКомпании.ПометкаУдаления
		|	И НЕ СкладыКомпании.НеАктивен
		|	И СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТНП)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СкладыКомпании.Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И НЕ СкладыКомпании.ПометкаУдаления
		|	И НЕ СкладыКомпании.НеАктивен
		|	И СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ОсновнойСклад)";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	СкладСсылка = ВыборкаДетальныеЗаписи.СкладСсылка;
	
	Возврат СкладСсылка;
	
	
КонецФункции

Функция НайтиТорговуюПлощадкуСкладаПоGLN(GLN)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ВЫБОР
		|		КОГДА СкладыКомпании.ПометкаУдаления
		|			ТОГДА 5
		|		КОГДА СкладыКомпании.ОсновнойСкладТорговойПлощадки
		|			ТОГДА 0
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
		|			ТОГДА 1
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон)
		|			ТОГДА 2
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ОсновнойСклад)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	НЕ СкладыКомпании.ПометкаУдаления
		|	И НЕ СкладыКомпании.НеАктивен
		|	И СкладыКомпании.ТочкаДоставки.GLN = &GLN
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("GLN", GLN);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	ТорговаяПлощадкаСсылка = ВыборкаДетальныеЗаписи.ТорговаяПлощадка;
	
	Возврат ТорговаяПлощадкаСсылка;

КонецФункции

Функция ОсновнойСкладТорговойПлощадкиGLN(ТорговаяПлощадка,GLN)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СкладыКомпании.Ссылка КАК СкладСсылка,
		|	ВЫБОР
		|		КОГДА СкладыКомпании.ПометкаУдаления
		|			ТОГДА 5
		|		КОГДА СкладыКомпании.ОсновнойСкладТорговойПлощадки
		|			ТОГДА 0
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйЗал)
		|			ТОГДА 1
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТорговыйПавильон)
		|			ТОГДА 2
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ОсновнойСклад)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И НЕ СкладыКомпании.ПометкаУдаления
		|	И НЕ СкладыКомпании.НеАктивен
		|	И СкладыКомпании.ТочкаДоставки.GLN = &GLN
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	Запрос.УстановитьПараметр("GLN", GLN);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СкладыКомпании.ПустаяСсылка();
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий(); 
	СкладСсылка = ВыборкаДетальныеЗаписи.СкладСсылка;
	
	Возврат СкладСсылка;
	
	
КонецФункции

Функция ПолучитьСкладПоТочкеДоставки(ТочкаДоставки) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СкладыКомпании.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СкладыКомпании КАК СкладыКомпании
	               |ГДЕ
	               |	СкладыКомпании.ТочкаДоставки = &ТочкаДоставки
	               |	И НЕ СкладыКомпании.ЭтоГруппа
	               |	И НЕ СкладыКомпании.ПометкаУдаления
	               |	И НЕ СкладыКомпании.НеАктивен";
	
	Запрос.УстановитьПараметр("ТочкаДоставки", ТочкаДоставки);
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Если Выборка.Следующий() Тогда 
		СкладСсылка = Выборка.Ссылка;
	Иначе
		СкладСсылка = Справочники.СкладыКомпании.ПустаяСсылка();	
	КонецЕсли;
	
	Возврат СкладСсылка;
	
КонецФункции


Функция ОрганизацияСклада(СкладыКомпании, НаДату = Неопределено) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация
	               |ИЗ
	               |	РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(&НаДату, ТорговаяПлощадка = ВЫРАЗИТЬ(&СкладыКомпании КАК Справочник.СкладыКомпании).ТорговаяПлощадка) КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних";
	
	Запрос.УстановитьПараметр("СкладыКомпании", СкладыКомпании);
	Запрос.УстановитьПараметр("НаДату", ?(НаДату <> Неопределено, НаДату, Неопределено));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Организация = Выборка.Организация;
	Иначе 
		Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	КонецЕсли;
	
	Возврат Организация;
КонецФункции

Функция НеВестиУчетПоХарактеристикамНаСкладе(СкладКомпании, НаДату = Неопределено) Экспорт 
	
	НеВестиУчетПоХарактеристикам = Ложь;
	
	Если Не ЗначениеЗаполнено(НаДату) Тогда 
		НаДату = ТекущаяДата();
	КонецЕсли;		
	
	ТаблицаУчета = ЗначениеНастроекПовтИсп.ПолучитьТаблицуУчетаХарактеристик(СкладКомпании);
	
	Если ТаблицаУчета.Количество() = 0 Тогда 
		Возврат НеВестиУчетПоХарактеристикам;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаУчета", ТаблицаУчета);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.Текст = "ВЫБРАТЬ
	               |	втУчетХарактеристик.Период КАК Период,
	               |	втУчетХарактеристик.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	               |ПОМЕСТИТЬ втУчетХарактеристик
	               |ИЗ
	               |	&ТаблицаУчета КАК втУчетХарактеристик
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	втУчетХарактеристик.Период КАК Период,
	               |	втУчетХарактеристик.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	               |ИЗ
	               |	втУчетХарактеристик КАК втУчетХарактеристик
	               |ГДЕ
	               |	втУчетХарактеристик.Период <= &НаДату
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период УБЫВ";	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		НеВестиУчетПоХарактеристикам = Выборка.НеВестиУчетПоХарактеристикам;
	КонецЕсли;
	
	Возврат НеВестиУчетПоХарактеристикам;
	
КонецФункции


Функция ПолучитьТаблицуУчетаХарактеристик(СкладКомпании) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаРезультат.Колонки.Добавить("НеВестиУчетПоХарактеристикам", Новый ОписаниеТипов("Булево"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_УчетХарактеристикНаСкладах.Период КАК Период,
	               |	ТК_УчетХарактеристикНаСкладах.НеВестиУчетПоХарактеристикам КАК НеВестиУчетПоХарактеристикам
	               |ИЗ
	               |	РегистрСведений.ТК_УчетХарактеристикНаСкладах КАК ТК_УчетХарактеристикНаСкладах
	               |ГДЕ
	               |	ТК_УчетХарактеристикНаСкладах.СкладКомпании = &СкладКомпании";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда 
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = ТаблицаРезультат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция МассивСкладовТорговойПлощадки(ТорговаяПлощадка)Экспорт
	
	МассивСкладов = Новый Массив;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкладыКомпании.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И СкладыКомпании.ЭтоГруппа = ЛОЖЬ
		|	И СкладыКомпании.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МассивСкладов.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		
	КонецЦикла;
	

	Возврат МассивСкладов;
	
КонецФункции


Функция МассивСкладовТорговойПлощадкиТСД(ТорговаяПлощадка)Экспорт
	
	МассивОтвет = Новый Массив;
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкладыКомпании.Ссылка КАК СкладСсылка,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СкладыКомпании.Ссылка) КАК СкладНаименование,
		|	ВЫБОР
		|		КОГДА СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТНП)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ТНП
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.ЭтоГруппа = ЛОЖЬ
		|	И СкладыКомпании.ПометкаУдаления = ЛОЖЬ
		|	И СкладыКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
		|	И СкладыКомпании.НеАктивен = ЛОЖЬ
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		СтуктураСклада = Новый Структура("Склад,СкладUUID,ТНП", ВыборкаДетальныеЗаписи.СкладНаименование,Строка(ВыборкаДетальныеЗаписи.СкладСсылка.УникальныйИдентификатор()),ВыборкаДетальныеЗаписи.ТНП);
		МассивОтвет.Добавить(СтуктураСклада);
	КонецЦикла;
	
	Возврат МассивОтвет;
	
КонецФункции

Функция ЭтоСкладТНП(СкладСсылка)Экспорт
	Возврат СкладСсылка.ВидСклада = Справочники.ВидыСкладов.ТНП;	
КонецФункции


Функция МассивСкладовПоПодразделению(Подразделение)	Экспорт
	
	МассивСкладов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкладыКомпании.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СкладыКомпании КАК СкладыКомпании
		|ГДЕ
		|	СкладыКомпании.Подразделение В(&Подразделение)
		|	И СкладыКомпании.ЭтоГруппа = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		МассивСкладов.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		
	КонецЦикла;

	Возврат МассивСкладов;
	
КонецФункции

Функция СкладUUID(СкладКомпании)Экспорт
	
	Возврат Строка(СкладКомпании.УникальныйИдентификатор());	
	
КонецФункции


#КонецОбласти

#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
Функция ПолучитьСхемуНалообложения(Контрагент, НаДату) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТК_УчетнаяПолитикаКонтрагентыСрезПоследних.СхемаНалогообложения КАК СхемаНалоогобложения
		|ИЗ
		|	РегистрСведений.ТК_УчетнаяПолитикаКонтрагенты.СрезПоследних(&НаДату, Контрагент = &Контрагент) КАК ТК_УчетнаяПолитикаКонтрагентыСрезПоследних";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Неопределено;
	Иначе 
		ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.СхемаНалоогобложения;		
	КонецЕсли;
	
	
КонецФункции

Функция ПолучитьКлючФоновогоЗаданияПоКонтрагенту(Контрагент) Экспорт

	Возврат СокрЛП(Контрагент.Наименование) + ", UID#" + Контрагент.УникальныйИдентификатор();	

КонецФункции // ПолучитьКлючФоновогоЗаданияПоСкладу()

Функция ПолучитьКонтрагентаПоКлючуФоновогоЗадания(КлючФоновогоЗадания) Экспорт

	ПозицияРазделителя = Найти(КлючФоновогоЗадания, "UID#");
	
	УникальныйИдентификаторКонтрагента = Сред(КлючФоновогоЗадания, ПозицияРазделителя + 4);
	
	Возврат Справочники.Контрагенты.ПолучитьСсылку(Новый УникальныйИдентификатор(УникальныйИдентификаторКонтрагента));

КонецФункции // ПолучитьСкладПоКлючуФоновогоЗадания()

Функция НайтиКонтрагентаПоЕДРПОУ(Знач ЕДРПОУ)Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =	  "ВЫБРАТЬ ПЕРВЫЕ 1
	 |	Контрагенты.Ссылка КАК Ссылка
	 |ИЗ
	 |	Справочник.Контрагенты КАК Контрагенты
	 |ГДЕ
	 |	Контрагенты.КодПоЕДРПОУ = &КодПоЕДРПОУ";
	Запрос.УстановитьПараметр("КодПоЕДРПОУ",ЕДРПОУ);
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	Выборка = Рез.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
	
КонецФункции

Функция ПолучитьРеквизитыКонтрагента(Дата, Контрагент)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВидСчета", "Текущий");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);

	Запрос.Текст = "ВЫБРАТЬ
	               |	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	               |	Контрагенты.ЮрФизЛицо КАК ЮрФизЛицо,
	               |	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	               |	КонтрагентыКонтактнаяИнформация.Представление КАК Адрес,
	               |	Контрагенты.КодФилиала КАК КодФилиала,
	               |	Контрагенты.ИННПлательщикаНДС КАК ИНН
	               |ИЗ
	               |	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	               |		ПРАВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	               |		ПО КонтрагентыКонтактнаяИнформация.Ссылка = Контрагенты.Ссылка
	               |			И (КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	               |ГДЕ
	               |	Контрагенты.Ссылка = &Контрагент
	               |;
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	БанковскиеСчетаКонтрагентов.Ссылка КАК Ссылка,
	               |	БанковскиеСчетаКонтрагентов.НомерСчета КАК НомерСчета,
	               |	БанковскиеСчетаКонтрагентов.КодБанка КАК КодБанка,
	               |	БанковскиеСчетаКонтрагентов.НаименованиеБанка КАК НаименованиеБанка,
	               |	БанковскиеСчетаКонтрагентов.АдресБанка КАК АдресБанка
	               |ИЗ
	               |	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	               |ГДЕ
	               |	БанковскиеСчетаКонтрагентов.ВидСчета = &ВидСчета
	               |	И БанковскиеСчетаКонтрагентов.Владелец = &Контрагент
	               |	И НЕ БанковскиеСчетаКонтрагентов.Закрыт";
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Реквизиты = МассивРезультатов[0].Выбрать();
	ВыборкаБС = МассивРезультатов[1].Выбрать();
						
	Реквизиты.Следующий();
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("НаименованиеПолное",  Реквизиты.НаименованиеПолное);
	СтруктураРеквизитов.Вставить("ЮрЛицо", 			    Реквизиты.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
	СтруктураРеквизитов.Вставить("КодПоЕДРПОУ", 		Реквизиты.КодПоЕДРПОУ);		
	СтруктураРеквизитов.Вставить("Адрес", 				Реквизиты.Адрес);		
	СтруктураРеквизитов.Вставить("КодФилиала", 			Реквизиты.КодФилиала);		
	СтруктураРеквизитов.Вставить("ИНН", 				Реквизиты.ИНН);		
	ВыборкаБС.Следующий();
	СтруктураРеквизитов.Вставить("НомерСчета", 			ВыборкаБС.НомерСчета);		
	СтруктураРеквизитов.Вставить("КодБанка", 			ВыборкаБС.КодБанка);		
	СтруктураРеквизитов.Вставить("НаименованиеБанка", 	ВыборкаБС.НаименованиеБанка);		
	СтруктураРеквизитов.Вставить("АдресБанка", 			ВыборкаБС.АдресБанка);		

	Возврат СтруктураРеквизитов;
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов


// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Код");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов


#КонецОбласти

Функция ЭтоВнутреннийКонтрагент(Контрагент) Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Контрагенты.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Ссылка = &Контрагент
	               |	И Контрагенты.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.Внутренний)";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ПолучитьПредставлениеДляПечати(Контрагент, Наименование = Истина, ИНН = Ложь, ЕДРПОУ = Ложь, Адрес = Ложь, НаДату = Неопределено, КодЯзыка = Неопределено, Разделитель = ", ", ПолучитьСтруктуру = Ложь) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НаДату = Неопределено Тогда 
		НаДату = ТекущаяДата();
	КонецЕсли;
	
	Если КодЯзыка = Неопределено Тогда 
		КодЯзыка = Лев(КодЛокализацииИнформационнойБазы(), 2);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Контрагенты.Ссылка КАК Ссылка,
	               |	Контрагенты.Наименование КАК Наименование,
	               |	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	               |	Контрагенты.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	               |	Контрагенты.ИННПлательщикаНДС КАК ИНН
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПредставления = Новый Структура;	
	Если Выборка.Следующий() Тогда 
		Если Наименование Тогда 
			Если ЗначениеЗаполнено(Выборка.НаименованиеПолное) Тогда 
				СтруктураПредставления.Вставить("Наименование", СокрЛП(Выборка.НаименованиеПолное));
			Иначе
				СтруктураПредставления.Вставить("Наименование", СокрЛП(Выборка.Наименование));
			КонецЕсли;
		КонецЕсли;
		
		Если ИНН И ЗначениеЗаполнено(Выборка.ИНН) Тогда 
			СтруктураПредставления.Вставить("ИНН", НСтр("ru = 'ИНН '; uk = 'ІПН '", КодЯзыка) + СокрЛП(Выборка.ИНН));
		КонецЕсли;
		
		Если ЕДРПОУ И ЗначениеЗаполнено(Выборка.КодПоЕДРПОУ) Тогда 
			СтруктураПредставления.Вставить("ЕДРПОУ", НСтр("ru = 'Код по ЕГРПОУ '; uk = 'Код за ЄДРПОУ '", КодЯзыка) + СокрЛП(Выборка.КодПоЕДРПОУ));
		КонецЕсли;
		
		Если Адрес Тогда 
			ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Юридический адрес контрагента");
			тАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(Контрагент, ВидКонтактнойИнформации,, НаДату);
			тАдрес = СтрЗаменить(тАдрес," дом ", " буд "); 
			Если ЗначениеЗаполнено(тАдрес) Тогда 
				СтруктураПредставления.Вставить("Адрес", СокрЛП(тАдрес));
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Наименование Тогда 
		СтруктураПредставления.Вставить("Наименование", Строка(Контрагент));
	КонецЕсли;
	
	Если ПолучитьСтруктуру Тогда 
		Возврат СтруктураПредставления;
	Иначе		
		Для Каждого Стр Из СтруктураПредставления Цикл 
			ПредставлениеДляПечати = ?(Не ПустаяСтрока(ПредставлениеДляПечати), ПредставлениеДляПечати + Разделитель, "") + Стр.Значение;
		КонецЦикла;
		
		Возврат ПредставлениеДляПечати;	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
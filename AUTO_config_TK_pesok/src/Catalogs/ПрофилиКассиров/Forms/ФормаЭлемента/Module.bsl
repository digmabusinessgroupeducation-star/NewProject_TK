
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	МодельPOS = ПредопределенноеЗначение("Перечисление.МоделиСерверовPOSТерминалов.КассовыйСерверОпенстор");
	
	ЗаполнитьПраваКассира();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьПраваКассира(ТекущийОбъект);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


#КонецОбласти


#Область ОбработчикиКомандФормы


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПраваКассира()
	
	ТаблицаПравПоУмолчанию = ТаблицаПравКассира.Выгрузить();
	ТаблицаПравПоУмолчанию.Очистить();
	
	Макет = Справочники.ПрофилиКассиров.ПолучитьМакет("ТД_MXL_КассовыйСерверОпенстор");
	ОбластьПрава = Макет.ПолучитьОбласть("Права");
	
	Для стр = 1 По ОбластьПрава.ВысотаТаблицы Цикл 
		НоваяСтрока = ТаблицаПравПоУмолчанию.Добавить();
		НоваяСтрока.ИдентификаторПрава = Число(ОбластьПрава.Область(стр, 1).Текст);
		НоваяСтрока.Право = ОбластьПрава.Область(стр, 2).Текст;
		НоваяСтрока.Разрешено = СокрЛП(НРег(ОбластьПрава.Область(стр, 3).Текст)) = "истина";
		НоваяСтрока.МодельPOS = МодельPOS;
	КонецЦикла;
	
	Если Объект.НастройкаПравКассира.Количество() > 0 Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПраваПоУмолчанию.ИдентификаторПрава КАК ИдентификаторПрава,
		               |	ПраваПоУмолчанию.Разрешено КАК Разрешено,
		               |	ПраваПоУмолчанию.Право КАК Право,
		               |	ПраваПоУмолчанию.МодельPOS КАК МодельPOS
		               |ПОМЕСТИТЬ ПраваПоУмолчанию
		               |ИЗ
		               |	&ПраваПоУмолчанию КАК ПраваПоУмолчанию
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Права.ИдентификаторПрава КАК ИдентификаторПрава,
		               |	Права.Разрешено КАК Разрешено,
		               |	Права.МодельPOS КАК МодельPOS
		               |ПОМЕСТИТЬ Права
		               |ИЗ
		               |	&Права КАК Права
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПраваПоУмолчанию.ИдентификаторПрава КАК ИдентификаторПрава,
		               |	ВЫБОР
		               |		КОГДА НЕ Права.Разрешено ЕСТЬ NULL
		               |			ТОГДА Права.Разрешено
		               |		ИНАЧЕ ПраваПоУмолчанию.Разрешено
		               |	КОНЕЦ КАК Разрешено,
		               |	ПраваПоУмолчанию.Право КАК Право,
		               |	ПраваПоУмолчанию.МодельPOS КАК МодельPOS
		               |ИЗ
		               |	ПраваПоУмолчанию КАК ПраваПоУмолчанию
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Права КАК Права
		               |		ПО ПраваПоУмолчанию.МодельPOS = Права.МодельPOS
		               |			И ПраваПоУмолчанию.ИдентификаторПрава = Права.ИдентификаторПрава";
		
		Запрос.УстановитьПараметр("ПраваПоУмолчанию", ТаблицаПравПоУмолчанию);
		Запрос.УстановитьПараметр("Права", Объект.НастройкаПравКассира.Выгрузить());
		
		ТаблицаПрав = Запрос.Выполнить().Выгрузить();
		
	Иначе
		
		ТаблицаПрав = ТаблицаПравПоУмолчанию;
		
	КонецЕсли;
	
	ТаблицаПравКассира.Загрузить(ТаблицаПрав);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПраваКассира(ТекОбъект)
	
	ТекОбъект.НастройкаПравКассира.Очистить();
	
	Для Каждого Стр Из ТаблицаПравКассира Цикл 
		НоваяСтрока = ТекОбъект.НастройкаПравКассира.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "МодельPOS, ИдентификаторПрава, Разрешено");
	КонецЦикла;
	
КонецПроцедуры



#КонецОбласти
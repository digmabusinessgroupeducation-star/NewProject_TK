&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда 
		ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	КонецЕсли;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("Рецептура"));	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)	
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	//// СтандартныеПодсистемы.ДатыЗапретаИзменения
	//ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	//// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();

	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();

	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если Не Объект.Актуальная Тогда 
		Объект.ОсновнаяРецептура = Ложь;	
	КонецЕсли;
	
	ПроверитьУстановкуОсновнойРецептуры();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда 
		Для Каждого Стр Из Объект.Товары Цикл 
			Если Стр.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Продукция И Стр.ЗапретитьАвтовыборРецептуры Тогда 
				Стр.Рецептура = Стр.ТекущаяРецептура;						
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	УстановитьСостояниеДокумента();
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	ПодразделениеКомпанииПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПодразделениеКомпанииПриИзмененииНаСервере()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, ЭтаФорма.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриИзменении(Элемент)
	
	Если Не Объект.Продукция.Пустая() Тогда 
		ПродукцияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПродукцияПриИзмененииНаСервере()
	
	СтруктураНоменклатура = Справочники.Номенклатура.ЗначенияРеквизитовНоменклатуры(Объект.Продукция);
	Объект.ЕдиницаИзмерения = СтруктураНоменклатура.ЕдиницаИзмерения;
	
	ЕдиницаИзмеренияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	
	Если Не Объект.ЕдиницаИзмерения.Пустая() Тогда 
		ЕдиницаИзмеренияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЕдиницаИзмеренияПриИзмененииНаСервере()
	
	Если Не Объект.ЕдиницаИзмерения.Пустая() И Не Объект.Продукция.Пустая() Тогда 
		СтруктураЕдИзм = Справочники.ЕдиницыИзмерения.КоэффициентВесОбъем(Объект.ЕдиницаИзмерения, Объект.Продукция);
		Объект.Коэффициент = СтруктураЕдИзм.Коэффициент;
	Иначе
		Объект.Коэффициент = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяРецептураПриИзменении(Элемент)

	ПроверитьУстановкуОсновнойРецептуры();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	//СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));
	СтруктураДействий.Вставить("ЗаполнитьВидНоменклатуры", Новый Структура("Номенклатура", "ВидНоменклатуры"));	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);			
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущаяСтрока.КоличествоНетто = 0 ИЛИ ТекущаяСтрока.КоличествоНетто > ТекущаяСтрока.Количество Тогда 
		ТекущаяСтрока.КоличествоНетто = ТекущаяСтрока.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыРецептураПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура; 
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании, ИмяПоля", Объект.ПодразделениеКомпании, "ТекущаяРецептура"));
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	//Элементы.ГруппаОснование.Видимость = Не Объект.ДокументОснование.Пустая();
	//Элементы.ТоварыПартия.Видимость = Объект.ДокументОснование.Пустая();
КонецПроцедуры

//Проверка на основную рецептуру
&НаСервереБезКонтекста
Функция ПолучитьОсновнуюРецептуру(Продукция, Подразделение)	
	
	СтруктураОснРецептуры = Документы.Рецептура.ПолучитьАктуальнуюРецептуру(Продукция, Подразделение, Истина);
	Возврат СтруктураОснРецептуры.Рецептура;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьУстановкуОсновнойРецептуры()
	
	Если Не Объект.ОсновнаяРецептура Тогда 
		Возврат;
	КонецЕсли;
	
	текОсновнаяРецептура = ПолучитьОсновнуюРецептуру(Объект.Продукция, Объект.ПодразделениеКомпании);
	Если текОсновнаяРецептура <> ПредопределенноеЗначение("Документ.Рецептура.ПустаяСсылка") И текОсновнаяРецептура <> Объект.Ссылка Тогда 		
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, текОсновнаяРецептура);
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Снять признак основной рецептуры с документа ''%1'''; uk = 'Зняти ознаку головної рецептури з документу ''%1'''"),
							текОсновнаяРецептура);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, текОсновнаяРецептура) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = "";
		Если Не СнятьПризнакОсновнойРецептурыНаСервере(текОсновнаяРецептура, ТекстОшибки) Тогда 
			Объект.ОсновнаяРецептура = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Параметры.Ключ);
		КонецЕсли;
	Иначе
		Объект.ОсновнаяРецептура = Ложь;
    КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СнятьПризнакОсновнойРецептурыНаСервере(текОсновнаяРецептура, ТекстОшибки)
	
	ВсеОк = Истина;
	
	Попытка
		ОснРецептураОбъект = текОсновнаяРецептура.ПолучитьОбъект();
		ОснРецептураОбъект.ОсновнаяРецептура = Ложь;
		ОснРецептураОбъект.Записать();
	Исключение
		ВсеОк = Ложь;
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ВсеОк;
КонецФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре()
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьВидНоменклатуры", Новый Структура("Номенклатура", "ВидНоменклатуры"));	
	
	спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Продукция Тогда 
			Если Не Стр.ЗапретитьАвтовыборРецептуры Тогда 
				Стр.ТекущаяРецептура = ПолучитьОсновнуюРецептуру(Стр.Номенклатура, Объект.ПодразделениеКомпании);
			Иначе
				Стр.ТекущаяРецептура = Стр.Рецептура;		
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры



#КонецОбласти                    

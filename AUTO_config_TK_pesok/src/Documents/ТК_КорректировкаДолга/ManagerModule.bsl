#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);

	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаВтТаблицаСостав(Запрос, ТекстыЗапроса);
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);					
	ПартионныйУчет.ПодготовитьТаблицыКорректировокДолга(Запрос,ДополнительныеСвойства);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Док.Дата КАК Период,
	               |	Док.Ссылка КАК Ссылка,
	               |	Док.ХозОперация КАК ХозОперация,
	               |	Док.Организация КАК Организация,
	               |	Док.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	Док.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	Док.ВалютаДокумента КАК ВалютаДокумента,
	               |	Док.МоментВремени КАК МоментВремени,
	               |	Док.КурсДокумента КАК КурсДокумента,
	               |	Док.СуммаДокументаПриход КАК СуммаДокументаПриход,
   	               |	Док.СуммаДокументаРасход КАК СуммаДокументаРасход,
	               |	Док.РегламентированныйУчет КАК РегламентированныйУчет
	               |ИЗ
	               |	Документ.ТК_КорректировкаДолга КАК Док
	               |ГДЕ
	               |	Док.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", 			Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", 		Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", 			Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("КурсДокумента", 		Реквизиты.КурсДокумента);
	Запрос.УстановитьПараметр("ВалютаДокумента",	Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ХозОперация", 		Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);	
	Запрос.УстановитьПараметр("ТипДоговораПокупка", Перечисления.ВидыДоговоров.Покупка);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", 	ВидДвиженияНакопления.Расход);	
	Запрос.УстановитьПараметр("ВидДвиженияПриход", 	ВидДвиженияНакопления.Приход);	

	Запрос.УстановитьПараметр("ВидОперацииПДЗ", Перечисления.ВидыОперацийВзаиморасчетов.ПогашениеДебиторскойЗадолженности);	
	Запрос.УстановитьПараметр("ВидОперацииПКЗ", Перечисления.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности);	
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаСостав(Запрос, ТекстыЗапроса)
	
	
	ИмяРегистра = "ВтТаблицаСостав";
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СоставДокумента.ДоговорВзаиморасчетов.Контрагент КАК Контрагент,
	               |	СоставДокумента.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	СоставДокумента.ДоговорВзаиморасчетов.ВидДоговора.ВидДоговора КАК ТипДоговора,
	               |	СоставДокумента.УвеличениеДолга КАК УвеличениеДолга,
	               |	СоставДокумента.УменьшениеДолга КАК УменьшениеДолга,
	               |	СоставДокумента.Сделка КАК Сделка
	               |ПОМЕСТИТЬ ВтТаблицаСостав
	               |ИЗ
	               |	Документ.ТК_КорректировкаДолга.Состав КАК СоставДокумента
	               |ГДЕ
	               |	СоставДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("Взаиморасчеты");	
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	&Дата КАК Период,
	                      |	ТК_КорректировкаДолгаСостав.Ссылка КАК Регистратор,
	                      |	ТК_КорректировкаДолгаСостав.ДоговорВзаиморасчетов КАК ДоговорКонтрагента
	                      |ИЗ
	                      |	Документ.ТК_КорректировкаДолга.Состав КАК ТК_КорректировкаДолгаСостав
	                      |ГДЕ
	                      |	ТК_КорректировкаДолгаСостав.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", ДополнительныеСвойства.ДляПроведения.Дата); 
		
	ТаблицыДляПоследовательности.Взаиморасчеты = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти	


#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	                                            
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти	


#КонецЕсли
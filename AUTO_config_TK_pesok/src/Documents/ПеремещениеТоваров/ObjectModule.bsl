#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий			
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	ЗакрытиеЗаказовПокупателей = 1;
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров")
		И ДанныеЗаполнения.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда 
		
		ЗаполнитьДокументПеремещениеТоваровИзФилиала(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров")	
		И ДанныеЗаполнения.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда 
		
		ЗаполнитьДокументПеремещениеТоваровВФилиал(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПоступления(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_ЗаявкаНаПеремещение") Тогда 
		ЗаполнитьДокументНаОснованииЗаявки(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВыпускПродукции") 
		И ДанныеЗаполнения.ХозОперация = Справочники.ХозОперации.ВыпускПродукции Тогда 
		
		ЗаполнитьДОкументНаОснованииВыпускаПродукции(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("Массив") Тогда 
		ЗаполнитьДокументНаОснованииСпискаЗаказов(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДокументОтгрузки = Документы.ПеремещениеТоваров.ПустаяСсылка();
	ДокументОснование = Неопределено;
	ИДДокументаОтгрузки = "";
	ВхДокНомер = "";
	ВхДокДата  = Неопределено;
	НомерRB = "";
	СтатусRB = Неопределено;
	
	РаспределениеОтгрузки.Очистить();
	
	ИнициализироватьДокумент();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
		ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
	КонецЕсли;
	
	
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
		ДополнительныеСвойства.Вставить("ВыполнитьКонтрольРезерва");
	КонецЕсли;
	
	
	
	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	ВЫБОР
		|		КОГДА Док.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|			ТОГДА Док.СкладКомпанииПолучатель
		|		ИНАЧЕ Док.СкладКомпании
		|	КОНЕЦ КАК СкладКомпании,
		|	Док.ХозОперация КАК ХозОперация,
		|	Док.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
		
		Если Выборка.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
			ДополнительныеСвойства.Вставить("СтарыйСкладКомпанииПолучатель", Выборка.СкладКомпанииПолучатель);
		Иначе
			ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПолучателяПоследовательности", Ложь);
		КонецЕсли;
		
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПолучателяПоследовательности", Ложь);
		
	КонецЕсли;
	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И Не ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
		
		//Vov41k 30.03.2022
		Если НЕ ДополнительныеСвойства.Свойство("НеКонтролироватьОстатки") 
			И НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "РазрешитьОтрицательныеОстатки", Ложь) Тогда 
			
			ВыполнитьКонтрольОстатков(Отказ,РежимПроведения)
		КонецЕсли;
	КонецЕсли;
	
	
	СуммаДокумента = Товары.Итог("СуммаВсего");	
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
		Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
			СкладПроверки = СкладКомпанииПолучатель;
		Иначе
			СкладПроверки = СкладКомпании;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладПроверки <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПолучателяПоследовательности") И ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда  
		ДополнительныеСвойства.Вставить("СкладКомпанииПолучательИзменен", СкладКомпанииПолучатель <> ДополнительныеСвойства.СтарыйСкладКомпанииПолучатель);
	КонецЕсли;
	
	Если ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал") Тогда
		ИДДокументаОтгрузки = Строка(Ссылка.УникальныйИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	
	Документы.ПеремещениеТоваров.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	Документы.ПеремещениеТоваров.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	
	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	Если НЕ ДополнительныеСвойства.ДляПроведения.БратьСтоимостьИзДокумента Тогда
		ЗапасыСервер.ОтразитьТоварыКПоступлению(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьТоварыТМП(ДополнительныеСвойства, Движения, Отказ);
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗаказыПокупателей") Тогда
		ЗаказыСервер.ОтразитьЗаказыПокупателей(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРезервыТоваров") Тогда
		ЗаказыСервер.ОтразитьРезервыТоваров(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОбработкаЗаказовПоставщику") Тогда
		ЗаказыСервер.ОтразитьОбработкаЗаказовПоставщику(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	//	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	//	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ПеремещениеТоваровПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));
	
	ОтправитьУведомление();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	
	Если ХозОперация <> Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТорговаяПлощадкаПолучатель");
	КонецЕсли;
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СкладКомпанииПолучатель");
	КонецЕсли;
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СкладКомпании");
	КонецЕсли;
	
	Если ХозОперация <> Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТорговаяПлощадка");
	КонецЕсли;
	
	Если ХозОперация <> Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДокументОтгрузки");
	КонецЕсли;
	
		
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
		Если Не Справочники.СкладыКомпании.ЭтоСкладТНП(СкладКомпанииПолучатель) Тогда
			 МассивНепроверяемыхРеквизитов.Добавить("СтатьяВозврата");
		КонецЕсли;
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяВозврата");
	КонецЕсли;

	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваров Тогда
		Если СкладКомпании = СкладКомпанииПолучатель Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Одинаковый склад отправитель и склад получатель';uk='Одинаковий склад відправник та склад одержувач'"),,,,Отказ);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
		Если НЕ ЗначениеЗаполнено(ДокументОтгрузки) Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не указан документ отгрузки';uk='Не вказано документ відвантаження'"),,,,Отказ);
		ИначеЕсли НЕ ДокументОтгрузки.Проведен Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не проведен документ отгрузки';uk='Не проведено документ відвантаження'"),,,,Отказ);
		КонецЕсли;
	КонецЕсли;
	
	
	
	// Выполнить контроль разделения Номенклатуры по Организациям Vov41k 08.01.2023
	Если ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал
		И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя")
		И ЗначениеНастроекПовтИсп.ДоступноРазделениеТоваровПоОрганизациям(ТорговаяПлощадка) Тогда 
		
		ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ);
	КонецЕсли;		
	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	Если ОбщегоНазначенияТК.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект,ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	
	
КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("Товары") Тогда
			Товары.Загрузить(ДанныеЗаполнения.Товары);
		КонецЕсли;
		
	Иначе
		
		Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
		ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	КонецЕсли;
	
	
КонецПроцедуры // ИнициализироватьДокумент()

Процедура ЗаполнитьРаспределениеОтгрузки(ПеремещениеТоваров)
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ПеремещениеТоваровРаспределениеОтгрузки.ЗаказПокупателя КАК Документ.ЗаказПокупателя) КАК ЗаказПокупателя
	|ПОМЕСТИТЬ втЗаказыПокупателей
	|ИЗ
	|	Документ.ПеремещениеТоваров.РаспределениеОтгрузки КАК ПеремещениеТоваровРаспределениеОтгрузки
	|ГДЕ
	|	ПеремещениеТоваровРаспределениеОтгрузки.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА ПеремещениеТоваровРаспределениеОтгрузки.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|					ИЛИ ПеремещениеТоваровРаспределениеОтгрузки.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваров)
	|				ТОГДА ПеремещениеТоваровРаспределениеОтгрузки.ЗаказПокупателя ССЫЛКА Документ.ЗаказПокупателя
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПокупателя)
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|					ИЛИ ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваров)
	|				ТОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ЗаказПокупателя.ДокументОснование КАК Документ.ЗаказПоставщику) КАК ЗаказПоставщику
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втЗаказыПокупателей.ЗаказПокупателя.Ссылка КАК ЗаказПокупателяСсылка
	|			ИЗ
	|				втЗаказыПокупателей КАК втЗаказыПокупателей)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказПокупателяРаспределениеЗаказовПоставщиков.ЗаказПоставщику
	|ИЗ
	|	Документ.ЗаказПокупателя.РаспределениеЗаказовПоставщиков КАК ЗаказПокупателяРаспределениеЗаказовПоставщиков
	|ГДЕ
	|	ЗаказПокупателяРаспределениеЗаказовПоставщиков.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втЗаказыПокупателей.ЗаказПокупателя.Ссылка КАК ЗаказПокупателяСсылка
	|			ИЗ
	|				втЗаказыПокупателей КАК втЗаказыПокупателей)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(ПеремещениеТоваровРаспределениеОтгрузки.ЗаказПокупателя КАК Документ.ЗаказПоставщику)
	|ИЗ
	|	Документ.ПеремещениеТоваров.РаспределениеОтгрузки КАК ПеремещениеТоваровРаспределениеОтгрузки
	|ГДЕ
	|	ПеремещениеТоваровРаспределениеОтгрузки.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА ПеремещениеТоваровРаспределениеОтгрузки.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|					ИЛИ ПеремещениеТоваровРаспределениеОтгрузки.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваров)
	|				ТОГДА ПеремещениеТоваровРаспределениеОтгрузки.ЗаказПокупателя ССЫЛКА Документ.ЗаказПоставщику
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПоставщику)
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	|					ИЛИ ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваров)
	|				ТОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Ссылка", ПеремещениеТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ЗаказПоставщику) Тогда
			Строка = РаспределениеОтгрузки.Добавить();
			Строка.ЗаказПокупателя = ВыборкаДетальныеЗаписи.ЗаказПоставщику
		КонецЕсли;;
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
КонецПроцедуры

Процедура ЗаполнитьДокументПеремещениеТоваровИзФилиала(ДанныеЗаполненеия)
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала");
	
	Автор =  Пользователи.ТекущийПользователь();
	//ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	//Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИДДокументаОтгрузки	  = ДанныеЗаполненеия.УникальныйИдентификатор();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПеремещениеТоваров.Ссылка КАК ДокументОтгрузки,
	|	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	|	ПеремещениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПеремещениеТоваров.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПеремещениеТоваров.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
	|	ПеремещениеТоваров.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	|	ПеремещениеТоваров.Номер КАК ВхДокНомер,
	|	ПеремещениеТоваров.Дата КАК ВхДокДата,
	|	ПеремещениеТоваров.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Коэффициент КАК Коэффициент,
	|		Количество КАК Количество,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС
	|	) КАК Товары,
	|	ПеремещениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПеремещениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполненеия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Если Не ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда 						
			СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Выборка.ТорговаяПлощадкаПолучатель);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТорговаяПлощадкаПолучатель) Тогда
			ТорговаяПлощадкаПолучатель =  СкладКомпанииПолучатель.ТорговаяПлощадка;
		КонецЕсли;
		
		ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладКомпанииПолучатель);
		Организация = Справочники.СкладыКомпании.ОрганизацияСклада(СкладКомпанииПолучатель);
		
		//Если ПодразделениеКомпании.Пустая() Тогда 
		//	ПодразделениеКомпании = Выборка.ПодразделениеКомпании;
		//КонецЕсли;
		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();				
		
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
		КонецЦикла;
		
		Если ПравоДоступа("Просмотр", Метаданные.Документы.ЗаказПокупателя) Тогда 
			ЗаполнитьРаспределениеОтгрузки(ДанныеЗаполненеия);		
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПеремещениеТоваровВФилиал(ДанныеЗаполненеия)
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
	
	Автор =  Пользователи.ТекущийПользователь();
	//ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	//Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИДДокументаОтгрузки	  = ДанныеЗаполненеия.УникальныйИдентификатор();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПеремещениеТоваров.Ссылка КАК ДокументОтгрузки,
	|	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	|	ПеремещениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПеремещениеТоваров.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПеремещениеТоваров.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
	|	ПеремещениеТоваров.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	|	ПеремещениеТоваров.ВхДокНомер КАК ВхДокНомер,
	|	ПеремещениеТоваров.ВхДокДата КАК ВхДокДата,
	|	ПеремещениеТоваров.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Коэффициент КАК Коэффициент,
	|		Количество КАК Количество,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС
	|	) КАК Товары,
	|	ПеремещениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПеремещениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполненеия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка,,"ДокументОтгрузки");
		
		Если Не ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда 			
			СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Выборка.ТорговаяПлощадкаПолучатель);
		КонецЕсли;
		
		ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладКомпанииПолучатель);
		Организация = Справочники.СкладыКомпании.ОрганизацияСклада(СкладКомпанииПолучатель);
		
		Если ПодразделениеКомпании.Пустая() Тогда 
			ПодразделениеКомпании = Выборка.ПодразделениеКомпании;
		КонецЕсли;				
		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();				
		
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииЗаказаПокупателя(Знач ЗаказПокупателя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	1 КАК КурсДокумента,
	               |	ЗаказПокупателя.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	               |	ЗаказПокупателя.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	1 КАК ЗакрытиеЗаказовПокупателей
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказПокупателяТовары.Количество КАК Количество,
	               |	ЗаказПокупателяТовары.Коэффициент КАК Коэффициент,
	               |	ЗаказПокупателяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ЗаказПокупателяТовары.Цена КАК Цена,
	               |	ЗаказПокупателяТовары.Сумма КАК Сумма,
	               |	ЗаказПокупателяТовары.СуммаВсего КАК СуммаВсего,
	               |	ЗаказПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ЗаказПокупателяТовары.СуммаНДС КАК СуммаНДС
	               |ИЗ
	               |	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	               |ГДЕ
	               |	ЗаказПокупателяТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервыТоваровОстатки.Номенклатура КАК Номенклатура,
	               |	РезервыТоваровОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РезервыТоваровОстатки.Характеристика КАК ХарактеристикаНоменклатуры,
	               |	РезервыТоваровОстатки.РезервОстаток КАК Количество,
	               |	РезервыТоваровОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	РезервыТоваровОстатки.РезервОстаток КАК КоличествоБазовое,
	               |	0 КАК Цена,
	               |	0 КАК ЦенаБезНалогов,
	               |	0 КАК Сумма,
	               |	0 КАК СуммаВсего,
	               |	РезервыТоваровОстатки.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	               |	0 КАК СуммаНДС,
	               |	0 КАК СуммаБезНалогов,
	               |	0 КАК ПроцентСкидки,
	               |	0 КАК СуммаСкидки,
	               |	РезервыТоваровОстатки.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования
	               |ИЗ
	               |	РегистрНакопления.РезервыТоваров.Остатки(&НаДату, Заказ = &Ссылка) КАК РезервыТоваровОстатки";
	
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказПокупателя);
	Запрос.УстановитьПараметр("Склад", ЗаказПокупателя.СкладКомпании);
	Если ЭтоНовый() Тогда
		НаДату = КонецДня(ТекущаяДата())
	Иначе
		НаДату = МоментВремени();
	КонецЕсли;
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаТоваровРезерв = МассивРезультатов[2].Выгрузить();
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал;
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;	
	
	СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ТорговаяПлощадкаПолучатель);
	
	Если ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда 
		ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладКомпанииПолучатель);	
	КонецЕсли;
	
	Товары.Очистить();

	Если ТаблицаТоваровРезерв.Количество() > 0 Тогда
		Товары.Загрузить(ТаблицаТоваровРезерв);
	Иначе
		Товары.Загрузить(ТаблицаТоваров);
	КонецЕсли;
		
	РаспределениеОтгрузки.Очистить();
	НовСтрока = РаспределениеОтгрузки.Добавить();
	НовСтрока.ЗаказПокупателя = ЗаказПокупателя;
	
	ПроверитьДокНаВоду();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииСпискаЗаказов(Знач МассивЗаказов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПоставщику.Организация КАК Организация,
	               |	ЗаказПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПоставщику.Контрагент КАК Контрагент,
	               |	ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	1 КАК КурсДокумента,
	               |	ЗаказПоставщику.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	               |	1 КАК ЗакрытиеЗаказПоставщиков
	               |ИЗ
	               |	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	               |ГДЕ
	               |	ЗаказПоставщику.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ЗаказПоставщикуТовары.Количество) КАК Количество,
	               |	ЗаказПоставщикуТовары.Коэффициент КАК Коэффициент,
	               |	СУММА(ЗаказПоставщикуТовары.КоличествоБазовое) КАК КоличествоБазовое,
	               |	ЗаказПоставщикуТовары.Цена КАК Цена,
	               |	СУММА(ЗаказПоставщикуТовары.Сумма) КАК Сумма,
	               |	ЗаказПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ЗаказПоставщикуТовары.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ЗаказПоставщикуТовары.Сумма) КАК СуммаВсего
	               |ИЗ
	               |	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	               |ГДЕ
	               |	ЗаказПоставщикуТовары.Ссылка В(&МассивЗаказов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаказПоставщикуТовары.Номенклатура,
	               |	ЗаказПоставщикуТовары.СтавкаНДС,
	               |	ЗаказПоставщикуТовары.ЕдиницаИзмерения,
	               |	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры,
	               |	ЗаказПоставщикуТовары.Цена,
	               |	ЗаказПоставщикуТовары.Коэффициент";
	
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("Ссылка", МассивЗаказов[0]);
	
	Если ЭтоНовый() Тогда
		НаДату = КонецДня(ТекущаяДата())
	Иначе
		НаДату = МоментВремени();
	КонецЕсли;
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();

	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал;
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;	
	
	СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ТорговаяПлощадкаПолучатель);
	
	Если ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда 
		ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладКомпанииПолучатель);	
	КонецЕсли;
	
	Товары.Очистить();

	Товары.Загрузить(ТаблицаТоваров);
	
	ПроверитьДокНаВоду();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПоступления(Знач ДанныеЗаполнения)
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваров");
	Автор =  Пользователи.ТекущийПользователь();
	КурсДокумента         = 1;
	Дата = ТекущаяДата();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
	|	ПоступлениеТоваров.ТипЦен КАК ТипЦен,
	|	ПоступлениеТоваров.СуммаДокумента КАК СуммаДокумента,
	|	ПоступлениеТоваров.Ссылка КАК ДокументОснование,
	|	ПоступлениеТоваров.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Коэффициент КАК Коэффициент,
	|		Количество КАК Количество,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС
	|	) КАК Товары
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		Товары.Загрузить(Выборка.Товары.Выгрузить());
	КонецЕсли;	
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииЗаявки(Знач ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_ЗаявкаНаПеремещение.Ссылка КАК ДокументОснование,
	               |	ТК_ЗаявкаНаПеремещение.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ТК_ЗаявкаНаПеремещение.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_ЗаявкаНаПеремещение.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_ЗаявкаНаПеремещение.СкладКомпании КАК СкладКомпании,
	               |	1 КАК КурсДокумента,
	               |	ТК_ЗаявкаНаПеремещение.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	               |	ТК_ЗаявкаНаПеремещение.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	ТК_ЗаявкаНаПеремещение.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
	               |	1 КАК ЗакрытиеЗаказовПокупателей
	               |ИЗ
	               |	Документ.ТК_ЗаявкаНаПеремещение КАК ТК_ЗаявкаНаПеремещение
	               |ГДЕ
	               |	ТК_ЗаявкаНаПеремещение.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_ЗаявкаНаПеремещениеТовары.Номенклатура КАК Номенклатура,
	               |	ТК_ЗаявкаНаПеремещениеТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ЗаявкаНаПеремещениеТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_ЗаявкаНаПеремещениеТовары.Количество КАК Количество,
	               |	ТК_ЗаявкаНаПеремещениеТовары.Коэффициент КАК Коэффициент,
	               |	ТК_ЗаявкаНаПеремещениеТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_ЗаявкаНаПеремещениеТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС
	               |ИЗ
	               |	Документ.ТК_ЗаявкаНаПеремещение.Товары КАК ТК_ЗаявкаНаПеремещениеТовары
	               |ГДЕ
	               |	ТК_ЗаявкаНаПеремещениеТовары.Ссылка = &Ссылка";
	
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;	
	
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
	Организация = Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(ЭтотОбъект.ТорговаяПлощадка);
	Если Не ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда
		СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(ТорговаяПлощадкаПолучатель);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СкладКомпанииПолучатель) Тогда 
		Если СкладКомпанииПолучатель.Розничный Тогда
			ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(СкладКомпанииПолучатель);
		Иначе
			ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(СкладКомпанииПолучатель);
		КонецЕсли;
	КонецЕсли;
	ВалютаДокумента = ТипЦен.ВалютаЦены;
	КурсДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента,Дата).Курс;
	Товары.Загрузить(ТаблицаТоваров);
	
КонецПроцедуры

Процедура ЗаполнитьДОкументНаОснованииВыпускаПродукции(Знач ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДОБАВИТЬКДАТЕ(ВыпускПродукции.Дата, СЕКУНДА, 10) КАК Дата,
	               |	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВЫРАЗИТЬ(ВыпускПродукции.ДокументОснование КАК Документ.ЗаказПокупателя).ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.КурсДокумента КАК КурсДокумента,
	               |	ВыпускПродукции.ТипЦен КАК ТипЦен,
	               |	ВыпускПродукции.Комментарий КАК Комментарий,
	               |	ВыпускПродукции.Ссылка КАК ДокументОснование,
	               |	ВЫРАЗИТЬ(ВыпускПродукции.ДокументОснование КАК Документ.ЗаказПокупателя) КАК ЗаказПокупателя,
	               |	ВыпускПродукции.Продукция.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Автор = Пользователи.ТекущийПользователь();
	ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал;
	СкладКомпанииПолучатель = Неопределено;
	
	Товары.Очистить();
	РаспределениеОтгрузки.Очистить();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Выборка.ТорговаяПлощадкаПолучатель);
		
		тзТовары = Выборка.Товары.Выгрузить();
		Товары.Загрузить(тзТовары);
		
		Если ЗначениеЗаполнено(Выборка.ЗаказПокупателя) Тогда 
			СтрРаспределение = РаспределениеОтгрузки.Добавить();
			СтрРаспределение.ЗаказПокупателя = Выборка.ЗаказПокупателя;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Массив.Добавить(Движения.ОстаткиТоваровКомпании);
		
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
	
КонецПроцедуры

Процедура СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц)
	
	Если ТаблицаОшибок.Количество()> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Перемещение превышает остаток товара компании на складе %1 %2 %3';uk='Переміщення перевищує залишок товару компанії на складі %1 %2 %3'"),
		СкладКомпании);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстСообщения,
		ЭтотОбъект);	
		ДополнительныеСвойства.РезультатОбработки.ДобавитьСтроку(ТекстСообщения);
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Номенклатура: %1, недостаточно %2 %3';uk='Номенклатура: %1, недостатньо %2 %3'"),
			НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.ХарактеристикаНоменклатуры),
			СтрокаТаблицы.Количество,
			СтрокаТаблицы.ЕдиницаИзмерения);
			
			ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			Ссылка);
			
			ДополнительныеСвойства.РезультатОбработки.ДобавитьСтроку(ТекстСообщения);
			
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ВыполнитьКонтрольОстатков(Отказ,РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный тогда
		ДатаКонтроля =  ТекущаяДатаСеанса();
	Иначе
		ДатаКонтроля =  Дата;
	КонецЕсли;
	
	
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ЗапасыСервер.УстановитьБлокировкуОстатоковТоваровКомпании(МенеджерВременныхТаблиц);
	ЗапасыСервер.ТаблицаОстатковТоваровКомпании(Ссылка, СкладКомпании, ДатаКонтроля, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	ТаблицаОшибок = ЗапасыСервер.ТаблицаОшибокОстатковТоваров();
	ЗапасыСервер.ЗаполнитьТаблицуОшибок(МенеджерВременныхТаблиц,ДополнительныеСвойства,ТаблицаОшибок,Отказ);	
	СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц);
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&ХозОперация КАК ХозОперация,
	|	&Организация КАК Организация,
	|	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	&СкладКомпании КАК СкладКомпании,
	|	&СкладКомпанииПолучатель КАК СкладКомпанииПолучательПолучатель,
	|	&ТорговаяПлощадка КАК ТорговаяПлощадкаОтправитель,
	|	&ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &НеВестиУчетПоХарактеристикам
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаТоваров.ХарактеристикаНоменклатуры
	|	КОНЕЦ КАК ХарактеристикаНоменклатуры,
	|	ТаблицаТоваров.КоличествоБазовое КАК Количество,
	|	&СкладКомпании КАК СкладКомпании
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров";
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	Запрос.УстановитьПараметр("Дата",								Дата);
	Запрос.УстановитьПараметр("ХозОперация",						ХозОперация);
	Запрос.УстановитьПараметр("НеВестиУчетПоХарактеристикам",		Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(СкладКомпании, Дата));
	Запрос.УстановитьПараметр("Организация",						Организация);
	Запрос.УстановитьПараметр("СкладКомпанииПолучатель",			СкладКомпанииПолучатель);
	Запрос.УстановитьПараметр("СкладКомпании",						СкладКомпании);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",				ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",					ТорговаяПлощадка);
	Запрос.УстановитьПараметр("ТорговаяПлощадкаПолучатель",			ТорговаяПлощадкаПолучатель);
	Запрос.УстановитьПараметр("ТаблицаТоваров",						Товары.Выгрузить());
	
	
	Запрос.Выполнить();
	
	
	
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Процедура ПроверитьДокНаВоду()
	
	//ТЕСТирование = Ложь;
	ТЕСТирование = Истина;
	УстановитьПривилегированныйРежим(Истина);
	
	Вода = Справочники.Номенклатура.НайтиПоКоду("235827");						//	Вода для кофе 1л
	
	стрВода = Товары.НайтиСтроки(Новый Структура("Номенклатура",Вода));
	
	Если стрВода.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЛитражБутылей = ТК_СправочникиПовтИсп.ЗначенияСвойства("ЕмкостьБутыля");
	
	Если ЛитражБутылей.Количество() = 0 Тогда
		ДополнительныеСвойства.Вставить("ОшибкаПоВоде","Бутыли не найдена. Установите литраж бутылей у СКЮ в доп. свойствах!!!");	
		Товары.Добавить();
		Возврат;
	КонецЕсли;
	
	Бутыли = Новый Соответствие;
	
	Если ТЕСТирование Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Бутыли.ОбъЕкт КАК Номенклатура,
		|	Бутыли.Значение КАК Литраж
		|ПОМЕСТИТЬ Бутыли
		|ИЗ
		|	&Бутыли КАК Бутыли
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Бутыли.Номенклатура КАК Номенклатура,
		|	Бутыли.Литраж КАК Литраж
		|ИЗ
		|	Бутыли КАК Бутыли
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОграничениеАссортимента.СрезПоследних(&НаДату, ТорговаяПлощадка = &ТорговаяПлощадка) КАК ОграничениеАссортиментаСрезПоследних
		|		ПО Бутыли.Номенклатура = ОграничениеАссортиментаСрезПоследних.Номенклатура
		|ГДЕ
		|	ОграничениеАссортиментаСрезПоследних.Состояние <> 0
		|	И ОграничениеАссортиментаСрезПоследних.ВидАссортимента = ЗНАЧЕНИЕ(Справочник.ВидыАссортимента.Незаказываемый)";
		
		Запрос.УстановитьПараметр("НаДату", ТекущаяДата());
		Запрос.УстановитьПараметр("Бутыли", ЛитражБутылей);
		Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадкаПолучатель);
		
		РезультатЗапроса = Запрос.Выполнить();	//	РезультатЗапроса.Выгрузить()
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Литраж) Тогда
				Бутыли.Вставить(Выборка.Литраж,Выборка.Номенклатура);	
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого Бутыль Из ЛитражБутылей Цикл
			Если ЗначениеЗаполнено(Бутыль.Значение) Тогда
				Бутыли.Вставить(Бутыль.Значение,Бутыль.Объект);	
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого стр Из стрВода Цикл
		Квант = РегистрыСведений.ТК_ОптимальноеКоличествоНоменклатуры.ПолучитьПоследнее(ТекущаяДата(),Новый Структура("ТорговаяПлощадка,Номенклатура",ТорговаяПлощадкаПолучатель,Вода)).КвантПоставки;
		Бутыль = Бутыли.Получить(Квант);
		
		строка = Товары.Добавить();
		
		Если ЗначениеЗаполнено(Бутыль) Тогда
			Количество = стр.КоличествоБазовое / Квант;
			строка.Номенклатура = Бутыль;
			строка.Количество = ?(Цел(Количество) = Количество, Количество, 0);
			
			ЦеныБезХарактеристики = Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(СкладКомпанииПолучатель, Дата);
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", строка.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", строка.ЕдиницаИзмерения);
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
			
			СтруктураПолейДляЗаполненияЦен = Новый Структура;		
			СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ЭтотОбъект));
			
			СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
			Если ЦеныБезХарактеристики Тогда 
				СтруктураДействий.Вставить("ЦеныБезХарактеристики", Истина);
			КонецЕсли;
			
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(строка, СтруктураДействий, Неопределено);
			
			Если строка.Количество <> Количество Тогда
				ДополнительныеСвойства.Вставить("ОшибкаПоВоде","Количество воды ["+стр.КоличествоБазовое+"] не кратно кванту ["+Квант+"] !!!");	
			КонецЕсли;		
		Иначе
			ДополнительныеСвойства.Вставить("ОшибкаПоВоде","Тара не найдена. Неоднозначный квант на воду <"+Квант+"> !!!");	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ)
	
	СписокНоменклатуры = Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	
	СоответствиеНоменклатуры = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(
								Организация,
								СписокНоменклатуры,
								ТорговаяПлощадка);
								
	Для Каждого Стр Из СоответствиеНоменклатуры Цикл 
		Если Стр.Значение.Организация <> Организация Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номенклатура: %1 закреплена за организацией ""%2""'; uk = 'Номенклатура: %1 закріплена за організацією ""%2""'"),
					Стр.Ключ,
					Стр.Значение.Организация),
				ЭтотОбъект,
				"Товары");
				
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтправитьУведомление()
	
	Если ЭтотОбъект.СкладКомпании.Код = "AC000021" Тогда
		
		ЭлАдрес = "Dmitriy.Ovcharov@digma.ua; kaydanovzzz@gmail.com";
		
		МассивДанных = Новый Массив;        
		МассивДанных.Добавить(Метаданные.Документы.ПеремещениеТоваров);
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("ФормаСписка", МассивДанных);
		
		мОбъектыПечати = Новый Массив();
		мОбъектыПечати.Добавить(ЭтотОбъект.Ссылка);
		
		НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
		НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.XLSX);
		
		ПечатнаяФорма = ВРег("Перемещение товаров");
		ДокументНапечатан = Ложь;
		Для Каждого Команда Из КомандыПечати Цикл 
			
			Если ВРег(Команда.Представление) = ПечатнаяФорма Тогда
				тзПечатныеФормы = УправлениеПечатью.НапечататьВФайл(Команда,мОбъектыПечати,НастройкиСохранения);  
				ДокументНапечатан = Истина;
				Прервать;
			КонецЕсли;    
		КонецЦикла;
		
		Если ДокументНапечатан Тогда
			Если тзПечатныеФормы.Количество() = 0 Тогда Возврат; КонецЕсли;
			
			ДД = тзПечатныеФормы[0].ДвоичныеДанные;  
			ИмяФайла = тзПечатныеФормы[0].ИмяФайла;
			АдресВХ =    ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор());
			МассивВложения = Новый Массив;
			СтруктураВложения = Новый Структура;
			СтруктураВложения.Вставить("Представление",ИмяФайла);
			СтруктураВложения.Вставить("АдресВоВременномХранилище",АдресВХ);
			СтруктураВложения.Вставить("Кодировка","");
			СтруктураВложения.Вставить("Идентификатор","");
			МассивВложения.Добавить(СтруктураВложения);
			
			ПараметрыПисьма = Новый Структура;
			ПараметрыПисьма.Вставить("Кому",ЭлАдрес);
			ПараметрыПисьма.Вставить("Тема","Накладная");
			ПараметрыПисьма.Вставить("Тело",ИмяФайла);
			ПараметрыПисьма.Вставить("Вложения",МассивВложения);
			
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
			РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти

	
	
#КонецЕсли

#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
												
	// Счет на оплату
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.СчетНаОплату";
    КомандаПечати.Идентификатор = "СчетНаУслугиДвойной";
    КомандаПечати.Представление = НСтр("ru = 'Счет на оплату (услуги) 2 на лист'; uk = 'Рахунок на оплату (послуги) 2 на аркуш'");
	КомандаПечати.СразуНаПринтер = Ложь;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 

	// Счет на оплату Казначейство
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.СчетНаОплату";
    КомандаПечати.Идентификатор = "СчетНаОлату";
    КомандаПечати.Представление = НСтр("ru = 'Счет на оплату'; uk = 'Рахунок на оплату '");
	КомандаПечати.СразуНаПринтер = Ложь;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 

    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.СчетНаОплату";
    КомандаПечати.Идентификатор = "СчетНаОлатуУслуги";
    КомандаПечати.Представление = НСтр("ru = 'Счет на оплату (услуга)'; uk = 'Рахунок на оплату (послуга)'");
	КомандаПечати.СразуНаПринтер = Ложь;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 

КонецПроцедуры


#КонецОбласти	

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаУслугиДвойной") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетНаУслугиДвойной",
			НСтр("ru = 'Счет на оплату'; uk = 'Рахунок на оплату'"), 
			ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.СчетНаОплату.ПФ_MXL_СчетНаУслугиДвойной");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОлату") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетНаОлату",
			НСтр("ru = 'Счет на оплату'; uk = 'Рахунок на оплату'"), 
			ПечатьСчетНаОплатуКазначейство(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.СчетНаОплату.ПФ_MXL_СчетНаОплату");		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОлатуУслуги") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетНаОлатуУслуги",
			НСтр("ru = 'Счет на оплату(услуга)'; uk = 'Рахунок на оплату (послуга)'"), 
			ПечатьСчетНаОплатуУслуга(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.СчетНаОплату.ПФ_MXL_СчетУслуга");		

	КонецЕсли;	
	
КонецПроцедуры


// СчетНаОплату
Функция ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетНаОплату.Номер КАК Номер,
	               |	СчетНаОплату.Дата КАК Дата,
	               |	СчетНаОплату.Автор КАК Автор,
	               |	СчетНаОплату.Организация КАК Организация,
	               |	СчетНаОплату.Контрагент КАК Контрагент,
	               |	СчетНаОплату.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	СчетНаОплату.СуммаНДСДокумента КАК СуммаНДСДокумента,
	               |	СчетНаОплату.СуммаДокумента КАК СуммаДокумента,
	               |	СчетНаОплату.ДокументОснование КАК ДокументОснование,
	               |	СчетНаОплату.РасчетныйСчетОрганизации КАК РасчетныйСчетОрганизации,
	               |	СчетНаОплату.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	СчетНаОплату.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |	) КАК Товары,
	               |	СчетНаОплату.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.СчетНаОплату КАК СчетНаОплату
	               |ГДЕ
	               |	СчетНаОплату.Ссылка В(&МассивСсылок)";
	               
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СчетНаОплату";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СчетНаОплату.ПФ_MXL_СчетНаУслугиДвойной");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьПробел 			= Макет.ПолучитьОбласть("Пробел");	
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
 	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьИтогоДополнительно= Макет.ПолучитьОбласть("ИтогоДополнительно");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьСертификаты		= Макет.ПолучитьОбласть("Область");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Счет на оплату №%1 от %2'; uk = 'Рахунок на оплату №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, НСтр("ru = 'Л=ru; ДЛФ=DD'; uk = 'Л=uk; ДЛФ=DD'",КодЯзыкаПечать)));
										
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
				
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		СтруктураШапки.Вставить("ПредставлениеПокупателя", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		                              
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ТабличныйДокумент.Вывести(ОбластьПробел);
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Цена, Сумма");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			СтруктураСтроки.Вставить("Товар", СокрЛП(ВыборкаТовары.Номенклатура) + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("Количество", Строка(ВыборкаТовары.Количество));
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			//КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
			//									ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
			//										ВыборкаТовары.Номенклатура,
			//										ВыборкаТовары.КоличествоБазовое,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
			//									СтруктураИтоговМест);

			//СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПробел);
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоБезНДС", СуммаВсего - СуммаНДС);
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("ИтогоСумма", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		//СтруктураПодвала.Вставить("ВсегоКво", Количество);
		//СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
		ОбластьИтогоНДС.Параметры.НДС = "В тому числі ПДВ:";
		ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		
		ОбластьИтогоДополнительно.Параметры.Заполнить(СтруктураПодвала);
		СтрВсего = "Всього без ПДВ:";		
		ОбластьИтогоДополнительно.Параметры.Подпись = СтрВсего;
		ТабличныйДокумент.Вывести(ОбластьИтогоДополнительно);	
		
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ОбластьИтого.Параметры.Итого = "Всього з ПДВ:";
		ТабличныйДокумент.Вывести(ОбластьИтого);		
				
		ОбластьПодписи.Параметры.СуммаПрописью  = "Загальна вартість робіт (послуг) склала з ПДВ " + СуммаПрописью+".";
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		//ТабличныйДокумент.Вывести(ОбластьСертификаты);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПечатьСчетНаОплатуКазначейство(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СчетНаОплату.Номер КАК Номер,
	|	СчетНаОплату.Дата КАК Дата,
	|	СчетНаОплату.Автор КАК Автор,
	|	СчетНаОплату.Организация КАК Организация,
	|	СчетНаОплату.Контрагент КАК Контрагент,
	|	СчетНаОплату.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	СчетНаОплату.СуммаНДСДокумента КАК СуммаНДСДокумента,
	|	СчетНаОплату.СуммаДокумента КАК СуммаДокумента,
	|	СчетНаОплату.ДокументОснование КАК ДокументОснование,
	|	СчетНаОплату.РасчетныйСчетОрганизации КАК РасчетныйСчетОрганизации,
	|	СчетНаОплату.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	СчетНаОплату.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Коэффициент КАК Коэффициент,
	|		Цена КАК Цена,
	|		ЦенаБезНалогов КАК ЦенаБезНалогов,
	|		СуммаБезНалогов КАК СуммаБезНалогов,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		СуммаВсего КАК СуммаВсего,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ВЫБОР
	|			КОГДА СчетНаОплату.Товары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				ТОГДА СчетНаОплату.Товары.Номенклатура.КодУКТВЭД
	|			КОГДА СчетНаОплату.Товары.ХарактеристикаНоменклатуры.УКТВЭД = ЗНАЧЕНИЕ(справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|				ТОГДА СчетНаОплату.Товары.Номенклатура.КодУКТВЭД
	|			ИНАЧЕ СчетНаОплату.Товары.ХарактеристикаНоменклатуры.УКТВЭД
	|		КОНЕЦ КАК КодУКТВЭД
	|	) КАК Товары,
	|	СчетНаОплату.Ссылка КАК Ссылка,
	|	СчетНаОплату.ВалютаДокумента КАК ВалютаДокумента
	|ИЗ
	|	Документ.СчетНаОплату КАК СчетНаОплату
	|ГДЕ
	|	СчетНаОплату.Ссылка В(&МассивСсылок)";
	               
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СчетНаОплату";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СчетНаОплату.ПФ_MXL_СчетНаОплату");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
 	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Счет на оплату №%1 от %2'; uk = 'Рахунок на оплату №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, НСтр("ru = 'Л=ru; ДЛФ=DD'; uk = 'Л=uk; ДЛФ=DD'",КодЯзыкаПечать)));
										
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
				
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		СтруктураШапки.Вставить("ПредставлениеПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		                              
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Цена, СуммаВсего");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			СтруктураСтроки.Вставить("СуммаНДС", Окр(ВыборкаТовары.СуммаНДС,3));
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			СтруктураСтроки.Вставить("Товар", СокрЛП(ВыборкаТовары.Номенклатура) + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), "")+", Код УКТВЭД - "+ВыборкаТовары.КодУКТВЭД);
			СтруктураСтроки.Вставить("Количество", Строка(ВыборкаТовары.Количество)+" " +Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			//КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
			//									ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
			//										ВыборкаТовары.Номенклатура,
			//										ВыборкаТовары.КоличествоБазовое,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
			//									СтруктураИтоговМест);

			//СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоНДС", Окр(СуммаНДС,3));
		СтруктураПодвала.Вставить("ИтогоСумма", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		СтруктураПодвала.Вставить("ВалютаДокумента", ВыборкаДокументов.ВалютаДокумента);

		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		//СтруктураПодвала.Вставить("ВсегоКво", Количество);
		//СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ОбластьПодвал.Параметры.СуммаПрописью  = "Загальна вартість робіт (послуг) склала з ПДВ " + СуммаПрописью+".";
		ТабличныйДокумент.Вывести(ОбластьПодвал);
					
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

// СчетНаОплату услуга
Функция ПечатьСчетНаОплатуУслуга(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетНаОплату.Номер КАК Номер,
	               |	СчетНаОплату.Дата КАК Дата,
	               |	СчетНаОплату.Автор КАК Автор,
	               |	СчетНаОплату.Организация КАК Организация,
	               |	СчетНаОплату.Контрагент КАК Контрагент,
	               |	СчетНаОплату.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	СчетНаОплату.СуммаНДСДокумента КАК СуммаНДСДокумента,
	               |	СчетНаОплату.СуммаДокумента КАК СуммаДокумента,
	               |	СчетНаОплату.ДокументОснование КАК ДокументОснование,
	               |	СчетНаОплату.РасчетныйСчетОрганизации КАК РасчетныйСчетОрганизации,
	               |	СчетНаОплату.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	СчетНаОплату.Ссылка КАК Ссылка,
	               |	СчетНаОплату.Организация.КодПоЕДРПОУ КАК ЕДРПОУОрганизации,
	               |	СчетНаОплату.РасчетныйСчетОрганизации.Банк КАК БанкОрганизации,
	               |	СчетНаОплату.РасчетныйСчетОрганизации.НомерСчета КАК НомерРасчетногоСчетаОрганизации,
	               |	СчетНаОплату.Организация.НаименованиеПолное КАК НазваниеОрганизации,
	               |	СчетНаОплату.РасчетныйСчетОрганизации.Банк.Код КАК МФОБанкаОрганизации,
	               |	СчетНаОплату.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		Содержание КАК Товар,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.СчетНаОплату КАК СчетНаОплату
	               |ГДЕ
	               |	СчетНаОплату.Ссылка В(&МассивСсылок)";
	               
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СчетНаОплатуУслуга";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СчетНаОплату.ПФ_MXL_СчетУслуга");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовокПред	= Макет.ПолучитьОбласть("ЗаголовокПредупреждение");
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокСчета	= Макет.ПолучитьОбласть("ЗаголовокСчета");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьПробел 			= Макет.ПолучитьОбласть("Пробел");	
	ОбластьДопИнформация	= Макет.ПолучитьОбласть("ДопИнформация");	
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицыБезСкидок");
 	ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаБезСкидок");
	ОбластьИтого			= Макет.ПолучитьОбласть("ИтогоБезСкидок");
	ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	//ОбластьИтогоДополнительно= Макет.ПолучитьОбласть("ИтогоДополнительно");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Счет на оплату №%1 от %2'; uk = 'Рахунок на оплату №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, НСтр("ru = 'Л=ru; ДЛФ=DD'; uk = 'Л=uk; ДЛФ=DD'",КодЯзыкаПечать)));
										
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
				
		//ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,,,,,,"Услуга"));
		СтруктураШапки.Вставить("ПредставлениеПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина,,,, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		
		СтруктураШапки.Вставить("Цена", "Ціна з ПДВ");
		СтруктураШапки.Вставить("Сумма", "Сумма з ПДВ");

		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовокПред.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовокПред);
		
		ОбластьЗаголовокСчета.Параметры.Заполнить(ВыборкаДокументов);
		ТабличныйДокумент.Вывести(ОбластьЗаголовокСчета);
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		                              
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ОбластьДопИнформация.Параметры.Заполнить(Новый Структура("НазваниеПараметра,ЗначениеПараметра", 
																"Договір",
																ВыборкаДокументов.ДоговорВзаиморасчетов.НаименованиеДляПечати));
		ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		
		ТабличныйДокумент.Вывести(ОбластьПробел);
		
		ОбластьШапкаТаблицы.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Цена, Сумма");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			СтруктураСтроки.Вставить("Артикул", СокрЛП(ВыборкаТовары.Номенклатура.Артикул));
			СтруктураСтроки.Вставить("Товар", СокрЛП(ВыборкаТовары.Товар));
			СтруктураСтроки.Вставить("Количество", Строка(ВыборкаТовары.Количество));
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПробел);
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("Всего", СуммаВсего);
		СтруктураПодвала.Вставить("ИтогоСумма", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		СуммаПрописьюПДВ = ЧислоПрописью(СуммаНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		
		СтруктураПодвала.Вставить("Отпустил", ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов,ВыборкаДокументов.Организация, ТекущаяДата()));
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
		ОбластьИтогоНДС.Параметры.НДС = "У тому числі ПДВ:";
		ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		
		ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока = "Всього найменувань "+Количество+", на сумму "+СуммаВсего+"UAH.";
		ОбластьСуммаПрописью.Параметры.СуммаПрописью  = СуммаПрописью+"."+Символы.ПС+"У т.ч.  ПДВ: "+СуммаПрописьюПДВ;
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

Функция СтруктураДокумента(ДокументСсылка,ТолькоСтруктуру)Экспорт
	
	СтруктураПоказателей = Новый Структура();
	СтруктураПоказателейТовары = Новый Структура();
	СтруктураПоказателей.Вставить("СчетНаОплату",   	Новый Структура());	// данные налоговой накладной
	СтруктураПоказателейТовары.Вставить("TAB",   	Новый ТаблицаЗначений());  // данные табличной части
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A1");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("NAME");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A3");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("EI");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("CODEI");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A5");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A4");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A6");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("SUMPDV");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("STAVKAPDV");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетНаОплату.Ссылка КАК Ссылка,
		|	СчетНаОплату.Номер КАК Номер,
		|	СчетНаОплату.Дата КАК Дата,
		|	СчетНаОплату.РасчетныйСчетОрганизации КАК РасчетныйСчетОрганизации,
		|	СчетНаОплату.Организация КАК Организация,
		|	СчетНаОплату.Автор КАК Автор,
		|	СчетНаОплату.СуммаДокумента КАК СуммаДокумента,
		|	СчетНаОплату.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	СчетНаОплату.Контрагент КАК Контрагент,
		|	СчетНаОплату.СуммаНДСДокумента КАК СуммаНДСДокумента,
		|	СчетНаОплату.СуммаДокумента - СчетНаОплату.СуммаНДСДокумента КАК СуммаБезПДВ
		|ИЗ
		|	Документ.СчетНаОплату КАК СчетНаОплату
		|ГДЕ
		|	СчетНаОплату.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетНаОплатуТовары.Ссылка КАК Ссылка,
		|	СчетНаОплатуТовары.Номенклатура КАК Номенклатура,
		|	СчетНаОплатуТовары.Количество КАК Количество,
		|	СчетНаОплатуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СчетНаОплатуТовары.Цена КАК Цена,
		|	СчетНаОплатуТовары.СуммаВсего КАК СуммаВсего,
		|	СчетНаОплатуТовары.СуммаСкидки КАК СуммаСкидки,
		|	СчетНаОплатуТовары.Номенклатура.Наименование2 КАК НоменклатураНаименование2,
		|	СчетНаОплатуТовары.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	СчетНаОплатуТовары.Содержание КАК Содержание,
		|	СчетНаОплатуТовары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	Документ.СчетНаОплату.Товары КАК СчетНаОплатуТовары
		|ГДЕ
		|	СчетНаОплатуТовары.Ссылка = &ДокументСсылка";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Украина", Справочники.СтраныМира.НайтиПоКоду("804"));

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ШапкаДокумента = РезультатЗапроса[0].Выбрать();
	ШапкаДокумента.Следующий();
	
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	
	РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация,"Услуги");
	Если НЕ ЗначениеЗаполнено(РеквизитыОрганизации.НомерСчета)  Тогда
		РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация,"Текущий");
	КонецЕсли; 
	
	СтруктураПоказателей.СчетНаОплату.Вставить("NUM",ШапкаДокумента.Номер);
	СтруктураПоказателей.СчетНаОплату.Вставить("DOCDATE",Формат(ШапкаДокумента.Дата,"ДФ=dd.MM.yyyy"));
	
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_NAME",		СокрЛП(РеквизитыОрганизации.НаименованиеПолное));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_RS",		СокрЛП(ШапкаДокумента.РасчетныйСчетОрганизации.НомерСчета));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_NMBANK",	СокрЛП(ШапкаДокумента.РасчетныйСчетОрганизации.Банк));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_CBANK",	СокрЛП(ШапкаДокумента.РасчетныйСчетОрганизации.КодБанка));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_ADR",		СокрЛП(РеквизитыОрганизации.Адрес));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_TELEFON",	"");
	СтруктураПоказателей.СчетНаОплату.Вставить("TEXT_FIRM_E_D",	"Код за ЄДРПОУ");
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_EDRPOU",	СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_INN",		СокрЛП(РеквизитыОрганизации.ИНН));
	СтруктураПоказателей.СчетНаОплату.Вставить("FIRM_DEPT",		"");
	СтруктураПоказателей.СчетНаОплату.Вставить("TAXSYSTEM",		"");

	СтруктураПоказателей.СчетНаОплату.Вставить("SIDE_CD_K",		СокрЛП(ШапкаДокумента.Контрагент.НаименованиеПолное));
	СтруктураПоказателей.СчетНаОплату.Вставить("TEXT_SIDE_E_D",	"Код за ЄДРПОУ");
	СтруктураПоказателей.СчетНаОплату.Вставить("SIDE_TEL_K",	"");
	СтруктураПоказателей.СчетНаОплату.Вставить("SIDE_EDRPOU_K",	СокрЛП(ШапкаДокумента.Контрагент.КодПоЕДРПОУ));
	СтруктураПоказателей.СчетНаОплату.Вставить("SIDE_DEPT_K",	"");
	
	СтруктураПоказателей.СчетНаОплату.Вставить("DOG_DATA_T",	"від");
	СтруктураПоказателей.СчетНаОплату.Вставить("DOG_NUM",		СокрЛП(ШапкаДокумента.ДоговорВзаиморасчетов.Номер));
	СтруктураПоказателей.СчетНаОплату.Вставить("DOG_NUM_T",		"№");
	СтруктураПоказателей.СчетНаОплату.Вставить("DOG_DATE",		Формат(ШапкаДокумента.ДоговорВзаиморасчетов.Дата, "ДФ=dd.MM.yyyy"));
	
	СтруктураПоказателей.СчетНаОплату.Вставить("DOCSUM",		Формат(ШапкаДокумента.СуммаДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.СчетНаОплату.Вставить("KVO",			ТаблицаТовары.Количество());
	СтруктураПоказателей.СчетНаОплату.Вставить("ZNYGKA", 		ТаблицаТовары.Итог("СуммаСкидки"));
	СтруктураПоказателей.СчетНаОплату.Вставить("SUMPDV",		Формат(ШапкаДокумента.СуммаНДСДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.СчетНаОплату.Вставить("DOCSUM_TEXT",	Формат(ШапкаДокумента.СуммаДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.СчетНаОплату.Вставить("SUMPDV_TEXT",	Формат(ШапкаДокумента.СуммаНДСДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	
	//СтруктураПоказателей.СчетНаОплату.Вставить("SUMWITHOUTPDV",	Формат(ШапкаДокумента.СуммаБезПДВ,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	//СтруктураПоказателей.СчетНаОплату.Вставить("SUMWITHOUTPDV_TEXT",	Формат(ШапкаДокумента.СуммаБезПДВ,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	//СтруктураПоказателей.СчетНаОплату.Вставить("VO_POSTNAME",	"директор");
	СтруктураПоказателей.СчетНаОплату.Вставить("VO_NAME",		СокрЛП(РеквизитыОрганизации.Руководитель));
	
	Для каждого СтрокаТаблицы Из ТаблицаТовары Цикл
		
		СтрокаСтруктуры = СтруктураПоказателейТовары.TAB.Добавить();
		
		СтрокаСтруктуры.NAME  = ?(Не ПустаяСтрока(СтрокаТаблицы.Содержание), СтрокаТаблицы.Содержание, СтрокаТаблицы.НоменклатураНаименование2);
		СтрокаСтруктуры.A3    = Формат(СтрокаТаблицы.Количество,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.EI    = СокрЛП("послуга");
		СтрокаСтруктуры.CODEI = СокрЛП("-");
		СтрокаСтруктуры.A5 	  = Формат(СтрокаТаблицы.Цена,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.A4 	  = Формат(СтрокаТаблицы.СуммаСкидки,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.A6 	  = Формат(СтрокаТаблицы.СуммаВсего,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
	    СтрокаСтруктуры.SUMPDV = Формат(СтрокаТаблицы.СуммаНДС,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.STAVKAPDV = "20";

	КонецЦикла;

	Если ТолькоСтруктуру Тогда
		Возврат СтруктураПоказателей;
	КонецЕсли;
	
	тДата = Формат(ТекущаяДатаСеанса(),"ДФ=dd.MM.yyyy");
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.УстановитьСтроку("windows-1251");
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	
	///ZVIT	
	ОбъектXML.ЗаписатьНачалоЭлемента("ZVIT");		
	
	///TRANSPORT	
	ОбъектXML.ЗаписатьНачалоЭлемента("TRANSPORT");		
	ЗаписатьXML(ОбъектXML, "4.1", "VERSION");
	ЗаписатьXML(ОбъектXML, тДата, "CREATEDATE");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///TRANSPORT	
	
	///ORG	
	ОбъектXML.ЗаписатьНачалоЭлемента("ORG");		
	
	///FIELDS	
	ОбъектXML.ЗаписатьНачалоЭлемента("FIELDS");
	ЗаписатьXML(ОбъектXML, СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ), "EDRPOU");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///FIELDS	
	
	//CARD
	ОбъектXML.ЗаписатьНачалоЭлемента("CARD");
	ОбъектXML.ЗаписатьАтрибут("RTFDOC","1");
	
	///FIELDS	
	ОбъектXML.ЗаписатьНачалоЭлемента("FIELDS");
	ЗаписатьXML(ОбъектXML, "Рахунок (ціна з ПДВ)", "DOCNAME");
	ЗаписатьXML(ОбъектXML, "1С82РАХ",			   "CHARCODE");
	ЗаписатьXML(ОбъектXML, "7",					   "PARTCODE");
	ЗаписатьXML(ОбъектXML, "", 					   "NOTATION");
	ЗаписатьXML(ОбъектXML, "10103", 			   "SDOCTYPE");
	ЗаписатьXML(ОбъектXML, тДата, 			  	   "CRTDATE");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///FIELDS	
	
	//DOCUMENT
	ОбъектXML.ЗаписатьНачалоЭлемента("DOCUMENT");
	
	Для Каждого Параметр Из СтруктураПоказателей.СчетНаОплату Цикл
		
		ОбъектXML.ЗаписатьНачалоЭлемента("ROW");											
		ОбъектXML.ЗаписатьАтрибут("LINE", "0");
		ОбъектXML.ЗаписатьАтрибут("TAB", "0");
		ОбъектXML.ЗаписатьАтрибут("NAME", Параметр.Ключ);
		ЗаписатьXML(ОбъектXML,Параметр.Значение, "VALUE");	
		ОбъектXML.ЗаписатьКонецЭлемента();

	КонецЦикла; 

	СоответствиеКолонокТовары = Новый Соответствие;
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_NAME", 				"NAME");
	СоответствиеКолонокТовары.Вставить("TAB1_A3", 								"A3");
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_CHARCODEUMEASURE", 	"EI");
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_NUMHBUMEASURE", 		"CODEI");
	СоответствиеКолонокТовары.Вставить("TAB1_A5", 								"A5");
	СоответствиеКолонокТовары.Вставить("TAB1_A4",								"A4");	
	СоответствиеКолонокТовары.Вставить("TAB1_A6",								"A6");	
	СоответствиеКолонокТовары.Вставить("TAB1_SUMPDV",							"SUMPDV");	
	СоответствиеКолонокТовары.Вставить("TAB1_STAVKAPDV",						"STAVKAPDV");	

	Для Каждого Параметр Из СоответствиеКолонокТовары Цикл
		Номер = 0;
		Для Каждого Стр Из СтруктураПоказателейТовары.TAB Цикл					
			Значение = Неопределено;
			
			ОбъектXML.ЗаписатьНачалоЭлемента("ROW");											
			ОбъектXML.ЗаписатьАтрибут("LINE", Строка(Номер));
			ОбъектXML.ЗаписатьАтрибут("TAB", "1");
			ОбъектXML.ЗаписатьАтрибут("NAME", Параметр.Ключ);
			ЗаписатьXML(ОбъектXML,Стр[Параметр.Значение], "VALUE");	
			ОбъектXML.ЗаписатьКонецЭлемента();
			
			Номер = Номер + 1;

		КонецЦикла;
	КонецЦикла;		
	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DOCUMENT	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///CARD	
	ОбъектXML.ЗаписатьКонецЭлемента(); //ORG
	ОбъектXML.ЗаписатьКонецЭлемента(); //ZVIT

	СтрXML = ОбъектXML.Закрыть();	
	
	
	ПотокДанных = Новый ПотокВПамяти();
	
	ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных,"windows-1251");
	ЗаписьДанных.ЗаписатьСтроку(СтрXML,"windows-1251");
	ЗаписьДанных.Закрыть();
	
	ДВ = ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные();
	СтруктураПоказателей.Вставить("ДанныеXML",ДВ);

	СтруктураПоказателей.Вставить("ТекстXML",СтрXML);
	СтруктураПоказателей.Вставить("ИмяФайла",СокрЛП(Строка(ШапкаДокумента.Организация)) + "_" + Формат(ШапкаДокумента.Дата, "ДФ=yyyyMMdd")+"_"+ШапкаДокумента.Номер+"_Рахунок.xml"); 
	
	Возврат СтруктураПоказателей;

КонецФункции

Функция СформироватьСчет(МассивДокументов,ТолькоСтруктуру=Ложь)Экспорт
	
	МассивВозврата = Новый Массив;
	
	
	Для Индекс = 0 ПО МассивДокументов.ВГраница() Цикл
		
		Документ = МассивДокументов[Индекс];
		
		СтруктруаДокумента = СтруктураДокумента(Документ,ТолькоСтруктуру);
		МассивВозврата.Добавить(Новый Структура("Документ,Выгрузка",Документ,СтруктруаДокумента));
		
	КонецЦикла;
	
	
	Возврат МассивВозврата;
	
КонецФункции

#КонецОбласти

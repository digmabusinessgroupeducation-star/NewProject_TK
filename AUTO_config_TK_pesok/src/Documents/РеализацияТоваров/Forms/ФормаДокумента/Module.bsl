&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
		//Объект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
		// СтандартныеПодсистемы.Свойства
	ГруппыДляРазмещения = Новый СписокЗначений;
	ГруппыДляРазмещения.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Документ_РеализацияТоваров_ТТН"), Элементы.ГруппаТТН.Имя);
	ГруппыДляРазмещения.Добавить("ВсеОстальные", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", ГруппыДляРазмещения);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	
	///EDI
	ПараметрыПриСозданииEDI = ТК_ОбменEDI.ПараметрыПриСозданииНаСервере_ФормаДокумента();
	ПараметрыПриСозданииEDI.Форма = ЭтотОбъект;
	ПараметрыПриСозданииEDI.ДокументСсылка = Объект.Ссылка;
	ПараметрыПриСозданииEDI.МестоРазмещенияКоманд = Элементы.КомандыEDI;
	ПараметрыПриСозданииEDI.ДекорацияСостояниеEDI = Элементы.ДекорацияСостояниеEDI;
	ПараметрыПриСозданииEDI.ГруппаСостояниеEDI    = Элементы.ГруппаСостояниеEDI;
	ТК_ОбменEDI.ПриСозданииНаСервере_ФормаДокумента(ПараметрыПриСозданииEDI);
	///EDI
	
	Контрагент = Объект.Контрагент;
	Договор    = Объект.ДоговорВзаиморасчетов;
	
	Элементы.ХозОперация.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ДокументОснование) Или ЗначениеЗаполнено(Объект.РаспределениеОтгрузки);
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("РеализацияТоваров"));
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриЧтенииСозданииНаСервере();
	
	УстановитьСостояниеДокумента();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ЭтотОбъект.Модифицированность Тогда
		СортироватьТЧ_НаСервере();
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура СортироватьТЧ_НаСервере()
	
	Если НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь() ,"ОтключитьАвтоСортировкуТчТовары", Ложь) Тогда 
		ОбработкаТабличнойЧастиСервер.СортироватьТЧ(Объект.Товары);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.РеализацияТоваровСписание")
		ИЛИ Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.УдержаниеЗаПовреждениеТовара") Тогда
		
		Если Объект.Товары.Итог("СуммаВсего") > 0 И Объект.РаспределениеСписания.Количество() > 0 Тогда 
			РаспределитьСуммуУдержания(ТекущийОбъект);
		ИначеЕсли Объект.РаспределениеСписания.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Таблица сотрудников не заполнена, проведение запрещено!'; uk = 'Таблиця співробітників не заповнена, проведення заборонено!'"), Объект.Ссылка, "РаспределениеСписания",, Отказ);
		КонецЕсли;		
	Иначе
		Объект.РаспределениеСписания.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	УстановитьСостояниеДокумента();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	//ПодразделениеКомпанииПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПодразделениеКомпанииПриИзмененииНаСервере()
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ПодразделенияКомпании.ИспользоватьПротоколыСогласованныхЦен КАК ИспользоватьПротоколыСогласованныхЦен
	|ИЗ
	|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	|ГДЕ
	|	ПодразделенияКомпании.Ссылка = &Ссылка
	|	И ПодразделенияКомпании.ИспользоватьПротоколыСогласованныхЦен = ИСТИНА");
	Запрос.УстановитьПараметр("Ссылка", Объект.ПодразделениеКомпании);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Объект.ИспользоватьЦеныИзПротоколов = Истина;
	Иначе 
		Объект.ИспользоватьЦеныИзПротоколов = Ложь;
	КонецЕсли;
	
	//ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, ЭтаФорма.Объект);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация,Объект.ПодразделениеКомпании);
		Объект.ДоговорВзаиморасчетов = СтруктураДанные.Договор;
		
		ДоговорПередИзменением = Договор;
		Договор = Объект.ДоговорВзаиморасчетов;
		
		КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные);
	Иначе
		Объект.ДоговорВзаиморасчетов = Договор;
	КонецЕсли;
	
	Если Строка(Объект.Контрагент.УникальныйИдентификатор()) = "9d8618af-e498-40d4-a832-18d6e7434381" Тогда	//	Списание компенсируемое сотрудником
		Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.РеализацияТоваровСписание");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры


&НаКлиенте
Процедура РегламентированныйУчетПриИзменении(Элемент)
	
	//
	СтруктураДействий = Новый Структура;	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);	
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ПриИзмененииДоговораНаСервере(СтруктураДействий);
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры


&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	ПриИзмененииСкладаСервер();
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииСкладаСервер()
	
	ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);
	
	Объект.Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, Объект.СкладКомпании);
	
КонецПроцедуры



&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
	
	// СтандартныеПодсистемы.Свойства
	ОбновитьЭлементыДополнительныхРеквизитов();
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПроведенПоКартеПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Объект.Организация,Объект.ТорговаяПлощадка,"Реализация"));
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);	
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказатели(ЭтаФорма);   
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБезНалоговПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьПроцентСкидки", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)	
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНалоговПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыТара

&НаКлиенте
Процедура ТараНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	//Новый документ, цены на тек дату
	Если  Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ДатаСрезаЦен = ТекущаяДата();
	Иначе
		ДатаСрезаЦен = Неопределено;
	КонецЕсли;		
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект, "Цены", ДатаСрезаЦен,, ПолучитьТипЦенСклада(Объект.СкладКомпании))));	
	СтруктураДействий.Вставить("ЦеныБезХарактеристики", Истина);
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТараКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
КонецПроцедуры

&НаКлиенте
Процедура ТараЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);			
	
КонецПроцедуры

&НаКлиенте
Процедура ТараСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура УстановитьЦенуПоТипу(Команда)
	Перем ТипЦен;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	
	ОповещениЕ = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект, Команда.Имя);
	ОткрытьФорму("Справочник.ТипыЦен.ФормаВыбора", ПараметрыФормы,ТипЦен,,,,ОповещениЕ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСкидкуНаЦену(Команда)
	ОповещениЕ = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект, Команда.Имя);
	ПоказатьВводЧисла(ОповещениЕ,0,"Скидка, %");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦену(Кнопка,Значение)
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Кнопка = "УстановитьЦенуПоТипу" Тогда
		СтруктураДействий = Новый Структура;
		
		
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,"Цены",,,Значение));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
		
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	ИначеЕсли Кнопка = "УстановитьСкидкуНаЦену" Тогда
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
		
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			СтрокаТаблицы.Цена = СтрокаТаблицы.Цена * (100 - Значение) /100;
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеТТН(Команда)
	ЗаполнитьДанныеТТННаСервере();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТТННаСервере()
	
	Документы.РеализацияТоваров.ЗаполнитьРеквизитыТТНПоУмолчанию(Объект);
	
	ЭтаФорма.Прочитать();
	
КонецПроцедуры

#КонецОбласти


#Область EDI

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуEDI(Команда)
	
	ТК_ОбменEDIКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));
	
	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
	"ВалютаДокумента",
	Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
	"ВалютаРасчетовКурсКратность",
	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Если ДоговорПередИзменением <> Объект.ДоговорВзаиморасчетов Тогда
		
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением);
		
	Иначе
		
		Объект.ДоговорВзаиморасчетов = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()
	
	
	
КонецПроцедуры // ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()

&НаКлиенте
Процедура ОбновитьПодвалФормы()
	
	СуммаВсегоСНДС = Объект.Товары.Итог("СуммаВсего");
	СуммаНДС       = Объект.Товары.Итог("СуммаНДС");
	
КонецПроцедуры // ОбновитьПодвалФормы()

&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	
	СтруктураДействий = Новый Структура;	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ПриИзмененииДоговораНаСервере(СтруктураДействий);
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДоговораНаСервере(СтруктураДействий)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
	
КонецПроцедуры       

&НаКлиенте
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий,ТипЦен = Неопределено)
	
	Если ТипЗнч(СтруктураДействий) <> Тип("Структура") Тогда
		СтруктураДействий = Новый Структура;
	КонецЕсли;
	
	//Новый документ, цены на тек дату
	Если  Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ДатаСрезаЦен = ТекущаяДата();
	Иначе
		ДатаСрезаЦен = Неопределено;
	КонецЕсли;
	
	
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект,,ДатаСрезаЦен));
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,"Цены",ДатаСрезаЦен,,ТипЦен));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбора(ТипЦен,Кнопка)		Экспорт
	УстановитьЦену(Кнопка,ТипЦен);
КонецПроцедуры


#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("СтраницаРаспределениеМаркетинга");
	МассивВсехРеквизитов.Добавить("Товары.ТипБонуса");
	МассивВсехРеквизитов.Добавить("УстановкаМаркетинга");
	МассивВсехРеквизитов.Добавить("РасчетБонусов");
	МассивВсехРеквизитов.Добавить("ГруппаОснование");
	МассивВсехРеквизитов.Добавить("Карта");
	МассивВсехРеквизитов.Добавить("СуммаНДС");
	МассивВсехРеквизитов.Добавить("ТоварыЦенаБезНалогов");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаБезНалогов");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаНДС");
	МассивВсехРеквизитов.Добавить("Товары.Содержание");
	МассивВсехРеквизитов.Добавить("ГруппаУстановкиМаркетинга");
	МассивВсехРеквизитов.Добавить("СтраницаТара");
	МассивВсехРеквизитов.Добавить("Распределенные");
	МассивВсехРеквизитов.Добавить("ГруппаСписание");

	МассивРеквизитовОперации = Новый Массив;
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.АктОбОказанииУслуг") Тогда 
		МассивРеквизитовОперации.Добавить("СтраницаРаспределениеМаркетинга");
		МассивРеквизитовОперации.Добавить("УстановкаМаркетинга");
		МассивРеквизитовОперации.Добавить("РасчетБонусов");
		МассивРеквизитовОперации.Добавить("ГруппаУстановкиМаркетинга");
		МассивРеквизитовОперации.Добавить("Товары.ТипБонуса");
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
	Иначе
		МассивРеквизитовОперации.Добавить("СтраницаТара");
		МассивРеквизитовОперации.Добавить("Распределенные");
		МассивРеквизитовОперации.Добавить("ГруппаСписание");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
		МассивРеквизитовОперации.Добавить("ГруппаОснование");
	КонецЕсли;
	
	Если Объект.ПроведенПоКарте Тогда 
		МассивРеквизитовОперации.Добавить("Карта");
	КонецЕсли;
	
	Если Объект.РегламентированныйУчет Тогда 
		МассивРеквизитовОперации.Добавить("СуммаНДС");
		МассивРеквизитовОперации.Добавить("ТоварыЦенаБезНалогов");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаБезНалогов");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивРеквизитовОперации);
	
КонецПроцедуры


#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
	Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	
	
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
		ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		
		ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);
		
	КонецЕсли;	
	
	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	// Расчет итога по табличной части "Товары"
	КоллекцияТовары 					= Форма.Объект.Товары;
	Форма.СуммаВсегоСНДС 				= КоллекцияТовары.Итог("СуммаВсего");
	Форма.СуммаНДС 						= КоллекцияТовары.Итог("СуммаНДС");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТипЦенСклада(Знач Склад)
	
	Возврат Справочники.СкладыКомпании.ТипЦенРозничный(Склад);
	
КонецФункции



#КонецОбласти


&НаКлиенте
Процедура РаспределениеСписанияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	РаспределитьСуммуУдержания(Объект)
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РаспределитьСуммуУдержания(тОбъект)
	
	ИтогУдержания = тОбъект.Товары.Итог("СуммаВсего");
	ВсегоСтрок = тОбъект.РаспределениеСписания.Количество();
	Если ВсегоСтрок > 0 Тогда
		Удержать = ИтогУдержания/ВсегоСтрок;
	Иначе
		Удержать = 0;
	КонецЕсли;
	
	Для Каждого Стр из тОбъект.РаспределениеСписания Цикл 
		Стр.СуммаВсего = Удержать;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура("СкладКомпании,НайтиПоТочномуСоответствию",Объект.СкладКомпании,Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора",ПараметрыФормы,Элементы.Товары,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбработатьВыборНаСервере(ВыбранноеЗначение);         
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища) Экспорт
	
	
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Товары.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Объект.Организация,Объект.ТорговаяПлощадка,"Реализация"));
		
		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект));
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		
		СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);
		
		
	КонецЦикла;
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры



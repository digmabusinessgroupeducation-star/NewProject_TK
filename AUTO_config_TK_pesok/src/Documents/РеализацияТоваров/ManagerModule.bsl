#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений	
	ЭтоАктОбОказанииУслу = ДополнительныеСвойства.ДляПроведения.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.АктОбОказанииУслуг");
	Запрос = Новый Запрос;
	
	//МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	//Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);	

	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		
		//ПартионныйУчет.ТекстЗапросаВтТекущиеДвиженияПартииТоваров(ТекстыЗапроса);

		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваров(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") Тогда
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуРасчетыСПокупателями(Запрос, ДополнительныеСвойства);
		
	Иначе
		
		Если Не ЭтоАктОбОказанииУслу Тогда 
			
			ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
			ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры);
			ТекстЗапросаВтТаблицаЗаказы(Запрос, ТекстыЗапроса);
			
		Иначе
			ТекстЗапросаТаблицаПродажиАктОбОказанииУслуг(Запрос, ТекстыЗапроса, Регистры);	
		КонецЕсли;
		
		//ТекстЗапросаТаблицаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);
		//ТекстЗапросаТаблицаРасчетыСПокупателямиПоДокументам(Запрос, ТекстыЗапроса, Регистры);			
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуРасчетыСПокупателями(Запрос, ДополнительныеСвойства);
		
		Если Не ЭтоАктОбОказанииУслу Тогда 		
			
			ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваров(Запрос, ДополнительныеСвойства);	
			ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);	
			ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
			
			ЗаказыСервер.ПодготовитьТаблицуЗакрытияЗаказовПокупателя(Запрос, ДополнительныеСвойства);
			ЗаказыСервер.ПодготовитьТаблицуЗакрытияРезервовПокупателя(Запрос, ДополнительныеСвойства);
			
		КонецЕсли;		
		
	КонецЕсли;
	
	
	
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;// Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваров.Дата КАК Период,
	|	РеализацияТоваров.Ссылка КАК Ссылка,
	|	РеализацияТоваров.ХозОперация КАК ХозОперация,
	|	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	РеализацияТоваров.Организация КАК Организация,
	|	РеализацияТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	РеализацияТоваров.СкладКомпании КАК СкладКомпании,
	|	РеализацияТоваров.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	|	РеализацияТоваров.Контрагент КАК Контрагент,
	|	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваров.ТипЦен КАК ТипЦен,
	|	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	|	РеализацияТоваров.КурсДокумента КАК КурсДокумента,
	|	РеализацияТоваров.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТипЦен",Реквизиты.ТипЦен);
	Запрос.УстановитьПараметр("ТочкаДоставки",Реквизиты.ТочкаДоставки);
	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
	
	ПроводитьПоПартиям = Ложь;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") ИЛИ ПолучитьФункциональнуюОпцию("ФормироватьДвиженияПоПартиямПриПроведении") Тогда
		ПроводитьПоПартиям = Истина;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПроводитьПоПартиям",ПроводитьПоПартиям);
	
	//
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании",Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("Валюта",Реквизиты.ВалютаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента",Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов", Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент",Реквизиты.Контрагент);
	
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	РеализацияТоваровТовары.Ссылка.Организация КАК Организация,
	               |	РеализацияТоваровТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РеализацияТоваровТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РеализацияТоваровТовары.Ссылка.СкладКомпании КАК СкладКомпании,
	               |	РеализацияТоваровТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РеализацияТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	               |	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	               |	РеализацияТоваровТовары.КоличествоБазовое КАК Количество,
	               |	РеализацияТоваровТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	РеализацияТоваровТовары.Цена КАК Цена,
	               |	РеализацияТоваровТовары.Сумма КАК Сумма,
	               |	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
	               |	ВЫБОР
	               |		КОГДА &РегламентированныйУчет
	               |			ТОГДА РеализацияТоваровТовары.СуммаНДС
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНДС,
	               |	ВЫБОР
	               |		КОГДА &РегламентированныйУчет
	               |			ТОГДА РеализацияТоваровТовары.СуммаБезНалогов
	               |		ИНАЧЕ РеализацияТоваровТовары.Сумма
	               |	КОНЕЦ КАК СуммаБезНалогов,
	               |	РеализацияТоваровТовары.Ссылка.Контрагент КАК Контрагент,
	               |	РеализацияТоваровТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РеализацияТоваровТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	РеализацияТоваровТовары.Ссылка.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваровТовары.Ссылка.ДокументОснование КАК ДокументОснование,
	               |	РеализацияТоваровТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровТовары.СуммаСкидки КАК СуммаСкидки,
	               |	0 КАК СуммаАкцизногоНалога,
	               |	ЛОЖЬ КАК Возврат,
	               |	ЗНАЧЕНИЕ(справочник.КлассификаторПродаж.пустаяСсылка) КАК КлассификаторПродажи,
	               |	0 КАК Порядок
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	
	ИмяРегистра = "ВтТаблицаТара";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РеализацияТоваровТара.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровТара.Количество КАК Количество,
	               |	РеализацияТоваровТара.Цена КАК Цена,
	               |	РеализацияТоваровТара.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВтТаблицаТара
	               |ИЗ
	               |	Документ.РеализацияТоваров.Тара КАК РеализацияТоваровТара
	               |ГДЕ
	               |	РеализацияТоваровТара.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
					|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
					|	&Период КАК Период,
					|	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
					|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
					|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
					|	ВтТаблицаТовары.Контрагент КАК Контрагент,
					|	ВтТаблицаТовары.Количество КАК Количество,
					|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
					|ИЗ
					|	ВтТаблицаТовары КАК ВтТаблицаТовары
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
					|	&Период,
					|	&СкладКомпании,
					|	ВтТаблицаТара.Номенклатура,
					|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
					|	&Контрагент,
					|	ВтТаблицаТара.Количество,
					|	ЗНАЧЕНИЕ(Справочник.ХозОперации.ВзаиморасчетыПоТаре)
					|ИЗ
					|	ВтТаблицаТара КАК ВтТаблицаТара"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВозвратнаяТара";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТара", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	              |	&Организация КАК Организация,
	              |	&Контрагент КАК Контрагент,
	              |	ВтТаблицаТара.Номенклатура КАК Номенклатура,
	              |	СУММА(ВтТаблицаТара.Количество) КАК Количество,
	              |	СУММА(ВтТаблицаТара.Сумма) КАК Сумма
	              |ИЗ
	              |	ВтТаблицаТара КАК ВтТаблицаТара
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТара.Номенклатура
	              |
	              |ИМЕЮЩИЕ
	              |	СУММА(ВтТаблицаТара.Количество) <> 0"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаЗаказы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаЗаказы";
	ТекстЗапроса = "ВЫБРАТЬ
	|	РеализацияТоваров.ДокументОснование КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВтТаблицаЗаказы
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка = &Ссылка
	|	И РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя
	|ИЗ
	|	Документ.РеализацияТоваров.РаспределениеОтгрузки КАК РеализацияТоваровРаспределениеОтгрузки
	|ГДЕ
	|	РеализацияТоваровРаспределениеОтгрузки.Ссылка = &Ссылка
	|	И РеализацияТоваровРаспределениеОтгрузки.ЗаказПокупателя ССЫЛКА Документ.ЗаказПокупателя";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции



Функция ТекстЗапросаТаблицаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПокупателями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности) КАК ВидОперации,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	|	ВтТаблицаТовары.Контрагент КАК Контрагент,
	|	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
	|	СУММА(ВтТаблицаТовары.СуммаВсего * ВтТаблицаТовары.КурсДокумента) КАК СуммаБаз
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.Ссылка,
	|	ВтТаблицаТовары.ХозОперация,
	|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
	|	ВтТаблицаТовары.Контрагент,
	|	ВтТаблицаТовары.РегламентированныйУчет"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПокупателямиПоДокументам(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПокупателямиПоДокументам";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	ВтТаблицаТовары.Ссылка КАК Сделка,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности) КАК ВидОперации,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	|	ВтТаблицаТовары.Контрагент КАК Контрагент,
	|	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
	|	СУММА(ВтТаблицаТовары.СуммаВсего * ВтТаблицаТовары.КурсДокумента) КАК СуммаБаз
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.ХозОперация,
	|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
	|	ВтТаблицаТовары.Контрагент,
	|	ВтТаблицаТовары.РегламентированныйУчет,
	|	ВтТаблицаТовары.Ссылка"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаПродажиАктОбОказанииУслуг(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Продажи";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	РаспределениеМаркетинга.Дата КАК Период,
	|	РаспределениеМаркетинга.ТорговаяПлощадка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	РаспределениеМаркетинга.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	&СкладКомпании КАК СкладКомпании,
	|	РаспределениеМаркетинга.Номенклатура КАК Номенклатура,
	|	РаспределениеМаркетинга.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	&ХозОперация КАК ХозОперация,
	|	&Контрагент КАК Покупатель,
	|	РаспределениеМаркетинга.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	РаспределениеМаркетинга.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	|	РаспределениеМаркетинга.СуммаВсего КАК Сумма,
	|	РаспределениеМаркетинга.СуммаНДС КАК СуммаНДС,
	|	РаспределениеМаркетинга.СуммаВсего * РаспределениеМаркетинга.Ссылка.КурсДокумента КАК СуммаУпр
	|ИЗ
	|	Документ.РеализацияТоваров.РаспределениеМаркетинга КАК РаспределениеМаркетинга
	|ГДЕ
	|	РаспределениеМаркетинга.Ссылка = &Ссылка"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ПродажиТоваровКомпании");	
	
	Возврат ТекстЗапроса;	
КонецФункции


#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
	
	Если ДополнительныеСвойства.ДляПроведения.ХозОперация <> ПредопределенноеЗначение("Справочник.ХозОперации.АктОбОказанииУслуг") Тогда 
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор   = Ссылка;
		НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
		НоваяСтрока.Период   	  = Ссылка.Дата;
		
		ТаблицыДляПоследовательности.Вставить("ПартииТоваров", ТаблицаПоследовательности);
	КонецЕсли;
	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.ДоговорКонтрагента = Ссылка.ДоговорВзаиморасчетов;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.Вставить("Взаиморасчеты", ТаблицаПоследовательности);
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности", ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Расходная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 				
	
	// Расходная накладная 2 шт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяДвойная";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная 2 шт. на лист'; uk = 'Видаткова накладна 2 шт. на лист'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Расходная накладная (Без шапки)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяБезШапки";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная (Без шапки)'; uk = 'Видаткова накладна (Без шапки)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Расходная накладная (с кодом УКТВЭД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяУКТВЭД";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная (с кодом УКТВЭД)'; uk = 'Видаткова накладна (з кодом УКТЗЕД)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Расходная накладная (с кодом УКТВЭД) 2 шт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяДвойнаяУКТВЭД";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная 2 шт.(с УКТВЭД)'; uk = 'Видаткова накладна 2 шт. (з УКТЗЕД)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Счет на оплату
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "СчетНаОплату";
	КомандаПечати.Представление = НСтр("ru = 'Счет на оплату'; uk = 'Рахунок на оплату'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Бланк расходной накладной
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "БланкРасходнойНакладной";
	КомандаПечати.Представление = НСтр("ru = 'Бланк расходной накладной (Киев)'; uk = 'Бланк видаткової накладної (Київ)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Расходная накладная (Без шапки)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяБезШапкиОтгрузка";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная для отгрузки (Без шапки)'; uk = 'Видаткова накладна для відвантаження (Без шапки)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 			
	
	// Расходная накладная ФС
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяФС";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная ФС';uk='Видаткова накладна ФС'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 				

	// ТТН Казначейство
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТК_ТТН";
	КомандаПечати.Идентификатор = "ТТН_Казн";
	КомандаПечати.Представление = НСтр("ru = 'Товаро-транспортная накладная (Казн)'; uk = 'Товаро-транспортна накладна (Казн)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	КомандаПечати.УсловияВидимости.Добавить(Новый Структура("Реквизит,ВидСравнения,Значение","РегламентированныйУчет",ВидСравненияКомпоновкиДанных.Равно,Истина)); 		
	
	// Бланк расходной накладной
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "АктОбОказанииУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказнии услуг'; uk = 'Акт про надання послуг'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	КомандаПечати.УсловияВидимости.Добавить(Новый Структура("Реквизит,ВидСравнения,Значение","ХозОперация",ВидСравненияКомпоновкиДанных.Равно,Справочники.ХозОперации.АктОбОказанииУслуг)); 		
	
	// ТТН ФС
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "ТТН_ФС";
	КомандаПечати.Представление = НСтр("ru = 'Товаро-транспортная накладная (Фабрика сыра)'; uk = 'Товаро-транспортна накладна (Фабрика сиру)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	КомандаПечати.УсловияВидимости.Добавить(Новый Структура("Реквизит,ВидСравнения,Значение","РегламентированныйУчет",ВидСравненияКомпоновкиДанных.Равно,Истина)); 		

	// Декларация производителя
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.РеализацияТоваров";
	КомандаПечати.Идентификатор = "ДекларацияПроизводителя";
	КомандаПечати.Представление = НСтр("ru = 'Декларация производителя'; uk = 'Декларація виробника'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				

КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка, ПустаяСсылка КАК Истина)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Процедура ЗаполнитьРеквизитыТТНПоУмолчанию(Объект) Экспорт 
		
	РеквизитыТТН = Новый Структура;
	
	НаборСвойств = УправлениеСвойствами.НаборСвойствПоИмени("Документ_РеализацияТоваров_ТТН");
	Для Каждого Стр Из НаборСвойств.ДополнительныеРеквизиты Цикл 
		Если Не Стр.ПометкаУдаления Тогда 
			РеквизитыТТН.Вставить(Стр.Свойство.Имя, Стр.Свойство);
		КонецЕсли;
	КонецЦикла;
	
	ТекДопРеквизиты = Объект.ДополнительныеРеквизиты.Выгрузить();
	
	ТаблицаДопРеквизитов = ТекДопРеквизиты.Скопировать();
	ТаблицаДопРеквизитов.Очистить();
	
	Для Каждого Стр Из ТекДопРеквизиты Цикл 
		Если Не РеквизитыТТН.Свойство(Стр.Свойство.Имя) Тогда 
			НоваяСтрока = ТаблицаДопРеквизитов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЕсли;
	КонецЦикла;

	Если Объект.ХозОперация = Справочники.ХозОперации.РеализацияТоваров Тогда 				
	
		МассивИменСвойствДоговора = Новый Массив;
		МассивИменСвойствДоговора.Добавить("ТТН_Автомобиль");
		МассивИменСвойствДоговора.Добавить("ТТН_Заказчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Перевозчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Водитель");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	РеализацияТоваров.СкладКомпании.ТочкаДоставки КАК ПунктПогрузки,
		               |	РеализацияТоваров.ТочкаДоставки КАК ПунктРазгрузки
		               |ИЗ
		               |	Документ.РеализацияТоваров КАК РеализацияТоваров
		               |ГДЕ
		               |	РеализацияТоваров.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДополнительныеСведения.Свойство.Имя КАК СвойствоИмя,
		               |	ДополнительныеСведения.Значение КАК Значение
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Объект = &Договор
		               |	И ДополнительныеСведения.Свойство.Имя В(&МассивСвойств)";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		Запрос.УстановитьПараметр("Договор", Объект.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("МассивСвойств", МассивИменСвойствДоговора);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ВыборкаРеквизиты  = МассивРезультатов[0].Выбрать();
		ВыборкаПоДоговору = МассивРезультатов[1].Выбрать();
		
		Если ВыборкаРеквизиты.Следующий() Тогда 
			ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_ПунктОтправитель", ВыборкаРеквизиты.ПунктПогрузки, РеквизитыТТН);			
			ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_ПунктПолучатель", ВыборкаРеквизиты.ПунктРазгрузки, РеквизитыТТН);
		КонецЕсли;
		
		Пока ВыборкаПоДоговору.Следующий() Цикл 
			Если ВыборкаПоДоговору.СвойствоИмя = "ТТН_Автомобиль" Тогда 
				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_Автомобиль", ВыборкаПоДоговору.Значение, РеквизитыТТН);				
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Водитель" Тогда 
				
				Водитель = ВыборкаПоДоговору.Значение;				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_Водитель", Водитель, РеквизитыТТН);				
				
				НомерПрав = "";
				Если ТипЗнч(Водитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
					НомерПрав = Справочники.ФизическиеЛица.ПолучитьНомерВодительскогоУдостоверения(Водитель, Объект.Дата);
				КонецЕсли;
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_НомерПрав", НомерПрав, РеквизитыТТН);				
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Перевозчик" Тогда 				
				
				Перевозчик = ВыборкаПоДоговору.Значение;
				ТипРеквизитаПеревозчик = ТипЗнч(Перевозчик);
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_Перевозчик", Перевозчик, РеквизитыТТН);
				
				ИННПеревозчика = "";
				Если ТипРеквизитаПеревозчик <> Тип("Строка") Тогда 
					Если ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Организации") Тогда 
						Запрос = Новый Запрос;
						Запрос.Текст = "ВЫБРАТЬ
						|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация,
						|	УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС КАК ИННПлательщикаНДС
						|ИЗ
						|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Дата, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
						Запрос.УстановитьПараметр("Дата", Объект.Дата);
						Запрос.УстановитьПараметр("Организация", Перевозчик);
						
						ВыборкаИНН = Запрос.Выполнить().Выбрать();        
						Если ВыборкаИНН.Следующий() Тогда 
							ИННПеревозчика = ВыборкаИНН.ИННПлательщикаНДС;	
						КонецЕсли;
					ИначеЕсли ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Контрагенты") Тогда 
						ИННПеревозчика = Перевозчик.ИННПлательщикаНДС;
					КонецЕсли;
				КонецЕсли;		
				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "РТ_ИННПеревозчика", ИННПеревозчика, РеквизитыТТН);
			
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;

	УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Объект.Ссылка, ТаблицаДопРеквизитов);
	
КонецПроцедуры

Процедура ДобавитьДопРеквизит(ТаблицаДопРеквизитов, ИмяСвойства, Значение, СтруктураДоступныхСвойств)	
	
	Если Не ЗначениеЗаполнено(Значение) ИЛИ Не ЗначениеЗаполнено(ИмяСвойства) Тогда 
		Возврат;
	КонецЕсли;
	
	Свойство = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоступныхСвойств, ИмяСвойства, Неопределено);
	
	Если Не ЗначениеЗаполнено(Свойство) Тогда 
		Возврат;
	КонецЕсли;
	
	мПроверка = ТаблицаДопРеквизитов.НайтиСтроки(Новый Структура("Свойство", Свойство));
	Если мПроверка.Количество() > 0 Тогда 
		Для Каждого Стр Из мПроверка Цикл 
			ТаблицаДопРеквизитов.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаДопРеквизитов.Добавить();
	НоваяСтрока.Свойство = Свойство;
	НоваяСтрока.Значение = Значение;
	
КонецПроцедуры

#КонецОбласти	


#Область ВыгрузкаEDI

Функция СформироватьСтруктуру_DESADV(ДокументСсылка)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваров.Номер КАК NUMBER,
	|	РеализацияТоваров.Дата КАК DATE,
	|	РеализацияТоваров.Организация.GLN КАК SUPPLIER,
	|	РеализацияТоваров.Контрагент.GLN КАК RECIPIENT,
	|	РеализацияТоваров.Контрагент.GLN КАК BUYER,
	|	РеализацияТоваров.ТочкаДоставки.GLN КАК DELIVERYPLACE,
	|	РеализацияТоваров.Контрагент.КодПоЕДРПОУ КАК КонтрагентОКПО,
	|	РеализацияТоваров.Контрагент.Код КАК CAMPAIGNNUMBER,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).ИДДокументаОтгрузки
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERNUMBER,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).ДатаСоздания
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERDATE,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерЗаказа,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).Дата
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаЗаказа
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровТовары.НомерСтроки КАК POSITIONNUMBER,
	|	РеализацияТоваровТовары.Номенклатура.Артикул КАК PRODUCTIDSUPPLIER,
	|	РеализацияТоваровТовары.Номенклатура.Наименование КАК DESCRIPTION,
	|	РеализацияТоваровТовары.Номенклатура.ОсновнойШтрихКод КАК PRODUCT,
	|	РеализацияТоваровТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.ORDERUNIT КАК ORDERUNIT,
	|	1 КАК QUANTITYOFCUINTU,
	|	РеализацияТоваровТовары.Количество * РеализацияТоваровТовары.Коэффициент КАК DELIVEREDQUANTITY,
	|	РеализацияТоваровТовары.Цена КАК PRICEWITHVAT,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровТовары.Номенклатура.ТипНоменклатуры.Весовой
	|				ИЛИ РеализацияТоваровТовары.Номенклатура.ТипНоменклатуры.ШтучноВесовой
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Весовой,
	|	РеализацияТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РеализацияТоваровТовары.ХарактеристикаНоменклатуры.ЗначениеМРЦ КАК МРЦ
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	|ГДЕ
	|	РеализацияТоваровТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровТовары.НомерСтроки";
	
	
	МассивРезультат = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультат[0].Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	
	СтруктураДокумента = Новый Структура;
	СтруктураДокумента.Вставить("ДокументКорректен",1);
	
	
	//DESADV
	СтруктураДокумента.Вставить("Дата",ВыборкаДетальныеЗаписи.DATE);
	СтруктураДокумента.Вставить("NUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	СтруктураДокумента.Вставить("DATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	СтруктураДокумента.Вставить("DELIVERYDATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERNUMBER) Тогда
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.ORDERNUMBER));
	Иначе
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.НомерЗаказа));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERDATE) Тогда
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ORDERDATE,"ДФ=""гггг-ММ-дд"""));
	Иначе
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ДатаЗаказа,"ДФ=""гггг-ММ-дд"""));
	КонецЕсли;
	
	СтруктураДокумента.Вставить("DELIVERYNOTENUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	СтруктураДокумента.Вставить("DELIVERYNOTEDATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	
	//HEAD
	СтруктураДокумента.Вставить("SUPPLIER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("BUYER",СокрЛП(ВыборкаДетальныеЗаписи.BUYER));
	СтруктураДокумента.Вставить("DELIVERYPLACE",СокрЛП(ВыборкаДетальныеЗаписи.DELIVERYPLACE));
	СтруктураДокумента.Вставить("SENDER","9864232378839");
	
	//СтруктураДокумента.Вставить("SENDER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("RECIPIENT",СокрЛП(ВыборкаДетальныеЗаписи.RECIPIENT));
	
	
	СтруктураДокумента.Вставить("HIERARCHICALID", 1);
	
	//POSITION
	POSITION=Новый ТаблицаЗначений;
	POSITION.Колонки.Добавить("POSITIONNUMBER");
	POSITION.Колонки.Добавить("PRODUCT");
	POSITION.Колонки.Добавить("DELIVEREDQUANTITY");
	
	POSITION.Колонки.Добавить("PRODUCTIDSUPPLIER");
	//	POSITION.Колонки.Добавить("QUANTITYOFCUINTU");
	POSITION.Колонки.Добавить("ORDERUNIT");
	POSITION.Колонки.Добавить("DESCRIPTION");
	POSITION.Колонки.Добавить("PRICEWITHVAT");
	POSITION.Колонки.Добавить("ADVICEPRICE");
	
	ВыборкаДетальныеЗаписи = МассивРезультат[1].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НоваяСтрока =  POSITION.Добавить();
		НоваяСтрока.POSITIONNUMBER	  = Формат(ВыборкаДетальныеЗаписи.POSITIONNUMBER,"ЧЦ=15; ЧГ=");
		НоваяСтрока.PRODUCT        	  = СокрЛП(ВыборкаДетальныеЗаписи.PRODUCT);
		НоваяСтрока.DELIVEREDQUANTITY = Формат(ВыборкаДетальныеЗаписи.DELIVEREDQUANTITY,"ЧРД=.; ЧГ=");
		НоваяСтрока.PRODUCTIDSUPPLIER = СокрЛП(ВыборкаДетальныеЗаписи.PRODUCTIDSUPPLIER);
		НоваяСтрока.ORDERUNIT         = СокрЛП(ВыборкаДетальныеЗаписи.ORDERUNIT);
		НоваяСтрока.DESCRIPTION       = СокрЛП(Лев(ВыборкаДетальныеЗаписи.DESCRIPTION,70));
		НоваяСтрока.PRICEWITHVAT 	  = Формат(ВыборкаДетальныеЗаписи.PRICEWITHVAT, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0" );
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры) Тогда
			НоваяСтрока.ADVICEPRICE 	  = Формат(ВыборкаДетальныеЗаписи.МРЦ, "ЧЦ=15; ЧДЦ=3; ЧРД=.; ЧН=; ЧГ=0" );
		КонецЕсли;
		
	КонецЦикла;
	
	
	
	СтруктураДокумента.Вставить("POSITION",POSITION);	
	
	//Если НЕ ЗначениеЗаполнено(СтруктураДокумента.ORDERNUMBER) Тогда
	//	ДобавитьОшибкуВСтруктуруДокумента("Не указан номер заказа",СтруктураДокумента);
	//КонецЕсли;
	//Если НЕ ЗначениеЗаполнено(СтруктураДокумента.ORDERDATE) Тогда
	//	ДобавитьОшибкуВСтруктуруДокумента("Не указан номер заказа",СтруктураДокумента);
	//КонецЕсли;
	
	
	Возврат СтруктураДокумента;
	
	
	
КонецФункции

#КонецОбласти	

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если ТипЗнч(ПараметрыПечати) <> Тип("Структура") Тогда 
		ПараметрыПечати = Новый Структура;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладная") Тогда   // Расходная накладная
		
		ПараметрыПечати.Вставить("QR_Заголовок", Ложь);
		ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
		ПараметрыПечати.Вставить("ВыводитьТМ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ5", Ложь);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Ложь);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладная",
		НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'"), 
		ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяБезШапки") Тогда   // Расходная накладная (Без шапки)
		
		ПараметрыПечати.Вставить("QR_Заголовок", Ложь);
		ПараметрыПечати.Вставить("ШапкаКратко", Истина);
		ПараметрыПечати.Вставить("ВыводитьТМ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Истина);
		ПараметрыПечати.Вставить("ВыводитьМРЦ5", Ложь);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Ложь);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяБезШапки",
		НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'"), 
		ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяФС") Тогда   // Расходная накладная ФС
		
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Ложь);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяФС",
		НСтр("ru = 'Расходная накладная ФС';uk='Видаткова накладна ФС'"), 
		ПечатьРасходнаяНакладнаяФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.ВыпускПродукции.ПФ_MXL_РасходнаяНакладная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяБезШапкиОтгрузка") Тогда   // Расходная накладная для отгрузки (Без шапки)
		
		ПараметрыПечати.Вставить("QR_Заголовок", Ложь);
		ПараметрыПечати.Вставить("ШапкаКратко", Истина);
		ПараметрыПечати.Вставить("ВыводитьТМ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Истина);
		ПараметрыПечати.Вставить("ВыводитьМРЦ5", Истина);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяБезШапкиОтгрузка",
		НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'"), 
		ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");			
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяУКТВЭД") Тогда   // Расходная накладная (УКТВЭД)
		
		ПараметрыПечати.Вставить("QR_Заголовок", Истина);
		ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
		ПараметрыПечати.Вставить("ВыводитьТМ", Истина);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Истина);
		ПараметрыПечати.Вставить("ВыводитьМРЦ5", Истина);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Истина);
		ПараметрыПечати.Вставить("ИтогиПоБазовомуКоличеству", Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяУКТВЭД",
		НСтр("ru = 'Расходная накладная (с кодом УКТВЭД)'; uk = 'Видаткова накладна (з кодом УКТЗЕД)'"), 
		ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяДвойнаяУКТВЭД") Тогда   // Расходная накладная 2 шт (УКТВЭД)
		
		ПараметрыПечати.Вставить("QR_Заголовок", Истина);
		ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
		ПараметрыПечати.Вставить("ВыводитьТМ", Истина);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ5", Истина);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Истина);
		ПараметрыПечати.Вставить("ИтогиПоБазовомуКоличеству", Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяДвойнаяУКТВЭД",
		НСтр("ru = 'Расходная накладная 2 шт.(с УКТВЭД)'; uk = 'Видаткова накладна 2 шт. (з УКТЗЕД)'"), 
		ПечатьРасходнаяНакладнаяДвойнаяУКТВЭД(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОплату") Тогда   // Счет на оплату
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СчетНаОплату",
		НСтр("ru = 'Счет на оплату'; uk = 'Рахунок на оплату'"), 
		ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_СчетНаОплату");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяДвойная") Тогда   // Расходная накладная 2 шт
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходнаяНакладнаяДвойная",
		НСтр("ru = 'Расходная накладная 2 шт. на лист'; uk = 'Видаткова накладна 2 шт. на лист'"),
		ПечатьРасходнаяНакладнаяДвойная(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_НакладнаяДвойная");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БланкРасходнойНакладной") Тогда // Бланк расходной накладной (Киев)
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"БланкРасходнойНакладной",
		НСтр("ru = 'Бланк расходной накладной (Киев)'; uk = 'Бланк видаткової накладної (Київ)'"),
		ПечатьБланкРасходнойНакладной(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_БланкРасходнойНакладной");		
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктОбОказанииУслуг") Тогда // Акт об оказании услуг
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"АктОбОказанииУслуг",
		НСтр("ru='Акт об оказании услуг';uk='Акт про надання послуг'"),
		ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.РеализацияТоваров.ПФ_MXL_Акт");		
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДекларацияПроизводителя") Тогда 			
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ДекларацияПроизводителя",
			НСтр("ru = 'Декларация производителя'; uk = 'Декларація виробника'"), 
			ПечатьДекларацияПроизводителя(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_ДекларацияПроизводителя");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН_ФС") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ТТН_ФС",
		НСтр("ru = 'Товаро-транспортная накладная (Фабрика сыра)'; uk = 'Товаро-транспортна накладна (Фабрика сиру)'"), 
		ПечатьТТН_ФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.ТК_ТТН.ПФ_MXL_ТТН_ФС");	
		
	КонецЕсли;	
	
КонецПроцедуры

// Расходная накладная
Функция ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)  Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Порядок 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
	QR_Заголовок	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "QR_Заголовок", Ложь);
	ШапкаКратко		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ШапкаКратко", Ложь);
	ВыводитьТМ 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьТМ", Ложь);
	ВыводитьМРЦ		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьМРЦ", Ложь);
	ВыводитьМРЦ5	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьМРЦ5", Ложь);
	ВыводитьУКТВЭД	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьУКТВЭД", Ложь);
	СсылкаТТН		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "СсылкаТТН", "");
	ИтогиПоБазовомуКоличеству = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ИтогиПоБазовомуКоличеству", Ложь);
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокСигаретный";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокПивной";
	ИначеЕсли Порядок = "ПакетДокументов" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	РеализацияТоваров.МоментВремени";
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТовары.Номенклатура.Артикул КАК Артикул,
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.Номенклатура) КАК ТоварПредставление,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатуры,
	               |	РеализацияТоваровТовары.Количество КАК Количество,
	               |	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	               |	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	РеализацияТоваровТовары.Цена КАК ЦенаПредставление,
	               |	РеализацияТоваровТовары.Сумма КАК Сумма,
	               |	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
	               |	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваровТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА РеализацияТоваровТовары.Номенклатура.КодУКТВЭД
	               |		КОГДА РеализацияТоваровТовары.ХарактеристикаНоменклатуры.УКТВЭД = ЗНАЧЕНИЕ(справочник.КлассификаторУКТВЭД.пустаяСсылка)
	               |			ТОГДА РеализацияТоваровТовары.Номенклатура.КодУКТВЭД
	               |		ИНАЧЕ РеализацияТоваровТовары.ХарактеристикаНоменклатуры.УКТВЭД
	               |	КОНЕЦ КАК КодУКТВЭД,
	               |	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	               |	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |	РеализацияТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	               |	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	               |	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |	ВЫБОР
	               |		КОГДА &ВыводитьТМ
	               |			ТОГДА РеализацияТоваровТовары.Номенклатура.ТоварнаяМарка.Родитель.НаименованиеПолное
	               |		ИНАЧЕ ВЫРАЗИТЬ("""" КАК СТРОКА(1))
	               |	КОНЕЦ КАК ПредставлениеТоварнаяМарка,
	               |	РеализацияТоваровТовары.Номенклатура.Наименование2 КАК ТоварПредставление1
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка В(&МассивСсылок)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	               |	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	               |	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод
	               |ПОМЕСТИТЬ втШтрихКода
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |ГДЕ
	               |	(ШтрихкодыНоменклатуры.Номенклатура, ШтрихкодыНоменклатуры.Упаковка) В
	               |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |				втТовары.Номенклатура КАК Номенклатура,
	               |				втТовары.ЕдиницаИзмерения КАК Упаковка
	               |			ИЗ
	               |				втТовары КАК втТовары)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ШтрихкодыНоменклатуры.Упаковка,
	               |	ШтрихкодыНоменклатуры.Номенклатура
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.НомерСтроки КАК НомерСтроки,
	               |	втТовары.Ссылка КАК Ссылка,
	               |	втТовары.Артикул КАК Артикул,
	               |	втТовары.Номенклатура КАК Номенклатура,
	               |	втТовары.ТоварПредставление КАК ТоварПредставление,
	               |	втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втТовары.Количество КАК Количество,
	               |	втТовары.Коэффициент КАК Коэффициент,
	               |	втТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	втТовары.ЦенаПредставление КАК ЦенаПредставление,
	               |	втТовары.Сумма КАК Сумма,
	               |	втТовары.СуммаВсего КАК СуммаВсего,
	               |	втТовары.СуммаНДС КАК СуммаНДС,
	               |	втТовары.КодУКТВЭД КАК КодУКТВЭД,
	               |	втТовары.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	               |	втТовары.ЕдиницаИзмеренияУпаковкиКоэффициент КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |	втТовары.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	               |	втТовары.ОсновнаяЕдиницаИзмеренияКоэффициент КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |	втТовары.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	               |	втТовары.ЕдиницаИзмеренияЯщиковКоэффициент КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |	втТовары.ПредставлениеТоварнаяМарка КАК ПредставлениеТоварнаяМарка,
	               |	ВЫБОР
	               |		КОГДА втТовары.Номенклатура.ОсновнойШтрихКод <> """"
	               |			ТОГДА втТовары.Номенклатура.ОсновнойШтрихКод
	               |		ИНАЧЕ ЕСТЬNULL(втШтрихКода.Штрихкод, """")
	               |	КОНЕЦ КАК Штрихкод,
	               |	втТовары.ТоварПредставление1 КАК ТоварПредставление1,
	               |	ВЫБОР
	               |		КОГДА втТовары.Номенклатура В ИЕРАРХИИ (&Табак)
	               |				И втТовары.Номенклатура.СтавкаНДС.Ставка = 0
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК АкцизСплачено
	               |ИЗ
	               |	втТовары КАК втТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втШтрихКода КАК втШтрихКода
	               |		ПО втТовары.Номенклатура = втШтрихКода.Номенклатура
	               |			И втТовары.ЕдиницаИзмерения = втШтрихКода.Упаковка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	ПредставлениеТоварнаяМарка,
	               |	НомерСтроки
	               |ИТОГИ
	               |	СУММА(Количество),
	               |	СУММА(КоличествоБазовое),
	               |	СУММА(Сумма),
	               |	СУММА(СуммаВсего),
	               |	СУММА(СуммаНДС),
	               |	МАКСИМУМ(АкцизСплачено)
	               |ПО
	               |	Ссылка,
	               |	ПредставлениеТоварнаяМарка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровТара.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТара.НомерСтроки КАК НомерСтроки,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТара.Номенклатура) КАК ТоварПредставление,
	               |	РеализацияТоваровТара.Количество КАК КоличествоПредставление,
	               |	РеализацияТоваровТара.Количество КАК КоличествоМестПредставление,
	               |	РеализацияТоваровТара.Цена КАК ЦенаПредставление,
	               |	РеализацияТоваровТара.Сумма КАК СуммаВсего
	               |ИЗ
	               |	Документ.РеализацияТоваров.Тара КАК РеализацияТоваровТара
	               |ГДЕ
	               |	РеализацияТоваровТара.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	НомерСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваров.Ссылка КАК Ссылка,
	               |	РеализацияТоваров.Организация КАК Поставщик,
	               |	РеализацияТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	               |	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РеализацияТоваров.СкладКомпании КАК Склад,
	               |	РеализацияТоваров.СкладКомпании.НаименованиеПредставление КАК ПредставлениеСкладаМестоСоставления,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ОснованиеПредставление,
	               |	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваров.ТочкаДоставки.Представление КАК АдресДоставки,
	               |	РеализацияТоваров.Дата КАК Дата,
	               |	РеализацияТоваров.Номер КАК Номер,
	               |	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.Порядок, 0) КАК ПорядокПивной,
	               |	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.ПорядокСигаретный, 0) КАК ПорядокСигаретный,
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	               |			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).Комментарий
	               |		ИНАЧЕ РеализацияТоваров.Комментарий
	               |	КОНЕЦ КАК Комментарий,
	               |	РеализацияТоваров.ДокументОснование КАК ДокументОснование,
	               |	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ТК_ТТНИсточники.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТК_ТТН.Источники КАК ТК_ТТНИсточники
	               |		ПО (РеализацияТоваров.Ссылка = ТК_ТТНИсточники.Документ
	               |				ИЛИ РеализацияТоваров.Ссылка = ТК_ТТНИсточники.Отгрузка)
	               |			И (ТК_ТТНИсточники.Ссылка = &СсылкаТТН)
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка В(&МассивСсылок)" + СортировкаЗапроса;
	
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	Запрос.УстановитьПараметр("СсылкаТТН", СсылкаТТН);	
	Запрос.УстановитьПараметр("ВыводитьТМ", ВыводитьТМ);
	Запрос.УстановитьПараметр("Табак", Справочники.Номенклатура.НайтиПоКоду("10070"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаТЧ_Товары  = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	ВыборкаДокументов = МассивРезультатов[4].Выбрать();
	тзТара 			  = МассивРезультатов[3].Выгрузить();
	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	Если QR_Заголовок Тогда 
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("ЗаголовокU_Track");				
	Иначе
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	КонецЕсли;
	
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьКомментарий		= Макет.ПолучитьОбласть("Комментарий");
	ОбластьМестоСоставления = Макет.ПолучитьОбласть("Склад");	
	ОбластьЗаказ			= Макет.ПолучитьОбласть("Заказ");
	
	ОбластьДопПодвал_СуммаМРЦ	= Макет.ПолучитьОбласть("ДопПодвалБезШапки|СуммаМРЦ");
	ОбластьДопПодвал_АкцизМРЦ	= Макет.ПолучитьОбласть("ДопПодвалБезШапки|АкцизМРЦ");
	ОбластьДопПодвал_СуммаМРЦПлюс= Макет.ПолучитьОбласть("ДопПодвалБезШапки|СуммаМРЦПлюс");
	
	ОбластьШапка_нпп 			= Макет.ПолучитьОбласть("ШапкаТаблицы|НомерПП");	
	ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|ДанныеТовара");		
	ОбластьШапка_ДанныеКол		= Макет.ПолучитьОбласть("ШапкаТаблицы|ДанныеКоличества");		
	ОбластьШапка_УКТВЭД			= Макет.ПолучитьОбласть("ШапкаТаблицы|КодУКТВЭД");		
	ОбластьШапка_Акциз			= Макет.ПолучитьОбласть("ШапкаТаблицы|ОплАкциза");		
	ОбластьШапка_МРЦ			= Макет.ПолучитьОбласть("ШапкаТаблицы|МРЦ");		
	ОбластьШапка_МРЦ1			= Макет.ПолучитьОбласть("ШапкаТаблицы|МРЦ1");		
	
	ОбластьСтрока_нпп 			= Макет.ПолучитьОбласть("Строка|НомерПП");	
	ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|ДанныеТовара");
	ОбластьСтрока_ДанныеКол		= Макет.ПолучитьОбласть("Строка|ДанныеКоличества");
	ОбластьСтрока_ДанныеРазб    = Макет.ПолучитьОбласть("СтрокаКол|ДанныеТовара");
	ОбластьСтрока_ДанныеРазбКол = Макет.ПолучитьОбласть("СтрокаКол|ДанныеКоличества");
	ОбластьСтрока_УКТВЭД		= Макет.ПолучитьОбласть("Строка|КодУКТВЭД");		
	ОбластьСтрока_Акциз			= Макет.ПолучитьОбласть("Строка|ОплАкциза");		
	ОбластьСтрока_МРЦ			= Макет.ПолучитьОбласть("Строка|МРЦ");		
	ОбластьСтрока_МРЦ1			= Макет.ПолучитьОбласть("Строка|МРЦ1");		
	
	ОбластьПодвал_нпп 			= Макет.ПолучитьОбласть("Подвал|НомерПП");	
	ОбластьПодвал_Данные		= Макет.ПолучитьОбласть("Подвал|ДанныеТовара");		
	ОбластьПодвал_ДанныеКол		= Макет.ПолучитьОбласть("Подвал|ДанныеКоличества");		
	ОбластьПодвал_УКТВЭД		= Макет.ПолучитьОбласть("Подвал|КодУКТВЭД");		
	ОбластьПодвал_Акциз			= Макет.ПолучитьОбласть("Подвал|ОплАкциза");		
	ОбластьПодвал_МРЦ			= Макет.ПолучитьОбласть("Подвал|МРЦ");		
	ОбластьПодвал_МРЦ1			= Макет.ПолучитьОбласть("Подвал|МРЦ1");		
	
	ОбластьГруппаТМ_нпп 		= Макет.ПолучитьОбласть("ГруппаТМ|НомерПП");	
	ОбластьГруппаТМ_Данные		= Макет.ПолучитьОбласть("ГруппаТМ|ДанныеТовара");		
	ОбластьГруппаТМ_УКТВЭД		= Макет.ПолучитьОбласть("ГруппаТМ|КодУКТВЭД");		
	ОбластьГруппаТМ_Акциз		= Макет.ПолучитьОбласть("ГруппаТМ|ОплАкциза");		
	ОбластьГруппаТМ_МРЦ			= Макет.ПолучитьОбласть("ГруппаТМ|МРЦ");		
	ОбластьГруппаТМ_МРЦ1		= Макет.ПолучитьОбласть("ГруппаТМ|МРЦ1");		
	
	ОбластьШапкаТара			= Макет.ПолучитьОбласть("ШапкаТара");
	ОбластьСтрокаТара			= Макет.ПолучитьОбласть("СтрокаТара");
	ОбластьПодвалТара			= Макет.ПолучитьОбласть("ПодвалТара");
	
	
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьСертификаты		= Макет.ПолучитьОбласть("Сертификаты");
	ОбластьПробел			= Макет.ПолучитьОбласть("Пробел");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		?(ВыборкаДокументов.РегламентированныйУчет, 
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
		Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("АдресДоставки", "");
		
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		Если QR_Заголовок Тогда 									
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор()),1,125);
				ОбластьЗаголовок.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;			
		КонецЕсли;		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);		
		
		Если ШапкаКратко Тогда 
			СтруктураШапки.Вставить("Комментарий", "");	
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьКомментарий.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьКомментарий);
		Иначе
			СтруктураШапки.Вставить("ПредставлениеПоставщика", "");
			СтруктураШапки.Вставить("Поставщик", "");			
			СтруктураШапки.Вставить("Основание", НСтр("ru = 'Основание'; uk = 'Підстава'", КодЯзыкаПечать));
			СтруктураШапки.Вставить("ОснованиеПредставлениеМесто", "");			
			СтруктураШапки.Вставить("ПредставлениеЗаказ", "");
			СтруктураШапки.Вставить("ПредставлениеРезерва", "");		
			СтруктураШапки.Вставить("ОснованиеМесто", "");			
			СтруктураШапки.Вставить("ОснованиеПредставление", "");
			
			СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.ПредставлениеСкладаМестоСоставления)), ВыборкаДокументов.ПредставлениеСкладаМестоСоставления, ВыборкаДокументов.Склад));
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			Если Порядок = "Пиво" Тогда 
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной/1000);
			Иначе
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный/100);
			КонецЕсли;
			
			СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
			
			СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Поставщик, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина,,,ТипЗнч(ВыборкаДокументов.Ссылка),ВыборкаДокументов.РегламентированныйУчет));
			СтруктураШапки.Вставить("РеквизитыПолучателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьМестоСоставления.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьМестоСоставления);
			
			ЗаказОснование = ВыборкаДокументов.ДокументОснование;			
			Если ЗначениеЗаполнено(ЗаказОснование) И ТипЗнч(ЗаказОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
				СтруктураШапки.Вставить("ПредставлениеЗаказ", Строка(ЗаказОснование));
				
				Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
				|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СвязанныеДокументы.Ссылка) КАК Резерв
				|ИЗ
				|	КритерийОтбора.СвязанныеДокументы(&Ссылка) КАК СвязанныеДокументы
				|ГДЕ
				|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.РезервированиеЗаказовПокупателя
				|	И ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РезервированиеЗаказовПокупателя).Проведен");
				Запрос.УстановитьПараметр("Ссылка", ЗаказОснование);
				ВыборкаРезерв = Запрос.Выполнить().Выбрать();
				Если ВыборкаРезерв.Следующий() Тогда 
					СтруктураШапки.Вставить("ПредставлениеРезерва", ВыборкаРезерв.Резерв);
				КонецЕсли;		
				
				ОбластьЗаказ.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаказ);
			КонецЕсли;
		КонецЕсли;								                              
		
		Если ВыводитьУКТВЭД Тогда 
			ВыборкаТЧ_Товары.Сбросить();
			Если ВыборкаТЧ_Товары.НайтиСледующий(ВыборкаДокументов.Ссылка, "Ссылка") Тогда 
				ВыводитьОплАкциза = ВыборкаТЧ_Товары.АкцизСплачено;
			Иначе
				ВыводитьОплАкциза = Ложь;
			КонецЕсли;
		Иначе
			ВыводитьОплАкциза = Ложь;
		КонецЕсли;
		
		//Вывод шапки
		ТабличныйДокумент.Вывести(ОбластьШапка_нпп);
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_УКТВЭД);
		КонецЕсли;				
		
		ТабличныйДокумент.Присоединить(ОбластьШапка_Данные);
		
		Если ВыводитьМРЦ Тогда
			ТабличныйДокумент.Присоединить(ОбластьШапка_МРЦ1);
		КонецЕсли; 
		ТабличныйДокумент.Присоединить(ОбластьШапка_ДанныеКол);
		
		Если ВыводитьОплАкциза Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_Акциз);
		КонецЕсли;
		
		Если ВыводитьМРЦ5 Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_МРЦ);
		КонецЕсли;
		                                                                 
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СуммаМРЦ 		= 0;
		СуммаМРЦПлюс 	= 0;
		
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТЧ_Товары.Сбросить();
		
		Пока ВыборкаТЧ_Товары.НайтиСледующий(ВыборкаДокументов.Ссылка, "Ссылка") Цикл 
			
			ВыборкаТМ = ВыборкаТЧ_Товары.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ОднаТМ = ВыборкаТМ.Количество() = 1;
			Пока ВыборкаТМ.Следующий() Цикл 
				
				Если ВыводитьТМ И Не (ОднаТМ И Не ЗначениеЗаполнено(ВыборкаТМ.ПредставлениеТоварнаяМарка)) Тогда 
					СтруктураТМ = Новый Структура;
					СтруктураТМ.Вставить("ПредставлениеТоварнаяМарка", ВыборкаТМ.ПредставлениеТоварнаяМарка);
					СтруктураТМ.Вставить("КоличествоТМ", ВыборкаТМ.КоличествоБазовое);
					
					ТабличныйДокумент.Вывести(ОбластьГруппаТМ_нпп);
					Если ВыводитьУКТВЭД Тогда 
						ТабличныйДокумент.Присоединить(ОбластьГруппаТМ_УКТВЭД);
					КонецЕсли;
					ОбластьГруппаТМ_Данные.Параметры.Заполнить(СтруктураТМ);
					ТабличныйДокумент.Присоединить(ОбластьГруппаТМ_Данные);
					Если ВыводитьОплАкциза Тогда 
						ТабличныйДокумент.Присоединить(ОбластьГруппаТМ_Акциз);
					КонецЕсли;
					Если ВыводитьМРЦ5 Тогда 
						ТабличныйДокумент.Присоединить(ОбластьГруппаТМ_МРЦ);
					КонецЕсли;
				КонецЕсли;
				
				ВыборкаТовары = ВыборкаТМ.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл 			
					нпп = нпп + 1;
					
					СтруктураСтроки = Новый Структура("Номенклатура, Штрихкод, ЦенаПредставление, СуммаВсего, АкцизСплачено");
					ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);						
					
					СтруктураСтроки.Вставить("НомерСтроки", нпп);
					
					Если Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда 
						знчМРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(ВыборкаТовары.ХарактеристикаНоменклатуры, "МРЦ", "")));
						стрзнчМРЦ = СокрЛП(СтрЗаменить(ВыборкаТовары.ХарактеристикаНоменклатуры, "МРЦ", ""));
					Иначе
						знчМРЦ = 0;
						стрзнчМРЦ = "";
					КонецЕсли;

					Если ВыводитьУКТВЭД Или ШапкаКратко Тогда
						стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление1);
						СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура);
						СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.КоличествоБазовое));
						СтруктураСтроки.Вставить("ЕдиницаИзмерения", Строка(ВыборкаТовары.ОсновнаяЕдиницаИзмерения));
						СтруктураСтроки.Вставить("МРЦ", стрзнчМРЦ);
					Иначе
						стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);
						СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура);
						СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
						СтруктураСтроки.Вставить("МРЦ", стрзнчМРЦ);
					КонецЕсли;
					
					
					КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
					ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
					ВыборкаТовары.Номенклатура,
					ВыборкаТовары.КоличествоБазовое,
					ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
					ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
					СтруктураИтоговМест);
					
					СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
					
					ОбластьСтрока_нпп.Параметры.Заполнить(СтруктураСтроки);
					ТабличныйДокумент.Вывести(ОбластьСтрока_нпп);
					
					Если ВыводитьУКТВЭД Тогда 
						СтруктураСтроки.Вставить("КодУКТВЭД", ВыборкаТовары.КодУКТВЭД);
						
						ОбластьСтрока_УКТВЭД.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_УКТВЭД);
						
					КонецЕсли;
					
					Если ВыводитьУКТВЭД или ШапкаКратко Тогда
						ОбластьСтрока_ДанныеРазб.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_ДанныеРазб);
					Иначе
						ОбластьСтрока_Данные.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_Данные);
					КонецЕсли;
					
					Если ВыводитьМРЦ Тогда
						ОбластьСтрока_МРЦ1.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_МРЦ1);
					КонецЕсли; 
					
					Если ВыводитьУКТВЭД или ШапкаКратко Тогда
						ОбластьСтрока_ДанныеРазбКол.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_ДанныеРазбКол);
					Иначе
						ОбластьСтрока_ДанныеКол.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_ДанныеКол);
					КонецЕсли;

					
					Если ВыводитьОплАкциза Тогда 
						ОбластьСтрока_Акциз.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_Акциз);
					КонецЕсли;
					
					Если ВыводитьМРЦ5 Тогда 
						СуммаМРЦ = СуммаМРЦ+(знчМРЦ*ВыборкаТовары.КоличествоБазовое);
						СуммаМРЦПлюс = СуммаМРЦПлюс+(Цел(знчМРЦ*1.05*100)/100*ВыборкаТовары.КоличествоБазовое);
						
						СтруктураСтроки.Вставить("ЦенаМРЦ_Плюс5Проц", Цел(знчМРЦ*1.05*100)/100);
						
						ОбластьСтрока_МРЦ.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Присоединить(ОбластьСтрока_МРЦ);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Если ШапкаКратко Тогда
				Количество 	= ВыборкаТЧ_Товары.КоличествоБазовое;
			Иначе
				Количество 	= ?(ИтогиПоБазовомуКоличеству, ВыборкаТЧ_Товары.КоличествоБазовое, ВыборкаТЧ_Товары.Количество);
			КонецЕсли;
			
			СуммаНДС 	= ВыборкаТЧ_Товары.СуммаНДС;
			СуммаВсего 	= ВыборкаТЧ_Товары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоБезНДС", Окр(СуммаВсего, 2) - Окр(СуммаНДС, 2));
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		СтруктураПодвала.Вставить("СуммаМРЦ", Формат(СуммаМРЦ, "ЧЦ=15; ЧДЦ=2"));
		СтруктураПодвала.Вставить("СуммаМРЦПлюс", Формат(СуммаМРЦПлюс, "ЧЦ=15; ЧДЦ=2"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ВсегоКво", Количество);
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		
		//Область Подвал
		ТабличныйДокумент.Вывести(ОбластьПодвал_нпп);						
		
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьПодвал_УКТВЭД);
		КонецЕсли;
		
		ОбластьПодвал_Данные.Параметры.Заполнить(СтруктураПодвала);		
		ТабличныйДокумент.Присоединить(ОбластьПодвал_Данные);
		
		Если ВыводитьМРЦ Тогда
			ТабличныйДокумент.Присоединить(ОбластьПодвал_МРЦ1);
		КонецЕсли; 
		
		ОбластьПодвал_ДанныеКол.Параметры.Заполнить(СтруктураПодвала);		
		ТабличныйДокумент.Присоединить(ОбластьПодвал_ДанныеКол);

		Если ВыводитьОплАкциза Тогда 
			ТабличныйДокумент.Присоединить(ОбластьПодвал_Акциз);
		КонецЕсли;
		
		Если ВыводитьМРЦ5 Тогда 
			ТабличныйДокумент.Присоединить(ОбластьПодвал_МРЦ);
			ОбластьДопПодвал_СуммаМРЦ.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьДопПодвал_СуммаМРЦ);
			Если ВыводитьОплАкциза Тогда 
				ТабличныйДокумент.Присоединить(ОбластьДопПодвал_АкцизМРЦ);
			КонецЕсли;
			ОбластьДопПодвал_СуммаМРЦПлюс.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Присоединить(ОбластьДопПодвал_СуммаМРЦПлюс);
		КонецЕсли;
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		//Возвратная тара
		нТара = тзТара.Скопировать(Новый Структура("Ссылка", ВыборкаДокументов.Ссылка));				
		Если нТара.Количество() > 0 Тогда 
			нТара.Сортировать("НомерСтроки");
			
			ТабличныйДокумент.Вывести(ОбластьПробел);
			
			ОбластьШапкаТара.Параметры.ИмяКолонкиТовар = НСтр("ru = 'Возвратная тара'; uk = 'Зворотня тара'", КодЯзыкаПечать);
			ТабличныйДокумент.Вывести(ОбластьШапкаТара);			
			
			Для Каждого СтрТара Из нТара Цикл 
				ОбластьСтрокаТара.Параметры.Заполнить(СтрТара);
				ТабличныйДокумент.Вывести(ОбластьСтрокаТара);
			КонецЦикла;			
			
			СтруктураПодвалТара = Новый Структура;
			СтруктураПодвалТара.Вставить("ВсегоКво", нТара.Итог("КоличествоПредставление"));
			СтруктураПодвалТара.Вставить("ВсегоСумма", нТара.Итог("СуммаВсего"));
			
			ОбластьПодвалТара.Параметры.Заполнить(СтруктураПодвалТара);
			ТабличныйДокумент.Вывести(ОбластьПодвалТара);
		КонецЕсли;		
		
		
		Если ВыборкаДокументов.РегламентированныйУчет И ВыводитьУКТВЭД Тогда
			Должность = РегистрыСведений.ОтветственныеЛицаОрганизаций.ПолучитьПоследнее(ВыборкаДокументов.Дата,Новый Структура("СтруктурнаяЕдиница,ОтветственноеЛицо",ВыборкаДокументов.Склад,Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов)).Должность;
			Отгрузил = ВыборкаДокументов.Ссылка.Ответственный;
			ОтгрузилСклад = ОтветственныеЛица.ПодобратьОтветственноеЛицоСклада(ВыборкаДокументов.Склад, ВыборкаДокументов.Поставщик , ВыборкаДокументов.Дата,Справочники.Должности.НайтиПоНаименованию("Директор"));
			Если НЕ ЗначениеЗаполнено(Отгрузил) Тогда
				Если ЗначениеЗаполнено(ОтгрузилСклад) Тогда
					Отгрузил = ОтгрузилСклад;
				Иначе
					Отгрузил = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, ВыборкаДокументов.Склад, ВыборкаДокументов.Дата);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Должность = "";
			Отгрузил = "";
		КонецЕсли;
		
		ОбластьПодписи.Параметры.Должность = НРег(Должность);
		ОбластьПодписи.Параметры.Отгрузил = Отгрузил;
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		//Вывод сертификатов
		Если ВыборкаДокументов.РегламентированныйУчет Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	МАКСИМУМ(ТК_СертификатыТовары.Ссылка.Номер) КАК Номер,
			|	ТК_СертификатыТовары.Номенклатура КАК Номенклатура
			|ПОМЕСТИТЬ втСертификаты
			|ИЗ
			|	Справочник.ТК_Сертификаты.Товары КАК ТК_СертификатыТовары
			|ГДЕ
			|	ТК_СертификатыТовары.Номенклатура В
			|			(ВЫБРАТЬ
			|				РеализацияТоваровТовары.Номенклатура КАК Номенклатура
			|			ИЗ
			|				Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
			|			ГДЕ
			|				РеализацияТоваровТовары.Ссылка = &Ссылка)
			|	И НЕ ТК_СертификатыТовары.Ссылка.ПометкаУдаления
			|	И ТК_СертификатыТовары.Ссылка.ДатаВыдачи <= &Дата
			|	И ТК_СертификатыТовары.Ссылка.ДатаОкончания > &Дата
			|
			|СГРУППИРОВАТЬ ПО
			|	ТК_СертификатыТовары.Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	втСертификаты.Номер КАК Номер
			|ИЗ
			|	втСертификаты КАК втСертификаты";
			
			Запрос.УстановитьПараметр("Ссылка", ВыборкаДокументов.Ссылка);
			Запрос.УстановитьПараметр("Дата", ВыборкаДокументов.Дата);
			Результат = Запрос.Выполнить();
			
			Если Не Результат.Пустой() Тогда 
				СтрокаСертификаты = "";
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл 
					СтрокаСертификаты = ?(Не ПустаяСтрока(СтрокаСертификаты), СтрокаСертификаты + ", ", "") + СокрЛП(Выборка.Номер);
				КонецЦикла;
				
				СтруктураСертификаты = Новый Структура;
				СтруктураСертификаты.Вставить("СертификатыПредставление", НСтр("ru = 'Сертификаты: '; uk = 'Сертифікати: '", КодЯзыкаПечать) + СтрокаСертификаты);
				
				ОбластьСертификаты.Параметры.Заполнить(СтруктураСертификаты);
				ТабличныйДокумент.Вывести(ОбластьСертификаты);
			КонецЕсли;
		КонецЕсли;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Расходная накладная 2 шт
Функция ПечатьРасходнаяНакладнаяДвойная(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК Ссылка,
	|	РеализацияТоваров.Организация КАК Поставщик,
	|	РеализацияТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	РеализацияТоваров.СкладКомпании КАК СкладКомпании,
	|	РеализацияТоваров.Контрагент КАК ПредставлениеПолучателя,
	|	РеализацияТоваров.Контрагент.НаименованиеПолное КАК ПредставлениеПолучателяПолное,
	|	РеализацияТоваров.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ОснованиеПредставление,
	|	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	|	РеализацияТоваров.ТочкаДоставки.Представление КАК АдресДоставки,
	|	РеализацияТоваров.Дата КАК Дата,
	|	РеализацияТоваров.Номер КАК Номер,
	|	РеализацияТоваров.ХозОперация КАК ХозОперация,
	|	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	РеализацияТоваров.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ЗаказКомментарий,
	|	РеализацияТоваров.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.Представление КАК ТоварПредставление,
	|		Номенклатура.ТоварнаяМарка.Родитель.НаименованиеПолное КАК ТоварнаяМарка,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры.Наименование КАК ХарактеристикаНоменклатуры,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		ЦенаБезНалогов КАК ЦенаБезНалогов,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		СуммаБезНалогов КАК СуммаБезНалогов,
	|		ПроцентСкидки КАК ПроцентСкидки,
	|		СуммаСкидки КАК СуммаСкидки,
	|		Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	|		Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	|		Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент
	|	) КАК Товары,
	|	РеализацияТоваров.РаспределениеСписания.(
	|		Сотрудник КАК Сотрудник,
	|		СуммаВсего КАК СуммаВсего
	|	) КАК РаспределениеСписания,
	|	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.Порядок, 0) КАК ПорядокПивной,
	|	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.ПорядокСигаретный, 0) КАК ПорядокСигаретный
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка В(&МассивСсылок)
	|АВТОУПОРЯДОЧИВАНИЕ";// + СортировкаЗапроса + ", НомерСтроки";
	
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();	//	Запрос.Выполнить().Выгрузить()
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладнаяДвойная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "ru"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_НакладнаяДвойная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьСклад			= Макет.ПолучитьОбласть("Склад");	
	ОбластьЗаказ			= Макет.ПолучитьОбласть("Заказ");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапкаТабБезНДС	= Макет.ПолучитьОбласть("ШапкаТаблицыБезНДС");	
	ОбластьГруппаТМ			= Макет.ПолучитьОбласть("ГруппаТМ");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");	
	ОбластьИтогПодвал		= Макет.ПолучитьОбласть("ИтогПодвал");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");	
	ОбластьИтогоПФ			= Макет.ПолучитьОбласть("ИтогоПФ");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодвалАктаУслуг	= Макет.ПолучитьОбласть("ПодвалАктаУслуг");	
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьПодписиПечать	= Макет.ПолучитьОбласть("ПодписиПечать");
	ОбластьСертификаты		= Макет.ПолучитьОбласть("Сертификаты");
	ОбластьПоставщикОднаШапка 	= Макет.ПолучитьОбласть("ПоставщикОднаШапка");
	ОбластьПокупательОднаШапка 	= Макет.ПолучитьОбласть("ПокупательОднаШапка");
	ОбластьСкладОднаШапка	= Макет.ПолучитьОбласть("СкладОднаШапка");
	ОбластьЗаказОднаШапка	= Макет.ПолучитьОбласть("ЗаказОднаШапка");
	ОбластьПодписиСписание	= Макет.ПолучитьОбласть("ПодписиСписание");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ПечатьАкта = ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.АктОбОказанииУслуг"); 
		РеглУчет = ВыборкаДокументов.РегламентированныйУчет;
		
		НомерСтраницы	= 2;
		НомерСтраницыПред = НомерСтраницы;
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ТекстЗаголовкаХФ", ЗаголовокЛок);
		
		Поставщик = ?(ПечатьАкта, НСтр("ru = 'Исполнитель : '; uk = 'Виконавець : '", КодЯзыкаПечать), НСтр("ru = 'Поставщик : '; uk = 'Постачальник : '", КодЯзыкаПечать));
		ПредставлениеПоставщика = ?(Не ПустаяСтрока(ВыборкаДокументов.ПредставлениеПоставщика), СокрЛП(ВыборкаДокументов.ПредставлениеПоставщика), СокрЛП(ВыборкаДокументов.Поставщик));
		
		СтруктураШапки.Вставить("Поставщик", Поставщик);		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ПредставлениеПоставщика);		
		
		СтруктураШапки.Вставить("ПоставщикХФ", ?(Не ПечатьАкта И РеглУчет, Поставщик, ""));
		СтруктураШапки.Вставить("ПредставлениеПоставщикаХФ", ?(Не ПечатьАкта И РеглУчет, ПредставлениеПоставщика, ""));
		
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ?(Не ПустаяСтрока(ВыборкаДокументов.ПредставлениеПолучателяПолное), СокрЛП(ВыборкаДокументов.ПредставлениеПолучателяПолное), СокрЛП(ВыборкаДокументов.ПредставлениеПолучателя)));
		
		АдресДоставки = "";
		Если Не ПечатьАкта 
			И (ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.РеализацияТоваровСписание")
			ИЛИ ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.УдержаниеЗаПовреждениеТовара")) Тогда
			
			СтрокаПолучатели = "";			
			ВыборкаСписания = ВыборкаДокументов.РаспределениеСписания.Выбрать();
			Пока ВыборкаСписания.Следующий() Цикл 
				СтрокаПолучатели = СтрокаПолучатели + СокрЛП(ВыборкаСписания.Сотрудник) + " - " + Формат(ВыборкаСписания.СуммаВсего, "ЧДЦ=2; ЧН=0,00") + " " + Символы.ПС;
			КонецЦикла;			
			
			АдресДоставки = СтрокаПолучатели;
			
		ИначеЕсли Не ПустаяСтрока(ВыборкаДокументов.АдресДоставки) Тогда 
			
			АдресДоставки = СокрЛП(ВыборкаДокументов.АдресДоставки);
			
		КонецЕсли;
		
		СтруктураШапки.Вставить("АдресДоставки", АдресДоставки);
		СтруктураШапки.Вставить("ОснованиеПредставление", НСтр("ru = 'Договор'; uk = 'Договір'", КодЯзыкаПечать) + " " + ВыборкаДокументов.ОснованиеПредставление); 
		
		ПредставлениеЗаказ = "";
		ПредставлениеРезерва = "";
		
		ДокументОснование = ВыборкаДокументов.ДокументОснование;		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
			ПредставлениеЗаказ =  Строка(ДокументОснование);
			
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	СвязанныеДокументы.Ссылка КАК Ссылка,
			|	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РезервированиеЗаказовПокупателя).Номер КАК Номер,
			|	ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РезервированиеЗаказовПокупателя).Дата КАК Дата
			|ИЗ
			|	КритерийОтбора.СвязанныеДокументы(&Документ) КАК СвязанныеДокументы
			|ГДЕ
			|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.РезервированиеЗаказовПокупателя");
			
			Запрос.УстановитьПараметр("Документ", ДокументОснование);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда 
				ПредставлениеРезерва = Строка(Выборка.Ссылка);
			КонецЕсли;				
		КонецЕсли;
		
		СтруктураШапки.Вставить("ПредставлениеЗаказ", ПредставлениеЗаказ);
		СтруктураШапки.Вставить("ПредставлениеРезерва", ПредставлениеРезерва);
		
		СкладКомпании = ВыборкаДокументов.СкладКомпании;
		
		НадписьСкладХФ = "";
		ПредставлениеСкладаХФ = "";
		
		Если Не ПечатьАкта И РеглУчет Тогда 
			НадписьСкладХФ = НСтр("ru = 'Склад отгрузки : '; uk = 'Склад відвантаження : '", КодЯзыкаПечать);
			ПредставлениеСкладаХФ = СкладКомпании;
		ИначеЕсли Не ПустаяСтрока(ВыборкаДокументов.ЗаказКомментарий) Тогда 
			НадписьСкладХФ = НСтр("ru = 'Примечание: '; uk = 'Примітка:'", КодЯзыкаПечать);
			ПредставлениеСкладаХФ = СокрЛП(ВыборкаДокументов.ЗаказКомментарий);
		КонецЕсли;
		
		СтруктураШапки.Вставить("ПредставлениеСклада", СкладКомпании);
		СтруктураШапки.Вставить("НадписьСкладХФ", НадписьСкладХФ);
		СтруктураШапки.Вставить("ПредставлениеСкладаХФ", ПредставлениеСкладаХФ);
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ОбластьЗаказ.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаказ);
		
		ОбластьСклад.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСклад);
		
		//Товары
		ТаблицаТовары = ВыборкаДокументов.Товары.Выгрузить();		
		
		нпп 		= 0;
		СуммаНДС 	= ТаблицаТовары.Итог("СуммаНДС");
		Количество	= ТаблицаТовары.Итог("Количество");
		СуммаВсего	= ТаблицаТовары.Итог("СуммаВсего");
		ЕстьНДС = СуммаНДС > 0;		
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();				
		
		Если ЕстьНДС Тогда 
			ТабличныйДокумент.Вывести(ОбластьШапка);
		Иначе
			ТабличныйДокумент.Вывести(ОбластьШапкаТабБезНДС);
		КонецЕсли;
		
		ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы-1,, ТабличныйДокумент.ВысотаТаблицы,);
		
		втТМ = ТаблицаТовары.Скопировать(,"ТоварнаяМарка");
		втТМ.Свернуть("ТоварнаяМарка");
		втТМ.Сортировать("ТоварнаяМарка");
		ОднаТМ = втТМ.Количество() = 1;
		
		Для Каждого ТМ Из втТМ Цикл
			ВТ = ТаблицаТовары.Скопировать(Новый Структура("ТоварнаяМарка",ТМ.ТоварнаяМарка));
			Если Не (ОднаТМ И Не ЗначениеЗаполнено(ТМ.ТоварнаяМарка)) Тогда
				
				СтруктураТМ = Новый Структура;
				СтруктураТМ.Вставить("ТоварнаяМарка", ТМ.ТоварнаяМарка);
				СтруктураТМ.Вставить("КоличествоТМ", ВТ.Итог("КоличествоБазовое"));
				
				ОбластьГруппаТМ.Параметры.Заполнить(СтруктураТМ);
				ТабличныйДокумент.Вывести(ОбластьГруппаТМ);
			КонецЕсли;
			Для Каждого ВыборкаТовары Из ВТ Цикл 
				нпп = нпп + 1;			
				
				СтруктураСтроки = Новый Структура;			
				СтруктураСтроки.Вставить("НомерСтроки", нпп);			
				
				стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
				СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
				СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
				
				КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
				ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
				ВыборкаТовары.Номенклатура,
				ВыборкаТовары.КоличествоБазовое,
				ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
				ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
				СтруктураИтоговМест);
				
				СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
				СтруктураСтроки.Вставить("Цена", ВыборкаТовары.Цена);
				СтруктураСтроки.Вставить("СуммаВсего", ВыборкаТовары.СуммаВсего);
				
				Если Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда 
					знчМРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(ВыборкаТовары.ХарактеристикаНоменклатуры, "МРЦ", "")));
				Иначе
					знчМРЦ = 0;
				КонецЕсли;
				
				СтруктураСтроки.Вставить("МРЦ_5Проц", Цел(знчМРЦ*1.05*100)/100);
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);						
			КонецЦикла;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоБезНДС", Окр(СуммаВсего, 2) - Окр(СуммаНДС, 2));
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("Всего", СуммаВсего);
		СтруктураПодвала.Вставить("ИтогКво", Количество); 
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ВсегоКво", Количество);
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ОбластьИтогПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтогПодвал);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);						
		
		Если ПечатьАкта Тогда 
			ТабличныйДокумент.Вывести(ОбластьПодвалАктаУслуг);
		КонецЕсли;
		
		//Сертификаты товаров
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Расходная накладная 2 шт УКТЗЕД
Функция ПечатьРасходнаяНакладнаяДвойнаяУКТВЭД(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Порядок 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
	QR_Заголовок	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "QR_Заголовок", Ложь);
	ШапкаКратко		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ШапкаКратко", Ложь);
	ВыводитьТМ 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьТМ", Ложь);
	ВыводитьМРЦ5		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьМРЦ5", Ложь);
	ВыводитьУКТВЭД	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьУКТВЭД", Ложь);
	ИтогиПоБазовомуКоличеству = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ИтогиПоБазовомуКоличеству", Ложь);
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокСигаретный";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокПивной";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	РеализацияТоваров.МоментВремени";
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровТовары.Номенклатура.Артикул КАК Артикул,
	|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.Номенклатура) КАК ТоварПредставление,
	|	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатуры,
	|	РеализацияТоваровТовары.Количество КАК Количество,
	|	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	|	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	РеализацияТоваровТовары.Цена КАК ЦенаПредставление,
	|	РеализацияТоваровТовары.Сумма КАК Сумма,
	|	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
	|	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровТовары.Номенклатура.КодУКТВЭД
	|		КОГДА РеализацияТоваровТовары.ХарактеристикаНоменклатуры.УКТВЭД = ЗНАЧЕНИЕ(справочник.КлассификаторУКТВЭД.пустаяСсылка)
	|			ТОГДА РеализацияТоваровТовары.Номенклатура.КодУКТВЭД
	|		ИНАЧЕ РеализацияТоваровТовары.ХарактеристикаНоменклатуры.УКТВЭД
	|	КОНЕЦ КАК КодУКТВЭД,
	|	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	|	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	|	РеализацияТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	|	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	|	ЕСТЬNULL(РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	|	ВЫБОР
	|		КОГДА &ВыводитьТМ
	|			ТОГДА РеализацияТоваровТовары.Номенклатура.ТоварнаяМарка.Родитель.НаименованиеПолное
	|		ИНАЧЕ ВЫРАЗИТЬ("""" КАК СТРОКА(1))
	|	КОНЕЦ КАК ПредставлениеТоварнаяМарка,
	|	РеализацияТоваровТовары.Номенклатура.Наименование2 КАК ТоварПредставление1
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	|ГДЕ
	|	РеализацияТоваровТовары.Ссылка В(&МассивСсылок)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка,
	|	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод
	|ПОМЕСТИТЬ втШтрихКода
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	(ШтрихкодыНоменклатуры.Номенклатура, ШтрихкодыНоменклатуры.Упаковка) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втТовары.Номенклатура КАК Номенклатура,
	|				втТовары.ЕдиницаИзмерения КАК Упаковка
	|			ИЗ
	|				втТовары КАК втТовары)
	|
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыНоменклатуры.Упаковка,
	|	ШтрихкодыНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.НомерСтроки КАК НомерСтроки,
	|	втТовары.Ссылка КАК Ссылка,
	|	втТовары.Артикул КАК Артикул,
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.ТоварПредставление КАК ТоварПредставление,
	|	втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	втТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втТовары.Количество КАК Количество,
	|	втТовары.Коэффициент КАК Коэффициент,
	|	втТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	втТовары.ЦенаПредставление КАК ЦенаПредставление,
	|	втТовары.Сумма КАК Сумма,
	|	втТовары.СуммаВсего КАК СуммаВсего,
	|	втТовары.СуммаНДС КАК СуммаНДС,
	|	втТовары.КодУКТВЭД КАК КодУКТВЭД,
	|	втТовары.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	|	втТовары.ЕдиницаИзмеренияУпаковкиКоэффициент КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	|	втТовары.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|	втТовары.ОсновнаяЕдиницаИзмеренияКоэффициент КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	|	втТовары.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	|	втТовары.ЕдиницаИзмеренияЯщиковКоэффициент КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	|	втТовары.ПредставлениеТоварнаяМарка КАК ПредставлениеТоварнаяМарка,
	|	ВЫБОР
	|		КОГДА втТовары.Номенклатура.ОсновнойШтрихКод <> """"
	|			ТОГДА втТовары.Номенклатура.ОсновнойШтрихКод
	|		ИНАЧЕ ЕСТЬNULL(втШтрихКода.Штрихкод, """")
	|	КОНЕЦ КАК Штрихкод,
	|	втТовары.ТоварПредставление1 КАК ТоварПредставление1
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втШтрихКода КАК втШтрихКода
	|		ПО втТовары.Номенклатура = втШтрихКода.Номенклатура
	|			И втТовары.ЕдиницаИзмерения = втШтрихКода.Упаковка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ПредставлениеТоварнаяМарка,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоБазовое),
	|	СУММА(Сумма),
	|	СУММА(СуммаВсего),
	|	СУММА(СуммаНДС)
	|ПО
	|	Ссылка,
	|	ПредставлениеТоварнаяМарка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК Ссылка,
	|	РеализацияТоваров.Организация КАК Поставщик,
	|	РеализацияТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	РеализацияТоваров.СкладКомпании КАК Склад,
	|	РеализацияТоваров.СкладКомпании.НаименованиеПредставление КАК ПредставлениеСкладаМестоСоставления,
	|	РеализацияТоваров.Контрагент КАК Контрагент,
	|	РеализацияТоваров.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	|	РеализацияТоваров.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ОснованиеПредставление,
	|	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	|	РеализацияТоваров.ТочкаДоставки.Представление КАК АдресДоставки,
	|	РеализацияТоваров.Дата КАК Дата,
	|	РеализацияТоваров.Номер КАК Номер,
	|	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.Порядок, 0) КАК ПорядокПивной,
	|	ЕСТЬNULL(РеализацияТоваров.ТочкаДоставки.ПорядокСигаретный, 0) КАК ПорядокСигаретный,
	|	ВЫБОР
	|		КОГДА РеализацияТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РеализацияТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).Комментарий
	|		ИНАЧЕ РеализацияТоваров.Комментарий
	|	КОНЕЦ КАК Комментарий,
	|	РеализацияТоваров.ДокументОснование КАК ДокументОснование,
	|	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка В(&МассивСсылок)" + СортировкаЗапроса;
	
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	Запрос.УстановитьПараметр("ВыводитьТМ", ВыводитьТМ);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаТЧ_Товары  = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	ВыборкаДокументов = МассивРезультатов[3].Выбрать();
	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладнаяДвойнаяУКТВЭД";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_НакладнаяДвойнаяУКТВЭД");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	Если QR_Заголовок Тогда 
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("ЗаголовокU_Track");				
	Иначе
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	КонецЕсли;
	
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьКомментарий		= Макет.ПолучитьОбласть("Комментарий");
	ОбластьМестоСоставления = Макет.ПолучитьОбласть("Склад");	
	ОбластьЗаказ			= Макет.ПолучитьОбласть("Заказ");
	
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьГруппаТМ 		= Макет.ПолучитьОбласть("ГруппаТМ");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаКол");	
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");	
	ОбластьДопПодвал        = Макет.ПолучитьОбласть("ДопПодвал");
	
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьСертификаты		= Макет.ПолучитьОбласть("Сертификаты");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		?(ВыборкаДокументов.РегламентированныйУчет, 
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
		Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("АдресДоставки", "");
		
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		Если QR_Заголовок Тогда 									
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор()),1,125);
				КартинкаQR = Новый Картинка(ДанныеQRКода); 
				Для Каждого Рисунок Из ОбластьЗаголовок.Рисунки Цикл
					Рисунок.Картинка = КартинкаQR;
				КонецЦикла;
				//ОбластьЗаголовок.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;			
		КонецЕсли;		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);		
		
		Если ШапкаКратко Тогда 
			СтруктураШапки.Вставить("Комментарий", "");	
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьКомментарий.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьКомментарий);
		Иначе
			СтруктураШапки.Вставить("ПредставлениеПоставщика", "");
			СтруктураШапки.Вставить("Поставщик", "");			
			СтруктураШапки.Вставить("Основание", НСтр("ru = 'Основание'; uk = 'Підстава'"));
			СтруктураШапки.Вставить("ОснованиеПредставлениеМесто", "");			
			СтруктураШапки.Вставить("ПредставлениеЗаказ", "");
			СтруктураШапки.Вставить("ПредставлениеРезерва", "");		
			СтруктураШапки.Вставить("ОснованиеМесто", "");			
			СтруктураШапки.Вставить("ОснованиеПредставление", "");
			
			СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.ПредставлениеСкладаМестоСоставления)), ВыборкаДокументов.ПредставлениеСкладаМестоСоставления, ВыборкаДокументов.Склад));
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			Если Порядок = "Пиво" Тогда 
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной/1000);
			Иначе
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный/100);
			КонецЕсли;
			
			СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
			
			СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Поставщик, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина,,,ТипЗнч(ВыборкаДокументов.Ссылка)));
			СтруктураШапки.Вставить("РеквизитыПолучателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьМестоСоставления.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьМестоСоставления);
			
			ЗаказОснование = ВыборкаДокументов.ДокументОснование;			
			Если ЗначениеЗаполнено(ЗаказОснование) И ТипЗнч(ЗаказОснование) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
				СтруктураШапки.Вставить("ПредставлениеЗаказ", Строка(ЗаказОснование));
				
				Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
				|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СвязанныеДокументы.Ссылка) КАК Резерв
				|ИЗ
				|	КритерийОтбора.СвязанныеДокументы(&Ссылка) КАК СвязанныеДокументы
				|ГДЕ
				|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.РезервированиеЗаказовПокупателя
				|	И ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РезервированиеЗаказовПокупателя).Проведен");
				Запрос.УстановитьПараметр("Ссылка", ЗаказОснование);
				ВыборкаРезерв = Запрос.Выполнить().Выбрать();
				Если ВыборкаРезерв.Следующий() Тогда 
					СтруктураШапки.Вставить("ПредставлениеРезерва", ВыборкаРезерв.Резерв);
				КонецЕсли;		
				
				ОбластьЗаказ.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаказ);
			КонецЕсли;
		КонецЕсли;								                              
		
		
		//Вывод шапки
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СуммаМРЦ 		= 0;
		СуммаМРЦПлюс 	= 0;
		
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТЧ_Товары.Сбросить();
		
		Пока ВыборкаТЧ_Товары.НайтиСледующий(ВыборкаДокументов.Ссылка, "Ссылка") Цикл 
			
			ВыборкаТМ = ВыборкаТЧ_Товары.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ОднаТМ = ВыборкаТМ.Количество() = 1;
			Пока ВыборкаТМ.Следующий() Цикл 
				
				Если ВыводитьТМ И Не (ОднаТМ И Не ЗначениеЗаполнено(ВыборкаТМ.ПредставлениеТоварнаяМарка)) Тогда 
					СтруктураТМ = Новый Структура;
					СтруктураТМ.Вставить("ПредставлениеТоварнаяМарка", ВыборкаТМ.ПредставлениеТоварнаяМарка);
					СтруктураТМ.Вставить("КоличествоТМ", ВыборкаТМ.КоличествоБазовое);
					
					ОбластьГруппаТМ.Параметры.Заполнить(СтруктураТМ);
					ТабличныйДокумент.Вывести(ОбластьГруппаТМ);
				КонецЕсли;
				
				ВыборкаТовары = ВыборкаТМ.Выбрать();
				Пока ВыборкаТовары.Следующий() Цикл 			
					нпп = нпп + 1;
					
					СтруктураСтроки = Новый Структура("Номенклатура, Штрихкод, ЦенаПредставление, СуммаВсего");
					ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);						
					
					СтруктураСтроки.Вставить("НомерСтроки", нпп);
					
					Если ВыводитьУКТВЭД Или ШапкаКратко Тогда
						стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление1);
						СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
						СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.КоличествоБазовое));
						СтруктураСтроки.Вставить("ЕдиницаИзмерения", Строка(ВыборкаТовары.ОсновнаяЕдиницаИзмерения));
					Иначе
						стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);
						СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
						СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
					КонецЕсли;
					
					
					КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
					ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
					ВыборкаТовары.Номенклатура,
					ВыборкаТовары.КоличествоБазовое,
					ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
					ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
					СтруктураИтоговМест);
					
					СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
					СтруктураСтроки.Вставить("КодУКТВЭД", ВыборкаТовары.КодУКТВЭД);
					
					Если ВыводитьМРЦ5 Тогда 
						Если Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры) Тогда 
							знчМРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(ВыборкаТовары.ХарактеристикаНоменклатуры, "МРЦ", "")));
						Иначе
							знчМРЦ = 0;
						КонецЕсли;
						
						СуммаМРЦ = СуммаМРЦ+(знчМРЦ*ВыборкаТовары.КоличествоБазовое);
						СуммаМРЦПлюс = СуммаМРЦПлюс+(Цел(знчМРЦ*1.05*100)/100*ВыборкаТовары.КоличествоБазовое);
						
						СтруктураСтроки.Вставить("ЦенаМРЦ_Плюс5Проц", Цел(знчМРЦ*1.05*100)/100);
						
						
					КонецЕсли;
					ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
					ТабличныйДокумент.Вывести(ОбластьСтрока);
					
				КонецЦикла;
			КонецЦикла;
			
			Если ШапкаКратко Тогда
				Количество 	= ВыборкаТЧ_Товары.КоличествоБазовое;
			Иначе
				Количество 	= ?(ИтогиПоБазовомуКоличеству, ВыборкаТЧ_Товары.КоличествоБазовое, ВыборкаТЧ_Товары.Количество);
			КонецЕсли;
			
			СуммаНДС 	= ВыборкаТЧ_Товары.СуммаНДС;
			СуммаВсего 	= ВыборкаТЧ_Товары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоБезНДС", Окр(СуммаВсего, 2) - Окр(СуммаНДС, 2));
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		СтруктураПодвала.Вставить("СуммаМРЦ", Формат(СуммаМРЦ, "ЧЦ=15; ЧДЦ=2"));
		СтруктураПодвала.Вставить("СуммаМРЦПлюс", Формат(СуммаМРЦПлюс, "ЧЦ=15; ЧДЦ=2"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ВсегоКво", Количество);
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		//Область Подвал
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);		
		ТабличныйДокумент.Вывести(ОбластьПодвал);						
		
		ОбластьДопПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьДопПодвал);	
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		Если ВыборкаДокументов.РегламентированныйУчет И ВыводитьУКТВЭД Тогда
			Должность = РегистрыСведений.ОтветственныеЛицаОрганизаций.ПолучитьПоследнее(ВыборкаДокументов.Дата,Новый Структура("СтруктурнаяЕдиница,ОтветственноеЛицо",ВыборкаДокументов.Склад,Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов)).Должность;
			Отгрузил = ВыборкаДокументов.Ссылка.Ответственный;
			ОтгрузилСклад = ОтветственныеЛица.ПодобратьОтветственноеЛицоСклада(ВыборкаДокументов.Склад, ВыборкаДокументов.Поставщик , ВыборкаДокументов.Дата,Справочники.Должности.НайтиПоНаименованию("Директор"));
			Если НЕ ЗначениеЗаполнено(Отгрузил) Тогда
				Если ЗначениеЗаполнено(ОтгрузилСклад) Тогда
					Отгрузил = ОтгрузилСклад;
				Иначе
					Отгрузил = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, ВыборкаДокументов.Склад, ВыборкаДокументов.Дата);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Должность = "";
			Отгрузил = "";
		КонецЕсли;
		
		ОбластьПодписи.Параметры.Должность = НРег(Должность);
		ОбластьПодписи.Параметры.Отгрузил = Отгрузил;
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		//Вывод сертификатов
		Если ВыборкаДокументов.РегламентированныйУчет Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	МАКСИМУМ(ТК_СертификатыТовары.Ссылка.Номер) КАК Номер,
			|	ТК_СертификатыТовары.Номенклатура КАК Номенклатура
			|ПОМЕСТИТЬ втСертификаты
			|ИЗ
			|	Справочник.ТК_Сертификаты.Товары КАК ТК_СертификатыТовары
			|ГДЕ
			|	ТК_СертификатыТовары.Номенклатура В
			|			(ВЫБРАТЬ
			|				РеализацияТоваровТовары.Номенклатура КАК Номенклатура
			|			ИЗ
			|				Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
			|			ГДЕ
			|				РеализацияТоваровТовары.Ссылка = &Ссылка)
			|	И НЕ ТК_СертификатыТовары.Ссылка.ПометкаУдаления
			|	И ТК_СертификатыТовары.Ссылка.ДатаВыдачи <= &Дата
			|	И ТК_СертификатыТовары.Ссылка.ДатаОкончания > &Дата
			|
			|СГРУППИРОВАТЬ ПО
			|	ТК_СертификатыТовары.Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	втСертификаты.Номер КАК Номер
			|ИЗ
			|	втСертификаты КАК втСертификаты";
			
			Запрос.УстановитьПараметр("Ссылка", ВыборкаДокументов.Ссылка);
			Запрос.УстановитьПараметр("Дата", ВыборкаДокументов.Дата);
			Результат = Запрос.Выполнить();
			
			Если Не Результат.Пустой() Тогда 
				СтрокаСертификаты = "";
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл 
					СтрокаСертификаты = ?(Не ПустаяСтрока(СтрокаСертификаты), СтрокаСертификаты + ", ", "") + СокрЛП(Выборка.Номер);
				КонецЦикла;
				
				СтруктураСертификаты = Новый Структура;
				СтруктураСертификаты.Вставить("СертификатыПредставление", НСтр("ru = 'Сертификаты: '; uk = 'Сертифікати: '", КодЯзыкаПечать) + СтрокаСертификаты);
				
				ОбластьСертификаты.Параметры.Заполнить(СтруктураСертификаты);
				ТабличныйДокумент.Вывести(ОбластьСертификаты);
			КонецЕсли;
		КонецЕсли;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Счет на оплату
Функция ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК Ссылка,
	|	РеализацияТоваров.Организация КАК Поставщик,
	|	РеализацияТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	РеализацияТоваров.СкладКомпании КАК ПредставлениеСкладаМестоСоставления,
	|	РеализацияТоваров.Контрагент КАК Контрагент,
	|	РеализацияТоваров.Контрагент.НаименованиеПолное КАК ПредставлениеПокупателя,
	|	РеализацияТоваров.ДоговорВзаиморасчетов КАК ОснованиеПредставление,
	|	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	|	РеализацияТоваров.ТочкаДоставки.Представление КАК АдресДоставки,
	|	РеализацияТоваров.Дата КАК Дата,
	|	РеализацияТоваров.Номер КАК Номер,
	|	РеализацияТоваров.Автор КАК Автор,
	|	РеализацияТоваров.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.Представление КАК ТоварПредставление,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры.Наименование КАК ХарактеристикаНоменклатуры,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		ЦенаБезНалогов КАК ЦенаБезНалогов,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		СуммаБезНалогов КАК СуммаБезНалогов,
	|		ПроцентСкидки КАК ПроцентСкидки,
	|		СуммаСкидки КАК СуммаСкидки,
	|		Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	|		Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	|		Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	|		ЕСТЬNULL(РеализацияТоваров.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент
	|	) КАК Товары
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваров.МоментВремени,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СчетНаОплату";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; // КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_СчетНаОплату");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовокСчета	= Макет.ПолучитьОбласть("ЗаголовокСчета");
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтогоПоСтранице	= Макет.ПолучитьОбласть("ИтогоПоСтранице");	
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл 
		
		СтруктураШапки = Новый Структура("ТекстЗаголовка, ПредставлениеПоставщика,РеквизитыПоставщика, ПредставлениеПокупателя");
		
		СтруктураШапки.ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Счет-фактура №%1 от %2'; uk = 'Рахунок-фактура №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Поставщик, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов, "ПредставлениеПоставщика, ПредставлениеПокупателя");
		
		СтруктураПодвала = Новый Структура("ВсегоМест, СуммаСкидки, СуммаНДС, СуммаВсего,  ВалютаДокумента, Автор", 0, 0, 0, 0,,);
		ЗаполнитьЗначенияСвойств(СтруктураПодвала, ВыборкаДокументов, "ВалютаДокумента, Автор");
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;	
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		
		//Заполняем документ
		ТабличныйДокумент.Вывести(ОбластьЗаголовокСчета);
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьШапка.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьШапка);						
		
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", ВыборкаТовары.НомерСтроки);
			СтруктураСтроки.Вставить("ТоварПредставление", СокрЛП(ВыборкаТовары.Артикул) + ", " + СокрЛП(ВыборкаТовары.ТоварПредставление));
			СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + СокрЛП(ВыборкаТовары.ЕдиницаИзмерения));
			
			КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
			ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
			ВыборкаТовары.Номенклатура,
			ВыборкаТовары.КоличествоБазовое,
			ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
			ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
			ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
			ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
			ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
			ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
			СтруктураИтоговМест);			
			
			СтруктураСтроки.Вставить("Цена", ВыборкаТовары.Цена);
			СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);						
			СтруктураСтроки.Вставить("ПредставлениеСкидки", ВыборкаТовары.СуммаСкидки);
			СтруктураСтроки.Вставить("СуммаНДС", ВыборкаТовары.СуммаНДС);
			СтруктураСтроки.Вставить("СуммаВсего", ВыборкаТовары.СуммаВсего);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			СтруктураПодвала.СуммаСкидки = СтруктураПодвала.СуммаСкидки + ВыборкаТовары.СуммаСкидки;
			СтруктураПодвала.СуммаНДС = СтруктураПодвала.СуммаНДС + ВыборкаТовары.СуммаНДС;
			СтруктураПодвала.СуммаВсего = СтруктураПодвала.СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СтруктураПодвала.СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Бланк расходной накладной
Функция ПечатьБланкРасходнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваров.Ссылка КАК Ссылка,
	|	РеализацияТоваров.Контрагент.НаименованиеПолное КАК Покупатель,
	|	РеализацияТоваров.Номер КАК Номер,
	|	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	|	РеализацияТоваров.ТочкаДоставки КАК АдресДоставки,
	|	РеализацияТоваров.Дата КАК Дата
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|ГДЕ
	|	РеализацияТоваров.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваров.МоментВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ДополнительныеСведения.Значение, """") КАК Код,
	|	РеализацияТоваровТовары.Номенклатура.ОсновнойШтрихКод КАК ШтрихКод,
	|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатуры,
	|	РеализацияТоваровТовары.Количество КАК Количество,
	|	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	|	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РеализацияТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНДС,
	|	РеализацияТоваровТовары.Цена КАК ЦенаСНДС,
	|	РеализацияТоваровТовары.СуммаБезНалогов КАК СуммаБезНДС,
	|	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровТовары.СуммаВсего КАК СуммаСНДС,
	|	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	|	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КоэффициентУпаковки
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО РеализацияТоваровТовары.Номенклатура = ДополнительныеСведения.Объект
	|			И (ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура)
	|			И (ДополнительныеСведения.Свойство.Имя = ""КодТедис"")
	|ГДЕ
	|	РеализацияТоваровТовары.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументов = МассивРезультатов[0].Выбрать();
	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаТоваров.Индексы.Добавить("Ссылка");
	ТаблицаТоваров.Сортировать("НомерСтроки");
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_БланкРасходнойНакладной";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; // КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_БланкРасходнойНакладной");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьДетали 			= Макет.ПолучитьОбласть("Детали");
	ОбластьОшибка			= Макет.ПолучитьОбласть("Ошибка");	
	ОбластьИтого 			= Макет.ПолучитьОбласть("Итого");
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл 
		
		СтруктураШапки = Новый Структура("Покупатель, Номер, ТочкаДоставки, АдресДоставки, Дата");		
		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);				
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;	
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;					
		
		
		//Заполняем документ
		ОбластьШапка.Параметры.Заполнить(СтруктураШапки);		
		ТабличныйДокумент.Вывести(ОбластьШапка);		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		МассивСтрок = ТаблицаТоваров.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументов.Ссылка));
		тчТовары = ТаблицаТоваров.Скопировать(МассивСтрок);
		
		Ит = 0;
		Для Каждого ВыборкаТовары Из тчТовары Цикл 			
			Ит = Ит + 1;
			
			СтруктураСтроки = Новый Структура("Код, ШтрихКод, Номенклатура, ХарактеристикаНоменклатуры, ЦенаБезНДС, ЦенаСНДС, СуммаБезНДС, СуммаСНДС");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);
			
			Если ЗначениеЗаполнено(ВыборкаТовары.КоэффициентУпаковки) Тогда 
				КолвоУпаковок = ВыборкаТовары.КоличествоБазовое / ВыборкаТовары.КоэффициентУпаковки;
				Если КолвоУпаковок = Цел(КолвоУпаковок) Тогда 
					СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмеренияУпаковки);
					СтруктураСтроки.Вставить("Количество", КолвоУпаковок);
				Иначе
					СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмерения);
					СтруктураСтроки.Вставить("Количество", ВыборкаТовары.Количество);
				КонецЕсли;
			Иначе
				СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмерения);
				СтруктураСтроки.Вставить("Количество", ВыборкаТовары.Количество);
			КонецЕсли;
			
			СтруктураСтроки.Вставить("НомерСтроки", Ит);
			
			ОбластьДетали.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьДетали);
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура("ВсегоСуммаБезНДС, ВсегоСуммаСНДС", 
		Окр(тчТовары.Итог("СуммаСНДС"), 2) - Окр(тчТовары.Итог("СуммаНДС"), 2), 
		тчТовары.Итог("СуммаСНДС"));
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
	
КонецФункции

// Акт об обказании услуг
Функция ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Ссылка КАК Ссылка,
	               |	РеализацияТоваров.Номер КАК Номер,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваров.ТочкаДоставки КАК АдресДоставки,
	               |	РеализацияТоваров.Дата КАК Дата,
	               |	РеализацияТоваров.Организация КАК Поставщик,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	РеализацияТоваров.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК НаименованиеДляПечати,
	               |	СчетНаОплату.Ссылка КАК СчетНаОплату
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплату КАК СчетНаОплату
	               |		ПО СчетНаОплату.ДокументОснование = РеализацияТоваров.Ссылка
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РеализацияТоваров.МоментВремени
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	РеализацияТоваровТовары.Номенклатура.ОсновнойШтрихКод КАК ШтрихКод,
	               |	РеализацияТоваровТовары.Номенклатура.Артикул КАК Артикул,
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеализацияТоваровТовары.ХарактеристикаНоменклатуры) КАК ХарактеристикаНоменклатуры,
	               |	РеализацияТоваровТовары.Количество КАК Количество,
	               |	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РеализацияТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНДС,
	               |	РеализацияТоваровТовары.Цена КАК ЦенаПредставление,
	               |	РеализацияТоваровТовары.СуммаБезНалогов КАК СуммаБезНДС,
	               |	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	               |	РеализацияТоваровТовары.Сумма КАК Сумма,
	               |	РеализацияТоваровТовары.СуммаСкидки КАК СуммаСкидки,
	               |	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
	               |	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	               |	РеализацияТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КоэффициентУпаковки,
	               |	РеализацияТоваровТовары.Содержание КАК ТоварПредставление
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументов = МассивРезультатов[0].Выбрать();
	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаТоваров.Индексы.Добавить("Ссылка");
	ТаблицаТоваров.Сортировать("НомерСтроки");

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктОбОказанииУслуг";

	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_Акт");
	
	КодЯзыкаПечать = "uk"; // КодЛокализацииИнформационнойБазы();
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	ОбластьШапка 				= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("Заголовок");
	ОбластьТекстАктаНачало 		= Макет.ПолучитьОбласть("ТекстАктаНачало");
	ОбластьТекстАктаКонец 		= Макет.ПолучитьОбласть("ТекстАктаКонец");
	ОбластьШапкаТаблицы			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапкаТаблицыБезСкидок= Макет.ПолучитьОбласть("ШапкаТаблицыБезСкидок");
	ОбластьДопИнформация		= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьПробел 				= Макет.ПолучитьОбласть("Пробел");
	ОбластьСтрока 				= Макет.ПолучитьОбласть("Строка");
	ОбластьСтрокаБезСкидок		= Макет.ПолучитьОбласть("СтрокаБезСкидок");
	ОбластьИтого 				= Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоБезСкидок		= Макет.ПолучитьОбласть("ИтогоБезСкидок");
	ОбластьИтогоНДС				= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью		= Макет.ПолучитьОбласть("СуммаПрописью");	
	ОбластьМестоСоставления		= Макет.ПолучитьОбласть("МестоСоставления");	
	ОбластьПодписи				= Макет.ПолучитьОбласть("Подписи");	
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокументов.Следующий() Цикл 
		
		СтруктураШапки = Новый Структура;
		
		РуководительПоставщика = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДокументов.Поставщик, ВыборкаДокументов.Дата);

		СтруктураШапки.Вставить("ДолжностьРуководителяПоставщика", "Директор");
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ?(ЗначениеЗаполнено(ВыборкаДокументов.ОрганизацияНаименованиеПолное), СокрЛП(ВыборкаДокументов.ОрганизацияНаименованиеПолное), Строка(ВыборкаДокументов.Поставщик)));
		СтруктураШапки.Вставить("РуководительПоставщика", РуководительПоставщика);
		СтруктураШапки.Вставить("ПредставительПоставщика", "Директор");
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Поставщик, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина,,,ТипЗнч(ВыборкаДокументов.Ссылка),ВыборкаДокументов.РегламентированныйУчет,"Услуги"));
					
		СтруктураШапки.Вставить("ДолжностьРуководителяПокупателя", "Директор");
		СтруктураШапки.Вставить("ПредставлениеПокупателя", ?(ЗначениеЗаполнено(ВыборкаДокументов.КонтрагентНаименованиеПолное), СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное), строка(ВыборкаДокументов.Контрагент)));
		СтруктураШапки.Вставить("РуководительПоставщика", РуководительПоставщика);
		СтруктураШапки.Вставить("ПредставительПокупателя", "Директор");
	    СтруктураШапки.Вставить("РеквизитыПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		СтруктураШапки.Вставить("ПредставительПокупателя", "Директор");

		СтруктураШапки.Вставить("ПредставлениеДоговор", ВыборкаДокументов.НаименованиеДляПечати);
		тСчетНаОплату = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Счет на оплату №%1 от %2'; uk = 'Рахунок на оплату покупцю №%1 від %2'", КодЯзыкаПечать),
				СокрЛП(ВыборкаДокументов.СчетНаОплату.Номер),
				Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
				
		СтруктураШапки.Вставить("СчетНаОплату", тСчетНаОплату);
		СтруктураШапки.Вставить("Дата", ВыборкаДокументов.Дата);

		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'АКТ оказания услуг №%1 от %2'; uk = 'АКТ надання послуг №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		?(ВыборкаДокументов.РегламентированныйУчет, 
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
		Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));

		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);

		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;	
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;					
		
		//Заполняем документ
		ОбластьШапка.Параметры.Заполнить(СтруктураШапки);		
		ТабличныйДокумент.Вывести(ОбластьШапка);		
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьТекстАктаНачало.Параметры.Заполнить(СтруктураШапки);		
		ТабличныйДокумент.Вывести(ОбластьТекстАктаНачало);
		
		ОбластьТекстАктаКонец.Параметры.Заполнить(СтруктураШапки);		
		ТабличныйДокумент.Вывести(ОбластьТекстАктаКонец);
		
		//МассивСтруктурСтрок = Новый Массив;
		//МассивСтруктурСтрок.Добавить(Новый Структура("ДоговорНаименованиеДляПечати","");
		//Для каждого СтруктураСтроки Из МассивСтруктурСтрок Цикл
		//	ОбластьДопИнформация.Параметры.Заполнить(СтруктураСтроки);
		//	ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		//КонецЦикла;		
		ТабличныйДокумент.Вывести(ОбластьПробел);
		

		МассивСтрок = ТаблицаТоваров.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументов.Ссылка));
		тчТовары = ТаблицаТоваров.Скопировать(МассивСтрок);
		
		ЕстьСкидки = (тчТовары.Итог("СуммаСкидки") <> 0);
		
		Ит = 0;
		Для Каждого ВыборкаТовары Из тчТовары Цикл 			
			Ит = Ит + 1;
			
			СтруктураСтроки = Новый Структура("Артикул, ТоварПредставление, ЦенаПредставление, СуммаСкидки, СуммаСНДС, Сумма, СуммаВсего");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);
			
			Если ЗначениеЗаполнено(ВыборкаТовары.КоэффициентУпаковки) Тогда 
				КолвоУпаковок = ВыборкаТовары.КоличествоБазовое / ВыборкаТовары.КоэффициентУпаковки;
				Если КолвоУпаковок = Цел(КолвоУпаковок) Тогда 
					СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмеренияУпаковки);
					СтруктураСтроки.Вставить("Количество", КолвоУпаковок);
				Иначе
					СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмерения);
					СтруктураСтроки.Вставить("Количество", ВыборкаТовары.Количество);
				КонецЕсли;
			Иначе
				СтруктураСтроки.Вставить("ЕдиницаИзмерения", ВыборкаТовары.ЕдиницаИзмерения);
				СтруктураСтроки.Вставить("Количество", ВыборкаТовары.Количество);
			КонецЕсли;
			
			СтруктураСтроки.Вставить("НомерСтроки", Ит);
			
			//Если ЕстьСкидки Тогда
			//	ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			//	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			//	ТабличныйДокумент.Вывести(ОбластьСтрока);
			//Иначе 
				ОбластьСтрокаБезСкидок.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыБезСкидок);
				ТабличныйДокумент.Вывести(ОбластьСтрокаБезСкидок);
			//КонецЕсли;
			
		КонецЦикла;
		
		Если тчТовары <> Неопределено Тогда

			СуммаВсего  	= Окр(тчТовары.Итог("СуммаВсего"),2);
			ВсегоНДС 		= Окр(тчТовары.Итог("СуммаНДС"),2);
			СуммаБезСкидки  = Окр(тчТовары.Итог("Сумма"),2);
			СуммаСкидки     = Окр(тчТовары.Итог("СуммаСкидки"),2);
			СуммаБезНДС     = СуммаВсего - ВсегоНДС;
		Иначе

			СуммаВсего  = 0;
			ВсегоНДС 	= 0;
			СуммаБезСкидки = 0;
			СуммаСкидки    = 0;

		КонецЕсли;
		
		СтруктураПодвала = Новый Структура;
		СтруктураПодвала.Вставить("СуммаВсего", СуммаВсего);
		СтруктураПодвала.Вставить("ВсегоНДС", ВсегоНДС);
		СтруктураПодвала.Вставить("Всего", СуммаВсего);
		СтруктураПодвала.Вставить("СуммаБезСкидки", СуммаБезСкидки);
		СтруктураПодвала.Вставить("СуммаСкидки", СуммаСкидки);
		СтруктураПодвала.Вставить("НДС", НСтр("ru='В том числе НДС:';uk='В тому числі ПДВ:'",КодЯзыкаПечать));
		
		//Если ЕстьСкидки  Тогда
		//	ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		//	ТабличныйДокумент.Вывести(ОбластьИтого);
		//Иначе 
			ОбластьИтогоБезСкидок.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоБезСкидок);
		//КонецЕсли;
		
		УчитыватьНДС =  (тчТовары.Итог("СуммаНДС") <> 0);
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		Если УчитыватьНДС Тогда
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
			ТекстСуммаПрописью = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Общая стоимость работ (услуг) составила без НДС %1, НДС %2, общая стоимость работ (услуг) с НДС %3.';uk='Загальна вартість робіт (послуг) склала без ПДВ %1, ПДВ %2, загальна вартість робіт (послуг) із ПДВ %3.'", КодЯзыкаПечать),
					ЧислоПрописью(СуммаБезНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью),
					ЧислоПрописью(ВсегоНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью),
					ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью));
		Иначе
			ТекстСуммаПрописью = НСтр("ru='Общая стоимость работ (услуг) составила ';uk='Загальна вартість робіт (послуг) склала '",КодЯзыкаПечать) +ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		КонецЕсли;
		
		СтруктураПодвала.Вставить("СуммаПрописью", ТекстСуммаПрописью);
		СтруктураПодвала.Вставить("МестоСоставления", "");
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);

		ОбластьМестоСоставления.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьМестоСоставления);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
	
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции // ПечатьАктаОбОказанииУслуг()

// ДекларацияПроизводителя
Функция ПечатьДекларацияПроизводителя(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	МассивДопРеквизитов = Новый Массив;
	МассивДопРеквизитов.Добавить("ФС_ДСТУ");
	МассивДопРеквизитов.Добавить("ФС_Влажность");
	МассивДопРеквизитов.Добавить("ФС_Жирность");
	МассивДопРеквизитов.Добавить("ФС_Кислотность");
	МассивДопРеквизитов.Добавить("ФС_ТемператураХранения");
	МассивДопРеквизитов.Добавить("ФС_ОрганолептическиеСвойства");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("ДопРеквизиты", МассивДопРеквизитов);
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТовары.Ссылка.Дата КАК Дата,
	               |	РеализацияТоваровТовары.Ссылка.Номер КАК Номер,
	               |	РеализацияТоваровТовары.Ссылка.Организация.НаименованиеПолное КАК Производитель,
	               |	РеализацияТоваровТовары.Ссылка.Контрагент.НаименованиеПолное КАК Получатель,
	               |	РеализацияТоваровТовары.НомерСтроки КАК НомерПП,
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РеализацияТоваровТовары.Номенклатура.Наименование2 КАК НаименованиеПолное,
	               |	РеализацияТоваровТовары.Номенклатура.СрокХраненияТовара КАК СрокХранения,
	               |	РеализацияТоваровТовары.Количество КАК Количество,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения.Вес КАК ЕдиницаИзмеренияВес
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка В(&МассивСсылок)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НоменклатураДополнительныеРеквизиты.Ссылка КАК Номенклатура,
	               |	НоменклатураДополнительныеРеквизиты.Свойство.Имя КАК Имя,
	               |	НоменклатураДополнительныеРеквизиты.Свойство.ИдентификаторДляФормул КАК ИдентификаторДляФормул,
	               |	НоменклатураДополнительныеРеквизиты.Значение КАК Значение
	               |ИЗ
	               |	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	               |ГДЕ
	               |	НоменклатураДополнительныеРеквизиты.Ссылка В
	               |			(ВЫБРАТЬ
	               |				втТовары.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				втТовары КАК втТовары
	               |			СГРУППИРОВАТЬ ПО
	               |				втТовары.Номенклатура)
	               |	И НоменклатураДополнительныеРеквизиты.Свойство.Имя В(&ДопРеквизиты)
	               |АВТОУПОРЯДОЧИВАНИЕ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.Ссылка КАК Ссылка,
	               |	втТовары.Дата КАК Дата,
	               |	втТовары.Номер КАК Номер,
	               |	втТовары.НомерПП КАК НомерПП,
	               |	втТовары.Номенклатура КАК Номенклатура,
	               |	втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втТовары.НаименованиеПолное КАК НаименованиеПолное,
	               |	втТовары.СрокХранения КАК СрокХранения,
	               |	втТовары.Количество КАК Количество,
	               |	втТовары.ЕдиницаИзмеренияВес КАК ЕдиницаИзмеренияВес,
	               |	втТовары.Производитель КАК Производитель,
	               |	втТовары.Получатель КАК Получатель
	               |ИЗ
	               |	втТовары КАК втТовары
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата,
	               |	Ссылка,
	               |	НомерПП
	               |ИТОГИ
	               |	МАКСИМУМ(Дата),
	               |	МАКСИМУМ(Номер),
	               |	МАКСИМУМ(Производитель),
	               |	МАКСИМУМ(Получатель)
	               |ПО
	               |	Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ГраницаМассива = МассивРезультатов.ВГраница();
	
	ТаблицаСвойства 	= МассивРезультатов[ГраницаМассива-1].Выгрузить();
	ВыборкаДокументы	= МассивРезультатов[ГраницаМассива].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_ДекларацияПроизводителя");
	Макет.КодЯзыка = КодЯзыкаПечать;
		
	ОбластьШапкаДокумента	= Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы	= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");	

	ПервыйДокумент = Истина;		
	Пока ВыборкаДокументы.Следующий() Цикл 
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				

		ДатаДокумента 	 = ВыборкаДокументы.Дата;
		ДатаПроизводства = ВыборкаДокументы.Дата;
		
		СтруктураШапка = Новый Структура;
		СтруктураШапка.Вставить("Производитель", СокрЛП(ВыборкаДокументы.Производитель));
		СтруктураШапка.Вставить("Получатель", СокрЛП(ВыборкаДокументы.Получатель));
		СтруктураШапка.Вставить("ДатаДокумента", Формат(ДатаДокумента, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		СтруктураШапка.Вставить("ДатаОтгрузки", Формат(ДатаДокумента, "ДФ=dd.MM.yyyy") + НСтр("ru = ' г.'; uk = ' р.'", КодЯзыкаПечать));
		СтруктураШапка.Вставить("НомерДокумента", СокрЛП(ВыборкаДокументы.Номер));
		

		
		ОбластьШапкаДокумента.Параметры.Заполнить(СтруктураШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаПродукция = ВыборкаДокументы.Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл 
			СтруктураСтроки = Новый Структура("НомерПП,Номенклатура,Гост,МассаНетто,Жирность,Кислотность,Температура,Влажность,Органолептика,СрокГодности");
			
			СтруктураСтроки.НомерПП = ВыборкаПродукция.НомерПП;
			СтруктураСтроки.Номенклатура = ВыборкаПродукция.НаименованиеПолное;
			
			СтруктураСтроки.Вставить("Партия", Формат(ДатаПроизводства, "ДФ=ddMMyy"));			
			
			Если ЗначениеЗаполнено(ВыборкаПродукция.СрокХранения) Тогда 
				СрокХранения = ВыборкаПродукция.СрокХранения;					
				СтруктураСтроки.СрокГодности = Строка(СрокХранения) + НСтр("ru = ' дней'; uk = ' діб'", КодЯзыкаПечать);
			Иначе
				СрокХранения = 14; // По умолчанию 14 дней
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ДатаПроизводства", Формат(ДатаПроизводства + 60*60*24 * (СрокХранения - 1), "ДФ=dd.MM.yyyy"));
			
			СтруктураСтроки.МассаНетто = ВыборкаПродукция.Количество * ВыборкаПродукция.ЕдиницаИзмеренияВес;
			
			нСвойства = ТаблицаСвойства.НайтиСтроки(Новый Структура("Номенклатура", ВыборкаПродукция.Номенклатура));
			
			Для Каждого Стр Из нСвойства Цикл 
				Если Стр.Имя = "ФС_ДСТУ" Тогда 
					СтруктураСтроки.Гост = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Влажность" Тогда
					СтруктураСтроки.Влажность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Жирность" Тогда 
					СтруктураСтроки.Жирность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Кислотность" Тогда 
					СтруктураСтроки.Кислотность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_ТемператураХранения" Тогда 
					СтруктураСтроки.Температура = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_ОрганолептическиеСвойства" Тогда 
					СтруктураСтроки.Органолептика = Стр.Значение;
				КонецЕсли;
			КонецЦикла;
			
			ОбластьСтрокаТаблицы.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураШапка);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
	КонецЦикла;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
КонецФункции

// Расходная накладная ФС
Функция ПечатьРасходнаяНакладнаяФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыводитьУКТВЭД	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьУКТВЭД", Ложь);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Ссылка КАК Ссылка,
	               |	РеализацияТоваров.Дата КАК Дата,
	               |	РеализацияТоваров.Номер КАК Номер,
	               |	РеализацияТоваров.Организация КАК Организация,
	               |	РеализацияТоваров.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РеализацияТоваров.СкладКомпании КАК СкладКомпании,
	               |	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РеализацияТоваров.Комментарий КАК Комментарий,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ДоговорНаименование,
	               |	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваров.ДокументОснование КАК ДокументОснование,
	               |	РеализацияТоваров.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Наименование2 КАК НоменклатураНаименование,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	РеализацияТоваров.МоментВремени";
		
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_РасходнаяНакладная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	

	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("ЗаголовокU_Track");
	ОбластьФизОбмен             = Макет.ПолучитьОбласть("ОбластьОбмен");
	ОбластьДопИнформация		= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьПоставщик 			= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 			= Макет.ПолучитьОбласть("Покупатель");
	
	ОбластьШапка_нпп 			= Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");	
	ОбластьШапка_УКТВЭД			= Макет.ПолучитьОбласть("ШапкаТаблицы|КодУКТВЭД");		
	ОбластьШапка_Код			= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");		
	ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
	ОбластьШапка_ДанныеКол		= Макет.ПолучитьОбласть("ШапкаТаблицы|ДанныеКоличества");		
	ОбластьШапка_Скидка			= Макет.ПолучитьОбласть("ШапкаТаблицы|Скидка");		
	ОбластьШапка_Сумма			= Макет.ПолучитьОбласть("ШапкаТаблицы|Сумма");		
	
	ОбластьСтрока_нпп 			= Макет.ПолучитьОбласть("Строка|НомерСтроки");	
	ОбластьСтрока_УКТВЭД		= Макет.ПолучитьОбласть("Строка|КодУКТВЭД");		
	ОбластьСтрока_Код			= Макет.ПолучитьОбласть("Строка|КолонкаКодов");		
	ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
	ОбластьСтрока_ДанныеКол		= Макет.ПолучитьОбласть("Строка|ДанныеКоличества");
	ОбластьСтрока_Скидка	    = Макет.ПолучитьОбласть("Строка|Скидка");
	ОбластьСтрока_Сумма			= Макет.ПолучитьОбласть("Строка|Сумма");		
	
	ОбластьИтого_нпп 			= Макет.ПолучитьОбласть("Итого|НомерСтроки");	
	ОбластьИтого_Код			= Макет.ПолучитьОбласть("Итого|КолонкаКодов");		
	ОбластьИтого_УКТВЭД			= Макет.ПолучитьОбласть("Итого|КодУКТВЭД");		
	ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
	ОбластьИтого_Скидка	    	= Макет.ПолучитьОбласть("Итого|Скидка");
	ОбластьИтого_Сумма			= Макет.ПолучитьОбласть("Итого|Сумма");
	
	ОбластьИтогоНДС				= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью		= Макет.ПолучитьОбласть("СуммаПрописью");	
	ОбластьПодписи 				= Макет.ПолучитьОбласть("Подписи_Новые");
	
	ОбластьКолонкаТовар 		= Макет.Область("Товар");
	
	ШиринаКолонкаТовар 	= ОбластьКолонкаТовар.ШиринаКолонки;
	ШиринаКолонкиКод 	= Макет.Область("КолонкаКодов").ШиринаКолонки;
	ШиринаКолонкиСкидка	= Макет.Область("СуммаБезСкидки").ШиринаКолонки + Макет.Область("СуммаСкидки").ШиринаКолонки;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		РеглУчет = ВыборкаДокументов.РегламентированныйУчет;
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							?(ВыборкаДокументов.РегламентированныйУчет, 
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
							Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
		
		СтруктураШапки = Новый Структура;
		Если ВыводитьУКТВЭД Тогда 
			СтруктураШапки.Вставить("ИмяКолонкиКодов", НСтр("ru = 'ШтрихКод'; uk = 'ШтрихКод'", КодЯзыкаПечать));
		Иначе 
			СтруктураШапки.Вставить("ИмяКолонкиКодов", НСтр("ru = 'Артикул'; uk = 'Артикул'", КодЯзыкаПечать));
		КонецЕсли;
		СтруктураШапки.Вставить("Цена", ?(РеглУчет, НСтр("ru = 'Цена с НДС'; uk = 'Ціна з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Цена'; uk = 'Ціна'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("Сумма", ?(РеглУчет, НСтр("ru = 'Сумма с НДС'; uk = 'Сума з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Сумма'; uk = 'Сума'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
		СтруктураШапки.Вставить("ТекстЗаголовка", Заголовок);				
		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ВыборкаДокументов.ОрганизацияНаименованиеПолное);
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		
		СтруктураШапки.Вставить("ПредставлениеПокупателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		СтруктураШапки.Вставить("РеквизитыПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		
		МассивДопИнформации = Новый Массив;
		Если ЗначениеЗаполнено(ВыборкаДокументов.ДоговорВзаиморасчетов) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Договор:'; uk = 'Договір:'", КодЯзыкаПечать),
					?(ЗначениеЗаполнено(ВыборкаДокументов.ДоговорНаименование), СокрЛП(ВыборкаДокументов.ДоговорНаименование), ВыборкаДокументов.ДоговорВзаиморасчетов)));
					
		КонецЕсли;
				
		Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Точка доставки:'; uk = 'Точка доставки:'", КодЯзыкаПечать),
					ВыборкаДокументов.ТочкаДоставки));			
		КонецЕсли;
		
		Если Не РеглУчет И Не ПустаяСтрока(ВыборкаДокументов.Комментарий) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Комментарий:'; uk = 'Коментар:'", КодЯзыкаПечать),
					СокрЛП(ВыборкаДокументов.Комментарий)));						
		КонецЕсли;
		
		Попытка
			ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор()),1,125);
			ОбластьЗаголовок.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
		Исключение КонецПопытки;			

		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		тзТовары = ВыборкаДокументов.Товары.Выгрузить();
		
		ВыводитьАртикул = Истина;				
		ВыводитьСкидки 	= тзТовары.Итог("СуммаСкидки") <> 0;
		ОбластьКолонкаТовар.ШиринаКолонки = ШиринаКолонкаТовар + ?(Не ВыводитьАртикул, ШиринаКолонкиКод, 0) + ?(Не ВыводитьСкидки, ШиринаКолонкиСкидка, 0);
		
		ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
		ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
		ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
		
		СтруктураШапки.Вставить("ВсегоСуммаБезСкидки", тзТовары.Итог("Сумма"));
		СтруктураШапки.Вставить("ВсегоСуммаСкидки", тзТовары.Итог("СуммаСкидки"));
		СтруктураШапки.Вставить("Всего", тзТовары.Итог("СуммаВсего"));
		СтруктураШапки.Вставить("ВсегоНДС", тзТовары.Итог("СуммаНДС"));
		
		//Вывод шапки таблицы Продцукия
		ТабличныйДокумент.Вывести(ОбластьШапка_нпп);
		
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_УКТВЭД);
		КонецЕсли;
		
		Если ВыводитьАртикул Тогда 
			ОбластьШапка_Код.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьШапка_Код);
		КонецЕсли;
		
		ОбластьШапка_Данные.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Данные);
		
		Если ВыводитьСкидки Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_Скидка);
		КонецЕсли;
		
		ОбластьШапка_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Сумма);
		
		// Вывод строк таблицы		
		нпп = 0;
		Для Каждого Стр Из тзТовары Цикл 
			нпп = нпп + 1;
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			Если ВыводитьУКТВЭД Тогда 
				СтруктураСтроки.Вставить("Артикул", Стр.Номенклатура.ОсновнойШтрихкод);
			Иначе
				СтруктураСтроки.Вставить("Артикул", Стр.Артикул);
			КонецЕсли;
			СтруктураСтроки.Вставить("КодУКТВЭД", Стр.Номенклатура.КодУКТВЭД);
			СтруктураСтроки.Вставить("Товар", ?(ЗначениеЗаполнено(Стр.НоменклатураНаименование), Стр.НоменклатураНаименование, Стр.Номенклатура));
			СтруктураСтроки.Вставить("Количество", Стр.Количество);
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Стр.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("Цена", Стр.Цена);
			СтруктураСтроки.Вставить("Сумма", Стр.СуммаВсего);
			
			Если ВыводитьСкидки Тогда 
				СтруктураСтроки.Вставить("СуммаБезСкидки", Стр.Сумма);
				СтруктураСтроки.Вставить("СуммаСкидки", Стр.СуммаСкидки);
			КонецЕсли;
			
			ОбластьСтрока_нпп.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока_нпп);
			
			Если ВыводитьУКТВЭД Тогда 
				ОбластьСтрока_УКТВЭД.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_УКТВЭД);
			КонецЕсли;
			
			Если ВыводитьАртикул Тогда 
				ОбластьСтрока_Код.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Код);
			КонецЕсли;
				
			ОбластьСтрока_Данные.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Данные);
			
			Если ВыводитьСкидки Тогда 
				ОбластьСтрока_Скидка.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Скидка);
			КонецЕсли;
			
			ОбластьСтрока_Сумма.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Сумма);			
		КонецЦикла;

		ТабличныйДокумент.Вывести(ОбластьИтого_нпп);
		
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьИтого_УКТВЭД);
		КонецЕсли;

		Если ВыводитьАртикул Тогда 
			ТабличныйДокумент.Присоединить(ОбластьИтого_Код);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьИтого_Данные);
		
		Если ВыводитьСкидки Тогда 
			ОбластьИтого_Скидка.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьИтого_Скидка);
		КонецЕсли;

		ОбластьИтого_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьИтого_Сумма);
		
		Если РеглУчет Тогда 
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		КонецЕсли;
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Всего наименований %1 на сумму %2 %3.'; uk = 'Всього найменувань %1 на суму %2 %3.'", КодЯзыкаПечать),
							нпп, 
							СтруктураШапки.Всего,
							ВыборкаДокументов.ВалютаДокумента);
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СтруктураШапки.Всего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		Если РеглУчет Тогда 
			СуммаНДСПрописью = ЧислоПрописью(СтруктураШапки.ВсегоНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
			СуммаПрописью = СуммаПрописью + Символы.ПС + НСтр("ru = 'В т.ч. НДС: '; uk = 'У т.ч. ПДВ: '", КодЯзыкаПечать) + СуммаНДСПрописью;
		КонецЕсли;
		
		СтруктураШапки.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		СтруктураШапки.Вставить("СуммаПрописью", СуммаПрописью);
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		Если ТипЗнч(Ответственный) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
			СтрокаОтветственный = НСтр("ru = 'Директор '; uk = 'Директор '", КодЯзыкаПечать) + Ответственный;
		Иначе
			СтрокаОтветственный = "";
		КонецЕсли;
		
		СтруктураШапки.Вставить("Ответственный", СтрокаОтветственный);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
			
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
		ПервыйДокумент = Ложь;		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Товаро-транспортная накладная (Фабрика сыра)
Функция ПечатьТТН_ФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивИменСвойств = Новый Массив;
	МассивИменСвойств.Добавить("ВП_Автомобиль");
	МассивИменСвойств.Добавить("ВП_Прицеп");
	МассивИменСвойств.Добавить("ВП_Водитель");
	МассивИменСвойств.Добавить("ВП_НомерПрав");
	МассивИменСвойств.Добавить("ВП_Перевозчик");
	МассивИменСвойств.Добавить("ВП_ИННПеревозчика");
	МассивИменСвойств.Добавить("ВП_ПунктОтправитель");
	МассивИменСвойств.Добавить("ВП_ПунктПолучатель");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("МассивИменСвойств", МассивИменСвойств);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Ссылка КАК Ссылка,
	               |	РеализацияТоваров.Дата КАК Дата,
	               |	РеализацияТоваров.Номер КАК Номер,
	               |	РеализацияТоваров.Организация КАК Организация,
	               |	РеализацияТоваров.СкладКомпании.ТочкаДоставки КАК ПунктОтправитель,
	               |	РеализацияТоваров.ТочкаДоставки КАК ПунктПолучатель,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровТовары.Ссылка КАК Ссылка,
	               |	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровТовары.Номенклатура.Наименование2 КАК НоменклатураНаименованиеПолное,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РеализацияТоваровТовары.Количество КАК Количество,
	               |	РеализацияТоваровТовары.КоличествоБазовое * РеализацияТоваровТовары.ЕдиницаИзмерения.Вес КАК Масса,
	               |	РеализацияТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	               |	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка В(&МассивОбъектов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	НомерСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровДополнительныеРеквизиты.Ссылка КАК Ссылка,
	               |	РеализацияТоваровДополнительныеРеквизиты.Свойство.Имя КАК СвойствоИмя,
	               |	РеализацияТоваровДополнительныеРеквизиты.Значение КАК Значение
	               |ИЗ
	               |	Документ.РеализацияТоваров.ДополнительныеРеквизиты КАК РеализацияТоваровДополнительныеРеквизиты
	               |ГДЕ
	               |	РеализацияТоваровДополнительныеРеквизиты.Ссылка В(&МассивОбъектов)
	               |	И РеализацияТоваровДополнительныеРеквизиты.Свойство.Имя В(&МассивИменСвойств)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументы 	= МассивРезультатов[0].Выбрать();
	ТаблицаТовары 		= МассивРезультатов[1].Выгрузить();
	ДопРеквизиты 	 	= МассивРезультатов[2].Выгрузить();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТТН_ФС";
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТК_ТТН.ПФ_MXL_ТТН_ФС");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьАвтотранспорт	= Макет.ПолучитьОбласть("ПараметрыАвтотранспорта");
	ОбластьПогрузочныеОперации = Макет.ПолучитьОбласть("ПогрузочныеОперации");
	
	КэшАвтотранспорт = Новый Соответствие;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
				
		ПредставлениеОрганизация = Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументы.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументы.Дата, КодЯзыкаПечать, Ложь);
		ПредставлениеКонтрагента = Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументы.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументы.Дата, КодЯзыкаПечать);
		
		мДопРеквизиты = ДопРеквизиты.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументы.Ссылка));
		
		стрДопРеквизиты = Новый Структура;
		Для Каждого Стр Из мДопРеквизиты Цикл 
			стрДопРеквизиты.Вставить(Стр.СвойствоИмя, Стр.Значение);
		КонецЦикла;
		
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("Дата", 			Формат(ВыборкаДокументы.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		СтруктураРеквизитов.Вставить("Номер", 			СокрЛП(ВыборкаДокументы.Номер));
		СтруктураРеквизитов.Вставить("ВидПеревозки", 	"автоперевезення");
		
		СтруктураРеквизитов.Вставить("Отправитель", 					ПредставлениеОрганизация);		
		СтруктураРеквизитов.Вставить("ПлательщикПредставление",			ПредставлениеОрганизация); //ПредставлениеКонтрагента); //Vov41k 05.02.2024
		СтруктураРеквизитов.Вставить("ГрузополучательПредставление", 	ПредставлениеКонтрагента);
		
		СтруктураРеквизитов.Вставить("ПунктОтправитель", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_ПунктОтправитель", ВыборкаДокументы.ПунктОтправитель));
		СтруктураРеквизитов.Вставить("ПунктПолучатель",	 ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_ПунктПолучатель", ВыборкаДокументы.ПунктПолучатель));
		
		Автомобиль = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_Автомобиль", Неопределено);
		РеквизитыАвтомобиля = Новый Структура("ВинКодАвтомобиля,ГодВыпускаАвтомобиля,ДлинаАвтомобиля,ШиринаАвтомобиля,ВысотаАвтомобиля,ВесАвтомобиля,ВесАвтомобиляСГрузом");
		Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автотранспорт") Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	Автотранспорт.Наименование КАК Наименование,
			               |	Автотранспорт.Модель КАК Модель,
			               |	Автотранспорт.ГосНомер КАК ГосНомер,
			               |	Автотранспорт.VIN КАК ВинКодАвтомобиля,
			               |	Автотранспорт.ГодВыпуска КАК ГодВыпускаАвтомобиля,
			               |	Автотранспорт.Высота КАК ВысотаАвтомобиля,
			               |	Автотранспорт.Длина КАК ДлинаАвтомобиля,
			               |	Автотранспорт.Ширина КАК ШиринаАвтомобиля,
			               |	Автотранспорт.Вес КАК ВесАвтомобиля,
			               |	Автотранспорт.Грузоподъемность + Автотранспорт.Вес КАК ВесАвтомобиляСГрузом
			               |ИЗ
			               |	Справочник.Автотранспорт КАК Автотранспорт
			               |ГДЕ
			               |	Автотранспорт.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", Автомобиль);
			
			ВыборкаАвто = Запрос.Выполнить().Выбрать();
			Если ВыборкаАвто.Следующий() Тогда 
				СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", СокрЛП(ВыборкаАвто.Модель) + ?(ЗначениеЗаполнено(ВыборкаАвто.ГосНомер), " №" + СокрЛП(ВыборкаАвто.ГосНомер), ""));
				
				Для Каждого Реквизит Из РеквизитыАвтомобиля Цикл 
					Значение = ВыборкаАвто[Реквизит.Ключ];
					Если ЗначениеЗаполнено(Значение) Тогда 
						РеквизитыАвтомобиля.Вставить(Реквизит.Ключ, Значение);
					КонецЕсли;
				КонецЦикла;
			Иначе
				СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", Автомобиль);			
			КонецЕсли;
		Иначе
			СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", Автомобиль);		
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, РеквизитыАвтомобиля, Истина);
		
		
		СтруктураРеквизитов.Вставить("ГосНомерПрицепа",  ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_Прицеп", "немає"));
		
		Перевозчик 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_Перевозчик", "");
		ТипРеквизитаПеревозчик = ТипЗнч(Перевозчик);
		СтруктураРеквизитов.Вставить("ОрганизацияПеревозчик", Перевозчик);
		
		ИННПеревозчика 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_ИННПеревозчика", "");
		Если Не ЗначениеЗаполнено(ИННПеревозчика) И ТипРеквизитаПеревозчик <> Тип("Строка") Тогда 
			Если ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Организации") Тогда 
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				               |	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация,
				               |	УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС КАК ИННПлательщикаНДС
				               |ИЗ
				               |	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Дата, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
				Запрос.УстановитьПараметр("Дата", ВыборкаДокументы.Дата);
				Запрос.УстановитьПараметр("Организация", Перевозчик);
				
				ВыборкаИНН = Запрос.Выполнить().Выбрать();        
				Если ВыборкаИНН.Следующий() Тогда 
					ИННПеревозчика = ВыборкаИНН.ИННПлательщикаНДС;	
				КонецЕсли;
			ИначеЕсли ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Контрагенты") Тогда 
				ИННПеревозчика = Перевозчик.ИННПлательщикаНДС;
			КонецЕсли;
		КонецЕсли;		
		СтруктураРеквизитов.Вставить("ИННПеревозчика", ИННПеревозчика);
		
		Водитель = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_Водитель", "");
		НомерУдостоверения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "РТ_НомерПрав", "");
				
		СтруктураРеквизитов.Вставить("Водитель", Водитель);
		СтруктураРеквизитов.Вставить("ВодительСНП", СокрЛП(Водитель) + ?(ЗначениеЗаполнено(НомерУдостоверения), ", " + СокрЛП(НомерУдостоверения), ""));				
		
		мТовары  = ТаблицаТовары.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументы.Ссылка));
		тзТовары = ТаблицаТовары.Скопировать(мТовары);
		
		Количество = тзТовары.Итог("Количество");
		МассаТонн = тзТовары.Итог("Масса") / 1000;
		СуммаВсего = тзТовары.Итог("СуммаВсего");
		СуммаНДС = тзТовары.Итог("СуммаНДС");
		
		СтруктураРеквизитов.Вставить("КоличествоМестПрописью", ЧислоПрописью(Число(Количество), "Л=uk_UA; ДП=Истина", ",,,с,,,,,0"));
		СтруктураРеквизитов.Вставить("МассаПрописью", ЧислоПрописью(Окр(МассаТонн, 2),"Л=uk_UA; ДП=Истина", "ціла, цілих, цілих, ж, тисячних, тисячних, тисячних, м, 3"));
		СтруктураРеквизитов.Вставить("ИтогоМасса", Формат(МассаТонн, "ЧДЦ=3"));
		СтруктураРеквизитов.Вставить("ОтпущеноНаСуммуПрописью", ЧислоПрописью(СуммаВсего, "Л=uk_UA; ДП = Истина", НСтр("ru = 'гривна, гривны, гривен, м, копейка, копейки, копеек, ж, 2'; uk = 'гривна, гривні, гривень, ж, копійка, копійки, копійок, ж, 2'", КодЯзыкаПечать)));//Константы.ВалютаРегламентированногоУчета.Получить().ПараметрыПрописи));
		СтруктураРеквизитов.Вставить("СуммаНДС", Окр(СуммаНДС,2));
		СтруктураРеквизитов.Вставить("ИтогоКоличество", Количество);
		СтруктураРеквизитов.Вставить("ИтогоСуммаСНДС", СуммаВсего);		
		
		ДокументыСопровождения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Расходная накладная №%1, Декларация производителя №%1'; uk = 'Видаткова накладна №%1, Декларація виробника №%1'", КодЯзыкаПечать),
									ВыборкаДокументы.Номер);
									
		ДокументыСопровожденияКратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Вид нак №%1 від %2 р., декларація виробника/посв про якість №%1 від %2 р.'; uk = 'Вид нак №%1 від %2 р., декларація виробника/посв про якість №%1 від %2 р.'", КодЯзыкаПечать),
									ВыборкаДокументы.Номер,
									Формат(ВыборкаДокументы.Дата, "ДФ=dd.MM.yy"));
									
		СтруктураРеквизитов.Вставить("ДокументыСопровождения", ДокументыСопровождения);

		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				

		ОбластьШапка.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		Ит = 0;
		Для Каждого стрТовар ИЗ тзТовары Цикл 
			Ит = Ит + 1;
						
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", Ит);
			СтруктураСтроки.Вставить("ТоварНаименование", ?(ЗначениеЗаполнено(стрТовар.НоменклатураНаименованиеПолное), стрТовар.НоменклатураНаименованиеПолное, стрТовар.Номенклатура));
			СтруктураСтроки.Вставить("БазоваяЕдиницаНаименование", стрТовар.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("Количество", стрТовар.Количество);
			СтруктураСтроки.Вставить("Цена", стрТовар.ЦенаБезНалогов);
			СтруктураСтроки.Вставить("Сумма", стрТовар.СуммаВсего);
			СтруктураСтроки.Вставить("ВидУпаковки", стрТовар.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("ДокументыСопровождения", ДокументыСопровожденияКратко); 
			СтруктураСтроки.Вставить("Масса", стрТовар.Масса);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ОбластьВсего.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьВсего);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		ОбластьАвтотранспорт.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьАвтотранспорт);		
		
		ОбластьПогрузочныеОперации.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьПогрузочныеОперации);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);		
			
		ПервыйДокумент = Ложь;
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецОбласти	


#Область СлужебныеПроцедурыИФункции

Функция СтруктураДокумента(ДокументСсылка,ТолькоСтруктуру)Экспорт
	
	СтруктураПоказателей = Новый Структура();
	СтруктураПоказателейТовары = Новый Структура();
	СтруктураПоказателей.Вставить("Акт",   	Новый Структура());	// данные налоговой накладной
	СтруктураПоказателейТовары.Вставить("TAB",   	Новый ТаблицаЗначений());  // данные табличной части
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A1");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("NAME");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A3");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("EI");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("CODEI");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A5");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A4");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("A6");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("SUMPDV");
	СтруктураПоказателейТовары.TAB.Колонки.Добавить("STAVKAPDV");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваров.Ссылка КАК Ссылка,
		|	РеализацияТоваров.Номер КАК Номер,
		|	РеализацияТоваров.Дата КАК Дата,
		|	РеализацияТоваров.Организация КАК Организация,
		|	РеализацияТоваров.Автор КАК Автор,
		|	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
		|	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	РеализацияТоваров.Контрагент КАК Контрагент
		|ИЗ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ГДЕ
		|	РеализацияТоваров.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровТовары.Ссылка КАК Ссылка,
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	РеализацияТоваровТовары.Количество КАК Количество,
		|	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	РеализацияТоваровТовары.Цена КАК Цена,
		|	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
		|	РеализацияТоваровТовары.СуммаСкидки КАК СуммаСкидки,
		|	РеализацияТоваровТовары.Номенклатура.Наименование2 КАК НоменклатураНаименование2,
		|	РеализацияТоваровТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	РеализацияТоваровТовары.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
		|	РеализацияТоваровТовары.Содержание КАК Содержание,
		|	РеализацияТоваровТовары.СуммаВсего - РеализацияТоваровТовары.СуммаНДС КАК СуммаБезПДВ
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &ДокументСсылка";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Украина", Справочники.СтраныМира.НайтиПоКоду("804"));

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ШапкаДокумента = РезультатЗапроса[0].Выбрать();
	ШапкаДокумента.Следующий();
	
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	
	РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация,"Услуги");
	Если НЕ ЗначениеЗаполнено(РеквизитыОрганизации.НомерСчета)  Тогда
		РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация,"Текущий");
	КонецЕсли; 

	РеквизитыКонтрагента = Справочники.Контрагенты.ПолучитьРеквизитыКонтрагента(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Контрагент);
	
	тДата = Формат(ТекущаяДатаСеанса(),"ДФ=dd.MM.yyyy");
	ДокОснование = Документы.СчетНаОплату.НайтиПоРеквизиту("ДокументОснование",ДокументСсылка);
	
	СтруктураПоказателей.Акт.Вставить("NUM",				ШапкаДокумента.Номер);
	СтруктураПоказателей.Акт.Вставить("DOCDATE",			Формат(ШапкаДокумента.Дата,"ДФ=dd.MM.yyyy"));
	СтруктураПоказателей.Акт.Вставить("DATA1",				Формат(ШапкаДокумента.Дата,"ДФ=dd.MM.yyyy"));
	СтруктураПоказателей.Акт.Вставить("DATA2",				Формат(ШапкаДокумента.Дата,"ДФ=dd.MM.yyyy"));

	СтруктураПоказателей.Акт.Вставить("FIRM_NAME",			СокрЛП(РеквизитыОрганизации.НаименованиеПолное));
	СтруктураПоказателей.Акт.Вставить("FIRM_RS",			СокрЛП(РеквизитыОрганизации.НомерСчета));
	СтруктураПоказателей.Акт.Вставить("FIRM_NMBANK",		СокрЛП(РеквизитыОрганизации.НаименованиеБанка));
	СтруктураПоказателей.Акт.Вставить("FIRM_CBANK",			СокрЛП(РеквизитыОрганизации.КодБанка));
	СтруктураПоказателей.Акт.Вставить("FIRM_ADR",			СокрЛП(РеквизитыОрганизации.Адрес));
	СтруктураПоказателей.Акт.Вставить("FIRM_TELEFON",		"");
	СтруктураПоказателей.Акт.Вставить("TEXT_FIRM_E_D",		"Код за ЄДРПОУ");
	СтруктураПоказателей.Акт.Вставить("FIRM_EDRPOU",		СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));
	СтруктураПоказателей.Акт.Вставить("FIRM_INN",			СокрЛП(РеквизитыОрганизации.ИНН));
	СтруктураПоказателей.Акт.Вставить("FIRM_DEPT",			"");
	СтруктураПоказателей.Акт.Вставить("FIRM_RUKPOS",		"Директор");
	СтруктураПоказателей.Акт.Вставить("FIRM_RUK",			РеквизитыОрганизации.Руководитель);
	СтруктураПоказателей.Акт.Вставить("TAXSYSTEM",			"");

	СтруктураПоказателей.Акт.Вставить("SIDE_DIR_POS",		"Директор");
	СтруктураПоказателей.Акт.Вставить("TEXT_SIDE_E_D",		"Код за ЄДРПОУ");
	СтруктураПоказателей.Акт.Вставить("SIDE_DIR_FIO",		"");
	СтруктураПоказателей.Акт.Вставить("SIDE_OTV_FIO",		"");
	СтруктураПоказателей.Акт.Вставить("SIDE_BANK_K",		СокрЛП(РеквизитыКонтрагента.НаименованиеБанка));
	СтруктураПоказателей.Акт.Вставить("SIDE_CDADR_K",		СокрЛП(РеквизитыКонтрагента.Адрес));
	СтруктураПоказателей.Акт.Вставить("SIDE_CDINDTAXNUM_K",	СокрЛП(РеквизитыКонтрагента.ИНН));
	СтруктураПоказателей.Акт.Вставить("SIDE_CDSHR_K",		СокрЛП(РеквизитыКонтрагента.НомерСчета));
	СтруктураПоказателей.Акт.Вставить("SIDE_MFO_K",			СокрЛП(РеквизитыКонтрагента.КодБанка));
	СтруктураПоказателей.Акт.Вставить("SIDE_CD_K",			СокрЛП(РеквизитыКонтрагента.НаименованиеПолное));
	СтруктураПоказателей.Акт.Вставить("SIDE_TEL_K",			"");
	СтруктураПоказателей.Акт.Вставить("SIDE_EDRPOU_K",		СокрЛП(РеквизитыКонтрагента.КодПоЕДРПОУ));
	СтруктураПоказателей.Акт.Вставить("SIDE_DEPT_K",		СокрЛП(РеквизитыКонтрагента.КодФилиала));
	
	СтруктураПоказателей.Акт.Вставить("DOG_DATA_T",			"від");
	СтруктураПоказателей.Акт.Вставить("DOG_NUM",			СокрЛП(ШапкаДокумента.ДоговорВзаиморасчетов.Номер));
	СтруктураПоказателей.Акт.Вставить("DOG_NUM_T",			"№");
	СтруктураПоказателей.Акт.Вставить("DOG_NAME",			"№");
	СтруктураПоказателей.Акт.Вставить("DOG_DATE",			Формат(ШапкаДокумента.ДоговорВзаиморасчетов.Дата, "ДФ=dd.MM.yyyy"));
	
	СтруктураПоказателей.Акт.Вставить("OSN_T",				"Розр. док");
	СтруктураПоказателей.Акт.Вставить("OSN_TIP",			"Рахунок на оплату покупцю");
	СтруктураПоказателей.Акт.Вставить("OSN_NUM_T",			"№");
	СтруктураПоказателей.Акт.Вставить("OSN_NOMER",			ДокОснование.Номер);
	СтруктураПоказателей.Акт.Вставить("OSN_DATA_T",			"від");
	СтруктураПоказателей.Акт.Вставить("OSN_DATA",			Формат(ДокОснование.Дата, "ДФ=dd.MM.yyyy"));
	
	СуммаБезПДВ = ТаблицаТовары.Итог("СуммаБезПДВ");
	СуммаНДС = ТаблицаТовары.Итог("СуммаНДС");
	СтруктураПоказателей.Акт.Вставить("DOCSUM",				Формат(ШапкаДокумента.СуммаДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("MISZE_SKL",			"м. Харків");
	СтруктураПоказателей.Акт.Вставить("ZNYGKA", 			ТаблицаТовары.Итог("СуммаСкидки"));
	СтруктураПоказателей.Акт.Вставить("SUMPDV",				Формат(СуммаНДС,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("DOCSUM_TEXT",		Формат(ШапкаДокумента.СуммаДокумента,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("SUMPDV_TEXT",		Формат(СуммаНДС,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("SUMWITHOUTPDV",		Формат(СуммаБезПДВ,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("SUMWITHOUTPDV_TEXT",	Формат(СуммаБезПДВ,"ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ="));
	СтруктураПоказателей.Акт.Вставить("VO_NAME",			СокрЛП(РеквизитыОрганизации.Руководитель));
	
	Для каждого СтрокаТаблицы Из ТаблицаТовары Цикл
		
		СтрокаСтруктуры = СтруктураПоказателейТовары.TAB.Добавить();
		
		СтрокаСтруктуры.NAME  = ?(Не ПустаяСтрока(СтрокаТаблицы.Содержание), СтрокаТаблицы.Содержание, СтрокаТаблицы.НоменклатураНаименование2);
		СтрокаСтруктуры.A3    = Формат(СтрокаТаблицы.Количество,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.EI    = СокрЛП("послуга");
		СтрокаСтруктуры.CODEI = СокрЛП("-");
		СтрокаСтруктуры.A5 	  = Формат(СтрокаТаблицы.Цена,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.A4 	  = Формат(СтрокаТаблицы.СуммаСкидки,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.A6 	  = Формат(СтрокаТаблицы.СуммаВсего,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.A6 	  = Формат(СтрокаТаблицы.СуммаВсего,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.SUMPDV = Формат(СтрокаТаблицы.СуммаНДС,"ЧЦ=27; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.STAVKAPDV = "20";
	
	КонецЦикла;

	Если ТолькоСтруктуру Тогда
		Возврат СтруктураПоказателей;
	КонецЕсли;
	
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.УстановитьСтроку("windows-1251");
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	
	///ZVIT	
	ОбъектXML.ЗаписатьНачалоЭлемента("ZVIT");		
	
	///TRANSPORT	
	ОбъектXML.ЗаписатьНачалоЭлемента("TRANSPORT");		
	ЗаписатьXML(ОбъектXML, "4.1", "VERSION");
	ЗаписатьXML(ОбъектXML, тДата, "CREATEDATE");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///TRANSPORT	
	
	///ORG	
	ОбъектXML.ЗаписатьНачалоЭлемента("ORG");		
	
	///FIELDS	
	ОбъектXML.ЗаписатьНачалоЭлемента("FIELDS");
	ЗаписатьXML(ОбъектXML, СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ), "EDRPOU");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///FIELDS	
	
	//CARD
	ОбъектXML.ЗаписатьНачалоЭлемента("CARD");
	ОбъектXML.ЗаписатьАтрибут("RTFDOC","1");
	
	///FIELDS	
	ОбъектXML.ЗаписатьНачалоЭлемента("FIELDS");
	ЗаписатьXML(ОбъектXML, "Акт (ціна з ПДВ)", "DOCNAME");
	ЗаписатьXML(ОбъектXML, "1С82АВР",			   "CHARCODE");
	ЗаписатьXML(ОбъектXML, "7",					   "PARTCODE");
	ЗаписатьXML(ОбъектXML, "", 					   "NOTATION");
	ЗаписатьXML(ОбъектXML, "10104", 			   "SDOCTYPE");
	ЗаписатьXML(ОбъектXML, тДата, 			  	   "CRTDATE");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///FIELDS	
	
	//DOCUMENT
	ОбъектXML.ЗаписатьНачалоЭлемента("DOCUMENT");
	
	Для Каждого Параметр Из СтруктураПоказателей.Акт Цикл
		
		ОбъектXML.ЗаписатьНачалоЭлемента("ROW");											
		ОбъектXML.ЗаписатьАтрибут("LINE", "0");
		ОбъектXML.ЗаписатьАтрибут("TAB", "0");
		ОбъектXML.ЗаписатьАтрибут("NAME", Параметр.Ключ);
		ЗаписатьXML(ОбъектXML,Параметр.Значение, "VALUE");	
		ОбъектXML.ЗаписатьКонецЭлемента();

	КонецЦикла; 

	СоответствиеКолонокТовары = Новый Соответствие;
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_NAME", 				"NAME");
	СоответствиеКолонокТовары.Вставить("TAB1_A3", 								"A3");
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_CHARCODEUMEASURE", 	"EI");
	СоответствиеКолонокТовары.Вставить("TAB1_NOMENKLATURA_NUMHBUMEASURE", 		"CODEI");
	СоответствиеКолонокТовары.Вставить("TAB1_A5", 								"A5");
	СоответствиеКолонокТовары.Вставить("TAB1_A4",								"A4");	
	СоответствиеКолонокТовары.Вставить("TAB1_A6",								"A6");	
	СоответствиеКолонокТовары.Вставить("TAB1_SUMPDV",							"SUMPDV");	
	СоответствиеКолонокТовары.Вставить("TAB1_STAVKAPDV",						"STAVKAPDV");	
	
	Для Каждого Параметр Из СоответствиеКолонокТовары Цикл
		Номер = 0;
		Для Каждого Стр Из СтруктураПоказателейТовары.TAB Цикл					
			Значение = Неопределено;
			
			ОбъектXML.ЗаписатьНачалоЭлемента("ROW");											
			ОбъектXML.ЗаписатьАтрибут("LINE", Строка(Номер));
			ОбъектXML.ЗаписатьАтрибут("TAB", "1");
			ОбъектXML.ЗаписатьАтрибут("NAME", Параметр.Ключ);
			ЗаписатьXML(ОбъектXML,Стр[Параметр.Значение], "VALUE");	
			ОбъектXML.ЗаписатьКонецЭлемента();
			
			Номер = Номер + 1;

		КонецЦикла;
	КонецЦикла;		
	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DOCUMENT	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///CARD	
	ОбъектXML.ЗаписатьКонецЭлемента(); //ORG
	ОбъектXML.ЗаписатьКонецЭлемента(); //ZVIT

	СтрXML = ОбъектXML.Закрыть();	
	
	
	ПотокДанных = Новый ПотокВПамяти();
	
	ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных,"windows-1251");
	ЗаписьДанных.ЗаписатьСтроку(СтрXML,"windows-1251");
	ЗаписьДанных.Закрыть();
	
	ДВ = ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные();
	СтруктураПоказателей.Вставить("ДанныеXML",ДВ);

	СтруктураПоказателей.Вставить("ТекстXML",СтрXML);
	СтруктураПоказателей.Вставить("ИмяФайла",СокрЛП(Строка(ШапкаДокумента.Организация)) + "_" + Формат(ШапкаДокумента.Дата, "ДФ=yyyyMMdd")+"_"+ШапкаДокумента.Номер+"_АКТ.xml"); 
	
	Возврат СтруктураПоказателей;

КонецФункции

Функция СформироватьАкт(МассивДокументов,ТолькоСтруктуру=Ложь)Экспорт
	
	МассивВозврата = Новый Массив;
	
	
	Для Индекс = 0 ПО МассивДокументов.ВГраница() Цикл
		
		Документ = МассивДокументов[Индекс];
		
		СтруктруаДокумента = СтруктураДокумента(Документ,ТолькоСтруктуру);
		МассивВозврата.Добавить(Новый Структура("Документ,Выгрузка",Документ,СтруктруаДокумента));
		
	КонецЦикла;
	
	
	Возврат МассивВозврата;
	
КонецФункции

#КонецОбласти




#КонецЕсли
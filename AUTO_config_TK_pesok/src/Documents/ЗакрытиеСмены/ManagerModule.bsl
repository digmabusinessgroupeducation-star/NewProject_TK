#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Проведение
	
	Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
		
		ИсточникиДанных = Новый Соответствие;
		
		Возврат ИсточникиДанных; 
		
	КонецФункции
	
	Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
		
		ИмяРегистра = "ВтТаблицаТовары";
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ЗакрытиеСменыТовары.НомерСтроки КАК НомерСтроки,
		               |	ЗакрытиеСменыТовары.Номенклатура КАК Номенклатура,
		               |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ЗакрытиеСменыТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		               |	ЗакрытиеСменыТовары.Коэффициент КАК Коэффициент,
		               |	ЗакрытиеСменыТовары.КоличествоБазовое КАК Количество,
		               |	ЗакрытиеСменыТовары.Цена КАК Цена,
		               |	ЗакрытиеСменыТовары.Сумма КАК Сумма,
		               |	ЗакрытиеСменыТовары.СтавкаНДС КАК СтавкаНДС,
		               |	ЗакрытиеСменыТовары.СуммаНДС КАК СуммаНДС,
		               |	ЗакрытиеСменыТовары.СуммаАкцизногоНалога КАК СуммаАкцизногоНалога,
		               |	ЗакрытиеСменыТовары.СуммаВсего КАК СуммаВсего,
		               |	ЗакрытиеСменыТовары.СуммаСкидки КАК СуммаСкидки,
		               |	ЗакрытиеСменыТовары.ПроцентСкидки КАК ПроцентСкидки,
		               |	ЗакрытиеСменыТовары.Возврат КАК Возврат,
		               |	ЗакрытиеСменыТовары.Ссылка.ХозОперация КАК ХозОперация,
		               |	ЗакрытиеСменыТовары.Ссылка.СкладКомпании КАК СкладКомпании,
		               |	ЗакрытиеСменыТовары.КлассификаторПродажи КАК КлассификаторПродажи,
		               |	ВЫБОР
		               |		КОГДА ЗакрытиеСменыТовары.КлассификаторПродажи = ЗНАЧЕНИЕ(Справочник.КлассификаторПродаж.ПустаяСсылка)
		               |			ТОГДА 100000
		               |		ИНАЧЕ ЗакрытиеСменыТовары.КлассификаторПродажи.Код
		               |	КОНЕЦ КАК Порядок
		               |ПОМЕСТИТЬ ВтТаблицаТовары
		               |ИЗ
		               |	Документ.ЗакрытиеСмены.Товары КАК ЗакрытиеСменыТовары
		               |ГДЕ
		               |	ЗакрытиеСменыТовары.Ссылка = &Ссылка
		               |	И ЗакрытиеСменыТовары.Номенклатура <> &НоменклатураВыигрыш";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ОстаткиТоваровКомпании";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
		|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ВтТаблицаТовары.Количество) КАК Количество,
		|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаТовары.ХозОперация,
		|	ВтТаблицаТовары.СкладКомпании,
		|	ВтТаблицаТовары.Номенклатура,
		|	ВтТаблицаТовары.ХарактеристикаНоменклатуры"; 
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос = Новый Запрос;
		
		//МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		//Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Реквизиты = ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		ДополнитьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства,Реквизиты);
		
		ТекстыЗапроса = Новый СписокЗначений;
		
		Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
			ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
			ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваров(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
			
			
		Иначе
			Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
				ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
			КонецЕсли; 
			
			Если ДополнительныеСвойства.ПроводитьПоОстаткам Тогда
				
				ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
				
			КонецЕсли;
			
			
			
			ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
			
			Запрос.Текст = "";
			
			
			ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваров(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
		КонецЕсли;
		
		
	КонецПроцедуры
	
	Функция ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	ДанныеДокумента.ХозОперация КАК ХозОперация,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
		|	ДанныеДокумента.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ДанныеДокумента.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	ДанныеДокумента.СкладКомпании КАК СкладКомпании,
		|	ДанныеДокумента.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
		|	ДанныеДокумента.КурсДокумента КАК КурсДокумента,
		|	ДанныеДокумента.РегламентированныйУчет КАК РегламентированныйУчет,
		|	ДанныеДокумента.СписыватьОстатки КАК СписыватьОстатки
		|ИЗ
		|	Документ.ЗакрытиеСмены КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка";
		
		Реквизиты = Запрос.Выполнить().Выбрать();
		Реквизиты.Следующий();
		
		Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
		Запрос.УстановитьПараметр("МоментВремени",                            Новый МоментВремени(Реквизиты.Период,ДокументСсылка));
		
		Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
		Запрос.УстановитьПараметр("НачалоМесяцаПериода",                      НачалоМесяца(Реквизиты.Период));
		Запрос.УстановитьПараметр("Валюта",                                   Реквизиты.Валюта);
		Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
		Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
		Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);
		Запрос.УстановитьПараметр("ТипЦенСебестоимости",                      Реквизиты.ТипЦенСебестоимости);
		Запрос.УстановитьПараметр("НоменклатураВыигрыш",                      Справочники.Номенклатура.НоменклатураВыигрышЛотерея());

		Возврат Реквизиты;	
		
	КонецФункции
	
	Процедура ДополнитьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства,Реквизиты)
		
		ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
		ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании",Реквизиты.СкладКомпании);
		ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент",Справочники.Контрагенты.ЧастноеЛицо);
		ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
		ДополнительныеСвойства.ДляПроведения.Вставить("Валюта",Реквизиты.Валюта);
		ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента",Реквизиты.КурсДокумента);
		ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
		ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
		ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
		ДополнительныеСвойства.Вставить("ПроводитьПоОстаткам",Реквизиты.СписыватьОстатки);
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	
	
	
	
	
	#Область Последовательность
	
	Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
		
		ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров");	
		
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
		
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор   = Ссылка;
		НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
		НоваяСтрока.Период   	  = Ссылка.Дата;
		ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
		
		
		
		ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ПрограммныйИнтерфейс
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(ПодразделениеКомпании)
		|	И ЗначениеРазрешено(ТорговаяПлощадка)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	
	#КонецОбласти
	
	
	
#КонецЕсли

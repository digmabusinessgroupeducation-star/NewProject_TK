
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Начало СтандартныеПодсистемы.УправлениеДоступом
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЗагрузкаБАТ", Ложь) Тогда 
		СопоставитьЗагружаемыеДанныеБАТ(Товары, ЗагружаемыеДанные, АдресТаблицыСопоставления, СписокНеоднозначностей, ДополнительныеПараметры);		
	Иначе
		СопоставитьЗагружаемыеДанныеУниверсально(Товары, ЗагружаемыеДанные, АдресТаблицыСопоставления, СписокНеоднозначностей);				
	КонецЕсли;	
		
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	ЗагрузкаБАТ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЗагрузкаБАТ", Ложь);
	
	Если ЗагрузкаБАТ И ИмяКолонки = "Номенклатура" Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДополнительныеСведения.Объект КАК Ссылка
		               |ПОМЕСТИТЬ ВТ
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Свойство.Имя = ""КодБАТ""
		               |	И ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
		               |	И ДополнительныеСведения.Значение = &КодБАТ
		               |	И &ИскатьПоКодуБАТ
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ДополнительныеСведения.Объект
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Свойство.Имя = ""КодТедис""
		               |	И ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
		               |	И ДополнительныеСведения.Значение = &КодТедис
		               |	И &ИскатьПоКодуТедис
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ВТ.Ссылка КАК Ссылка
		               |ИЗ
		               |	ВТ КАК ВТ";
		
		Запрос.УстановитьПараметр("КодБАТ", СокрЛП(ЗагружаемыеЗначенияСтрока.КодБат));
		Запрос.УстановитьПараметр("ИскатьПоКодуБАТ", Не ПустаяСтрока(СокрЛП(ЗагружаемыеЗначенияСтрока.КодБат)));
		
		Запрос.УстановитьПараметр("КодТедис", СокрЛП(ЗагружаемыеЗначенияСтрока.КодТедис));
		Запрос.УстановитьПараметр("ИскатьПоКодуТедис", Не ПустаяСтрока(СокрЛП(ЗагружаемыеЗначенияСтрока.КодТедис)));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
	ИначеЕсли ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;	
		
	ИначеЕсли ИмяКолонки = "Область" Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТК_Области.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.ТК_Области КАК ТК_Области
		               |ГДЕ
		               |	ТК_Области.Наименование = &Наименование
		               |	И НЕ ТК_Области.ПометкаУдаления";
		
		Запрос.УстановитьПараметр(СокрЛП(ЗагружаемыеЗначенияСтрока.Область));
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура СопоставитьЗагружаемыеДанныеУниверсально(Товары, ЗагружаемыеДанные, АдресТаблицыСопоставления, СписокНеоднозначностей)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_Области.Ссылка КАК Ссылка,
	               |	ТК_Области.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.ТК_Области КАК ТК_Области
	               |ГДЕ
	               |	НЕ ТК_Области.ПометкаУдаления
	               |	И ТК_Области.Наименование В(&МассивОбластей)";

	СписокОбластей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗагружаемыеДанные.ВыгрузитьКолонку("Область"));			
	МассивОбластей = Новый Массив;
	Для Каждого Стр Из СписокОбластей Цикл 
		текОбласть = СокрЛП(Стр);
		Если Не ПустаяСтрока(текОбласть) Тогда 
			МассивОбластей.Добавить(текОбласть);
		КонецЕсли;
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	Запрос.УстановитьПараметр("МассивОбластей", МассивОбластей);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаНоменклатура = МассивРезультатов[6].Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
	
	ТаблицаОбластей = МассивРезультатов[7].Выгрузить();
	ТаблицаОбластей.Индексы.Добавить("Наименование");
		
	СписокКвот = Перечисления.ТК_ВидыКвот.ПолучитьДанныеВыбора(Новый Структура);	
	КэшВидыКвот = Новый Соответствие;
	Для Каждого Стр Из СписокКвот Цикл 
		КэшВидыКвот.Вставить(ВРег(Стр.Представление), Стр.Значение.Значение);
	КонецЦикла;
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Порог = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаТаблицы.Порог);
		Товар.Процент = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаТаблицы.Процент);		
		Товар.ВидКвоты = КэшВидыКвот.Получить(ВРег(СокрЛП(СтрокаТаблицы.ВидКвоты)));
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
		нОбласти = ТаблицаОбластей.НайтиСтроки(Новый Структура("Наименование", СокрЛП(СтрокаТаблицы.Область)));
		Если нОбласти.Количество() = 1 Тогда 
			
			Товар.Область = нОбласти[0].Ссылка;			
			
		ИначеЕсли нОбласти.Количество() > 1 Тогда 
			
			ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
			ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
			ЗаписьОНеоднозначности.Колонка = "Область";
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

Процедура СопоставитьЗагружаемыеДанныеБАТ(Товары, ЗагружаемыеДанные, АдресТаблицыСопоставления, СписокНеоднозначностей, ДополнительныеПараметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.КодБАТ КАК СТРОКА(20)) КАК КодБАТ,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.КодТедис КАК СТРОКА(20)) КАК КодТедис,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураКодБАТ
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ПО (ДанныеДляСопоставления.КодБАТ <> """")
	               |			И (ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура)
	               |			И (ДополнительныеСведения.Свойство.Имя = ""КодБАТ"")
	               |			И (ДанныеДляСопоставления.КодБАТ = (ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК СТРОКА(20))))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.КодТедис КАК КодТедис,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияТедис
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	ДанныеДляСопоставления.КодТедис <> """"
	               |	И НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ
	               |					СопоставленнаяНоменклатураКодБАТ.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураКодБАТ КАК СопоставленнаяНоменклатураКодБАТ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Номенклатура,
	               |	ДанныеДляСопоставленияТедис.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураТедис
	               |ИЗ
	               |	ДанныеДляСопоставленияТедис КАК ДанныеДляСопоставленияТедис
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ПО (ДанныеДляСопоставленияТедис.КодТедис <> """")
	               |			И (ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура)
	               |			И (ДополнительныеСведения.Свойство.Имя = ""КодТедис"")
	               |			И (ДанныеДляСопоставленияТедис.КодТедис = (ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК СТРОКА(20))))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(СопоставленнаяНоменклатураКодБАТ.Номенклатура) КАК Номенклатура,
	               |	СопоставленнаяНоменклатураКодБАТ.Идентификатор КАК Идентификатор,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураКодБАТ.Идентификатор) КАК Количество,
	               |	""БАТ"" КАК ПолеПоиска
	               |ИЗ
	               |	СопоставленнаяНоменклатураКодБАТ КАК СопоставленнаяНоменклатураКодБАТ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураКодБАТ.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	МАКСИМУМ(СопоставленнаяНоменклатураТедис.Номенклатура),
	               |	СопоставленнаяНоменклатураТедис.Идентификатор,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураТедис.Идентификатор),
	               |	""Тедис""
	               |ИЗ
	               |	СопоставленнаяНоменклатураТедис КАК СопоставленнаяНоменклатураТедис
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураТедис.Идентификатор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_Области.Ссылка КАК Ссылка,
	               |	ТК_Области.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.ТК_Области КАК ТК_Области
	               |ГДЕ
	               |	НЕ ТК_Области.ПометкаУдаления
	               |	И ТК_Области.Наименование В(&МассивОбластей)";

	СписокОбластей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ЗагружаемыеДанные.ВыгрузитьКолонку("Область"));			
	МассивОбластей = Новый Массив;
	Для Каждого Стр Из СписокОбластей Цикл 
		текОбласть = СокрЛП(Стр);
		Если Не ПустаяСтрока(текОбласть) Тогда 
			МассивОбластей.Добавить(текОбласть);
		КонецЕсли;
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	Запрос.УстановитьПараметр("МассивОбластей", МассивОбластей);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаНоменклатура = МассивРезультатов[4].Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
	
	ТаблицаОбластей = МассивРезультатов[5].Выгрузить();
	ТаблицаОбластей.Индексы.Добавить("Наименование");
	
	СписокВидовКвот = Новый Структура;
	СписокВидовКвот.Вставить("RE", ПредопределенноеЗначение("Перечисление.ТК_ВидыКвот.RE"));
	СписокВидовКвот.Вставить("RI", ПредопределенноеЗначение("Перечисление.ТК_ВидыКвот.RI"));
	СписокВидовКвот.Вставить("EX", ПредопределенноеЗначение("Перечисление.ТК_ВидыКвот.EX"));
	СписокВидовКвот.Вставить("SE", ПредопределенноеЗначение("Перечисление.ТК_ВидыКвот.SE"));
	СписокВидовКвот.Вставить("SR", ПредопределенноеЗначение("Перечисление.ТК_ВидыКвот.SR"));
	
	СписокКвот = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Номенклатура;		
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
		нОбласти = ТаблицаОбластей.НайтиСтроки(Новый Структура("Наименование", СокрЛП(СтрокаТаблицы.Область)));
		Если нОбласти.Количество() = 1 Тогда 
			
			Товар.Область = нОбласти[0].Ссылка;			
			
		ИначеЕсли нОбласти.Количество() > 1 Тогда 
			
			ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
			ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
			ЗаписьОНеоднозначности.Колонка = "Область";
			
		КонецЕсли;
				
		//Заполним список квот		
		МассивКвот = Новый Массив;
		
		Для Каждого Квота Из СписокВидовКвот Цикл
			стрПорог = "Порог_" + Квота.Ключ;
			стрПроцент = "Процент_" + Квота.Ключ;
			текСтрока = Новый Структура(стрПорог + "," + стрПроцент, 0, 0);			
			ЗаполнитьЗначенияСвойств(текСтрока, СтрокаТаблицы);
			
			текПопрог = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(текСтрока[стрПорог]);
			текПроцент = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(текСтрока[стрПроцент]);
			
			СтруктураКвоты = Новый Структура;			
			Если ЗначениеЗаполнено(текПопрог) ИЛИ ЗначениеЗаполнено(текПроцент) Тогда 
				СтруктураКвоты.Вставить("Порог", текПопрог);
				СтруктураКвоты.Вставить("Процент", текПроцент);
				СтруктураКвоты.Вставить("ВидКвоты", Квота.Значение);
				
				МассивКвот.Добавить(СтруктураКвоты);
			КонецЕсли;
		КонецЦикла;		
		
		Если МассивКвот.Количество() > 0 Тогда 
			СписокКвот.Вставить(СтрокаТаблицы.Идентификатор, МассивКвот);
		КонецЕсли;
	КонецЦикла;
	
	АдресДопДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "АдресДопДанных", "");
	Если Не ПустаяСтрока(АдресДопДанных) Тогда 
		ПоместитьВоВременноеХранилище(Новый Структура("СписокКвот", СписокКвот), АдресДопДанных);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла



#КонецОбласти	



#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;		
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	            	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаТК_КвотыНаОтгрузку(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТК_УстановкаКвотНаОтгрузку.Дата КАК Период,
	|	ТК_УстановкаКвотНаОтгрузку.Ссылка КАК Ссылка,
	|	ТК_УстановкаКвотНаОтгрузку.ХозОперация КАК ХозОперация,
	|	ТК_УстановкаКвотНаОтгрузку.Организация КАК Организация,
	|	ТК_УстановкаКвотНаОтгрузку.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ТК_УстановкаКвотНаОтгрузку.ДатаНачалаДействия КАК ДатаНачалаДействия
	|ИЗ
	|	Документ.ТК_УстановкаКвотНаОтгрузку КАК ТК_УстановкаКвотНаОтгрузку
	|ГДЕ
	|	ТК_УстановкаКвотНаОтгрузку.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("МоментВремени",                            Новый МоментВремени(Реквизиты.Период,ДокументСсылка));
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ХозОперация",							  Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("ДатаНачалаДействия",						  Реквизиты.ДатаНачалаДействия);
		
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДатаНачалаДействия", Реквизиты.ДатаНачалаДействия);

КонецПроцедуры
	
Функция ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.Номенклатура КАК Номенклатура,
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.Область КАК Область,
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.ВидКвоты КАК ВидКвоты,
	               |	ВЫБОР
	               |		КОГДА &ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.УстановкаКвотНаОтгрузку)
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Состояние,
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.Порог КАК Порог,
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.Процент КАК Процент
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_УстановкаКвотНаОтгрузку.Товары КАК ТК_УстановкаКвотНаОтгрузкуТовары
	               |ГДЕ
	               |	ТК_УстановкаКвотНаОтгрузкуТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТК_КвотыНаОтгрузку(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТК_КвотыНаОтгрузку";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&ДатаНачалаДействия КАК Период,
	               |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	               |	ВтТаблицаТовары.Область КАК Область,
	               |	ВтТаблицаТовары.ВидКвоты КАК ВидКвоты,
	               |	ВтТаблицаТовары.Состояние КАК Состояние,
	               |	ВтТаблицаТовары.Порог КАК Порог,
	               |	ВтТаблицаТовары.Процент КАК Процент
	               |ИЗ
	               |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти






#КонецЕсли

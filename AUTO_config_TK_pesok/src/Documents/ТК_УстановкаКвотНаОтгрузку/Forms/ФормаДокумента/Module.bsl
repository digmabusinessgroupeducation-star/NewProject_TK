&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ
Перем Разбить;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);	
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ТК_УстановкаКвотНаОтгрузку"));
	
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения		
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ХозОперацияПриИзмененииНаСервере()
	Если Объект.ХозОперация <> ПредопределенноеЗначение("Справочник.ХозОперации.УстановкаКвотНаОтгрузку") И Объект.Товары.Количество() > 0 Тогда 
		тчТовары = Объект.Товары.Выгрузить();
		
		тчТовары.ЗаполнитьЗначения(0, "Порог, Процент");
		
		Объект.Товары.Загрузить(тчТовары);
	КонецЕсли;
	
	УстановитьВидимостьКолонок();
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	ХозОперацияПриИзмененииНаСервере();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПроцентПриИзменении(Элемент)
	
	текСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если текСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если текСтрока.Процент > 100 Тогда 
		текСтрока.Процент = 100;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Процент от заказа не должен быть более 100%'; uk = 'Відсоток від замовлення повинен бути не більше 100%'"), Объект.Ссылка, "Товары["+Элементы.Товары.ТекущаяСтрока+"].Процент");
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт

    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);

КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ТК_УстановкаКвотНаОтгрузку.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка квот из файла'; uk = 'Завантаження списка квот із файлу'");		
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);	
		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаБАТ(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗагрузкаБАТ", Истина);
	ДополнительныеПараметры.Вставить("АдресДопДанных", ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор));
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ТК_УстановкаКвотНаОтгрузку.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка квот из файла (БАТ)'; uk = 'Завантаження списка квот із файлу (БАТ)'");		
	ПараметрыЗагрузки.ИмяМакетаСШаблоном = "ЗагрузкаИзФайлаТоварыБАТ";
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	   
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);	

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
    
    Если АдресЗагруженныхДанных = Неопределено Тогда 
        Возврат;
    КонецЕсли;
	
    ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры)
	
	Товары = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	ЗагрузкаБАТ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ЗагрузкаБАТ", Ложь);	
	АдресДопДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "АдресДопДанных", "");
	
	СписокКвот = Неопределено;	
	Если Не ПустаяСтрока(АдресДопДанных) Тогда 							
		Попытка
			ДопДанные = ПолучитьИзВременногоХранилища(АдресДопДанных);
			СписокКвот = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДопДанные, "СписокКвот", Неопределено);
		Исключение КонецПопытки;		
		
		Попытка
			УдалитьИзВременногоХранилища(АдресДопДанных);
		Исключение КонецПопытки;
	КонецЕсли;
	
	Если ЗагрузкаБАТ И (ТипЗнч(СписокКвот) <> Тип("Соответствие") ИЛИ СписокКвот.Количество() = 0) Тогда 
		Возврат;
	КонецЕсли;	
	
	ТоварДобавлен = Ложь;
	
	Если ЗагрузкаБАТ Тогда 
		
		ТипМассив = Тип("Массив");
		
		Для Каждого СтрокаТовары Из Товары Цикл 
			Если Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда 
				Продолжить;
			КонецЕсли;
			
			МассивКвот = СписокКвот.Получить(СтрокаТовары.Идентификатор);
			Если ТипЗнч(МассивКвот) <> ТипМассив ИЛИ МассивКвот.Количество() = 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			Для Ит = 0 По МассивКвот.ВГраница() Цикл 
				текСтрока = МассивКвот.Получить(Ит);
				НоваяСтрока = Объект.Товары.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары, "Номенклатура, Область");
				ЗаполнитьЗначенияСвойств(НоваяСтрока, текСтрока, "ВидКвоты, Порог, Процент");
				
				ТоварДобавлен = Истина;
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаТЧ Из Товары Цикл 
			Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда 
				НоваяСтрока = Объект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
				
				ТоварДобавлен = Истина;
			КонецЕсли;			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТоварДобавлен Тогда 
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла



&НаКлиенте
Процедура ОчиститьТовары(Команда)
	Объект.Товары.Очистить();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции



#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонок()
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, "ТоварыПорог, ТоварыПроцент", "Видимость", Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.УстановкаКвотНаОтгрузку"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры



#КонецОбласти


#Область РаботаСБуферомОбмена


#КонецОбласти

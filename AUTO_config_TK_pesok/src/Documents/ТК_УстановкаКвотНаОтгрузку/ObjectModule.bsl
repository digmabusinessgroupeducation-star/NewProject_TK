
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата 				= ТекущаяДата();
	ДатаНачалаДействия 	= Дата;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_УстановкаКвотНаОтгрузку.Дата КАК Дата
		|ИЗ
		|	Документ.ТК_УстановкаКвотНаОтгрузку КАК ТК_УстановкаКвотНаОтгрузку
		|ГДЕ
		|	ТК_УстановкаКвотНаОтгрузку.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ТК_УстановкаКвотНаОтгрузку.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ЗапасыСервер.ОтразитьКвотыНаОтгрузку(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ТК_УстановкаКвотНаОтгрузкуПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));
	
КонецПроцедуры


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата				  = ТекущаяДата();
	ДатаНачалаДействия	  = Дата;
	Автор				  = Пользователи.ТекущийПользователь();
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ХозОперация			  = ПредопределенноеЗначение("Справочник.ХозОперации.УстановкаКвотНаОтгрузку");
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти






#КонецЕсли
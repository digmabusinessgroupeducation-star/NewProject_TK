#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПоступленияТоваров(ДанныеЗаполнения);	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПеремещенияТоваров(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_ЗаявкаНаВозвратПоставщику") Тогда	
		ЗаполнитьДокументНаОснованииЗаявкиНаВозврат(ДанныеЗаполнения);

	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения,ТипДанныхЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено,ТипДанныхЗаполнения)
	
	
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("Товары") Тогда
			Товары.Загрузить(ДанныеЗаполнения.Товары);
		КонецЕсли;

		
		Если НЕ ДанныеЗаполнения.Свойство("Автор") Тогда
			 Автор =  Пользователи.ТекущийПользователь();
		КонецЕсли;
		Если НЕ ДанныеЗаполнения.Свойство("КурсДокумента") Тогда
			 КурсДокумента = 1;
		КонецЕсли;
		Если НЕ ДанныеЗаполнения.Свойство("ВалютаДокумента") Тогда
			 ВалютаДокумента = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
		КонецЕсли;
		Если НЕ ДанныеЗаполнения.Свойство("ПодразделениеКомпании") Тогда
			 ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
		КонецЕсли;
		Если НЕ ДанныеЗаполнения.Свойство("Организация") Тогда
			 Организация =  ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);
		КонецЕсли;
		
		Если НЕ ДанныеЗаполнения.Свойство("СкладКомпании") Тогда
			СкладКомпании =  ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);
		КонецЕсли;
			
		Если НЕ ДанныеЗаполнения.Свойство("ТипЦен") Тогда
			Если ЗначениеЗаполнено(СкладКомпании.ТипЦенСебестоимости) Тогда
				ТипЦен = СкладКомпании.ТипЦенСебестоимости;
			Иначе
				ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Организация <> ДоговорВзаиморасчетов.Организация Тогда
			ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуТранзиты;
		КонецЕсли;
		
		
		
		
	Иначе
		Дата = ТекущаяДата();
		Автор =  Пользователи.ТекущийПользователь();
		ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
		КурсДокумента         = 1;
		Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
		ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
		//ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
		СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
		Если ЗначениеЗаполнено(СкладКомпании.ТипЦенСебестоимости) Тогда
			ТипЦен = СкладКомпании.ТипЦенСебестоимости;
		Иначе
			ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенЗакупки;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры


Процедура ЗаполнитьДокументНаОснованииПоступленияТоваров(Знач ПоступлениеОснование)
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ПоступлениеТоваров.Организация <> ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.Хозоперации.ВозвратТоваровПоставщикуТранзиты)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщику)
	               |	КОНЕЦ КАК ХозОперация,
	               |	ПоступлениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ПоступлениеТоваров.Организация КАК Организация,
	               |	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
	               |	ПоступлениеТоваров.Контрагент КАК Контрагент,
	               |	ПоступлениеТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
	               |	ПоступлениеТоваров.ТипЦен КАК ТипЦен,
	               |	ПоступлениеТоваров.СуммаДокумента КАК СуммаДокумента,
	               |	ПоступлениеТоваров.Ссылка КАК ДокументОснование,
	               |	ПоступлениеТоваров.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Ссылка КАК Партия
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	               |ГДЕ
	               |	ПоступлениеТоваров.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ПоступлениеОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		ТаблицаТоваров = Выборка.Товары.Выгрузить();
		Если ТаблицаТоваров.Количество() > 0 Тогда 
			Товары.Загрузить(ТаблицаТоваров);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПеремещенияТоваров(Знач ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ВозвратТоваровПоставщику");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпании.Подразделение
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИЗФилиала)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпанииПолучатель.Подразделение
	               |		ИНАЧЕ ПеремещениеТоваров.ПодразделениеКомпании
	               |	КОНЕЦ КАК ПодразделениеКомпании,
	               |	ВЫБОР
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпании.ТорговаяПлощадка
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИЗФилиала)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпанииПолучатель.ТорговаяПлощадка
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТорговыеПлощадки.ПустаяСсылка)
	               |	КОНЕЦ КАК ТорговаяПлощадка,
	               |	ПеремещениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВЫБОР
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпании
	               |		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИЗФилиала)
	               |			ТОГДА ПеремещениеТоваров.СкладКомпанииПолучатель
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
	               |	КОНЕЦ КАК СкладКомпании,
	               |	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	               |	ПеремещениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПеремещениеТоваров.КурсДокумента КАК КурсДокумента,
	               |	ПеремещениеТоваров.ТипЦен КАК ТипЦен,
	               |	ПеремещениеТоваров.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Ссылка КАК Партия
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	               |ГДЕ
	               |	ПеремещениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		ВыборкаТовары = Выборка.Товары.Выбрать();		
		Товары.Очистить();
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");		
		
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииЗаявкиНаВозврат(Знач ЗаявкаНаВозвратОснование)
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ТК_ЗаявкаНаВозвратПоставщику.Организация <> ТК_ЗаявкаНаВозвратПоставщику.ДоговорВзаиморасчетов.Организация
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.Хозоперации.ВозвратТоваровПоставщикуТранзиты)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщику)
	               |	КОНЕЦ КАК ХозОперация,
	               |	ТК_ЗаявкаНаВозвратПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ТК_ЗаявкаНаВозвратПоставщику.Организация КАК Организация,
	               |	ТК_ЗаявкаНаВозвратПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_ЗаявкаНаВозвратПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_ЗаявкаНаВозвратПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ТК_ЗаявкаНаВозвратПоставщику.Контрагент КАК Контрагент,
	               |	ТК_ЗаявкаНаВозвратПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ТК_ЗаявкаНаВозвратПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_ЗаявкаНаВозвратПоставщику.КурсДокумента КАК КурсДокумента,
	               |	ТК_ЗаявкаНаВозвратПоставщику.ТипЦен КАК ТипЦен,
	               |	ТК_ЗаявкаНаВозвратПоставщику.СуммаДокумента КАК СуммаДокумента,
	               |	ТК_ЗаявкаНаВозвратПоставщику.Ссылка КАК ДокументОснование,
	               |	ТК_ЗаявкаНаВозвратПоставщику.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Ссылка КАК Партия
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ТК_ЗаявкаНаВозвратПоставщику КАК ТК_ЗаявкаНаВозвратПоставщику
	               |ГДЕ
	               |	ТК_ЗаявкаНаВозвратПоставщику.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ЗаявкаНаВозвратОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		ТаблицаТоваров = Выборка.Товары.Выгрузить();
		Если ТаблицаТоваров.Количество() > 0 Тогда 
			Товары.Загрузить(ТаблицаТоваров);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//Если Не ЗначениеЗаполнено(ДокументОснование) Тогда 
	//	ПроверяемыеРеквизиты.Добавить("Товары.Партия");
	//КонецЕсли;
	
	
	Если ОбщегоНазначенияТК.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект,  ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;	
	
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
		ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение	 Тогда
		//добавить проверку прав на проведение без контроля
		Если НЕ ДополнительныеСвойства.Свойство("ОбменТК") Тогда 
			ВыполнитьКонтрольОстатков(Отказ)
		КонецЕсли;
		
	КонецЕсли;
	
	ОрганизацияАналитики = Организация;
	Если ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуТранзиты Тогда
		ОрганизацияАналитики = ДоговорВзаиморасчетов.Организация;
	КонецЕсли;
	
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,ОрганизацияАналитики);
	
	Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
		ТипЦен = СкладКомпании.ТипЦенСебестоимости;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	Док.СкладКомпании КАК СкладКомпании,
		|	ВЫБОР
		|		КОГДА Док.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)
		|			ТОГДА Док.ДоговорВзаиморасчетов.Организация
		|		ИНАЧЕ Док.Организация
		|	КОНЕЦ КАК Организация
		|ИЗ
		|	Документ.ВозвратПоставщику КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
		ДополнительныеСвойства.Вставить("СтарыйДоговорДокумента", Выборка.ДоговорВзаиморасчетов);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияДоговораПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;

	
	Если РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение")
		И Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "НеКонтролироватьОрганизациюПартииПриВозвратеПоставщику", Ложь) Тогда 
		
		ТаблицаПартий = Товары.Выгрузить(, "Партия");
		ТаблицаПартий.Свернуть("Партия");
				
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТаблицаПартий.Партия КАК Партия
		               |ПОМЕСТИТЬ втПартии
		               |ИЗ
		               |	&ТаблицаПартий КАК ТаблицаПартий
		               |
		               |ИНДЕКСИРОВАТЬ ПО
		               |	Партия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПоступлениеТоваров.Ссылка КАК Партия
		               |ИЗ
		               |	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		               |ГДЕ
		               |	ПоступлениеТоваров.Ссылка В
		               |			(ВЫБРАТЬ
		               |				втПартии.Партия КАК Партия
		               |			ИЗ
		               |				втПартии КАК втПартии
		               |			ГДЕ
		               |				втПартии.Партия ССЫЛКА Документ.ПоступлениеТоваров)
		               |	И ПоступлениеТоваров.ДоговорВзаиморасчетов <> &Договор";
		
		Запрос.УстановитьПараметр("ТаблицаПартий", ТаблицаПартий);
		Запрос.УстановитьПараметр("Договор", ДоговорВзаиморасчетов);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда 
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл 
				нПартии = Товары.НайтиСтроки(Новый Структура("Партия", Выборка.Партия));
				Для Каждого Стр Из нПартии Цикл 
					
					ОбщегоНазначения.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Строка %1, различаются договора документа и партии ''%2'''; uk = 'Строка %1, відрізняються договори документу та партії ''%2'''"),
							Стр.НомерСтроки, 
							Стр.Партия),
						ЭтотОбъект,
						"Товары["+Товары.Индекс(Стр)+"].Партия",
						,
						Отказ);
						
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияДоговораПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовИзменен", ДоговорВзаиморасчетов <> ДополнительныеСвойства.СтарыйДоговорДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ОрганизацияПроверки = Организация;
		Если ХозОперация = Справочники.ХозОперации.ВозвратТоваровПоставщикуТранзиты Тогда
			ОрганизацияПроверки = ДоговорВзаиморасчетов.Организация;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", ОрганизацияПроверки <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;

		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ВозвратПоставщику.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ВозвратПоставщику.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);

	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПотребностиТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьПотребностьОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПродажиТоваровКомпании") Тогда
		ЗапасыСервер.ОтразаитьПродажиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	ВзаиморасчетыСервер.ОтразитьРасчетыСПоставщиками(ДополнительныеСвойства, Движения, Отказ);
	ВзаиморасчетыСервер.ОтразитьРасчетыСПоставщикамиПоДокументам(ДополнительныеСвойства, Движения, Отказ);


	
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ВозвратПоставщикуПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда 
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	Для Каждого Стр Из Товары Цикл 
		Стр.Партия = Неопределено;
	КонецЦикла;
	
КонецПроцедуры




#Область КонтрольОстатков


Процедура ВыполнитьКонтрольОстатков(Отказ)

	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ЗапасыСервер.УстановитьБлокировкуОстатоковТоваровКомпании(МенеджерВременныхТаблиц);
	ЗапасыСервер.ТаблицаОстатковТоваровКомпании(Ссылка, СкладКомпании, Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	ТаблицаОшибок = ЗапасыСервер.ТаблицаОшибокОстатковТоваров();
	ЗапасыСервер.ЗаполнитьТаблицуОшибок(МенеджерВременныхТаблиц,ДополнительныеСвойства,ТаблицаОшибок,Отказ);	
	СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц)
	
	Если ТаблицаОшибок.Количество()> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Возврат поставщику превышает остаток товара компании на складе %1 %2 %3';uk='Повернення постачальнику перевищує залишок товару компанії на складі %1 %2 %3'"),
		СкладКомпании);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстСообщения,
		ЭтотОбъект);	
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Номенклатура: %1, недостаточно %2 %3';uk='Номенклатура: %1, недостатньо %2 %3'"),
			НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.ХарактеристикаНоменклатуры),
			СтрокаТаблицы.Количество,
			СтрокаТаблицы.ЕдиницаИзмерения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Ссылка);
			
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	 |ВЫБРАТЬ
	|	&Дата															КАК Дата,
	|	&ХозОперация													КАК ХозОперация,
	|	&Организация													КАК Организация,
	|	&ПодразделениеКомпании											КАК ПодразделениеКомпании,
	|	&СкладКомпании													КАК СкладКомпании,
	|	&ТорговаяПлощадка												КАК ТорговаяПлощадкаОтправитель
	|	
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|; 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки					КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура					КАК Номенклатура,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры	КАК ХарактеристикаНоменклатуры,
	|	ТаблицаТоваров.КоличествоБазовое			КАК Количество,
	|	&СкладКомпании								КАК СкладКомпании
	|
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;";
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	Запрос.УстановитьПараметр("Дата",								Дата);
	Запрос.УстановитьПараметр("ХозОперация",						ХозОперация);
	Запрос.УстановитьПараметр("Организация",						Организация);
	Запрос.УстановитьПараметр("СкладКомпании",						СкладКомпании);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",				ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",					ТорговаяПлощадка);
	Запрос.УстановитьПараметр("ТаблицаТоваров",						Товары.Выгрузить());
	
	 Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции


#КонецОбласти



#КонецЕсли



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда	
	
	
#Область ОбработчикиСобытий			

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);	
	КонецЕсли;	
	

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = ТекущаяДата();
	ИДДокументаПродажи = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Продукция.Итог("СуммаВсего");
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	//ДополнительныеСвойства.Вставить("РежимИспользованияАналогов", РежимИспользованияАналогов);
	//ДополнительныеСвойства.Вставить("РежимРасчетаСписанияВПроизводство", РежимРасчетаСписанияВПроизводство);
	//ДополнительныеСвойства.Вставить("ВидПроизводства", ВидПроизводства);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыпускПродукции.Дата КАК Дата,
		|	ВыпускПродукции.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.ВыпускПродукции КАК ВыпускПродукции
		|ГДЕ
		|	ВыпускПродукции.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И НЕ ДополнительныеСвойства.Свойство("ОбменТК") 
		И НЕ ДополнительныеСвойства.Свойство("НеКонтролироватьОстатки") 
		И НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "РазрешитьОтрицательныеОстатки", Ложь) Тогда
		
		ВыполнитьКонтрольОстатков(Отказ,РежимПроведения)
	КонецЕсли;	
	
	Если ХозОперация <> Справочники.ХозОперации.ВыпускПродукцииСРеализацией
		И РаспределениеСписания.Количество() > 0 Тогда 
		
		РаспределениеСписания.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() И Не (РегламентированныйУчет И ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией) Тогда 
		ДополнительныеСвойства.Вставить("НеСоздаватьНН", Истина);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("ТипЦен");
	
	Если ХозОперация <> Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорВзаиморасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("ТипЦен");
		МассивНепроверяемыхРеквизитов.Добавить("Продукция.Цена");
		МассивНепроверяемыхРеквизитов.Добавить("Продукция.ЦенаБезНалогов");
		МассивНепроверяемыхРеквизитов.Добавить("Продукция.СтавкаНДС");
	КонецЕсли;
	
	Если ХозОперация <> Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ТорговаяПлощадкаПолучатель");
		МассивНепроверяемыхРеквизитов.Добавить("СкладКомпанииПолучатель");
	КонецЕсли;
		
	МассивНепроверяемыхРеквизитов.Добавить("РежимИспользованияАналогов");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ВыпускПродукции.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	Документы.ВыпускПродукции.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПроизводство(ДополнительныеСвойства, Движения, Отказ);
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПриходованиеТоваров") Тогда
		ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПродажиТоваровКомпании") Тогда
		ЗапасыСервер.ОтразаитьПродажиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПокупателями") Тогда
		ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателями(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПокупателямиПоДокументам") Тогда
		ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателямиПоДокументам(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗаказыПокупателей") Тогда
		ЗаказыСервер.ОтразитьЗаказыПокупателей(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРезервыТоваров") Тогда
		ЗаказыСервер.ОтразитьРезервыТоваров(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаВозвратнаяТара") Тогда
		ЗапасыСервер.ОтразитьВозвратнаяТара(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;		
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ВыпускПродукцииПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));
	
КонецПроцедуры

#КонецОбласти


#Область ИнициализацияИЗаполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор, "ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	СкладКомпании 		  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор, "ОсновнойСклад",, СкладКомпании);		
	ТорговаяПлощадка	  = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(СкладКомпании);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	
КонецПроцедуры // ИнициализироватьДокумент()

// Заказ покупателя
Процедура ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.ХозОперация КАК ХозОперацияЗаказ,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.Комментарий КАК Комментарий,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПокупателя.ТипЦен КАК ТипЦен,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	               |	ЗаказПокупателя.Дата КАК ДатаЗаказа,
	               |	ЗаказПокупателя.СрокПоставки КАК СрокПоставки,
	               |	ЗаказПокупателя.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		Коэффициент КАК Коэффициент,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Автор = Пользователи.ТекущийПользователь();
	ЗакрытиеЗаказовПокупателей = 1;
	РежимИспользованияАналогов = Перечисления.РежимыИспользованияАналогов.Разрешить;
	РежимРасчетаСписанияВПроизводство = Перечисления.РежимыРасчетаСписанияВПроизводство.ПоФакту;
	ВидПроизводства = Перечисления.ВидыПроизводства.ЗаготовкиНеГотовятся;
	УчитыватьОстаткиБлюдНаСкладе = Ложь;
	РазрешитьНедовложения = Ложь;
	
	ДатаДокумента = ?(Выборка.СрокПоставки <> Дата("00010101"), Выборка.СрокПоставки, Выборка.ДатаЗаказа + 60*60*24);	
	
	Если ЭтоНовый() Тогда 
		Дата = Дата(Формат(ДатаДокумента, "ДФ='dd.MM.yyyy 09:00:00'"));	
	КонецЕсли;
	
	Если Выборка.ХозОперацияЗаказ = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка, "Организация,ПодразделениеКомпании,ТорговаяПлощадка,Комментарий,ВалютаДокумента,КурсДокумента,ТипЦен,СкладКомпании,РегламентированныйУчет,ДокументОснование");	
		ХозОперация = Справочники.ХозОперации.ВыпускПродукции;
	Иначе
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);	
		ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией;
	КонецЕсли;		
	
	ВыборкаТовары = Выборка.Товары.Выбрать();
		
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", ПодразделениеКомпании));

	Продукция.Очистить();
	Ингредиенты.Очистить();
	
	Пока ВыборкаТовары.Следующий() Цикл 
		НоваяСтрока = Продукция.Добавить();
	 	
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары,, "ЕдиницаИзмерения, Количество, КоличествоБазовое, Коэффициент");
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
		
		Если Не ЗначениеЗаполнено(НоваяСтрока.ЕдиницаИзмерения) Тогда 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары, "ЕдиницаИзмерения, Количество, КоличествоБазовое, Коэффициент");
		ИначеЕсли НоваяСтрока.ЕдиницаИзмерения = ВыборкаТовары.ЕдиницаИзмерения ИЛИ НоваяСтрока.Коэффициент = ВыборкаТовары.Коэффициент Тогда 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары, "Количество, КоличествоБазовое, Коэффициент");
		Иначе 
			Если НоваяСтрока.Коэффициент = 0 Тогда 
				НоваяСтрока.Коэффициент = 1;
			КонецЕсли;
			
			КоэффициентПересчета = 1 / НоваяСтрока.Коэффициент;
			
			НоваяСтрока.Количество = ВыборкаТовары.Количество * КоэффициентПересчета;
			НоваяСтрока.КоличествоБазовое = ВыборкаТовары.КоличествоБазовое;
		КонецЕсли;
	КонецЦикла;	
	
	Документы.ВыпускПродукции.ПерезаполнитьИнгридиенты(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	               |	ТаблицаТоваров.Ингредиент КАК Номенклатура,
	               |	ВЫРАЗИТЬ(ТаблицаТоваров.Ингредиент КАК Справочник.Номенклатура).Производитель КАК Производитель,
	               |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
	               |	ТаблицаТоваров.КоличествоИнгредиентаФакт КАК Количество
	               |ПОМЕСТИТЬ втТаблица
	               |ИЗ
	               |	&ТаблицаТоваров КАК ТаблицаТоваров
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Дата КАК Дата,
	               |	&ХозОперация КАК ХозОперация,
	               |	&Организация КАК Организация,
	               |	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	&Контрагент КАК Контрагент,
	               |	&ТочкаДоставки КАК ТочкаДоставки
	               |ПОМЕСТИТЬ ТаблицаДанныхДокумента
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТаблица.НомерСтроки КАК НомерСтроки,
	               |	втТаблица.Номенклатура КАК Номенклатура,
	               |	втТаблица.Производитель КАК Производитель,
	               |	втТаблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(втТаблица.Количество) КАК Количество
	               |ПОМЕСТИТЬ ВтТаблицаТоваров
	               |ИЗ
	               |	втТаблица КАК втТаблица
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	втТаблица.ХарактеристикаНоменклатуры,
	               |	втТаблица.Номенклатура,
	               |	втТаблица.НомерСтроки,
	               |	втТаблица.Производитель
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(втТаблица.Количество) > 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ втТаблица";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	Запрос.УстановитьПараметр("Дата",								Дата);
	Запрос.УстановитьПараметр("Организация",						Организация);
	Запрос.УстановитьПараметр("СкладКомпании",						СкладКомпании);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",				ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент",							Контрагент);
	Запрос.УстановитьПараметр("ТочкаДоставки",						ТочкаДоставки);
	Запрос.УстановитьПараметр("ХозОперация",						ХозОперация);
	Запрос.УстановитьПараметр("ТаблицаТоваров",						Ингредиенты.Выгрузить());	
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Процедура СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц)
	
	Если ТаблицаОшибок.Количество()> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Недостаточно ингридиентов на складе %1'; uk = 'Недостатьньо інгридієнтів на складі %1'"),
							СкладКомпании);
							
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект);	
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Номенклатура: %1, недостаточно %2 %3';uk='Номенклатура: %1, недостатньо %2 %3'"),
			НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.ХарактеристикаНоменклатуры),
			СтрокаТаблицы.Количество,
			СтрокаТаблицы.ЕдиницаИзмерения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Ссылка);
			
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ВыполнитьКонтрольОстатков(Отказ, РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ЗапасыСервер.УстановитьБлокировкуОстатоковТоваровКомпании(МенеджерВременныхТаблиц);
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный тогда
		ДатаКонтроля =  ТекущаяДатаСеанса();
	Иначе
		ДатаКонтроля =  Дата;
	КонецЕсли;
	
	ЗапасыСервер.ТаблицаОстатковТоваровКомпании(Ссылка, СкладКомпании, ДатаКонтроля, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	ТаблицаОшибок = ЗапасыСервер.ТаблицаОшибокОстатковТоваров();
	ЗапасыСервер.ЗаполнитьТаблицуОшибок(МенеджерВременныхТаблиц,ДополнительныеСвойства,ТаблицаОшибок,Отказ);	
	СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц);
	
КонецПроцедуры


#КонецОбласти

	
	

#КонецЕсли
&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ГруппыДляРазмещения = Новый СписокЗначений;
	ГруппыДляРазмещения.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Документ_ВыпускПродукции_ТТН"), Элементы.ГруппаТТН.Имя);
	ГруппыДляРазмещения.Добавить("ВсеОстальные", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", ГруппыДляРазмещения);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Контрагент 		= Объект.Контрагент;
	Договор 		= Объект.ДоговорВзаиморасчетов;
	ВалютаДокумента = Объект.ВалютаДокумента;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ВыпускПродукции"));
	
	Если Объект.Ссылка.Пустая() Тогда 
		ПриСозданииЧтенииНаСервере();
	Иначе 
		ПроверитьКорректностьЗаполненияИнгредиентов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	УправлениеЭлементамиФормы();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
	
	
	УстановитьСостояниеДокумента();
	
	ЗаполнитьТаблицуВыпущеннаяПродукция();	
	
	ПриСозданииЧтенииНаСервере();	
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ИнгредиентыПерезаполнены Тогда 
		Отказ = Истина;
		ВопросПерезаполненияИнгредиентов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаВыпуск И Не ИнгредиентыПерезаполнены Тогда 
		ВопросПерезаполненияИнгредиентов();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	
	ПриСозданииЧтенииНаСервере();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();     
	
	// СтандартныеПодсистемы.Свойства
	ОбновитьЭлементыДополнительныхРеквизитов();
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеКомпанииПриИзмененииНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();

КонецПроцедуры

&НаКлиенте
Процедура ВидПроизводстваПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		//ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		                             
		СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация,Объект.ПодразделениеКомпании);
		Объект.ДоговорВзаиморасчетов = СтруктураДанные.Договор;
		
		ДоговорПередИзменением = Договор;
		Договор = Объект.ДоговорВзаиморасчетов;
		
		КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные);
	Иначе
		Объект.ДоговорВзаиморасчетов = Договор;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	ОбработатьИзменениеДоговора();
КонецПроцедуры

&НаСервере
Процедура ТорговаяПлощадкаПолучательПриИзмененииНаСервере()
	Объект.СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Объект.ТорговаяПлощадкаПолучатель);
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПолучательПриИзменении(Элемент)
	ТорговаяПлощадкаПолучательПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СкладКомпанииПолучательПриИзмененииНаСервере()
	Объект.ТорговаяПлощадкаПолучатель = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Объект.СкладКомпанииПолучатель);
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПолучательПриИзменении(Элемент)
	СкладКомпанииПолучательПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснование1ПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	
	Объект.ТорговаяПлощадка = ПолучитьТорговуюПлощадкуСклада(Объект.СкладКомпании);
	
КонецПроцедуры

&НаКлиенте
Процедура РеализацияРеглУчетПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентированныйУчетПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();	
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Объект.Организация,Объект.ТорговаяПлощадка,"Реализация"));
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));

	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ПерезаполнитьИнгридиенты();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

	ИнгредиентыПерезаполнены = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказатели(ЭтаФорма); 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБезНалоговПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьПроцентСкидки", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНалоговПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	ИнгредиентыПерезаполнены = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Объект.Организация,Объект.ТорговаяПлощадка,"Реализация"));
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));
	
	
	ОбработатьВыборНаСервере(ВыбранноеЗначение, СтруктураДействий);
	
	ИнгредиентыПерезаполнены = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища, СтруктураДействий) Экспорт
	
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Продукция.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Продукция.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);

	КонецЦикла;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыПродукция

&НаКлиенте
Процедура ПродукцияПриАктивизацииСтроки(Элемент)

	ТекСтрока = Элемент.ТекущиеДанные;
	УстановитьОтборПоКлючу(ТекСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпущеннаяПродукцияПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	УстановитьОтборПоКлючу(ТекСтрока);	
	
КонецПроцедуры


&НаКлиенте
Процедура ПродукцияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекСтрока = Элемент.ТекущиеДанные;
	УстановитьОтборПоКлючу(ТекСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПослеУдаления(Элемент)
	ПерезаполнитьИнгридиенты();	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда 
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	ПерезаполнитьИнгридиенты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	ПерезаполнитьИнгридиенты();	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияРецептураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	ПерезаполнитьИнгридиенты();	
	
КонецПроцедуры



&НаКлиенте
Процедура УстановитьОтборПоКлючу(ТекСтрока)
	
	Если ТекСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Отбор = Новый ФиксированнаяСтруктура("Рецептура, Номенклатура", ТекСтрока.Рецептура, ТекСтрока.Номенклатура);
	
	Элементы.Ингредиенты.ОтборСтрок = Отбор;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыИнгредиенты

&НаКлиенте
Процедура ИнгредиентыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекПродукция = Элементы.ВыпущеннаяПродукция.ТекущаяСтрока;
	
	Если ТекПродукция = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Для добавления ингредиента вручную необходимо выбрать требуемую продукцию'; uk = 'Для додавання інгредієнту необхідно обрати потрібну продукцію'"),
			Объект.Ссылка,
			"ВыпущеннаяПродукция");
		
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ И Копирование Тогда 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнгредиентыПередНачаломИзменения(Элемент, Отказ)
	
	ТекДанные = Элементы.Ингредиенты.ТекущиеДанные;
	
	Если ТекДанные.КоличествоИнгредиентаНорма <> 0 И Элементы.Ингредиенты.ТекущийЭлемент.Имя = "ИнгредиентыИнгридиент" Тогда 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнгредиентыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда 
		ДанныеПродукции = Элементы.ВыпущеннаяПродукция.ДанныеСтроки(Элементы.ВыпущеннаяПродукция.ТекущаяСтрока);	
		ДанныеИнгредиент = Элементы.Ингредиенты.ДанныеСтроки(Элементы.Ингредиенты.ТекущаяСтрока);
		
		ЗаполнитьЗначенияСвойств(ДанныеИнгредиент, ДанныеПродукции, "Номенклатура, Рецептура, Автопередел");
		ДанныеИнгредиент.КоличествоНоменклатуры 		= ДанныеПродукции.Количество;
		ДанныеИнгредиент.ЕдиницаИзмеренияНоменклатуры 	= ДанныеПродукции.ЕдиницаИзмерения;
		ДанныеИнгредиент.КоэффициентНоменклатуры 		= ДанныеПродукции.Коэффициент;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнгредиентыИнгридиентПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Ингредиенты.ТекущиеДанные;
	
	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("Номенклатура", 		ТекущаяСтрока.Ингредиент);
	ДанныеСтроки.Вставить("ЕдиницаИзмерения", 	ТекущаяСтрока.ЕдиницаИзмеренияИнгредиента);
	ДанныеСтроки.Вставить("Коэффициент", 		ТекущаяСтрока.КоэффициентИнгредиента);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ДанныеСтроки.ЕдиницаИзмерения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ДанныеСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ТекущаяСтрока.ЕдиницаИзмеренияИнгредиента = ДанныеСтроки.ЕдиницаИзмерения;
	ТекущаяСтрока.КоэффициентИнгредиента = ДанныеСтроки.Коэффициент;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнгредиентыЕдиницаИзмеренияИнгредиентаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Ингредиенты.ТекущиеДанные;
	
	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("Номенклатура", 		ТекущаяСтрока.Ингредиент);
	ДанныеСтроки.Вставить("ЕдиницаИзмерения", 	ТекущаяСтрока.ЕдиницаИзмеренияИнгредиента);
	ДанныеСтроки.Вставить("Коэффициент", 		ТекущаяСтрока.КоэффициентИнгредиента);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ДанныеСтроки.ЕдиницаИзмерения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ДанныеСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ТекущаяСтрока.ЕдиницаИзмеренияИнгредиента = ДанныеСтроки.ЕдиницаИзмерения;
	ТекущаяСтрока.КоэффициентИнгредиента = ДанныеСтроки.Коэффициент;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнгредиентыПередУдалением(Элемент, Отказ)
	
	ТекСтрока = Элементы.Ингредиенты.ТекущиеДанные;
	
	Если ТекСтрока <> Неопределено И ТекСтрока.КоличествоИнгредиентаНорма <> 0 Тогда 
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры




#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТара

&НаКлиенте
Процедура ТараНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
		
	//Новый документ, цены на тек дату
	Если  Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ДатаСрезаЦен = ТекущаяДата();
	Иначе
		ДатаСрезаЦен = Неопределено;
	КонецЕсли;		
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект, "Цены", ДатаСрезаЦен,, ПолучитьТипЦенСклада(Объект.СкладКомпании))));	
	СтруктураДействий.Вставить("ЦеныБезХарактеристики", Истина);
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТараКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
КонецПроцедуры

&НаКлиенте
Процедура ТараЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);			
	
КонецПроцедуры

&НаКлиенте
Процедура ТараСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Тара.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы", "Количество");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ВыпускПродукции.Продукция";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка товаров из файла'; uk = 'Завантаження списка товарів із файлу'");		
	//ПараметрыЗагрузки.ДополнительныеПараметры = Новый структура;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Объект.Организация,Объект.ТорговаяПлощадка,"Реализация"));
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьРецептуру", Новый Структура("ПодразделениеКомпании", Объект.ПодразделениеКомпании));

	
	ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных, СтруктураДействий);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных, СтруктураДействий)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл         
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовары = Объект.Продукция.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.Количество = СтрокаТаблицы.Количество;
		
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", НоваяСтрокаТовары.ЕдиницаИзмерения);				
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, КэшированныеЗначения); 
		
		ТоварыДобавлены = Истина;
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		Модифицированность = Истина;
		РассчитатьИтоговыеПоказатели(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

&НаКлиенте
Процедура КнопкаПерезаполнитьИнгридиентыНаКлиенте(Команда)
	ПерезаполнитьИнгридиенты();	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПродукцию(Команда)
	ЗаполнитьТаблицуВыпущеннаяПродукция();
КонецПроцедуры


&НаКлиенте
Процедура Подбор(Команда)
		
	ПараметрыФормы = Новый Структура("СкладКомпании, НайтиПоТочномуСоответствию", Объект.СкладКомпании, Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыФормы, Элементы.Товары,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦенуПоТипу(Команда)
	Перем ТипЦен;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	
	ОповещениЕ = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект, Команда.Имя);
	ОткрытьФорму("Справочник.ТипыЦен.ФормаВыбора", ПараметрыФормы,ТипЦен,,,,ОповещениЕ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьДаннымиОснования(Команда)
	ПерезаполнитьДаннымиОснованияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДаннымиОснованияНаСервере()
		
	ТекущийДокумент = РеквизитФормыВЗначение("Объект", Тип("ДокументОбъект.ВыпускПродукции"));
	ТекущийДокумент.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийДокумент, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеТТН(Команда)
	ЗаполнитьДанныеТТННаСервере();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТТННаСервере()
	
	Документы.ВыпускПродукции.ЗаполнитьРеквизитыТТНПоУмолчанию(Объект);
	
	ЭтаФорма.Прочитать();
	
КонецПроцедуры


#КонецОбласти



#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ВопросПерезаполненияИнгредиентов(ДопПараметры = Неопределено)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПерезаполненияИнгредиентовПродолжение", ЭтаФорма, ДопПараметры);
	
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр("ru = 'Обнаружены изменения в таблице ""Товары"".
              |Перезаполнить ингредиенты?'; uk = 'Виявлені зміни у таблиці ""Товари"".
              |Перезаповнити інгредієнти?'"),
		РежимДиалогаВопрос.ДаНетОтмена,,
		КодВозвратаДиалога.Да,
		НСтр("ru = 'Ингредиенты'; uk = 'Інгредіенти'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерезаполненияИнгредиентовПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ПерезаполнитьИнгридиенты();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда 
		ИнгредиентыПерезаполнены = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьКорректностьЗаполненияИнгредиентов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("тчТовары", Объект.Продукция.Выгрузить());
	Запрос.УстановитьПараметр("тчИнгредиенты", Объект.Ингредиенты.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.КоличествоБазовое КАК Количество
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	&тчТовары КАК тчТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	тчИнгредиенты.Номенклатура КАК Номенклатура,
	               |	тчИнгредиенты.КоличествоНоменклатуры КАК Количество
	               |ПОМЕСТИТЬ втИнгредиенты
	               |ИЗ
	               |	&тчИнгредиенты КАК тчИнгредиенты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	СУММА(ВложенныйЗапрос.Количество_Товары) КАК Количество_Товары,
	               |	СУММА(ВложенныйЗапрос.Количество_Ингредиенты) КАК Количество_Ингредиенты
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		втТовары.Номенклатура КАК Номенклатура,
	               |		СУММА(втТовары.Количество) КАК Количество_Товары,
	               |		NULL КАК Количество_Ингредиенты
	               |	ИЗ
	               |		втТовары КАК втТовары
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втТовары.Номенклатура
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		втИнгредиенты.Номенклатура,
	               |		NULL,
	               |		МАКСИМУМ(втИнгредиенты.Количество)
	               |	ИЗ
	               |		втИнгредиенты КАК втИнгредиенты
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		втИнгредиенты.Номенклатура) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Номенклатура
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВложенныйЗапрос.Количество_Товары) <> СУММА(ВложенныйЗапрос.Количество_Ингредиенты)";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		ИнгредиентыПерезаполнены = Истина;		
	Иначе
		ИнгредиентыПерезаполнены = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	МассивЭлементовТолькоПросмотр = Новый Массив;
	МассивЭлементовТолькоПросмотр.Добавить("ТоварыЦена");
	МассивЭлементовТолькоПросмотр.Добавить("ТоварыЦенаБезНалогов");
	МассивЭлементовТолькоПросмотр.Добавить("ТоварыСумма");
	
	РазрешитьРедактироватьЦены = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(),"РазрешитьРедактироватьЦеныПродажа", Ложь);
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементовТолькоПросмотр, "ТолькоПросмотр", Не РазрешитьРедактироватьЦены);		
	
	ЗаполнитьСебестоимость();
	
	Элементы.ЗаполнитьДанныеТТН.Видимость = Не Объект.Ссылка.Пустая();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормы()	
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ГруппаРеализация");
	МассивВсехРеквизитов.Добавить("ГруппаПеремещение");
	МассивВсехРеквизитов.Добавить("ТоварыЦена");
	МассивВсехРеквизитов.Добавить("ТоварыСумма");
	МассивВсехРеквизитов.Добавить("ТоварыСтавкаНДС");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаНДС");
	МассивВсехРеквизитов.Добавить("ТоварыГруппаСкидкаРучная");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаВсего");
	МассивВсехРеквизитов.Добавить("ГруппаЦеныВалюта");
	МассивВсехРеквизитов.Добавить("ГруппаРаспределенные");
	МассивВсехРеквизитов.Добавить("РегламентированныйУчет");
	МассивВсехРеквизитов.Добавить("ВыпущеннаяПродукцияАвтопередел");
	МассивВсехРеквизитов.Добавить("ИнгредиентыИзАвтопеределов");
	МассивВсехРеквизитов.Добавить("ИнгредиентыКоличествоИнгредиентаФакт");
	МассивВсехРеквизитов.Добавить("ГруппаОснование");
	МассивВсехРеквизитов.Добавить("ТоварыПерезаполнитьДаннымиОснования");
	МассивВсехРеквизитов.Добавить("ГруппаТТН");
	МассивВсехРеквизитов.Добавить("Себестоимость");
	МассивВсехРеквизитов.Добавить("СуммаНДС");
	МассивВсехРеквизитов.Добавить("СуммаВсегоСНДС");
	МассивВсехРеквизитов.Добавить("ВалютаДокумента");
	МассивВсехРеквизитов.Добавить("СтраницаТара");
	МассивВсехРеквизитов.Добавить("ГруппаСписание");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ВыпускПродукцииСРеализацией") Тогда 
		МассивРеквизитовОперации.Добавить("ГруппаРеализация");
		МассивРеквизитовОперации.Добавить("ТоварыЦена");
		МассивРеквизитовОперации.Добавить("ТоварыСумма");
		МассивРеквизитовОперации.Добавить("ТоварыСтавкаНДС");		
		МассивРеквизитовОперации.Добавить("ТоварыГруппаСкидкаРучная");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаВсего");
		МассивРеквизитовОперации.Добавить("ГруппаЦеныВалюта");
		МассивРеквизитовОперации.Добавить("ГруппаРаспределенные");
		МассивРеквизитовОперации.Добавить("ГруппаТТН");
		МассивРеквизитовОперации.Добавить("СтраницаТара");
		МассивРеквизитовОперации.Добавить("ГруппаСписание");
		
		МассивРеквизитовОперации.Добавить("СуммаВсегоСНДС");
		
		Если Объект.РегламентированныйУчет Тогда 
			МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");	
			МассивРеквизитовОперации.Добавить("СуммаНДС");
		КонецЕсли;
		
	ИначеЕсли Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ВыпускПродукцииСПеремещением") Тогда 
		МассивРеквизитовОперации.Добавить("ГруппаПеремещение");	
		МассивРеквизитовОперации.Добавить("РегламентированныйУчет");
	Иначе
		МассивРеквизитовОперации.Добавить("РегламентированныйУчет");
	КонецЕсли;
		
	Если Объект.ВидПроизводства <> ПредопределенноеЗначение("Перечисление.ВидыПроизводства.ЗаготовкиНеГотовятся") Тогда 
		МассивРеквизитовОперации.Добавить("ВыпущеннаяПродукцияАвтопередел");
		МассивРеквизитовОперации.Добавить("ИнгредиентыИзАвтопеределов");
	КонецЕсли;
	
	Если Объект.РежимРасчетаСписанияВПроизводство = ПредопределенноеЗначение("Перечисление.РежимыРасчетаСписанияВПроизводство.ПоФакту") Тогда 
		МассивРеквизитовОперации.Добавить("ИнгредиентыКоличествоИнгредиентаФакт");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		МассивРеквизитовОперации.Добавить("ГруппаОснование");
		МассивРеквизитовОперации.Добавить("ТоварыПерезаполнитьДаннымиОснования");
	КонецЕсли;
	
	Если Объект.Проведен Тогда 
		МассивРеквизитовОперации.Добавить("Себестоимость");	
	КонецЕсли;
	
	Если МассивРеквизитовОперации.Найти("СуммаВсегоСНДС") <> Неопределено
		ИЛИ МассивРеквизитовОперации.Найти("Себестоимость") <> Неопределено Тогда 
		
		МассивРеквизитовОперации.Добавить("ВалютаДокумента");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивРеквизитовОперации);			
	
КонецПроцедуры	
		
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьИнгридиенты()
	
	Документы.ВыпускПродукции.ПерезаполнитьИнгридиенты(Объект);
	
	ЗаполнитьТаблицуВыпущеннаяПродукция();
	
	ИнгредиентыПерезаполнены = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
	
КонецПроцедуры  
 
&НаКлиенте
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий,ТипЦен = Неопределено)
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ВыпускПродукцииСРеализацией") Тогда 
		Если ТипЗнч(СтруктураДействий) <> Тип("Структура") Тогда
			СтруктураДействий = Новый Структура;
		КонецЕсли;
		
		//Новый документ, цены на тек дату
		Если  Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ДатаСрезаЦен = ТекущаяДата();
		Иначе
			ДатаСрезаЦен = Неопределено;
		КонецЕсли;		
		
		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект,,ДатаСрезаЦен));
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,,ДатаСрезаЦен));
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	// Расчет итога по табличной части "Товары"
	КоллекцияТовары 					= Форма.Объект.Продукция;
	Форма.СуммаВсегоСНДС 				= КоллекцияТовары.Итог("СуммаВсего");
	Форма.СуммаНДС 						= КоллекцияТовары.Итог("СуммаНДС");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВыпущеннаяПродукция()
	
	ВыпущеннаяПродукция.Очистить();
	
	ТаблицаПроизводства = Объект.Ингредиенты.Выгрузить();
	ТаблицаПроизводства.Свернуть("Номенклатура,ЕдиницаИзмеренияНоменклатуры,КоэффициентНоменклатуры,Рецептура,Автопередел,КоличествоНоменклатуры");

	Для Каждого Стр Из ТаблицаПроизводства Цикл 
		НоваяПродукция = ВыпущеннаяПродукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПродукция, Стр, "Номенклатура,Рецептура,Автопередел");
		
		НоваяПродукция.ЕдиницаИзмерения = Стр.ЕдиницаИзмеренияНоменклатуры;
		НоваяПродукция.Коэффициент = Стр.КоэффициентНоменклатуры;
		НоваяПродукция.Количество = ?(Стр.КоэффициентНоменклатуры > 0, Стр.КоличествоНоменклатуры / Стр.КоэффициентНоменклатуры, Стр.КоличествоНоменклатуры);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Если ДоговорПередИзменением <> Объект.ДоговорВзаиморасчетов Тогда
		
		//ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением);
		
	Иначе
		
		Объект.ДоговорВзаиморасчетов = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	
	СтруктураДействий = Новый Структура;	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
		
	ПриИзмененииДоговораНаСервере(СтруктураДействий);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
	"ВалютаДокумента",
	Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
	"ВалютаРасчетовКурсКратность",
	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииДоговораНаСервере(СтруктураДействий)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Продукция, СтруктураДействий, Неопределено);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьПослеВыбора(ТипЦен,Кнопка) Экспорт
	УстановитьЦену(Кнопка,ТипЦен);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦену(Кнопка,Значение)
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Кнопка = "УстановитьЦенуПоТипу" Тогда
		СтруктураДействий = Новый Структура;
			
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,"Цены",,,Значение));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
		
		Для Каждого СтрокаТаблицы Из Объект.Продукция Цикл
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	ИначеЕсли Кнопка = "УстановитьСкидкуНаЦену" Тогда
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);
		
		Для Каждого СтрокаТаблицы Из Объект.Продукция Цикл
			СтрокаТаблицы.Цена = СтрокаТаблицы.Цена * (100 - Значение) /100;
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимРасчетаСписанияВПроизводствоПриИзменении(Элемент)
	
	Для Каждого Стр Из Объект.Ингредиенты Цикл 
		Стр.КоличествоИнгредиентаФакт = Стр.КоличествоИнгредиентаНорма;
	КонецЦикла;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСебестоимость()
	
	Себестоимость = 0;
	
	Если Не Объект.Проведен Тогда 		
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	тзНоменклатура = Объект.Ингредиенты.Выгрузить(Новый Структура("Автопередел", Ложь), "Номенклатура");
	тзНоменклатура.Свернуть("Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Номенклатура", тзНоменклатура.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("СкладКомпании", Объект.СкладКомпании);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	               |ГДЕ
	               |	ПартииТоваровКомпании.Регистратор = &Ссылка
	               |	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |	И ПартииТоваровКомпании.Номенклатура В(&Номенклатура)
	               |	И ПартииТоваровКомпании.СкладКомпании = &СкладКомпании";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Себестоимость = Выборка.Сумма;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТорговуюПлощадкуСклада(Знач Склад)
	
	Возврат Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Склад);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТипЦенСклада(Знач Склад)
	
	Возврат Справочники.СкладыКомпании.ТипЦенРозничный(Склад);
	
КонецФункции


#КонецОбласти


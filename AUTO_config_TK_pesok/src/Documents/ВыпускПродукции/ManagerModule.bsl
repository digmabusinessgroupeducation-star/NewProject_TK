
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Расходная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "РасходнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСРеализацией, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;
	
	// Расходная накладная с кодом УКТВЭД
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "РасходнаяНакладнаяУКТВЭД";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная (с кодом УКТВЭД)'; uk = 'Видаткова накладна (з кодом УКТЗЕД)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСРеализацией, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;

	// Перемещение товаров
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "ПеремещениеТоваров";
	КомандаПечати.Представление = НСтр("ru = 'Перемещение товаров';uk='Переміщення товарів'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСПеремещением, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;	
	
	// Декларация производителя
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "ДекларацияПроизводителя";
	КомандаПечати.Представление = НСтр("ru = 'Декларация производителя'; uk = 'Декларація виробника'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	// Счет на оплату покупателя
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "СчетНаОплату";
	КомандаПечати.Представление = НСтр("ru = 'Счет на оплату покупателя'; uk = 'Рахунок на оплату покупця'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСРеализацией, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;
	
	
	// Товаро-транспортная накладная (Фабрика сыра)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "ТТН_ФС";
	КомандаПечати.Представление = НСтр("ru = 'Товаро-транспортная накладная (Фабрика сыра)'; uk = 'Товаро-транспортна накладна (Фабрика сиру)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукцииСРеализацией, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыпускПродукции";
	КомандаПечати.Идентификатор = "Фасовка";
	КомандаПечати.Представление = НСтр("ru = 'Выпуск продукции(Фасовка)'; uk = 'Випук продукції (Фасовка)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 				
	
	МассивУсловий = Новый Массив;
	МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ХозОперация", Справочники.ХозОперации.ВыпускПродукции, ВидСравнения.Равно));	
	КомандаПечати.УсловияВидимости = МассивУсловий;

КонецПроцедуры

// Начало СтандартныеПодсистемы.УправлениеДоступом
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
		
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

#Область ВыгрузкаEDI

Функция СформироватьСтруктуру_DESADV(ДокументСсылка)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Док.Номер КАК NUMBER,
	|	Док.Дата КАК DATE,
	|	Док.Организация.GLN КАК SUPPLIER,
	|	Док.Контрагент.GLN КАК RECIPIENT,
	|	Док.Контрагент.GLN КАК BUYER,
	|	Док.ТочкаДоставки.GLN КАК DELIVERYPLACE,
	|	Док.Контрагент.КодПоЕДРПОУ КАК КонтрагентОКПО,
	|	Док.Контрагент.Код КАК CAMPAIGNNUMBER,
	|	ВЫБОР
	|		КОГДА Док.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Док.ДокументОснование КАК Документ.ЗаказПокупателя).ИДДокументаОтгрузки
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERNUMBER,
	|	ВЫБОР
	|		КОГДА Док.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Док.ДокументОснование КАК Документ.ЗаказПокупателя).ДатаСоздания
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERDATE,
	|	ВЫБОР
	|		КОГДА Док.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Док.ДокументОснование КАК Документ.ЗаказПокупателя).Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерЗаказа,
	|	ВЫБОР
	|		КОГДА Док.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(Док.ДокументОснование КАК Документ.ЗаказПокупателя).Дата
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаЗаказа,
	|	Док.ПодразделениеКомпании КАК ПодразделениеКомпании
	|ИЗ
	|	Документ.ВыпускПродукции КАК Док
	|ГДЕ
	|	Док.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокТовары.НомерСтроки КАК POSITIONNUMBER,
	|	ДокТовары.Номенклатура.Артикул КАК PRODUCTIDSUPPLIER,
	|	ДокТовары.Номенклатура.Наименование КАК DESCRIPTION,
	|	ДокТовары.Номенклатура.ОсновнойШтрихКод КАК PRODUCT,
	|	ДокТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.ORDERUNIT КАК ORDERUNIT,
	|	1 КАК QUANTITYOFCUINTU,
	|	ДокТовары.Количество * ДокТовары.Коэффициент КАК DELIVEREDQUANTITY,
	|	ДокТовары.Цена КАК PRICEWITHVAT,
	|	ВЫБОР
	|		КОГДА ДокТовары.Номенклатура.ТипНоменклатуры.Весовой
	|				ИЛИ ДокТовары.Номенклатура.ТипНоменклатуры.ШтучноВесовой
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Весовой,
	|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ДокТовары.ХарактеристикаНоменклатуры.ЗначениеМРЦ КАК МРЦ,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ДокТовары.Номенклатура.Артикул, 1, 2) = ДокТовары.Номенклатура.Артикул
	|			ТОГДА ""200000"" + ДокТовары.Номенклатура.Артикул + ""00000""
	|		КОГДА ПОДСТРОКА(ДокТовары.Номенклатура.Артикул, 1, 3) = ДокТовары.Номенклатура.Артикул
	|			ТОГДА ""20000"" + ДокТовары.Номенклатура.Артикул + ""00000""
	|		КОГДА ПОДСТРОКА(ДокТовары.Номенклатура.Артикул, 1, 4) = ДокТовары.Номенклатура.Артикул
	|			ТОГДА ""2000"" + ДокТовары.Номенклатура.Артикул + ""00000""
	|		КОГДА ПОДСТРОКА(ДокТовары.Номенклатура.Артикул, 1, 5) = ДокТовары.Номенклатура.Артикул
	|			ТОГДА ""200"" + ДокТовары.Номенклатура.Артикул + ""00000""
	|		КОГДА ПОДСТРОКА(ДокТовары.Номенклатура.Артикул, 1, 6) = ДокТовары.Номенклатура.Артикул
	|			ТОГДА ""20"" + ДокТовары.Номенклатура.Артикул + ""00000""
	|	КОНЕЦ КАК ШК_Артикул
	|ИЗ
	|	Документ.ВыпускПродукции.Продукция КАК ДокТовары
	|ГДЕ
	|	ДокТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокТовары.НомерСтроки";
	
	
	МассивРезультат = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультат[0].Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	
	СтруктураДокумента = Новый Структура;
	СтруктураДокумента.Вставить("ДокументКорректен",1);
	
	
	//DESADV
	СтруктураДокумента.Вставить("Дата",ВыборкаДетальныеЗаписи.DATE);
	СтруктураДокумента.Вставить("NUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	СтруктураДокумента.Вставить("DATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	СтруктураДокумента.Вставить("DELIVERYDATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERNUMBER) Тогда
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.ORDERNUMBER));
	Иначе
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.НомерЗаказа));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERDATE) Тогда
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ORDERDATE,"ДФ=""гггг-ММ-дд"""));
	Иначе
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ДатаЗаказа,"ДФ=""гггг-ММ-дд"""));
	КонецЕсли;
	
	СтруктураДокумента.Вставить("DELIVERYNOTENUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	СтруктураДокумента.Вставить("DELIVERYNOTEDATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	
	//HEAD
	СтруктураДокумента.Вставить("SUPPLIER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("BUYER",СокрЛП(ВыборкаДетальныеЗаписи.BUYER));
	СтруктураДокумента.Вставить("DELIVERYPLACE",СокрЛП(ВыборкаДетальныеЗаписи.DELIVERYPLACE));
	Если Справочники.ПодразделенияКомпании.ЭтоФабрикаСыра(ВыборкаДетальныеЗаписи.ПодразделениеКомпании) Тогда
		СтруктураДокумента.Вставить("SENDER","9864232725152");
	Иначе
		СтруктураДокумента.Вставить("SENDER","9864232378839");
	КонецЕсли;
	
	//СтруктураДокумента.Вставить("SENDER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("RECIPIENT",СокрЛП(ВыборкаДетальныеЗаписи.RECIPIENT));
	
	
	СтруктураДокумента.Вставить("HIERARCHICALID", 1);
	
	//POSITION
	POSITION=Новый ТаблицаЗначений;
	POSITION.Колонки.Добавить("POSITIONNUMBER");
	POSITION.Колонки.Добавить("PRODUCT");
	POSITION.Колонки.Добавить("DELIVEREDQUANTITY");
	
	POSITION.Колонки.Добавить("PRODUCTIDSUPPLIER");
	//	POSITION.Колонки.Добавить("QUANTITYOFCUINTU");
	POSITION.Колонки.Добавить("ORDERUNIT");
	POSITION.Колонки.Добавить("DESCRIPTION");
	POSITION.Колонки.Добавить("PRICEWITHVAT");
	POSITION.Колонки.Добавить("ADVICEPRICE");
	
	ВыборкаДетальныеЗаписи = МассивРезультат[1].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НоваяСтрока =  POSITION.Добавить();
		НоваяСтрока.POSITIONNUMBER	  = Формат(ВыборкаДетальныеЗаписи.POSITIONNUMBER,"ЧЦ=15; ЧГ=");
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.PRODUCT) Тогда
			НоваяСтрока.PRODUCT        	  = СокрЛП(ВыборкаДетальныеЗаписи.PRODUCT);
		Иначе
			НоваяСтрока.PRODUCT        	  = ВыборкаДетальныеЗаписи.ШК_Артикул;
		КонецЕсли;
		НоваяСтрока.DELIVEREDQUANTITY = Формат(ВыборкаДетальныеЗаписи.DELIVEREDQUANTITY,"ЧРД=.; ЧГ=");
		НоваяСтрока.PRODUCTIDSUPPLIER = СокрЛП(ВыборкаДетальныеЗаписи.PRODUCTIDSUPPLIER);
		НоваяСтрока.ORDERUNIT         = СокрЛП(ВыборкаДетальныеЗаписи.ORDERUNIT);
		НоваяСтрока.DESCRIPTION       = СокрЛП(Лев(ВыборкаДетальныеЗаписи.DESCRIPTION,70));
		НоваяСтрока.PRICEWITHVAT 	  = Формат(ВыборкаДетальныеЗаписи.PRICEWITHVAT, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0" );
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры) Тогда
			НоваяСтрока.ADVICEPRICE 	  = Формат(ВыборкаДетальныеЗаписи.МРЦ, "ЧЦ=15; ЧДЦ=3; ЧРД=.; ЧН=; ЧГ=0" );
		КонецЕсли;
		
	КонецЦикла;
	
	
	
	СтруктураДокумента.Вставить("POSITION",POSITION);	
	
	
	Возврат СтруктураДокумента;
	
	
	
КонецФункции

#КонецОбласти	


Процедура ПерезаполнитьИнгридиенты(Объект) Экспорт 
	
	Объект.Ингредиенты.Очистить();			
	
	Если Объект.Продукция.Количество() = 0 Тогда 		
		Возврат;
	КонецЕсли;
	
	ТаблицаРецептур = Объект.Продукция.Выгрузить(, "Рецептура, КоличествоБазовое");	
	ТаблицаРецептур.Колонки["КоличествоБазовое"].Имя = "Количество";
	ТаблицаРецептур.Свернуть("Рецептура", "Количество");
	
	МассивПустыхРецептур = ТаблицаРецептур.НайтиСтроки(Новый Структура("Рецептура", ПредопределенноеЗначение("Документ.Рецептура.ПустаяСсылка")));
	
	Если МассивПустыхРецептур.Количество() > 0 Тогда 
		Для Каждого Стр Из МассивПустыхРецептур Цикл 
			ТаблицаРецептур.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
	
	Если ТаблицаРецептур.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТаблицаИнгридиенты = Объект.Ингредиенты.Выгрузить();
	ТаблицаИнгридиенты.Колонки.Добавить("Порядок");
	
	ВидПроизводства = Объект.ВидПроизводства;
	
	Для Ит = 1 По 10 Цикл // Доступно 10 уровней вложенности рецептур
		
		Если ТаблицаРецептур.Количество() = 0 Тогда 
			Прервать;
		КонецЕсли;    
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("табРецептур", ТаблицаРецептур);
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	табРецептур.Рецептура КАК Рецептура,
		               |	табРецептур.Количество КАК Количество
		               |ПОМЕСТИТЬ втРецептуры
		               |ИЗ
		               |	&табРецептур КАК табРецептур
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	РецептураТовары.Ссылка.Продукция КАК Номенклатура,
		               |	РецептураТовары.Номенклатура КАК Ингредиент,
		               |	втРецептуры.Количество КАК КоличествоНоменклатуры,
		               |	РецептураТовары.Ссылка.Количество КАК КоличествоВРецептуре,
		               |	РецептураТовары.Ссылка.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНоменклатуры,
		               |	РецептураТовары.Ссылка.Коэффициент КАК КоэффициентНоменклатуры,
		               |	РецептураТовары.ЕдиницаИзмерения КАК ЕдиницаИзмеренияИнгредиента,
		               |	РецептураТовары.Коэффициент КАК КоэффициентИнгредиента,
		               |	РецептураТовары.Количество КАК ИнгредиентНорма,
		               |	РецептураТовары.Ссылка КАК Рецептура,
		               |	РецептураТовары.Рецептура КАК РецептураАвтопередела,
		               |	РецептураТовары.ЗапретитьАвтовыборРецептуры КАК ЗапретитьАвтовыборРецептуры,
		               |	РецептураТовары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
		               |ИЗ
		               |	втРецептуры КАК втРецептуры
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Рецептура.Товары КАК РецептураТовары
		               |		ПО втРецептуры.Рецептура = РецептураТовары.Ссылка
		               |ГДЕ
		               |	втРецептуры.Количество > 0";
		
		Запрос.УстановитьПараметр("табРецептур", ТаблицаРецептур);
		
		Выборка = Запрос.Выполнить().Выбрать();	
		
		ТаблицаРецептур.Очистить();
		
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = ТаблицаИнгридиенты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,, "РецептураАвтопередела");
			
			ОснМножитель = Выборка.КоличествоНоменклатуры;
			
			БазовоеКоличествоНоменклатура = Выборка.КоличествоВРецептуре * Выборка.КоэффициентНоменклатуры;
			БазовоеКоличествоИнгредиент = Выборка.ИнгредиентНорма * Выборка.КоэффициентИнгредиента;
			
			ИнгредиентКоличествоНорма = (БазовоеКоличествоИнгредиент / БазовоеКоличествоНоменклатура) / Выборка.КоэффициентИнгредиента * ОснМножитель;
			
			НоваяСтрока.КоличествоИнгредиентаНорма = ИнгредиентКоличествоНорма;
			НоваяСтрока.КоличествоИнгредиентаФакт = ИнгредиентКоличествоНорма;
			
			НоваяСтрока.Автопередел = Ит > 1;
			НоваяСтрока.Порядок = Ит;
			
			Если ВидПроизводства <> Перечисления.ВидыПроизводства.ЗаготовкиНеГотовятся
				И Выборка.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Продукция Тогда
				
				Если Не Выборка.ЗапретитьАвтовыборРецептуры Тогда
					Структура = Документы.Рецептура.ПолучитьАктуальнуюРецептуру(Выборка.Ингредиент, Объект.ПодразделениеКомпании, Ложь);
					Рецептура = Структура.Рецептура;
				Иначе
					Рецептура = Выборка.РецептураАвтопередела;
				КонецЕсли;
				
				Если Рецептура <> Документы.Рецептура.ПустаяСсылка() Тогда				
					НоваяСтрока.ИзАвтопеределов = Истина;
					НоваяСтрока.РецептураАвтопередела = Рецептура;
					
					НоваяРецептура = ТаблицаРецептур.Добавить();
					НоваяРецептура.Рецептура = Рецептура;
					НоваяРецептура.Количество = ИнгредиентКоличествоНорма;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЦикла;
	
	ТаблицаИнгридиенты.Сортировать("Порядок Убыв");	
	
	Объект.Ингредиенты.Загрузить(ТаблицаИнгридиенты);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыТТНПоУмолчанию(Объект) Экспорт 
		
	РеквизитыТТН = Новый Структура;
	
	НаборСвойств = УправлениеСвойствами.НаборСвойствПоИмени("Документ_ВыпускПродукции_ТТН");
	Для Каждого Стр Из НаборСвойств.ДополнительныеРеквизиты Цикл 
		Если Не Стр.ПометкаУдаления Тогда 
			РеквизитыТТН.Вставить(Стр.Свойство.Имя, Стр.Свойство);
		КонецЕсли;
	КонецЦикла;
	
	ТекДопРеквизиты = Объект.ДополнительныеРеквизиты.Выгрузить();
	
	ТаблицаДопРеквизитов = ТекДопРеквизиты.Скопировать();
	ТаблицаДопРеквизитов.Очистить();
	
	Для Каждого Стр Из ТекДопРеквизиты Цикл 
		Если Не РеквизитыТТН.Свойство(Стр.Свойство.Имя) Тогда 
			НоваяСтрока = ТаблицаДопРеквизитов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЕсли;
	КонецЦикла;

	//МассивИменСвойств = Новый Массив;
	//МассивИменСвойств.Добавить("ВП_Автомобиль");
	//МассивИменСвойств.Добавить("ВП_Прицеп");
	//МассивИменСвойств.Добавить("ВП_Водитель");
	//МассивИменСвойств.Добавить("ВП_НомерПрав");
	//МассивИменСвойств.Добавить("ВП_Перевозчик");
	//МассивИменСвойств.Добавить("ВП_ИННПеревозчика");
	//МассивИменСвойств.Добавить("ВП_ПунктОтправитель");
	//МассивИменСвойств.Добавить("ВП_ПунктПолучатель");	
	
	Если Объект.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 				
	
		МассивИменСвойствДоговора = Новый Массив;
		МассивИменСвойствДоговора.Добавить("ТТН_Автомобиль");
		МассивИменСвойствДоговора.Добавить("ТТН_Заказчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Перевозчик");
		МассивИменСвойствДоговора.Добавить("ТТН_Водитель");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВыпускПродукции.СкладКомпании.ТочкаДоставки КАК ПунктПогрузки,
		               |	ВыпускПродукции.ТочкаДоставки КАК ПунктРазгрузки
		               |ИЗ
		               |	Документ.ВыпускПродукции КАК ВыпускПродукции
		               |ГДЕ
		               |	ВыпускПродукции.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДополнительныеСведения.Свойство.Имя КАК СвойствоИмя,
		               |	ДополнительныеСведения.Значение КАК Значение
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Объект = &Договор
		               |	И ДополнительныеСведения.Свойство.Имя В(&МассивСвойств)";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
		Запрос.УстановитьПараметр("Договор", Объект.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("МассивСвойств", МассивИменСвойствДоговора);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ВыборкаРеквизиты  = МассивРезультатов[0].Выбрать();
		ВыборкаПоДоговору = МассивРезультатов[1].Выбрать();
		
		Если ВыборкаРеквизиты.Следующий() Тогда 
			ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_ПунктОтправитель", ВыборкаРеквизиты.ПунктПогрузки, РеквизитыТТН);			
			ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_ПунктПолучатель", ВыборкаРеквизиты.ПунктРазгрузки, РеквизитыТТН);
		КонецЕсли;
		
		Пока ВыборкаПоДоговору.Следующий() Цикл 
			Если ВыборкаПоДоговору.СвойствоИмя = "ТТН_Автомобиль" Тогда 
				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_Автомобиль", ВыборкаПоДоговору.Значение, РеквизитыТТН);				
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Водитель" Тогда 
				
				Водитель = ВыборкаПоДоговору.Значение;				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_Водитель", Водитель, РеквизитыТТН);				
				
				НомерПрав = "";
				Если ТипЗнч(Водитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
					НомерПрав = Справочники.ФизическиеЛица.ПолучитьНомерВодительскогоУдостоверения(Водитель, Объект.Дата);
				КонецЕсли;
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_НомерПрав", НомерПрав, РеквизитыТТН);				
				
			ИначеЕсли ВыборкаПоДоговору.СвойствоИмя = "ТТН_Перевозчик" Тогда 				
				
				Перевозчик = ВыборкаПоДоговору.Значение;
				ТипРеквизитаПеревозчик = ТипЗнч(Перевозчик);
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_Перевозчик", Перевозчик, РеквизитыТТН);
				
				ИННПеревозчика = "";
				Если ТипРеквизитаПеревозчик <> Тип("Строка") Тогда 
					Если ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Организации") Тогда 
						Запрос = Новый Запрос;
						Запрос.Текст = "ВЫБРАТЬ
						|	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация,
						|	УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС КАК ИННПлательщикаНДС
						|ИЗ
						|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Дата, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
						Запрос.УстановитьПараметр("Дата", Объект.Дата);
						Запрос.УстановитьПараметр("Организация", Перевозчик);
						
						ВыборкаИНН = Запрос.Выполнить().Выбрать();        
						Если ВыборкаИНН.Следующий() Тогда 
							ИННПеревозчика = ВыборкаИНН.ИННПлательщикаНДС;	
						КонецЕсли;
					ИначеЕсли ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Контрагенты") Тогда 
						ИННПеревозчика = Перевозчик.ИННПлательщикаНДС;
					КонецЕсли;
				КонецЕсли;		
				
				ДобавитьДопРеквизит(ТаблицаДопРеквизитов, "ВП_ИННПеревозчика", ИННПеревозчика, РеквизитыТТН);
			
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;

	УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Объект.Ссылка, ТаблицаДопРеквизитов);
	
КонецПроцедуры

Процедура ДобавитьДопРеквизит(ТаблицаДопРеквизитов, ИмяСвойства, Значение, СтруктураДоступныхСвойств)	
	
	Если Не ЗначениеЗаполнено(Значение) ИЛИ Не ЗначениеЗаполнено(ИмяСвойства) Тогда 
		Возврат;
	КонецЕсли;
	
	Свойство = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоступныхСвойств, ИмяСвойства, Неопределено);
	
	Если Не ЗначениеЗаполнено(Свойство) Тогда 
		Возврат;
	КонецЕсли;
	
	мПроверка = ТаблицаДопРеквизитов.НайтиСтроки(Новый Структура("Свойство", Свойство));
	Если мПроверка.Количество() > 0 Тогда 
		Для Каждого Стр Из мПроверка Цикл 
			ТаблицаДопРеквизитов.Удалить(Стр);
		КонецЦикла;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаДопРеквизитов.Добавить();
	НоваяСтрока.Свойство = Свойство;
	НоваяСтрока.Значение = Значение;
	
КонецПроцедуры

#КонецОбласти	



#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	            	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		
		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией
			ИЛИ ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
			
			ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);
			ТекстЗапосаВтТаблицаЗаказы(Запрос, ТекстыЗапроса);
		Иначе 
			ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);	
		КонецЕсли;
		
		ТекстЗапросаВтПроизводство(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВыпускПродукцииНовый(Запрос, ДополнительныеСвойства);		
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
		
		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 
			ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);			
		КонецЕсли;
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") Тогда
		
		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией
			ИЛИ ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
			
			ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);
			ТекстЗапосаВтТаблицаЗаказы(Запрос, ТекстыЗапроса);
		Иначе 
			ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);	
		КонецЕсли;
		
		ТекстЗапросаВтПроизводство(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуРасчетыСПокупателями(Запрос, ДополнительныеСвойства);				
		
	Иначе
				
		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией
			ИЛИ ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
			
			ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);
			ТекстЗапосаВтТаблицаЗаказы(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры, ДополнительныеСвойства.ДляПроведения.ХозОперация);		
		ТекстЗапросаТаблицаПроизводство(Запрос, ТекстыЗапроса, Регистры);		
		
		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 
			ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры);
		КонецЕсли;
		
		// Исполнение запроса и выгрузка полученных таблиц для движений.
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВыпускПродукцииНовый(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);

		Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда
			ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуРасчетыСПокупателями(Запрос, ДополнительныеСвойства);
			ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);	
			
			ЗаказыСервер.ПодготовитьТаблицуЗакрытияЗаказовПокупателя(Запрос, ДополнительныеСвойства);
		ИначеЕсли ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
			ЗаказыСервер.ПодготовитьТаблицуЗакрытияЗаказовПокупателя(Запрос, ДополнительныеСвойства);
		КонецЕсли;				
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыпускПродукции.Дата КАК Период,
	|	ВыпускПродукции.Ссылка КАК Ссылка,
	|	ВыпускПродукции.ХозОперация КАК ХозОперация,
	|	ВыпускПродукции.Организация КАК Организация,
	|	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ВыпускПродукции.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	|	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ВыпускПродукции.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	|	ВыпускПродукции.РежимИспользованияАналогов КАК РежимИспользованияАналогов,
	|	ВыпускПродукции.РежимРасчетаСписанияВПроизводство КАК РежимРасчетаСписанияВПроизводство,
	|	ВыпускПродукции.ВидПроизводства КАК ВидПроизводства,
	|	ВыпускПродукции.Контрагент КАК Контрагент,
	|	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ВыпускПродукции.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
	|	ВыпускПродукции.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	|	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки
	|ИЗ
	|	Документ.ВыпускПродукции КАК ВыпускПродукции
	|ГДЕ
	|	ВыпускПродукции.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("МоментВремени",                            Новый МоментВремени(Реквизиты.Период,ДокументСсылка));
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("ХозОперация",							  Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Контрагент",								  Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",					  Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("СкладКомпанииПолучатель",				  Реквизиты.СкладКомпанииПолучатель);
	Запрос.УстановитьПараметр("ТорговаяПлощадкаПолучатель",				  Реквизиты.ТорговаяПлощадкаПолучатель);
	Запрос.УстановитьПараметр("РегламентированныйУчет",					  Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("ЭтоВыпускСРеализацией",			  		  Реквизиты.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией);
	
		
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка", 		Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", 			Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости", 	Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",	Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",			Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("РежимИспользованияАналогов", Реквизиты.РежимИспользованияАналогов);
	ДополнительныеСвойства.ДляПроведения.Вставить("РежимРасчетаСписанияВПроизводство", Реквизиты.РежимРасчетаСписанияВПроизводство);
	ДополнительныеСвойства.ДляПроведения.Вставить("ВидПроизводства", 		Реквизиты.ВидПроизводства);
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент",				Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов",  Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента",			1);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпанииПолучатель",Реквизиты.СкладКомпанииПолучатель);
	
КонецПроцедуры

Функция ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Контрагент КАК Контрагент,
	               |	&ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	Товары.НомерСтроки КАК НомерСтроки,
	               |	Товары.Номенклатура КАК Номенклатура,
	               |	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Товары.Коэффициент КАК Коэффициент,
	               |	Товары.Количество КАК Количество,
	               |	Товары.КоличествоБазовое КАК КоличествоБазовое,
	               |	Товары.Цена КАК Цена,
	               |	Товары.СтавкаНДС КАК СтавкаНДС,
	               |	Товары.Сумма КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА &РегламентированныйУчет
	               |			ТОГДА Товары.СуммаНДС
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНДС,
	               |	Товары.СуммаВсего КАК СуммаВсего,
	               |	Товары.Рецептура КАК Рецептура,
	               |	0 КАК СуммаАкцизногоНалога,
	               |	ЛОЖЬ КАК Возврат,
	               |	ЗНАЧЕНИЕ(Справочник.КлассификаторПродаж.ПустаяСсылка) КАК КлассификаторПродажи,
	               |	0 КАК Порядок,
	               |	Товары.СуммаСкидки КАК СуммаСкидки
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ВыпускПродукции.Продукция КАК Товары
	               |ГДЕ
	               |	Товары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтТаблицаТара";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВыпускПродукцииТара.Номенклатура КАК Номенклатура,
	               |	ВыпускПродукцииТара.Количество КАК Количество,
	               |	ВыпускПродукцииТара.Цена КАК Цена,
	               |	ВыпускПродукцииТара.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ВтТаблицаТара
	               |ИЗ
	               |	Документ.ВыпускПродукции.Тара КАК ВыпускПродукцииТара
	               |ГДЕ
	               |	ВыпускПродукцииТара.Ссылка = &Ссылка
	               |	И &ЭтоВыпускСРеализацией";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);		
	
	Возврат ТекстыЗапроса;
	
КонецФункции

Функция ТекстЗапосаВтТаблицаЗаказы(Запос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаЗаказы";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВыпускПродукции.ДокументОснование КАК ЗаказПокупателя
	               |ПОМЕСТИТЬ ВтТаблицаЗаказы
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &Ссылка
	               |	И ВыпускПродукции.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВыпускПродукцииРаспределениеОтгрузки.ЗаказПокупателя
	               |ИЗ
	               |	Документ.ВыпускПродукции.РаспределениеОтгрузки КАК ВыпускПродукцииРаспределениеОтгрузки
	               |ГДЕ
	               |	ВыпускПродукцииРаспределениеОтгрузки.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстыЗапроса;		
КонецФункции

Функция ТекстЗапросаВтПроизводство(Запрос, ТекстыЗапроса)
			
	ИмяРегистра = "ВтТаблицаИнгредиенты";	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВыпускПродукцииИнгредиенты.НомерСтроки КАК НомерСтроки,
	               |	ВыпускПродукцииИнгредиенты.Рецептура КАК Рецептура,
	               |	ВыпускПродукцииИнгредиенты.Номенклатура КАК Номенклатура,
	               |	ВыпускПродукцииИнгредиенты.Ингредиент КАК Ингредиент,
	               |	ВыпускПродукцииИнгредиенты.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
	               |	ВыпускПродукцииИнгредиенты.ЕдиницаИзмеренияНоменклатуры КАК ЕдиницаИзмеренияНоменклатуры,
	               |	ВыпускПродукцииИнгредиенты.КоэффициентНоменклатуры КАК КоэффициентНоменклатуры,
	               |	ВыпускПродукцииИнгредиенты.ЕдиницаИзмеренияИнгредиента КАК ЕдиницаИзмеренияИнгредиента,
	               |	ВыпускПродукцииИнгредиенты.КоэффициентИнгредиента КАК КоэффициентИнгредиента,
	               |	ВыпускПродукцииИнгредиенты.КоличествоИнгредиентаНорма КАК КоличествоИнгредиентаНорма,
	               |	ВыпускПродукцииИнгредиенты.КоличествоИнгредиентаФакт КАК КоличествоИнгредиентаФакт,
	               |	ВыпускПродукцииИнгредиенты.Автопередел КАК Автопередел,
	               |	ВыпускПродукцииИнгредиенты.ИзАвтопеределов КАК ИзАвтопеределов,
	               |	ВыпускПродукцииИнгредиенты.РецептураАвтопередела КАК РецептураАвтопередела
	               |ПОМЕСТИТЬ ВтТаблицаИнгредиенты
	               |ИЗ
	               |	Документ.ВыпускПродукции.Ингредиенты КАК ВыпускПродукцииИнгредиенты
	               |ГДЕ
	               |	ВыпускПродукцииИнгредиенты.Ссылка = &Ссылка";
				   
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтТаблицаПродукция";	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВтТаблицаИнгредиенты.Номенклатура КАК Номенклатура,
	               |	ВтТаблицаИнгредиенты.Рецептура КАК Рецептура,
	               |	ВтТаблицаИнгредиенты.Автопередел КАК Автопередел,
	               |	МАКСИМУМ(ВтТаблицаИнгредиенты.КоличествоНоменклатуры) КАК Количество
	               |ПОМЕСТИТЬ ВтТаблицаПродукция
	               |ИЗ
	               |	ВтТаблицаИнгредиенты КАК ВтТаблицаИнгредиенты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаИнгредиенты.Номенклатура,
	               |	ВтТаблицаИнгредиенты.Автопередел,
	               |	ВтТаблицаИнгредиенты.Рецептура";
				   
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры, ХозОперация)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаИнгредиенты", ТекстыЗапроса) Тогда
		ТекстЗапросаВтПроизводство(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&Период КАК Период,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	ВтТаблицаИнгредиенты.Ингредиент КАК Номенклатура,
	               |	СУММА(ВтТаблицаИнгредиенты.КоличествоИнгредиентаФакт * ВтТаблицаИнгредиенты.КоэффициентИнгредиента) КАК Количество,
	               |	&ХозОперация КАК ХозОперация
	               |ИЗ
	               |	ВтТаблицаИнгредиенты КАК ВтТаблицаИнгредиенты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаИнгредиенты.Ингредиент
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВтТаблицаИнгредиенты.КоличествоИнгредиентаФакт * ВтТаблицаИнгредиенты.КоэффициентИнгредиента) > 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	&Период,
	               |	&СкладКомпании,
	               |	ВтТаблицаПродукция.Номенклатура,
	               |	СУММА(ВтТаблицаПродукция.Количество),
	               |	&ХозОперация
	               |ИЗ
	               |	ВтТаблицаПродукция КАК ВтТаблицаПродукция
	               |ГДЕ
	               |	ВтТаблицаПродукция.Автопередел
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаПродукция.Номенклатура
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	               |	&Период,
	               |	&СкладКомпании,
	               |	ВтТаблицаПродукция.Номенклатура,
	               |	СУММА(ВтТаблицаПродукция.Количество),
	               |	&ХозОперация
	               |ИЗ
	               |	ВтТаблицаПродукция КАК ВтТаблицаПродукция
	               |ГДЕ
	               |	ВтТаблицаПродукция.Автопередел
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаПродукция.Номенклатура
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	&Период,
	               |	&СкладКомпании,
	               |	ВтТаблицаПродукция.Номенклатура,
	               |	СУММА(ВтТаблицаПродукция.Количество),
	               |	&ХозОперация
	               |ИЗ
	               |	ВтТаблицаПродукция КАК ВтТаблицаПродукция
	               |ГДЕ
	               |	НЕ ВтТаблицаПродукция.Автопередел
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаПродукция.Номенклатура";

	Если ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + 
					"ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	               |	&Период,
	               |	&СкладКомпании,
	               |	ВтТаблицаПродукция.Номенклатура,
	               |	СУММА(ВтТаблицаПродукция.Количество),
	               |	&ХозОперация
	               |ИЗ
	               |	ВтТаблицаПродукция КАК ВтТаблицаПродукция
	               |ГДЕ
	               |	НЕ ВтТаблицаПродукция.Автопередел
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаПродукция.Номенклатура
				   |
				   |ОБЪЕДИНИТЬ ВСЕ
				   |
				   |ВЫБРАТЬ
				   |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
				   |	&Период,
				   |	&СкладКомпании,
				   |	ВтТаблицаТара.Номенклатура,
				   |	СУММА(ВтТаблицаТара.Количество),
				   |	ЗНАЧЕНИЕ(Справочник.ХозОперации.ВзаиморасчетыПоТаре)
				   |ИЗ
				   |	ВтТаблицаТара КАК ВтТаблицаТара
				   |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаТара.Номенклатура"; 
				   
	ИначеЕсли ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + 
					"ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
					|	&Период,
					|	&СкладКомпании,
					|	ВтТаблицаПродукция.Номенклатура,
					|	СУММА(ВтТаблицаПродукция.Количество),
					|	&ХозОперация
					|ИЗ
					|	ВтТаблицаПродукция КАК ВтТаблицаПродукция
					|ГДЕ
					|	НЕ ВтТаблицаПродукция.Автопередел
					|
					|СГРУППИРОВАТЬ ПО
					|	ВтТаблицаПродукция.Номенклатура
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ
					|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
					|	&Период,
					|	&СкладКомпанииПолучатель,
					|	ВтТаблицаПродукция.Номенклатура,
					|	СУММА(ВтТаблицаПродукция.Количество),
					|	&ХозОперация
					|ИЗ
					|	ВтТаблицаПродукция КАК ВтТаблицаПродукция
					|ГДЕ
					|	НЕ ВтТаблицаПродукция.Автопередел
					|
					|СГРУППИРОВАТЬ ПО
					|	ВтТаблицаПродукция.Номенклатура" 					
	КонецЕсли;
	          		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВозвратнаяТара";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТара", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
				  |	&Период КАК Период,
				  |	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	              |	&Организация КАК Организация,
	              |	&Контрагент КАК Контрагент,
	              |	ВтТаблицаТара.Номенклатура КАК Номенклатура,
	              |	СУММА(ВтТаблицаТара.Количество) КАК Количество,
	              |	СУММА(ВтТаблицаТара.Сумма) КАК Сумма
	              |ИЗ
	              |	ВтТаблицаТара КАК ВтТаблицаТара
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТара.Номенклатура
	              |
	              |ИМЕЮЩИЕ
	              |	СУММА(ВтТаблицаТара.Количество) <> 0"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПроизводство(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "Производство";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаИнгредиенты", ТекстыЗапроса) Тогда
		ТекстЗапросаВтПроизводство(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ВтТаблицаИнгредиенты.Номенклатура КАК Продукция,
	               |	ВтТаблицаИнгредиенты.Ингредиент КАК Ингредиент,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	ВтТаблицаИнгредиенты.Рецептура КАК Рецептура,
	               |	СУММА(ВтТаблицаИнгредиенты.КоличествоНоменклатуры) КАК КоличествоПродукции,
	               |	СУММА(ВтТаблицаИнгредиенты.КоличествоИнгредиентаФакт * ВтТаблицаИнгредиенты.КоэффициентИнгредиента) КАК КоличествоИнгридиента,
	               |	СУММА(ВтТаблицаИнгредиенты.КоличествоИнгредиентаНорма * ВтТаблицаИнгредиенты.КоэффициентИнгредиента) КАК КоличествоИнгридиентаНорма,
	               |	&ХозОперация КАК ХозОперация
	               |ИЗ
	               |	ВтТаблицаИнгредиенты КАК ВтТаблицаИнгредиенты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаИнгредиенты.Ингредиент,
	               |	ВтТаблицаИнгредиенты.Номенклатура,
	               |	ВтТаблицаИнгредиенты.Рецептура";	
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти



#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = ДополнительныеСвойства.ДляПроведения.СкладКомпании;
	НоваяСтрока.Период   	  = ДополнительныеСвойства.ДляПроведения.Дата;
	
	Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСПеремещением Тогда 
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор   = Ссылка;
		НоваяСтрока.СкладКомпании = ДополнительныеСвойства.ДляПроведения.СкладКомпанииПолучатель;
		НоваяСтрока.Период   	  = ДополнительныеСвойства.ДляПроведения.Дата;
	КонецЕсли;
	
	ТаблицыДляПоследовательности.Вставить("ПартииТоваров", ТаблицаПоследовательности);
	
	Если ДополнительныеСвойства.ДляПроведения.ХозОперация = Справочники.ХозОперации.ВыпускПродукцииСРеализацией Тогда 
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор        = Ссылка;
		НоваяСтрока.ДоговорКонтрагента = ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
		НоваяСтрока.Период             = ДополнительныеСвойства.ДляПроведения.Дата;
		
		ТаблицыДляПоследовательности.Вставить("Взаиморасчеты", ТаблицаПоследовательности);	
	КонецЕсли;	
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры


#КонецОбласти



#Область Печать

	
// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если ТипЗнч(ПараметрыПечати) <> Тип("Структура") Тогда 
		ПараметрыПечати = Новый Структура;
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладная") Тогда   // Расходная накладная
		
		ПараметрыПечати.Вставить("QR_Заголовок", Ложь);
		ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
		ПараметрыПечати.Вставить("ВыводитьТМ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Ложь);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РасходнаяНакладная",
			НСтр("ru = 'Расходная накладная';uk='Видаткова накладна'"), 
			ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_РасходнаяНакладная");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяУКТВЭД") Тогда   // Расходная накладная (УКТВЭД)
		
		ПараметрыПечати.Вставить("QR_Заголовок", Ложь);
		ПараметрыПечати.Вставить("ШапкаКратко", Ложь);
		ПараметрыПечати.Вставить("ВыводитьТМ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьМРЦ", Ложь);
		ПараметрыПечати.Вставить("ВыводитьУКТВЭД", Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РасходнаяНакладнаяУКТВЭД",
			НСтр("ru = 'Расходная накладная (с кодом УКТВЭД)'; uk = 'Видаткова накладна (з кодом УКТЗЕД)'"), 
			ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_РасходнаяНакладная");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПеремещениеТоваров") Тогда    
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПеремещениеТоваров",
			НСтр("ru = 'Перемещение товаров';uk='Переміщення товарів'"), 
			ПечатьПеремещениеТоваров(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваров");

	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДекларацияПроизводителя") Тогда 			
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ДекларацияПроизводителя",
			НСтр("ru = 'Декларация производителя'; uk = 'Декларація виробника'"), 
			ПечатьДекларацияПроизводителя(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_ДекларацияПроизводителя");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетНаОплату") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетНаОплату",
			НСтр("ru = 'Счет на оплату покупателя'; uk = 'Рахунок на оплату покупця'"), 
			ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_СчетНаОплату");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН_ФС") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ТТН_ФС",
			НСтр("ru = 'Товаро-транспортная накладная (Фабрика сыра)'; uk = 'Товаро-транспортна накладна (Фабрика сиру)'"), 
			ПечатьТТН_ФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ТК_ТТН.ПФ_MXL_ТТН_ФС");	
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Фасовка") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Фасовка",
			НСтр("ru = 'Выпуск продукции(Фасовка)'; uk = 'Випуск подукції (фавування)'"), 
			ПечатьФасовки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ВыпускПродукции.ПФ_MXL_ВыпускФасовка");	
	
	КонецЕсли;
	
	
КонецПроцедуры


// Расходная накладная
Функция ПечатьРасходнаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыводитьУКТВЭД	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьУКТВЭД", Ложь);

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.Ссылка КАК Ссылка,
	               |	ВыпускПродукции.Дата КАК Дата,
	               |	ВыпускПродукции.Номер КАК Номер,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВыпускПродукции.Комментарий КАК Комментарий,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ДоговорНаименование,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВыпускПродукции.ДокументОснование КАК ДокументОснование,
	               |	ВыпускПродукции.Продукция.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Наименование2 КАК НоменклатураНаименование,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Продукция,
	               |	ВыпускПродукции.Тара.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Товар,
	               |		Количество КАК Количество,
	               |		Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Тара
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка В(&МассивСсылок)
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВыпускПродукции.МоментВремени";
		
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_РасходнаяНакладная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	

	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("ЗаголовокU_Track");
	ОбластьФизОбмен             = Макет.ПолучитьОбласть("ОбластьОбмен");
	ОбластьДопИнформация		= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьПоставщик 			= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 			= Макет.ПолучитьОбласть("Покупатель");
	
	ОбластьШапка_нпп 			= Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");	
	ОбластьШапка_УКТВЭД			= Макет.ПолучитьОбласть("ШапкаТаблицы|КодУКТВЭД");		
	ОбластьШапка_Код			= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");		
	ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
	ОбластьШапка_Скидка			= Макет.ПолучитьОбласть("ШапкаТаблицы|Скидка");		
	ОбластьШапка_Сумма			= Макет.ПолучитьОбласть("ШапкаТаблицы|Сумма");		
	
	ОбластьСтрока_нпп 			= Макет.ПолучитьОбласть("Строка|НомерСтроки");	
	ОбластьСтрока_УКТВЭД		= Макет.ПолучитьОбласть("Строка|КодУКТВЭД");		
	ОбластьСтрока_Код			= Макет.ПолучитьОбласть("Строка|КолонкаКодов");		
	ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
	ОбластьСтрока_Скидка	    = Макет.ПолучитьОбласть("Строка|Скидка");
	ОбластьСтрока_Сумма			= Макет.ПолучитьОбласть("Строка|Сумма");		
	
	ОбластьИтого_нпп 			= Макет.ПолучитьОбласть("Итого|НомерСтроки");	
	ОбластьИтого_Код			= Макет.ПолучитьОбласть("Итого|КолонкаКодов");		
	ОбластьИтого_УКТВЭД			= Макет.ПолучитьОбласть("Итого|КодУКТВЭД");		
	ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
	ОбластьИтого_Скидка	    	= Макет.ПолучитьОбласть("Итого|Скидка");
	ОбластьИтого_Сумма			= Макет.ПолучитьОбласть("Итого|Сумма");
	
	ОбластьИтогоНДС				= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью		= Макет.ПолучитьОбласть("СуммаПрописью");	
	ОбластьПодписи 				= Макет.ПолучитьОбласть("Подписи_Новые");
	
	ОбластьКолонкаТовар 		= Макет.Область("Товар");
	
	ОбластьШапкаТара			= Макет.ПолучитьОбласть("ШапкаТаблицыТара");
	ОбластьСтрокаТара			= Макет.ПолучитьОбласть("СтрокаТара");
	ОбластьИтогоТара			= Макет.ПолучитьОбласть("ИтогоТара");
	ОбластьПробел				= Макет.ПолучитьОбласть("Пробел");
	
	ШиринаКолонкаТовар 	= ОбластьКолонкаТовар.ШиринаКолонки;
	ШиринаКолонкиКод 	= Макет.Область("КолонкаКодов").ШиринаКолонки;
	ШиринаКолонкиСкидка	= Макет.Область("СуммаБезСкидки").ШиринаКолонки + Макет.Область("СуммаСкидки").ШиринаКолонки;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		РеглУчет = ВыборкаДокументов.РегламентированныйУчет;
		ЭтоФизобмен = Ложь;
		Если ЗначениеЗаполнено(ВыборкаДокументов.ДокументОснование) И ТипЗнч(ВыборкаДокументов.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя")
			И ВыборкаДокументов.ДокументОснование.ХозОперация = Справочники.ХозОперации.ЗаказПокупателяФизОбмен Тогда
			ЭтоФизобмен = Истина;	
		КонецЕсли;
		
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							?(ВыборкаДокументов.РегламентированныйУчет, 
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
							Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
		
		СтруктураШапки = Новый Структура;
		Если ВыводитьУКТВЭД Тогда 
			СтруктураШапки.Вставить("ИмяКолонкиКодов", НСтр("ru = 'ШтрихКод'; uk = 'ШтрихКод'", КодЯзыкаПечать));
		Иначе 
			СтруктураШапки.Вставить("ИмяКолонкиКодов", НСтр("ru = 'Артикул'; uk = 'Артикул'", КодЯзыкаПечать));
		КонецЕсли;
		СтруктураШапки.Вставить("Цена", ?(РеглУчет, НСтр("ru = 'Цена с НДС'; uk = 'Ціна з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Цена'; uk = 'Ціна'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("Сумма", ?(РеглУчет, НСтр("ru = 'Сумма с НДС'; uk = 'Сума з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Сумма'; uk = 'Сума'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
		СтруктураШапки.Вставить("ТекстЗаголовка", Заголовок);				
		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ВыборкаДокументов.ОрганизацияНаименованиеПолное);
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		
		СтруктураШапки.Вставить("ПредставлениеПокупателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		СтруктураШапки.Вставить("РеквизитыПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		
		МассивДопИнформации = Новый Массив;
		Если ЗначениеЗаполнено(ВыборкаДокументов.ДоговорВзаиморасчетов) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Договор:'; uk = 'Договір:'", КодЯзыкаПечать),
					?(ЗначениеЗаполнено(ВыборкаДокументов.ДоговорНаименование), СокрЛП(ВыборкаДокументов.ДоговорНаименование), ВыборкаДокументов.ДоговорВзаиморасчетов)));
					
		КонецЕсли;
				
		Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Точка доставки:'; uk = 'Точка доставки:'", КодЯзыкаПечать),
					ВыборкаДокументов.ТочкаДоставки));			
		КонецЕсли;
		
		Если Не РеглУчет И Не ПустаяСтрока(ВыборкаДокументов.Комментарий) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Комментарий:'; uk = 'Коментар:'", КодЯзыкаПечать),
					СокрЛП(ВыборкаДокументов.Комментарий)));						
		КонецЕсли;
		
		Попытка
			ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор()),1,125);
			ОбластьЗаголовок.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
		Исключение КонецПопытки;			

		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		Если ЭтоФизобмен Тогда
			ТабличныйДокумент.Вывести(ОбластьФизОбмен);
		КонецЕсли;
		
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		тзТовары = ВыборкаДокументов.Продукция.Выгрузить();
		тзТара = ВыборкаДокументов.Тара.Выгрузить();
		
		ВыводитьАртикул = Истина;				
		ВыводитьСкидки 	= тзТовары.Итог("СуммаСкидки") <> 0;
		ОбластьКолонкаТовар.ШиринаКолонки = ШиринаКолонкаТовар + ?(Не ВыводитьАртикул, ШиринаКолонкиКод, 0) + ?(Не ВыводитьСкидки, ШиринаКолонкиСкидка, 0);
		
		ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
		ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
		ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
		
		СтруктураШапки.Вставить("ВсегоСуммаБезСкидки", тзТовары.Итог("Сумма"));
		СтруктураШапки.Вставить("ВсегоСуммаСкидки", тзТовары.Итог("СуммаСкидки"));
		СтруктураШапки.Вставить("Всего", тзТовары.Итог("СуммаВсего"));
		СтруктураШапки.Вставить("ВсегоНДС", тзТовары.Итог("СуммаНДС"));
		
		//Вывод шапки таблицы Продцукия
		ТабличныйДокумент.Вывести(ОбластьШапка_нпп);
		
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_УКТВЭД);
		КонецЕсли;
		
		Если ВыводитьАртикул Тогда 
			ОбластьШапка_Код.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьШапка_Код);
		КонецЕсли;
		
		ОбластьШапка_Данные.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Данные);
		
		Если ВыводитьСкидки Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_Скидка);
		КонецЕсли;
		
		ОбластьШапка_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Сумма);
		
		// Вывод строк таблицы		
		нпп = 0;
		Для Каждого Стр Из тзТовары Цикл 
			нпп = нпп + 1;
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			Если ВыводитьУКТВЭД Тогда 
				СтруктураСтроки.Вставить("Артикул", Стр.Номенклатура.ОсновнойШтрихкод);
			Иначе
				СтруктураСтроки.Вставить("Артикул", Стр.Артикул);
			КонецЕсли;
			СтруктураСтроки.Вставить("КодУКТВЭД", Стр.Номенклатура.КодУКТВЭД);
			СтруктураСтроки.Вставить("Товар", ?(ЗначениеЗаполнено(Стр.НоменклатураНаименование), Стр.НоменклатураНаименование, Стр.Номенклатура));
			СтруктураСтроки.Вставить("Количество", Стр.Количество);
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Стр.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("Цена", Стр.Цена);
			СтруктураСтроки.Вставить("Сумма", Стр.СуммаВсего);
			
			Если ВыводитьСкидки Тогда 
				СтруктураСтроки.Вставить("СуммаБезСкидки", Стр.Сумма);
				СтруктураСтроки.Вставить("СуммаСкидки", Стр.СуммаСкидки);
			КонецЕсли;
			
			ОбластьСтрока_нпп.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока_нпп);
			
			Если ВыводитьУКТВЭД Тогда 
				ОбластьСтрока_УКТВЭД.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_УКТВЭД);
			КонецЕсли;
			
			Если ВыводитьАртикул Тогда 
				ОбластьСтрока_Код.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Код);
			КонецЕсли;
				
			ОбластьСтрока_Данные.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Данные);
			
			Если ВыводитьСкидки Тогда 
				ОбластьСтрока_Скидка.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Скидка);
			КонецЕсли;
			
			ОбластьСтрока_Сумма.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Сумма);			
		КонецЦикла;

		ТабличныйДокумент.Вывести(ОбластьИтого_нпп);
		
		Если ВыводитьУКТВЭД Тогда 
			ТабличныйДокумент.Присоединить(ОбластьИтого_УКТВЭД);
		КонецЕсли;

		Если ВыводитьАртикул Тогда 
			ТабличныйДокумент.Присоединить(ОбластьИтого_Код);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьИтого_Данные);
		
		Если ВыводитьСкидки Тогда 
			ОбластьИтого_Скидка.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьИтого_Скидка);
		КонецЕсли;

		ОбластьИтого_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьИтого_Сумма);
		
		Если РеглУчет Тогда 
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		КонецЕсли;
		
		Если тзТара.Количество() > 0 Тогда 						
			ОбластьШапкаТара.Параметры.ИмяКолонкиКодов = НСтр("ru = 'Артикул'; uk = 'Артикул'");
			ТабличныйДокумент.Вывести(ОбластьПробел);
			ТабличныйДокумент.Вывести(ОбластьШапкаТара);
			
			Для Каждого СтрТара Из тзТара Цикл 
				ОбластьСтрокаТара.Параметры.Заполнить(СтрТара);
				ТабличныйДокумент.Вывести(ОбластьСтрокаТара);
			КонецЦикла;
			
			ОбластьИтогоТара.Параметры.Всего = тзТара.Итог("Сумма");
			ТабличныйДокумент.Вывести(ОбластьИтогоТара);
		КонецЕсли;		
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Всего наименований %1 на сумму %2 %3.'; uk = 'Всього найменувань %1 на суму %2 %3.'", КодЯзыкаПечать),
							нпп, 
							СтруктураШапки.Всего,
							ВыборкаДокументов.ВалютаДокумента);
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СтруктураШапки.Всего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		Если РеглУчет Тогда 
			СуммаНДСПрописью = ЧислоПрописью(СтруктураШапки.ВсегоНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
			СуммаПрописью = СуммаПрописью + Символы.ПС + НСтр("ru = 'В т.ч. НДС: '; uk = 'У т.ч. ПДВ: '", КодЯзыкаПечать) + СуммаНДСПрописью;
		КонецЕсли;
		
		СтруктураШапки.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		СтруктураШапки.Вставить("СуммаПрописью", СуммаПрописью);
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		Если ТипЗнч(Ответственный) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
			СтрокаОтветственный = НСтр("ru = 'Директор '; uk = 'Директор '", КодЯзыкаПечать) + Ответственный;
		Иначе
			СтрокаОтветственный = "";
		КонецЕсли;
		
		СтруктураШапки.Вставить("Ответственный", СтрокаОтветственный);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
			
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
		ПервыйДокумент = Ложь;		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// ПеремещениеТоваров
Функция ПечатьПеремещениеТоваров(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	Порядок		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
	СсылкаТТН	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "СсылкаТТН", "");
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокСигаретный,ТорговаяПлощадкаПолучатель";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокПивной,ТорговаяПлощадкаПолучатель";
	ИначеЕсли Порядок = "ПакетДокументов" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	Дата,ТорговаяПлощадкаПолучатель";
	КонецЕсли;		
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПеремещениеТоваров.Ссылка КАК Ссылка,
	               |	ПеремещениеТоваров.Номер КАК Номер,
	               |	ПеремещениеТоваров.Дата КАК Дата,
	               |	ПеремещениеТоваров.ХозОперация КАК ХозОперация,
	               |	ПеремещениеТоваров.Организация КАК Организация,
	               |	ПеремещениеТоваров.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоЕДРПОУ,
	               |	ПеремещениеТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	               |	ПеремещениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПеремещениеТоваров.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
	               |	ПеремещениеТоваров.СкладКомпании КАК ПредставлениеОтправителя,
	               |	ПеремещениеТоваров.СкладКомпанииПолучатель КАК ПредставлениеПолучателя,
	               |	ПеремещениеТоваров.СуммаДокумента КАК СуммаДокумента,
	               |	ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки.Порядок КАК ПорядокПивной,
	               |	ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки.ПорядокСигаретный КАК ПорядокСигаретный,
	               |	ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки КАК ТочкаДоставки,
	               |	ПеремещениеТоваров.СкладКомпании.ТочкаДоставки КАК АдресОтправителя,
	               |	ТК_ТТНИсточники.НомерСтроки КАК НомерСтроки,
	               |	ПеремещениеТоваров.Продукция.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Наименование2 КАК ТоварПредставление,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		Сумма КАК СуммаВсего,
	               |		ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара) КАК ЭтоТара,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ПеремещениеТоваров.Продукция.ЕдиницаИзмерения
	               |			ИНАЧЕ ПеремещениеТоваров.Продукция.Номенклатура.ЕдиницаИзмеренияУпаковки
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ПеремещениеТоваров.Продукция.Коэффициент
	               |			ИНАЧЕ ЕСТЬNULL(ПеремещениеТоваров.Продукция.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0)
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ПеремещениеТоваров.Продукция.Номенклатура.ОсновнаяЕдиницаИзмерения
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмерения,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ЕСТЬNULL(ПеремещениеТоваров.Продукция.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0)
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ПеремещениеТоваров.Продукция.Номенклатура.ЕдиницаИзмеренияЯщиков
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиков,
	               |		ВЫБОР
	               |			КОГДА ПеремещениеТоваров.Продукция.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ЕСТЬNULL(ПеремещениеТоваров.Продукция.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0)
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиковКоэффициент
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ПеремещениеТоваров
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТК_ТТН.Источники КАК ТК_ТТНИсточники
	               |		ПО (ТК_ТТНИсточники.Ссылка = &СсылкаТТН)
	               |			И (ПеремещениеТоваров.Ссылка = ТК_ТТНИсточники.Документ
	               |				ИЛИ ПеремещениеТоваров.Ссылка = ТК_ТТНИсточники.Отгрузка)
	               |ГДЕ
	               |	ПеремещениеТоваров.Ссылка В(&МассивСсылок)" + СортировкаЗапроса;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("СсылкаТТН", СсылкаТТН);	
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПеремещениеТоваров";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваров");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокВФилиал = Макет.ПолучитьОбласть("ЗаголовокВФилиал");	
	ОбластьЗаголовокТара	= Макет.ПолучитьОбласть("ЗаголовокТара");	
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьРазделитель		= Макет.ПолучитьОбласть("Разделитель");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЭтоПеремещениеВФилиал = ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Перемещение товаров №%1 от %2'; uk = 'Переміщення товарів №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ТекстЗаголовкаТара", НСтр("ru = 'Возврат тары к '; uk = 'Повернення тари до '") + ЗаголовокЛок);		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", "");		
		СтруктураШапки.Вставить("ПредставлениеОтправителя", "");
		//СтруктураШапки.Вставить("АдресОтправителя", "");						
		
		НомерМаршрута = 0;
		Если Порядок = "Пиво" И ЗначениеЗаполнено(ВыборкаДокументов.ПорядокПивной) Тогда 
			НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной/1000);
		ИначеЕсли ЗначениеЗаполнено(ВыборкаДокументов.ПорядокСигаретный) Тогда
			НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный/100);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НомерМаршрута) Тогда 
			СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		СтруктураШапки.Вставить("АдресПоставщика", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.Организация, "ЮридическийАдресОрганизации",, ТекущаяДата()));		
		
		Если ЗначениеЗаполнено(ВыборкаДокументов.ПредставлениеПолучателя) Тогда
			СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ПредставлениеПолучателя);
		ИначеЕсли ЗначениеЗаполнено(ВыборкаДокументов.ТорговаяПлощадкаПолучатель) Тогда 
			СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ТорговаяПлощадкаПолучатель);	
		Иначе
			СтруктураШапки.Вставить("ПредставлениеПолучателя", "");						
		КонецЕсли;
		
		//Если ВыборкаДокументов.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал И ЗначениеЗаполнено(
		Если ЗначениеЗаполнено(ВыборкаДокументов.АдресОтправителя) Тогда 
			СтруктураШапки.Вставить("АдресОтправителя", ВыборкаДокументов.АдресОтправителя);
		ИначеЕсли ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда 
			СтруктураШапки.Вставить("АдресОтправителя", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадка,"ЮридическийАдресТорговойПлощадки",,ТекущаяДата()));
		КонецЕсли;				
		
		Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда
			СтруктураШапки.Вставить("АдресПолучателя", ВыборкаДокументов.ТочкаДоставки);
		ИначеЕсли ЭтоПеремещениеВФилиал Тогда
			СтруктураШапки.Вставить("АдресПолучателя",УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадкаПолучатель,"ЮридическийАдресТорговойПлощадки",,ТекущаяДата()));
		КонецЕсли;
		
		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		Если ЭтоПеремещениеВФилиал Тогда 
			
			ИДД = "";
			Если НЕ ПустаяСтрока(ВыборкаДокументов.ИДДокументаОтгрузки) Тогда 
				ИДД = ВыборкаДокументов.ИДДокументаОтгрузки;
			Иначе
				ИДД = Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор());
			КонецЕсли;
			
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(ИДД,1,125);
				ОбластьЗаголовокВФилиал.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;
			
			ОбластьЗаголовокВФилиал.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовокВФилиал);
			
		Иначе
			
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
		КонецЕсли;
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		КоличествоМест 	= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		ЕстьТара 		= Ложь;
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
			
			Если Не ЕстьТара И ЭтоПеремещениеВФилиал И ВыборкаТовары.ЭтоТара Тогда 
				ЕстьТара = Истина;
			КонецЕсли;
			
			СтруктураСтроки = Новый Структура("Номенклатура, Цена, СуммаВсего");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
			СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
			ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
			ВыборкаТовары.Номенклатура,
			ВыборкаТовары.КоличествоБазовое,
			ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
			ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
			ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
			ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
			ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
			ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
			СтруктураИтоговМест);
			
			СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		Если ЕстьТара Тогда 
			
			ТабличныйДокумент.Вывести(ОбластьРазделитель);			
			ОбластьЗаголовокТара.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовокТара);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
			нпп = 0;
			ВыборкаТовары.Сбросить();
			Пока ВыборкаТовары.Следующий() Цикл 
				Если Не ВыборкаТовары.ЭтоТара Тогда 
					Продолжить;
				КонецЕсли;
				
				нпп = нпп + 1;
				
				СтруктураСтроки = Новый Структура("Номенклатура, КоличествоПредставление, Цена, СуммаВсего");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "Номенклатура, Цена");
				СтруктураСтроки.Вставить("НомерСтроки", нпп);				
				
				стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
				СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
				
				КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
				ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
				ВыборкаТовары.Номенклатура,
				ВыборкаТовары.КоличествоБазовое,
				ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
				ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
				СтруктураИтоговМест);
				
				СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);									
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);				
			КонецЦикла;			
			
			ОбластьИтого.Параметры.ВсегоМест = "";
			ОбластьИтого.Параметры.Всего = "";
			ТабличныйДокумент.Вывести(ОбластьИтого);
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
		КонецЕсли;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции

// ДекларацияПроизводителя
Функция ПечатьДекларацияПроизводителя(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	МассивДопРеквизитов = Новый Массив;
	МассивДопРеквизитов.Добавить("ФС_ДСТУ");
	МассивДопРеквизитов.Добавить("ФС_Влажность");
	МассивДопРеквизитов.Добавить("ФС_Жирность");
	МассивДопРеквизитов.Добавить("ФС_Кислотность");
	МассивДопРеквизитов.Добавить("ФС_ТемператураХранения");
	МассивДопРеквизитов.Добавить("ФС_ОрганолептическиеСвойства");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("ДопРеквизиты", МассивДопРеквизитов);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукцииПродукция.Ссылка КАК Ссылка,
	               |	ВыпускПродукцииПродукция.Ссылка.Дата КАК Дата,
	               |	ВЫРАЗИТЬ(ВыпускПродукцииДополнительныеРеквизиты.Значение КАК ДАТА) КАК ДатаДекларации,
	               |	ВыпускПродукцииПродукция.Ссылка.Номер КАК Номер,
	               |	ВыпускПродукцииПродукция.Ссылка.Организация.НаименованиеПолное КАК Производитель,
	               |	ВыпускПродукцииПродукция.Ссылка.Контрагент.НаименованиеПолное КАК Получатель,
	               |	ВыпускПродукцииПродукция.НомерСтроки КАК НомерПП,
	               |	ВыпускПродукцииПродукция.Номенклатура КАК Номенклатура,
	               |	ВыпускПродукцииПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВыпускПродукцииПродукция.Номенклатура.Наименование2 КАК НаименованиеПолное,
	               |	ВыпускПродукцииПродукция.Номенклатура.СрокХраненияТовара КАК СрокХранения,
	               |	ВыпускПродукцииПродукция.Количество КАК Количество,
	               |	ВыпускПродукцииПродукция.ЕдиницаИзмерения.Вес КАК ЕдиницаИзмеренияВес
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	Документ.ВыпускПродукции.Продукция КАК ВыпускПродукцииПродукция
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.ДополнительныеРеквизиты КАК ВыпускПродукцииДополнительныеРеквизиты
	               |		ПО ВыпускПродукцииПродукция.Ссылка = ВыпускПродукцииДополнительныеРеквизиты.Ссылка
	               |			И (ВыпускПродукцииДополнительныеРеквизиты.Свойство.Имя = ""ДатаДекларации"")
	               |ГДЕ
	               |	ВыпускПродукцииПродукция.Ссылка В(&МассивСсылок)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НоменклатураДополнительныеРеквизиты.Ссылка КАК Номенклатура,
	               |	НоменклатураДополнительныеРеквизиты.Свойство.Имя КАК Имя,
	               |	НоменклатураДополнительныеРеквизиты.Свойство.ИдентификаторДляФормул КАК ИдентификаторДляФормул,
	               |	НоменклатураДополнительныеРеквизиты.Значение КАК Значение
	               |ИЗ
	               |	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	               |ГДЕ
	               |	НоменклатураДополнительныеРеквизиты.Ссылка В
	               |			(ВЫБРАТЬ
	               |				втТовары.Номенклатура КАК Номенклатура
	               |			ИЗ
	               |				втТовары КАК втТовары
	               |			СГРУППИРОВАТЬ ПО
	               |				втТовары.Номенклатура)
	               |	И НоменклатураДополнительныеРеквизиты.Свойство.Имя В(&ДопРеквизиты)
	               |АВТОУПОРЯДОЧИВАНИЕ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.Ссылка КАК Ссылка,
	               |	втТовары.ДатаДекларации КАК ДатаДекларации,
	               |	втТовары.Дата КАК Дата,
	               |	втТовары.Номер КАК Номер,
	               |	втТовары.НомерПП КАК НомерПП,
	               |	втТовары.Номенклатура КАК Номенклатура,
	               |	втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втТовары.НаименованиеПолное КАК НаименованиеПолное,
	               |	втТовары.СрокХранения КАК СрокХранения,
	               |	втТовары.Количество КАК Количество,
	               |	втТовары.ЕдиницаИзмеренияВес КАК ЕдиницаИзмеренияВес,
	               |	втТовары.Производитель КАК Производитель,
	               |	втТовары.Получатель КАК Получатель
	               |ИЗ
	               |	втТовары КАК втТовары
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Дата,
	               |	Ссылка,
	               |	НомерПП
	               |ИТОГИ
	               |	МАКСИМУМ(ДатаДекларации),
	               |	МАКСИМУМ(Дата),
	               |	МАКСИМУМ(Номер),
	               |	МАКСИМУМ(Производитель),
	               |	МАКСИМУМ(Получатель)
	               |ПО
	               |	Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ГраницаМассива = МассивРезультатов.ВГраница();
	
	ТаблицаСвойства 	= МассивРезультатов[ГраницаМассива-1].Выгрузить();
	ВыборкаДокументы	= МассивРезультатов[ГраницаМассива].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_ДекларацияПроизводителя");
	Макет.КодЯзыка = КодЯзыкаПечать;
		
	ОбластьШапкаДокумента	= Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы	= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");	

	ПервыйДокумент = Истина;		
	Пока ВыборкаДокументы.Следующий() Цикл 
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				

		Если ЗначениеЗаполнено(ВыборкаДокументы.ДатаДекларации) Тогда
			ДатаДокумента 	 = ВыборкаДокументы.ДатаДекларации;
			ДатаПроизводства = ВыборкаДокументы.ДатаДекларации;
		Иначе
			ДатаДокумента 	 = ВыборкаДокументы.Дата;
			ДатаПроизводства = ВыборкаДокументы.Дата;
		КонецЕсли;
		
		СтруктураШапка = Новый Структура;
		СтруктураШапка.Вставить("Производитель", СокрЛП(ВыборкаДокументы.Производитель));
		СтруктураШапка.Вставить("Получатель", СокрЛП(ВыборкаДокументы.Получатель));
		СтруктураШапка.Вставить("ДатаДокумента", Формат(ДатаДокумента, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		СтруктураШапка.Вставить("ДатаОтгрузки", Формат(ДатаДокумента, "ДФ=dd.MM.yyyy") + НСтр("ru = ' г.'; uk = ' р.'", КодЯзыкаПечать));
		СтруктураШапка.Вставить("НомерДокумента", СокрЛП(ВыборкаДокументы.Номер));
		

		
		ОбластьШапкаДокумента.Параметры.Заполнить(СтруктураШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаПродукция = ВыборкаДокументы.Выбрать();
		Пока ВыборкаПродукция.Следующий() Цикл 
			СтруктураСтроки = Новый Структура("НомерПП,Номенклатура,Гост,МассаНетто,Жирность,Кислотность,Температура,Влажность,Органолептика,СрокГодности");
			
			СтруктураСтроки.НомерПП = ВыборкаПродукция.НомерПП;
			СтруктураСтроки.Номенклатура = ВыборкаПродукция.НаименованиеПолное;
			
			СтруктураСтроки.Вставить("Партия", Формат(ДатаПроизводства, "ДФ=ddMMyy"));			
			
			Если ЗначениеЗаполнено(ВыборкаПродукция.СрокХранения) Тогда 
				СрокХранения = ВыборкаПродукция.СрокХранения;					
				СтруктураСтроки.СрокГодности = Строка(СрокХранения) + НСтр("ru = ' дней'; uk = ' діб'", КодЯзыкаПечать);
			Иначе
				СрокХранения = 14; // По умолчанию 14 дней
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ДатаПроизводства", Формат(ДатаПроизводства + 60*60*24 * (СрокХранения - 1), "ДФ=dd.MM.yyyy"));
			
			СтруктураСтроки.МассаНетто = ВыборкаПродукция.Количество * ВыборкаПродукция.ЕдиницаИзмеренияВес;
			
			нСвойства = ТаблицаСвойства.НайтиСтроки(Новый Структура("Номенклатура", ВыборкаПродукция.Номенклатура));
			
			Для Каждого Стр Из нСвойства Цикл 
				Если Стр.Имя = "ФС_ДСТУ" Тогда 
					СтруктураСтроки.Гост = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Влажность" Тогда
					СтруктураСтроки.Влажность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Жирность" Тогда 
					СтруктураСтроки.Жирность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_Кислотность" Тогда 
					СтруктураСтроки.Кислотность = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_ТемператураХранения" Тогда 
					СтруктураСтроки.Температура = Стр.Значение;
				ИначеЕсли Стр.Имя = "ФС_ОрганолептическиеСвойства" Тогда 
					СтруктураСтроки.Органолептика = Стр.Значение;
				КонецЕсли;
			КонецЦикла;
			
			ОбластьСтрокаТаблицы.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураШапка);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
	КонецЦикла;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
КонецФункции

// Счет на оплату покупателя
Функция ПечатьСчетНаОплату(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.Ссылка КАК Ссылка,
	               |	ВыпускПродукции.Номер КАК Номер,
	               |	ДОБАВИТЬКДАТЕ(ВыпускПродукции.Дата, ДЕНЬ, -1) КАК Дата,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов.Дата КАК ДоговорДата,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов.Номер КАК ДоговорНомер,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ДоговорНаименование,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов.ВидДоговора КАК ВидДоговораКонтрагента,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.Организация КАК Поставщик,
	               |	ВыпускПродукции.СуммаДокумента КАК СуммаДокумента,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.Дата КАК СрокДоставки,
	               |	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВыпускПродукции.Комментарий КАК Комментарий,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВыпускПродукции.Продукция.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Наименование2 КАК НоменклатураНаименование,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Продукция
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка В(&МассивСсылок)
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВыпускПродукции.МоментВремени";
		
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СчетНаОплату";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_СчетНаОплату");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовокСчета		= Макет.ПолучитьОбласть("ЗаголовокСчета");
	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("Заголовок");
	ОбластьДопИнформация		= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьПоставщик 			= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 			= Макет.ПолучитьОбласть("Покупатель");
	
	ОбластьШапка_нпп 			= Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");	
	ОбластьШапка_Код			= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");		
	ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
	ОбластьШапка_Скидка			= Макет.ПолучитьОбласть("ШапкаТаблицы|Скидка");		
	ОбластьШапка_Сумма			= Макет.ПолучитьОбласть("ШапкаТаблицы|Сумма");		
	
	ОбластьСтрока_нпп 			= Макет.ПолучитьОбласть("Строка|НомерСтроки");	
	ОбластьСтрока_Код			= Макет.ПолучитьОбласть("Строка|КолонкаКодов");		
	ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
	ОбластьСтрока_Скидка	    = Макет.ПолучитьОбласть("Строка|Скидка");
	ОбластьСтрока_Сумма			= Макет.ПолучитьОбласть("Строка|Сумма");		
	
	ОбластьИтого_нпп 			= Макет.ПолучитьОбласть("Итого|НомерСтроки");	
	ОбластьИтого_Код			= Макет.ПолучитьОбласть("Итого|КолонкаКодов");		
	ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
	ОбластьИтого_Скидка	    	= Макет.ПолучитьОбласть("Итого|Скидка");
	ОбластьИтого_Сумма			= Макет.ПолучитьОбласть("Итого|Сумма");
	
	ОбластьИтогоНДС				= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью		= Макет.ПолучитьОбласть("СуммаПрописью");	
	ОбластьПодписи 				= Макет.ПолучитьОбласть("Подписи");
	
	ОбластьКолонкаТовар 		= Макет.Область("Товар");
	
	ШиринаКолонкаТовар 	= ОбластьКолонкаТовар.ШиринаКолонки;
	ШиринаКолонкиКод 	= Макет.Область("КолонкаКодов").ШиринаКолонки;
	ШиринаКолонкиСкидка	= Макет.Область("СуммаБезСкидки").ШиринаКолонки + Макет.Область("СуммаСкидки").ШиринаКолонки;
	
	КэшБанковскиеРеквизиты = Новый Соответствие;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		РеглУчет = ВыборкаДокументов.РегламентированныйУчет;
		
		Если ВыборкаДокументов.ВидДоговораКонтрагента = Перечисления.ВидыДоговоров.Комиссия Тогда 
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Список товаров на комиссию №%1 от %2'; uk = 'Список товарів на комісію №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								?(ВыборкаДокументов.РегламентированныйУчет, 
								Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
								Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));			
		Иначе
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Счет на оплату №%1 от %2'; uk = 'Рахунок на оплату №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								?(ВыборкаДокументов.РегламентированныйУчет, 
								Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"),
								Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
		КонецЕсли;
		
		БанковскиеРеквизиты = КэшБанковскиеРеквизиты.Получить(ВыборкаДокументов.Организация);		
		Если БанковскиеРеквизиты = Неопределено Тогда 
			
			БанковскиеРеквизиты = Новый Структура("НазваниеОрганизации,ЕДРПОУОрганизации,БанкОрганизации,МФОБанкаОрганизации,НомерРасчетногоСчетаОрганизации");	
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	Организации.НаименованиеПолное КАК НазваниеОрганизации,
			               |	Организации.КодПоЕДРПОУ КАК ЕДРПОУОрганизации,
			               |	БанковскиеСчетаОрганизаций.Банк.Наименование КАК БанкОрганизации,
			               |	БанковскиеСчетаОрганизаций.Банк.Код КАК МФОБанкаОрганизации,
			               |	БанковскиеСчетаОрганизаций.НомерСчета КАК НомерРасчетногоСчетаОрганизации
			               |ИЗ
			               |	Справочник.Организации КАК Организации
			               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
			               |		ПО Организации.Ссылка = БанковскиеСчетаОрганизаций.Владелец
			               |			И (БанковскиеСчетаОрганизаций.ВидСчета = ""Текущий"")
			               |ГДЕ
			               |	Организации.Ссылка = &Организация";
			
			Запрос.УстановитьПараметр("Организация", ВыборкаДокументов.Организация);	
		
			ВыборкаБанковскиеРеквизиты = Запрос.Выполнить().Выбрать();			
			Если ВыборкаБанковскиеРеквизиты.Следующий() Тогда 
				ЗаполнитьЗначенияСвойств(БанковскиеРеквизиты, ВыборкаБанковскиеРеквизиты);
				КэшБанковскиеРеквизиты.Вставить(ВыборкаДокументов.Организация, БанковскиеРеквизиты);
			КонецЕсли;
		КонецЕсли;
		
		СтруктураШапки = Новый Структура;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураШапки, БанковскиеРеквизиты, Истина);
		
		СтруктураШапки.Вставить("ИмяКолонкиКодов", НСтр("ru = 'Артикул'; uk = 'Артикул'", КодЯзыкаПечать));
		СтруктураШапки.Вставить("Цена", ?(РеглУчет, НСтр("ru = 'Цена с НДС'; uk = 'Ціна з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Цена'; uk = 'Ціна'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("Сумма", ?(РеглУчет, НСтр("ru = 'Сумма с НДС'; uk = 'Сума з ПДВ'", КодЯзыкаПечать), НСтр("ru = 'Сумма'; uk = 'Сума'", КодЯзыкаПечать)));
		СтруктураШапки.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
		СтруктураШапки.Вставить("ТекстЗаголовка", Заголовок);				
		
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ВыборкаДокументов.ОрганизацияНаименованиеПолное);
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		
		СтруктураШапки.Вставить("ПредставлениеПокупателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		СтруктураШапки.Вставить("РеквизитыПокупателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));		
		
		МассивДопИнформации = Новый Массив;
		Если ЗначениеЗаполнено(ВыборкаДокументов.ДоговорВзаиморасчетов) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Договор:'; uk = 'Договір:'", КодЯзыкаПечать),
					?(ЗначениеЗаполнено(ВыборкаДокументов.ДоговорНаименование), СокрЛП(ВыборкаДокументов.ДоговорНаименование), ВыборкаДокументов.ДоговорВзаиморасчетов)));
					
		КонецЕсли;
				
		Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда 
			МассивДопИнформации.Добавить(
				Новый Структура("НазваниеПараметра, ЗначениеПараметра",
					НСтр("ru = 'Точка доставки:'; uk = 'Точка доставки:'", КодЯзыкаПечать),
					ВыборкаДокументов.ТочкаДоставки));			
		КонецЕсли;
		
		МассивДопИнформации.Добавить(
			Новый Структура("НазваниеПараметра, ЗначениеПараметра",
				НСтр("ru = 'Срок поставки:'; uk = 'Строк поставки:'", КодЯзыкаПечать),
				Формат(ВыборкаДокументов.СрокДоставки, "ДФ=dd.MM.yyyy")));			
		
		
		//Если Не РеглУчет И Не ПустаяСтрока(ВыборкаДокументов.Комментарий) Тогда 
		//	МассивДопИнформации.Добавить(
		//		Новый Структура("НазваниеПараметра, ЗначениеПараметра",
		//			НСтр("ru = 'Комментарий:'; uk = 'Коментар:'", КодЯзыкаПечать),
		//			СокрЛП(ВыборкаДокументов.Комментарий)));						
		//КонецЕсли;
		
		ОбластьЗаголовокСчета.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовокСчета);
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		тзТовары = ВыборкаДокументов.Продукция.Выгрузить();
		ВыводитьАртикул = Ложь;				
		ВыводитьСкидки 	= тзТовары.Итог("СуммаСкидки") <> 0;
		ОбластьКолонкаТовар.ШиринаКолонки = ШиринаКолонкаТовар + ?(Не ВыводитьАртикул, ШиринаКолонкиКод, 0) + ?(Не ВыводитьСкидки, ШиринаКолонкиСкидка, 0);
		
		ОбластьШапка_Данные			= Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");		
		ОбластьСтрока_Данные		= Макет.ПолучитьОбласть("Строка|Данные");
		ОбластьИтого_Данные			= Макет.ПолучитьОбласть("Итого|Данные");
		
		
		СтруктураШапки.Вставить("ВсегоСуммаБезСкидки", тзТовары.Итог("Сумма"));
		СтруктураШапки.Вставить("ВсегоСуммаСкидки", тзТовары.Итог("СуммаСкидки"));
		СтруктураШапки.Вставить("Всего", тзТовары.Итог("СуммаВсего"));
		СтруктураШапки.Вставить("ВсегоНДС", тзТовары.Итог("СуммаНДС"));
		
		//Вывод шапки таблицы Продцукия
		ТабличныйДокумент.Вывести(ОбластьШапка_нпп);
		
		Если ВыводитьАртикул Тогда 
			ОбластьШапка_Код.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьШапка_Код);
		КонецЕсли;
		
		ОбластьШапка_Данные.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Данные);
		
		Если ВыводитьСкидки Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапка_Скидка);
		КонецЕсли;
		
		ОбластьШапка_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьШапка_Сумма);
		
	
		
		// Вывод строк таблицы		
		нпп = 0;
		Для Каждого Стр Из тзТовары Цикл 
			нпп = нпп + 1;
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("Артикул", Стр.Артикул);
			СтруктураСтроки.Вставить("Товар", ?(ЗначениеЗаполнено(Стр.НоменклатураНаименование), Стр.НоменклатураНаименование, Стр.Номенклатура));
			СтруктураСтроки.Вставить("Количество", Стр.Количество);
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Стр.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("Цена", Стр.Цена);
			СтруктураСтроки.Вставить("Сумма", Стр.СуммаВсего);
			
			Если ВыводитьСкидки Тогда 
				СтруктураСтроки.Вставить("СуммаБезСкидки", Стр.Сумма);
				СтруктураСтроки.Вставить("СуммаСкидки", Стр.СуммаСкидки);
			КонецЕсли;
			
			ОбластьСтрока_нпп.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока_нпп);
			
			Если ВыводитьАртикул Тогда 
				ОбластьСтрока_Код.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Код);
			КонецЕсли;
			
			ОбластьСтрока_Данные.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Данные);
			
			Если ВыводитьСкидки Тогда 
				ОбластьСтрока_Скидка.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Присоединить(ОбластьСтрока_Скидка);
			КонецЕсли;
			
			ОбластьСтрока_Сумма.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Присоединить(ОбластьСтрока_Сумма);			
		КонецЦикла;

		ТабличныйДокумент.Вывести(ОбластьИтого_нпп);
		
		Если ВыводитьАртикул Тогда 
			ТабличныйДокумент.Присоединить(ОбластьИтого_Код);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьИтого_Данные);
		
		Если ВыводитьСкидки Тогда 
			ОбластьИтого_Скидка.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Присоединить(ОбластьИтого_Скидка);
		КонецЕсли;

		ОбластьИтого_Сумма.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Присоединить(ОбластьИтого_Сумма);
		
		Если РеглУчет Тогда 
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		КонецЕсли;
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Всего наименований %1 на сумму %2 %3.'; uk = 'Всього найменувань %1 на суму %2 %3.'", КодЯзыкаПечать),
							нпп, 
							СтруктураШапки.Всего,
							ВыборкаДокументов.ВалютаДокумента);
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СтруктураШапки.Всего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		Если РеглУчет Тогда 
			СуммаНДСПрописью = ЧислоПрописью(СтруктураШапки.ВсегоНДС, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
			СуммаПрописью = СуммаПрописью + Символы.ПС + НСтр("ru = 'В т.ч. НДС: '; uk = 'У т.ч. ПДВ: '", КодЯзыкаПечать) + СуммаНДСПрописью;
		КонецЕсли;
		
		СтруктураШапки.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		СтруктураШапки.Вставить("СуммаПрописью", СуммаПрописью);
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		Если ТипЗнч(Ответственный) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
			СтрокаОтветственный = НСтр("ru = 'Директор '; uk = 'Директор '", КодЯзыкаПечать) + Ответственный;
		Иначе
			СтрокаОтветственный = "";
		КонецЕсли;
		
		СтруктураШапки.Вставить("Ответственный", СтрокаОтветственный);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
			
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
		ПервыйДокумент = Ложь;		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Товаро-транспортная накладная (Фабрика сыра)
Функция ПечатьТТН_ФС(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивИменСвойств = Новый Массив;
	МассивИменСвойств.Добавить("ВП_Автомобиль");
	МассивИменСвойств.Добавить("ВП_Прицеп");
	МассивИменСвойств.Добавить("ВП_Водитель");
	МассивИменСвойств.Добавить("ВП_НомерПрав");
	МассивИменСвойств.Добавить("ВП_Перевозчик");
	МассивИменСвойств.Добавить("ВП_ИННПеревозчика");
	МассивИменСвойств.Добавить("ВП_ПунктОтправитель");
	МассивИменСвойств.Добавить("ВП_ПунктПолучатель");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("МассивИменСвойств", МассивИменСвойств);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.Ссылка КАК Ссылка,
	               |	ВыпускПродукции.Дата КАК Дата,
	               |	ВыпускПродукции.Номер КАК Номер,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.СкладКомпании.ТочкаДоставки КАК ПунктОтправитель,
	               |	ВыпускПродукции.ТочкаДоставки КАК ПунктПолучатель,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка В(&МассивОбъектов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВыпускПродукцииПродукция.Ссылка КАК Ссылка,
	               |	ВыпускПродукцииПродукция.НомерСтроки КАК НомерСтроки,
	               |	ВыпускПродукцииПродукция.Номенклатура КАК Номенклатура,
	               |	ВыпускПродукцииПродукция.Номенклатура.Наименование2 КАК НоменклатураНаименованиеПолное,
	               |	ВыпускПродукцииПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВыпускПродукцииПродукция.Количество КАК Количество,
	               |	ВыпускПродукцииПродукция.КоличествоБазовое * ВыпускПродукцииПродукция.ЕдиницаИзмерения.Вес КАК Масса,
	               |	ВыпускПродукцииПродукция.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ВыпускПродукцииПродукция.СуммаНДС КАК СуммаНДС,
	               |	ВыпускПродукцииПродукция.СуммаВсего КАК СуммаВсего
	               |ИЗ
	               |	Документ.ВыпускПродукции.Продукция КАК ВыпускПродукцииПродукция
	               |ГДЕ
	               |	ВыпускПродукцииПродукция.Ссылка В(&МассивОбъектов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	НомерСтроки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВыпускПродукцииДополнительныеРеквизиты.Ссылка КАК Ссылка,
	               |	ВыпускПродукцииДополнительныеРеквизиты.Свойство.Имя КАК СвойствоИмя,
	               |	ВыпускПродукцииДополнительныеРеквизиты.Значение КАК Значение
	               |ИЗ
	               |	Документ.ВыпускПродукции.ДополнительныеРеквизиты КАК ВыпускПродукцииДополнительныеРеквизиты
	               |ГДЕ
	               |	ВыпускПродукцииДополнительныеРеквизиты.Ссылка В(&МассивОбъектов)
	               |	И ВыпускПродукцииДополнительныеРеквизиты.Свойство.Имя В(&МассивИменСвойств)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументы 	= МассивРезультатов[0].Выбрать();
	ТаблицаТовары 		= МассивРезультатов[1].Выгрузить();
	ДопРеквизиты 	 	= МассивРезультатов[2].Выгрузить();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТТН_ФС";
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТК_ТТН.ПФ_MXL_ТТН_ФС");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьВсего			= Макет.ПолучитьОбласть("Всего");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьАвтотранспорт	= Макет.ПолучитьОбласть("ПараметрыАвтотранспорта");
	ОбластьПогрузочныеОперации = Макет.ПолучитьОбласть("ПогрузочныеОперации");
	
	КэшАвтотранспорт = Новый Соответствие;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
				
		ПредставлениеОрганизация = Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументы.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументы.Дата, КодЯзыкаПечать, Ложь);
		ПредставлениеКонтрагента = Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументы.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументы.Дата, КодЯзыкаПечать);
		
		мДопРеквизиты = ДопРеквизиты.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументы.Ссылка));
		
		стрДопРеквизиты = Новый Структура;
		Для Каждого Стр Из мДопРеквизиты Цикл 
			стрДопРеквизиты.Вставить(Стр.СвойствоИмя, Стр.Значение);
		КонецЦикла;
		
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("Дата", 			Формат(ВыборкаДокументы.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		СтруктураРеквизитов.Вставить("Номер", 			СокрЛП(ВыборкаДокументы.Номер));
		СтруктураРеквизитов.Вставить("ВидПеревозки", 	"автоперевезення");
		
		СтруктураРеквизитов.Вставить("Отправитель", 					ПредставлениеОрганизация);		
		СтруктураРеквизитов.Вставить("ПлательщикПредставление",			ПредставлениеОрганизация); //ПредставлениеКонтрагента); //Vov41k 05.02.2024
		СтруктураРеквизитов.Вставить("ГрузополучательПредставление", 	ПредставлениеКонтрагента);
		
		СтруктураРеквизитов.Вставить("ПунктОтправитель", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_ПунктОтправитель", ВыборкаДокументы.ПунктОтправитель));
		СтруктураРеквизитов.Вставить("ПунктПолучатель",	 ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_ПунктПолучатель", ВыборкаДокументы.ПунктПолучатель));
		
		Автомобиль = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_Автомобиль", Неопределено);
		РеквизитыАвтомобиля = Новый Структура("ВинКодАвтомобиля,ГодВыпускаАвтомобиля,ДлинаАвтомобиля,ШиринаАвтомобиля,ВысотаАвтомобиля,ВесАвтомобиля,ВесАвтомобиляСГрузом");
		Если ЗначениеЗаполнено(Автомобиль) И ТипЗнч(Автомобиль) = Тип("СправочникСсылка.Автотранспорт") Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	Автотранспорт.Наименование КАК Наименование,
			               |	Автотранспорт.Модель КАК Модель,
			               |	Автотранспорт.ГосНомер КАК ГосНомер,
			               |	Автотранспорт.VIN КАК ВинКодАвтомобиля,
			               |	Автотранспорт.ГодВыпуска КАК ГодВыпускаАвтомобиля,
			               |	Автотранспорт.Высота КАК ВысотаАвтомобиля,
			               |	Автотранспорт.Длина КАК ДлинаАвтомобиля,
			               |	Автотранспорт.Ширина КАК ШиринаАвтомобиля,
			               |	Автотранспорт.Вес КАК ВесАвтомобиля,
			               |	Автотранспорт.Грузоподъемность + Автотранспорт.Вес КАК ВесАвтомобиляСГрузом
			               |ИЗ
			               |	Справочник.Автотранспорт КАК Автотранспорт
			               |ГДЕ
			               |	Автотранспорт.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", Автомобиль);
			
			ВыборкаАвто = Запрос.Выполнить().Выбрать();
			Если ВыборкаАвто.Следующий() Тогда 
				СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", СокрЛП(ВыборкаАвто.Модель) + ?(ЗначениеЗаполнено(ВыборкаАвто.ГосНомер), " №" + СокрЛП(ВыборкаАвто.ГосНомер), ""));
				
				Для Каждого Реквизит Из РеквизитыАвтомобиля Цикл 
					Значение = ВыборкаАвто[Реквизит.Ключ];
					Если ЗначениеЗаполнено(Значение) Тогда 
						РеквизитыАвтомобиля.Вставить(Реквизит.Ключ, Значение);
					КонецЕсли;
				КонецЦикла;
			Иначе
				СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", Автомобиль);			
			КонецЕсли;
		Иначе
			СтруктураРеквизитов.Вставить("МаркаИГосНомерАвтомобиля", Автомобиль);		
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураРеквизитов, РеквизитыАвтомобиля, Истина);
		
		
		СтруктураРеквизитов.Вставить("ГосНомерПрицепа",  ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_Прицеп", "немає"));
		
		Перевозчик 		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_Перевозчик", "");
		ТипРеквизитаПеревозчик = ТипЗнч(Перевозчик);
		СтруктураРеквизитов.Вставить("ОрганизацияПеревозчик", Перевозчик);
		
		ИННПеревозчика 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_ИННПеревозчика", "");
		Если Не ЗначениеЗаполнено(ИННПеревозчика) И ТипРеквизитаПеревозчик <> Тип("Строка") Тогда 
			Если ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Организации") Тогда 
				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ
				               |	УчетнаяПолитикаОрганизацийСрезПоследних.Организация КАК Организация,
				               |	УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС КАК ИННПлательщикаНДС
				               |ИЗ
				               |	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&Дата, Организация = &Организация) КАК УчетнаяПолитикаОрганизацийСрезПоследних";
				Запрос.УстановитьПараметр("Дата", ВыборкаДокументы.Дата);
				Запрос.УстановитьПараметр("Организация", Перевозчик);
				
				ВыборкаИНН = Запрос.Выполнить().Выбрать();        
				Если ВыборкаИНН.Следующий() Тогда 
					ИННПеревозчика = ВыборкаИНН.ИННПлательщикаНДС;	
				КонецЕсли;
			ИначеЕсли ТипРеквизитаПеревозчик = Тип("СправочникСсылка.Контрагенты") Тогда 
				ИННПеревозчика = Перевозчик.ИННПлательщикаНДС;
			КонецЕсли;
		КонецЕсли;		
		СтруктураРеквизитов.Вставить("ИННПеревозчика", ИННПеревозчика);
		
		Водитель = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_Водитель", "");
		НомерУдостоверения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(стрДопРеквизиты, "ВП_НомерПрав", "");
				
		СтруктураРеквизитов.Вставить("Водитель", Водитель);
		СтруктураРеквизитов.Вставить("ВодительСНП", СокрЛП(Водитель) + ?(ЗначениеЗаполнено(НомерУдостоверения), ", " + СокрЛП(НомерУдостоверения), ""));				
		
		мТовары  = ТаблицаТовары.НайтиСтроки(Новый Структура("Ссылка", ВыборкаДокументы.Ссылка));
		тзТовары = ТаблицаТовары.Скопировать(мТовары);
		
		Количество = тзТовары.Итог("Количество");
		МассаТонн = тзТовары.Итог("Масса") / 1000;
		СуммаВсего = тзТовары.Итог("СуммаВсего");
		СуммаНДС = тзТовары.Итог("СуммаНДС");
		
		СтруктураРеквизитов.Вставить("КоличествоМестПрописью", ЧислоПрописью(Число(Количество), "Л=uk_UA; ДП=Истина", ",,,с,,,,,0"));
		СтруктураРеквизитов.Вставить("МассаПрописью", ЧислоПрописью(Окр(МассаТонн, 2),"Л=uk_UA; ДП=Истина", "ціла, цілих, цілих, ж, тисячних, тисячних, тисячних, м, 3"));
		СтруктураРеквизитов.Вставить("ИтогоМасса", Формат(МассаТонн, "ЧДЦ=3"));
		СтруктураРеквизитов.Вставить("ОтпущеноНаСуммуПрописью", ЧислоПрописью(СуммаВсего, "Л=uk_UA; ДП = Истина", НСтр("ru = 'гривна, гривны, гривен, м, копейка, копейки, копеек, ж, 2'; uk = 'гривна, гривні, гривень, ж, копійка, копійки, копійок, ж, 2'", КодЯзыкаПечать)));//Константы.ВалютаРегламентированногоУчета.Получить().ПараметрыПрописи));
		СтруктураРеквизитов.Вставить("СуммаНДС", Окр(СуммаНДС,2));
		СтруктураРеквизитов.Вставить("ИтогоКоличество", Количество);
		СтруктураРеквизитов.Вставить("ИтогоСуммаСНДС", СуммаВсего);		
		
		ДокументыСопровождения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Расходная накладная №%1, Декларация производителя №%1'; uk = 'Видаткова накладна №%1, Декларація виробника №%1'", КодЯзыкаПечать),
									ВыборкаДокументы.Номер);
									
		ДокументыСопровожденияКратко = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Вид нак №%1 від %2 р., декларація виробника/посв про якість №%1 від %2 р.'; uk = 'Вид нак №%1 від %2 р., декларація виробника/посв про якість №%1 від %2 р.'", КодЯзыкаПечать),
									ВыборкаДокументы.Номер,
									Формат(ВыборкаДокументы.Дата, "ДФ=dd.MM.yy"));
									
		СтруктураРеквизитов.Вставить("ДокументыСопровождения", ДокументыСопровождения);

		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				

		ОбластьШапка.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		Ит = 0;
		Для Каждого стрТовар ИЗ тзТовары Цикл 
			Ит = Ит + 1;
						
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", Ит);
			СтруктураСтроки.Вставить("ТоварНаименование", ?(ЗначениеЗаполнено(стрТовар.НоменклатураНаименованиеПолное), стрТовар.НоменклатураНаименованиеПолное, стрТовар.Номенклатура));
			СтруктураСтроки.Вставить("БазоваяЕдиницаНаименование", стрТовар.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("Количество", стрТовар.Количество);
			СтруктураСтроки.Вставить("Цена", стрТовар.ЦенаБезНалогов);
			СтруктураСтроки.Вставить("Сумма", стрТовар.СуммаВсего);
			СтруктураСтроки.Вставить("ВидУпаковки", стрТовар.ЕдиницаИзмерения);
			СтруктураСтроки.Вставить("ДокументыСопровождения", ДокументыСопровожденияКратко); 
			СтруктураСтроки.Вставить("Масса", стрТовар.Масса);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ОбластьВсего.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьВсего);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		ОбластьАвтотранспорт.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьАвтотранспорт);		
		
		ОбластьПогрузочныеОперации.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьПогрузочныеОперации);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);		
			
		ПервыйДокумент = Ложь;
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Фасовка
Функция ПечатьФасовки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.Ссылка КАК Ссылка,
	               |	ВыпускПродукции.Номер КАК Номер,
	               |	ВыпускПродукции.Дата КАК Дата,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.Продукция.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Наименование2 КАК НоменклатураНаименование,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое
	               |	) КАК Продукция
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка В(&МассивСсылок)
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукции)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВыпускПродукции.МоментВремени";
		
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Фасовка";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыпускПродукции.ПФ_MXL_ВыпускФасовка");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 			= Макет.ПолучитьОбласть("Заголовок");
	ОбластьДопИнформация		= Макет.ПолучитьОбласть("ДопИнформация");	
	ОбластьШапка	 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока	 			= Макет.ПолучитьОбласть("Строка");	
	//ОбластьИтого	 			= Макет.ПолучитьОбласть("Итого");	
	ОбластьПодписи 				= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;		
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
	
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Выпуск продукции №%1 от %2'; uk = 'Випуск продукції №%1 від %2'", КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));			
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", Заголовок);
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		МассивДопИнформации = новый Массив;
		МассивДопИнформации.Добавить(
			Новый Структура("НазваниеПараметра, ЗначениеПараметра",
			НСтр("ru = 'Склад Компаниии:'; uk = 'Склад компанії:'", КодЯзыкаПечать),
			ВыборкаДокументов.СкладКомпании));			

		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		тзТовары = ВыборкаДокументов.Продукция.Выгрузить();
		ВыводитьАртикул = Истина;				
				
		// Вывод строк таблицы		
		нпп = 0;
		Для Каждого Стр Из тзТовары Цикл 
			нпп = нпп + 1;
			
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("Артикул", Стр.Артикул);
			СтруктураСтроки.Вставить("Товар", ?(ЗначениеЗаполнено(Стр.НоменклатураНаименование), Стр.НоменклатураНаименование, Стр.Номенклатура));
			СтруктураСтроки.Вставить("Количество", Стр.Количество);
			СтруктураСтроки.Вставить("ЕдиницаИзмерения", Стр.ЕдиницаИзмерения);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;

	
		//Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		//Если ТипЗнч(Ответственный) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		//	СтрокаОтветственный = НСтр("ru = 'Директор '; uk = 'Директор '", КодЯзыкаПечать) + Ответственный;
		//Иначе
		//	СтрокаОтветственный = "";
		//КонецЕсли;
		//
		//СтруктураШапки.Вставить("Ответственный", СтрокаОтветственный);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
			
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		
		ПервыйДокумент = Ложь;		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецОбласти



#КонецЕсли

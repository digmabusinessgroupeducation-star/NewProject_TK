#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_НалоговаяНакладная") Тогда
		ЗаполнитьДокументНаОсновании_ТК_НалоговаяНакладная(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_Приложение2КНалоговойНакладной") Тогда
		ЗаполнитьДокументНаОсновании_ТК_Приложение2КНалоговойНакладной(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДата();
	
	ДокументОснование = Неопределено;
	ВхДокНомер = "";
	ВхДокДата = Неопределено;
	ИДДокументаОтгрузки = "";
	НомерRB = "";
	СтатусRB = Неопределено;
	
КонецПроцедуры
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	СуммаДокумента = Товары.Итог("СуммаВсего");
	
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,Организация);

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.Организация КАК Организация
		|ИЗ
		|	Документ.КонсолидированныеОстаткиОрганизации КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);

	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;

	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
		
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", Организация <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;

	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.КонсолидированныеОстаткиОрганизации.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.КонсолидированныеОстаткиОрганизации.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПотребностиТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьПотребностьОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Вставить содержимое обработчика.
КонецПроцедуры


#КонецЕсли


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	

КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ТК_НалоговаяНакладная(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(справочник.ХозОперации.КонсолидированныеПоступленияОрганизации) КАК ХозОперация,
	               |	ТК_НалоговаяНакладная.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_НалоговаяНакладная.Организация.Контрагент КАК Контрагент,
	               |	ТК_НалоговаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_НалоговаяНакладная.ТипЦен КАК ТипЦен,
	               |	ТК_НалоговаяНакладная.КурсДокумента КАК КурсДокумента,
	               |	ТК_НалоговаяНакладная.Ссылка КАК НалоговаяНакладная,
	               |	ТК_НалоговаяНакладная.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	Организации.Ссылка КАК Организация,
	               |	ТК_НалоговаяНакладная.Ссылка КАК ДокументОснование,
	               |	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов.Номер КАК НомерДоговора,
	               |	ТК_НалоговаяНакладная.НомерРН КАК ВхДокНомер,
	               |	ТК_НалоговаяНакладная.Дата КАК ВхДокДата,
	               |	ТК_НалоговаяНакладная.Автор КАК Автор,
	               |	ТК_НалоговаяНакладная.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		СуммаВсего КАК СуммаВсего,
	               |		ЗНАЧЕНИЕ(Перечисление.ВидыДвижений.Приход) КАК ВидДвижения
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	               |		ПО ТК_НалоговаяНакладная.Контрагент = Организации.Контрагент
	               |ГДЕ
	               |	ТК_НалоговаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		МассивДоговоров = Новый Массив;
		МассивДоговоров.Добавить(Справочники.ВидыДоговоров.Поставка);
		тДоговор = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Выборка.Контрагент,Выборка.Организация,Выборка.ПодразделениеКомпании,МассивДоговоров,,ТекущаяДата(),Выборка.НомерДоговора);
		
		Если тДоговор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка() Тогда
			ЭтотОбъект.Комментарий = "НЕТ Договора";
		Иначе
			ЭтотОбъект.ДоговорВзаиморасчетов = тДоговор;
		КонецЕсли;
	
		тзТовары = Выборка.Товары.Выгрузить();
		
		Товары.Загрузить(тзТовары);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ТК_Приложение2КНалоговойНакладной(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(справочник.ХозОперации.КонсолидированныеПоступленияОрганизации) КАК ХозОперация,
	               |	ТК_Приложение2КНалоговойНакладной.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_Приложение2КНалоговойНакладной.Организация.Контрагент КАК Контрагент,
	               |	ТК_Приложение2КНалоговойНакладной.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_Приложение2КНалоговойНакладной.ТипЦен КАК ТипЦен,
	               |	ТК_Приложение2КНалоговойНакладной.КурсДокумента КАК КурсДокумента,
	               |	ТК_Приложение2КНалоговойНакладной.Ссылка КАК НалоговаяНакладная,
	               |	ТК_Приложение2КНалоговойНакладной.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	Организации.Ссылка КАК Организация,
	               |	ТК_Приложение2КНалоговойНакладной.Ссылка КАК ДокументОснование,
	               |	ТК_Приложение2КНалоговойНакладной.ДоговорВзаиморасчетов.Номер КАК НомерДоговора,
	               |	ТК_Приложение2КНалоговойНакладной.Автор КАК Автор,
	               |	ТК_Приложение2КНалоговойНакладной.Номер КАК ВхДокНомер,
	               |	ТК_Приложение2КНалоговойНакладной.Дата КАК ВхДокДата,
	               |	ТК_Приложение2КНалоговойНакладной.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		-ТК_Приложение2КНалоговойНакладной.Товары.ИзменениеКоличество КАК Количество,
	               |		-ТК_Приложение2КНалоговойНакладной.Товары.ИзменениеКоличество * ТК_Приложение2КНалоговойНакладной.Товары.Коэффициент КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		(ТК_Приложение2КНалоговойНакладной.Товары.ЦенаБезНалогов - ТК_Приложение2КНалоговойНакладной.Товары.ИзменениеЦенаБезНалогов) КАК ЦенаБезНалогов,
	               |		-ТК_Приложение2КНалоговойНакладной.Товары.ИзменениеСуммаБезНалогов КАК СуммаБезНалогов,
	               |		-ТК_Приложение2КНалоговойНакладной.Товары.ИзменениеСуммаНДС КАК СуммаНДС,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		ЗНАЧЕНИЕ(Перечисление.ВидыДвижений.Расход) КАК ВидДвижения
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной КАК ТК_Приложение2КНалоговойНакладной
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	               |		ПО ТК_Приложение2КНалоговойНакладной.Контрагент = Организации.Контрагент
	               |ГДЕ
	               |	ТК_Приложение2КНалоговойНакладной.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		МассивДоговоров = Новый Массив;
		МассивДоговоров.Добавить(Справочники.ВидыДоговоров.Поставка);
		тДоговор = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Выборка.Контрагент,Выборка.Организация,Выборка.ПодразделениеКомпании,МассивДоговоров,,ТекущаяДата(),Выборка.НомерДоговора);
		
		Если тДоговор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка() Тогда
			ЭтотОбъект.Комментарий = "НЕТ Договора";
		Иначе
			ЭтотОбъект.ДоговорВзаиморасчетов = тДоговор;
		КонецЕсли;
	
		тзТовары = Выборка.Товары.Выгрузить();

		Товары.Загрузить(тзТовары);
				
		СтруктураДействий = Новый Структура;
		//СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Товары, СтруктураДействий, Неопределено);		

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВозвратОтПокупателя(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") Тогда
		
		ТекстЗапросаТаблицаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);	
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);

		
	Иначе
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);	
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		Запрос.Текст = "";
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВозвратОтПокупателя(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуПродажТоваров(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры


Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВозвратОтПокупателя.Дата КАК Период,
	               |	ВозвратОтПокупателя.Ссылка КАК Ссылка,
	               |	ВозвратОтПокупателя.ХозОперация КАК ХозОперация,
	               |	ВозвратОтПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВозвратОтПокупателя.Организация КАК Организация,
	               |	ВозвратОтПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВозвратОтПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратОтПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ВозвратОтПокупателя.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	               |	ВозвратОтПокупателя.Контрагент КАК Контрагент,
	               |	ВозвратОтПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратОтПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратОтПокупателя.ТипЦен КАК ТипЦен,
	               |	ВозвратОтПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВозвратОтПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ВозвратОтПокупателя.ДокументОснование КАК ДокументОснование,
	               |	ВозвратОтПокупателя.СуммаДокумента КАК СуммаДокумента
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя КАК ВозвратОтПокупателя
	               |ГДЕ
	               |	ВозвратОтПокупателя.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТипЦен",Реквизиты.ТипЦен);
	Запрос.УстановитьПараметр("ТочкаДоставки",Реквизиты.ТочкаДоставки);
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("ТипЦенСебестоимости", Реквизиты.ТипЦенСебестоимости);
	Запрос.УстановитьПараметр("ЗаполненДокументОснование", ЗначениеЗаполнено(Реквизиты.ДокументОснование));
	
	ПроводитьПоПартиям = Ложь;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") ИЛИ ПолучитьФункциональнуюОпцию("ФормироватьДвиженияПоПартиямПриПроведении") Тогда
		ПроводитьПоПартиям = Истина;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПроводитьПоПартиям",ПроводитьПоПартиям);
		
	//
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании",Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости", Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("Валюта",Реквизиты.ВалютаДокумента);	
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента",Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("СуммаДокумента", Реквизиты.СуммаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент", Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("Сделка", Реквизиты.ДокументОснование);
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВозвратОтПокупателяТовары.Ссылка КАК Ссылка,
	               |	ВозвратОтПокупателяТовары.НомерСтроки КАК НомерСтроки,
	               |	ВозвратОтПокупателяТовары.Ссылка.Организация КАК Организация,
	               |	ВозвратОтПокупателяТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВозвратОтПокупателяТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратОтПокупателяТовары.Ссылка.СкладКомпании КАК СкладКомпании,
	               |	ВозвратОтПокупателяТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВозвратОтПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	ВозвратОтПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВозвратОтПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВЫБОР
	               |		КОГДА ВозвратОтПокупателяТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	               |			ТОГДА ВозвратОтПокупателяТовары.Номенклатура.СтавкаНДС
	               |		ИНАЧЕ ВозвратОтПокупателяТовары.СтавкаНДС
	               |	КОНЕЦ КАК СтавкаНДС,
	               |	ВозвратОтПокупателяТовары.Коэффициент КАК Коэффициент,
	               |	ВозвратОтПокупателяТовары.КоличествоБазовое КАК Количество,
	               |	ВозвратОтПокупателяТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	ВозвратОтПокупателяТовары.Цена КАК Цена,
	               |	ВозвратОтПокупателяТовары.Сумма КАК Сумма,
	               |	ВозвратОтПокупателяТовары.СуммаВсего КАК СуммаВсего,
	               |	ВЫБОР
	               |		КОГДА &РегламентированныйУчет
	               |			ТОГДА ВозвратОтПокупателяТовары.СуммаНДС
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаНДС,
	               |	ВЫБОР
	               |		КОГДА &РегламентированныйУчет
	               |			ТОГДА ВозвратОтПокупателяТовары.СуммаБезНалогов
	               |		ИНАЧЕ ВозвратОтПокупателяТовары.Сумма
	               |	КОНЕЦ КАК СуммаБезНалогов,
	               |	ВозвратОтПокупателяТовары.Ссылка.Контрагент КАК Контрагент,
	               |	ВозвратОтПокупателяТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратОтПокупателяТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ВозвратОтПокупателяТовары.Ссылка.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВозвратОтПокупателяТовары.Ссылка.ДокументОснование КАК ДокументОснование,
	               |	ВозвратОтПокупателяТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
	               |	ВозвратОтПокупателяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ЗНАЧЕНИЕ(Справочник.КлассификаторПродаж.ПустаяСсылка) КАК КлассификаторПродажи,
	               |	0 КАК Порядок,
	               |	0 КАК СуммаАкцизногоНалога,
	               |	ИСТИНА КАК Возврат,
	               |	ВЫБОР
	               |		КОГДА &ЗаполненДокументОснование
	               |			ТОГДА ВозвратОтПокупателяТовары.Ссылка.ДокументОснование
	               |		ИНАЧЕ ВозвратОтПокупателяТовары.ДокументПродажи
	               |	КОНЕЦ КАК ДокументПродажи,
	               |	ВозвратОтПокупателяТовары.Партия КАК Партия,
	               |	ВозвратОтПокупателяТовары.Себестоимость КАК Себестоимость
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя.Товары КАК ВозвратОтПокупателяТовары
	               |ГДЕ
	               |	ВозвратОтПокупателяТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтТаблицаТара";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВозвратОтПокупателяТара.Номенклатура КАК Номенклатура,
	               |	ВозвратОтПокупателяТара.Количество КАК Количество,
	               |	ВозвратОтПокупателяТара.Цена КАК Цена,
	               |	ВозвратОтПокупателяТара.Сумма КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА &ЗаполненДокументОснование
	               |			ТОГДА ВозвратОтПокупателяТара.Ссылка.ДокументОснование
	               |		ИНАЧЕ ВозвратОтПокупателяТара.ДокументПродажи
	               |	КОНЕЦ КАК ДокументПродажи
	               |ПОМЕСТИТЬ ВтТаблицаТара
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя.Тара КАК ВозвратОтПокупателяТара
	               |ГДЕ
	               |	ВозвратОтПокупателяТара.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);		
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.Количество КАК Количество,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |
	              |ОБЪЕДИНИТЬ ВСЕ
	              |
	              |ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	              |	&Период,
	              |	&СкладКомпании,
	              |	ВтТаблицаТара.Номенклатура,
	              |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	              |	&Контрагент,
	              |	ВтТаблицаТара.Количество,
	              |	ЗНАЧЕНИЕ(Справочник.ХозОперации.ВзаиморасчетыПоТаре)
	              |ИЗ
	              |	ВтТаблицаТара КАК ВтТаблицаТара";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВозвратнаяТара";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТара", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	&Период КАК Период,
				  |	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	              |	&Организация КАК Организация,
	              |	&Контрагент КАК Контрагент,
	              |	ВтТаблицаТара.Номенклатура КАК Номенклатура,
	              |	СУММА(ВтТаблицаТара.Количество) КАК Количество,
	              |	СУММА(ВтТаблицаТара.Сумма) КАК Сумма
	              |ИЗ
	              |	ВтТаблицаТара КАК ВтТаблицаТара
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТара.Номенклатура
	              |
	              |ИМЕЮЩИЕ
	              |	СУММА(ВтТаблицаТара.Количество) <> 0"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПокупателями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеДебиторскойЗадолженности) КАК ВидОперации,
	              |	&Период КАК Период,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
				  |	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
	              |	СУММА(ВтТаблицаТовары.СуммаВсего * ВтТаблицаТовары.КурсДокумента) КАК СуммаБаз
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.Ссылка,
	              |	ВтТаблицаТовары.ХозОперация,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов,
	              |	ВтТаблицаТовары.Контрагент,
				  |	ВтТаблицаТовары.РегламентированныйУчет"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции


#КонецОбласти  


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров,Взаиморасчеты");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");

	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
	НоваяСтрока.Период   	  = Ссылка.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");

	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.ДоговорКонтрагента = Ссылка.ДоговорВзаиморасчетов;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.Взаиморасчеты = ТаблицаПоследовательности;

	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры


#КонецОбласти


#Область ДополнительныеПроцедурыИФункции

//Процедура ЗаполнитьДокументыПродажиВТЧТовары(Объект) Экспорт 
//	
//	СкладКомпании = Объект.СкладКомпании;
//	ДоговорВзаиморасчетов = Объект.ДоговорВзаиморасчетов;
//	тзТовары = Объект.Товары.Выгрузить();
//	
//	Запрос = Новый Запрос;
//	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
//	               |	тзТовары.Номенклатура КАК Номенклатура,
//	               |	тзТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
//	               |ПОМЕСТИТЬ СписокТоваров
//	               |ИЗ
//	               |	&тзТовары КАК тзТовары
//	               |;
//	               |
//	               |////////////////////////////////////////////////////////////////////////////////
//	               |ВЫБРАТЬ
//	               |	СписокТоваров.Номенклатура КАК Номенклатура,
//	               |	СписокТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
//	               |	Продажи.Регистратор КАК ДокументПродажи,
//	               |	Продажи.Партия КАК Партия,
//	               |	Продажи.Количество КАК Количество,
//	               |	Продажи.Сумма КАК Сумма,
//	               |	Продажи.СуммаНДС КАК СуммаНДС,
//	               |	Продажи.СуммаСкидки КАК СуммаСкидки,
//	               |	Продажи.Себестоимость КАК Себестоимость
//	               |ИЗ
//	               |	СписокТоваров КАК СписокТоваров
//	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи КАК Продажи
//	               |		ПО СписокТоваров.Номенклатура = Продажи.Номенклатура
//	               |			И СписокТоваров.ХарактеристикаНоменклатуры = Продажи.ХарактеристикаНоменклатуры
//	               |			И (Продажи.ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
//	               |				И Продажи.СкладКомпании = &ССкладКомпании
//	               |				И Продажи.Регистратор ССЫЛКА Документ.РеализацияТоваров)";
//	
//	Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
//	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", ДоговорВзаиморасчетов);
//	Запрос.УстановитьПараметр("тзТовары", тзТовары);
//	
//	//ТаблицаСделки = 
//КонецПроцедуры


#КонецОбласти


#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	                                            
	// Возврат от покупателя
    КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ВозвратОтПокупателя";
    КомандаПечати.Идентификатор = "ВозвратОтПокупателя";
    КомандаПечати.Представление = НСтр("ru = 'Возврат от покупателя'; uk = 'Повернення від покупця'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
		
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Возврат поставщику с партиями
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВозвратОтПокупателя") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ВозвратОтПокупателя",
		НСтр("ru = 'Возврат от покупателя'; uk = 'Повернення від покупця'"), 
		ПечатьВозвратОтПокупателя(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ВозвратОтПокупателя.ПФ_MXL_ВозвратОтПокупателя");
	КонецЕсли;	
	
КонецПроцедуры

// Возврат от покупателя
Функция ПечатьВозвратОтПокупателя(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВозвратОтПокупателя.Ссылка КАК Ссылка,
	               |	ВозвратОтПокупателя.Номер КАК Номер,
	               |	ВозвратОтПокупателя.Дата КАК Дата,
	               |	ВозвратОтПокупателя.Контрагент КАК Контрагент,
	               |	ВозвратОтПокупателя.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ВозвратОтПокупателя.Организация КАК Организация,
	               |	ВозвратОтПокупателя.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	ВозвратОтПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ВозвратОтПокупателя.СкладКомпании.НаименованиеПредставление КАК ПредставлениеСкладаМестоСоставления,
	               |	ВозвратОтПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратОтПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ВозвратОтПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратОтПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратОтПокупателя.ТочкаДоставки.Представление КАК АдресДоставки,
	               |	ВозвратОтПокупателя.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ОснованиеПредставление,
	               |	ВозвратОтПокупателя.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Сумма КАК СуммаВсего,
	               |		Партия.ВхДокНомер КАК ВхДокНомер,
	               |		Партия.ВхДокДата КАК ВхДокДата,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя КАК ВозвратОтПокупателя
	               |ГДЕ
	               |	ВозвратОтПокупателя.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВозвратОтПокупателя";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратОтПокупателя.ПФ_MXL_ВозвратОтПокупателя");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("ЗаголовокН");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьМестоСоставления = Макет.ПолучитьОбласть("Склад");	

	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;					
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Возврат от покупателя №%1 от %2'; uk = 'Повернення від покупця №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
				
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное)), СокрП(ВыборкаДокументов.КонтрагентНаименованиеПолное), ВыборкаДокументов.Контрагент));
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ?(Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.ОрганизацияНаименованиеПолное)), СокрП(ВыборкаДокументов.ОрганизацияНаименованиеПолное), ВыборкаДокументов.Организация));

		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать,Истина));
		СтруктураШапки.Вставить("РеквизитыПолучателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		
		СтруктураШапки.Вставить("Основание", НСтр("ru = 'Основание:'; uk = 'Підстава:'",КодЯзыкаПечать));
		СтруктураШапки.Вставить("ОснованиеПредставление", ВыборкаДокументов.ОснованиеПредставление);
		СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", ВыборкаДокументов.ПредставлениеСкладаМестоСоставления);
		СтруктураШапки.Вставить("АдресДоставки", ВыборкаДокументов.АдресДоставки);

		СтруктураШапки.Вставить("СтрокаСвойств", "");						 
				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ОбластьМестоСоставления.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьМестоСоставления);

		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		СуммаСкидки		= 0;
		СуммаНДС		= 0;
		СуммаВсего 		= 0;
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
						
			СтруктураСтроки = Новый Структура("Количество, ЕдиницаИзмерения, Цена, СуммаНДС, СуммаВсего");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("Код", СокрЛП(ВыборкаТовары.Артикул));
			
			Если ВыборкаТовары.СуммаСкидки > 0 Тогда 
				СтруктураСтроки.Вставить("ПредставлениеСкидки", ВыборкаТовары.СуммаСкидки);
			КонецЕсли;
			
			стрНоменклатура = ?(Не ПустаяСтрока(ВыборкаТовары.ТоварПредставление), СокрЛП(ВыборкаТовары.ТоварПредставление), ВыборкаТовары.Номенклатура);						
			СтруктураСтроки.Вставить("ТоварНаименование", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
						
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			СуммаСкидки = СуммаСкидки + ВыборкаТовары.СуммаСкидки;
			СуммаНДС 	= СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего 	= СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("СуммаВсего", СуммаВсего);		
		СтруктураПодвала.Вставить("НДСВсего", СуммаНДС);		
		СтруктураПодвала.Вставить("СкидкаВсего", СуммаСкидки);
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
				
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


#КонецОбласти	





#КонецЕсли


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ИзменениеПараметровСистемыЗаказов"));
	СформироватьДерево();
	ЗаполнитьТочки();
	
	ЗаполнитьСоответствиеКолонок();
	
	Элементы.ГруппаТоварыДокумента.ТолькоПросмотр = НЕ Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьУсловноеОформлениеНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
КонецПроцедуры


&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	УстановитьУсловноеОформлениеНаКлиенте();
КонецПроцедуры


&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	Объект.ТоварыДокумента.Очистить();
	ЗаполнитьТочки();
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	УстановитьПараметр(Точки,"Договор",Объект.Договор);	
	УстановитьПараметр(Объект.ТоварыДокумента,"Договор",Объект.Договор);	
КонецПроцедуры

&НаКлиенте
Процедура ТипПараметраПриИзменении(Элемент)
	Элементы.ГруппаУстановить.Доступность = ЗначениеЗаполнено(ТипПараметра);
	Элементы.ЗначениеПараметра.РежимВыбораИзСписка = ТипПараметра="ПериодичностьЗаказа" ИЛИ ТипПараметра="ВариантОкругления" ИЛИ ТипПараметра="МодельРасчета";
	Элементы.ЗначениеПараметра.СписокВыбора.Очистить();
	Если ТипПараметра="РегламентЗаказа" Тогда
		Тип1=Тип("Булево");
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = Ложь;
	ИначеЕсли ТипПараметра="ПериодичностьЗаказа" Тогда
		Элементы.ЗначениеПараметра.СписокВыбора.ЗагрузитьЗначения(Элементы.ТочкиПериодичностьЗаказа.СписокВыбора.ВыгрузитьЗначения());
		Тип1=Тип("ПеречислениеСсылка.ВидыПериодичностиЗаказов");
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = ПредопределенноеЗначение("Перечисление.ВидыПериодичностиЗаказов.Ежедневно");
	ИначеЕсли ТипПараметра="ДниНедели" Тогда
		Тип1=Тип("Строка");
		КС = Новый КвалификаторыСтроки(7);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КС);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = "";
	ИначеЕсли ТипПараметра="СрокПоставки" Тогда
		Тип1=Тип("Число");
		КЧ = Новый КвалификаторыЧисла(3,0);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КЧ);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		//ЗначениеПараметра = 0;//Элементы.ЗначениеПараметра.ОграничениеТипа.ПривестиЗначение(ЗначениеПараметра);
		ЗначениеПараметра = Элементы.ЗначениеПараметра.ОграничениеТипа.ПривестиЗначение(ЗначениеПараметра);
	ИначеЕсли ТипПараметра="СрокПоставкиПоДнямНедели" Тогда
		Тип1=Тип("Строка");
		КС = Новый КвалификаторыСтроки(28);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КС);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = "";
	ИначеЕсли ТипПараметра="ВариантОкругления" Тогда
		Элементы.ЗначениеПараметра.СписокВыбора.ЗагрузитьЗначения(Элементы.ТоварыДокумента1ВариантОкругления.СписокВыбора.ВыгрузитьЗначения());
		Тип1=Тип("ПеречислениеСсылка.ВариантыОкругленияКоличества");
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = ПредопределенноеЗначение("Перечисление.ВариантыОкругленияКоличества.КвантПоставкиВБольшуюСторону");
	ИначеЕсли ТипПараметра="КвантПоставки" Тогда
		Тип1=Тип("Число");
		КЧ = Новый КвалификаторыЧисла(15,3);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КЧ);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = 0;
	ИначеЕсли ТипПараметра="МодельРасчета" Тогда
		Элементы.ЗначениеПараметра.СписокВыбора.ЗагрузитьЗначения(Элементы.ТоварыДокумента1МодельРасчета.СписокВыбора.ВыгрузитьЗначения());
		Тип1=Тип("ПеречислениеСсылка.ВидыКоличестваЗаказа");
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = ПредопределенноеЗначение("Перечисление.ВидыКоличестваЗаказа.АвтоматическийРассчет");
	ИначеЕсли ТипПараметра="Количество" Тогда
		Тип1=Тип("Число");
		КЧ = Новый КвалификаторыЧисла(15,3);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КЧ);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = Элементы.ЗначениеПараметра.ОграничениеТипа.ПривестиЗначение(ЗначениеПараметра);
	ИначеЕсли ТипПараметра="МинимальныйОстаток" Тогда
		Тип1=Тип("Число");
		КЧ = Новый КвалификаторыЧисла(15,3);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КЧ);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = Элементы.ЗначениеПараметра.ОграничениеТипа.ПривестиЗначение(ЗначениеПараметра);
	ИначеЕсли ТипПараметра="ОстатокВитрина" Тогда
		Тип1=Тип("Число");
		КЧ = Новый КвалификаторыЧисла(15,3);
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(Тип1);
		ОграничениеТипов = Новый ОписаниеТипов(МассивТипов,,КЧ);
		Элементы.ЗначениеПараметра.ОграничениеТипа = ОграничениеТипов;
		ЗначениеПараметра = Элементы.ЗначениеПараметра.ОграничениеТипа.ПривестиЗначение(ЗначениеПараметра);
	Иначе
		Сообщить("ой бро...");
	КонецЕсли;
КонецПроцедуры




#КонецОбласти

#Область ОбработчикиСобытийТабличныхЧастей

&НаКлиенте
Процедура ТоварыДокументаДниНеделиПриИзменении(Элемент)
	ПроверкаДниНедели();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументаДниНеделиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Оповещение = Новый ОписаниеОповещения("ПолучитьДниНедели",ЭтотОбъект,Ложь);
	ОткрытьФорму("ОбщаяФорма.ДниНедели", Новый Структура("Переодичность,ДниНедели", Ложь, СокрЛП(Элемент.ТекстРедактирования)), ЭтотОбъект,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументаПериодичностьПоДнямНеделиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаТоварыДокумента Тогда
		ТекущиеДанные = Элементы.ТоварыДокумента.ТекущиеДанные;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПараметрыЗаказов Тогда
		ТекущиеДанные = Элементы.Точки.ТекущиеДанные;
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("ПолучитьДниНедели",ЭтотОбъект,Истина);
	ОткрытьФорму("ОбщаяФорма.ДниНедели", Новый Структура("Переодичность,ДниНедели,ДеньПоставкиПоДнямНедели", Истина, СокрЛП(ТекущиеДанные.ДниНедели), СокрЛП(Элемент.ТекстРедактирования)), ЭтотОбъект,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТочкиПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.Точки.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		//При активизации строки ТекДанные не заполонены
		Элементы.ТоварыДокумента1.ОтборСтрок = Неопределено;
	Иначе
		// Для ТЧ устанавливаем отбор по текущему сотруднику
		ТочкаДоставки = ТекДанные.ТочкаДоставки;
		Элементы.ТоварыДокумента1.ОтборСтрок = Новый ФиксированнаяСтруктура("ТочкаДоставки", ТочкаДоставки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТочкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если Не ОтменаРедактирования Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		мСтрокТоваров = Объект.ТоварыДокумента.НайтиСтроки(Новый Структура("ТочкаДоставки",ТекущиеДанные.ТочкаДоставки));
		ЧтоМеняем = ЧтоМеняем();
		УстановитьПараметр(мСтрокТоваров,ЧтоМеняем,ТекущиеДанные[ЧтоМеняем]);	
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаКлиенте
Процедура ДобавитьТовар(Команда)
	ТочкиТекущиеДанные = Элементы.Точки.ТекущиеДанные;
	Если ТочкиТекущиеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект.ТоварыДокумента.Добавить(),ТочкиТекущиеДанные);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТоварПоСписку(Команда)
	ВыделенныеСтроки = Элементы.Точки.ВыделенныеСтроки;
	Если ВыделенныеСтроки = Неопределено ИЛИ ВыделенныеСтроки.Количество() <= 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выделено ни одной точки доставки.'"));
		Возврат;
	КонецЕсли;
	ЗаполнитьПоСписку(ВыделенныеСтроки,Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТоварПоШтрихкоду(Команда)
	ВыделенныеСтроки = Элементы.Точки.ВыделенныеСтроки;
	Если ВыделенныеСтроки = Неопределено ИЛИ ВыделенныеСтроки.Количество() <= 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выделено ни одной точки доставки.'"));
		Возврат;
	КонецЕсли;
	ЗаполнитьПоСписку(ВыделенныеСтроки,Истина);	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТовар(Команда)
	Если ЭтоСтраницаСДеревом() Тогда
		ТекущаяСтрока = ДеревоДанных.НайтиПоИдентификатору(Элементы.ДеревоДанных.ТекущаяСтрока);
		СтрокаРодитель      = ТекущаяСтрока.ПолучитьРодителя();
		Если СтрокаРодитель = Неопределено Тогда
			ДеревоДанных.ПолучитьЭлементы().Удалить(ТекущаяСтрока);
		Иначе
			СтрокаРодитель.ПолучитьЭлементы().Удалить(ТекущаяСтрока);
		КонецЕсли;
	Иначе
		ТекущаяСтрока = Объект.ТоварыДокумента.НайтиПоИдентификатору(Элементы.ТоварыДокумента1.ТекущаяСтрока);
		Объект.ТоварыДокумента.Удалить(ТекущаяСтрока);
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТочку(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрДляВыделенного(Команда)
	НужныйТипПараметра = СоответствиеКолонок.НайтиПоЗначению(ТипПараметра);
	Если НужныйТипПараметра <> Неопределено Тогда
		Если ЭтоСтраницаСДеревом() Тогда
			ВыделенныеСтроки = Элементы.ДеревоДанных.ВыделенныеСтроки;	
			Если ВыделенныеСтроки = Неопределено ИЛИ ВыделенныеСтроки.Количество() <= 0 Тогда
				ПоказатьПредупреждение(,НСтр("ru = 'Не выделено ни одной точки доставки.'"));
				Возврат;
			КонецЕсли;
			
			Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
				Строка = ДеревоДанных.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если НужныйТипПараметра.Пометка Тогда
					Если Строка.ПолучитьРодителя() = Неопределено Тогда
						Строка[НужныйТипПараметра.Представление] = ЗначениеПараметра;
					КонецЕсли;
				Иначе
					Если Строка.ПолучитьРодителя() = Неопределено Тогда
						Товары = Строка.ПолучитьЭлементы();
						Для Каждого Товар Из Товары Цикл
							Товар[НужныйТипПараметра.Представление] = ЗначениеПараметра;
						КонецЦикла;
					Иначе
						Строка[НужныйТипПараметра.Представление] = ЗначениеПараметра;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			ВыделенныеСтроки = Элементы.Точки.ВыделенныеСтроки;
			Если ВыделенныеСтроки = Неопределено ИЛИ ВыделенныеСтроки.Количество() <= 0 Тогда
				ПоказатьПредупреждение(,НСтр("ru = 'Не выделено ни одной точки доставки.'"));
				Возврат;
			КонецЕсли;
			
			Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
				КопируемаяСтрока = Точки.НайтиПоИдентификатору(ИдентификаторСтроки);
				Если НужныйТипПараметра.Пометка Тогда
					КопируемаяСтрока[ТипПараметра] = ЗначениеПараметра;
				КонецЕсли;
				мСтрокТоваров = Объект.ТоварыДокумента.НайтиСтроки(Новый Структура("ТочкаДоставки",КопируемаяСтрока.ТочкаДоставки));
				УстановитьПараметр(мСтрокТоваров,ТипПараметра,ЗначениеПараметра);	
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрДляВсего(Команда)
	НужныйТипПараметра = СоответствиеКолонок.НайтиПоЗначению(ТипПараметра);
	Если НужныйТипПараметра <> Неопределено Тогда
		Если ЭтоСтраницаСДеревом() Тогда
			ТТочки = ДеревоДанных.ПолучитьЭлементы();
			Для Каждого Точка Из ТТочки Цикл
				Если НужныйТипПараметра.Пометка Тогда
					Точка[НужныйТипПараметра.Представление] = ЗначениеПараметра;
				Иначе
					Товары = Точка.ПолучитьЭлементы();
					Для Каждого Товар Из Товары Цикл
						Товар[НужныйТипПараметра.Представление] = ЗначениеПараметра;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если НужныйТипПараметра.Пометка Тогда
				УстановитьПараметр(Точки,ТипПараметра,ЗначениеПараметра);	
			КонецЕсли;
		КонецЕсли;
		УстановитьПараметр(Объект.ТоварыДокумента,ТипПараметра,ЗначениеПараметра);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура РазвернутьУзлыДерева(Команда)
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоДанных",,Истина)
КонецПроцедуры

&НаКлиенте
Процедура СвернутьУзлыДерева(Команда)
	КоллекцияЭлементовДерева = ДеревоДанных.ПолучитьЭлементы();
	//Свернуть дерево 
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоДанных.Свернуть(ИдентификаторСтроки);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДерево(Команда)
	СформироватьДерево();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Процедура УстановитьУсловноеОформлениеНаКлиенте()		
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДниНедели(Результат,Переодичность)	Экспорт
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаТоварыДокумента Тогда
		ТекущиеДанные = Элементы.ТоварыДокумента.ТекущиеДанные;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПараметрыЗаказов Тогда
		ТекущиеДанные = Элементы.Точки.ТекущиеДанные;
	КонецЕсли;
	Если Переодичность Тогда
		ТекущиеДанные.СрокПоставкиПоДнямНедели = Результат;
	Иначе
		ТекущиеДанные.ДниНедели = Результат;
		ПроверкаДниНедели();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаДниНедели()
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаТоварыДокумента Тогда
		ТекущиеДанные = Элементы.ТоварыДокумента.ТекущиеДанные;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаПараметрыЗаказов Тогда
		ТекущиеДанные = Элементы.Точки.ТекущиеДанные;
	КонецЕсли;
	
	Если ТекущиеДанные.ПериодичностьЗаказа = ПредопределенноеЗначение("Перечисление.ВидыПериодичностиЗаказов.Неактуальный") Тогда
	ИначеЕсли СтрДлина(СокрЛП(ТекущиеДанные.ДниНедели))>1 И ТекущиеДанные.ПериодичностьЗаказа <> ПредопределенноеЗначение("Перечисление.ВидыПериодичностиЗаказов.Ежедневно")
		И ТекущиеДанные.ПериодичностьЗаказа <> ПредопределенноеЗначение("Перечисление.ВидыПериодичностиЗаказов.НеИспользовать") Тогда
		ПоказатьПредупреждение(,"Установленная периодичность '"+ТекущиеДанные.ПериодичностьЗаказа+"' подразумевает выбор только одного дня недели!!!");	
	ИначеЕсли СтрДлина(СокрЛП(ТекущиеДанные.ДниНедели))=1 И ТекущиеДанные.ПериодичностьЗаказа = ПредопределенноеЗначение("Перечисление.ВидыПериодичностиЗаказов.Ежедневно") Тогда
		ПоказатьПредупреждение(,"Установленная периодичность '"+ТекущиеДанные.ПериодичностьЗаказа+"'"+Символы.ПС+" НЕ подразумевает выбор только одного дня недели!!!");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьДерево()
	
	_ДеревоДанных = РеквизитФормыВЗначение("ДеревоДанных");
	_ДеревоДанных.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.Договор КАК Договор,
	|	ТЧ.ТочкаДоставки КАК ТочкаДоставки,
	|	ТЧ.РегламентЗаказа КАК РегламентЗаказа,
	|	ТЧ.ПериодичностьЗаказа КАК ПериодичностьЗаказа,
	|	ТЧ.ДниНедели КАК ДниНедели,
	|	ТЧ.СрокПоставки КАК СрокПоставки,
	|	ТЧ.СрокПоставкиПоДнямНедели КАК СрокПоставкиПоДнямНедели,
	|	ТЧ.Номенклатура КАК Номенклатура,
	|	ТЧ.ВариантОкругления КАК ВариантОкругления,
	|	ТЧ.КвантПоставки КАК КвантПоставки,
	|	ТЧ.МодельРасчета КАК МодельРасчета,
	|	ТЧ.Количество КАК Количество,
	|	ТЧ.МинимальныйОстаток КАК МинимальныйОстаток,
	|	ТЧ.ОстатокВитрина КАК ОстатокВитрина
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЧ КАК ТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Договор КАК Договор,
	|	ВТ.ТочкаДоставки КАК ТочкаДоставки,
	|	ВТ.РегламентЗаказа КАК РегламентЗаказа,
	|	ВТ.ПериодичностьЗаказа КАК ПериодичностьЗаказа,
	|	ВТ.ДниНедели КАК ДниНедели,
	|	ВТ.СрокПоставки КАК СрокПоставки,
	|	ВТ.СрокПоставкиПоДнямНедели КАК СрокПоставкиПоДнямНедели,
	|	ВТ.Номенклатура КАК Номенклатура,
	|	ВТ.ВариантОкругления КАК ВариантОкругления,
	|	ВТ.КвантПоставки КАК КвантПоставки,
	|	ВТ.МодельРасчета КАК МодельРасчета,
	|	ВТ.Количество КАК Количество,
	|	ВТ.МинимальныйОстаток КАК МинимальныйОстаток,
	|	ВТ.ОстатокВитрина КАК ОстатокВитрина
	|ИЗ
	|	ВТ КАК ВТ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Договор,
	|	ТочкаДоставки,
	|	Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(Договор),
	|	МАКСИМУМ(РегламентЗаказа),
	|	МАКСИМУМ(ПериодичностьЗаказа),
	|	МАКСИМУМ(ДниНедели),
	|	МАКСИМУМ(СрокПоставки),
	|	МАКСИМУМ(СрокПоставкиПоДнямНедели)
	|ПО
	|	ТочкаДоставки
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ТЧ", Объект.ТоварыДокумента.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТочкаДоставки = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаТочкаДоставки.Следующий() Цикл
		стрТочкаДоставки = _ДеревоДанных.Строки.Добавить();
		стрТочкаДоставки.ТочкаДоставки			= ВыборкаТочкаДоставки.ТочкаДоставки;
		стрТочкаДоставки.ТТ_Товар				= ВыборкаТочкаДоставки.ТочкаДоставки;
		стрТочкаДоставки.Код					= ВыборкаТочкаДоставки.ТочкаДоставки.Global_ID;
		стрТочкаДоставки.Договор_Округление		= ВыборкаТочкаДоставки.Договор;
		стрТочкаДоставки.Регламент_Квант		= ВыборкаТочкаДоставки.РегламентЗаказа;
		стрТочкаДоставки.Периодичность_Расчет	= ВыборкаТочкаДоставки.ПериодичностьЗаказа;
		стрТочкаДоставки.ДниНедели_Количество	= ВыборкаТочкаДоставки.ДниНедели;
		стрТочкаДоставки.СрокПоставки_МинОстаток= ВыборкаТочкаДоставки.СрокПоставки;
		стрТочкаДоставки.СрокПоставкиДни_Витрина= ВыборкаТочкаДоставки.СрокПоставкиПоДнямНедели;
		
		ВыборкаТовар = ВыборкаТочкаДоставки.Выбрать();
		
		Пока ВыборкаТовар.Следующий() Цикл
			стрТовар = стрТочкаДоставки.Строки.Добавить();
			стрТовар.ТТ_Товар				= ВыборкаТовар.Номенклатура;
			стрТовар.Код					= ВыборкаТовар.Номенклатура.Артикул;
			стрТовар.Договор_Округление		= ВыборкаТовар.ВариантОкругления;
			стрТовар.Регламент_Квант		= ВыборкаТовар.КвантПоставки;
			стрТовар.Периодичность_Расчет	= ВыборкаТовар.МодельРасчета;
			стрТовар.ДниНедели_Количество	= ВыборкаТовар.Количество;
			стрТовар.СрокПоставки_МинОстаток= ВыборкаТовар.МинимальныйОстаток;
			стрТовар.СрокПоставкиДни_Витрина= ВыборкаТовар.ОстатокВитрина;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(_ДеревоДанных, "ДеревоДанных");
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТочки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НЕ ИзменениеПараметровСистемыЗаказовТоварыДокумента.ТочкаДоставки ЕСТЬ NULL КАК Есть_,
	|	ТочкиДоставки.Ссылка КАК ТочкаДоставки,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.РегламентЗаказа КАК РегламентЗаказа,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.Ссылка.Договор КАК Договор,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ПериодичностьЗаказа КАК ПериодичностьЗаказа,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ДниНедели КАК ДниНедели,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.СрокПоставки КАК СрокПоставки,
	|	ИзменениеПараметровСистемыЗаказовТоварыДокумента.СрокПоставкиПоДнямНедели КАК СрокПоставкиПоДнямНедели
	|ИЗ
	|	Справочник.ТочкиДоставки КАК ТочкиДоставки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровСистемыЗаказов.ТоварыДокумента КАК ИзменениеПараметровСистемыЗаказовТоварыДокумента
	|		ПО ТочкиДоставки.Ссылка = ИзменениеПараметровСистемыЗаказовТоварыДокумента.ТочкаДоставки
	|			И (ИзменениеПараметровСистемыЗаказовТоварыДокумента.Ссылка = &Ссылка)
	|ГДЕ
	|	ТочкиДоставки.Владелец = &Владелец
	|	И ТочкиДоставки.Действует
	|	И НЕ ТочкиДоставки.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Есть_ УБЫВ,
	|	Договор,
	|	ТочкаДоставки
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Контрагент);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Точки.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоСписку(ВыделенныеСтроки, ПоШтрихкоду)
	Подсказка = "Введите "+?(ПоШтрихкоду,"штрихкод","артикул")+" с новой строки";
	Строка = "";
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводСтроки", ЭтаФорма,Новый Структура("ВыделенныеСтроки, ПоШтрихкоду",ВыделенныеСтроки, ПоШтрихкоду));
	ПоказатьВводСтроки(ОписаниеОповещения, Строка, Подсказка,,Истина);
КонецПроцедуры

&НаСервере
Процедура ВводСтроки(Текст, ПереданныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Текст) Тогда 
		Возврат;
	КонецЕсли;
	
	ПоШтрихкоду = ПереданныеПараметры.ПоШтрихкоду;
	ВыделенныеСтроки = ПереданныеПараметры.ВыделенныеСтроки;
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		КопируемаяСтрока = Точки.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		Для й=1 По СтрЧислоСтрок(Текст) Цикл
			зн = СокрЛП(СтрПолучитьСтроку(Текст,й));
			Если ПустаяСтрока(зн) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПоШтрихкоду Тогда
				СтруктураПоШтрихКоду = Справочники.Номенклатура.НайтиНоменклатуруПоШтрихКоду(СокрЛП(зн));
				Если СтруктураПоШтрихКоду = Неопределено Тогда				
					ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не найдена номенклатура по ШтрихКоду ""%1""'; uk = 'Не знайдена номенклатура по штрихкоду ""%1""'"),
					зн));	
					Продолжить;
				Иначе
					Номенклатура = СтруктураПоШтрихКоду.Номенклатура;	
				КонецЕсли;
			Иначе
				Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", зн);
				Если Номенклатура.Пустая() Тогда				
					ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не найдена номенклатура с артикулом ""%1""'; uk = 'Не знайдена номенклатура з артикулом ""%1""'"),
					зн));	
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если Номенклатура.ЭтоГруппа Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Введена группа номенклатуры ""%1"" %2'; uk = 'Введено групу номенклатури ""%1"" %2'"),
				зн,
				Номенклатура));	
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = Объект.ТоварыДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, КопируемаяСтрока);
			НоваяСтрока.Номенклатура = Номенклатура;
		КонецЦикла;
		КопируемаяСтрока.Есть_ = Истина;
		Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьПараметр(СтрокиТоваров,ТипПараметра,ЗначениеПараметра)
	Для Каждого СтрокаТоваров Из СтрокиТоваров Цикл
		СтрокаТоваров[ТипПараметра] = ЗначениеПараметра;
	КонецЦикла;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ПолучитьТочкиКолонки() 
	РеквизитыТочки = ПолучитьРеквизиты("Точки");
	Для Каждого РеквизитТочки Из РеквизитыТочки Цикл 
		СоответствиеКолонок.Добавить(РеквизитТочки.Имя,);
	КонецЦикла;
КонецПроцедуры


&НаСервере
Функция ЧтоМеняем()
	ПутьКДанным = Элементы.Точки.ТекущийЭлемент.ПутьКДанным;
	ЧтоМеняем = СтрЗаменить(ПутьКДанным,"Точки.","");
	Возврат ЧтоМеняем;
КонецФункции

&НаКлиенте
Функция ЭтоСтраницаСДеревом()
	ЭтоСтраницаСДеревом = Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаДеревоПараметров;
	Возврат ЭтоСтраницаСДеревом;
КонецФункции

&НаСервере
Процедура ЗаполнитьСоответствиеКолонок()
	СоответствиеКолонок.Добавить("РегламентЗаказа",			"Регламент_Квант",			Истина);
	СоответствиеКолонок.Добавить("ПериодичностьЗаказа",		"Периодичность_Расчет",		Истина);
	СоответствиеКолонок.Добавить("ДниНедели",				"ДниНедели_Количество",		Истина);
	СоответствиеКолонок.Добавить("СрокПоставки",			"СрокПоставки_МинОстаток",	Истина);
	СоответствиеКолонок.Добавить("СрокПоставкиПоДнямНедели","СрокПоставкиДни_Витрина",	Истина);
	СоответствиеКолонок.Добавить("ВариантОкругления",		"Договор_Округление");
	СоответствиеКолонок.Добавить("КвантПоставки",			"Регламент_Квант");
	СоответствиеКолонок.Добавить("МодельРасчета",			"Периодичность_Расчет");
	СоответствиеКолонок.Добавить("Количество",				"ДниНедели_Количество");
	СоответствиеКолонок.Добавить("МинимальныйОстаток",		"СрокПоставки_МинОстаток");
	СоответствиеКолонок.Добавить("ОстатокВитрина",			"СрокПоставкиДни_Витрина");
КонецПроцедуры




#КонецОбласти
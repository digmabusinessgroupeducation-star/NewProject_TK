#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Проведение
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос = Новый Запрос;
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстыЗапроса = Новый СписокЗначений;
		ТекстЗапросаПараметрыСисетмыЗаказов(Запрос, ТекстыЗапроса, Регистры);
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		//Заполним порядок для регистра "Параметры системы заказов"
		ТаблицаПараметрыСисетмыЗаказов = Неопределено;
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПараметрыСисетмыЗаказов", ТаблицаПараметрыСисетмыЗаказов) И ТипЗнч(ТаблицаПараметрыСисетмыЗаказов) = Тип("ТаблицаЗначений") Тогда 		
			ДатаДокумента = Формат(ДополнительныеСвойства.ДляПроведения.Дата, "ДФ=ггггММддЧЧммсс");
			НомерДокумента = СокрЛП(ДополнительныеСвойства.ДляПроведения.Номер);
			
			Для Каждого Стр Из ТаблицаПараметрыСисетмыЗаказов Цикл 
				НачалоДействия = Формат(Стр.НачалоДействия, "ДФ=ггггММдд");
				ВремяОкончания = Формат(Окр((Дата("20500101") - Стр.КонецДействия)/86400, 0), "ЧГ=0");
				Пока СтрДлина(ВремяОкончания) < 5 Цикл 
					ВремяОкончания = "0" + ВремяОкончания;
				КонецЦикла;
				
				Стр.Порядок = НачалоДействия + ВремяОкончания + ДатаДокумента + НомерДокумента;
			КонецЦикла;
			
			ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыСисетмыЗаказов", ТаблицаПараметрыСисетмыЗаказов);
		КонецЕсли;
		
	КонецПроцедуры
	
	Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ИзменениеПараметровСистемыЗаказов.Дата КАК Период,
		               |	ИзменениеПараметровСистемыЗаказов.Ссылка КАК Ссылка,
		               |	ИзменениеПараметровСистемыЗаказов.Номер КАК Номер,
		               |	ИзменениеПараметровСистемыЗаказов.ХозОперация КАК ХозОперация,
		               |	ИзменениеПараметровСистемыЗаказов.Организация КАК Организация,
		               |	ИзменениеПараметровСистемыЗаказов.ПодразделениеКомпании КАК ПодразделениеКомпании,
		               |	ИзменениеПараметровСистемыЗаказов.Контрагент КАК Контрагент,
		               |	ИзменениеПараметровСистемыЗаказов.Договор КАК Договор
		               |ИЗ
		               |	Документ.ИзменениеПараметровСистемыЗаказов КАК ИзменениеПараметровСистемыЗаказов
		               |ГДЕ
		               |	ИзменениеПараметровСистемыЗаказов.Ссылка = &Ссылка";
		Реквизиты = Запрос.Выполнить().Выбрать();
		Реквизиты.Следующий();
		
		Запрос.УстановитьПараметр("Период",Реквизиты.Период);
		Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
		Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
		Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
		Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		Запрос.УстановитьПараметр("Договор",Реквизиты.Договор);
		
		ДополнительныеСвойства.ДляПроведения.Вставить("Номер", Реквизиты.Номер);
		ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
		ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);
		
	КонецПроцедуры
	
	Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)	
		
		ХозОперация = Неопределено;
		Запрос.Параметры.Свойство("ХозОперация", ХозОперация);
		
		ИмяРегистра = "ВтТаблицаТовары";
		
		//Если ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.УстановкаПараметровСистемыЗаказовВыборочная") Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.Ссылка КАК Ссылка,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ТочкаДоставки КАК ТочкаДоставки,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.Номенклатура КАК Номенклатура,
		               |	&Договор КАК Договор,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ПериодичностьЗаказа КАК ПериодичностьЗаказа,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.МодельРасчета КАК МодельРасчета,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ВариантОкругления КАК ВариантОкругления,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ДниНедели КАК ДниНедели,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.СрокПоставкиПоДнямНедели КАК СрокПоставкиПоДнямНедели,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.СрокПоставки КАК СрокПоставки,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.КвантПоставки КАК КвантПоставки,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.Количество КАК Количество,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.МинимальныйОстаток КАК МинимальныйОстаток,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.ОстатокВитрина КАК ОстатокВитрина,
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.РегламентЗаказа КАК РегламентЗаказа
		               |ПОМЕСТИТЬ ВтТаблицаТовары
		               |ИЗ
		               |	Документ.ИзменениеПараметровСистемыЗаказов.ТоварыДокумента КАК ИзменениеПараметровСистемыЗаказовТоварыДокумента
		               |ГДЕ
		               |	ИзменениеПараметровСистемыЗаказовТоварыДокумента.Ссылка = &Ссылка";
		
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
	КонецФункции
	
	Функция ТекстЗапросаПараметрыСисетмыЗаказов(Запрос, ТекстыЗапроса, Регистры)	
		
		ИмяРегистра = "ПараметрыСистемыЗаказов";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	&Период КАК Период,
		               |	ВтТаблицаТовары.ТочкаДоставки КАК ТочкаДоставки,
		               |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
		               |	ВтТаблицаТовары.Договор КАК Договор,
		               |	ВтТаблицаТовары.ПериодичностьЗаказа КАК ПериодичностьЗаказа,
		               |	ВтТаблицаТовары.МодельРасчета КАК МодельРасчета,
		               |	ВтТаблицаТовары.ВариантОкругления КАК ВариантОкругления,
		               |	ВтТаблицаТовары.ДниНедели КАК ДниНедели,
		               |	ВтТаблицаТовары.СрокПоставкиПоДнямНедели КАК СрокПоставкиПоДнямНедели,
		               |	ВтТаблицаТовары.СрокПоставки КАК СрокПоставки,
		               |	ВтТаблицаТовары.КвантПоставки КАК КвантПоставки,
		               |	ВтТаблицаТовары.Количество КАК Количество,
		               |	ВтТаблицаТовары.МинимальныйОстаток КАК МинимальныйОстаток,
		               |	ВтТаблицаТовары.ОстатокВитрина КАК ОстатокВитрина,
		               |	ВтТаблицаТовары.РегламентЗаказа КАК РегламентЗаказа
		               |ИЗ
		               |	ВтТаблицаТовары КАК ВтТаблицаТовары";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	
	#КонецОбласти	
	
	
	#Область ПрограммныйИнтерфейс
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(ПодразделениеКомпании)
		|	И ЗначениеРазрешено(ТорговаяПлощадка)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	
	#КонецОбласти
	
	
	
	
#КонецЕсли
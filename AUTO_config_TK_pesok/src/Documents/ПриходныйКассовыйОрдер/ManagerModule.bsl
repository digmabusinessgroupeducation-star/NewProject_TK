#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Стандартная для данной конфигурации функция форматирования сумм
//
// Параметры:
//  Сумма  - число, которое мы хотим форматировать,
//  Валюта - ссылка на элемент справочника валют, если задан, то к в результирующую строку
//           будет добавлено представление валюты
//  ЧН     - строка, представляющая нулевое значение числа,
//  ЧРГ    - символ-разделитель групп целой части числа.
//
// Возвращаемое значение:
//  Отформатированная должным образом строковое представление суммы.
//
Функция ФорматСумм(Сумма, Валюта = Неопределено, ЧН = "", ЧРГ = "") Экспорт

	ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2" +
					?(НЕ ЗначениеЗаполнено(ЧН), "", ";" + "ЧН=" + ЧН) +
					?(НЕ ЗначениеЗаполнено(ЧРГ),"", ";" + "ЧРГ=" + ЧРГ);

	РезультирующаяСтрока = СокрЛ(Формат(Сумма, ФорматнаяСтрока));

	Если ЗначениеЗаполнено(Валюта) Тогда
		РезультирующаяСтрока = РезультирующаяСтрока + " " + СокрП(Валюта);
	КонецЕсли;

	Возврат РезультирующаяСтрока;

КонецФункции // ФорматСумм()	
	
#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);

	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	Контрагент 	= ДополнительныеСвойства.ДляПроведения.Контрагент;
	Договор		= ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
	СтатьяДДС 	= ДополнительныеСвойства.ДляПроведения.СтатьяДДС;
	ВидДоговора = ДополнительныеСвойства.ДляПроведения.ВидДоговора;
	
	Если ВидДоговора = Перечисления.ВидыДоговоров.Покупка
		ИЛИ ВидДоговора = Перечисления.ВидыДоговоров.Бонус Тогда
		
		ЭтоРасчетСПоставщиком = Истина;
	Иначе
		ЭтоРасчетСПоставщиком = Ложь;
	КонецЕсли;

	ДДС_Взаиморасчеты = ДополнительныеСвойства.ДляПроведения.ДДС_Взаиморасчеты;
	ДДС_Дублировать = ДополнительныеСвойства.ДляПроведения.ДДС_Дублировать;	
	РегламентированныйУчет = ДополнительныеСвойства.ДляПроведения.РегламентированныйУчет;		
	
	ДвиженияВзаиморасчеты = ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(Договор);
	
	//Если ЗначениеЗаполнено(Контрагент) Тогда 
	//	Если ДДС_Взаиморасчеты Тогда 
	//		ДвиженияВзаиморасчеты = Истина;	
	//	ИначеЕсли НЕ (ДДС_Дублировать И РегламентированныйУчет)
	//		И НЕ (СтатьяДДС = ПредопределенноеЗначение("Справочник.СтатьиДДС.ИнкассацияДСчерезФР") И Не РегламентированныйУчет) Тогда		
	//		
	//		ДвиженияВзаиморасчеты = Истина;
	//	КонецЕсли;
	//КонецЕсли;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры);	
	
	Если ДвиженияВзаиморасчеты Тогда 
		Если ЭтоРасчетСПоставщиком Тогда 			
			ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);			
		Иначе
			ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);	
		КонецЕсли;		
	КонецЕсли;
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	Если ДвиженияВзаиморасчеты Тогда 		
		Если ЭтоРасчетСПоставщиком Тогда 
			ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПоставщиками(Запрос, ДополнительныеСвойства);
		Иначе
			ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
		КонецЕсли;	
	КонецЕсли;		
			
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПриходныйКассовыйОрдер.Дата КАК Период,
	               |	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	ПриходныйКассовыйОрдер.ХозОперация КАК ХозОперация,
	               |	ПриходныйКассовыйОрдер.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ПриходныйКассовыйОрдер.Организация КАК Организация,
	               |	ПриходныйКассовыйОрдер.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПриходныйКассовыйОрдер.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПриходныйКассовыйОрдер.Контрагент КАК Контрагент,
	               |	ПриходныйКассовыйОрдер.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПриходныйКассовыйОрдер.ДоговорВзаиморасчетов.ВидДоговора.ВидДоговора КАК ВидДоговора,
	               |	ПриходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПриходныйКассовыйОрдер.МоментВремени КАК МоментВремени,
	               |	ПриходныйКассовыйОрдер.КурсДокумента КАК КурсДокумента,
	               |	ПриходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	               |	ПриходныйКассовыйОрдер.ДокументОснование КАК Сделка,
	               |	ПриходныйКассовыйОрдер.СтатьяДДС КАК СтатьяДДС,
	               |	ПриходныйКассовыйОрдер.СтатьяДДС.Взаиморасчеты КАК ДДС_Взаиморасчеты,
	               |	ПриходныйКассовыйОрдер.СтатьяДДС.Дублировать КАК ДДС_Дублировать
	               |ИЗ
	               |	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	               |ГДЕ
	               |	ПриходныйКассовыйОрдер.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент", Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов", Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("ВидДоговора", Реквизиты.ВидДоговора);
	ДополнительныеСвойства.ДляПроведения.Вставить("СуммаДокумента", Реквизиты.СуммаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента", Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("Сделка", Реквизиты.Сделка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СтатьяДДС", Реквизиты.СтатьяДДС);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДДС_Взаиморасчеты", Реквизиты.ДДС_Взаиморасчеты);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДДС_Дублировать", Реквизиты.ДДС_Дублировать);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);

КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	ПриходныйКассовыйОрдер.Номер КАК Номер,
	               |	ПриходныйКассовыйОрдер.Дата КАК Дата,
	               |	ПриходныйКассовыйОрдер.Организация КАК Организация,
	               |	ПриходныйКассовыйОрдер.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПриходныйКассовыйОрдер.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПриходныйКассовыйОрдер.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ПриходныйКассовыйОрдер.Контрагент КАК Контрагент,
	               |	ПриходныйКассовыйОрдер.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПриходныйКассовыйОрдер.КассаКомпании КАК КассаКомпании,
	               |	ПриходныйКассовыйОрдер.ХозОперация КАК ХозОперация,
	               |	ПриходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	               |	ПриходныйКассовыйОрдер.СтавкаНДС КАК СтавкаНДС,
	               |	ПриходныйКассовыйОрдер.СуммаНДС КАК СуммаНДС,
	               |	ПриходныйКассовыйОрдер.СтатьяДДС КАК СтатьяДДС,
	               |	ПриходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПриходныйКассовыйОрдер.КурсДокумента КАК КурсДокумента,
	               |	ПриходныйКассовыйОрдер.ПринятоОТ КАК ПринятоОт,
	               |	ПриходныйКассовыйОрдер.Основание КАК Основание,
	               |	ПриходныйКассовыйОрдер.Приложение КАК Приложение,
	               |	ПриходныйКассовыйОрдер.НомерЧека КАК НомерЧека,
	               |	ПриходныйКассовыйОрдер.НомерКО КАК НомерКО,
	               |	ПриходныйКассовыйОрдер.КассаККМ КАК КассаККМ,
	               |	ПриходныйКассовыйОрдер.Директор КАК Директор,
	               |	ПриходныйКассовыйОрдер.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	               |	ПриходныйКассовыйОрдер.СтаршийКассир КАК СтаршийКассир,
	               |	ПриходныйКассовыйОрдер.ДокументОснование КАК ДокументОснование
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	               |ГДЕ
	               |	ПриходныйКассовыйОрдер.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.ВалютаДокумента КАК Валюта,
	              |	ВтТаблицаТовары.КассаКомпании КАК СтруктурнаяЕдиница,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ВтТаблицаТовары.СтатьяДДС КАК СтатьяДДС
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.СуммаДокумента * ВтТаблицаТовары.КурсДокумента КАК СуммаБаз,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеДебиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПокупателями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.СуммаДокумента * ВтТаблицаТовары.КурсДокумента КАК СуммаБаз,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеДебиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
		
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");

	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.ДоговорКонтрагента = ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
	НоваяСтрока.Период             = ДополнительныеСвойства.ДляПроведения.Дата;
	
	ТаблицыДляПоследовательности.Вставить("Взаиморасчеты", ТаблицаПоследовательности);
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности", ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приходный кассовый ордер
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ПриходныйКассовыйОрдер";
    КомандаПечати.Идентификатор = "ПриходныйКассовыйОрдер";
    КомандаПечати.Представление = НСтр("ru = 'Приходный кассовый ордер'; uk = 'Прибутковий касовий ордер'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	//МассивУсловий = Новый Массив;
	//МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "РегламентированныйУчет", Истина, ВидСравнения.Равно));	
	//КомандаПечати.УсловияВидимости = МассивУсловий;

	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Приходный кассовый ордер
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходныйКассовыйОрдер") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПриходныйКассовыйОрдер",
		НСтр("ru = 'Приходный кассовый ордер'; uk = 'Прибутковий касовий ордер'"), 
		ПечатьПриходныйКассовыйОрдер(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ПриходныйКассовыйОрдер.ПФ_MXL_ПриходныйКассовыйОрдер");
	КонецЕсли;	
	
КонецПроцедуры

// Приходный кассовый ордер
Функция ПечатьПриходныйКассовыйОрдер(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПриходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	ПриходныйКассовыйОрдер.Дата КАК Дата,
	               |	ПриходныйКассовыйОрдер.Номер КАК Номер,
	               |	ПриходныйКассовыйОрдер.Организация КАК Организация,
	               |	ПриходныйКассовыйОрдер.Организация.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	               |	ПриходныйКассовыйОрдер.НомерКО КАК НомерОрдера,
	               |	ПриходныйКассовыйОрдер.Организация.НаименованиеПолное КАК ПолноеНаименование,
	               |	ПриходныйКассовыйОрдер.СтатьяДДС.КоррCчет КАК Счет,
	               |	ПриходныйКассовыйОрдер.СуммаДокумента КАК Сумма,
	               |	ПриходныйКассовыйОрдер.ПринятоОТ КАК ПринятоОТ,
	               |	ПриходныйКассовыйОрдер.Основание КАК Основание,
	               |	ПриходныйКассовыйОрдер.Приложение КАК Приложение,
	               |	ПриходныйКассовыйОрдер.ГлавныйБухгалтер КАК ФИОГлавногоБухгалтера,
	               |	ПриходныйКассовыйОрдер.СтаршийКассир КАК ФИОКассира,
	               |	ПриходныйКассовыйОрдер.СуммаНДС КАК СуммаНДС,
	               |	ПриходныйКассовыйОрдер.ВыводитьНаПечатьСуммуНДС КАК ВыводитьНаПечатьСуммуНДС,
	               |	ПриходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента
	               |ИЗ
	               |	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	               |ГДЕ
	               |	ПриходныйКассовыйОрдер.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПКО";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk";
	//КодЯзыкаПечать = КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_КО1");

	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");

		ДопПараметрыСтроки = Новый Структура;
		ДопПараметрыСтроки.Вставить("ДатаДокумента", Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(ВыборкаДокументов.Сумма, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);

		ДопПараметрыСтроки.Вставить("СуммаПрописью", СуммаПрописью);
										
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаДокументов);
		ОбластьШапка.Параметры.Заполнить(ДопПараметрыСтроки);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		СуммаНДС =  ВыборкаДокументов.СуммаНДС;
		
		Если ВыборкаДокументов.ВыводитьНаПечатьСуммуНДС Тогда
			
			ОбластьСекцияВТомЧисле = Макет.ПолучитьОбласть("СекцияВТомЧисле");
			
			Если СуммаНДС = 0 Тогда
				ОбластьСекцияВТомЧисле.Параметры.ВТомЧисле = "0,00"+ " " + СокрП(ВыборкаДокументов.ВалютаДокумента);
			Иначе
				ОбластьСекцияВТомЧисле.Параметры.ВТомЧисле = ФорматСумм(СуммаНДС, ВыборкаДокументов.ВалютаДокумента);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСекцияВТомЧисле);
			
		КонецЕсли;

		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		ДопПараметрыСтроки.Вставить("ФИОГлавногоБухгалтера",  ОбщегоНазначенияТК.ФамилияИнициалы(ВыборкаДокументов.ФИОГлавногоБухгалтера.ФИО));
		ДопПараметрыСтроки.Вставить("ФИОКассира",			  ОбщегоНазначенияТК.ФамилияИнициалы(ВыборкаДокументов.ФИОКассира.ФИО));		
		
		ОбластьПодвал.Параметры.Заполнить(ВыборкаДокументов);
		ОбластьПодвал.Параметры.Заполнить(ДопПараметрыСтроки);
		
		ТабличныйДокумент.Вывести(ОбластьПодвал);

				
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


#КонецОбласти	
	
	
	
	
	
	
#КонецЕсли
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;

	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("РасходныйКассовыйОрдер"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьСостояниеДокумента();
	
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПриЧтенииСозданииНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	УстановитьСостояниеДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
        И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
        СвойстваВыполнитьОтложеннуюИнициализацию();
        УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
    КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти


#Область ОбработчикиСобытийРеквизитовШапки

//Изменение реквизита "Дата"
&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	ЗаполнитьПоказателиОстатков(Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "КассаКомпании"
&НаСервере
Процедура КассаКомпанииПриИзмененииНаСервере()
	ЗаполнитьПоказателиОстатков(Истина);
КонецПроцедуры

&НаКлиенте
Процедура КассаКомпанииПриИзменении(Элемент)
	КассаКомпанииПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "Контрагент"
&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	ЗаполнитьПоказателиОстатков(, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "ДоговорВзаиморасчетов"
&НаСервере
Процедура ДоговорВзаиморасчетовПриИзмененииНаСервере()
	ЗаполнитьПоказателиОстатков(, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	ДоговорВзаиморасчетовПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "Организация"
&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "ТорговаяПлощадка"
&НаСервере
Процедура ТорговаяПлощадкаПриИзмененииНаСервере()
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПриИзменении(Элемент)
	ТорговаяПлощадкаПриИзмененииНаСервере();
КонецПроцедуры

//Изменение реквизита "РегламентированныйУчет"
&НаСервере
Процедура РегламентированныйУчетПриИзмененииНаСервере()
	ЗаполнитьПоказателиОстатков(Истина, Истина);
КонецПроцедуры

&НаКлиенте
Процедура РегламентированныйУчетПриИзменении(Элемент)
	РегламентированныйУчетПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства


&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	ЗаполнитьПоказателиОстатков(Истина, Истина);
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	//УстановитьВидимостьПоказателейОстаткаИДолга();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПоказателейОстаткаИДолга(ЗаполнитьОстатокВКассе, ЗаполнитьДолгПоДоговору) Экспорт 
	Если ЗаполнитьОстатокВКассе Тогда 
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НадписьОстаток", "Видимость", ЗначениеЗаполнено(Объект.КассаКомпании) И ЗначениеЗаполнено(Объект.ВалютаДокумента));
	КонецЕсли;
	
	Если ЗаполнитьДолгПоДоговору Тогда 
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НадписьДолгПоДоговору", "Видимость", ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиОстатков(ЗаполнитьОстатокВКассе = Ложь, ЗаполнитьДолгПоДоговору = Ложь) Экспорт 
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Дата", Новый МоментВремени(Объект.Дата, Объект.Ссылка));
	
	УстановитьВидимостьПоказателейОстаткаИДолга(ЗаполнитьОстатокВКассе, ЗаполнитьДолгПоДоговору);
	
	ВалютаДокумента = Объект.ВалютаДокумента;
	Если ЗаполнитьОстатокВКассе И Элементы.НадписьОстаток.Видимость Тогда 			
		СтруктураПараметров.Вставить("КассаКомпании", Объект.КассаКомпании);
		СтруктураПараметров.Вставить("ВалютаДокумента", ВалютаДокумента);						
		
		чОстатокВКассе = ПолучитьОстатокВКассе(СтруктураПараметров);
		
		ОстатокВКассе = НСтр("ru = 'Остаток в кассе: '; uk = 'Залишок в касі: '") + Формат(чОстатокВКассе, "ЧДЦ=2; ЧН=") + " " + ВалютаДокумента;
		Элементы.НадписьОстаток.Заголовок = ОстатокВКассе;
	КонецЕсли;
	
	Если ЗаполнитьДолгПоДоговору И Элементы.НадписьДолгПоДоговору.Видимость Тогда 		
		СтруктураПараметров.Вставить("РегламентированныйУчет", Объект.РегламентированныйУчет);
		СтруктураПараметров.Вставить("ДоговорВзаиморасчетов", Объект.ДоговорВзаиморасчетов);
		
		чДолгПоДоговору = ПолучитьДолгПоДоговору(СтруктураПараметров);		
		
		ДолгПоДоговору = НСтр("ru = 'По договору долг контрагента составляет: '; uk = 'По договору долг контрагента составляет: '") + Формат(чДолгПоДоговору, "ЧДЦ=2; ЧН=") + " " + ВалютаДокумента;
		Элементы.НадписьДолгПоДоговору.Заголовок = ДолгПоДоговору;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОстатокВКассе(СтруктураПараметров)
	
	Если ПравоДоступа("Чтение",Метаданные.РегистрыНакопления.ДенежныеСредстваКомпании)Тогда	
		
		ТекДата = ?(Не ЗначениеЗаполнено(СтруктураПараметров.Дата), ТекущаяДата(), СтруктураПараметров.Дата);
		Граница = Новый Граница(ТекДата, ВидГраницы.Включая);
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ДенежныеСредстваКомпанииОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКомпании.Остатки(
		|			&Дата,
		|			СтруктурнаяЕдиница ССЫЛКА Справочник.КассыКомпании
		|				И СтруктурнаяЕдиница = &КассаКомпании
		|				И Валюта = &ВалютаДокумента) КАК ДенежныеСредстваКомпанииОстатки");
		
		
		Запрос.УстановитьПараметр("Дата", Граница);
		Запрос.УстановитьПараметр("КассаКомпании", СтруктураПараметров.КассаКомпании);
		Запрос.УстановитьПараметр("ВалютаДокумента", СтруктураПараметров.ВалютаДокумента);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда 
			Возврат Выборка.Сумма;
		Иначе 
			Возврат 0;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДолгПоДоговору(СтруктураПараметров)
	
	Если ПравоДоступа("Чтение",Метаданные.РегистрыНакопления.РасчетыСПоставщиками) Тогда	
		
		ТекДата = ?(Не ЗначениеЗаполнено(СтруктураПараметров.Дата), ТекущаяДата(), СтруктураПараметров.Дата);
		Граница = Новый Граница(ТекДата, ВидГраницы.Включая);
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК Долг
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
		|			&Дата,
		|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПоставщикамиОстатки,
		|	РегистрНакопления.РасчетыСПокупателями.Остатки(
		|			&Дата,
		|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		|				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПокупателямиОстатки");
		
		Запрос.УстановитьПараметр("Дата", Граница);
		Запрос.УстановитьПараметр("РегламентированныйУчет", СтруктураПараметров.РегламентированныйУчет);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", СтруктураПараметров.ДоговорВзаиморасчетов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда 
			Возврат Выборка.Долг;
		Иначе 
			Возврат 0;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
		
КонецФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры


#КонецОбласти
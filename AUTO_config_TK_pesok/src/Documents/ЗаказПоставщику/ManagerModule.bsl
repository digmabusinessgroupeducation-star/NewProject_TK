#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаЗаказыПоставщикам(Запрос, ТекстыЗапроса, Регистры);
	Если ДокументСсылка.Контрагент.ВидКонтрагента = Перечисления.ВидыКонтрагентов.Внутренний Тогда
		ТекстЗапросаТаблицаОбработкаЗаказовПоставщику(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПоставщику.Дата КАК Период,
	|	ЗаказПоставщику.Ссылка КАК Ссылка,
	|	ЗаказПоставщику.ХозОперация КАК ХозОперация,
	|	ЗаказПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПоставщику.Организация КАК Организация,
	|	ЗаказПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПоставщику.Контрагент КАК Контрагент,
	|	ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
	|	ЗаказПоставщикуТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказПоставщикуТовары.Ссылка.Организация КАК Организация,
	|	ЗаказПоставщикуТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПоставщикуТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.Коэффициент КАК Коэффициент,
	|	ЗаказПоставщикуТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	|	ЗаказПоставщикуТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказПоставщикуТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ЗаказПоставщикуТовары.Ссылка.ХозОперация КАК ХозОперация,
	|	ЗаказПоставщикуТовары.КоличествоБазовое КАК Количество,
	|	ЗаказПоставщикуТовары.СкладКомпании КАК СкладКомпании,
	|	ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка = &Ссылка
	|	И ЗаказПоставщикуТовары.Количество > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗаказыПоставщикам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЗаказыПоставщикам";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.Контрагент КАК Контрагент,
	|	ВтТаблицаТовары.Ссылка КАК ЗаказПоставщику,
	|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВтТаблицаТовары.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	СУММА(ВтТаблицаТовары.Количество) КАК Заказано,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.Ссылка,
	|	ВтТаблицаТовары.ХозОперация,
	|	ВтТаблицаТовары.Контрагент,
	|	ВтТаблицаТовары.Номенклатура,
	|	ВтТаблицаТовары.ТорговаяПлощадка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОбработкаЗаказовПоставщику(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОбработкаЗаказовПоставщику";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.Ссылка КАК ЗаказПоставщику,
	|	ВтТаблицаТовары.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	1 КАК Обработан,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|ГДЕ
	|	ВтТаблицаТовары.ТорговаяПлощадка <> ЗНАЧЕНИЕ(Справочник.ТорговыеПлощадки.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти	



#Область EDI

Процедура ОтправитьПоEDI(ЗаказСсылка) Экспорт 
	
	
	Если НЕ ЗаказСсылка.Проведен Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Документ %1 не проведен, отправка отменена!'; uk = 'Документ %1 не проведений, відправка скасована!'"),
		Строка(ЗаказСсылка)));
		Возврат;
	КонецЕсли;
	
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НаДату", ЗаказСсылка.Дата);
	СтруктураПараметров.Вставить("ДокументСсылка", ЗаказСсылка);
	
	ТаблицаЗаказовПоставщику = ТК_ОбменEDI.ПолучитьТаблицуЗаказовПоставщику(СтруктураПараметров);
	
	Если ТаблицаЗаказовПоставщику.Количество() = 0 Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Документ %1 не выгружен!'; uk = 'Документ %1 не вивантажений!'"),
		Строка(ЗаказСсылка)));
		
		Возврат;
	КонецЕсли;	
	
	СтруктураПараметров.Вставить("ТаблицаДокументов", ТаблицаЗаказовПоставщику);
	
	ТК_ОбменEDI.СформироватьИсходящийДокумент(СтруктураПараметров);
	
КонецПроцедуры


Функция СвормироватьСтруктуру_ORDER(ДокументСсылка,ТорговаяПлощадка = Неопределено)Экспорт
	
	МассивДокументов = Новый Массив;
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",ТорговаяПлощадка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщику.Номер КАК Номер,
	|	ЗаказПоставщику.Дата КАК Дата,
	|	ЗаказПоставщику.СрокПоставки КАК СрокПоставки,
	|	ЗаказПоставщику.ДоговорВзаиморасчетов.Организация.GLN КАК ОрганизацияGLN,
	|	ЗаказПоставщику.Контрагент.GLN КАК КонтрагентGLN,
	|	ЗаказПоставщику.Контрагент.КодПоЕДРПОУ КАК ПоставщикОКПО,
	|	ЗаказПоставщику.Контрагент.Код КАК CAMPAIGNNUMBER,
	|	ЗаказПоставщику.ПодразделениеКомпании.GLN КАК ИдентификаторGLN,
	|	ЗаказПоставщику.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	NUMBER 		 = СокрЛП(ВыборкаДетальныеЗаписи.Номер);
	DATE   		 = Формат(ВыборкаДетальныеЗаписи.Дата,"ДФ=""гггг-ММ-дд""");
	DELIVERYDATE = ?(ВыборкаДетальныеЗаписи.СрокПоставки=Дата("00010101"),Формат(ВыборкаДетальныеЗаписи.Дата,"ДФ=""гггг-ММ-дд"""),Формат(ВыборкаДетальныеЗаписи.СрокПоставки,"ДФ=""гггг-ММ-дд""")); 
	DELIVERYTIME = ?(ВыборкаДетальныеЗаписи.СрокПоставки=Дата("00010101"),Неопределено,Формат(ВыборкаДетальныеЗаписи.СрокПоставки,"ДФ=HH:mm")); 
	CAMPAIGNNUMBER =  СокрЛП(ВыборкаДетальныеЗаписи.CAMPAIGNNUMBER);
	CURRENCY       = "UAH";
	INFO           = Неопределено;
	SUPPLIER       =  СокрЛП(ВыборкаДетальныеЗаписи.КонтрагентGLN);
	BUYER          =  СокрЛП(ВыборкаДетальныеЗаписи.ОрганизацияGLN);
	INVOICEPARTNER =  СокрЛП(ВыборкаДетальныеЗаписи.ОрганизацияGLN);
	SENDER         =  СокрЛП(ВыборкаДетальныеЗаписи.ОрганизацияGLN);
	RECIPIENT      =  СокрЛП(ВыборкаДетальныеЗаписи.КонтрагентGLN);
	ПоставщикОКПО  =  СокрЛП(ВыборкаДетальныеЗаписи.ПоставщикОКПО);
	Идентификатор  = ?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ИдентификаторGLN),ВыборкаДетальныеЗаписи.ИдентификаторGLN,Неопределено);      //Идентификатор сети
	Запрос.УстановитьПараметр("Контрагент",ВыборкаДетальныеЗаписи.Контрагент);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	НоменклатураПоставщиков.Номенклатура КАК Номенклатура,
	|	НоменклатураПоставщиков.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	МАКСИМУМ(НоменклатураПоставщиков.Артикул) КАК Артикул
	|ПОМЕСТИТЬ вт_ТоварыПоставщика
	|ИЗ
	|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|ГДЕ
	|	НоменклатураПоставщиков.Владелец = &Контрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураПоставщиков.Номенклатура,
	|	НоменклатураПоставщиков.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.СкладКомпании КАК СкладКомпании,
	|	ЗаказПоставщикуТовары.СкладКомпании.Global_ID КАК СкладGlobal_ID,
	|	ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадкаПолучатель,
	|	ВЫБОР
	|		КОГДА НЕ ЗаказПоставщикуТовары.СкладКомпании.ТочкаДоставки = ЗНАЧЕНИЕ(Справочник.ТочкиДоставки.ПустаяСсылка)
	|				И НЕ ЗаказПоставщикуТовары.СкладКомпании.ТочкаДоставки.GLN = """"
	|			ТОГДА ЗаказПоставщикуТовары.СкладКомпании.ТочкаДоставки.GLN
	|		ИНАЧЕ ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка.GLN
	|	КОНЕЦ КАК АдресДоставкиGLN,
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	ЗаказПоставщикуТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнойШтрихКод КАК ОсновнойШтрихКод,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.ORDERUNIT КАК ORDERUNIT,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Номенклатура.КоэффициентВыгрузки <> 0
	|			ТОГДА 1
	|		ИНАЧЕ ЗаказПоставщикуТовары.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Номенклатура.КоэффициентВыгрузки <> 0
	|			ТОГДА ЗаказПоставщикуТовары.Количество / ЗаказПоставщикуТовары.Номенклатура.КоэффициентВыгрузки
	|		ИНАЧЕ ЗаказПоставщикуТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫРАЗИТЬ(ЗаказПоставщикуТовары.Цена КАК ЧИСЛО(7, 3)) КАК ЦенаСНДС,
	|	ВЫРАЗИТЬ(ЗаказПоставщикуТовары.Цена / (1 + ЗаказПоставщикуТовары.Номенклатура.СтавкаНДС.Ставка / 100) КАК ЧИСЛО(7, 3)) КАК ЦенаБезНДС,
	|	ЗаказПоставщикуТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПоставщикуТовары.Номенклатура.СтавкаНДС.Ставка КАК СтавкаНДСЧислом,
	|	ЗаказПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	|	ЗаказПоставщикуТовары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Номенклатура.ТипНоменклатуры.Весовой
	|				ИЛИ ЗаказПоставщикуТовары.Номенклатура.ТипНоменклатуры.ШтучноВесовой
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Весовой,
	|	ЕСТЬNULL(вт_ТоварыПоставщика.Артикул, ""0"") КАК PRODUCTIDSUPPLIER
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_ТоварыПоставщика КАК вт_ТоварыПоставщика
	|		ПО ЗаказПоставщикуТовары.Номенклатура = вт_ТоварыПоставщика.Номенклатура
	|			И ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры = вт_ТоварыПоставщика.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка = &Ссылка
	|	И &ТорговаяПлощадка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкладКомпании,
	|	ЗаказПоставщикуТовары.НомерСтроки";
	
	Если ТорговаяПлощадка = Неопределено Тогда
		СтрокаЗамены = "";
	Иначе
		СтрокаЗамены = "И ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = &ТорговаяПлощадка";
	КонецЕсли;
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &ТорговаяПлощадка",СтрокаЗамены);
	Запрос.Текст =  ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	нпп = 0;
	Пока ВыборкаДетальныеЗаписи.СледующийПоЗначениюПоля("СкладКомпании") Цикл
		нпп = нпп + 1;
		Если ВыборкаДетальныеЗаписи.СкладGlobal_ID = 0 Тогда
			тИД = Формат(нпп,"ЧЦ=5; ЧГ=");
			тNUMBER = NUMBER+"_"+ тИД;
		Иначе
			тИД =  Формат(ВыборкаДетальныеЗаписи.СкладGlobal_ID,"ЧЦ=5; ЧГ=");
			тNUMBER = NUMBER+"_"+ тИД;
		КонецЕсли;
		
		
		СтруктураДокумента = Новый Структура;
		
		СтруктураДокумента.Вставить("NUMBER",тNUMBER);
		СтруктураДокумента.Вставить("ТорговаяПлощадкаПолучатель",ВыборкаДетальныеЗаписи.ТорговаяПлощадкаПолучатель);
		СтруктураДокумента.Вставить("ПоставщикОКПО",ПоставщикОКПО);
		СтруктураДокумента.Вставить("DATE",DATE);
		СтруктураДокумента.Вставить("DELIVERYDATE",DELIVERYDATE);
		СтруктураДокумента.Вставить("DELIVERYTIME", DELIVERYTIME);
		СтруктураДокумента.Вставить("CAMPAIGNNUMBER", CAMPAIGNNUMBER); 
		СтруктураДокумента.Вставить("CURRENCY",CURRENCY);
		СтруктураДокумента.Вставить("VAT",Формат(ВыборкаДетальныеЗаписи.СтавкаНДСЧислом,"ЧЦ=10; ЧН=0; ЧГ="));
		
		СтруктураДокумента.Вставить("INFO",INFO); 
		
		СтруктураДокумента.Вставить("SUPPLIER", SUPPLIER);
		СтруктураДокумента.Вставить("BUYER",BUYER);
		СтруктураДокумента.Вставить("Идентификатор",Идентификатор);
		
		СтруктураДокумента.Вставить("DELIVERYPLACE",СокрЛП(ВыборкаДетальныеЗаписи.АдресДоставкиGLN));
		СтруктураДокумента.Вставить("INVOICEPARTNER",INVOICEPARTNER);
		СтруктураДокумента.Вставить("SENDER",SENDER);
		СтруктураДокумента.Вставить("RECIPIENT",RECIPIENT);
		СтруктураДокумента.Вставить("EDIMESSAGE",Формат(ТекущаяДата(), "ДФ=""ггггММддЧЧммсс000""") + "-OUT-" + NUMBER+"-"+тИД);
		
		
		//Табличная часть
		POSITION = Новый ТаблицаЗначений;
		POSITION.Колонки.Добавить("POSITIONNUMBER");
		POSITION.Колонки.Добавить("PRODUCT");
		POSITION.Колонки.Добавить("PRODUCTIDBUYER");
		POSITION.Колонки.Добавить("PRODUCTIDSUPPLIER");
		POSITION.Колонки.Добавить("ORDEREDQUANTITY");
		POSITION.Колонки.Добавить("QUANTITYOFCUINTU");
		POSITION.Колонки.Добавить("ORDERUNIT");
		POSITION.Колонки.Добавить("ORDERPRICE");
		POSITION.Колонки.Добавить("PRICEWITHVAT");
		POSITION.Колонки.Добавить("VAT");
		POSITION.Колонки.Добавить("DESCRIPTION");
		
		лНомерПП = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			лНомерПП = лНомерПП + 1;
			НовСтрока=POSITION.Добавить();
			НовСтрока.POSITIONNUMBER = лНомерПП;
			Если ПустаяСтрока(ВыборкаДетальныеЗаписи.ОсновнойШтрихКод) Тогда
				НовСтрока.PRODUCT =  РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьШтрихКодАртикула(ВыборкаДетальныеЗаписи.НоменклатураАртикул);
			Иначе
				НовСтрока.PRODUCT = СокрЛП(ВыборкаДетальныеЗаписи.ОсновнойШтрихКод);
			КонецЕсли;
			НовСтрока.PRODUCTIDBUYER    = СокрЛП(ВыборкаДетальныеЗаписи.НоменклатураАртикул);
			НовСтрока.PRODUCTIDSUPPLIER = СокрЛП(ВыборкаДетальныеЗаписи.PRODUCTIDSUPPLIER);
			НовСтрока.ORDEREDQUANTITY   = ?(ВыборкаДетальныеЗаписи.Весовой,ВыборкаДетальныеЗаписи.Количество, Окр(ВыборкаДетальныеЗаписи.Количество, 0 ,РежимОкругления.Окр15как20));
			НовСтрока.QUANTITYOFCUINTU  = ВыборкаДетальныеЗаписи.Коэффициент;
			НовСтрока.DESCRIPTION		 = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(СокрЛП(Лев(ВыборкаДетальныеЗаписи.НоменклатураНаименование,70)));
			НовСтрока.ORDERUNIT         = ?(ПустаяСтрока(ВыборкаДетальныеЗаписи.ORDERUNIT),"PCE",СокрЛП(ВыборкаДетальныеЗаписи.ORDERUNIT));
			НовСтрока.ORDERPRICE        = ВыборкаДетальныеЗаписи.ЦенаБезНДС;
			НовСтрока.PRICEWITHVAT      = ВыборкаДетальныеЗаписи.ЦенаСНДС;
			НовСтрока.VAT 				 = ВыборкаДетальныеЗаписи.СтавкаНДСЧислом;
			
		КонецЦикла;
		СтруктураДокумента.Вставить("POSITION",POSITION);
		
		МассивДокументов.Добавить(СтруктураДокумента);
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

Процедура СоздатьЗаказПоставщикуНаОснованииDESADV(СообщениеОбъектDESADV,СтруктураДокумента)Экспорт
	
	Если ЗначениеЗаполнено(СообщениеОбъектDESADV.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДокОбъект = Документы.ЗаказПоставщику.СоздатьДокумент();
	ДокОбъект.ХозОперация = Справочники.ХозОперации.ЗаказПоставщику;
	ДокОбъект.Автор 				  = Пользователи.ТекущийПользователь();
	ДокОбъект.ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ДокОбъект.КурсДокумента         = 1;
	ДокОбъект.Дата = ТекущаяДатаСеанса();
	ДокОбъект.РегламентированныйУчет = Истина;
	ДокОбъект.СрокПоставки           = ДокОбъект.Дата;//СтруктураДокумента.ДатаПоставки;
	ДокОбъект.ПодразделениеКомпании = СтруктураДокумента.ПодразделениеКомпании;
	ДокОбъект.ТорговаяПлощадка      = СтруктураДокумента.ТорговаяПлощадка;
	ДокОбъект.СкладКомпании         = СтруктураДокумента.СкладКомпании;
	ДокОбъект.СтатусЗаказаВеб       = Перечисления.СтатусыЗаказовВеб.НеПодтвержден;
	стрСклад = ДокОбъект.СкладыКомпании.Добавить();
	стрСклад.СкладКомпании = СтруктураДокумента.СкладКомпании;
	стрСклад.НаименованиеСклада =  СокрЛП(СтруктураДокумента.СкладКомпании.Наименование);		
	стрСклад.ИмяПоля = "скл" + СтрЗаменить(НРег(Строка(СтруктураДокумента.СкладКомпании.УникальныйИдентификатор())), "-", "");
	
	ДокОбъект.Организация = Справочники.ТорговыеПлощадки.ОрганизацияТорговойПлощадки(СтруктураДокумента.ТорговаяПлощадка,ДокОбъект.Дата);
	ДокОбъект.Контрагент	= СтруктураДокумента.Контрагент;
	ВидыДоговоров = Новый Массив;
	ВидыДоговоров.Добавить(Справочники.ВидыДоговоров.Поставка);
	ДокОбъект.ДоговорВзаиморасчетов  = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(ДокОбъект.Контрагент,ДокОбъект.Организация,ДокОбъект.ПодразделениеКомпании,ВидыДоговоров,,ТекущаяДатаСеанса());
	
	Для Каждого СтрТабТовары Из  СтруктураДокумента.ДокументТЧ Цикл 
		Если СтрТабТовары.КоличествоБазовое = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДокОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТабТовары);
		НоваяСтрока.СкладКомпании = ДокОбъект.СкладКомпании;
		
	КонецЦикла;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);		
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ДокОбъект, "СогласованиеЦен"));
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);		
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ДокОбъект.Товары,СтруктураДействий,Неопределено);
	
	Если ДокОбъект.ПроверитьЗаполнение() Тогда
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный);
		СообщениеОбъектDESADV.ДокументОснование = ДокОбъект.Ссылка;
	Иначе
		ВызватьИсключение "Ошибка проверки документа заказ поставщику ну основании документа EDI";
	КонецЕсли;
	
	
КонецПроцедуры



#КонецОбласти	



#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Процедура СформироватьОтгрузку(Заказ, ВидОтгрузки) Экспорт 
	
	Если ВидОтгрузки = "ПеремещениеВФилал" Тогда 
		СформироватьПеремещениеВФилиал(Заказ);
	Иначе
		ВызватьИсключение "Неизвестный вид отгрузки " + ВидОтгрузки;
	КонецЕсли;			
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//	Заказ поставщику
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	КомандаПечати.Идентификатор = "ЗаказПоставщику";
	КомандаПечати.Представление = НСтр("ru = 'Заказ поставщику';uk='Замовлення постачальникові'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Заказ поставщику сводный Склады в колонках
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	КомандаПечати.Идентификатор = "ЗаказПоставщикуСводныйСкладыВКолонках";
	КомандаПечати.Представление = НСтр("ru = 'Сводный, склады в колонках';uk='Зведений, склади у колонках'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	КомандаПечати.СписокФорм = "ФормаДокумента";
	
	// Заказ поставщику сводный Товар в колонках
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	КомандаПечати.Идентификатор = "ЗаказПоставщикуСводныйТоварВКолонках";
	КомандаПечати.Представление = НСтр("ru = 'Сводный, товар в колонках';uk='Зведений, товар у колонках'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	КомандаПечати.СписокФорм = "ФормаДокумента";
	
	//	Заказ Тедису
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	КомандаПечати.Идентификатор = "ЗаказПоставщикуНаБланкеТедиса";
	КомандаПечати.Представление = НСтр("ru = 'На бланке Тедиса';uk='На бланку Тедіса'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//	Заказ поставщику
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПоставщику") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПоставщику",
		НСтр("ru = 'Заказ поставщику';uk='Замовлення постачальникові'"),
		ПечатьЗаказПоставщику(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщику");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПоставщикуСводныйСкладыВКолонках") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПоставщикуСводныйСкладыВКолонках",
		НСтр("ru = 'Сводный, склады в колонках';uk='Зведений, склади у колонках'"),
		ПечатьЗаказПоставщикуСводный(МассивОбъектов, ОбъектыПечати, "СкладыВКолонках"),
		,
		"Документ.ЗаказПоставщику.ПФ_СКД_ЗаказПоставщику");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПоставщикуСводныйТоварВКолонках") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПоставщикуСводныйТоварВКолонках",
		НСтр("ru = 'Сводный, товар в колонках';uk='Зведений, товар у колонках'"),
		ПечатьЗаказПоставщикуСводный(МассивОбъектов, ОбъектыПечати, "ТоварВКолонках"),
		,
		"Документ.ЗаказПоставщику.ПФ_СКД_ЗаказПоставщику");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПоставщикуНаБланкеТедиса") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПоставщикуНаБланкеТедиса",
		НСтр("ru = 'На бланке Тедиса';uk='На бланку Тедіса'"),
		ПечатьЗаказПоставщикуНаБланкеТедиса(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщикуТедис");
	КонецЕсли;	
	
КонецПроцедуры

//	Заказ поставщику
Функция ПечатьЗаказПоставщику(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка КАК Ссылка,
	|	ЗаказПоставщику.Номер КАК Номер,
	|	ЗаказПоставщику.Дата КАК Дата,
	|	ЗаказПоставщику.Проведен КАК Проведен,
	|	ЗаказПоставщику.Автор КАК Автор,
	|	ЗаказПоставщику.ХозОперация КАК ХозОперация,
	|	ЗаказПоставщику.ДоговорВзаиморасчетов.Организация КАК Организация,
	|	ЗаказПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ЗаказПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПоставщику.Контрагент КАК Контрагент,
	|	ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ЗаказПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПоставщику.Комментарий КАК Комментарий,
	|	ЗаказПоставщику.СрокПоставки КАК СрокПоставки,
	|	ЗаказПоставщику.СкладКомпании КАК СкладКомпании,
	|	ЗаказПоставщику.Товары.(
	|		Ссылка КАК Ссылка,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		СУММА(Количество) КАК Количество,
	|		СУММА(КоличествоБазовое) КАК КоличествоБазовое,
	|		Номенклатура.ОсновнойШтрихКод КАК ОсновнойШтрихКод
	|	) КАК Товары
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщику.Товары.(Ссылка,
	|	Номенклатура.Артикул,
	|	Номенклатура,
	|	Номенклатура.НаименованиеПолное,
	|	ХарактеристикаНоменклатуры,
	|	ЕдиницаИзмерения,
	|	Номенклатура.ОсновнойШтрихКод)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ЗаказПоставщику";
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщику");
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьГрузополучатель 	= Макет.ПолучитьОбласть("Грузополучатель");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заказ поставщику №%1 от %2'; uk = 'Замовлення постачальникові №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		Организация = ?(ВыборкаДокументов.ХозОперация = Справочники.ХозОперации.ЗаказПоставщикуТранзиты,ВыборкаДокументов.ДоговорВзаиморасчетов.Организация,ВыборкаДокументов.Организация);
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		СтруктураШапки.Вставить("ПредставлениеПолучателя", Справочники.Организации.ПолучитьПредставлениеДляПечати(Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
		//СтруктураШапки.Вставить("АдресОтправителя", "");						
		
		Если ЗначениеЗаполнено(ВыборкаДокументов.СрокПоставки) Тогда 
			СтруктураШапки.Вставить("СрокПоставки", Формат(ВыборкаДокументов.СрокПоставки, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		//СтруктураШапки.Вставить("АдресПоставщика", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.Организация, "ЮридическийАдресОрганизации",, ТекущаяДата()));		
		
		//Если ЗначениеЗаполнено(ВыборкаДокументов.ПредставлениеПолучателя) Тогда
		//	СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ПредставлениеПолучателя);
		//ИначеЕсли ЗначениеЗаполнено(ВыборкаДокументов.ТорговаяПлощадкаПолучатель) Тогда 
		//	СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ТорговаяПлощадкаПолучатель);	
		//Иначе
		//	СтруктураШапки.Вставить("ПредставлениеПолучателя", "");						
		//КонецЕсли;
		
		//Если ЗначениеЗаполнено(ВыборкаДокументов.АдресОтправителя) Тогда 
		//	СтруктураШапки.Вставить("АдресОтправителя", ВыборкаДокументов.АдресОтправителя);
		//ИначеЕсли ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда 
		//	СтруктураШапки.Вставить("АдресОтправителя", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадка,"ЮридическийАдресТорговойПлощадки",,ТекущаяДата()));
		//КонецЕсли;				
		//
		//Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда
		//	СтруктураШапки.Вставить("АдресПолучателя", ВыборкаДокументов.ТочкаДоставки);
		//ИначеЕсли ЭтоПеремещениеВФилиал Тогда
		//	СтруктураШапки.Вставить("АдресПолучателя",УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадкаПолучатель,"ЮридическийАдресТорговойПлощадки",,ТекущаяДата()));
		//КонецЕсли;
		
		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		СуммаВсего 		= 0;
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Артикул, ОсновнойШтрихКод, ЕдиницаИзмерения, Количество");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			//КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
			//									ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
			//										ВыборкаТовары.Номенклатура,
			//										ВыборкаТовары.КоличествоБазовое,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
			//										ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
			//										ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
			//									СтруктураИтоговМест);
			//
			//СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
			СтруктураСтроки.Вставить("КоличествоМестПредставление", );
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			СуммаВсего = СуммаВсего + ВыборкаТовары.КоличествоБазовое;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		//СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Заказ поставщику сводный
Функция ПечатьЗаказПоставщикуСводный(МассивОбъектов, ОбъектыПечати, Вариант)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка КАК Ссылка,
	|	ЗаказПоставщику.Номер КАК Номер,
	|	ЗаказПоставщику.Дата КАК ДатаЗаказа,
	|	ЗаказПоставщику.СрокПоставки КАК СрокПоставки,
	|	ЗаказПоставщику.Организация КАК Организация,
	|	ЗаказПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПоставщику.Контрагент КАК Контрагент,
	|	ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ЗаказПоставщику.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить();
	ШапкаДок = ВыборкаДокументов.Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТоварыЗаказПоставщику",МассивОбъектов[0].Товары);
	ВнешниеНаборыДанных.Вставить("ДокументЗаказПоставщику",ШапкаДок);
	СхемаКомпоновкиДанных = ПолучитьМакет("ПФ_СКД_ЗаказПоставщику");
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	//Настройки.ЗагрузитьНастройки(СхемаКомпоновкиДанных.ВариантыНастроек.Найти(Вариант).Настройки);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	//МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,Настройки);
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,СхемаКомпоновкиДанных.ВариантыНастроек.Найти(Вариант).Настройки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,ВнешниеНаборыДанных);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	//Заполним параметры табличного документа
	ДокументРезультат.ОтображатьСетку = Ложь;
	ДокументРезультат.ОтображатьЗаголовки  = Ложь;
	ДокументРезультат.ПолеСнизу  = 0;
	ДокументРезультат.ТолькоПросмотр = Истина;
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ЗаказПоставщикуСводный";
	
	//ТабличныйДокумент.ПолеСверху = 0;
	//ТабличныйДокумент.ПолеСлева  = 0;
	//ТабличныйДокумент.ПолеСнизу  = 0;
	//ТабличныйДокумент.ПолеСправа = 0;
	//ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	
	ПервыйДокумент = Истина;
	//Пока ВыборкаДокументов.Следующий() Цикл
	
	
	
	Если Не ПервыйДокумент Тогда
		// Все документы нужно выводить на разных страницах.
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	ПервыйДокумент = Ложь;
	
	// Запомним номер строки, с которой начали выводить текущий документ.
	НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;				
	
	
	
	// В табличном документе необходимо задать имя области, в которую был 
	// выведен объект. Нужно для возможности печати комплектов документов.
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, 
	НомерСтрокиНачало, ОбъектыПечати, МассивОбъектов[0]);
	//КонецЦикла;
	
	Возврат ДокументРезультат;
КонецФункции

//	Заказ Тедису
Функция ПечатьЗаказПоставщикуНаБланкеТедиса(МассивОбъектов, ОбъектыПечати)
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ЗаказПоставщику";
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщикуТедис");
	
	ОбластьШапкаТовар	= Макет.ПолучитьОбласть("Шапка|Товар");	
	ОбластьШапкаСклад	= Макет.ПолучитьОбласть("Шапка|Склад");	
	ОбластьСтрокаТовар	= Макет.ПолучитьОбласть("Строка|Товар");
	ОбластьСтрокаСклад	= Макет.ПолучитьОбласть("Строка|Склад");
	
	ПервыйДокумент = Истина;
	
	
	Для Каждого Объект Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Номенклатура.Артикул КАК Артикул,
		|	ТедисНоменклатура.Значение КАК ТедисНоменклатура,
		|	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод,
		|	ЗаказПоставщикуТовары.СкладКомпании КАК СкладКомпании,
		|	ЗаказПоставщикуТовары.СкладКомпании.Код КАК КодСклада,
		|	ТедисСклад.Значение КАК ТедисСклад,
		|	СРЕДНЕЕ(ЗаказПоставщикуТовары.КоличествоБазовое / ВЫБОР
		|			КОГДА ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент = 0
		|				ТОГДА 1
		|			ИНАЧЕ ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
		|		КОНЕЦ) КАК КолВо,
		|	ВЫБОР
		|		КОГДА ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент = 0
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
		|	КОНЕЦ КАК УпаковкиКоэффициент
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО ЗаказПоставщикуТовары.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
		|			И ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки = ШтрихкодыНоменклатуры.Упаковка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ТедисНоменклатура
		|		ПО ЗаказПоставщикуТовары.Номенклатура = ТедисНоменклатура.Объект
		|			И (ТедисНоменклатура.Свойство = &СвойствоНоменклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ТедисСклад
		|		ПО ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = ТедисСклад.Объект
		|			И (ТедисСклад.Свойство = &СвойствоПлощадка)
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&МассивСсылок)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТедисСклад.Значение,
		|	ТедисНоменклатура.Значение,
		|	ЗаказПоставщикуТовары.Ссылка,
		|	ЗаказПоставщикуТовары.СкладКомпании,
		|	ЗаказПоставщикуТовары.Номенклатура,
		|	ВЫБОР
		|		КОГДА ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент = 0
		|			ТОГДА 1
		|		ИНАЧЕ ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
		|	КОНЕЦ,
		|	ЗаказПоставщикуТовары.Номенклатура.Артикул,
		|	ЗаказПоставщикуТовары.СкладКомпании.Код";
		
		Запрос.УстановитьПараметр("МассивСсылок", Объект);
		Запрос.УстановитьПараметр("СвойствоНоменклатура", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","КодТедис"));
		Запрос.УстановитьПараметр("СвойствоПлощадка", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","КодТорговойПлощадкиТедис"));
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		тзСклады = Результат.Скопировать(,"СкладКомпании,КодСклада,ТедисСклад");
		тзСклады.Свернуть("СкладКомпании,КодСклада,ТедисСклад"); 
		тзСклады.Сортировать("ТедисСклад"); 
		
		тзТовары = Результат.Скопировать(,"Номенклатура,Артикул,ТедисНоменклатура,Штрихкод,УпаковкиКоэффициент");
		тзТовары.Свернуть("Номенклатура,Артикул,ТедисНоменклатура,Штрихкод,УпаковкиКоэффициент"); 
		тзТовары.Сортировать("Номенклатура"); 
		
		тзУпаковки = тзТовары.Скопировать(,"УпаковкиКоэффициент");
		тзУпаковки.Свернуть("УпаковкиКоэффициент"); 
		
		СоответствиеБлоков = Новый Соответствие;
		Для Каждого Блок ИЗ тзУпаковки Цикл
			УпаковкиКоэффициент = Блок.УпаковкиКоэффициент; 
			СоответствиеБлоков.Вставить(УпаковкиКоэффициент,"Блок" + ?(УпаковкиКоэффициент = 10,"",УпаковкиКоэффициент));
		КонецЦикла;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТовар);
		Для Каждого Склад ИЗ тзСклады Цикл
			ОбластьШапкаСклад.Параметры.Заполнить(Склад);
			ТабличныйДокумент.Присоединить(ОбластьШапкаСклад);
		КонецЦикла;
		
		Для Каждого Товар ИЗ тзТовары Цикл
			ОбластьСтрокаТовар.Параметры.Заполнить(Товар);
			ОбластьСтрокаТовар.Параметры.Блок = СоответствиеБлоков.Получить(Товар.УпаковкиКоэффициент);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТовар);
			Для Каждого Склад ИЗ тзСклады Цикл
				мДанных = Результат.НайтиСтроки(Новый Структура("Номенклатура,СкладКомпании",Товар.Номенклатура,Склад.СкладКомпании));
				Количество = ?(мДанных.Количество() = 0,0,мДанных[0].КолВо);
				ОбластьСтрокаСклад.Параметры.КолВо = Количество;
				ТабличныйДокумент.Присоединить(ОбластьСтрокаСклад);
			КонецЦикла;
		КонецЦикла;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Объект);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецОбласти	


#Область СлужебныеПроцедурыИФункции

Процедура СформироватьПеремещениеВФилиал(Заказ, тДата = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не ЗначениеЗаполнено(тДата) Тогда 
		тДата=НачалоДня(ТекущаяДата())+75600;
	КонецЕсли;
	
	Зап=Новый Запрос;
	Зап.Текст="ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Номенклатура КАК Товар,
	|	ЗаказПоставщикуТовары.Количество КАК Кво,
	|	ЗаказПоставщикуТовары.СкладКомпании КАК Склад,
	|	ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадкаСклада,
	|	ЗаказПоставщикуТовары.СкладКомпании.Подразделение КАК ПодразделениеСклада,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения КАК Ед,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения.Коэффициент КАК Коф,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Номенклатура.ГруппаЦенообразования = ЗНАЧЕНИЕ(Справочник.ГруппыЦенообразования.МобильноеПополнение)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ГЦ,
	|	ЗаказПоставщикуТовары.Ссылка.СкладКомпании КАК СкладРезервирования,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
	|				И НЕ ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли ЕСТЬ NULL
	|			ТОГДА ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
	|		ИНАЧЕ ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенСебестоимости
	|	КОНЕЦ КАК СкладРезервированияТипЦен,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
	|				И НЕ ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли ЕСТЬ NULL
	|			ТОГДА ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли.ВалютаЦены
	|		ИНАЧЕ ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТипЦенСебестоимости.ВалютаЦены
	|	КОНЕЦ КАК СкладРезервированияВалюта,
	|	ЗаказПоставщикуТовары.Ссылка.СкладКомпании.ТорговаяПлощадка КАК СкладРезервированияТорговаяПлощадка,
	|	ЕСТЬNULL(ТК_УчетХарактеристикНаСкладахСрезПоследних.НеВестиУчетПоХарактеристикам, ЛОЖЬ) КАК БезУчетаХарактеристик
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_УчетХарактеристикНаСкладах.СрезПоследних(&НаДату, ) КАК ТК_УчетХарактеристикНаСкладахСрезПоследних
	|		ПО ЗаказПоставщикуТовары.СкладКомпании = ТК_УчетХарактеристикНаСкладахСрезПоследних.СкладКомпании
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка КАК Ссылка,
	|	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПеремещениеТоваров.СкладКомпанииПолучатель КАК СкладКомпанииПолучатель,
	|	ПеремещениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПеремещениеТоваров.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
	|	И ПеремещениеТоваров.ДокументОснование = &Ссылка
	|	И НЕ ПеремещениеТоваров.ПометкаУдаления
	|	И ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)";
	
	Зап.УстановитьПараметр("Ссылка", Заказ);
	Зап.УстановитьПараметр("НаДату", тДата);
	МассивРезультатов = Зап.ВыполнитьПакет();
	
	Рез = МассивРезультатов[0];
	ТаблицаПеремещений = МассивРезультатов[1].Выгрузить();
	
	Если ТаблицаПеремещений.Количество() > 1 Тогда 
		ТаблицаПеремещений.Индексы.Добавить("СкладКомпанииПолучатель");
	КонецЕсли;
	
	Если Рез.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Рез.Выбрать();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	
	Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл 
		
		ТекСклад = Выборка.Склад;
		
		Если ТаблицаПеремещений.Количество() > 0 Тогда 
			нПеремещения = ТаблицаПеремещений.НайтиСтроки(Новый Структура("СкладКомпанииПолучатель", Выборка.Склад));
			Если нПеремещения.Количество() > 0 Тогда 
				ПДок = нПеремещения[0].Ссылка.ПолучитьОбъект();
				ПДок.Товары.Очистить();
				ПДок.РаспределениеОтгрузки.Очистить();
			Иначе
				ПДок=Документы.ПеремещениеТоваров.СоздатьДокумент();
			КонецЕсли;
		Иначе
			ПДок=Документы.ПеремещениеТоваров.СоздатьДокумент();	
		КонецЕсли;		
		
		ПДок.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
		ПДок.ПодразделениеКомпании = Выборка.ПодразделениеСклада;
		ПДок.Организация = Справочники.СкладыКомпании.ОрганизацияСклада(Выборка.Склад);
		ПДок.Автор = Пользователи.ТекущийПользователь();
		ПДок.ТипЦен = Выборка.СкладРезервированияТипЦен;
		ПДок.ВалютаДокумента = Выборка.СкладРезервированияВалюта;
		ПДок.КурсДокумента = 1;
		ПДок.РегламентированныйУчет = Истина;
		ПДок.Дата=ТекущаяДатаСеанса();
		ПДок.УстановитьНовыйНомер();
		
		ПДок.СкладКомпании   = Выборка.СкладРезервирования;
		ПДок.ТорговаяПлощадка = Выборка.СкладРезервированияТорговаяПлощадка;
		
		ПДок.СкладКомпанииПолучатель = Выборка.Склад;
		ПДок.ТорговаяПлощадкаПолучатель = Выборка.ТорговаяПлощадкаСклада;
		
		ПДок.ДокументОснование = Заказ;
		ПДок.ЗакрытиеЗаказовПокупателей = 2;
		нРО = ПДок.РаспределениеОтгрузки.Добавить();
		нРО.ЗаказПокупателя  = Заказ;
		
		Если Не СтруктураДействий.Свойство("ЗаполнитьЦены") Тогда 
			СтруктураПолейДляЗаполненияЦен = Новый Структура;
			СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ПДок));
			СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		КонецЕсли;                    
		
		Пока Выборка.Следующий() Цикл 			
			тсс=ПДок.Товары.Добавить();
			тсс.Номенклатура =Выборка.Товар;
			тсс.Количество =Выборка.Кво;
			тсс.ЕдиницаИзмерения = Выборка.Ед;
			тсс.Коэффициент = Выборка.Коф;							
		КонецЦикла;
		
		Если Не Выборка.БезУчетаХарактеристик Тогда 
			Документы.ПеремещениеТоваров.ЗаполнитьХарактеристикиПоОстаткамПартий(ПДок);
		КонецЕсли;
		
		СтруктураПолейДляЗаполненияЦен = Новый Структура;		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ПДок));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		СтруктураДействий.Вставить("ЦеныБезХарактеристики", Выборка.БезУчетаХарактеристик);			
		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ПДок.Товары, СтруктураДействий, Неопределено);
		
		ПДок.СуммаДокумента = ПДок.Товары.Итог("СуммаВсего");
		
		Попытка
			Если ПДок.ПроверитьЗаполнение() Тогда 
				ПДок.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ПДок.Записать(РежимЗаписиДокумента.Запись);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документ %1 по складу %2 не удалось проверсти. Документ записан'; uk = 'Документ %1 по складу %2 не вдалося проверсти. Документ записаний'"),
				Строка(ПДок),
				ТекСклад),
				ПДок.Ссылка);						
				
			КонецЕсли;
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать документ Перемещение в филиал по складу %1
			|Ошибки: %2'; uk = 'Не вдалося записати документ Переміщення у філіал по складу %1
			|Помилки: %2'"),
			ТекСклад,
			ОписаниеОшибки()));
			Продолжить;
		КонецПопытки;
		
		тДата=тДата+1;
		
		//Приходуем
		Если Не ПДок.ЭтоНовый() Тогда 
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	СвязанныеДокументы.Ссылка КАК Ссылка
			|ИЗ
			|	КритерийОтбора.СвязанныеДокументы(&Ссылка) КАК СвязанныеДокументы
			|ГДЕ
			|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПеремещениеТоваров
			|	И ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПеремещениеТоваров).ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
			|	И НЕ ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПеремещениеТоваров).ПометкаУдаления";
			
			Запрос.УстановитьПараметр("Ссылка", ПДок.Ссылка);
			СвязанныеДокументы = Запрос.Выполнить().Выбрать();			
			Если СвязанныеДокументы.Следующий() Тогда 
				ДокПриход = СвязанныеДокументы.Ссылка.ПолучитьОбъект();
			Иначе
				ДокПриход = Документы.ПеремещениеТоваров.СоздатьДокумент();
			КонецЕсли;
		Иначе 
			ДокПриход = Документы.ПеремещениеТоваров.СоздатьДокумент();	
		КонецЕсли;		
		
		ДокПриход.Заполнить(ПДок.Ссылка);			
		ДокПриход.Дата = ТекущаяДатаСеанса();	
		ДокПриход.ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(ДокПриход.СкладКомпанииПолучатель);		
		//ЗаполнитьЗначенияСвойств(ДокПриход, ПДок, "ПодразделениеКомпании, Организация, ВалютаДокумента, СуммаДокумента");
		
		Если ДокПриход.ЭтоНовый() Тогда 
			ДокПриход.УстановитьНовыйНомер();
		КонецЕсли;
		
		//ДокПриход.ОбменДанными.Загрузка = Истина;
		Попытка
			ДокПриход.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать документ Перемещение из филиал по складу %1
			|Ошибки: %2'; uk = 'Не вдалося записати документ Переміщення із філіалу по складу %1
			|Помилки: %2'"),
			ТекСклад,
			ОписаниеОшибки()));
			
		КонецПопытки;
		
		тДата=тДата+1;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ВыгрузитьЗаказВДБФ(ЗаказСсылка)	Экспорт
	
	Если ЗаказСсылка.ПометкаУдаления Тогда
		Сообщить(""+ЗаказСсылка+" помечен на удаление!!!");
		Возврат;
	КонецЕсли;
	
	тВремя=ТекущаяДата();
	НужныйКаталог = "\\space.digma\office\shared\AttikaZakaz"+"\"+СокрЛП(ЗаказСсылка.Контрагент)+"\";
	СоздатьКаталог(НужныйКаталог);
	ВремФайл = КаталогВременныхФайлов()+"TM"+СтрЗаменить(Формат(тВремя,"ДЛФ=T"),":","")+".dbf";
	//ИмяФайла=СтрЗаменить(НужныйКаталог+Формат(тВремя,"ДФ=yyyy-MM-dd")+" "+Формат(тВремя,"ДЛФ=T")+" "+СокрЛП(ЗаказСсылка.Организация)+".dbf",":",".");
	ИмяФайла=НужныйКаталог+Лев(ЗаказСсылка.Организация,1)+Формат(тВремя,"ДФ=MMdd")+Прав(ЗаказСсылка.Номер,3);
	
	ДбфЗ=Новый XBase;
	ДбфЗ.поля.Добавить("KOD","N",6,0);
	ДбфЗ.поля.Добавить("NAME","S",14);
	ДбфЗ.поля.Добавить("KVO","N",6,0);
	ДбфЗ.поля.Добавить("EDIN","N",2,0);
	ДбфЗ.поля.Добавить("BASEDIN","N",4,0);
	ДбфЗ.поля.Добавить("KOEF","N",6,0);
	ДбфЗ.поля.Добавить("CENABNDS","N",15,5);
	ДбфЗ.поля.Добавить("SUMMABNDS","N",18,5);
	ДбфЗ.поля.Добавить("KIOSK","N",4,0);
	ДбфЗ.поля.Добавить("PRODUCT","N",13,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.СкладКомпании.Код КАК КодСклада,
	|	ЗаказПоставщикуТовары.Номенклатура.Артикул КАК Артикул,
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.Номенклатура.Наименование КАК Наименование,
	|	ЗаказПоставщикуТовары.Количество КАК Кво,
	|	ЗаказПоставщикуТовары.Коэффициент КАК Кф,
	|	ЗаказПоставщикуТовары.ЕдиницаИзмерения.Код КАК КодЕд,
	|	ЗаказПоставщикуТовары.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК БазЕдКод,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнойШтрихКод КАК ОсновнойШтрихКод,
	|	ЗаказПоставщикуТовары.Цена КАК Цена,
	|	ЗаказПоставщикуТовары.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодСклада,
	|	Наименование";
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("Нет данных для выгрузки, заказ пустой...");
	Иначе
		
		ДбфЗ.СоздатьФайл(ВремФайл);
		ДбфЗ.АвтоСохранение=Истина;
		доб=Ложь;
		
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			КодСклада =  СокрЛП(ВыборкаДетальныеЗаписи.КодСклада);
			КодСклада =  Прав(КодСклада,СтрДлина(КодСклада)-1);
			Попытка КодСклада = Число(КодСклада) Исключение КодСклада=0 КонецПопытки;
			
			ДбфЗ.Добавить();
			ДбфЗ.KOD=ВыборкаДетальныеЗаписи.Артикул;
			ДбфЗ.NAME=ВыборкаДетальныеЗаписи.Наименование;
			ДбфЗ.KVO=ВыборкаДетальныеЗаписи.Кво;
			ДбфЗ.EDIN=ВыборкаДетальныеЗаписи.КодЕд;
			ДбфЗ.BASEDIN=ВыборкаДетальныеЗаписи.БазЕдКод;
			ДбфЗ.KOEF=ВыборкаДетальныеЗаписи.Кф;
			ДбфЗ.CENABNDS=ВыборкаДетальныеЗаписи.Цена;
			ДбфЗ.SUMMABNDS=ВыборкаДетальныеЗаписи.Сумма;
			ДбфЗ.KIOSK=КодСклада;
			ДбфЗ.PRODUCT=ВыборкаДетальныеЗаписи.ОсновнойШтрихКод;
			
		КонецЦикла;
		ДбфЗ.ЗакрытьФайл();
		ПереместитьФайл(ВремФайл,ИмяФайла);
		Сообщить("Заказ выгружен в "+ИмяФайла);
	КонецЕсли;
КонецПроцедуры


Функция СоздатьСводныйЗаказ(ДанныеЗаполнения)	Экспорт
	
	Заказ								= ДанныеЗаполнения.Заказ;
	Склад								= ДанныеЗаполнения.Склад;
	Договор								= ДанныеЗаполнения.Договор;
	Товары								= ДанныеЗаполнения.Товары.Выгрузить(,"Номенклатура, КоличествоБазовое");
	СводныйЗаказ						= Документы.ЗаказПоставщику.СоздатьДокумент();
	СводныйЗаказ.Дата					= ТекущаяДата();
	СводныйЗаказ.Автор					= Пользователи.ТекущийПользователь();
	СводныйЗаказ.ВалютаДокумента		= ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	СводныйЗаказ.КурсДокумента			= 1;
	СводныйЗаказ.СтатусЗаказаВеб		= ПредопределенноеЗначение("Перечисление.СтатусыЗаказовВеб.НеОтправлен");
	СводныйЗаказ.ХозОперация			= ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПоставщику");
	//СводныйЗаказ.СкладКомпании		= Склад;
	СводныйЗаказ.РегламентированныйУчет	= Истина;
	СводныйЗаказ.ТорговаяПлощадка		= Склад.ТорговаяПлощадка;
	СводныйЗаказ.ДоговорВзаиморасчетов	= Договор;
	СводныйЗаказ.Организация			= Договор.Организация;
	СводныйЗаказ.ПодразделениеКомпании	= Договор.Подразделение;
	СводныйЗаказ.Контрагент				= Договор.Контрагент;
	СводныйЗаказ.СрокПоставки			= КонецДня(СводныйЗаказ.Дата)+86400;
	СводныйЗаказ.Комментарий			= "Сводный заказ к документу: "+ Заказ;
	
	стрСклады = СводныйЗаказ.СкладыКомпании.Добавить();
	
	УИД = НРег(Строка(Склад.УникальныйИдентификатор()));;
	УИД = СтрЗаменить(УИД, "-", "");		
	
	стрСклады.СкладКомпании		= Склад;
	стрСклады.НаименованиеСклада= СокрЛП(Склад.Наименование);
	стрСклады.ИмяПоля			= "скл" + УИД;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Товары.КоличествоБазовое КАК КоличествоБазовое
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&СкладКомпании КАК СкладКомпании,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.КоличествоНеобходимое КАК КоличествоНеобходимое,
	|	ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0) КАК Остаток,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) = 0
	|			ТОГДА 1
	|		ИНАЧЕ Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
	|	КОНЕЦ КАК КвантУпаковвки,
	|	(Товары.КоличествоНеобходимое - ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток, 0)) / Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК ЗаказУпаковка
	|ПОМЕСТИТЬ Заказать
	|ИЗ
	|	(ВЫБРАТЬ
	|		втТовары.Номенклатура КАК Номенклатура,
	|		СУММА(втТовары.КоличествоБазовое) КАК КоличествоНеобходимое
	|	ИЗ
	|		втТовары КАК втТовары
	|	
	|	СГРУППИРОВАТЬ ПО
	|		втТовары.Номенклатура) КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&НаДату, СкладКомпании = &СкладКомпании) КАК ОстаткиТоваровКомпанииОстатки
	|		ПО Товары.Номенклатура = ОстаткиТоваровКомпанииОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заказать.СкладКомпании КАК СкладКомпании,
	|	Заказать.Номенклатура КАК Номенклатура,
	|	Заказать.КоличествоНеобходимое КАК КоличествоНеобходимое,
	|	Заказать.Остаток КАК Остаток,
	|	Заказать.КвантУпаковвки КАК КвантУпаковвки,
	|	Заказать.ЗаказУпаковка КАК ЗаказУпаковка,
	|	Заказать.КвантУпаковвки * ВЫБОР
	|		КОГДА Заказать.ЗаказУпаковка <> 0
	|				И Заказать.ЗаказУпаковка = (ВЫРАЗИТЬ(Заказать.ЗаказУпаковка - 0.5 КАК ЧИСЛО(15, 0)))
	|			ТОГДА Заказать.ЗаказУпаковка
	|		ИНАЧЕ ВЫРАЗИТЬ(Заказать.ЗаказУпаковка + 0.5 КАК ЧИСЛО(15, 0))
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ Итог
	|ИЗ
	|	Заказать КАК Заказать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.СкладКомпании КАК СкладКомпании,
	|	Итог.Номенклатура КАК Номенклатура,
	|	Итог.КоличествоНеобходимое КАК КоличествоНеобходимое,
	|	Итог.Остаток КАК Остаток,
	|	Итог.КвантУпаковвки КАК КвантУпаковвки,
	|	Итог.ЗаказУпаковка КАК ЗаказУпаковка,
	|	Итог.Количество КАК Количество
	|ИЗ
	|	Итог КАК Итог
	|ГДЕ
	|	Итог.Количество > 0";
	
	Запрос.УстановитьПараметр("НаДату",ТекущаяДата());
	Запрос.УстановитьПараметр("Товары",Товары);
	Запрос.УстановитьПараметр("СкладКомпании",Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СводныйЗаказ.Товары.Загрузить(РезультатЗапроса.Выгрузить());
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);		
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу");
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(СводныйЗаказ, "СогласованиеЦен"));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);		
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(СводныйЗаказ.Товары, СтруктураДействий, Неопределено);
	
	СводныйЗаказ.СуммаДокумента = СводныйЗаказ.Товары.Итог("Сумма");
	
	Возврат СводныйЗаказ;
	
КонецФункции

#КонецОбласти


#КонецЕсли
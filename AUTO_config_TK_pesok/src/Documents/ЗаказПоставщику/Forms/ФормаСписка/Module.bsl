
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	БлокируемыеПоля = Новый Массив;
	БлокируемыеПоля.Добавить("Обработан");
	
	Список.УстановитьОграниченияИспользованияВОтборе(БлокируемыеПоля);
	Список.УстановитьОграниченияИспользованияВПорядке(БлокируемыеПоля);
	Список.УстановитьОграниченияИспользованияВГруппировке(БлокируемыеПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	//Заполним отбор списка
	ПараметрыОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
		ПараметрыОтбора.Вставить("ПериодВыборки", ПериодВыборки);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда 
		ПараметрыОтбора.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборПодразделениеКомпании) Тогда 
		ПараметрыОтбора.Вставить("ПодразделениеКомпании", ОтборПодразделениеКомпании);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборТорговаяПлощадка) Тогда 
		ПараметрыОтбора.Вставить("ТорговаяПлощадка", ОтборТорговаяПлощадка);
	КонецЕсли;	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда 
		ПараметрыОтбора.Вставить("Контрагент", ОтборКонтрагент);
	КонецЕсли;
	
	УстановитьОтборСписка(Список, ПараметрыОтбора);		
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ЗаказПоставщику КАК ЗаказПоставщику,
	|	СУММА(ВложенныйЗапрос.ОбработанОстаток) КАК ОбработанОстаток
	|ПОМЕСТИТЬ Обработанные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбработкаЗаказовПоставщикуОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
	|		ОбработкаЗаказовПоставщикуОстатки.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|		ОбработкаЗаказовПоставщикуОстатки.ОбработанОстаток КАК ОбработанОстаток
	|	ИЗ
	|		РегистрНакопления.ОбработкаЗаказовПоставщику.Остатки(, ЗаказПоставщику В (&мЗаказПоставщику)) КАК ОбработкаЗаказовПоставщикуОстатки) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.ОбработанОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ЗаказПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбработкаЗаказовПоставщику.ЗаказПоставщику КАК ЗаказПоставщику
	|ПОМЕСТИТЬ ЗП
	|ИЗ
	|	РегистрНакопления.ОбработкаЗаказовПоставщику КАК ОбработкаЗаказовПоставщику
	|ГДЕ
	|	ОбработкаЗаказовПоставщику.ЗаказПоставщику В(&мЗаказПоставщику)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗП.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА Обработанные.ОбработанОстаток ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Обработан,
	|	ЗП.ЗаказПоставщику.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.Внутренний) КАК ВидКонтрагента
	|ИЗ
	|	ЗП КАК ЗП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Обработанные КАК Обработанные
	|		ПО ЗП.ЗаказПоставщику = Обработанные.ЗаказПоставщику";
	
	Запрос.УстановитьПараметр("мЗаказПоставщику", Строки.ПолучитьКлючи());
	
	РезультатЗапроса = Запрос.Выполнить();	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Обработан = ВыборкаДетальныеЗаписи.Обработан;
		СтрокаСписка = Строки[ВыборкаДетальныеЗаписи.ЗаказПоставщику];
		СтрокаСписка.Данные.Обработан = Обработан;
		
		Если Не Обработан И ВыборкаДетальныеЗаписи.ВидКонтрагента Тогда
			Если СтрокаСписка.Оформление.Получить("Обработан") <> Неопределено Тогда
				СтрокаСписка.Оформление["Обработан"].УстановитьЗначениеПараметра("ЦветФона",WebЦвета.Розовый);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборТорговаяПлощадкаПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ТорговаяПлощадка", ОтборТорговаяПлощадка));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("Организация", ОтборОрганизация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеКомпанииПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ПодразделениеКомпании", ОтборПодразделениеКомпании));	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("Контрагент", ОтборКонтрагент));
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыборкиПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ПериодВыборки", ПериодВыборки));
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Элементы.СписокОбработатьДляРЦ.Доступность = Ложь;
	Иначе
		Элементы.СписокОбработатьДляРЦ.Доступность = ДоступностьКомандаОбработатьДляРЦ(ТекДанные.Ссылка);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ПериодВыборки = Неопределено;
	Если ПараметрыОтбора.Свойство("ПериодВыборки", ПериодВыборки) Тогда 
		
		ЭлементыОтбора = Список.Отбор.Элементы;
		
		ЭлементОтбораДанных = Неопределено;
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			Если ЭлементОтбора.Представление = "ОтборПериод" Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
			
			Если ЭлементОтбораДанных = Неопределено Тогда
				ГруппаОтборПериод = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтборПериод.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли; 
				ГруппаОтборПериод.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
				ГруппаОтборПериод.Использование = Истина;
				ГруппаОтборПериод.Представление = "ОтборПериод";
			Иначе
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Истина;
			КонецЕсли;	
			
			ГруппаДатаСортировки = ГруппаОтборПериод.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаДатаСортировки.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ; 
			ГруппаДатаСортировки.Использование = Истина;
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаНачала) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаНачала;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаОкончания;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;
			
			Если ГруппаДатаСортировки.Элементы.Количество() = 0 Тогда 
				ГруппаОтборПериод.Элементы.Удалить(ГруппаДатаСортировки);
			КонецЕсли;
			
		Иначе
			
			Если ЭлементОтбораДанных <> Неопределено Тогда
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Для Каждого текОтбор Из ПараметрыОтбора Цикл 		
		ИмяРеквизита = текОтбор.Ключ;
		Значение = текОтбор.Значение;
		
		Если текОтбор.Ключ = "ПериодВыборки" Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, 
		ИмяРеквизита, 
		Значение, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Значение));
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправитьПоставщику(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ОтправитьПоставщикуНаСервере(ВыделенныеСтроки);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьПоставщикуНаСервере(МассивНаВыгрузку)
	
	Для Каждого Стр Из МассивНаВыгрузку Цикл 
		Если РольДоступна("ТК_АдминистрированиеEDI") Тогда
			Документы.ЗаказПоставщику.ОтправитьПоEDI(Стр);
		Иначе
			Если Стр.СтатусЗаказаВеб = Перечисления.СтатусыЗаказовВеб.НеОтправлен тогда
				Документы.ЗаказПоставщику.ОтправитьПоEDI(Стр);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыгрузитьВДБФНаСервере(МассивНаВыгрузку)
	Для Каждого Стр Из МассивНаВыгрузку Цикл 
		Документы.ЗаказПоставщику.ВыгрузитьЗаказВДБФ(Стр);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВДБФ(Команда)
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ВыгрузитьВДБФНаСервере(ВыделенныеСтроки);
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура ОбработатьДляРЦНаСервере(Заказ, УникальныйИдентификаторФормы)
	Ошибка = "";
	СкладОтгрузки = Заказ.СкладКомпании;
	ЗаказПроведен = Заказ.Проведен;
	Обработан = УправлениеСвойствами.ЗначениеСвойства(Заказ,"ОбработанДляРЦ");
	СкладЗаполнен = ЗначениеЗаполнено(СкладОтгрузки);
	Если Не ЗаказПроведен Тогда
		Ошибка = Строка(Заказ) + Символы.ПС + "не проведен !!!";
	КонецЕсли;
	Если Обработан = Истина Тогда
		Ошибка2 = "уже обработан для РЦ !!!";
		Ошибка = ?(ПустаяСтрока(Ошибка),Строка(Заказ)+Символы.ПС+Ошибка2,Ошибка+Символы.ПС+Ошибка2);
	КонецЕсли;
	Если СкладЗаполнен Тогда
		КонсолидацияЗаказов = УправлениеСвойствами.ЗначениеСвойства(СкладОтгрузки,"КонсолидацияЗаказов");
		//Если КонсолидацияЗаказов = Истина Тогда
		//	Ошибка4 = "консолидация заказов пока не готова, находится в разработке !!!";
		//	Ошибка = ?(ПустаяСтрока(Ошибка),Строка(Заказ)+Символы.ПС+Ошибка4,Ошибка+Символы.ПС+Ошибка4);
		//КонецЕсли;
	Иначе
		Ошибка3 = "не заполнен склад отгрузки !!!";
		Ошибка = ?(ПустаяСтрока(Ошибка),Строка(Заказ)+Символы.ПС+Ошибка3,Ошибка+Символы.ПС+Ошибка3);
	КонецЕсли;
	Если Не ПустаяСтрока(Ошибка) Тогда
		ВызватьИсключение(Ошибка);
	КонецЕсли;
	
	ВсеОК = Истина;
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Заказ", Заказ);
	СтруктураПараметров.Вставить("Адрес", ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификаторФормы));
	//СтруктураПараметров.Вставить("ТекПользователь", Пользователи.ТекущийПользователь());
	//СтруктураПараметров.Вставить("СкладРезервирования", Заказ.СкладКомпании);
	//СтруктураПараметров.Вставить("Регламент", Заказ.РегламентированныйУчет);
	//СтруктураПараметров.Вставить("РезервироватьВОсновныхЕдиницах", Ложь);
	СтруктураПараметров.Вставить("КонсолидированныйЗаказ", КонсолидацияЗаказов = Истина);
	//СтруктураПараметров.Вставить("Период", Заказ.ДатаОтгрузкиРЦ);
	//СтруктураПараметров.Вставить("Организация", Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(Заказ.Контрагент));
	
	НачатьТранзакцию();
	Документы.ЗаказПокупателя.СформироватьЗаказыПокупателейИзЗаказаПоставщика(СтруктураПараметров, ВсеОК);
	Если ВсеОК Тогда
		//МассивСтруктур = Новый Массив;
		//УстановитьПривилегированныйРежим(Истина);
		//ДопСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ОбработанДляРЦ");
		//МассивСтруктур.Добавить(Новый Структура("Свойство, Значение", ДопСвойство,Истина));
		//УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Заказ, МассивСтруктур);
		//УстановитьПривилегированныйРежим(Ложь);
		ЗафиксироватьТранзакцию();
		ОбщегоНазначения.СообщитьПользователю("Готово!");
	Иначе
		ОтменитьТранзакцию();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДляРЦ(Команда)
	Заказ = Элементы.Список.ТекущаяСтрока;
	Если ЗначениеЗаполнено(Заказ) Тогда
		ОбработатьДляРЦНаСервере(Заказ, ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступностьКомандаОбработатьДляРЦ(Заказ)
	ДоступностьКоманды = ЗначениеЗаполнено(Заказ) И Заказ.Проведен И (ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(Заказ.Контрагент) ИЛИ Заказ.Контрагент.КодПоЕДРПОУ = "40448684.");
	Возврат ДоступностьКоманды;
КонецФункции;



#КонецОбласти


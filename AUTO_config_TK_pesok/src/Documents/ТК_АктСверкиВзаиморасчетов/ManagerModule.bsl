#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт сверки
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ТК_АктСверкиВзаиморасчетов";
    КомандаПечати.Идентификатор = "АктСверки";
    КомандаПечати.Представление = НСтр("ru = 'Акт сверки'; uk = 'Акт звіряння'");
	КомандаПечати.СразуНаПринтер = Ложь;
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ДоговорыВзаиморасчетов.ДоговорВзаиморасчетов, Null Как Истина)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом


Процедура ЗаполнитьПоДаннымУправленческогоУчета(Объект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Объект.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Объект.ДатаОкончания));
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", Объект.ДоговорыВзаиморасчетов.Выгрузить(, "ДоговорВзаиморасчетов").ВыгрузитьКолонку("ДоговорВзаиморасчетов"));
	Запрос.УстановитьПараметр("РегламентированныйУчет", Объект.РегламентированныйУчет);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СУММА(ВложенныйЗапрос.ОстатокНаНачало) КАК ОстатокНаНачало,
	               |	СУММА(ВложенныйЗапрос.ОстатокНаКонец) КАК ОстатокНаКонец
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ЕСТЬNULL(РасчетыСПокупателямиОстаткиИОбороты.СуммаНачальныйОстаток, 0) КАК ОстатокНаНачало,
	               |		ЕСТЬNULL(РасчетыСПокупателямиОстаткиИОбороты.СуммаКонечныйОстаток, 0) КАК ОстатокНаКонец
	               |	ИЗ
	               |		РегистрНакопления.РасчетыСПокупателями.ОстаткиИОбороты(
	               |				&ДатаНачала,
	               |				&ДатаОкончания,
	               |				,
	               |				,
	               |				ДоговорВзаиморасчетов В (&ДоговорВзаиморасчетов)
	               |					И Контрагент = &Контрагент
	               |					И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПокупателямиОстаткиИОбороты
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ЕСТЬNULL(РасчетыСПоставщикамиОстаткиИОбороты.СуммаНачальныйОстаток, 0),
	               |		ЕСТЬNULL(РасчетыСПоставщикамиОстаткиИОбороты.СуммаКонечныйОстаток, 0)
	               |	ИЗ
	               |		РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(
	               |				&ДатаНачала,
	               |				&ДатаОкончания,
	               |				,
	               |				,
	               |				ДоговорВзаиморасчетов В (&ДоговорВзаиморасчетов)
	               |					И Контрагент = &Контрагент
	               |					И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПоставщикамиОстаткиИОбороты) КАК ВложенныйЗапрос
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РасчетыСПокупателямиОбороты.Период КАК Дата,
	               |	РасчетыСПокупателямиОбороты.Регистратор КАК ДокументРегистратор,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПокупателямиОбороты.СуммаОборот > 0
	               |			ТОГДА РасчетыСПокупателямиОбороты.СуммаОборот
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Дебет,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПокупателямиОбороты.СуммаОборот < 0
	               |			ТОГДА РасчетыСПокупателямиОбороты.СуммаОборот * -1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Кредит,
	               |	ВЫБОР
	               |		КОГДА НЕ РасчетыСПокупателямиОбороты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	               |			ТОГДА РасчетыСПокупателямиОбороты.Регистратор.ХозОперация
	               |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	               |	КОНЕЦ КАК ХозОперация,
	               |	РасчетыСПокупателямиОбороты.Регистратор.Дата КАК РегистраторДата,
	               |	РасчетыСПокупателямиОбороты.Регистратор.Номер КАК РегистраторНомер
	               |ИЗ
	               |	РегистрНакопления.РасчетыСПокупателями.Обороты(
	               |			&ДатаНачала,
	               |			&ДатаОкончания,
	               |			Регистратор,
	               |			ДоговорВзаиморасчетов В (&ДоговорВзаиморасчетов)
	               |				И Контрагент = &Контрагент
	               |				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПокупателямиОбороты
	               |ГДЕ
	               |	РасчетыСПокупателямиОбороты.СуммаОборот <> 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	РасчетыСПоставщикамиОбороты.Период,
	               |	РасчетыСПоставщикамиОбороты.Регистратор,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПоставщикамиОбороты.СуммаОборот > 0
	               |			ТОГДА РасчетыСПоставщикамиОбороты.СуммаОборот
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПоставщикамиОбороты.СуммаОборот < 0
	               |			ТОГДА РасчетыСПоставщикамиОбороты.СуммаОборот * -1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА НЕ РасчетыСПоставщикамиОбороты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	               |			ТОГДА РасчетыСПоставщикамиОбороты.Регистратор.ХозОперация
	               |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПоставщикамиОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
	               |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщикамиОбороты.Регистратор КАК Документ.ПоступлениеТоваров).ВхДокДата
	               |		ИНАЧЕ РасчетыСПоставщикамиОбороты.Регистратор.Дата
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА РасчетыСПоставщикамиОбороты.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
	               |			ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщикамиОбороты.Регистратор КАК Документ.ПоступлениеТоваров).ВхДокНомер
	               |		ИНАЧЕ РасчетыСПоставщикамиОбороты.Регистратор.Номер
	               |	КОНЕЦ
	               |ИЗ
	               |	РегистрНакопления.РасчетыСПоставщиками.Обороты(
	               |			&ДатаНачала,
	               |			&ДатаОкончания,
	               |			Регистратор,
	               |			ДоговорВзаиморасчетов В (&ДоговорВзаиморасчетов)
	               |				И Контрагент = &Контрагент
	               |				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПоставщикамиОбороты
	               |ГДЕ
	               |	РасчетыСПоставщикамиОбороты.СуммаОборот <> 0";

	
	Запрос.Текст = ТекстЗапроса;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Итоги = МассивРезультатов[0].Выбрать();
	Если Итоги.Следующий() Тогда 
		Объект.ОстатокНаНачало = Итоги.ОстатокНаНачало;
	Иначе
		Объект.ОстатокНаНачало = 0;
	КонецЕсли;
		
	ТаблицаДокументов = МассивРезультатов[1].Выгрузить();

	ТаблицаДокументов.Сортировать("Дата");	
	ТаблицаДокументов.Колонки.Добавить("Представление");
	
	Для Каждого Стр Из ТаблицаДокументов Цикл 
		Стр.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '%1 №%2 от %3'; uk = '%1 №%2 від %3'"),
							?(ЗначениеЗаполнено(Стр.ХозОперация), Стр.ХозОперация, Строка(ТипЗнч(Стр.ДокументРегистратор))),
							СокрЛП(Стр.РегистраторНомер),
							Формат(Стр.РегистраторДата, "ДФ='дд ММ гггг'"));
	КонецЦикла;	
	
	Объект.ПоДаннымОрганизации.Загрузить(ТаблицаДокументов);
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымОрганизации(Объект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеОрганизации = Объект.ПоДаннымОрганизации.Выгрузить();
	ДанныеКонтрагента = Объект.ПоДаннымКонтрагента.Выгрузить();
	
	ДанныеКонтрагента.Очистить();
	
	Для Каждого стрОрг Из ДанныеОрганизации Цикл 
		НоваяСтрока = ДанныеКонтрагента.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, стрОрг, "Дата, ДокументРегистратор");
		НоваяСтрока.Дебет = стрОрг.Кредит;
		НоваяСтрока.Кредит = стрОрг.Дебет;		
		
		текПредставление = СокрЛП(стрОрг.Представление);
		Представление = "";		
		
		ДатаНомер = Прав(текПредставление, СтрДлина(текПредставление) - Найти(текПредставление, "№") + 1);
		ТипРегистратора = ТипЗнч(стрОрг.ДокументРегистратор);
		
		Если ТипРегистратора = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
			
			Представление = НСтр("ru = 'Реализация товаров '; uk = 'Реализація товарів '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.ВозвратПоставщику") Тогда 
			
			Представление = НСтр("ru = 'Возврат от покупателя '; uk = 'Повернення від покупця '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда 
			
			Представление = НСтр("ru = 'Возврат поставщику '; uk = 'Повернення постачальнику '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда 
			
			Представление = НСтр("ru = 'Передача денежных средств (нал.) '; uk = 'Передача грошових коштів (гот.) '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств") Тогда 
			
			Представление = НСтр("ru = 'Передача денежных средств (б/н) '; uk = 'Передача грошових коштів (б/г) '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда 
			
			Представление = НСтр("ru = 'Поступление денежных средств (нал.) '; uk = 'Надходження грошових коштів (гот.) '") + ДатаНомер;
			
		ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда 			
			
			Представление = НСтр("ru = 'Поступление денежных средств (б/н) '; uk = 'Надходження грошових коштів (б/г) '") + ДатаНомер;			
			
		Иначе
			
			Представление = текПредставление;
			
		КонецЕсли;
		
		НоваяСтрока.Представление = Представление;
		
	КонецЦикла;
	
	Объект.ПоДаннымКонтрагента.Загрузить(ДанныеКонтрагента);
	
КонецПроцедуры


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Акт сверки
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСверки") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"АктСверки",
		НСтр("ru = 'Акт сверки'; uk = 'Акт звіряння'"), 
		ПечатьАктСверки(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ТК_АктСверкиВзаиморасчетов.ПФ_MXL_АктСверки");
		
	КонецЕсли;	

	
КонецПроцедуры

Функция ПечатьАктСверки(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_АктСверкиВзаиморасчетов.Ссылка КАК Ссылка,
	               |	ТК_АктСверкиВзаиморасчетов.ДатаНачала КАК ДатаНачала,
	               |	ТК_АктСверкиВзаиморасчетов.ДатаОкончания КАК ДатаОкончания,
	               |	ТК_АктСверкиВзаиморасчетов.Организация КАК Организация,
	               |	ТК_АктСверкиВзаиморасчетов.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	ТК_АктСверкиВзаиморасчетов.Контрагент КАК Контрагент,
	               |	ТК_АктСверкиВзаиморасчетов.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ТК_АктСверкиВзаиморасчетов.ОстатокНаНачало КАК ОстатокНаНачало,
	               |	ТК_АктСверкиВзаиморасчетов.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_АктСверкиВзаиморасчетов.ПредставительКонтрагента КАК ПредставительКонтрагента,
	               |	ТК_АктСверкиВзаиморасчетов.ПредставительОрганизации КАК ПредставительОрганизации,
	               |	ТК_АктСверкиВзаиморасчетов.СверкаСогласована КАК СверкаСогласована,
	               |	ТК_АктСверкиВзаиморасчетов.ПоДаннымОрганизации.(
	               |		Дата КАК ДатаДокумента,
	               |		Представление КАК РегистраторПредставление,
	               |		Дебет КАК СуммаОборотДт,
	               |		Кредит КАК СуммаОборотКт,
	               |		ДокументРегистратор КАК ДокументРегистратор
	               |	) КАК ПоДаннымОрганизации,
	               |	ТК_АктСверкиВзаиморасчетов.ПоДаннымКонтрагента.(
	               |		Дата КАК ДатаДокументаКонтр,
	               |		Представление КАК РегистраторПредставлениеКонтр,
	               |		Дебет КАК СуммаОборотДтКонтр,
	               |		Кредит КАК СуммаОборотКтКонтр
	               |	) КАК ПоДаннымКонтрагента,
	               |	ТК_АктСверкиВзаиморасчетов.ДоговорыВзаиморасчетов.(
	               |		Ссылка КАК Ссылка,
	               |		ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |	) КАК ДоговорыВзаиморасчетов,
	               |	ТК_АктСверкиВзаиморасчетов.ДолжностьПредставителяОрганизации КАК ДолжностьПредставителяОрганизации,
	               |	ТК_АктСверкиВзаиморасчетов.ДолжностьПредставителяКонтрагент КАК ДолжностьПредставителяКонтрагент
	               |ИЗ
	               |	Документ.ТК_АктСверкиВзаиморасчетов КАК ТК_АктСверкиВзаиморасчетов
	               |ГДЕ
	               |	ТК_АктСверкиВзаиморасчетов.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктСверки";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТК_АктСверкиВзаиморасчетов.ПФ_MXL_АктСверки");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок    = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьНачОстатки   = Макет.ПолучитьОбласть("НачОстатки");
	ОбластьОбороты      = Макет.ПолучитьОбласть("Обороты");
	ОбластьОборотыИтог  = Макет.ПолучитьОбласть("ОборотыИтог");
	ОбластьКонОстатки   = Макет.ПолучитьОбласть("КонОстатки");
	ОбластьПодвал       = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл		
												
		текОрганизация = ?(ЗначениеЗаполнено(ВыборкаДокументов.ОрганизацияНаименованиеПолное), СокрЛП(ВыборкаДокументов.ОрганизацияНаименованиеПолное), СокрЛП(ВыборкаДокументов.Организация));
		текКонтрагент = ?(ЗначениеЗаполнено(ВыборкаДокументов.КонтрагентНаименованиеПолное), СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное), СокрЛП(ВыборкаДокументов.Контрагент));
		Валюта = СокрЛП(ВыборкаДокументов.ВалютаДокумента.НаименованиеПолное);
		
		ПредставительОрг = СокрЛП(ВыборкаДокументов.ПредставительОрганизации);
		ПредставительКонтр = СокрЛП(ВыборкаДокументов.ПредставительКонтрагента);
		
		ДолжностьПредставительОрг = СокрЛП(ВыборкаДокументов.ДолжностьПредставителяОрганизации);
		ДолжностьПредставительКонтр = СокрЛП(ВыборкаДокументов.ДолжностьПредставителяКонтрагент);
		
		тДоговора = ВыборкаДокументов.ДоговорыВзаиморасчетов.Выгрузить();
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'взаимных расчетов за период с %1 по %2
                                  |между %3
                                  |и %4'; uk = 'взаємних розрахунків за період з %1 по %2
                                  |між %3
                                  |та %4'", КодЯзыкаПечать),
							Формат(ВыборкаДокументов.ДатаНачала, "ДФ=dd.MM.yyyy"),
							Формат(ВыборкаДокументов.ДатаОкончания, "ДФ=dd.MM.yyyy"),
							текОрганизация,
							текКонтрагент);
							
		Если тДоговора.Количество() = 1 Тогда 
			Договор = тДоговора[0].ДоговорВзаиморасчетов.НаименованиеДляПечати;
			Заголовок = Заголовок + Символы.ПС+НСтр("ru='по договору ';uk='за договором '",КодЯзыкаПечать)+Сред(Договор,8);
		КонецЕсли;
							
		ЗаголовокАкт = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Мы, нижеподписавшиеся, %1 %2 %3, с одной стороны и %4 %5 %6, с другой стороны, составили настоящий акт сверки в том, что состояние взаимных расчетов по данным учета следующее:'; uk = 'Ми, що нижче підписалися, %1 %2 %3, з одного боку та %4 %5 %6, з іншого боку, склали цей акт звіряння у тому, що стан взаємних розрахунків по даним обліку наступний:'", КодЯзыкаПечать),
							?(ЗначениеЗаполнено(ДолжностьПредставительОрг), ДолжностьПредставительОрг, "_______________________"),
							текОрганизация,
							?(ЗначениеЗаполнено(ПредставительОрг), ПредставительОрг, "__________________________________________"),
							?(ЗначениеЗаполнено(ДолжностьПредставительКонтр), ДолжностьПредставительКонтр, "_______________________"),
							текКонтрагент,
							?(ЗначениеЗаполнено(ПредставительКонтр), ПредставительКонтр, "__________________________________________"));
							
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", Заголовок);
		СтруктураШапки.Вставить("СтрЗаголовокТаблица", ЗаголовокАкт);
		
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("НазваниеОрганизации", текОрганизация);
		СтруктураРеквизитов.Вставить("НаименованиеКонтрагента", текКонтрагент);
		СтруктураРеквизитов.Вставить("ВалютаДокумента", Валюта);		
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		// Реестр документов
		СверкаСогласована = ВыборкаДокументов.СверкаСогласована;
		
		тзПоДаннымОрганизации = ВыборкаДокументов.ПоДаннымОрганизации.Выгрузить();
		тзПоДаннымКонтрагента = ВыборкаДокументов.ПоДаннымКонтрагента.Выгрузить();
		
		квоСтрокОрганизации = тзПоДаннымОрганизации.Количество();
		квоСтрокКонтрагента = тзПоДаннымКонтрагента.Количество();
		ЕстьДанныеКонтрагента = квоСтрокКонтрагента > 0;
		
		НачОстатокОрг = ВыборкаДокументов.ОстатокНаНачало;		
		НачОстатокКонтр = ?(ЕстьДанныеКонтрагента, НачОстатокОрг, 0);
		
		НачДт = ?(НачОстатокОрг > 0, НачОстатокОрг, 0);
		НачКт = ?(НачОстатокОрг < 0, -НачОстатокОрг, 0);
		
		ОборотДт = тзПоДаннымОрганизации.Итог("СуммаОборотДт");
		ОборотКт = тзПоДаннымОрганизации.Итог("СуммаОборотКт");
		ОборотДтК = ?(ЕстьДанныеКонтрагента, тзПоДаннымКонтрагента.Итог("СуммаОборотДтКонтр"), 0);
		ОборотКтК = ?(ЕстьДанныеКонтрагента, тзПоДаннымКонтрагента.Итог("СуммаОборотКтКонтр"), 0);
		
		КонОстатокОрг = НачОстатокОрг + ОборотДт - ОборотКт;
		КонОстатокКонтр = НачОстатокКонтр + ОборотДтК - ОборотКтК;
		
		СтруктураСальдо = Новый Структура;
		СтруктураСальдо.Вставить("СуммаНачальныйОстатокДт", НачДт);
		СтруктураСальдо.Вставить("СуммаНачальныйОстатокКт", НачКт);
		СтруктураСальдо.Вставить("СуммаНачальныйОстатокКтКонтр", ?(ЕстьДанныеКонтрагента, НачДт, 0));
		СтруктураСальдо.Вставить("СуммаНачальныйОстатокДтКонтр", ?(ЕстьДанныеКонтрагента, НачКт, 0));
		
		СтруктураСальдо.Вставить("СуммаОборотДт", ОборотДт);
		СтруктураСальдо.Вставить("СуммаОборотКт", ОборотКт);
		СтруктураСальдо.Вставить("СуммаОборотДтКонтр", ?(СверкаСогласована, ОборотДтК, 0));
		СтруктураСальдо.Вставить("СуммаОборотКтКонтр", ?(СверкаСогласована, ОборотКтК, 0));
		
		СтруктураСальдо.Вставить("СуммаКонечныйОстатокДт", ?(КонОстатокОрг > 0, КонОстатокОрг, 0));
		СтруктураСальдо.Вставить("СуммаКонечныйОстатокКт", ?(КонОстатокОрг < 0, -КонОстатокОрг, 0));
		СтруктураСальдо.Вставить("СуммаКонечныйОстатокДтКонтр", ?(СверкаСогласована И КонОстатокКонтр > 0, КонОстатокКонтр, 0));
		СтруктураСальдо.Вставить("СуммаКонечныйОстатокКтКонтр", ?(СверкаСогласована И КонОстатокКонтр < 0, -КонОстатокКонтр, 0));
		
		ОбластьНачОстатки.Параметры.Заполнить(СтруктураСальдо);
		ТабличныйДокумент.Вывести(ОбластьНачОстатки);
		
		ТабличныйДокумент.НачатьАвтогруппировкуСтрок();		
		
		МаксИндекс = Макс(квоСтрокОрганизации, квоСтрокКонтрагента) - 1;
		Для Инд = 0 По МаксИндекс Цикл 
			СтруктураСтроки = Новый Структура("ДатаДокумента, РегистраторПредставление, СуммаОборотДт, СуммаОборотКт, ДатаДокументаКонтр, РегистраторПредставлениеКонтр, СуммаОборотДтКонтр, СуммаОборотКтКонтр");
			Если Инд < квоСтрокОрганизации Тогда 
				Если ТипЗнч(тзПоДаннымОрганизации[Инд].ДокументРегистратор) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
					РегистраторПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
						СокрЛП(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Номер),
						Формат(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Дата, "ДФ=dd.MM.yyyy"));
				ИначеЕсли ТипЗнч(тзПоДаннымОрганизации[Инд].ДокументРегистратор) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств") Тогда 
					РегистраторПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Платежное поручение входящее №%1 от %2'; uk = 'Платіжне доручення №%1 від %2'", КодЯзыкаПечать),
						СокрЛП(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Номер),
						Формат(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Дата, "ДФ=dd.MM.yyyy"));
				ИначеЕсли ТипЗнч(тзПоДаннымОрганизации[Инд].ДокументРегистратор) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда 
					РегистраторПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Приходный кассовый ордер №%1 от %2'; uk = 'Прибутковий кассовий ордер №%1 від %2'", КодЯзыкаПечать),
						СокрЛП(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Номер),
						Формат(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Дата, "ДФ=dd.MM.yyyy"));
				ИначеЕсли ТипЗнч(тзПоДаннымОрганизации[Инд].ДокументРегистратор) = Тип("ДокументСсылка.ТК_КорректировкаДолга") Тогда 
					РегистраторПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Корректировка долга №%1 от %2'; uk = 'Корегування боргу №%1 від %2'", КодЯзыкаПечать),
						СокрЛП(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Номер),
						Формат(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Дата, "ДФ=dd.MM.yyyy"));
				ИначеЕсли ТипЗнч(тзПоДаннымОрганизации[Инд].ДокументРегистратор) = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда 
					РегистраторПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Возврат от покупателя №%1 от %2'; uk = 'Повернення від покупця №%1 від %2'", КодЯзыкаПечать),
						СокрЛП(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Номер),
						Формат(тзПоДаннымОрганизации[Инд].ДокументРегистратор.Дата, "ДФ=dd.MM.yyyy"));
				Иначе
					 РегистраторПредставление = тзПоДаннымОрганизации[Инд].РегистраторПредставление;
				КонецЕсли;

				СтруктураСтроки.Вставить("ДатаДокумента", Формат(тзПоДаннымОрганизации[Инд].ДатаДокумента,"ДФ=dd.MM.yyyy"));
				СтруктураСтроки.Вставить("РегистраторПредставление", РегистраторПредставление);
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, тзПоДаннымОрганизации[Инд],  "СуммаОборотДт, СуммаОборотКт");
			КонецЕсли;
			
			Если Инд < квоСтрокКонтрагента Тогда 
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, тзПоДаннымКонтрагента[Инд], "ДатаДокументаКонтр, РегистраторПредставлениеКонтр, СуммаОборотДтКонтр, СуммаОборотКтКонтр");
			КонецЕсли;
			
			ОбластьОбороты.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьОбороты);
		КонецЦикла;
		
		ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
		
		ОбластьОборотыИтог.Параметры.Заполнить(СтруктураСальдо);
		ТабличныйДокумент.Вывести(ОбластьОборотыИтог);
		
		ОбластьКонОстатки.Параметры.Заполнить(СтруктураСальдо);
		ТабличныйДокумент.Вывести(ОбластьКонОстатки);
		
		// Результаты сверки
		РезультатСверки = "";
		РезультатСверкиК = "";
		ПоДаннымКонтрагента = "";
		ИтогСверки = "";
		
		Если квоСтрокОрганизации = 0 И НачОстатокОрг = 0 Тогда 
			РезультатСверки = НСтр("ru = '<сверка не проведена>'; uk = '<звірка не проведена>'", КодЯзыкаПечать);
		ИначеЕсли Не ЗначениеЗаполнено(ВыборкаДокументов.ДатаОкончания) Тогда 
			РезультатСверки = НСтр("ru = '<не указана дата сверки>'; uk = '<не вказана дата звіряння>'", КодЯзыкаПечать);
		ИначеЕсли Не ЗначениеЗаполнено(ВыборкаДокументов.Контрагент) Тогда 
			РезультатСверки = НСтр("ru = '<не указан контрагент>'; uk = '<не вказаний контрагент>'", КодЯзыкаПечать);
		Иначе
			Результат_А = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'на %1 задолженность '; uk = 'на %1 заборгованість '", КодЯзыкаПечать),
								Формат(ВыборкаДокументов.ДатаОкончания, "ДФ=dd.MM.yyyy"));
								
			Если КонОстатокОрг = 0 Тогда 
				Результат_Б = НСтр("ru = 'отсутствует.'; uk = 'відсутня.'", КодЯзыкаПечать);
			Иначе
				ОстДт = КонОстатокОрг > 0;

				СуммаИтог = ?(ОстДт, КонОстатокОрг, -КонОстатокОрг);
				ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
				СуммаПрописью = ЧислоПрописью(СуммаИтог, "ЧЦ=21; ЧДЦ=2; Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);

				Результат_Б = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'в пользу %1 %2 %3 (%4)'; uk = 'на користь %1 %2 %3 (%4)'", КодЯзыкаПечать),
								    ?(ОстДт, текОрганизация, текКонтрагент),
									СуммаИтог,
									Валюта,
									СуммаПрописью);								
			КонецЕсли;
			
			РезультатСверки = Результат_А + Результат_Б;
		КонецЕсли;
		
		Если СверкаСогласована Тогда 
			ПоДаннымКонтрагента = НСтр("ru = 'По данным '; uk = 'По даним '", КодЯзыкаПечать) + текКонтрагент;
			Если квоСтрокКонтрагента = 0 И НачОстатокОрг = 0 Тогда 
				РезультатСверкиК = НСтр("ru = '<сверка не проведена>'; uk = '<звірка не проведена>'", КодЯзыкаПечать);
			ИначеЕсли Не ЗначениеЗаполнено(ВыборкаДокументов.ДатаОкончания) ИЛИ Не ЗначениеЗаполнено(ВыборкаДокументов.Контрагент) Тогда 
				РезультатСверкиК = РезультатСверки;
			Иначе
				Результат_А = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'на %1 задолженность '; uk = 'на %1 заборгованість '", КодЯзыкаПечать),
								Формат(ВыборкаДокументов.ДатаОкончания, "ДФ=dd.MM.yyyy"));
				
				Если КонОстатокОрг = 0 Тогда 
					Результат_Б = НСтр("ru = 'отсутствует.'; uk = 'відсутня.'", КодЯзыкаПечать);
				Иначе
					ОстДт = КонОстатокКонтр > 0;

					СуммаИтог = ?(ОстДт, КонОстатокКонтр, -КонОстатокКонтр);
					ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
					СуммаПрописью = ЧислоПрописью(СуммаИтог, "ЧЦ=21; ЧДЦ=2; Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);

					Результат_Б = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'в пользу %1 %2 %3 (%4)'; uk = 'на користь %1 %2 %3 (%4)'", КодЯзыкаПечать),
									?(ОстДт, текОрганизация, текКонтрагент),
									СуммаИтог,
									Валюта,
									СуммаПрописью);								
				КонецЕсли;
				
				РезультатСверкиК = Результат_А + Результат_Б;
			КонецЕсли;	
			
			Расхождения = КонОстатокОрг + КонОстатокКонтр;
			Если Расхождения <> 0 Тогда 
				СуммаИтог = Макс(Расхождения, -Расхождения);
				ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
				СуммаПрописью = ЧислоПрописью(СуммаИтог, "ЧЦ=21; ЧДЦ=2; Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);

				ИтогСверки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = '
                                      |В результате сверки выявлено расхождение информации о состоянии расчетов в размере %1 %2 (%3)
                                      |'; uk = '
                                      |У результаті звіряння виявлені розбіжності інформації про стан розрахунків у розмірі %1 %2 (%3)
                                      |'", КодЯзыкаПечать),
								СуммаИтог,
								Валюта,
								СуммаПрописью) + Символы.ПС;
			КонецЕсли;
		КонецЕсли;
		
		СтруктураРеквизитов.Вставить("РезультатыСверки", РезультатСверки);
		СтруктураРеквизитов.Вставить("РезультатыСверкиК", РезультатСверкиК);
		СтруктураРеквизитов.Вставить("ИтогСверки", ИтогСверки);
		СтруктураРеквизитов.Вставить("ПоДаннымКонтрагента", ПоДаннымКонтрагента);
		СтруктураРеквизитов.Вставить("Должность", ?(ЗначениеЗаполнено(ДолжностьПредставительОрг), ДолжностьПредставительОрг, "_______________________"));
		СтруктураРеквизитов.Вставить("ДолжностьК", ?(ЗначениеЗаполнено(ДолжностьПредставительКонтр), ДолжностьПредставительКонтр, "_______________________"));
		СтруктураРеквизитов.Вставить("ФИОПредставителя", ?(ЗначениеЗаполнено(ПредставительОрг), ПредставительОрг, "(_______________________)"));
		СтруктураРеквизитов.Вставить("ФИОПредставителяК", ?(ЗначениеЗаполнено(ПредставительКонтр), ПредставительКонтр, "(_______________________)"));
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураРеквизитов);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции


#КонецОбласти	



#Иначе
	
#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//КомандаПечати = КомандыПечати.Добавить();
	//КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
	//КомандаПечати.Идентификатор = "АктНедопоставки";
	//КомандаПечати.Представление = НСтр("ru = 'Акт недопоставки';uk='Акт недопостачання'");
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	//МассивУсловий = Новый Массив;
	//МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ИспользоватьЦеныИзПротоколов", Истина, ВидСравнения.Равно));	
	////МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ЕстьРасхождения", Истина, ВидСравнения.Равно));	
	//КомандаПечати.УсловияВидимости = МассивУсловий;
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
	КонецЕсли;
	
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	


#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;		
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	            	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаТК_ПравилаСписанияДопМатериалов(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТК_ПравилаСписанияДопМатериалов.Дата КАК Период,
	|	ТК_ПравилаСписанияДопМатериалов.ХозОперация КАК ХозОперация,
	|	ТК_ПравилаСписанияДопМатериалов.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ТК_ПравилаСписанияДопМатериалов.Номенклатура КАК Номенклатура,
	|	ТК_ПравилаСписанияДопМатериалов.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	ТК_ПравилаСписанияДопМатериалов.Количество * ЕСТЬNULL(ТК_ПравилаСписанияДопМатериалов.ЕдиницаИзмерения.Коэффициент, 1) КАК КоличествоБазовое,
	|	ТК_ПравилаСписанияДопМатериалов.СтатьяСписанияТМЦ КАК СтатьяСписания
	|ИЗ
	|	Документ.ТК_ПравилаСписанияДопМатериалов КАК ТК_ПравилаСписанияДопМатериалов
	|ГДЕ
	|	ТК_ПравилаСписанияДопМатериалов.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ХозОперация",							  Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Номенклатура", 							  Реквизиты.Номенклатура);
	Запрос.УстановитьПараметр("КоличествоБазовое",						  Реквизиты.КоличествоБазовое);
	Запрос.УстановитьПараметр("СтатьяСписания", 						  Реквизиты.СтатьяСписания);
		
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);

КонецПроцедуры
	
Функция ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаДопМатериалы";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ПравилаСписанияДопМатериаловДопМатериалы.Номенклатура КАК ДопМатериал,
	               |	ТК_ПравилаСписанияДопМатериаловДопМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ПравилаСписанияДопМатериаловДопМатериалы.Коэффициент КАК Коэффициент,
	               |	ТК_ПравилаСписанияДопМатериаловДопМатериалы.Количество КАК Количество
	               |ПОМЕСТИТЬ ВтТаблицаДопМатериалы
	               |ИЗ
	               |	Документ.ТК_ПравилаСписанияДопМатериалов.ДопМатериалы КАК ТК_ПравилаСписанияДопМатериаловДопМатериалы
	               |ГДЕ
	               |	ТК_ПравилаСписанияДопМатериаловДопМатериалы.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		                  
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТК_ПравилаСписанияДопМатериалов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТК_ПравилаСписанияДопМатериалов";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаДопМатериалы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	&Номенклатура КАК Номенклатура,
	               |	ВтТаблицаДопМатериалы.ДопМатериал КАК ДопМатериал,
	               |	ВтТаблицаДопМатериалы.Количество * ВтТаблицаДопМатериалы.Коэффициент / &КоличествоБазовое КАК Количество,
	               |	ВЫБОР
	               |		КОГДА &ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ОтменаПравилаСписанияДопМатериалов)
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК Статус,
	               |	&СтатьяСписания КАК СтатьяСписания
	               |ИЗ
	               |	ВтТаблицаДопМатериалы КАК ВтТаблицаДопМатериалы"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти



#Иначе
	
#КонецЕсли
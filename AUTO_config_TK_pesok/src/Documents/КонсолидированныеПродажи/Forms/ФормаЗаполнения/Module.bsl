
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДокПродажи = Параметры.Ссылка;
	Период.Вариант = ВариантСтандартногоПериода.Последние7Дней;
КонецПроцедуры

&НаСервере
Функция ПолучитьПродажи(Номенклатура,Всего)
			СписокОпераций = Новый СписокЗначений;
		//СписокОпераций.Добавить(Справочники.ХозОперации.ВозвратТоваровОтПокупателя);
		//СписокОпераций.Добавить(Справочники.ХозОперации.РеализацияТоваров);
		//СписокОпераций.Добавить(Справочники.ХозОперации.РеализацияТоваровРозница);
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСмены);
		СписокОпераций.Добавить(Справочники.ХозОперации.ЗакрытиеСменыВозврат);
		
		Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	|	ПродажиОбороты.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПродажиОбороты.СкладКомпании КАК СкладКомпании,
	|	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот,
	|	ПродажиОбороты.СуммаОборот / ПродажиОбороты.КоличествоОборот КАК ЦенаПродажи,
	|	ПродажиОбороты.СебестоимостьУпрОборот / ПродажиОбороты.КоличествоОборот КАК ЦенаСеб,
	|	ПродажиОбороты.КоличествоОборот / &Всего КАК Поле1
	|ИЗ
	|	РегистрНакопления.Продажи.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			ХозОперация В (&СписокХоз)
	|				И Номенклатура = &Номенклатура
	|				И СкладКомпании В ИЕРАРХИИ (&СкладКомпании)) КАК ПродажиОбороты
	|ГДЕ
	|	ПродажиОбороты.КоличествоОборот > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодразделениеКомпании,
	|	СкладКомпании
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("СкладКомпании", Склады);
	Запрос.УстановитьПараметр("СписокХоз", СписокОпераций);
	Запрос.УстановитьПараметр("Всего", Всего);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТЧТовар(ДокПродажи)
	ОбъектДокПродажи = ДокПродажи.ПолучитьОбъект();
	ОбъектДокПродажи.Товары.Очистить();
	
	Для Каждого стрТовар  из Товары Цикл 
		тзОборот = ПолучитьПродажи(стрТовар.Номенклатура,стрТовар.Количество);
		Всего = тзОборот.Итог("КоличествоОборот");
		
		Для Каждого стрОборот из ТзОборот Цикл
			
			Коф = стрОборот.КоличествоОборот/ Всего;
			Количество = Окр( Коф* стрТовар.Количество);
			Если Количество = 0 Тогда Продолжить; КонецЕсли;
			стТов = ОбъектДокПродажи.Товары.Добавить();
			стТов.СтатусПартии			= стрОборот.СтатусПартии;
			стТов.ПодразделениеКомпании	= стрОборот.ПодразделениеКомпании;
			стТов.СкладКомпании			= стрОборот.СкладКомпании;
			стТов.Номенклатура			= стрТовар.Номенклатура;
			стТов.ХарактеристикаНоменклатуры = стрТовар.МРЦ;
			стТов.СтавкаНДС				= стТов.Номенклатура.СтавкаНДС;
			стТов.Количество			= Количество;
			стТов.Себестоимость			= стТов.Количество*стрОборот.ЦенаСеб;
			стТов.СуммаВсего			= стТов.Количество*стрОборот.ЦенаПродажи;
			стТов.СуммаНДС				= стТов.СуммаВсего*стТов.СтавкаНДС.Ставка/(100+стТов.СтавкаНДС.Ставка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ОбъектДокПродажи.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТовар()
	Товары.Очистить();
	
	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		КодТовара = СокрЛП(ТабличныйДокумент.Область(НомерСтроки,3,НомерСтроки,3).Текст);
		
		Если ПустаяСтрока(КодТовара) Тогда 
			Продолжить; 
		КонецЕсли; 						
		
		КодТовара = СтрЗаменить(КодТовара, " ", "");
		
		ТекстОшибки = "";
		
		ТоварСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", КодТовара); 
		Если ТоварСсылка.Пустая() Тогда 
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Товар с артикулом %2 - не найден!'; uk = 'Строка %1. Товар з артикулом %2 - не знайдений!'"),
			НомерСтроки,
			КодТовара);
			
		ИначеЕсли ТоварСсылка.ПометкаУдаления Тогда 
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Строка %1. Товар  [%2] %3 - помечен на удаление!'; uk = 'Строка %1. Товар  [%2] %3 - відмічений на видалення!'"),
			НомерСтроки,
			КодТовара,
			ТоварСсылка);
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		знМРЦ = СокрЛП(ТабличныйДокумент.Область(НомерСтроки,2,НомерСтроки,2).Текст);
		знМРЦ = Число(знМРЦ);
		Если ЗначениеЗаполнено(знМРЦ) Тогда
			МРЦ = Справочники.ХарактеристикиНоменклатуры.НайтиПоРеквизиту("ЗначениеМРЦ", знМРЦ,,ТоварСсылка);
		Иначе
			МРЦ = "";
		КонецЕсли;
		
		Количество = ТабличныйДокумент.Область(НомерСтроки,4,НомерСтроки,4).Текст;		
		
		стрТовары = Товары.Добавить();
		стрТовары.Номенклатура = ТоварСсылка;
		стрТовары.МРЦ = МРЦ;
		стрТовары.Количество = Количество;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ВсеОк = Истина;
	Если Не ТабличныйДокумент.ВысотаТаблицы > 1 Тогда 
		ВсеОк = Ложь;
		ТекстСообщения = НСтр("ru = 'Нет данных для загрузки'; uk = 'Немає даних для завантаження'");
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
	Если ВсеОк Тогда 
		ОчиститьСообщения();
		ЗаполнитьТовар();
		Если ЗначениеЗаполнено(Товары) Тогда
			ЗаполнитьТЧТовар(ДокПродажи);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры



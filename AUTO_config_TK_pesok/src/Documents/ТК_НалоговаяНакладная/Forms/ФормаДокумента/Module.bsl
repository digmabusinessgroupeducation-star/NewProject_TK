&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ
Перем Разбить;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	Контрагент = Объект.Контрагент;
	Договор = Объект.ДоговорВзаиморасчетов;
	ВалютаДокумента = Объект.ВалютаДокумента;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ТК_НалоговаяНакладная"));
	
	ТабДок = Документы.ТК_НалоговаяНакладная.ПолучитьМакет("ЗагрузкаСЭкселя");

	УстановитьУсловноеОформление();
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриЧтенииСозданииНаСервере();

	УстановитьСостояниеДокумента();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

	Если ИмяСобытия = "ОбработанаТабличнаяЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	ЗаполнениеНомераСтроки(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	УстановитьСостояниеДокумента();	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦенуПоТипу(Команда)
	Перем ТипЦен;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	
	ОповещениЕ = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект, Команда.Имя);
	ОткрытьФорму("Справочник.ТипыЦен.ФормаВыбора", ПараметрыФормы,ТипЦен,,,,ОповещениЕ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСкидкуНаЦену(Команда)
	ОповещениЕ = Новый ОписаниеОповещения("ВыполнитьПослеВыбора", ЭтотОбъект, Команда.Имя);
	ПоказатьВводЧисла(ОповещениЕ,0,"Скидка, %");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦену(Кнопка,Значение)
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Кнопка = "УстановитьЦенуПоТипу" Тогда
		СтруктураДействий = Новый Структура;
		
		
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,"Цены",,,Значение));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление", Истина);
		
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	ИначеЕсли Кнопка = "УстановитьСкидкуНаЦену" Тогда
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление", Истина);
		
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			СтрокаТаблицы.Цена = СтрокаТаблицы.Цена * (100 - Значение) /100;
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
		КонецЦикла;
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация,Объект.ПодразделениеКомпании);
		Объект.ДоговорВзаиморасчетов = СтруктураДанные.Договор;
		
		ДоговорПередИзменением = Договор;
		Договор = Объект.ДоговорВзаиморасчетов;
		
		ПроверитьСхемуНалообложения();
		
		КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные);
	Иначе
		Объект.ДоговорВзаиморасчетов = Договор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаСервере
Процедура ПроверитьСхемуНалообложения()
	
	СхемаНалогообложения = Справочники.Контрагенты.ПолучитьСхемуНалообложения(Контрагент, ТекущаяДата());
	Если СхемаНалогообложения = Перечисления.СхемыНалогообложения.НеПлательщик Тогда
		Объект.ТипПричиныНевыдачиПокупателю = 2 
	Иначе
		Объект.ТипПричиныНевыдачиПокупателю =0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");

	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));

	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);	
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказатели(ЭтаФорма);   
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБезНалоговПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьПроцентСкидки", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)	
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
			
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНалоговПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РНПриИзменении(Элемент)
	Элементы.ГруппаРеквизитыПечатиРН.Видимость = Объект.РН;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));

	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаДокумента",
		Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Если ДоговорПередИзменением <> Объект.ДоговорВзаиморасчетов Тогда
		
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением);
		
	Иначе
		
		Объект.ДоговорВзаиморасчетов = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()
		
	
	
КонецПроцедуры // ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()

&НаКлиенте
Процедура ОбновитьПодвалФормы()
	
	СуммаВсегоСНДС = Объект.Товары.Итог("СуммаВсего");
	СуммаНДС       = Объект.Товары.Итог("СуммаНДС");
	
КонецПроцедуры // ОбновитьПодвалФормы()

&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	Если Объект.РН = Ложь Тогда 
		СтруктураДействий = Новый Структура;	
		ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
		
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
		
		ПриИзмененииДоговораНаСервере(СтруктураДействий);
		
		ОбновитьПодвалФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДоговораНаСервере(СтруктураДействий)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));

КонецПроцедуры       

&НаКлиенте
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий)
	
	Если ТипЗнч(СтруктураДействий) <> Тип("Структура") Тогда
		СтруктураДействий = Новый Структура;
	КонецЕсли;
	
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект, Истина));
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеНомераСтроки(Объект)
	
	Для Каждого Стр из Объект.Товары Цикл 
		Если НЕ ЗначениеЗаполнено(Стр.НомерСтрокиНН) Тогда
			Стр.НомерСтрокиНН = Стр.НомерСтроки;
		КонецЕсли;
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбора(ТипЦен,Кнопка)		Экспорт
	УстановитьЦену(Кнопка,ТипЦен);
КонецПроцедуры


#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("СтраницаРаспределениеМаркетинга");
	МассивВсехРеквизитов.Добавить("ДоговорВзаиморасчетов");
	МассивВсехРеквизитов.Добавить("ГруппаОснование");
	МассивВсехРеквизитов.Добавить("ГруппаРеквизитыПечатиРН");
	МассивВсехРеквизитов.Добавить("СуммаНДСДокумента");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаНДС");
	МассивВсехРеквизитов.Добавить("ТоварыВходящийДокумент");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если Объект.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяАктУслуг Тогда 
		
		МассивРеквизитовОперации.Добавить("СтраницаРаспределениеМаркетинга");
		МассивРеквизитовОперации.Добавить("ДоговорВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("СуммаНДСДокумента");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");
		
	ИначеЕсли Объект.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа Тогда 
		
		Объект.ДоговорВзаиморасчетов = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		
		МассивРеквизитовОперации.Добавить("СуммаНДСДокумента");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");
		
	ИначеЕсли Объект.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая Тогда 
		
		Объект.ДоговорВзаиморасчетов = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();				
		
	ИначеЕсли Объект.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяСводнаяКомпенсирующая Тогда 
		
		Объект.ДоговорВзаиморасчетов = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		
		МассивРеквизитовОперации.Добавить("ТоварыВходящийДокумент");
		МассивРеквизитовОперации.Добавить("СуммаНДСДокумента");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");		
		
	Иначе
		
		МассивРеквизитовОперации.Добавить("ДоговорВзаиморасчетов");
		МассивРеквизитовОперации.Добавить("СуммаНДСДокумента");
		МассивРеквизитовОперации.Добавить("ТоварыСуммаНДС");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
		МассивРеквизитовОперации.Добавить("ГруппаОснование");
	КонецЕсли;
	
	Если Объект.РН Тогда 
		МассивРеквизитовОперации.Добавить("ГруппаРеквизитыПечатиРН");
	КонецЕсли;
	
	ДенежныеСредстваСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивРеквизитовОперации);	

КонецПроцедуры


#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));

											
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
		//ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		//
		//ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);
		
	КонецЕсли;	
	
	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	// Расчет итога по табличной части "Товары"
	КоллекцияТовары 					= Форма.Объект.Товары;
	Форма.СуммаВсегоСНДС 				= КоллекцияТовары.Итог("СуммаВсего");
	Форма.СуммаНДС 						= КоллекцияТовары.Итог("СуммаНДС");
		
КонецПроцедуры


#КонецОбласти


&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура("СкладКомпании,НайтиПоТочномуСоответствию",Объект.СкладКомпании,Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора",ПараметрыФормы,Элементы.Товары,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
 	ОбработатьВыборНаСервере(ВыбранноеЗначение);         
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища) Экспорт
	
	
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Товары.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
		
		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект, Истина));
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		
		СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);

		
	КонецЦикла;
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеОкончания(Результат, ДополнительныеПараметры)  Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		 РазбитьНННаСервере();
		 ЭтаФорма.Закрыть();
	 КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РазбитьНННаСервере()
	  Обработка = Обработки.РазбиениеННПоСумме.Создать();
	  Обработка.Документ = Объект.Ссылка;
	  Обработка.ВерхнийПорог =Объект.ВерхнийПорог;
	  Обработка.НижнийПорог = Объект.НижнийПорог;
	  Обработка.РазбитьНаСервере();
	  
КонецПроцедуры

&НаКлиенте
Процедура РазбитьНН(Команда)
	Разбить = Ложь;
	ОткрытьФорму("Документ.ТК_НалоговаяНакладная.Форма.Форма",, ЭтаФорма,,,,Новый ОписаниеОповещения("ВыполнитьПослеОкончания", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаВсегоПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры


&НаСервере
Процедура ЗарегистрироватьНаСервере()
		
	Запись = РегистрыСведений.РегистрацияНН.СоздатьНаборЗаписей();
	Запись.Отбор.НалоговаяНакладная.Установить(Объект.Ссылка);
	Запись.Отбор.ДокументОснование.Установить(Объект.ДокументОснование);
	Запись.Прочитать();
	Если Запись.Количество()=0 Тогда
		НоваяЗапись = Запись.Добавить();
		НоваяЗапись.НалоговаяНакладная = Объект.Ссылка;
		НоваяЗапись.ДокументОснование = Объект.ДокументОснование;
	Иначе 
	    НоваяЗапись = Запись[0];
	КонецЕсли;
	
	НоваяЗапись.СтатусРегистрации = Перечисления.СтатусыНН.Новая;
	Запись.Записать();
	
КонецПроцедуры


&НаКлиенте
Процедура Зарегистрировать(Команда)
	ЗарегистрироватьНаСервере();
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ТабДок = Документы.ТК_НалоговаяНакладная.ПолучитьМакет("ЗагрузкаСЭкселя");
				
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
	Элементы.СтраницаЗагрузкаТабличнаяЧасть.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗагрузкаТабличнаяЧасть;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса1", ЭтотОбъект, Параметры);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Очистить табличную загрузки?';"
	+ " uk = 'Очистити таблицю?'"), Режим);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса1(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере();
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ЗагрузитьВТаблицуНаСервере(ВсеОк)
		
	кАртикул = 1;
	кМРЦ = 3;
	кКоличество = 4;
	кЦена = 6;
	кСуммаБезНалогов = 8;
	кСумма = 9;
	
	ТаблицаЗагрузки = Новый ТаблицаЗначений;
	ТаблицаЗагрузки.Колонки.Добавить("Номенклатура");
	ТаблицаЗагрузки.Колонки.Добавить("ХарактеристикаНоменклатуры");
	ТаблицаЗагрузки.Колонки.Добавить("Количество");
	ТаблицаЗагрузки.Колонки.Добавить("КоличествоБазовое");
	ТаблицаЗагрузки.Колонки.Добавить("Цена");
	ТаблицаЗагрузки.Колонки.Добавить("СуммаБезНалогов");
	ТаблицаЗагрузки.Колонки.Добавить("Сумма");
	
	//ПереборТаблицы
	Для Стр = 2 По ТабДок.ВысотаТаблицы Цикл 
		
		тАртикул = СокрЛП(ТабДок.Область(Стр, кАртикул).Текст);
		тНаименование = СокрЛП(ТабДок.Область(Стр, 2).Текст); 
		тХарактеристика =  СокрЛП(ТабДок.Область(Стр, кМРЦ).Текст);
	
		Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", тАртикул);
		
		Если Номенклатура = Справочники.Номенклатура.ПустаяСсылка() Тогда
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("НаименованиеПолное", тНаименование);
			Если Номенклатура = Справочники.Номенклатура.ПустаяСсылка() Тогда
				ОбщегоНазначения.СообщитьПользователю("Обработка прервана!!!! Номер строки "+стр+". Не найдена номеклатура с артикулом " + тАртикул+". Проверьте правильность написания артикула и повторите загрузку.");
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		Если тХарактеристика = "" Тогда
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
		Иначе
			МРЦСтрока = СтрЗаменить(тХарактеристика,"МРЦ","");
			МРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(МРЦСтрока);
			Если МРЦ = 0 Тогда 
				ОбщегоНазначения.СообщитьПользователю("Номер строки "+стр+". Характеристика для номенктаруты  " + Номенклатура+" равна 0. Проверьте правильность заполненой характеристики.");
			КонецЕсли;				
			ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ВернутьХарактеристикуПоЗначениюМРЦ(Номенклатура, МРЦ);
			Если ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда
				ОбщегоНазначения.СообщитьПользователю("Номер строки "+стр+". Не найдена характеристика для номенктаруты  " + Номенклатура+". Проверьте правильность заполненой характеристики.");
			КонецЕсли;	
		КонецЕсли;
		
		тКоличествоСтрока = ТабДок.Область(Стр, кКоличество).Текст;
		тКоличество = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(тКоличествоСтрока);
		тЦенаСтрока = ТабДок.Область(Стр, кЦена).Текст;
		тЦена = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(тЦенаСтрока);
		тСуммаБезНалогов =ТабДок.Область(Стр, кСуммаБезНалогов).Текст;
		тСуммаБезНалогов = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(тСуммаБезНалогов);
		тСумма = ТабДок.Область(Стр, кСумма).Текст;
		тСумма = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(тСумма);

		НоваяСтрока = ТаблицаЗагрузки.Добавить();
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = ХарактеристикаНоменклатуры;
		НоваяСтрока.Количество = тКоличество;
		НоваяСтрока.КоличествоБазовое = тКоличество;
		НоваяСтрока.Цена = тЦена;
		НоваяСтрока.СуммаБезНалогов = тСуммаБезНалогов;
		НоваяСтрока.Сумма = тСумма;
	КонецЦикла;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	Для Каждого стрТовары из ТаблицаЗагрузки Цикл
		СтрокаТовары = Объект.Товары.Добавить(); 
		ЗаполнитьЗначенияСвойств(СтрокаТовары, стрТовары);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТовары.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТовары.ЕдиницаИзмерения);
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, КэшированныеЗначения);
		
		СтруктураДействий = Новый Структура;
		
		Если стрТовары.Цена = 0 Тогда
			Если стрТовары.Сумма = 0 Тогда
				СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");
				СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
			Иначе
				СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
			КонецЕсли;
		Иначе
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, КэшированныеЗначения);

	КонецЦикла;
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
			Объект.Товары.Очистить();
	КонецЕсли;
	ВсеОк = Истина;

	ЗагрузитьВТаблицуНаСервере(ВсеОк);
	
	Если ВсеОк Тогда
		Элементы.СтраницаЗагрузкаТабличнаяЧасть.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаТовары;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВТаблицу(Команда)
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Очистить табличную часть?';"
   			 + " uk = 'Очистити таблицю?'"), Режим);
КонецПроцедуры


&НаКлиенте
Процедура Отмена(Команда)
	Элементы.СтраницаЗагрузкаТабличнаяЧасть.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаТовары;
КонецПроцедуры


&НаКлиенте
Процедура ИзменитьТовары(Команда)
	
	АдресХранилищаТовары = ПоместитьТоварыВоВременноеХранилищеНаСервере();

	ПараметрыФормы = ПолучитьПараметрыОбработкиТабличнойЧастиТовары(АдресХранилищаТовары);
	
	Если ПараметрыФормы <> Неопределено Тогда
		ОткрытьФорму("Обработка.ИзменениеТаблицыТоваров.Форма.Форма", ПараметрыФормы,
		ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
		
	ОбновитьПодвалФормы();

КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыОбработкиТабличнойЧастиТовары(АдресХранилищаТовары)

	ПараметрыОбработки = Новый Структура;
	
	ПараметрыОбработки.Вставить("АдресХранилищаТовары", 		АдресХранилищаТовары);
	
	ПараметрыОбработки.Вставить("ДокументСсылка", 				Объект.Ссылка);
	ПараметрыОбработки.Вставить("ДокументДата", 				Объект.Дата);
	ПараметрыОбработки.Вставить("ДокументОрганизация", 			Объект.Организация);
	ПараметрыОбработки.Вставить("ДокументВалюта", 				Объект.ВалютаДокумента);
	ПараметрыОбработки.Вставить("ДокументКурс",					Объект.КурсДокумента);
	ПараметрыОбработки.Вставить("ДокументСуммаНДСДокумента", 	Объект.СуммаНДСДокумента);
	ПараметрыОбработки.Вставить("ДокументТипЦен", 				Объект.ТипЦен);
	ПараметрыОбработки.Вставить("ДокументСуммаВключаетНДС", 	Истина);

	Возврат ПараметрыОбработки;
	
КонецФункции


&НаСервере
Функция ПоместитьТоварыВоВременноеХранилищеНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)


	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресОбработаннойТабличнойЧастиТоварыВХранилище);
	
	Отбор = Новый Структура("НомерСтрокиДокумента", 0);
	ТаблицаОбработки.Индексы.Добавить("НомерСтрокиДокумента");
	ДобавленныеСтроки = ТаблицаОбработки.НайтиСтроки(Отбор);
	
	Объект.Товары.Загрузить(ТаблицаОбработки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЦену(Команда)
	
	Для Каждого тСтрока Из Объект.Товары Цикл
		ЦенаБНДС =  Окр(тСтрока.Цена/1.2,2);
		тСтрока.Цена = ЦенаБНДС*1.2;

		СтруктураДействий = Новый Структура;
		
	    СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(тСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ОкруглитьСуммуНаСервере()
	Для Каждого стр Из Объект.Товары цикл
		Стр.СуммаБезНалогов = Окр(Стр.СуммаБезНалогов,2);
		Стр.Сумма 			= Окр(Стр.Сумма,2);	
		Стр.СуммаВсего 		= Окр(Стр.СуммаВсего,2);	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОкруглитьСумму(Команда)
	ОкруглитьСуммуНаСервере();
КонецПроцедуры



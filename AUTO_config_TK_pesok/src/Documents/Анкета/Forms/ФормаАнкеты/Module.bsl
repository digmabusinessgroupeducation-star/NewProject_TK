
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
	Если Не Объект.ШаблонАнкеты.Пустая() Тогда 
		Анкетирование.УстановитьКорневойЭлементДереваАнкеты(ДеревоАнкеты);
		Анкетирование.ЗаполнитьДеревоШаблонаАнкеты(ЭтотОбъект,"ДеревоАнкеты", Объект.ШаблонАнкеты);
		АнкетированиеКлиентСервер.СформироватьНумерациюДерева(ДеревоАнкеты);
		
		ДеревоАнкетыСервер = РеквизитФормыВЗначение("ДеревоАнкеты", Тип("ДеревоЗначений"));
		ЗаполнитьТаблицуВопросвРекурсивно(ДеревоАнкетыСервер.Строки);	
		
		Если Не Параметры.Ключ.Пустая() Тогда 
			ЗаполнитьСохраненныеОтветыНаВопросы();
		КонецЕсли;
		
		ОтобразитьВопросыАнкеты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) И Не ЗначениеЗаполнено(Объект.ШаблонАнкеты) Тогда 
		Отказ = Истина;
		ОткрытьФорму("Документ.Анкета.Форма.ФормаВыбораШаблона");
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Для Каждого Стр Из СписокЭлементовИсключений Цикл 
			ИсключениеВопросаПриИзменении(Элементы[Стр.Значение], Ложь);
		КонецЦикла;	
		СписокЭлементовИсключений.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого Стр Из ОтветыНаВопросы Цикл 
		Стр.НомерЯчейки = ?(Стр.ИсключитьВопрос, 1, 0);
	КонецЦикла;	
	
	ТекущийОбъект.Состав.Загрузить(ОтветыНаВопросы.Выгрузить());		
	
	ТекущийОбъект.ДатаРедактирования = ТекущаяДатаСеанса();	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ТекущийОбъект.Интервьюер.Пустая() Тогда 
		ТекущийОбъект.Интервьюер = ТекущийПользователь;
	КонецЕсли;
	
	//Комментарий = ТекущийОбъект.Комментарий;
	//ТекущийОбъект.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	//								"%1%2 - %3, %4",
	//								?(Не ПустаяСтрока(Комментарий), Комментарий + Символы.ПС, ""),
	//								Формат(ТекущийОбъект.ДатаРедактирования, "ДЛФ=DT"),
	//								ТекущийПользователь,
	//								Строка(ПараметрыЗаписи.РежимЗаписи));
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсключениеВопросаПриИзменении(Элемент, ВопросДоступен = Неопределено)

	Если ВопросДоступен = Неопределено Тогда 
		ВопросДоступен = Истина;
		
		ГУИД_Строки = Элемент.Имя;
		ГУИД_Строки = СтрЗаменить(ГУИД_Строки, "_ИсключитьВопрос", "");
		ГУИД_Строки = СтрЗаменить(ГУИД_Строки, "Вопрос_", "");
		ГУИД_Строки = СтрЗаменить(ГУИД_Строки, "_", "-");
		
		ГУИД = Новый УникальныйИдентификатор(ГУИД_Строки);
		
		мСтроки = ОтветыНаВопросы.НайтиСтроки(Новый Структура("КлючСтроки", ГУИД));					
		
		Если мСтроки.Количество() > 0 Тогда 
			текСтрока = мСтроки[0];
			Если текСтрока.ИсключитьВопрос Тогда 
				ВопросДоступен = Ложь;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого текЭлемент Из Элемент.Родитель.ПодчиненныеЭлементы Цикл 
		Если текЭлемент <> Элемент Тогда 
			текЭлемент.Доступность = ВопросДоступен;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары


#КонецОбласти

#Область ОбработчикиКомандФормы


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВопросвРекурсивно(ДеревоСтроки);
	
	Для Каждого Строка Из ДеревоСтроки Цикл 
		
		Если Строка.ТипВопроса = ПредопределенноеЗначение("Перечисление.ТипыВопросовШаблонаАнкеты.Простой") Тогда 
			НоваяСтрока = ОтветыНаВопросы.Добавить();
			НоваяСтрока.КлючСтроки = Строка.КлючСтроки;
			НоваяСтрока.Вопрос = Строка.ВопросШаблона;
			НоваяСтрока.ЭлементарныйВопрос = Строка.ЭлементарныйВопрос;
		КонецЕсли;
		
		ПодчиненныеСтроки = Строка.Строки;
		Если ПодчиненныеСтроки.Количество() > 0 Тогда 
			ЗаполнитьТаблицуВопросвРекурсивно(ПодчиненныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьВопросыАнкеты()
	
	ДеревоАнкетыСервер = РеквизитФормыВЗначение("ДеревоАнкеты", Тип("ДеревоЗначений"));
	
	ДобавитьВопросыРекурсивно(ДеревоАнкетыСервер.Строки);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВопросыРекурсивно(Дерево)
	
	Для Каждого Стр Из Дерево Цикл 		
		
		ИмяВопроса = АнкетированиеКлиентСервер.ПолучитьИмяВопроса(Стр.КлючСтроки);		
		
		Если Стр.ТипСтроки = "Раздел" Тогда 					
			
			НовыйЭлемент = Элементы.Добавить(ИмяВопроса, Тип("ДекорацияФормы"), Элементы.ГруппаВопросыАнкеты);
			НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
			НовыйЭлемент.Заголовок = "Раздел " + Лев(Стр.ПолныйКод, 1) + ". " + Стр.Формулировка;
			НовыйЭлемент.ЦветТекста = ЦветаСтиля.ЦветРазделаПанелиФункций;
			НовыйЭлемент.Шрифт = ШрифтыСтиля.КрупныйШрифтТекста;
			
		ИначеЕсли Стр.ТипСтроки = "Вопрос" Тогда 
			
			ДобавитьВопрос = Истина;
			
			ЭтоЧисло = Стр.ТипОтвета = ПредопределенноеЗначение("Перечисление.ТипыОтветовНаВопрос.Число");
			ЭтоБулево = Стр.ТипОтвета = ПредопределенноеЗначение("Перечисление.ТипыОтветовНаВопрос.Булево");
			
			Если Не ЭтоЧисло И Не ЭтоБулево Тогда 
				ДобавитьВопрос = Ложь;
			КонецЕсли;

			Если ДобавитьВопрос Тогда 
				МассивВопросов = ОтветыНаВопросы.НайтиСтроки(Новый Структура("КлючСтроки", Стр.КлючСтроки));				
				Если МассивВопросов.Количество() = 0 Тогда 
					ДобавитьВопрос = Ложь;
				КонецЕсли;
				
				СтрОтвет = МассивВопросов[0];				
				ИД = СтрОтвет.ПолучитьИдентификатор();			
				
				
				ДобавитьЭлементыВопрос(Стр, Элементы.ГруппаВопросыАнкеты, ЭтаФорма, ИД);																			
			КонецЕсли;
						
		КонецЕсли;
		
		ПодчиненныеСтроки = Стр.Строки;
		Если ПодчиненныеСтроки.Количество() > 0 Тогда 
			ДобавитьВопросыРекурсивно(ПодчиненныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеКлючаСтроки(Знач ГУИДСтроки)
	
	Возврат СтрЗаменить(СокрЛП(Строка(ГУИДСтроки)), "-", "_");
	
КонецФункции

&НаСервере
Процедура ДобавитьЭлементыВопрос(СтрокаТаблицы,ЭлементГруппа,Форма, ИД)
	
	Если СтрокаТаблицы.ТипВопроса = Перечисления.ТипыВопросовШаблонаАнкеты.Табличный 
		ИЛИ СтрокаТаблицы.ТипВопроса = Перечисления.ТипыВопросовШаблонаАнкеты.Комплексный Тогда
		
		Возврат;
	КонецЕсли;
	
	ИмяВопроса = АнкетированиеКлиентСервер.ПолучитьИмяВопроса(СтрокаТаблицы.КлючСтроки);
	ПутьКДанным = "ОтветыНаВопросы[" + ИД + "].Ответ";
	ПутьККомментарий = "ОтветыНаВопросы[" + ИД + "].ОткрытыйОтвет";
	ПутьКИсключению = "ОтветыНаВопросы[" + ИД + "].ИсключитьВопрос";
	
	// Зададим элемент группы для вопроса.
	ЭлементГруппаВопроса = Форма.Элементы.Добавить(ИмяВопроса + "_Группа" ,Тип("ГруппаФормы"),ЭлементГруппа);
	ЭлементГруппаВопроса.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементГруппаВопроса.ОтображатьЗаголовок        = Ложь;
	ЭлементГруппаВопроса.Отображение                = ОтображениеОбычнойГруппы.Нет;
	ЭлементГруппаВопроса.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ЭлементГруппаВопроса.РастягиватьПоГоризонтали   = Истина;
	ЭлементГруппаВопроса.РастягиватьПоВертикали     = Ложь;
		
	Если СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Булево Тогда
		
		ЭлементГруппаВопросаБулево = Форма.Элементы.Добавить(
			ИмяВопроса + "_Группа_Булево" ,Тип("ГруппаФормы"),ЭлементГруппаВопроса);
		ЭлементГруппаВопросаБулево.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
		ЭлементГруппаВопросаБулево.ОтображатьЗаголовок        = Ложь;
		ЭлементГруппаВопросаБулево.Отображение                = ОтображениеОбычнойГруппы.Нет;
		ЭлементГруппаВопросаБулево.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		ЭлементГруппаВопросаБулево.РастягиватьПоГоризонтали   = Истина;
		ЭлементГруппаВопросаБулево.РастягиватьПоВертикали     = Ложь;
		
	КонецЕсли;
		    
	Если СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Булево Тогда
		Элемент = Форма.Элементы.Добавить(
			ИмяВопроса + "_Формулировка", Тип("ДекорацияФормы"), ЭлементГруппаВопросаБулево);
	Иначе
		Элемент = Форма.Элементы.Добавить(
			ИмяВопроса + "_Формулировка", Тип("ДекорацияФормы"), ЭлементГруппаВопроса); // РасширениеДекорацииФормыДляНадписи
	КонецЕсли;
	Элемент.Вид                      = ВидДекорацииФормы.Надпись;
	Элемент.ВертикальноеПоложение    = ВертикальноеПоложениеЭлемента.Верх;
	Элемент.Заголовок                = ПолныйКодНаименование(СтрокаТаблицы);
	Элемент.АвтоМаксимальнаяШирина   = Ложь;
	Элемент.МаксимальнаяШирина       = 100;
	Элемент.РастягиватьПоГоризонтали = Ложь;
	Элемент.РастягиватьПоВертикали   = Ложь;
	Элемент.Подсказка                = СтрокаТаблицы.Подсказка;
	
	
	Если Не СтрокаТаблицы.Обязательный Тогда 
		
		ОписаниеТипа = Новый ОписаниеТипов("Булево");					
		
		Элемент = Форма.Элементы.Добавить(ИмяВопроса + "_ИсключитьВопрос",Тип("ПолеФормы"),ЭлементГруппаВопроса);
		Элемент.Вид = ВидПоляФормы.ПолеФлажка;
		Элемент.ВидФлажка = ВидФлажка.Выключатель;
		Элемент.ОграничениеТипа    = ОписаниеТипа;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Элемент.ПутьКДанным        = ПутьКИсключению;		
		Элемент.УстановитьДействие("ПриИзменении", "ИсключениеВопросаПриИзменении");
			
	КонецЕсли;	
	
	
	Если СтрокаТаблицы.СпособОтображенияПодсказки = Перечисления.СпособыОтображенияПодсказок.ЗнакВопросаСправа Тогда
		Элемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элемент.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	КонецЕсли;
	
	ЭлементГруппаВопросКомментарий = Форма.Элементы.Добавить(ИмяВопроса + "_Группа_Вопрос_Комментарий",Тип("ГруппаФормы"),ЭлементГруппаВопроса);
	ЭлементГруппаВопросКомментарий.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементГруппаВопросКомментарий.Отображение         = ОтображениеОбычнойГруппы.Нет;
	ЭлементГруппаВопросКомментарий.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ЭлементГруппаВопросКомментарий.ОтображатьЗаголовок = Ложь;
	
	Если СтрокаТаблицы.ТипВопроса = Перечисления.ТипыВопросовШаблонаАнкеты.Табличный Тогда
		
		//ДобавитьЭлементыТабличныйВопрос(СтрокаТаблицы,ЭлементГруппаВопроса,Форма);
				
	ИначеЕсли СтрокаТаблицы.ТипВопроса = Перечисления.ТипыВопросовШаблонаАнкеты.Комплексный Тогда
		
		//ДобавитьЭлементыКомплексныйВопрос(СтрокаТаблицы,ЭлементГруппаВопроса,Форма);
		
	Иначе
		
		Если СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Строка Тогда
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                        = ВидПоляФормы.ПолеВвода;
			Элемент.ПоложениеЗаголовка         = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.АвтоОтметкаНезаполненного  = СтрокаТаблицы.Обязательный;
			Элемент.ПутьКДанным                = ПутьКДанным;
			Элемент.АвтоМаксимальнаяШирина     = Ложь;
			Элемент.РастягиватьПоГоризонтали   = Ложь;
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Текст Тогда
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                       = ВидПоляФормы.ПолеВвода;
			Элемент.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.АвтоОтметкаНезаполненного = СтрокаТаблицы.Обязательный;
			Элемент.ПутьКДанным               = ПутьКДанным;
			Элемент.РастягиватьПоВертикали    = Ложь;
			Элемент.АвтоМаксимальнаяШирина    = Ложь;
			Анкетирование.УстановитьПараметрыЭлементаЯчейкаТекст(Элемент); 
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Булево Тогда  //
			
			ОписаниеТипа = Новый ОписаниеТипов("Булево");					
						
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"), ЭлементГруппаВопросаБулево);
			Элемент.Вид = ВидПоляФормы.ПолеВвода;			
			Элемент.ОграничениеТипа    = ОписаниеТипа;
			Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.ПутьКДанным        = ПутьКДанным;
			
			Элемент.РастягиватьПоГоризонтали = Ложь;
						
			СтрОтвет = ОтветыНаВопросы.НайтиПоИдентификатору(ИД);
			СтрОтвет.Ответ = Элемент.ОграничениеТипа.ПривестиЗначение(СтрОтвет.Ответ);			
						
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Дата Тогда
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                       = ВидПоляФормы.ПолеВвода;
			Элемент.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.АвтоОтметкаНезаполненного = СтрокаТаблицы.Обязательный;
			Элемент.ПутьКДанным               = ПутьКДанным;
			Элемент.АвтоМаксимальнаяШирина    = Ложь;
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.Число Тогда //
			
			ОписаниеТипа = ОбщегоНазначения.ОписаниеТипаЧисло(СтрокаТаблицы.Длина, СтрокаТаблицы.Точность, ДопустимыйЗнак.Неотрицательный);		
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                       = ВидПоляФормы.ПолеВвода;
			Элемент.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.АвтоОтметкаНезаполненного = СтрокаТаблицы.Обязательный;
			Элемент.МинимальноеЗначение       = ?(СтрокаТаблицы.МинимальноеЗначение = 0,Неопределено,СтрокаТаблицы.МинимальноеЗначение);
			Элемент.МаксимальноеЗначение      = ?(СтрокаТаблицы.МаксимальноеЗначение = 0,Неопределено,СтрокаТаблицы.МаксимальноеЗначение);
			Элемент.КнопкаВыбора              = Ложь;
			Элемент.ПутьКДанным               = ПутьКДанным;
			Элемент.АвтоМаксимальнаяШирина    = Ложь;
			Элемент.РастягиватьПоГоризонтали  = Ложь;
			Элемент.Ширина = 3;
			Элемент.ОграничениеТипа    = ОписаниеТипа;
			
			СтрОтвет = ОтветыНаВопросы.НайтиПоИдентификатору(ИД);
			СтрОтвет.Ответ = Элемент.ОграничениеТипа.ПривестиЗначение(СтрОтвет.Ответ);			
			
			Если СтрокаТаблицы.МинимальноеЗначение <> 0 ИЛИ СтрокаТаблицы.МаксимальноеЗначение <> 0 Тогда
				Элемент.КнопкаРегулирования = Истина;
				
				ТекстПодсказки = "";
				Если СтрокаТаблицы.МинимальноеЗначение <> 0 И СтрокаТаблицы.МаксимальноеЗначение <> 0 Тогда
					ТекстПодсказки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Допустим ввод значения от %1 до %2'"),
						СтрокаТаблицы.МинимальноеЗначение, СтрокаТаблицы.МаксимальноеЗначение);
				ИначеЕсли СтрокаТаблицы.МинимальноеЗначение = 0 И СтрокаТаблицы.МаксимальноеЗначение <> 0 Тогда
					ТекстПодсказки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Допустим ввод значения до %1'"), СтрокаТаблицы.МаксимальноеЗначение);
				Иначе
					ТекстПодсказки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Допустим ввод значения от %1'"), СтрокаТаблицы.МинимальноеЗначение);
				КонецЕсли;
				
				Элемент.Подсказка = ТекстПодсказки;
				
			КонецЕсли;
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.ЗначениеИнформационнойБазы Тогда
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                        = ВидПоляФормы.ПолеВвода;
			Элемент.ПоложениеЗаголовка         = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.АвтоОтметкаНезаполненного  = СтрокаТаблицы.Обязательный;
			Элемент.ПутьКДанным                = ПутьКДанным;
			Элемент.АвтоМаксимальнаяШирина     = Ложь;
			Элемент.РастягиватьПоГоризонтали   = Ложь;
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.ОдинВариантИз Тогда
			
			ВариантыОтветовНаВопрос = Анкетирование.ПолучитьВариантыОтветовНаВопрос(СтрокаТаблицы.ЭлементарныйВопрос,Форма);
			
			Элемент = Форма.Элементы.Добавить(ИмяВопроса,Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                     = ВидПоляФормы.ПолеПереключателя;
			Элемент.ПоложениеЗаголовка      = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элемент.ПутьКДанным             = ПутьКДанным;
			Элемент.КоличествоКолонок       = 1;
			Элемент.ВысотаЭлемента          = 1;
			Элемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
			
			Если СтрокаТаблицы.ВидПереключателя = Перечисления.ВидыПереключателяВАнкетах.Тумблер Тогда
				Элемент.ВидПереключателя = ВидПереключателя.Тумблер;
				Элемент.КоличествоКолонок = 0;
				Элемент.ОдинаковаяШиринаКолонок = Ложь;
			Иначе
				Элемент.ВидПереключателя = ВидПереключателя.Переключатель;
				Элемент.КоличествоКолонок = 1;
			КонецЕсли;
			
			Для каждого ВариантОтвета Из ВариантыОтветовНаВопрос Цикл
				СписокВыбораЭлемента = Элемент.СписокВыбора; // СписокЗначений
				СписокВыбораЭлемента.Добавить(ВариантОтвета.Ответ,ВариантОтвета.Представление);
			КонецЦикла;
			
		ИначеЕсли СтрокаТаблицы.ТипОтвета = Перечисления.ТипыОтветовНаВопрос.НесколькоВариантовИз Тогда
			
			ВариантыОтветовНаВопрос = Анкетирование.ПолучитьВариантыОтветовНаВопрос(СтрокаТаблицы.ЭлементарныйВопрос,Форма);
			Счетчик = 0;
			
			ЭлементГруппаВариантыОтветов = Форма.Элементы.Добавить(ИмяВопроса + "_Группа_Варианты",Тип("ГруппаФормы"),ЭлементГруппаВопросКомментарий);
			
			ЭлементГруппаВариантыОтветов.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
			ЭлементГруппаВариантыОтветов.Отображение         = ОтображениеОбычнойГруппы.Нет;
			ЭлементГруппаВариантыОтветов.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			ЭлементГруппаВариантыОтветов.ОтображатьЗаголовок = Ложь;
			
			Для каждого ВариантОтвета Из ВариантыОтветовНаВопрос Цикл
				
				Счетчик = Счетчик + 1;
				
				ЭлементГруппаВариантОтвета = Форма.Элементы.Добавить(ИмяВопроса + "_Группа_ВариантОтвета_" + XMLСтрока(Счетчик),
					Тип("ГруппаФормы"), ЭлементГруппаВариантыОтветов);
				
				ЭлементГруппаВариантОтвета.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
				ЭлементГруппаВариантОтвета.Отображение                = ОтображениеОбычнойГруппы.Нет;
				ЭлементГруппаВариантОтвета.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
				ЭлементГруппаВариантОтвета.ОтображатьЗаголовок        = Ложь;
				ЭлементГруппаВариантОтвета.РастягиватьПоГоризонтали   = Истина;
				
				ИмяРеквизитаВопроса = ИмяВопроса + "_Реквизит_" + Счетчик;
				Элемент = Форма.Элементы.Добавить(ИмяРеквизитаВопроса,Тип("ПолеФормы"),ЭлементГруппаВариантОтвета);
				
				Элемент.Вид                = ВидПоляФормы.ПолеФлажка;
				Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				Элемент.ПутьКДанным        = ПутьКДанным;
				Элемент.ВысотаЗаголовка    = 1;
				
				Если ВариантОтвета.ТребуетОткрытогоОтвета Тогда
					ИмяРеквизитаКомментария = ИмяВопроса + "_Комментарий_" + Счетчик;
					Элемент = Форма.Элементы.Добавить(ИмяРеквизитаКомментария,Тип("ПолеФормы"),ЭлементГруппаВариантОтвета);
					Элемент.Вид 		= ВидПоляФормы.ПолеФлажка;
					Элемент.ПутьКДанным	= ПутьКДанным;
					Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Комментарии к вопросам
		Если (СтрокаТаблицы.ТипОтвета <> Перечисления.ТипыОтветовНаВопрос.НесколькоВариантовИз) И (СтрокаТаблицы.ТребуетсяКомментарий) Тогда
			
			Элемент                        = Форма.Элементы.Добавить(ИмяВопроса + "_Комментарий",Тип("ПолеФормы"),ЭлементГруппаВопросКомментарий);
			Элемент.Вид                    = ВидПоляФормы.ПолеВвода;
			Элемент.ПутьКДанным            = ПутьККомментарий;
			Элемент.АвтоМаксимальнаяШирина = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолныйКодНаименование(СтрокаТаблицы)

	Возврат ?(СтрокаТаблицы.ТипСтроки = "Раздел","Раздел ","") + СтрокаТаблицы.ПолныйКод + " "
		+ ?(СтрокаТаблицы.ТипСтроки="Раздел", СтрокаТаблицы.Наименование, СтрокаТаблицы.Формулировка);

КонецФункции

&НаСервере
Процедура ЗаполнитьСохраненныеОтветыНаВопросы()
	
	Если Объект.Состав.Количество() = 0 ИЛИ ОтветыНаВопросы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	МассивИсключений = Новый Массив;
	
	Для Каждого Стр Из Объект.Состав Цикл 
		
		СтруктураОтбора = Новый Структура("Вопрос,ЭлементарныйВопрос");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Стр);		
		МассивВопросов = ОтветыНаВопросы.НайтиСтроки(СтруктураОтбора);
		
		Если МассивВопросов.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		СтрОтвет = МассивВопросов[0];
		ЗаполнитьЗначенияСвойств(СтрОтвет, Стр, "Ответ, ОткрытыйОтвет, НомерЯчейки");
		
		Если СтрОтвет.НомерЯчейки <> 0 Тогда 
			СтрОтвет.ИсключитьВопрос = Истина;
			
			МассивИсключений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										"Вопрос_%1_ИсключитьВопрос",
										СтрЗаменить(Строка(СтрОтвет.КлючСтроки), "-", "_")));
										
		КонецЕсли;		
	КонецЦикла;
	
	Если МассивИсключений.Количество() > 0 Тогда 
		СписокЭлементовИсключений.ЗагрузитьЗначения(МассивИсключений);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область РаботаСБуферомОбмена


#КонецОбласти



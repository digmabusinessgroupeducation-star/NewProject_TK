
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Автор =  Пользователи.ТекущийПользователь();
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИспользоватьДопАнкету = Ложь;
	
	Если ТекущаяДата() >= Дата("20240901") Тогда 
		ШаблонАнкеты = Справочники.ШаблоныАнкет.НайтиПоКоду("000000009");
	КонецЕсли;
	
	ШаблонАнкетыДоп = Справочники.ШаблоныАнкет.НайтиПоКоду("000000010"); //Акционный товар
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ИспользоватьДопАнкету Тогда 
		ПроверяемыеРеквизиты.Добавить("ШаблонАнкетыДоп");
	КонецЕсли;
	
	Если Не Отказ И ИспользоватьДопАнкету И ШаблонАнкеты = ШаблонАнкетыДоп Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Шаблон ""%1"" уже используется в роли основного'; uk = 'Шаблон ""%1"" вже використовується в якості головного'"),
				СокрЛП(Строка(ШаблонАнкетыДоп))),,
				"ШаблонАнкетыДоп",,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура РеспондентПриИзмененииНаСервере()
	
	Если ТипЗнч(Респондент) = Тип("СправочникСсылка.ТорговыеПлощадки") Тогда 
		ПодразделениеКомпании = Респондент.ПодразделениеКомпании;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеспондентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Респондент) Тогда 
		РеспондентПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДопАнкетуПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьАнкетирование(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;		
	КонецЕсли;
	
	Комментарий = "Анкета создана вручную";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Дата", ТекущаяДата());
	ПараметрыФормы.Вставить("Респондент", Респондент);
	ПараметрыФормы.Вставить("ПодразделениеКомпании", ПодразделениеКомпании); 
	ПараметрыФормы.Вставить("Интервьюер", Пользователь);
	ПараметрыФормы.Вставить("ШаблонАнкеты", ШаблонАнкеты);
	ПараметрыФормы.Вставить("РежимАнкетирования", ПредопределенноеЗначение("Перечисление.РежимыАнкетирования.Интервью"));
	ПараметрыФормы.Вставить("Комментарий", Комментарий);
	//ПараметрыФормы.Вставить("Реализатор", чКассир);
	
	Если ИспользоватьДопАнкету Тогда 	
		ПараметрыФормы.Вставить("ШаблонАнкетыДоп", ШаблонАнкетыДоп);
	КонецЕсли;
	
	Если ИспользоватьДопАнкету Тогда 
		ОписаниеЗакрытия = Новый ОписаниеОповещения("РезультатЗакрытияФормыАнкеты", ЭтаФорма, Неопределено);		
		ОткрытьФорму("Обработка.ТК_АнкетированиеПоЧекам.Форма.ФормаАнкеты", ПараметрыФормы, ЭтаФорма,,,, ОписаниеЗакрытия);
	Иначе
		ОткрытьФорму("Документ.Анкета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РезультатЗакрытияФормыАнкеты(мРезультаты, ДопПараметры) Экспорт 
	
	Если ТипЗнч(мРезультаты) <> Тип("Массив") Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Результат Из мРезультаты Цикл 
		Если Результат.Свойство("Сообщение") Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю(
			Результат.Сообщение,
			?(Результат.Свойство("Ссылка"), Результат.Ссылка, Неопределено));
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	
	Элементы.ШаблонАнкетыДоп.Видимость = ИспользоватьДопАнкету;
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции


#КонецОбласти

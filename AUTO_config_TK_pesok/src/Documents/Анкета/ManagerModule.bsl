///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗагрузитьАнкетуИзСтартСтоп(Данные)	Экспорт
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(Данные);
	ОтветРезультат = ПрочитатьJSON(Чтение);
	
	Дата = ТекущаяДатаСеанса();
	
	ОбъектАнкета = Документы.Анкета.СоздатьДокумент();
	
	ОбъектАнкета.Дата = Дата;
	ОбъектАнкета.ДатаРедактирования = Дата;
	ТорговаяПлощадка = Справочники.ТорговыеПлощадки.ПолучитьСсылку(Новый УникальныйИдентификатор(ОтветРезультат.GuidShop));
	ОбъектАнкета.Респондент = ТорговаяПлощадка;
	ОбъектАнкета.ПодразделениеКомпании = ТорговаяПлощадка.ПодразделениеКомпании;
	ОбъектАнкета.ШаблонАнкеты = Справочники.ШаблоныАнкет.ПолучитьСсылку(Новый УникальныйИдентификатор(ОтветРезультат.GuidCheckList));
	Пользователь = Справочники.ФизическиеЛица.ПользовательПоКодуФизЛица(ОтветРезультат.INN);
	ОбъектАнкета.Интервьюер = Пользователь;
	ОбъектАнкета.РежимАнкетирования = Перечисления.РежимыАнкетирования.Интервью;
	ОбъектАнкета.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Анкета загружена из Старт/Стоп%1'; uk = 'Анкета завантажена зі Старт/Стоп%1'"),
	?(ЗначениеЗаполнено(Пользователь),""," # "+ОтветРезультат.INN));
	
	МассивВопросов = ОтветРезультат.CheckList;
	
	Для Каждого стрВопрос Из МассивВопросов Цикл 
		НоваяСтрока = ОбъектАнкета.Состав.Добавить();
		Вопрос = Справочники.ВопросыШаблонаАнкеты.ПолучитьСсылку(Новый УникальныйИдентификатор(стрВопрос.Guid));
		НоваяСтрока.Вопрос				= Вопрос;
		НоваяСтрока.ЭлементарныйВопрос	= Вопрос.ЭлементарныйВопрос;
		НоваяСтрока.Ответ				= стрВопрос.Response;
		НоваяСтрока.ОткрытыйОтвет		= стрВопрос.Comment;
	КонецЦикла;
	
	Ответ = Новый Структура("BceOK, Description");
	
	Попытка
		ОбъектАнкета.Записать(РежимЗаписиДокумента.Проведение);
		Ответ.BceOK	= Истина;
		Ответ.Description = Строка(ОбъектАнкета.Ссылка);
	Исключение						
		Ответ.BceOK	= Ложь;
		Ответ.Description = ОписаниеОшибки();
	КонецПопытки;
	
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	ЗаписатьJSON(ЗаписьJSON, Ответ); 
	ОтветJSON = ЗаписьJSON.Закрыть();
	
	Возврат ОтветJSON;
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив Из Строка
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Респондент");
	Результат.Добавить("ДатаРедактирования");
	Результат.Добавить("Комментарий");
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов


// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(Интервьюер)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления.

// Регистрирует на плане обмена ОбновлениеИнформационнойБазы объекты,
// которые необходимо обновить на новую версию.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Анкета.Ссылка
	|ИЗ
	|	Документ.Анкета КАК Анкета
	|ГДЕ
	|	Анкета.РежимАнкетирования = &ПустаяСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Анкета.Дата УБЫВ";
	Запрос.Параметры.Вставить("ПустаяСсылка", Перечисления.РежимыАнкетирования.ПустаяСсылка());
	
	Результат = Запрос.Выполнить().Выгрузить();
	МассивСсылок = Результат.ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
КонецПроцедуры

// Заполнить значение нового реквизита РежимАнкетирования у документа Анкета.
// 
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, "Документ.Анкета");
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			ЗаполнитьРеквизитРежимАнкетирования(Выборка);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			// Если не удалось обработать анкету, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать анкету: %1 по причине:
			|%2'"), 
			Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.Документы.Анкета, Выборка.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, "Документ.Анкета");
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Процедуре ЗаполнитьРеквизитРежимАнкетирования не удалось обработать некоторые анкеты (пропущены): %1'"), 
		ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
		Метаданные.Документы.Анкета,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Процедура ЗаполнитьРеквизитРежимАнкетирования обработала очередную порцию анкет: %1'"),
		ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет значение нового реквизита РежимАнкетирования у переданного документа.
//
Процедура ЗаполнитьРеквизитРежимАнкетирования(Выборка)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.Анкета");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		Если ДокументОбъект.РежимАнкетирования <> Перечисления.РежимыАнкетирования.ПустаяСсылка() Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		// Обработка объекта.
		ДокументОбъект.РежимАнкетирования = Перечисления.РежимыАнкетирования.Анкета;
		
		// Запись обработанного объекта.
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий	

Процедура ПриКопировании(ОбъектКопирования)
	ДокументОснование = "";	//	Тихий
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения,ДанныеЗаполнения.Дата);
	ИначеЕсли ТипДанныхЗаполнения = Тип("Массив") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения,ТекущаяДатаСеанса());
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый"		, ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи"	, РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.РезервированиеЗаказовПокупателя.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаказыСервер.ОтразитьРезервыТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	
	//Данченко--- Переделать, так как резерв теперь может быть на несколько заказов, организацию проверять из заказа!!!
	//// Выполнить контроль разделения Номенклатуры по Организациям Vov41k 08.01.2023
	//Если ЗначениеНастроекПовтИсп.ДоступноРазделениеТоваровПоОрганизациям(ТорговаяПлощадка) Тогда 
	//	ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ);
	//КонецЕсли;		

КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументНаОснованииЗаказаПокупателя(Знач ДанныеЗаказа,парамДата)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбъединениеРезервов = ТипЗнч(ДанныеЗаказа) = Тип("Массив");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.РезервированиеПокупателя) КАК ХозОперация,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.РезервироватьВОсновныхЕдиницах КАК ОсновныеЕдиницы
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка В(&ДокументОснование)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмеренияИзЗаказа,
	               |	ЗаказПокупателяТовары.Коэффициент КАК КоэффициентИзЗаказа,
	               |	ЗаказПокупателяТовары.Количество КАК КоличествоИзЗаказа,
	               |	ЗаказПокупателяТовары.Ссылка.СкладКомпании КАК МестоРазмещения,
	               |	ЗаказПокупателяТовары.Ссылка КАК ЗаказПокупателя,
	               |	ВЫБОР
	               |		КОГДА ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВариантОкругления = ЗНАЧЕНИЕ(Перечисление.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону)
	               |			ТОГДА ЗаказПокупателяТовары.ЕдиницаИзмерения
	               |		ИНАЧЕ ЕСТЬNULL(ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения, ЗаказПокупателяТовары.ЕдиницаИзмерения)
	               |	КОНЕЦ КАК ЕдиницаИзмерения,
	               |	ВЫБОР
	               |		КОГДА ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВариантОкругления = ЗНАЧЕНИЕ(Перечисление.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону)
	               |			ТОГДА ЗаказПокупателяТовары.Коэффициент
	               |		ИНАЧЕ ЕСТЬNULL(ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.Коэффициент, ЗаказПокупателяТовары.Коэффициент)
	               |	КОНЕЦ КАК Коэффициент,
	               |	ВЫБОР
	               |		КОГДА ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВариантОкругления = ЗНАЧЕНИЕ(Перечисление.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону)
	               |			ТОГДА ЗаказПокупателяТовары.Количество
	               |		ИНАЧЕ ВЫРАЗИТЬ(ЗаказПокупателяТовары.Количество * (ЗаказПокупателяТовары.Коэффициент / ЕСТЬNULL(ТК_ЕдиницыИзмеренияЗаказов.ЕдиницаИзмерения.Коэффициент, ЗаказПокупателяТовары.Коэффициент)) КАК ЧИСЛО(15, 0))
	               |	КОНЕЦ КАК Количество,
	               |	ЗаказПокупателяТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаУпаковки,
	               |	ЗаказПокупателяТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент КАК КоэффициентУпаковки,
	               |	ЗаказПокупателяТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиница,
	               |	ЗаказПокупателяТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК ОсновнаяЕдиницаКоффициент
	               |ИЗ
	               |	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ЕдиницыИзмеренияЗаказов КАК ТК_ЕдиницыИзмеренияЗаказов
	               |		ПО ЗаказПокупателяТовары.Номенклатура = ТК_ЕдиницыИзмеренияЗаказов.Номенклатура
	               |			И ЗаказПокупателяТовары.Ссылка.ПодразделениеКомпании = ТК_ЕдиницыИзмеренияЗаказов.Подразделение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ОптимальноеКоличествоНоменклатуры.СрезПоследних(&Дата, ) КАК ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних
	               |		ПО ЗаказПокупателяТовары.Ссылка.ТорговаяПлощадкаПолучатель = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ТорговаяПлощадка
	               |			И ЗаказПокупателяТовары.Номенклатура = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Номенклатура
	               |ГДЕ
	               |	ЗаказПокупателяТовары.Ссылка В(&ДокументОснование)";
	
	Запрос.УстановитьПараметр("Дата", парамДата);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаказа);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;
	РаспределениеОтгрузки.Очистить();

	Если ОбъединениеРезервов Тогда
		ДокументОснование = Документы.ЗаказПокупателя.ПустаяСсылка();
		Для Каждого тЗаказ Из ДанныеЗаказа Цикл 
			нСтрокаЗаказ =  РаспределениеОтгрузки.Добавить();
			нСтрокаЗаказ.ЗаказПокупателя = тЗаказ;
		КонецЦикла;
	КонецЕсли;
	
	Товары.Очистить();
	Если ТаблицаТоваров.Количество() Тогда 
		Для Каждого СтрЗаказ Из ТаблицаТоваров Цикл 
			нСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(нСтрока,СтрЗаказ);
			
			Если ВыборкаШапка.ОсновныеЕдиницы Тогда
				
				Если ЗначениеЗаполнено(СтрЗаказ.ОсновнаяЕдиница) Тогда
					
					БазКолчество  = СтрЗаказ.КоличествоИзЗаказа * СтрЗаказ.КоэффициентИзЗаказа;
					ОснКоличество =  Окр(БазКолчество /?(СтрЗаказ.ОсновнаяЕдиницаКоффициент=0,1,СтрЗаказ.ОсновнаяЕдиницаКоффициент),3);
					нСтрока.Количество 		 = ОснКоличество;
					нСтрока.Коэффициент 	 = СтрЗаказ.ОсновнаяЕдиницаКоффициент;
					нСтрока.ЕдиницаИзмерения = СтрЗаказ.ОсновнаяЕдиница;
					
				Иначе
					нСтрока.Количество 		 = СтрЗаказ.КоличествоИзЗаказа;
					нСтрока.Коэффициент 	 = СтрЗаказ.КоэффициентИзЗаказа;
					нСтрока.ЕдиницаИзмерения = СтрЗаказ.ЕдиницаИзмеренияИзЗаказа;
					
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(СтрЗаказ.ЕдиницаУпаковки) Тогда
				
				Если СтрЗаказ.ЕдиницаИзмерения <>  СтрЗаказ.ЕдиницаУпаковки И   СтрЗаказ.Количество >= СтрЗаказ.КоэффициентУпаковки Тогда
					
					нКоличество =  Окр(СтрЗаказ.Количество *(СтрЗаказ.Коэффициент/СтрЗаказ.КоэффициентУпаковки),3);
					Если Цел(нКоличество) = нКоличество Тогда
						нСтрока.Количество 		 = нКоличество;
						нСтрока.Коэффициент 	 = СтрЗаказ.КоэффициентУпаковки;
						нСтрока.ЕдиницаИзмерения = СтрЗаказ.ЕдиницаУпаковки;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			
		КонецЦикла;
		
		Товары.Свернуть("Номенклатура,ХарактеристикаНоменклатуры,ЕдиницаИзмерения,Коэффициент,МестоРазмещения,ЗаказПокупателя","Количество");
		
	КонецЕсли;
	
	
	//	Товары.Загрузить(ТаблицаТоваров);
	
	Автор = Пользователи.ТекущийПользователь();
	
	//Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(ЭтотОбъект);
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор 				  = Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	СкладКомпании 		  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
	
КонецПроцедуры	


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ)
	
	СписокНоменклатуры = Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	
	СоответствиеНоменклатуры = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(
	Организация,
	СписокНоменклатуры,
	ТорговаяПлощадка);
	
	Для Каждого Стр Из СоответствиеНоменклатуры Цикл 
		Если Стр.Значение.Организация <> Организация Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номенклатура: %1 закреплена за организацией ""%2""'; uk = 'Номенклатура: %1 закріплена за організацією ""%2""'"),
			Стр.Ключ,
			Стр.Значение.Организация),
			ЭтотОбъект,
			"Товары");
			
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#КонецЕсли

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;	
	ТекстЗапросаОптимальноеКоличествоНоменклатуры(Запрос, ТекстыЗапроса, Регистры);
			
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_ОптимальноеКоличествоНоменклатуры.Дата КАК Период,
	               |	ТК_ОптимальноеКоличествоНоменклатуры.Ссылка КАК Ссылка,
	               |	ТК_ОптимальноеКоличествоНоменклатуры.ХозОперация КАК ХозОперация,
	               |	ТК_ОптимальноеКоличествоНоменклатуры.ПодразделениеКомпании КАК ПодразделениеКомпании
	               |ИЗ
	               |	Документ.ТК_ОптимальноеКоличествоНоменклатуры КАК ТК_ОптимальноеКоличествоНоменклатуры
	               |ГДЕ
	               |	ТК_ОптимальноеКоличествоНоменклатуры.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка КАК Ссылка,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.НомерСтроки КАК НомерСтроки,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.Номенклатура КАК Номенклатура,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.МодельЗаказа КАК МодельЗаказа,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.ВариантОкругления КАК ВариантОкругления,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.МинимальныйОстаток КАК МинимальныйОстаток,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.МаксимальныйЗапас КАК МаксимальныйЗапас,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.ПоДоговоруОстаток КАК ПоДоговоруОстаток,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.ВитринаОстаток КАК ВитринаОстаток,
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.КвантПоставки КАК КвантПоставки
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_ОптимальноеКоличествоНоменклатуры.Товары КАК ТК_ОптимальноеКоличествоНоменклатурыТовары
	               |ГДЕ
	               |	ТК_ОптимальноеКоличествоНоменклатурыТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОптимальноеКоличествоНоменклатуры(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТК_ОптимальноеКоличествоНоменклатуры";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ВтТаблицаТовары.Ссылка КАК Ссылка,
	|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВтТаблицаТовары.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ВтТаблицаТовары.МодельЗаказа КАК МодельЗаказа,
	|	ВтТаблицаТовары.ВариантОкругления КАК ВариантОкругления,
	|	ВтТаблицаТовары.МинимальныйОстаток КАК МинимальныйОстаток,
	|	ВтТаблицаТовары.МаксимальныйЗапас КАК МаксимальныйЗапас,
	|	ВтТаблицаТовары.ПоДоговоруОстаток КАК ПоДоговоруОстаток,
	|	ВтТаблицаТовары.ВитринаОстаток КАК ВитринаОстаток,
	|	ВтТаблицаТовары.КвантПоставки КАК КвантПоставки
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти


#Область ПрограммныйИнтерфейс	


// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти	

#КонецЕсли

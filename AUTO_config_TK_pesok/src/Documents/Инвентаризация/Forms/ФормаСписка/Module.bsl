
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Элементы.ОтборХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("Инвентаризация"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	//Заполним отбор списка
	ПараметрыОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
		ПараметрыОтбора.Вставить("ПериодВыборки", ПериодВыборки);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборПодразделениеКомпании) Тогда 
		ПараметрыОтбора.Вставить("ПодразделениеКомпании", ОтборПодразделениеКомпании);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда 
		ПараметрыОтбора.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборАвтор) Тогда 
		ПараметрыОтбора.Вставить("Автор", ОтборАвтор);
	КонецЕсли;
	
	УстановитьОтборСписка(Список, ПараметрыОтбора);		
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыборкиПриИзменении(Элемент)

	УстановитьОтборСписка(Список, Новый Структура("ПериодВыборки", ПериодВыборки));	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеКомпанииПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ПодразделениеКомпании", ОтборПодразделениеКомпании));	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("Автор", ОтборАвтор));	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("Организация", ОтборОрганизация));			
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияПриИзменении(Элемент)

	УстановитьОтборСписка(Список, Новый Структура("ХозОперация", ОтборХозОперация));				
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПересчитатьКоличествоКнижн(Команда)
	
	МассивИнвентаризаций = Новый Массив;
	ВыбранныеСтроки = Элементы.Список.ВыделенныеСтроки;		
	Для Ит = 0 По ВыбранныеСтроки.ВГраница() Цикл 
		МассивИнвентаризаций.Добавить(ВыбранныеСтроки.Получить(Ит));	
	КонецЦикла;
	
	Если МассивИнвентаризаций.Количество() = 0 Тогда 
		Возврат;          
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	//ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Неопределено;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	//ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "e1cib/app/Обработка.ТК_ОбменOpenStore";
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Интервал = 2;	
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Перезаполнение книжного количества'; uk = 'Перезаповнення книжної кількості'"); 		
	
	ДлительнаяОперация = НачатьВыполнениеОперации(МассивИнвентаризаций, УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияДлительнойОперации", ЭтотОбъект);		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);	

КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьВыполнениеОперации(Знач МассивИнвентаризаций, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);		
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Документы.Инвентаризация.ПересчитатьКоличествоКнижнИнвентаризаций", МассивИнвентаризаций);
			
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Операция, ДопПараметры) Экспорт
	
	Картинка = БиблиотекаКартинок.Информация32;
	
	Если Операция = Неопределено Тогда 		
		ОповещениеТекст = НСтр("ru = 'Отмена операции'; uk = 'Відміна операції'");	
		ОповещениеПояснение = НСтр("ru = 'Операция отменена'; uk = 'Операція відмінена'");
		Картинка = БиблиотекаКартинок.Ошибка32;		
	Иначе		
		СтатусОперации = Операция.Статус;
		
		Если Операция.Свойство("АдресРезультата") И Не ПустаяСтрока(Операция.АдресРезультата) Тогда 						
			Результат = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);				
		КонецЕсли;		
		
		Если СтатусОперации = "Выполнено" Тогда
			Картинка = БиблиотекаКартинок.Информация32;	
			ОповещениеПояснение = НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'");
			Если ТипЗнч(Результат) = Тип("Структура") Тогда 
				ОповещениеТекст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Обработано %1 из %2'; uk = 'Оброблено %1 із %2'"),
										Результат.Успешно,
										Результат.Всего);
	
			КонецЕсли;
		ИначеЕсли СтатусОперации = "Ошибка" Тогда 				
			ОповещениеТекст = НСтр("ru = 'Ошибка выполнения операции'; uk = 'Помилка виконання операції'");	
			ОповещениеПояснение = ""; 
			Картинка = БиблиотекаКартинок.Предупреждение32;	
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(Операция.ПодробноеПредставлениеОшибки);
		Иначе
			ОповещениеТекст = НСтр("ru = 'Операция выполнена'; uk = 'Операція виконана'");
			ОповещениеПояснение = "";
		КонецЕсли;			
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(ОповещениеТекст, , ОповещениеПояснение, Картинка);
	   
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ПериодВыборки = Неопределено;
	Если ПараметрыОтбора.Свойство("ПериодВыборки", ПериодВыборки) Тогда 
		
		ЭлементыОтбора = Список.Отбор.Элементы;
		
		ЭлементОтбораДанных = Неопределено;
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			Если ЭлементОтбора.Представление = "ОтборПериод" Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
			
			Если ЭлементОтбораДанных = Неопределено Тогда
				ГруппаОтборПериод = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтборПериод.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли; 
				ГруппаОтборПериод.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
				ГруппаОтборПериод.Использование = Истина;
				ГруппаОтборПериод.Представление = "ОтборПериод";
			Иначе
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Истина;
			КонецЕсли;	
			
			ГруппаДатаСортировки = ГруппаОтборПериод.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаДатаСортировки.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ; 
			ГруппаДатаСортировки.Использование = Истина;
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаНачала) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаНачала;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаОкончания;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;
			
			Если ГруппаДатаСортировки.Элементы.Количество() = 0 Тогда 
				ГруппаОтборПериод.Элементы.Удалить(ГруппаДатаСортировки);
			КонецЕсли;
			
		Иначе
			
			Если ЭлементОтбораДанных <> Неопределено Тогда
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Для Каждого текОтбор Из ПараметрыОтбора Цикл 		
		ИмяРеквизита = текОтбор.Ключ;
		Значение = текОтбор.Значение;
		
		Если текОтбор.Ключ = "ПериодВыборки" Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, 
			ИмяРеквизита, 
			Значение, 
			ВидСравненияКомпоновкиДанных.Равно,
			, 
			ЗначениеЗаполнено(Значение));
		
	КонецЦикла;
			
КонецПроцедуры


#КонецОбласти
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровИнвентаризация(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуСписанияОприходованияТоваров(Запрос, ДополнительныеСвойства);
		
	Иначе
		
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
		
		Запрос.Текст = "";
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровИнвентаризация(Запрос, ДополнительныеСвойства);	
		ПартионныйУчет.ПодготовитьТаблицуСписанияОприходованияТоваров(Запрос, ДополнительныеСвойства);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Инвентаризация.Дата КАК Период,
	|	Инвентаризация.Ссылка КАК Ссылка,
	|	Инвентаризация.ХозОперация КАК ХозОперация,
	|	Инвентаризация.Организация КАК Организация,
	|	Инвентаризация.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Инвентаризация.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	Инвентаризация.СкладКомпании КАК СкладКомпании,
	|	Инвентаризация.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	|	Инвентаризация.ПодразделениеКомпании.ИспользоватьПротоколыСогласованныхЦен КАК ИспользоватьПротоколыЦен
	|ИЗ
	|	Документ.Инвентаризация КАК Инвентаризация
	|ГДЕ
	|	Инвентаризация.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании",Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("ИспользоватьПротоколыЦен",Реквизиты.ИспользоватьПротоколыЦен);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	|	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИнвентаризацияТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ИнвентаризацияТовары.Коэффициент КАК Коэффициент,
	|	ИнвентаризацияТовары.КоличествоФакт КАК КоличествоФакт,
	|	ИнвентаризацияТовары.КоличествоКнижн КАК КоличествоКнижн,
	|	ИнвентаризацияТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияТовары.КоличествоКнижн < 0
	|			ТОГДА ИнвентаризацияТовары.КоличествоФакт
	|		ИНАЧЕ ИнвентаризацияТовары.КоличествоФакт - ИнвентаризацияТовары.КоличествоКнижн
	|	КОНЕЦ КАК КоличествоПартии
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
	|ГДЕ
	|	ИнвентаризацияТовары.Ссылка = &Ссылка
	//  |	И ИнвентаризацияТовары.Количество <> 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&СкладКомпании КАК СкладКомпании,
	|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(-ВтТаблицаТовары.Количество) КАК Количество,
	|	&ХозОперация КАК ХозОперация
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|ГДЕ
	|	ВтТаблицаТовары.Количество < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.Номенклатура,
	|	ВтТаблицаТовары.ХарактеристикаНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&СкладКомпании,
	|	ВтТаблицаТовары.Номенклатура,
	|	ВтТаблицаТовары.ХарактеристикаНоменклатуры,
	|	СУММА(ВтТаблицаТовары.Количество),
	|	&ХозОперация
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|ГДЕ
	|	ВтТаблицаТовары.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.Номенклатура,
	|	ВтТаблицаТовары.ХарактеристикаНоменклатуры"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
	
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
	НоваяСтрока.Период   	  = Ссылка.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт инвентаризации розничный
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.Инвентаризация";
	КомандаПечати.Идентификатор = "АктИнвентаризацииРозничный";
	КомандаПечати.Представление = НСтр("ru = 'Акт инвентаризации розничный'; uk = 'Акт інвентаризації роздрібний'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	
	//// Инвентаризационная опись ОС
	//КомандаПечати = КомандыПечати.Добавить();
	//КомандаПечати.МенеджерПечати = "Документ.Инвентаризация";
	//КомандаПечати.Идентификатор = "ИнвентаризационнаяОпись572";
	//КомандаПечати.Представление = НСтр("ru='Инвентаризационная опись запасов';uk='Інвентаризаційний опис запасів'");
	////КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"ОписьИнвентаризация", Ложь) Тогда 
		// Инвентаризационная опись
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.Инвентаризация";
		КомандаПечати.Идентификатор = "ИнвентаризационнаяОпись";
		КомандаПечати.Представление = НСтр("ru = 'Инвентаризационная опись'; uk = 'Інвентаризаційний опис'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	КонецЕсли;	
	
	Если ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"ПустографкаИнвентаризация", Ложь) Тогда 
		// Пустографка
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.Инвентаризация";
		КомандаПечати.Идентификатор = "Пустографка";
		КомандаПечати.Представление = НСтр("ru = 'Пустографка'; uk = 'Пустографка'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КонецЕсли;
	
	//ТК_УправлениеПечатью.УстановитьУсловиеВидимостиКомандыПечати(КомандаПечати);
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


Функция НуженПересчетОстатков(ДокументСсылка)Экспорт
	
	СтатусВозврат = Истина;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст ="ВЫБРАТЬ
	|	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ИнвентаризацияТовары.КоличествоКнижн КАК КоличествоКнижн
	|ПОМЕСТИТЬ вт_Товары
	|ИЗ
	|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
	|ГДЕ
	|	ИнвентаризацияТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК Остаток
	|ПОМЕСТИТЬ вт_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|			&НаМомент,
	|			СкладКомпании = &СкладКомпании
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						вт_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						вт_Товары КАК вт_Товары)) КАК ОстаткиТоваровКомпанииОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Товары.Номенклатура КАК Номенклатура,
	|	вт_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	вт_Товары.КоличествоКнижн КАК КоличествоКнижн,
	|	вт_Остатки.Остаток КАК Остаток
	|ИЗ
	|	вт_Товары КАК вт_Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Остатки КАК вт_Остатки
	|		ПО вт_Товары.Номенклатура = вт_Остатки.Номенклатура
	|			И вт_Товары.ХарактеристикаНоменклатуры = вт_Остатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	вт_Товары.КоличествоКнижн <> ЕСТЬNULL(вт_Остатки.Остаток, 0)";
	
	
	
	
	Запрос.УстановитьПараметр("НаМомент", ДокументСсылка.МоментВремени());
	Запрос.УстановитьПараметр("СкладКомпании", ДокументСсылка.СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		СтатусВозврат = ложь;
	КонецЕсли;
	
	Возврат СтатусВозврат;
КонецФункции

Функция ПолучитьИтогПоИнвентаризации(ДокументСсылка,ТипЦен)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ИнвентаризацияТовары.КоличествоКнижн КАК ОстатокПоУчету,
	|	ИнвентаризацияТовары.КоличествоФакт КАК Фактически,
	|	ИнвентаризацияТовары.Количество КАК Разница
	|ПОМЕСТИТЬ ВТ_ТоварыИН
	|ИЗ
	|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
	|ГДЕ
	|	ИнвентаризацияТовары.Ссылка = &ДокументСсылка
	|	И ИнвентаризацияТовары.Количество <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|ПОМЕСТИТЬ втЦены
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&НаДату,
	|			ТипЦен = &ТипЦен
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ВТ_ТоварыИН.Номенклатура КАК Номенклатура
	|					ИЗ
	|						ВТ_ТоварыИН КАК ВТ_ТоварыИН)) КАК ЦеныСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ВТ_ТоварыИН.Разница * ЕСТЬNULL(втЦены.Цена, 0), 0)) КАК СуммаИтог
	|ИЗ
	|	ВТ_ТоварыИН КАК ВТ_ТоварыИН
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦены КАК втЦены
	|		ПО ВТ_ТоварыИН.Номенклатура = втЦены.Номенклатура
	|			И ВТ_ТоварыИН.Характеристика = втЦены.Характеристика";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("НаДату", ДокументСсылка.МоментВремени());
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	
	
	Рез = Запрос.Выполнить();
	
	Если Рез.Пустой() Тогда
		Возврат 0 ;
	КонецЕсли;
	
	
	Выборка = Рез.Выбрать();
	
	Выборка.Следующий();
	СуммаИтог = Выборка.СуммаИтог;
	Если СуммаИтог = Null Тогда
		СуммаИтог = 0;
	КонецЕсли;
	
	
	Возврат  СуммаИтог;
	
	
	
КонецФункции

//обмен OS
Функция СформироватьТаблицуИтоговПоИнвентаризации(ДокументСсылка, ДатаСрезаЦен, ТипЦенИтогов)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИнвентаризацияТовары.Номенклатура КАК НоменклатураСсылка,
	|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВЫБОР
	|			КОГДА ИнвентаризацияТовары.Номенклатура.ТипНоменклатуры.Весовой
	|				ТОГДА ИнвентаризацияТовары.КоличествоКнижн * 1000
	|			ИНАЧЕ ИнвентаризацияТовары.КоличествоКнижн
	|		КОНЕЦ) КАК ОстатокПоУчету,
	|	СУММА(ВЫБОР
	|			КОГДА ИнвентаризацияТовары.Номенклатура.ТипНоменклатуры.Весовой
	|				ТОГДА ИнвентаризацияТовары.КоличествоФакт * 1000
	|			ИНАЧЕ ИнвентаризацияТовары.КоличествоФакт
	|		КОНЕЦ) КАК Фактически,
	|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена
	|ИЗ
	|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&Дата, ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	|		ПО ИнвентаризацияТовары.Номенклатура = ЦеныСрезПоследних.Номенклатура
	|			И ИнвентаризацияТовары.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.Характеристика
	|ГДЕ
	|	ИнвентаризацияТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры,
	|	ИнвентаризацияТовары.Номенклатура,
	|	ИнвентаризацияТовары.ЕдиницаИзмерения,
	|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Дата", ДатаСрезаЦен);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦенИтогов);
	
	ТаблицаИтогов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаИтогов;
	
	
КонецФункции


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Акт инвентаризации розничный
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктИнвентаризацииРозничный") Тогда    
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"АктИнвентаризацииРозничный",
		НСтр("ru = 'Акт инвентаризации розничный'; uk = 'Акт інвентаризації роздрібний'"), 
		ПечатьАктИнвентаризацииРозничный(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.Инвентаризация.ПФ_MXL_АктИнвентаризацииРозничный");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвентаризационнаяОпись") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ИнвентаризационнаяОпись",
		НСтр("ru = 'Инвентаризационная опись'; uk = 'Інвентаризаційний опис'"), 
		ПечатьАктИнвентаризацииРозничный(МассивОбъектов, ОбъектыПечати, Истина),
		,
		"Документ.Инвентаризация.ПФ_MXL_АктИнвентаризацииРозничный");			
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Пустографка") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Пустографка",
		НСтр("ru = 'Пустографка'; uk = 'Пустографка'"), 
		ПечатьПустографка(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.Инвентаризация.ПФ_MXL_Пустографка");
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнвентаризационнаяОпись572") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИнвентаризационнаяОпись572", НСтр("ru='Инвентаризационная опись запасов';uk='Інвентаризаційний опис запасів'"), 
		ПечатьИнвентаризационнаяОпись572(МассивОбъектов, ОбъектыПечати, ПараметрыВывода,ПараметрыПечати), , "Документ.Инвентаризация.ПФ_MXL_UK_ИнвентаризационнаяОпись");
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьАктИнвентаризацииРозничный(МассивОбъектов, ОбъектыПечати, ИнвентаризационнаяОпись = Ложь)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Инвентаризация.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Инвентаризация.ТипЦенИтогов <> ЗНАЧЕНИЕ(Справочник.ТипыЦен.ПустаяСсылка)
	|			ТОГДА Инвентаризация.ТипЦенИтогов
	|		ИНАЧЕ Инвентаризация.СкладКомпании.ТипЦенРозничнойТорговли
	|	КОНЕЦ КАК ТипЦен,
	|	Инвентаризация.Дата КАК Дата,
	|	Инвентаризация.МоментВремени КАК МоментВремени,
	|	Инвентаризация.Номер КАК Номер,
	|	Инвентаризация.Организация КАК Организация,
	|	Инвентаризация.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	Инвентаризация.СкладКомпании.Код КАК КодСклада,
	|	Инвентаризация.СкладКомпании КАК СкладКомпании,
	|	Инвентаризация.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	Инвентаризация.СкладКомпании.Розничный КАК СкладРозничный,
	|	Инвентаризация.РевизионнаяИнвентаризация КАК РевизионнаяИнвентаризация,
	|	Инвентаризация.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	Инвентаризация.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоЕДРПОУ
	|ИЗ
	|	Документ.Инвентаризация КАК Инвентаризация
	|ГДЕ
	|	Инвентаризация.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	МИНИМУМ(ВложенныйЗапрос.НомерСтроки) КАК НомерСтроки,
	|	МИНИМУМ(ВложенныйЗапрос.Артикул) КАК Артикул,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	МИНИМУМ(ВложенныйЗапрос.Ед) КАК Ед,
	|	СУММА(ВложенныйЗапрос.ОстатокПоУчету) КАК ОстатокПоУчету,
	|	СУММА(ВложенныйЗапрос.Фактически) КАК Фактически,
	|	СУММА(ВложенныйЗапрос.Разница) КАК Инв_Разница,
	|	СУММА(ВложенныйЗапрос.Опт_Разница) КАК Опт_Разница,
	|	СУММА(ВложенныйЗапрос.Опт_Сумма) КАК Опт_Сумма,
	|	СУММА(ВложенныйЗапрос.Опт_КоличествоДоприход) КАК Опт_КоличествоДоприход,
	|	СУММА(ВложенныйЗапрос.Опт_СуммаДоприход) КАК Опт_СуммаДоприход
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИнвентаризацияТовары.Ссылка КАК Ссылка,
	|		ИнвентаризацияТовары.НомерСтроки КАК НомерСтроки,
	|		ИнвентаризацияТовары.Номенклатура.Артикул КАК Артикул,
	|		ИнвентаризацияТовары.Номенклатура КАК Номенклатура,
	|		ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|		ИнвентаризацияТовары.ЕдиницаИзмерения КАК Ед,
	|		ИнвентаризацияТовары.КоличествоКнижн КАК ОстатокПоУчету,
	|		ИнвентаризацияТовары.КоличествоФакт КАК Фактически,
	|		ИнвентаризацияТовары.Количество КАК Разница,
	|		0 КАК Опт_Разница,
	|		0 КАК Опт_Сумма,
	|		0 КАК Опт_КоличествоДоприход,
	|		0 КАК Опт_СуммаДоприход
	|	ИЗ
	|		Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
	|	ГДЕ
	|		ИнвентаризацияТовары.Ссылка В(&МассивСсылок)
	|		И ИнвентаризацияТовары.Количество <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПартииТоваровКомпании.Регистратор,
	|		99999,
	|		ПартииТоваровКомпании.Номенклатура.Артикул,
	|		ПартииТоваровКомпании.Номенклатура,
	|		ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	|		ПартииТоваровКомпании.Номенклатура.ОсновнаяЕдиницаИзмерения,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ПартииТоваровКомпании.Количество * -1
	|			ИНАЧЕ ПартииТоваровКомпании.Количество
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ПартииТоваровКомпании.Сумма * -1
	|			ИНАЧЕ ПартииТоваровКомпании.Сумма
	|		КОНЕЦ,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	|	ГДЕ
	|		ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.Инвентаризация
	|		И ПартииТоваровКомпании.Регистратор В(&МассивСсылок)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПриходованиеТоваров.Регистратор,
	|		99999,
	|		ПриходованиеТоваров.Номенклатура.Артикул,
	|		ПриходованиеТоваров.Номенклатура,
	|		ПриходованиеТоваров.ХарактеристикаНоменклатуры,
	|		ПриходованиеТоваров.Номенклатура.ОсновнаяЕдиницаИзмерения,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ПриходованиеТоваров.Количество,
	|		ПриходованиеТоваров.Сумма
	|	ИЗ
	|		РегистрНакопления.ПриходованиеТоваров КАК ПриходованиеТоваров
	|	ГДЕ
	|		ПриходованиеТоваров.Регистратор ССЫЛКА Документ.Инвентаризация
	|		И ПриходованиеТоваров.Регистратор В(&МассивСсылок)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Номенклатура";
	
	//"УПОРЯДОЧИТЬ ПО
	//|	Ссылка,
	//|	ВложенныйЗапрос.Номенклатура.Родитель.Родитель.Родитель.Наименование,
	//|	ВложенныйЗапрос.Номенклатура.Родитель.Родитель.Наименование,
	//|	ВложенныйЗапрос.Номенклатура.Родитель.Наименование,
	//|	ВложенныйЗапрос.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
	//|	ВложенныйЗапрос.Номенклатура.Наименование";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументов = МассивРезультатов[0].Выбрать();		
	ТаблицаСтрок 	  = МассивРезультатов[1].Выгрузить();
	
	ТаблицаСтрок.Индексы.Добавить("Ссылка");
	
	ЗапросЦены = Новый Запрос;
	ЗапросЦены.Текст = "ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ втТаблицаТоваров
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	|	ЦеныСрезПоследних.Цена КАК Цена
	|ИЗ
	|	РегистрСведений.Цены.СрезПоследних(
	|			&Момент,
	|			ТипЦен = &ТипЦен
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						втТаблицаТоваров.Номенклатура КАК Номенклатура
	|					ИЗ
	|						втТаблицаТоваров КАК втТаблицаТоваров
	|					СГРУППИРОВАТЬ ПО
	|						втТаблицаТоваров.Номенклатура)) КАК ЦеныСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаТоваров КАК втТаблицаТоваров
	|		ПО ЦеныСрезПоследних.Номенклатура = втТаблицаТоваров.Номенклатура
	|			И ЦеныСрезПоследних.Характеристика = втТаблицаТоваров.Характеристика";
	
	
	Если ИнвентаризационнаяОпись Тогда 
		КлючПараметровПечати = "ПараметрыПечати_ИнвентаризационнаяОпись";
	Иначе
		КлючПараметровПечати = "ПараметрыПечати_АктИнвентаризацииРозничный";
	КонецЕсли;	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = КлючПараметровПечати;
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Инвентаризация.ПФ_MXL_АктИнвентаризацииРозничный");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаОпись		= Макет.ПолучитьОбласть("ШапкаОпись");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");		
	ОбластьПодвалОпись		= Макет.ПолучитьОбласть("ПодвалОпись");	
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		СкладРозничный = ВыборкаДокументов.СкладРозничный;
		ТекСсылка = ВыборкаДокументов.Ссылка;			
		
		МассивСтрок = ТаблицаСтрок.НайтиСтроки(Новый Структура("Ссылка", ТекСсылка));		
		тчТовары = ТаблицаСтрок.Скопировать(МассивСтрок);
		тчТовары.Сортировать("НомерСтроки");
		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;								
		
		
		СтруктураШапки = Новый Структура;		
		
		Если ИнвентаризационнаяОпись Тогда 
			СтруктураШапки.Вставить("Организация", СокрЛП(ВыборкаДокументов.ОрганизацияНаименованиеПолное));
			СтруктураШапки.Вставить("ЕДРПОУ", СокрЛП(ВыборкаДокументов.ОрганизацияКодПоЕДРПОУ));
			СтруктураШапки.Вставить("Номер", ВыборкаДокументов.Номер);
			СтруктураШапки.Вставить("Дата", ВыборкаДокументов.Дата);
			СтруктураШапки.Вставить("Склад", ВыборкаДокументов.СкладКомпании);			
			
			ОбластьШапкаОпись.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьШапкаОпись);
		Иначе
			ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Инвентаризация №%1 от %2'; uk = 'Інвентаризація №%1 від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
			
			
			СтруктураШапки.Вставить("парЗаголовок", ЗаголовокЛок);
			СтруктураШапки.Вставить("парНашаОрганизация", ВыборкаДокументов.Организация);
			СтруктураШапки.Вставить("парКиоск", СокрЛП(ВыборкаДокументов.КодСклада) + " " + СокрЛП(ВыборкаДокументов.СкладКомпании));
			СтруктураШапки.Вставить("парРевизионнаяИнвентаризация", ?(ВыборкаДокументов.РевизионнаяИнвентаризация, НСтр("ru = 'Ревизионная инвентаризация'; uk = 'Ревізійна інвентаризація'", КодЯзыкаПечать), ""));
			
			ОбластьШапка.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьШапка);			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		нпп = 0;
		СуммаНедостачи = 0;
		СуммаИзлишки = 0;
		
		Если СкладРозничный Тогда 			
			ТаблицаТоваров = тчТовары.Скопировать();
			ТаблицаТоваров.Свернуть("Номенклатура, Характеристика");
			
			ЗапросЦены.УстановитьПараметр("Момент", 			 ВыборкаДокументов.МоментВремени);
			ЗапросЦены.УстановитьПараметр("ТипЦен", 			 ВыборкаДокументов.ТипЦен);
			ЗапросЦены.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаТоваров);
			
			ТаблицаЦен = ЗапросЦены.Выполнить().Выгрузить();
			ТаблицаЦен.Индексы.Добавить("Номенклатура, Характеристика");
		КонецЕсли;
		
		Для Каждого Стр Из тчТовары Цикл
			нпп = нпп + 1;
			Цена = 0;
			Количество = 0;
			Сумма = 0;
			
			Если СкладРозничный Тогда 			
				Количество = Стр.Инв_Разница;
				
				СтрокиЦены = ТаблицаЦен.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Стр.Номенклатура, Стр.Характеристика));			
				Если СтрокиЦены.Количество() > 0 Тогда 
					Цена = СтрокиЦены[0].Цена;
				КонецЕсли;
				
				Сумма = Количество * Цена;
				
				Если Количество > 0 Тогда 
					СуммаИзлишки = СуммаИзлишки + Сумма;
				ИначеЕсли Количество < 0 Тогда 
					СуммаНедостачи = СуммаНедостачи + Сумма;
				КонецЕсли;
				
			Иначе			
				Количество = Стр.Инв_Разница; //Стр.Опт_Разница;
				
				Сумма = Стр.Опт_Сумма + Стр.Опт_СуммаДоприход;
				Количество_Партия = Стр.Опт_Разница + Стр.Опт_КоличествоДоприход;
				
				Если Сумма = 0 Тогда 
					Продолжить;
				КонецЕсли;
				
				Если Количество_Партия <> 0 Тогда 
					Цена = Сумма / Количество_Партия;					
				ИначеЕсли Стр.Опт_КоличествоДоприход <> 0 И Стр.Опт_СуммаДоприход <> 0 Тогда
					Цена = Стр.Опт_СуммаДоприход / Стр.Опт_КоличествоДоприход;
				Иначе
					Цена = Сумма;					
				КонецЕсли;
				
				Если Сумма > 0 Тогда 
					СуммаИзлишки = СуммаИзлишки + Сумма;
				ИначеЕсли Сумма < 0 Тогда 
					СуммаНедостачи = СуммаНедостачи + Сумма;
				КонецЕсли;
			КонецЕсли;
			
			СтруктураСтроки = Новый Структура("Артикул, Ед, ОстатокПоУчету, Фактически");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, Стр, "Артикул, Ед, ОстатокПоУчету, Фактически");			
			СтруктураСтроки.Вставить("нпп", Стр.НомерСтроки);
			СтруктураСтроки.Вставить("Номенклатура", СокрЛП(Стр.Номенклатура) + ?(ЗначениеЗаполнено(Стр.Характеристика), ", " + СокрЛП(Стр.Характеристика), ""));
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("Цена", Формат(Цена, "ЧЦ=10; ЧДЦ=2"));  
			СтруктураСтроки.Вставить("Разница", Количество);
			СтруктураСтроки.Вставить("Сумма", Сумма);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);			
		КонецЦикла;				
		
		//Если Не СкладРозничный Тогда 
		//	СуммаДооприход = тчТовары.Итог("Опт_СуммаДоприход");
		//	СуммаИзлишки = СуммаИзлишки + СуммаДооприход;
		//КонецЕсли;
		
		СтруктураПодвала = Новый Структура;
		СтруктураПодвала.Вставить("парИзлишек", НСтр("ru = 'Излишек: '; uk = 'Надлишок: '", КодЯзыкаПечать) + Формат(СуммаИзлишки, "ЧДЦ=2; ЧН=0,00") + " грн.");
		СтруктураПодвала.Вставить("парНедостача", НСтр("ru = 'Недостача: '; uk = 'Недостача: '", КодЯзыкаПечать) + Формат(СуммаНедостачи, "ЧДЦ=2; ЧН=0,00") + " грн.");
		СтруктураПодвала.Вставить("парИтог", НСтр("ru = 'Итог: '; uk = 'Всього: '", КодЯзыкаПечать) + Формат(СуммаИзлишки + СуммаНедостачи, "ЧДЦ=2; ЧН=0,00") + " грн.");
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		Если ИнвентаризационнаяОпись Тогда 
			ОтветственноеЛицо = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизаций.Руководитель"), ВыборкаДокументов.Организация);
			Если ЗначениеЗаполнено(ОтветственноеЛицо) Тогда 
				ПредставлениеОтветственноего = "Директор " + ОтветственноеЛицо;
			Иначе
				ПредставлениеОтветственноего = "";
			КонецЕсли;
			
			СтрокаОписиНомеров = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 с №1 по №%2'; uk = '%1 з №1 по №%2'", КодЯзыкаПечать),
			ЧислоПрописью(
			нпп,
			НСтр("ru = 'Л = ru; НП = Ложь; НД = Ложь'; uk = 'Л = uk; НП = Ложь; НД = Ложь'", КодЯзыкаПечать),
			",,,,,,,,0"),
			нпп);
			
			СтруктураПодвала = Новый Структура;
			СтруктураПодвала.Вставить("Дата", ВыборкаДокументов.Дата);
			СтруктураПодвала.Вставить("ОписьНомеров", СтрокаОписиНомеров);
			СтруктураПодвала.Вставить("ПредставлениеРуководитель", ПредставлениеОтветственноего);
			
			ОбластьПодвалОпись.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьПодвалОпись);
		КонецЕсли;
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ТекСсылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Пустографка
Функция ПечатьПустографка(МассивОбъектов, Объектыпечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Инвентаризация.Ссылка КАК Ссылка,
	|	Инвентаризация.Организация КАК Организация,
	|	Инвентаризация.Дата КАК Дата,
	|	Инвентаризация.СкладКомпании КАК ПодразделениеКомпании,
	|	Инвентаризация.Номер КАК Номер,
	|	Инвентаризация.Комментарий КАК ВидЦенностей,
	|	Инвентаризация.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура.Артикул КАК Артикул,
	|		Номенклатура КАК Номенклатура,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Коэффициент КАК Коэффициент
	|	) КАК Товары
	|ИЗ
	|	Документ.Инвентаризация КАК Инвентаризация
	|ГДЕ
	|	Инвентаризация.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Пустографка";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Инвентаризация.ПФ_MXL_Пустографка");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвалТаблицы	= Макет.ПолучитьОбласть("ПодвалТаблицы");
	ОбластьПодписи			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ТекДата = Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy");
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'ПУСТОГРАФКА №%1 от %2'; uk = 'ПУСТОГРАФКА №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		ТекДата);									
		
		СтруктураШапки = Новый Структура("Организация, ПодразделениеКомпании, ВидЦенностей");
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов, "Организация, ПодразделениеКомпании, ВидЦенностей");
		
		СтруктураШапки.Вставить("ДатаНачала", ТекДата);
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("НачальныйНомерПоПорядку", "_________");
		СтруктураШапки.Вставить("НомерКонца", "_________");
		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			
			СтруктураСтроки = Новый Структура("НомерСтроки, ЕдиницаИзмерения, Коэффициент");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "НомерСтроки, ЕдиницаИзмерения, Коэффициент");			
			ТоварПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1  %2%3",
			СокрЛП(ВыборкаТовары.Артикул),
			СокрЛП(ВыборкаТовары.Номенклатура),
			?(ЗначениеЗаполнено(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("ТоварПредставление", ТоварПредставление);									
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);			
			ТабличныйДокумент.Вывести(ОбластьСтрока);			
		КонецЦикла;				
		
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции

Функция ПечатьИнвентаризационнаяОпись572(МассивОбъектов, ОбъектыПечати, ПараметрыВывода, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Инвентаризация_ИнвентаризационнаяОпись";
	
	// Зададим параметры макета по умолчанию
	ТабДокумент.ПолеСверху              = 10;
	ТабДокумент.ПолеСлева               = 0;
	ТабДокумент.ПолеСнизу               = 0;
	ТабДокумент.ПолеСправа              = 0;
	ТабДокумент.РазмерКолонтитулаСверху = 10;
	ТабДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.АвтоМасштаб             = Истина;
	
	// Всегда на украинском языке
	КодЯзыкаПечать = "uk";
	ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
	
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Инвентаризация.ПФ_MXL_UK_ИнвентаризационнаяОпись");
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = Макет.ПолучитьОбласть("Шапка");
	Подпись          = Макет.ПолучитьОбласть("Подпись");
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ИтогоПоСтранице  = Макет.ПолучитьОбласть("ИтогоПоСтранице");	
	Итоги            = Макет.ПолучитьОбласть("Итоги");	
	Дно	             = Макет.ПолучитьОбласть("Дно");	
	СтрокаСтраница 	 = Макет.ПолучитьОбласть("СтрокаСтраница");	
	НумерацияКолонокТаблицы = Макет.ПолучитьОбласть("НумерацияКолонокТаблицы");	
	
	// Создаем колонки Таблицы итогов
	ТаблицаИтогов = Новый ТаблицаЗначений();
	ТаблицаИтогов.Колонки.Добавить("ПорядковыйНомерИтогов");
	ТаблицаИтогов.Колонки.Добавить("ИтогНаличиеФактическое");
	ТаблицаИтогов.Колонки.Добавить("ИтогСтоимостьФактическая");
	ТаблицаИтогов.Колонки.Добавить("ИтогНаличиеПоДаннымУчета");
	ТаблицаИтогов.Колонки.Добавить("ИтогСтоимостьПоДаннымУчета");
	ТаблицаИтогов.Колонки.Добавить("ИтогНакопленнаяАмортизация");
	ТаблицаИтогов.Колонки.Добавить("ИтогБалансоваяСтоимость");
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru='Председатель комиссии';uk='Голова комісії'",КодЯзыкаПечать));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru='Члены комиссии';uk='Члени комісії'",КодЯзыкаПечать));
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Инвентаризация.Дата КАК ДатаДокумента,
		|	Инвентаризация.Номер КАК Номер,
		|	Инвентаризация.Организация.НаименованиеПолное КАК Организация,
		|	Инвентаризация.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоОКПО,
		|	Инвентаризация.СкладКомпании КАК ПодразделениеПредставление,
		|	Инвентаризация.Дата КАК ДатаНачалаИнвентаризации,
		|	Инвентаризация.Дата КАК ДатаОкончанияИнвентаризации,
		|	281 КАК СписокСчетов
		|ИЗ
		|	Документ.Инвентаризация КАК Инвентаризация
		|ГДЕ
		|	Инвентаризация.Ссылка = &Ссылка";
		
		Док = Запрос.Выполнить().Выбрать();
		Док.Следующий();
		
		ВыборкаПоКомиссии = ПараметрыПечати.СведенияОКомиссии;
		ВыборкаПоПриказу = ПараметрыПечати.СведенияОПриказе;
		
		//СписокОС = Ссылка.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Момент", Новый Граница(Ссылка.МоментВремени(), ВидГраницы.Исключая));
		
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияТовары.Ссылка КАК Ссылка,
		|	ИнвентаризацияТовары.НомерСтроки КАК НомерСтроки,
		|	ИнвентаризацияТовары.Номенклатура КАК ОсновноеСредство,
		|	ИнвентаризацияТовары.Номенклатура.НаименованиеПолное КАК ОсновноеСредствоНаименованиеПолное,
		|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ИнвентаризацияТовары.Коэффициент КАК Коэффициент,
		|	ИнвентаризацияТовары.КоличествоКнижн КАК НаличиеПоДаннымУчета,
		|	ИнвентаризацияТовары.КоличествоФакт КАК НаличиеФактическое,
		|	ИнвентаризацияТовары.Количество КАК Количество,
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаБазОстаток КАК СуммаБазОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СуммаНДСОстаток,
		|	281 КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток * ИнвентаризацияТовары.КоличествоФакт
		|	КОНЕЦ КАК СтоимостьФактическая,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток * ИнвентаризацияТовары.КоличествоКнижн
		|	КОНЕЦ КАК СтоимостьПоДаннымУчета,
		|	0 КАК НакопленнаяАмортизация,
		|	0 КАК БалансоваяСтоимость
		|ИЗ
		|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки КАК ПартииТоваровКомпанииОстатки
		|		ПО ИнвентаризацияТовары.Номенклатура = ПартииТоваровКомпанииОстатки.Номенклатура
		|			И ИнвентаризацияТовары.ХарактеристикаНоменклатуры = ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		|			И ИнвентаризацияТовары.Ссылка.СкладКомпании = ПартииТоваровКомпанииОстатки.СкладКомпании
		|ГДЕ
		|	ИнвентаризацияТовары.Ссылка = &Ссылка";
		
		
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияТовары.Ссылка КАК Ссылка,
		|	ИнвентаризацияТовары.НомерСтроки КАК НомерСтроки,
		|	ИнвентаризацияТовары.Номенклатура КАК ОсновноеСредство,
		|	ИнвентаризацияТовары.Номенклатура.НаименованиеПолное КАК ОсновноеСредствоНаименованиеПолное,
		|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ИнвентаризацияТовары.Коэффициент КАК Коэффициент,
		|	ИнвентаризацияТовары.КоличествоКнижн КАК НаличиеПоДаннымУчета,
		|	ИнвентаризацияТовары.КоличествоФакт КАК НаличиеФактическое,
		|	ИнвентаризацияТовары.Количество КАК Количество,
		|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СуммаНДСОстаток,
		|	281 КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток * ИнвентаризацияТовары.КоличествоФакт
		|	КОНЕЦ КАК СтоимостьФактическая,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпанииОстатки.КоличествоОстаток, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ (ПартииТоваровКомпанииОстатки.СуммаОстаток - ПартииТоваровКомпанииОстатки.СуммаНДСОстаток) / ПартииТоваровКомпанииОстатки.КоличествоОстаток * ИнвентаризацияТовары.КоличествоКнижн
		|	КОНЕЦ КАК СтоимостьПоДаннымУчета,
		|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
		|	ИнвентаризацияТовары.Ссылка.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(&Момент, ) КАК ПартииТоваровКомпанииОстатки
		|		ПО ИнвентаризацияТовары.Номенклатура = ПартииТоваровКомпанииОстатки.Номенклатура
		|			И ИнвентаризацияТовары.ХарактеристикаНоменклатуры = ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры
		|			И ИнвентаризацияТовары.Ссылка.СкладКомпании = ПартииТоваровКомпанииОстатки.СкладКомпании
		|ГДЕ
		|	ИнвентаризацияТовары.Ссылка = &Ссылка";
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваровКомпании.Регистратор КАК Ссылка,
		|	281 КАК СчетУчета,
		|	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
		|	ПартииТоваровКомпании.Номенклатура.НаименованиеПолное КАК НоменклатураНаименованиеПолное,
		|	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровКомпании.Количество КАК НаличиеПоДаннымУчета,
		|	0 КАК НаличиеФактическое,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпании.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(ПартииТоваровКомпании.Сумма / ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 3))
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпании.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ((ВЫРАЗИТЬ(ПартииТоваровКомпании.Сумма / ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 3))) * ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 2))
		|	КОНЕЦ КАК СтоимостьПоДаннымУчета,
		|	0 КАК СтоимостьФактическая,
		|	ПартииТоваровКомпании.Сумма КАК СуммаОстаток,
		|	ПартииТоваровКомпании.СуммаНДС КАК СуммаНДСОстаток,
		|	ПартииТоваровКомпании.СкладКомпании КАК СкладКомпании,
		|	НЕ ПартииТоваровКомпании.Партия ССЫЛКА Документ.ПоступлениеТоваров КАК НеПоступление,
		|	ПартииТоваровКомпании.Партия КАК Партия,
		|	ПартииТоваровКомпании.Партия.РегламентированныйУчет КАК ПартияРегламентированныйУчет,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпании.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ((ПартииТоваровКомпании.Сумма - ПартииТоваровКомпании.СуммаНДС) / ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 3))
		|	КОНЕЦ КАК ЦенаБНДС,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПартииТоваровКомпании.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ((ВЫРАЗИТЬ((ПартииТоваровКомпании.Сумма - ПартииТоваровКомпании.СуммаНДС) / ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 3))) * ПартииТоваровКомпании.Количество КАК ЧИСЛО(10, 2))
		|	КОНЕЦ КАК СтоимостьПоДаннымУчетаБНДС
		|ИЗ
		|	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
		|ГДЕ
		|	ПартииТоваровКомпании.Регистратор = &Ссылка
		|	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)";
		
		

		Если ТабДокумент.КоличествоСтраниц() > 0 Тогда 
			// Выведем разрыв страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;	
		
		//////////////////////////////////////////////////////////////////////
		// 1-я страница формы
		
		// Выведем шапку документа
		
		Шапка.Параметры.Заполнить(Док);
		Шапка.Параметры.ДокументОснованиеДата	= ВыборкаПоПриказу.ПриказДата;
		Шапка.Параметры.ДокументОснованиеНомер	= ВыборкаПоПриказу.ПриказНомер;
		//Шапка.Параметры.Организация          = Док.Организации;
		//Шапка.Параметры.ОрганизацияКодПоОКПО = Док.Организации;
		Шапка.Параметры.Подразделение        = Док.ПодразделениеПредставление;
		
		//Шапка.Параметры.НомерДокумента = ОбщегоНазначения.ПолучитьНомерНаПечать(Док);
		//Шапка.Параметры.ДатаДокумента  		= Док.Дата;
		
		//Шапка.Параметры.СписокСчетов  		= СтрокаСчетов;
		
		//ДанныеМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Ссылка.Организация, ВыборкаМОЛ.МОЛ, Ссылка.Дата);
		
		//Шапка.Параметры.Должность  			= ДанныеМОЛ.Должность;
		//Шапка.Параметры.РасшифровкаПодписи  = ДанныеМОЛ.Представление;
		Шапка.Параметры.Должность  			= ВыборкаПоКомиссии.ОтветственныйДолжность;
		Шапка.Параметры.РасшифровкаПодписи  = ВыборкаПоКомиссии.ОтветственныйФИО;
		
		ТабДокумент.Вывести(Шапка);
		
		// Выведем разрыв страницы
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		//////////////////////////////////////////////////////////////////////
		// 2-я страница формы
		
		// Выведем заголовок таблицы	
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		ТабДокумент.Вывести(НумерацияКолонокТаблицы);
		
		ПорядковыйНомерИтогов = 1; // счетчик количества итоговых строк в таблице
		
		НаличиеФактическоеПоСтранице     = 0;
		СтоимостьФактическаяПоСтранице   = 0;
		НаличиеПоДаннымУчетаПоСтранице   = 0;
		СтоимостьПоДаннымУчетаПоСтранице = 0;
		
		НакопленнаяАмортизацияПоСтранице = 0;
		БалансоваяСтоимостьПоСтранице 	 = 0;
		
		ПорядковыйНомерСтроки = 0; // счетчик количества строк в таблице
		
		ТаблицаИтогов.Очистить();
		
		РезЗапроса = Запрос.Выполнить();
		//	РезЗапроса.Выгрузить()
		Выборка = РезЗапроса.Выбрать();
		
		// Выведем строки таблицы
		Пока Выборка.Следующий() Цикл
			
			
			Если НЕ Выборка.Цена > 0 Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '<%1> - нет цены партии (%2) %3'; 
				|uk = '<%1> - немає ціни партії (%2) %3'"),
				Выборка.Ссылка,
				Выборка.Номенклатура.Артикул,
				Выборка.Номенклатура));
			КонецЕсли;
			СтрокаОС = Выборка;
			
			//Если Выборка.НеПоступление Тогда
			//	ПолучитьЗаменуПартии(СтрокаОС);
			//КонецЕсли;
			
			ПорядковыйНомерСтроки = ПорядковыйНомерСтроки + 1;
			
			СтрокаТаблицы   = Макет.ПолучитьОбласть("СтрокаТаблицы");
			
			СтрокаТаблицы.Параметры.Заполнить(СтрокаОС);
			
			СтрокаТаблицы.Параметры.НомерСтроки = ПорядковыйНомерСтроки;
			
			// Присвоим имена с номерами строк областям таблицы.
			// Номера потребуются для включения/отключения видимости значений в ячейках.
			СтрокаТаблицы.Области.НаличиеФактическое.Имя     = "НаличиеФактическое" + ПорядковыйНомерСтроки;
			СтрокаТаблицы.Области.СтоимостьФактическая.Имя   = "СтоимостьФактическая" + ПорядковыйНомерСтроки;
			СтрокаТаблицы.Области.НаличиеПоДаннымУчета.Имя   = "НаличиеПоДаннымУчета" + ПорядковыйНомерСтроки;
			СтрокаТаблицы.Области.СтоимостьПоДаннымУчета.Имя = "СтоимостьПоДаннымУчета" + ПорядковыйНомерСтроки;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			//СтрокаСПодвалом.Добавить(ИтогоПоСтранице);
			//СтрокаСПодвалом.Добавить(Итоги);
			
			Если НЕ ТабДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				
				// Добавим значения итогов по странице в таблицу итогов.
				// Они потребуются при включении видимости значений ячеек.
				НоваяСтрока = ТаблицаИтогов.Добавить();
				НоваяСтрока.ПорядковыйНомерИтогов 		= ПорядковыйНомерИтогов;
				НоваяСтрока.ИтогНаличиеФактическое 		= НаличиеФактическоеПоСтранице;
				НоваяСтрока.ИтогСтоимостьФактическая 	= СтоимостьФактическаяПоСтранице;
				НоваяСтрока.ИтогНаличиеПоДаннымУчета 	= НаличиеПоДаннымУчетаПоСтранице;
				НоваяСтрока.ИтогСтоимостьПоДаннымУчета 	= СтоимостьПоДаннымУчетаПоСтранице;
				НоваяСтрока.ИтогНакопленнаяАмортизация 	= НакопленнаяАмортизацияПоСтранице;
				НоваяСтрока.ИтогБалансоваяСтоимость	  	= БалансоваяСтоимостьПоСтранице;
				
				ПорядковыйНомерИтогов = ПорядковыйНомерИтогов + 1;
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Выведем заголовок таблицы	
				//ТабДокумент.Вывести(ЗаголовокТаблицы);
				ТабДокумент.Вывести(НумерацияКолонокТаблицы);
				
				НаличиеФактическоеПоСтранице     = 0;
				СтоимостьФактическаяПоСтранице   = 0;
				НаличиеПоДаннымУчетаПоСтранице   = 0;
				СтоимостьПоДаннымУчетаПоСтранице = 0;
				
				НакопленнаяАмортизацияПоСтранице = 0;
				БалансоваяСтоимостьПоСтранице    = 0;
				
				
			КонецЕсли;
			
			ТабДокумент.Вывести(СтрокаТаблицы);
			
			НаличиеФактическоеПоСтранице     = НаличиеФактическоеПоСтранице + СтрокаОС.НаличиеФактическое;
			СтоимостьФактическаяПоСтранице   = СтоимостьФактическаяПоСтранице + СтрокаОС.СтоимостьФактическая;
			НаличиеПоДаннымУчетаПоСтранице   = НаличиеПоДаннымУчетаПоСтранице + СтрокаОС.НаличиеПоДаннымУчета;
			СтоимостьПоДаннымУчетаПоСтранице = СтоимостьПоДаннымУчетаПоСтранице + СтрокаОС.СтоимостьПоДаннымУчета;
			
		КонецЦикла;		
		
		ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		
		// Присвоим имена с порядковыми номерами итогов областям таблицы.
		// Номера потребуются для включения/отключения видимости значений в ячейках.
		ИтогоПоСтранице.Области.ИтогНаличиеФактическое.Имя     = "ИтогНаличиеФактическое" + ПорядковыйНомерИтогов;
		ИтогоПоСтранице.Области.ИтогСтоимостьФактическая.Имя   = "ИтогСтоимостьФактическая" + ПорядковыйНомерИтогов;
		ИтогоПоСтранице.Области.ИтогНаличиеПоДаннымУчета.Имя   = "ИтогНаличиеПоДаннымУчета" + ПорядковыйНомерИтогов;
		ИтогоПоСтранице.Области.ИтогСтоимостьПоДаннымУчета.Имя = "ИтогСтоимостьПоДаннымУчета" + ПорядковыйНомерИтогов;
		
		// Добавим значения итогов по странице в таблицу итогов.
		// Они потребуются при включении видимости значений ячеек.
		НоваяСтрока = ТаблицаИтогов.Добавить();
		НоваяСтрока.ПорядковыйНомерИтогов 		= ПорядковыйНомерИтогов;
		НоваяСтрока.ИтогНаличиеФактическое 		= НаличиеФактическоеПоСтранице;
		НоваяСтрока.ИтогСтоимостьФактическая 	= СтоимостьФактическаяПоСтранице;
		НоваяСтрока.ИтогНаличиеПоДаннымУчета 	= НаличиеПоДаннымУчетаПоСтранице;
		НоваяСтрока.ИтогСтоимостьПоДаннымУчета 	= СтоимостьПоДаннымУчетаПоСтранице;
		
		ИтогСтоимостьФактическая   = ТаблицаИтогов.Итог("ИтогСтоимостьФактическая");
		ИтогНаличиеФактическое     = ТаблицаИтогов.Итог("ИтогНаличиеФактическое");
		ИтогСтоимостьПоДаннымУчета = ТаблицаИтогов.Итог("ИтогСтоимостьПоДаннымУчета");
		ИтогНаличиеПоДаннымУчета   = ТаблицаИтогов.Итог("ИтогНаличиеПоДаннымУчета");
		
		// Выведем заголовок итогов по странице
		ИтогоПоСтранице.Параметры.ИтогСтоимостьФактическая   = ИтогСтоимостьФактическая;
		ИтогоПоСтранице.Параметры.ИтогНаличиеФактическое     = ИтогНаличиеФактическое;
		ИтогоПоСтранице.Параметры.ИтогСтоимостьПоДаннымУчета = ИтогСтоимостьПоДаннымУчета;
		ИтогоПоСтранице.Параметры.ИтогНаличиеПоДаннымУчета   = ИтогНаличиеПоДаннымУчета;
		
		//ТабДокумент.Вывести(СтрокаСтраница);
		ТабДокумент.Вывести(ИтогоПоСтранице);
		
		Итоги = Макет.ПолучитьОбласть("Итоги");	
		
		// Присвоим имена с порядковыми номерами итогов областям таблицы.
		// Номера потребуются для включения/отключения видимости значений в ячейках.
		Итоги.Области.КоличествоФактическоеПрописью.Имя   = "КоличествоФактическоеПрописью" + ПорядковыйНомерИтогов;
		Итоги.Области.СтоимостьФактическаяПрописью.Имя    = "СтоимостьФактическаяПрописью" + ПорядковыйНомерИтогов;
		
		КоличествоФактическоеПрописью = ЧислоПрописью(ИтогНаличиеФактическое,"Л="+"uk_UA", ",,,ж,,,,,0");
		СтоимостьФактическаяПрописью  = ЧислоПрописью(ИтогСтоимостьФактическая, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		КоличествоПрописью 	= ЧислоПрописью(ИтогНаличиеПоДаннымУчета,"Л="+"uk_UA", ",,,ж,,,,,0");
		СуммаПрописью  		= ЧислоПрописью(ИтогСтоимостьПоДаннымУчета, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		// Выведем итоговые строки
		Итоги.Параметры.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(ПорядковыйНомерСтроки,"Л="+"uk_UA", ",,,м,,,,,0");
		Итоги.Параметры.КоличествоФактическоеПрописью       = КоличествоФактическоеПрописью;
		Итоги.Параметры.СтоимостьФактическаяПрописью        = СтоимостьФактическаяПрописью;
		Итоги.Параметры.КоличествоПрописью       			= КоличествоПрописью;
		Итоги.Параметры.СуммаПрописью        				= СуммаПрописью;
		
		ТабДокумент.Вывести(Итоги);
		
		ПорядковыйНомерИтогов = ПорядковыйНомерИтогов + 1;
		
		// Выведем разрыв страницы
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		////////////////////////////////////////////////////////////////////////
		//// 3-я страница формы
		
		// Выведем подпись председателя инвентаризационной комиссии
		Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
		Подпись.Параметры.Должность                = ВыборкаПоКомиссии.ПредседательКомиссииДолжность;
		Подпись.Параметры.РасшифровкаПодписи       = ВыборкаПоКомиссии.ПредседательКомиссииФИО;
		
		ТабДокумент.Вывести(Подпись);
		
		// Выведем подписи членов комиссии
		ВыводитьЗаголовок = Истина;
		
		НаименованиеЧленовКомиссии = Новый Массив;
		НаименованиеЧленовКомиссии.Добавить("ПервыйЧленКомиссии");
		НаименованиеЧленовКомиссии.Добавить("ВторойЧленКомиссии");
		//НаименованиеЧленовКомиссии.Добавить("ТретийЧленКомиссии");
		
		// Сначала выведем членов комиссии из выборки
		Для Каждого ЧленКомиссии Из НаименованиеЧленовКомиссии Цикл
			
			Если НЕ ТабДокумент.ПроверитьВывод(Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, 
			ЗаголовокРазделаПодписей.ЧленыКомиссии,
			"");
			Подпись.Параметры.Должность                = ВыборкаПоКомиссии[ЧленКомиссии + "Должность"];
			Подпись.Параметры.РасшифровкаПодписи       = ВыборкаПоКомиссии[ЧленКомиссии + "ФИО"];
			
			ТабДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		Дно.Параметры.ПорядковыйНомер = ПорядковыйНомерСтроки;
		
		Дно.Параметры.ДатаДокумента  		= Док.ДатаДокумента;
		
		Дно.Параметры.Должность 		 	= ВыборкаПоКомиссии.ОтветственныйДолжность;
		Дно.Параметры.РасшифровкаПодписи 	= ВыборкаПоКомиссии.ОтветственныйФИО;
		
		Дно.Параметры.ДиректорДолжность	 	= ВыборкаПоКомиссии.ДиректорДолжность;
		Дно.Параметры.ДиректорФИО			= ВыборкаПоКомиссии.ДиректорФИО;
		
		ТабДокумент.Вывести(Дно);
		
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Процедура ПолучитьЗаменуПартии(СтрокаОС);
	
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПартииТоваровКомпанииОстатки.Партия КАК Партия,
	|	ПартииТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ПартииТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ПартииТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПартииТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ПартииТоваровКомпанииОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ПартииТоваровКомпанииОстатки.СуммаНДСОстаток КАК СуммаНДСОстаток
	|ИЗ
	|	РегистрНакопления.ПартииТоваровКомпании.Остатки(
	|			&Момент,
	|			Номенклатура = &Номенклатура
	|				И СкладКомпании = &СкладКомпании
	|				И ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	|				И Партия ССЫЛКА Документ.ПоступлениеТоваров
	|				И Партия.РегламентированныйУчет) КАК ПартииТоваровКомпанииОстатки";
	
	Запрос.УстановитьПараметр("Момент", Новый Граница(СтрокаОС.Партия.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Номенклатура", СтрокаОС.Номенклатура);
	Запрос.УстановитьПараметр("СкладКомпании", СтрокаОС.СкладКомпании);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаОС.ХарактеристикаНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	//	РезультатЗапроса.Выгрузить()
	
	
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '<%1> - нет партии (%2) %3'; 
		|uk = '<%1> - немає партії (%2) %3'"),
		СтрокаОС.Ссылка,
		СтрокаОС.Номенклатура.Артикул,
		СтрокаОС.Номенклатура));
	КонецЕсли;
	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
	
КонецПроцедуры



#КонецОбласти	

#Область Заполнение

// Процедура - Заполнить товары по остаткам на складе
//
// Параметры:
//  тчТовары					 - Табличная часть "Товары" из документа инвентаризация
//  ПараметрыЗаполненияОстатков	 - Структура
//  Параметры, передаваемые в структуре:
//		- Команда - имя команды заполнения остатков: "ЗаполнитьПоОстаткамНаСкладе" или "ЗаполнитьПоДвижениюНаСкладе";
//		- Момент - момент времени документа;
//		- СкладКомпании - склад, по которому берутся остатки.
//
Процедура ЗаполнитьТоварыПоОстаткамНаСкладе(тчТовары, ПараметрыЗаполненияОстатков) Экспорт 
	Перем Момент, СкладКомпании, Команда,НачалоИнвентаризации, КомпоновщикНастроек, МассивОтборов, Отдел;
	
	Если Не ПараметрыЗаполненияОстатков.Свойство("Момент", Момент) ИЛИ Не ПараметрыЗаполненияОстатков.Свойство("СкладКомпании", СкладКомпании) ИЛИ 
		Не ПараметрыЗаполненияОстатков.Свойство("Команда", Команда) Тогда 
		ВызватьИсключение НСтр("ru = 'Не правильно переданы параметры в процедуру заполнения остатков товаров.
		|Обратитесь к разработчикам ПО.'; uk = 'Не правильно передані параметри в процедуру заповнення залишків товарів.
		|Зверніться до розробників ПЗ.
		|'");
	КонецЕсли;	
	
	ПараметрыЗаполненияОстатков.Свойство("КомпоновщикНастроек", КомпоновщикНастроек);
	ПараметрыЗаполненияОстатков.Свойство("Отдел", Отдел);
	
	Если КомпоновщикНастроек <> Неопределено Тогда 
		СКД = ПолучитьОбщийМакет("СхемаКомпоновкиОтбораНоменклатуры");
		
		НастройкиКомпоновщика = КомпоновщикНастроек.Настройки;
		ПараметрыНастройки = КомпоновщикНастроек.Настройки.ПараметрыДанных;
		
		//Получим макет компоновки    
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; 
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, НастройкиКомпоновщика,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")); 
		
		//Через процессор компоновки получим результат
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных); 
		
		ТаблицаРезультат = Новый ТаблицаЗначений; 
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений; 
		
		ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат); 
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных); 
		
		МассивОтборов = ТаблицаРезультат.ВыгрузитьКолонку("Номенклатура");
	КонецЕсли;
	
	
	УстановитьПривилегированныйРежим(Истина);
	Если Команда = "ЗаполнитьПоОстаткамНаСкладе" Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Коэффициент,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|				И ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		|			ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
		|	КОНЕЦ КАК КоличествоКнижн,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|				И ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		|			ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
		|	КОНЕЦ КАК КоличествоФакт,
		|	0 КАК Количество,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент, СкладКомпании = &СкладКомпании %1) КАК ОстаткиТоваровКомпанииОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_КассоваяЛента КАК ТК_КассоваяЛента
		|		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура <> ТК_КассоваяЛента.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Родитель.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование";
		
		Запрос = Новый Запрос;
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ?(МассивОтборов <> Неопределено, "И Номенклатура В (&МассивНоменклатуры)", ""));		
		
		Запрос.УстановитьПараметр("Момент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивОтборов);
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		тчТовары.Загрузить(Результат);
		
	ИначеЕсли Команда = "ЗаполнитьПоОстаткамНаСкладеОС" Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	НоменклатураОтделов.Номенклатура КАК рНоменклатура,
		               |	НоменклатураОтделов.Отдел КАК Отдел
		               |ПОМЕСТИТЬ ВТ_ОтборНоменклатура
		               |ИЗ
		               |	РегистрСведений.НоменклатураОтделов КАК НоменклатураОтделов
		               |ГДЕ
		               |	НоменклатураОтделов.Склад = &СкладКомпании
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	НоменклатураОтделов.Отдел,
		               |	НоменклатураОтделов.Номенклатура
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		               |	ВЫБОР
		               |		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		               |			ТОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		               |		ИНАЧЕ 1
		               |	КОНЕЦ КАК Коэффициент,
		               |	ВЫБОР
		               |		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		               |				И ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		               |			ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		               |		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
		               |	КОНЕЦ КАК КоличествоКнижн,
		               |	ВЫБОР
		               |		КОГДА ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		               |				И ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		               |			ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		               |		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
		               |	КОНЕЦ КАК КоличествоФакт,
		               |	0 КАК Количество,
					   |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются
					   |ИЗ
					   |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Момент, СкладКомпании = &СкладКомпании %1) КАК ОстаткиТоваровКомпанииОстатки
					   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_КассоваяЛента КАК ТК_КассоваяЛента
					   |		ПО ОстаткиТоваровКомпанииОстатки.Номенклатура <> ТК_КассоваяЛента.Номенклатура
					   |        %2
					   |
					   |УПОРЯДОЧИТЬ ПО
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Родитель.Родитель.Наименование,
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Родитель.Наименование,
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Родитель.Наименование,
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
		               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование";
		
		Запрос = Новый Запрос;
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ?(МассивОтборов <> Неопределено, "И Номенклатура В (&МассивНоменклатуры)", ""),		
		?(Отдел <> Неопределено, 
		" 		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОтборНоменклатура КАК ВТ_ОтборНоменклатура
		|		ПО (ВЫБОР
		|				КОГДА &Отдел = ЗНАЧЕНИЕ(Справочник.Отделы.ПустаяСсылка)
		|					ТОГДА НЕ ОстаткиТоваровКомпанииОстатки.Номенклатура В ИЕРАРХИИ
		|					(ВЫБРАТЬ
		|						ВТ_ОтборНоменклатура.рНоменклатура КАК рНоменклатура
		|					ИЗ
		|						ВТ_ОтборНоменклатура КАК ВТ_ОтборНоменклатура)
		|				ИНАЧЕ ОстаткиТоваровКомпанииОстатки.Номенклатура В Иерархии
		|						(ВЫБРАТЬ
		|							ВТ_ОтборНоменклатура.рНоменклатура КАК рНоменклатура
		|						ИЗ
		|							ВТ_ОтборНоменклатура КАК ВТ_ОтборНоменклатура
		|						ГДЕ
		|							ВТ_ОтборНоменклатура.Отдел В (&Отдел))
		|			КОНЕЦ)",""));
		
		Запрос.УстановитьПараметр("Момент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивОтборов);
		Запрос.УстановитьПараметр("Отдел", Отдел);
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		тчТовары.Загрузить(Результат);
		
	ИначеЕсли Команда = "ЗаполнитьПоДвижениюНаСкладе" Тогда 
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Коэффициент,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|				И ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		|			ТОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток / ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток
		|	КОНЕЦ КАК КоличествоКнижн,
		|	ВЫБОР
		|		КОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|				И ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент > 0
		|			ТОГДА ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток / ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
		|		ИНАЧЕ ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток
		|	КОНЕЦ КАК КоличествоФакт,
		|	0 КАК Количество,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоРасход КАК КоличествоРасход
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.ОстаткиИОбороты(&НачалоОборотов, &Момент, , , СкладКомпании = &СкладКомпании %1) КАК ОстаткиТоваровКомпанииОстаткиИОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_КассоваяЛента КАК ТК_КассоваяЛента
		|		ПО ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура <> ТК_КассоваяЛента.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.Родитель.Родитель.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.Родитель.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.ТоварнаяМарка.Родитель.Наименование,
		|	ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура.Наименование";
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ?(МассивОтборов <> Неопределено, "И Номенклатура В (&МассивНоменклатуры)", ""));
		
		Если ТипЗнч(Момент) = Тип("МоментВремени") Тогда 
			НачалоОборотов = НачалоДня(ДобавитьМесяц(Момент.Дата, -2));
		Иначе
			НачалоОборотов = НачалоДня(ДобавитьМесяц(Момент, -2));
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НачалоОборотов", НачалоОборотов);
		Запрос.УстановитьПараметр("Момент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивОтборов);
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		тчТовары.Загрузить(Результат);
	ИначеЕсли Команда = "ЗаполнитьДаннымиТСД" Тогда 
		
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ТК_ДанныеИнвентаризацийТСД.Номенклатура КАК Номенклатура,
		|	СУММА(ТК_ДанныеИнвентаризацийТСД.Количество) КАК Количество,
		|	ТК_ДанныеИнвентаризацийТСД.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ ВТ_ДанныеТСД
		|ИЗ
		|	РегистрСведений.ТК_ДанныеИнвентаризацийТСД КАК ТК_ДанныеИнвентаризацийТСД
		|ГДЕ
		|	ТК_ДанныеИнвентаризацийТСД.СкладКомпании = &СкладКомпании
		|	И ТК_ДанныеИнвентаризацийТСД.ДатаЗагрузки >= &НачалоИнвентаризации
		|	И ТК_ДанныеИнвентаризацийТСД.Номенклатура В(&МассивНоменклатуры)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТК_ДанныеИнвентаризацийТСД.Номенклатура,
		|	ТК_ДанныеИнвентаризацийТСД.ХарактеристикаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	СУММА(ВложенныйЗапрос.КоличествоКнижн) КАК КоличествоКнижн,
		|	СУММА(ВложенныйЗапрос.КоличествоФакт) КАК КоличествоФакт,
		|	СУММА(ВложенныйЗапрос.КоличествоПриход) КАК КоличествоПриход,
		|	СУММА(ВложенныйЗапрос.КоличествоРасход) КАК КоличествоРасход
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_ДанныеТСД.Номенклатура КАК Номенклатура,
		|		ВТ_ДанныеТСД.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		0 КАК КоличествоКнижн,
		|		ВТ_ДанныеТСД.Количество КАК КоличествоФакт,
		|		0 КАК КоличествоПриход,
		|		0 КАК КоличествоРасход
		|	ИЗ
		|		ВТ_ДанныеТСД КАК ВТ_ДанныеТСД
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОстаткиТоваровКомпанииОстаткиИОбороты.Номенклатура,
		|		ОстаткиТоваровКомпанииОстаткиИОбороты.ХарактеристикаНоменклатуры,
		|		ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоКонечныйОстаток,
		|		0,
		|		ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоПриход,
		|		ОстаткиТоваровКомпанииОстаткиИОбороты.КоличествоРасход
		|	ИЗ
		|		РегистрНакопления.ОстаткиТоваровКомпании.ОстаткиИОбороты(
		|				&НачалоОборотов,
		|				&Момент,
		|				,
		|				,
		|				СкладКомпании = &СкладКомпании
		|					И Номенклатура В (&МассивНоменклатуры)) КАК ОстаткиТоваровКомпанииОстаткиИОбороты) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.ХарактеристикаНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
		|	ВТ_Товары.КоличествоКнижн КАК КоличествоКнижн,
		|	ВТ_Товары.КоличествоФакт КАК КоличествоФакт,
		|	ВТ_Товары.КоличествоФакт - ВТ_Товары.КоличествоКнижн КАК Количество,
		|	&ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары";
		Если МассивОтборов =  Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И Номенклатура В (&МассивНоменклатуры)","");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И ТК_ДанныеИнвентаризацийТСД.Номенклатура В(&МассивНоменклатуры)","");
		КонецЕсли;
		
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =  ТекстЗапроса;
		
		
		Если ТипЗнч(Момент) = Тип("МоментВремени") Тогда 
			НачалоОборотов = НачалоДня(ДобавитьМесяц(Момент.Дата, -2));
		Иначе
			НачалоОборотов = НачалоДня(ДобавитьМесяц(Момент, -2));
		КонецЕсли;
		НачалоИнвентаризации = Неопределено;
		ПараметрыЗаполненияОстатков.Свойство("НачалоИнвентаризации", НачалоИнвентаризации);
		
		Запрос.УстановитьПараметр("НачалоОборотов", НачалоОборотов);
		Запрос.УстановитьПараметр("Момент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		Запрос.УстановитьПараметр("ХарактеристикиИспользуются", Не СкладКомпании.НеВестиУчетПоХарактеристикам);
		
		Запрос.УстановитьПараметр("НачалоИнвентаризации", НачалоИнвентаризации);
		Запрос.УстановитьПараметр("МассивНоменклатуры", МассивОтборов);
		
		Результат = Запрос.Выполнить().Выгрузить();
		
		тчТовары.Загрузить(Результат);
		
	ИначеЕсли Команда = "ПересчитатьКоличествоКнижн" Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст =  "ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ вт_Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
		|			&НаМомент,
		|			СкладКомпании = &СкладКомпании
		|				И Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_Товары.Номенклатура КАК Номенклатура
		|					ИЗ
		|						вт_Товары КАК вт_Товары)) КАК ОстаткиТоваровКомпанииОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Товары.Номенклатура КАК Номенклатура,
		|	вт_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).Родитель КАК нРодитель,
		|	ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ТоварнаяМарка КАК нТоварнаяМарка,
		|	ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).Наименование КАК нНаименование,
		|	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	вт_Товары КАК вт_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
		|		ПО вт_Товары.Номенклатура = ВТ_Остатки.Номенклатура
		|			И вт_Товары.ХарактеристикаНоменклатуры = ВТ_Остатки.ХарактеристикаНоменклатуры";
		
		Запрос.УстановитьПараметр("Товары", тчТовары.Выгрузить(,"Номенклатура,ХарактеристикаНоменклатуры"));
		Запрос.УстановитьПараметр("НаМомент", Момент);
		Запрос.УстановитьПараметр("СкладКомпании", СкладКомпании);
		
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			мнСтрки = 	тчТовары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",ВыборкаДетальныеЗаписи.Номенклатура,ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры));						
			СтрокаТЧ = мнСтрки[0];
			
			КоличествоОстаток = ВыборкаДетальныеЗаписи.КоличествоОстаток;
			Коэф = СтрокаТЧ.Коэффициент;
			
			СтрокаТЧ.КоличествоКнижн = КоличествоОстаток / ?(Коэф > 0, Коэф, 1);
			СтрокаТЧ.Количество = СтрокаТЧ.КоличествоФакт - СтрокаТЧ.КоличествоКнижн;
		КонецЦикла;
		
	КонецЕсли;
	
	МассивБезЕдИзм = тчТовары.НайтиСтроки(Новый Структура("ЕдиницаИзмерения", ПредопределенноеЗначение("Справочник.ЕдиницыИзмерения.ПустаяСсылка")));
	Если МассивБезЕдИзм.Количество() > 0 Тогда
		МенеджерЕдИзм = Справочники.ЕдиницыИзмерения;
		Для Каждого Стр Из МассивБезЕдИзм Цикл 
			тКоэф = ?(Стр.Коэффициент > 0, Стр.Коэффициент, 1);
			ТекЕдИзм = МенеджерЕдИзм.НайтиЕдиницуИзмеренийПоКоф(Стр.Номенклатура, тКоэф);
			Если Не ЗначениеЗаполнено(ТекЕдИзм) Тогда 
				ТекЕдИзм = МенеджерЕдИзм.НайтиЕдиницуИзмеренийПоБазовой(Стр.Номенклатура);
				тКоэф = ?(ТекЕдИзм.Коэффициент > 0, ТекЕдИзм.Коэффициент, 1);
			КонецЕсли;
			
			Стр.ЕдиницаИзмерения = ТекЕдИзм;
			Стр.Коэффициент = тКоэф;
			Стр.КоличествоКнижн = Стр.КоличествоКнижн / ?(тКоэф > 0, тКоэф, 1);
			Стр.Количество = Стр.КоличествоФакт - Стр.КоличествоКнижн; 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПересчитатьКоличествоКнижнИнвентаризаций(Знач МассивИнвентаризаций) Экспорт 
	
	Всего = МассивИнвентаризаций.Количество();
	Обработано = 0;
	Успешно = 0;
	Для Каждого ссИнв Из МассивИнвентаризаций Цикл 				
		Обработано = Обработано + 1;			
		
		ДлительныеОперации.СообщитьПрогресс(Цел(Обработано/Всего * 100), 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Обработано %1 из %2'; uk = 'Оброблено %1 із %2'"),
		Обработано,
		Всего));
		
		обИнв = ссИнв.ПолучитьОбъект();		
		Если обИнв.ПометкаУдаления Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ < %1> помечен на удаление. Количество не пересчитано'; uk = 'Документ <%1> помічений на видалення. Кількість не перераховано'"),
			Строка(ссИнв),
			ссИнв));			
			Продолжить;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Момент", обИнв.Ссылка.МоментВремени());
		ДополнительныеПараметры.Вставить("СкладКомпании", обИнв.СкладКомпании);	
		ДополнительныеПараметры.Вставить("Команда", "ПересчитатьКоличествоКнижн");
		
		Документы.Инвентаризация.ЗаполнитьТоварыПоОстаткамНаСкладе(обИнв.Товары, ДополнительныеПараметры);
		
		Попытка
			РежимЗаписи = ?(обИнв.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
			обИнв.Записать(РежимЗаписи);
			Успешно = Успешно + 1;
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка %2 документа < %1>
			|%3'; uk = 'Помилка %2 документу <%1>
			|%3'"),
			Строка(ссИнв),
			?(РежимЗаписи = РежимЗаписиДокумента.Проведение, НСтр("ru = 'проведения'; uk = 'проведення'"), НСтр("ru = 'записи'; uk = 'запису'")),
			ОписаниеОшибки()),
			ссИнв);
		КонецПопытки;
	КонецЦикла;
	
	Возврат Новый Структура("Всего, Успешно", Всего, Успешно);
КонецФункции


#КонецОбласти	

#КонецЕсли
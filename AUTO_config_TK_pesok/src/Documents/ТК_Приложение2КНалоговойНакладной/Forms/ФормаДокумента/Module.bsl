&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ
Перем Разбить;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	Контрагент = Объект.Контрагент;
	Договор = Объект.ДоговорВзаиморасчетов;
	ВалютаДокумента = Объект.ВалютаДокумента;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ТК_Приложение2КНалоговойНакладной"));
	
	УстановитьУсловноеОформление();
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПриЧтенииСозданииНаСервере();

	УстановитьСостояниеДокумента();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	УстановитьСостояниеДокумента();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация,Объект.ПодразделениеКомпании);
		Объект.ДоговорВзаиморасчетов = СтруктураДанные.Договор;
		
		ДоговорПередИзменением = Договор;
		Договор = Объект.ДоговорВзаиморасчетов;
		
		КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные);
	Иначе
		Объект.ДоговорВзаиморасчетов = Договор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяНакладнаяПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.НалоговаяНакладная) Тогда 
		Возврат;
	КонецЕсли;
		
	Если Объект.НалоговаяНакладная <> НалоговаяПередИзменением Тогда 
		ОкончаниеВопроса = Новый ОписаниеОповещения("ИзменениеНалоговойНакладнойПродолжение", ЭтаФорма, Неопределено);
		ПоказатьВопрос(
			ОкончаниеВопроса, 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заполнить документ на основании ""%1""?'; uk = 'Заповнити документ на підставі ""%1""?'"),
				Объект.НалоговаяНакладная),
			РежимДиалогаВопрос.ДаНет,
			,
			, 
			НСтр("ru = 'Изменение налоговой накладной'; uk = 'Зміна податкової накладної'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеНалоговойНакладнойПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ИзменениеНалоговойНакладнойНаСервере();		
	Иначе
		Объект.НалоговаяНакладная = НалоговаяПередИзменением;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеНалоговойНакладнойНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");	
	
	ДокументОбъект.Заполнить(Объект.НалоговаяНакладная);
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");

	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);	
	//СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));		
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, ЭтаФорма);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказатели(ЭтаФорма);   
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБезНалоговПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	РассчитатьИтоговыеПоказатели(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)	
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
			
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНалоговПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИзменениеКоличествоПриИзменении(Элемент)
	
	текСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если текСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Если текСтрока.ИзменениеКоличество <> 0 И текСтрока.ИзменениеЦенаБезНалогов <> 0 Тогда 
		текСтрока.ИзменениеЦенаБезНалогов = 0;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru='Изменена цена';uk='Змінена ціна'"),
			,
			НСтр("ru='Разрешается корректировать или цену или количество';uk='Дозволяється коригувати або ціну або кількість'"));
	КонецЕсли;
	
	Если текСтрока.ИзменениеКоличество < 0 Тогда 
		текСтрока.ИзменениеКоличество = Макс(текСтрока.ИзменениеКоличество, - текСтрока.Количество);
	КонецЕсли;
	
	РассчитатьСуммыИзменений(текСтрока);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИзменениеЦенаПриИзменении(Элемент)
	
	текСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если текСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Если текСтрока.ИзменениеЦенаБезНалогов <> 0 И текСтрока.ИзменениеКоличество <> 0 Тогда 
		текСтрока.ИзменениеКоличество = 0;
		
		ПоказатьОповещениеПользователя(
			НСтр("ru='Изменено количество';uk='Змінено кількість'"),
			,
			НСтр("ru='Разрешается корректировать или цену или количество';uk='Дозволяється коригувати або ціну або кількість'"));
	КонецЕсли;
	
	Если текСтрока.ИзменениеЦенаБезНалогов < 0 Тогда 
		текСтрока.ИзменениеЦенаБезНалогов = Макс(текСтрока.ИзменениеЦенаБезНалогов, - текСтрока.ЦенаБезНалогов);
	КонецЕсли;
	
	РассчитатьСуммыИзменений(текСтрока);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммыИзменений(СтрокаТЧ)
	
	Коэф = 0;
	Если СтрокаТЧ.ИзменениеКоличество <> 0 Тогда
		
		ИсходноеКоличество = СтрокаТЧ.Количество;		
		Если ИсходноеКоличество = 0 Тогда
			ИсходноеКоличество = 1;
		КонецЕсли;
			 
		Коэф =  СтрокаТЧ.ИзменениеКоличество / ИсходноеКоличество;
		
	ИначеЕсли СтрокаТЧ.ИзменениеЦенаБезНалогов <> 0 Тогда
		
		ИсходнаяЦена = СтрокаТЧ.ЦенаБезНалогов;		
		Если ИсходнаяЦена = 0 Тогда
			ИсходнаяЦена = 1;
		КонецЕсли;

		Коэф =  СтрокаТЧ.ИзменениеЦенаБезНалогов/ИсходнаяЦена;	
		
	КонецЕсли;
	
	СтрокаТЧ.ИзменениеСуммаБезНалогов	= СтрокаТЧ.СуммаБезНалогов * Коэф;
	СтрокаТЧ.ИзменениеСуммаНДС			= СтрокаТЧ.СуммаНДС * Коэф;		
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СкопироватьИзНалоговой(Команда)
	
	Если ЗначениеЗаполнено(Объект.НалоговаяНакладная) Тогда 
		ЗаполнитьИзНалоговойНакладнойНаСервере(Объект.НалоговаяНакладная);
		РассчитатьИтоговыеПоказатели(ЭтаФорма);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен реквизит ""Налоговая накладная""'; uk = 'Не заповнений реквізит ""Податкова накладна""'"),, НСтр("ru = 'Заполнение табличной части'; uk = 'Заповнення табличної частини'"));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМинусовыеЗначения(Команда)
	
	Для Каждого текСтрока Из Объект.Товары Цикл 
		текСтрока.ИзменениеКоличество = текСтрока.Количество * -1;
		РассчитатьСуммыИзменений(текСтрока);
	КонецЦикла;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзВыбраннойНалоговой(Команда)
	
	ПродолжениеВыбораНакладной = Новый ОписаниеОповещения("ЗаполнитьИзВыбраннойНалоговойПродолжение", ЭтаФорма, Неопределено);
	
	ОткрытьФорму(
		"Документ.ТК_НалоговаяНакладная.ФормаВыбора",
		, 
		ЭтаФорма, 
		Истина,
		,
		,
		ПродолжениеВыбораНакладной,
		ПредопределенноеЗначение("РежимОткрытияОкнаФормы.БлокироватьОкноВладельца"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзВыбраннойНалоговойПродолжение(Выбрано, ДополнительныеПараметры) Экспорт 
	
	Если Выбрано <> Неопределено Тогда 
		ЗаполнитьИзНалоговойНакладнойНаСервере(Выбрано);			
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));

	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаДокумента",
		Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Если ДоговорПередИзменением <> Объект.ДоговорВзаиморасчетов Тогда
		
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением);
		
	Иначе
		
		Объект.ДоговорВзаиморасчетов = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()
		
	
	
КонецПроцедуры // ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	// Расчет итога по табличной части "Товары"
	КоллекцияТовары 					= Форма.Объект.Товары;
	Форма.СуммаБезНДС 				= КоллекцияТовары.Итог("ИзменениеСуммаБезНалогов");
	Форма.СуммаНДС 						= КоллекцияТовары.Итог("ИзменениеСуммаНДС");
		
КонецПроцедуры


&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	 	
	СтруктураДействий = Новый Структура;	

	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");

	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	

	ПриИзмененииДоговораНаСервере(СтруктураДействий);

	РассчитатьИтоговыеПоказатели(ЭтаФорма);	
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДоговораНаСервере(СтруктураДействий)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");

	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));
	
КонецПроцедуры       

//&НаКлиенте
//Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий)
//	
//	Если ТипЗнч(СтруктураДействий) <> Тип("Структура") Тогда
//		СтруктураДействий = Новый Структура;
//	КонецЕсли;
//	
//	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
//	СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект));
//	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
//	
//	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
//	
//	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
//КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзНалоговойНакладнойНаСервере(НалоговаяНакладная)
	
	Документы.ТК_Приложение2КНалоговойНакладной.СкопироватьТоварыИзНалоговойНакладной(Объект, НалоговаяНакладная);
	
КонецПроцедуры

#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ДоговорВзаиморасчетов");

	МассивРеквизитовОперации = Новый Массив;
	
	Если Объект.ХозОперация <> Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат Тогда 
		МассивРеквизитовОперации.Добавить("ДоговорВзаиморасчетов");
	КонецЕсли;
	
	ДенежныеСредстваСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивРеквизитовОперации);	
	
КонецПроцедуры


#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	НалоговаяПередИзменением = Объект.НалоговаяНакладная;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));

											
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
		//ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		//
		//ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);
		
	КонецЕсли;	
	
	
	
КонецПроцедуры

#КонецОбласти


&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура("СкладКомпании,НайтиПоТочномуСоответствию",Объект.СкладКомпании,Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора",ПараметрыФормы,Элементы.Товары,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
 	ОбработатьВыборНаСервере(ВыбранноеЗначение);         
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища) Экспорт
	
	
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Товары.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
		
		СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
		СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Объект));
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьСуммуРучнойСкидки", Ложь));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
		
		СтруктураДействий.Вставить("ЗаполнитьОстаток",Новый Структура("Дата,СкладКомпании",Объект.Дата,Объект.СкладКомпании));
		
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);

		
	КонецЦикла;
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры


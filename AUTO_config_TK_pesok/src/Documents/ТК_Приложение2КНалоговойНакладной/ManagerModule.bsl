#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);

	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровОрганизации(Запрос, ДополнительныеСвойства);
	

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Док.Дата КАК Период,
	               |	Док.Ссылка КАК Ссылка,
	               |	Док.ХозОперация КАК ХозОперация,
	               |	Док.МоментВремени КАК МоментВремени,
	               |	Док.НалоговаяНакладная КАК НакладнаяКорректировки
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной КАК Док
	               |ГДЕ
	               |	Док.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", 				Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", 			Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", 				Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("НакладнаяКорректировки", Реквизиты.НакладнаяКорректировки);

	
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация); 	
	
	РегистрацияПоследовательности =  ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период);
	
	Если Реквизиты.ХозОперация = Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат Тогда
		ПроводитьПоОстаткамОрганизации = Ложь;	
	Иначе
		ПроводитьПоОстаткамОрганизации = РегистрацияПоследовательности;
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",ПроводитьПоОстаткамОрганизации);	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);	
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДокТовары.Ссылка КАК Ссылка,
	               |	ДокТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ДокТовары.ИзменениеКоличество КАК Количество,
	               |	ДокТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ДокТовары
	               |ГДЕ
	               |	ДокТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииОрганизаций");	


	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("Организация");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.Организация 	   = Ссылка.Организация;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;

	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Процедура СкопироватьТоварыИзНалоговойНакладной(Объект, ДокументНН) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_НалоговаяНакладнаяТовары.Коэффициент КАК Коэффициент,
	               |	ТК_НалоговаяНакладнаяТовары.Количество КАК Количество,
	               |	ТК_НалоговаяНакладнаяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_НалоговаяНакладнаяТовары.Цена КАК Цена,
	               |	ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.Сумма КАК Сумма,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаВсего КАК СуммаВсего
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументНН);	
	тзТовары = Запрос.Выполнить().Выгрузить();
	
	Объект.Товары.Загрузить(тзТовары);
	
КонецПроцедуры


Функция СтруктураДокументаПриложение2кНН(ДокументСсылка,ТолькоСтруктуру) Экспорт
	
	СтруктураПоказателей = Новый Структура();
	
	СтруктураПоказателей.Вставить("Приложение2кНН",   	Новый Структура());	// данные налоговой накладной
	СтруктураПоказателей.Приложение2кНН.Вставить("R",   Новый ТаблицаЗначений());  // данные табличной части
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG001");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG21");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG22");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG3S");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG32");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG4");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG4S");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG105_2S");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG5");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG6");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG7");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG8");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG008");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG009");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG010");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("RXXXXG11_10");
	СтруктураПоказателей.Приложение2кНН.R.Колонки.Добавить("ROWNUM");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_Приложение2КНалоговойНакладной.Дата КАК Дата,
		|	ТК_Приложение2КНалоговойНакладной.Организация КАК Организация,
		|	ТК_Приложение2КНалоговойНакладной.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ТК_Приложение2КНалоговойНакладной.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|			ТОГДА ""Неплатник""
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА ""Неплатник""
		|		ИНАЧЕ ТК_Приложение2КНалоговойНакладной.Контрагент.НаименованиеПолное
		|	КОНЕЦ КАК НазваниеКонтрагента,
		|	ТК_Приложение2КНалоговойНакладной.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ТК_Приложение2КНалоговойНакладной.ВалютаДокумента КАК ВалютаДокумента,
		|	ТК_Приложение2КНалоговойНакладной.ТипЦен КАК ТипЦен,
		|	ТК_Приложение2КНалоговойНакладной.КтоВыписалНалоговуюНакладную КАК КтоВыписалНалоговуюНакладную,
		|	ТК_Приложение2КНалоговойНакладной.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
		|	ВЫБОР
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|			ТОГДА ""100000000000""
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА ""100000000000""
		|		ИНАЧЕ ТК_Приложение2КНалоговойНакладной.Контрагент.ИННПлательщикаНДС
		|	КОНЕЦ КАК КодКонтрагента,
		|	ВЫБОР
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|			ТОГДА """"
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА """"
		|		ИНАЧЕ ТК_Приложение2КНалоговойНакладной.Контрагент.КодПоЕДРПОУ
		|	КОНЕЦ КАК ЕДРПОУКонтрагента,
		|	ВЫБОР
		|		КОГДА ТК_Приложение2КНалоговойНакладной.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТК_Приложение2КНалоговойНакладной.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|	КОНЕЦ КАК ЮрлицоКонтрагент,
		|	ТК_Приложение2КНалоговойНакладной.СуммаДокумента КАК СуммаДокумента,
		|	ТК_Приложение2КНалоговойНакладной.СуммаНДСДокумента КАК СуммаНДСДокумента,
		|	ТК_Приложение2КНалоговойНакладной.Номер КАК Номер,
		|	ТК_Приложение2КНалоговойНакладной.ТипПричиныНевыдачиПокупателю КАК ТипНеВыдачи,
		|	ТК_Приложение2КНалоговойНакладной.ХозОперация КАК ХозОперация,
		|	ТК_Приложение2КНалоговойНакладной.КтоВыписалНалоговуюНакладную.ФИО КАК КтоВыписалНалоговуюФио,
		|	ТК_Приложение2КНалоговойНакладной.НалоговаяНакладная.Дата КАК ДатаНН,
		|	ТК_Приложение2КНалоговойНакладной.НалоговаяНакладная.Номер КАК НомерНН,
		|	ТК_Приложение2КНалоговойНакладной.НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерииНН
		|ИЗ
		|	Документ.ТК_Приложение2КНалоговойНакладной КАК ТК_Приложение2КНалоговойНакладной
		|ГДЕ
		|	ТК_Приложение2КНалоговойНакладной.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТК_Приложение2КНалоговойНакладнойТовары.НомерСтроки КАК НомерСтроки,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура КАК Номенклатура,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Коэффициент КАК Коэффициент,
		|	ТК_Приложение2КНалоговойНакладнойТовары.КоличествоБазовое КАК Количество,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Цена КАК Цена,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СуммаНДС КАК СуммаНДС,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СуммаБезНалогов КАК СуммаБезНДС,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Сумма КАК Сумма,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СуммаСкидки КАК СуммаСкидки,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СуммаВсего КАК СуммаВсего,
		|	ТК_Приложение2КНалоговойНакладнойТовары.НомерСтрокиНН КАК НомерСтрокиНН,
		|	ТК_Приложение2КНалоговойНакладнойТовары.СтавкаНДС.Ставка КАК НДССтавка,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.КодУКТВЭД КАК КодУКТВЭД,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.Наименование2 КАК НоменклатураНаименование2,
		|	ТК_Приложение2КНалоговойНакладнойТовары.Номенклатура.СтранаПроисхождения <> &Украина КАК Импортированный,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеКоличество КАК ИзменениеКоличество,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеЦенаБезНалогов КАК ИзменениеЦенаБезНалогов,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
		|	ТК_Приложение2КНалоговойНакладнойТовары.ИзменениеСуммаНДС КАК ИзменениеСуммаНДС,
		|	ТК_Приложение2КНалоговойНакладнойТовары.КодПричины КАК КодПричины,
		|	ТК_Приложение2КНалоговойНакладнойТовары.НомерГруппы КАК НомерГруппы
		|ИЗ
		|	Документ.ТК_Приложение2КНалоговойНакладной.Товары КАК ТК_Приложение2КНалоговойНакладнойТовары
		|ГДЕ
		|	ТК_Приложение2КНалоговойНакладнойТовары.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Украина", Справочники.СтраныМира.НайтиПоКоду("804"));

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ШапкаДокумента = РезультатЗапроса[0].Выбрать();
	ШапкаДокумента.Следующий();
	
	СтруктураПоказателей.Вставить("Дата",ШапкаДокумента.Дата);
	СтруктураПоказателей.Вставить("Номер",ШапкаДокумента.Номер);
	
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	
	ТаблицаСумм = ТаблицаТовары.Скопировать(, "НДССтавка, ИзменениеСуммаБезНалогов, ИзменениеСуммаНДС");
	ТаблицаСумм.Свернуть("НДССтавка", "ИзменениеСуммаБезНалогов, ИзменениеСуммаНДС");
		
	Для Каждого Стр Из ТаблицаСумм Цикл 		
		ИзменениеСуммаБезНалогов = ОбщегоНазначенияТККлиентСервер.ОкруглитьСУчетомПогрешности(Стр.ИзменениеСуммаБезНалогов, 2);
		ИзменениеСуммаНДС 		 = ОбщегоНазначенияТККлиентСервер.ОкруглитьСУчетомПогрешности(Стр.ИзменениеСуммаНДС, 2);
		
		Стр.ИзменениеСуммаНДС = ИзменениеСуммаНДС;
		Стр.ИзменениеСуммаБезНалогов = ИзменениеСуммаБезНалогов;
		
		Если Стр.НДССтавка = 20 Тогда 
			СтруктураПоказателей.Приложение2кНН.Вставить("R02G9", ИзменениеСуммаНДС);
			СтруктураПоказателей.Приложение2кНН.Вставить("R01G9", ИзменениеСуммаБезНалогов);
		ИначеЕсли Стр.НДССтавка = 7 Тогда 
			СтруктураПоказателей.Приложение2кНН.Вставить("R02G111", ИзменениеСуммаНДС);	
			СтруктураПоказателей.Приложение2кНН.Вставить("R01G111", ИзменениеСуммаБезНалогов);	
		КонецЕсли;
	КонецЦикла;
	
	ИтогИзмНДС    	 = ОбщегоНазначенияТККлиентСервер.ОкруглитьСУчетомПогрешности(ТаблицаСумм.Итог("ИзменениеСуммаНДС"), 2);
	ИтогИзмСуммаБНДС = ОбщегоНазначенияТККлиентСервер.ОкруглитьСУчетомПогрешности(ТаблицаСумм.Итог("ИзменениеСуммаБезНалогов"), 2);	
	
	СтруктураПоказателей.Приложение2кНН.Вставить("R001G03", ИтогИзмНДС);
		
	РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация);
	                         
	ИсточникКодаПокупателя = Документы.ТК_НалоговаяНакладная.КодИсточникаНалоговогоНомера(ШапкаДокумента.ЕДРПОУКонтрагента, ШапкаДокумента.ЮрлицоКонтрагент);
	ИсточникКодаПродавца   = Документы.ТК_НалоговаяНакладная.КодИсточникаНалоговогоНомера(РеквизитыОрганизации.КодПоЕДРПОУ, РеквизитыОрганизации.ЮрЛицо);
	
	СтруктураПоказателей.Приложение2кНН.Вставить("HERPN0", "1");
	СтруктураПоказателей.Приложение2кНН.Вставить("R01G1", "");
	СтруктураПоказателей.Приложение2кНН.Вставить("R03G10S", "");
	СтруктураПоказателей.Приложение2кНН.Вставить("TIN", СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));			
	
	Если ШапкаДокумента.ХозОперация = Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат Тогда 				
		СтруктураПоказателей.Приложение2кНН.Вставить("HORIG1",	"1");
		СтруктураПоказателей.Приложение2кНН.Вставить("HTYPR",	"11");		
		СтруктураПоказателей.Приложение2кНН.Вставить("HKB",		"");
	ИначеЕсли ШапкаДокумента.ТипНеВыдачи = 2 ИЛИ  ШапкаДокумента.ТипНеВыдачи = 11 Тогда
		СтруктураПоказателей.Приложение2кНН.Вставить("HORIG1",	"1");
		СтруктураПоказателей.Приложение2кНН.Вставить("HTYPR",	Формат(ШапкаДокумента.ТипНеВыдачи,"ЧЦ=2; ЧВН="));		
		СтруктураПоказателей.Приложение2кНН.Вставить("HKB",		"");
	Иначе
		СтруктураПоказателей.Приложение2кНН.Вставить("HORIG1",	"");
		СтруктураПоказателей.Приложение2кНН.Вставить("HTYPR",	"");
		СтруктураПоказателей.Приложение2кНН.Вставить("HKB",		СокрЛП(ИсточникКодаПокупателя));
	КонецЕсли;
	
	ЧастиИмя =   ФизическиеЛицаКлиентСервер.ЧастиИмени(ШапкаДокумента.КтоВыписалНалоговуюФио);
	СтруктураПоказателей.Приложение2кНН.Вставить("HFILL",	ШапкаДокумента.Дата);
	СтруктураПоказателей.Приложение2кНН.Вставить("HNUM",	Документы.ТК_НалоговаяНакладная.ПолучитьНомерДокумента(ШапкаДокумента.Дата, ШапкаДокумента.НомерДляБухгалтерии));
	СтруктураПоказателей.Приложение2кНН.Вставить("HNUM1",	"");
	СтруктураПоказателей.Приложение2кНН.Вставить("HNUM2",	"");
	СтруктураПоказателей.Приложение2кНН.Вставить("HFBUY",	"");
	СтруктураПоказателей.Приложение2кНН.Вставить("HPODFILL", Формат(ШапкаДокумента.ДатаНН, "ДФ=ddMMyyyy"));
	СтруктураПоказателей.Приложение2кНН.Вставить("HPODNUM",  Документы.ТК_НалоговаяНакладная.ПолучитьНомерДокумента(ШапкаДокумента.ДатаНН, ШапкаДокумента.НомерДляБухгалтерииНН));
	СтруктураПоказателей.Приложение2кНН.Вставить("HPODNUM1", "");
	СтруктураПоказателей.Приложение2кНН.Вставить("HPODNUM2", "");
	
	СтруктураПоказателей.Приложение2кНН.Вставить("HNAMESEL",	СокрЛП(ШапкаДокумента.ОрганизацияНаименованиеПолное));
	СтруктураПоказателей.Приложение2кНН.Вставить("HNAMEBUY",	СокрЛП(ШапкаДокумента.НазваниеКонтрагента));
	СтруктураПоказателей.Приложение2кНН.Вставить("HKSEL",	СокрЛП(РеквизитыОрганизации.ИНН));
	СтруктураПоказателей.Приложение2кНН.Вставить("HKBUY",	СокрЛП(ШапкаДокумента.КодКонтрагента));
	СтруктураПоказателей.Приложение2кНН.Вставить("HBOS",	""+ЧастиИмя.Имя +" "+ЧастиИмя.Фамилия+"");
	СтруктураПоказателей.Приложение2кНН.Вставить("HKBOS",	СокрЛП(РеквизитыОрганизации.РуководительКод));
	СтруктураПоказателей.Приложение2кНН.Вставить("HTINSEL",	СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));
	СтруктураПоказателей.Приложение2кНН.Вставить("HTINBUY",	СокрЛП(ШапкаДокумента.ЕДРПОУКонтрагента));
	СтруктураПоказателей.Приложение2кНН.Вставить("HKS",		СокрЛП(ИсточникКодаПродавца));

	СтруктураПоказателей.Приложение2кНН.Вставить("R003G10S", "");	

	СуммаНДСРасчет = 0;
	
	Для каждого СтрокаТаблицы Из ТаблицаТовары Цикл
		
		СтрокаСтруктуры = СтруктураПоказателей.Приложение2кНН.R.Добавить();
		СтрокаСтруктуры.RXXXXG001   = СтрокаТаблицы.НомерСтрокиНН;
		СтрокаСтруктуры.RXXXXG21	= СтрокаТаблицы.КодПричины; //?(ЗначениеЗаполнено(СтрокаТаблицы.КодПричины), СтрокаТаблицы.КодПричины, СокрЛП(ШапкаДокумента.ТипНеВыдачи));		
		СтрокаСтруктуры.RXXXXG22	= СтрокаТаблицы.НомерГруппы;
		
		СтрокаСтруктуры.RXXXXG3S 	= ?(Не ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименование2), СтрокаТаблицы.НоменклатураНаименование2, СтрокаТаблицы.НоменклатураНаименование);
		СтрокаСтруктуры.RXXXXG4		= СтрЗаменить(СокрЛП(СтрокаТаблицы.КодУКТВЭД), " ", "");
		СтрокаСтруктуры.RXXXXG32	= ?(СтрокаТаблицы.Импортированный,"1","");
		//СтрокаСтруктуры.RXXXXG33	= "";
		
		СтрокаСтруктуры.RXXXXG4S	= СокрЛП(СтрокаТаблицы.ЕдиницаИзмерения);
		СтрокаСтруктуры.RXXXXG105_2S = СокрЛП(СтрокаТаблицы.ЕдиницаИзмеренияКод);
		
		СтрокаСтруктуры.RXXXXG5		= Формат(СтрокаТаблицы.ИзменениеКоличество, "ЧЦ=27; ЧДЦ=12; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.RXXXXG6		= Формат(СтрокаТаблицы.ЦенаБезНалогов, "ЧЦ=27; ЧДЦ=12; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.RXXXXG7 	= ""; 
		СтрокаСтруктуры.RXXXXG8		= Формат(СтрокаТаблицы.ИзменениеЦенаБезНалогов, "ЧЦ=27; ЧДЦ=12; ЧРД=.; ЧН=; ЧГ=");
				
		СтрокаСтруктуры.RXXXXG008  	= Формат(СтрокаТаблицы.НДССтавка,"ЧЦ=2; ЧН=");
		СтрокаСтруктуры.RXXXXG009	= "";		
		СтрокаСтруктуры.RXXXXG010 	= Формат(СтрокаТаблицы.ИзменениеСуммаБезНалогов,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=");
		СтрокаСтруктуры.RXXXXG11_10	= Формат(СтрокаТаблицы.ИзменениеСуммаНДС,  "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
		//СтрокаСтруктуры.RXXXXG011	= "";
		
		СтрокаСтруктуры.ROWNUM		= СтрокаТаблицы.НомерСтроки;
		
		//РасчСуммаНДС6Зн = Окр(НДСОбщегоНазначенияКлиентСервер.РассчитатьСуммуНДС(СтрокаТаблицы.СуммаБезНДСРегл, Ложь, СтрокаТаблицы.НДССтавка), 6 );
		//СтрокаСтруктуры.G11_10Регл     = РасчСуммаНДС6Зн;
		//СтрокаСтруктуры.G11_10Документ = СтрокаТаблицы.СуммаНДСРегл;
		//СуммаНДСРасчет = СуммаНДСРасчет + РасчСуммаНДС6Зн;
	КонецЦикла;

	
	Если ТолькоСтруктуру Тогда
		Возврат СтруктураПоказателей;
	КонецЕсли;
	
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.УстановитьСтроку("windows-1251");
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLAR");
	Схема = "J1201213.xsd";
	
	ОбъектXML.ЗаписатьНачалоАтрибута("xmlns:xsi"); 
	ОбъектXML.ЗаписатьТекст("http://www.w3.org/2001/XMLSchema-instance"); 
	ОбъектXML.ЗаписатьКонецАтрибута();
	
	ОбъектXML.ЗаписатьНачалоАтрибута("xsi:noNamespaceSchemaLocation");
	ОбъектXML.ЗаписатьТекст(Схема);
	ОбъектXML.ЗаписатьКонецАтрибута();
	
	Схема = СтрЗаменить(Схема, ".xsd", "");
	
	///DECLARHEAD	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLARHEAD");		
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.TIN, "TIN");
	ЗаписатьXML(ОбъектXML, Лев(Схема, 3), "C_DOC");
	ЗаписатьXML(ОбъектXML, Сред(Схема, 4, 3), "C_DOC_SUB");
	ЗаписатьXML(ОбъектXML, Прав(Схема, 2), "C_DOC_VER");
	ЗаписатьXML(ОбъектXML, "0", "C_DOC_TYPE");
	ЗаписатьXML(ОбъектXML, "21", "C_DOC_CNT");
	ЗаписатьXML(ОбъектXML, "20", "C_REG");
	ЗаписатьXML(ОбъектXML, "23", "C_RAJ");
	
	ЗаписатьXML(ОбъектXML, Месяц(СтруктураПоказателей.Дата),"PERIOD_MONTH");
	ЗаписатьXML(ОбъектXML, 1,"PERIOD_TYPE");
	ЗаписатьXML(ОбъектXML, Год(СтруктураПоказателей.Дата), "PERIOD_YEAR");
	ЗаписатьXML(ОбъектXML, "2033", "C_STI_ORIG");
	ЗаписатьXML(ОбъектXML, 1,"C_DOC_STAN");
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.Дата,"ДФ=ddMMyyyy"), "D_FILL");
	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARHEAD	
	
	///DECLARBODY	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLARBODY");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HERPN0, 	"HERPN0");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.R01G1, 	"R01G1"); 
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.R03G10S, "R03G10S");   
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HORIG1, 	"HORIG1");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HTYPR, 	"HTYPR");		
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.Приложение2кНН.HFILL,"ДФ=ddMMyyyy"), "HFILL");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HNUM, 	"HNUM");	//номер документа
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HNUM1, 	"HNUM1");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HPODFILL,"HPODFILL");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HPODNUM, "HPODNUM");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HPODNUM1, "HPODNUM1");
	//HPODNUM1
	//HPODNUM2
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HNAMESEL,"HNAMESEL");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HNAMEBUY,"HNAMEBUY");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HKSEL, 	"HKSEL");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HTINSEL, "HTINSEL");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HKS, 	"HKS");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HKBUY, 	"HKBUY");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HTINBUY,	"HTINBUY");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HKB, 	"HKB");
	
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.Приложение2кНН.R001G03, "ЧРД=.; ЧГ=0"), "R001G03");
	
	//20
	Если СтруктураПоказателей.Приложение2кНН.Свойство("R02G9") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.Приложение2кНН.R02G9, "ЧРД=.; ЧГ=0"), "R02G9"); //НДС20
	КонецЕсли;
	Если СтруктураПоказателей.Приложение2кНН.Свойство("R01G9") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.Приложение2кНН.R01G9,"ЧРД=.; ЧГ=0"), "R01G9"); //БНДС20	
	КонецЕсли;
	
	//7
	Если СтруктураПоказателей.Приложение2кНН.Свойство("R02G111") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.Приложение2кНН.R02G111,"ЧРД=.; ЧГ=0"), "R02G111");//НДС7	
	КонецЕсли;
	Если СтруктураПоказателей.Приложение2кНН.Свойство("R01G111") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.Приложение2кНН.R01G111,"ЧРД=.; ЧГ=0"), "R01G111");//БНДС7	
	КонецЕсли;	
		
	СоответствиеКолонок = Новый Соответствие;
	СоответствиеКолонок.Вставить("RXXXXG001");
	СоответствиеКолонок.Вставить("RXXXXG21");
	СоответствиеКолонок.Вставить("RXXXXG22");
	СоответствиеКолонок.Вставить("RXXXXG3S");
	СоответствиеКолонок.Вставить("RXXXXG32");
	СоответствиеКолонок.Вставить("RXXXXG4");
	СоответствиеКолонок.Вставить("RXXXXG4S");
	СоответствиеКолонок.Вставить("RXXXXG105_2S");
	СоответствиеКолонок.Вставить("RXXXXG5");
	СоответствиеКолонок.Вставить("RXXXXG6");
	СоответствиеКолонок.Вставить("RXXXXG7");
	СоответствиеКолонок.Вставить("RXXXXG8");
	СоответствиеКолонок.Вставить("RXXXXG008");
	СоответствиеКолонок.Вставить("RXXXXG009");
	СоответствиеКолонок.Вставить("RXXXXG010");
	СоответствиеКолонок.Вставить("RXXXXG11_10");	
	
	Для Каждого Параметр Из СоответствиеКолонок Цикл
		Номер = 0;
		Для Каждого Стр Из СтруктураПоказателей.Приложение2кНН.R Цикл					
			Значение = Строка(Стр[Параметр.Ключ]);			
			
			Номер = Номер + 1;
			ОбъектXML.ЗаписатьНачалоЭлемента(Параметр.Ключ);											
			ОбъектXML.ЗаписатьАтрибут("ROWNUM", Формат(Номер, "ЧРД=.; ЧГ=0"));
			ОбъектXML.ЗаписатьТекст(Значение);						
			ОбъектXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
	КонецЦикла;		
	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HBOS, "HBOS");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.Приложение2кНН.HKBOS, "HKBOS");	
	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARBODY	
	ОбъектXML.ЗаписатьКонецЭлемента(); //DECLAR
	
	СтрXML = ОбъектXML.Закрыть();	
	
	
	ПотокДанных = Новый ПотокВПамяти();
	
	ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных,"windows-1251");
	ЗаписьДанных.ЗаписатьСтроку(СтрXML,"windows-1251");
	ЗаписьДанных.Закрыть();
	
	ДВ = ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные();
	СтруктураПоказателей.Вставить("ДанныеXML",ДВ);

	СтруктураПоказателей.Вставить("ТекстXML",СтрXML);
	СтруктураПоказателей.Вставить("ИмяФайла",СокрЛП(Строка(ШапкаДокумента.Организация)) + "_" + Формат(ШапкаДокумента.Дата, "ДФ=yyyyMMdd")+"_"+ШапкаДокумента.Номер+"_П2кНН.xml"); 
	
	Возврат СтруктураПоказателей;

КонецФункции

Функция СформироватьПриложение2кНН(МассивДокументов,ТолькоСтруктуру=Ложь) Экспорт
	
	МассивВозврата = Новый Массив;
	
	
	Для Индекс = 0 ПО МассивДокументов.ВГраница() Цикл
		
		Документ = МассивДокументов[Индекс];
		
		СтруктруаДокумента = СтруктураДокументаПриложение2кНН(Документ, ТолькоСтруктуру);
		МассивВозврата.Добавить(Новый Структура("Документ,Выгрузка",Документ,СтруктруаДокумента));
		
	КонецЦикла;
	
	
	Возврат МассивВозврата;
	
КонецФункции


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры


#КонецОбласти	


#КонецЕсли
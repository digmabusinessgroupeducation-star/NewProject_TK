
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_НалоговаяНакладная") Тогда
		ЗаполнитьДокументНаОсновании_ТК_НалоговаяНакладная(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда
		ЗаполнитьДокументНаОсновании_ВозвратОтПокупателя(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;
	НалоговаяНакладная = Неопределено;
	
	НомерДляБухгалтерии = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

			
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,Организация);

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.Организация КАК Организация
		|ИЗ
		|	Документ.ТК_Приложение2КНалоговойНакладной КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;

	
	СуммаДокумента = Товары.Итог("ИзменениеСуммаБезНалогов");
	СуммаНДСДокумента = Товары.Итог("ИзменениеСуммаНДС");
	
	тНомерДляБухгалтерии = ОбщегоНазначенияТК.ПолучитьНомерНалоговогоДокументаДляБухгалтерии(Дата, Номер, Организация);

	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения И Не Отказ И НомерДляБухгалтерии<>тНомерДляБухгалтерии ИЛИ Не ЗначениеЗаполнено(НомерДляБухгалтерии)Тогда 
		НомерДляБухгалтерии = тНомерДляБухгалтерии;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;

	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", Организация <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ХозОперация = Справочники.ХозОперации.Приложение2КНалоговойНакладнойИтоговаяРозницаОблагаемыеОперацииВозврат Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорВзаиморасчетов");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ТК_Приложение2КНалоговойНакладной.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ТК_Приложение2КНалоговойНакладной.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	//
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПотребностиТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьПотребностьОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("Приложение2КНалоговойНакладнойПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

	
КонецПроцедуры





#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата				  = ТекущаяДата();
	Автор				  = Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	//ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
	СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
	ХозОперация			  = ПредопределенноеЗначение("Справочник.ХозОперации.Приложение2КНалоговойНакладной");
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ТК_НалоговаяНакладная(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(справочник.ХозОперации.Приложение2КНалоговойНакладной) КАК ХозОперация,
	               |	ТК_НалоговаяНакладная.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
	               |	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ТК_НалоговаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_НалоговаяНакладная.ТипЦен КАК ТипЦен,
	               |	ТК_НалоговаяНакладная.КурсДокумента КАК КурсДокумента,
	               |	ТК_НалоговаяНакладная.ТочкаДоставки КАК ТочкаДоставки,
	               |	ТК_НалоговаяНакладная.СкладКомпании КАК СкладКомпании,
	               |	ТК_НалоговаяНакладная.Ссылка КАК НалоговаяНакладная,
	               |	ТК_НалоговаяНакладная.КтоВыписалНалоговуюНакладную КАК КтоВыписалНалоговуюНакладную,
	               |	ТК_НалоговаяНакладная.ФормаРасчетов КАК ФормаРасчетов,
	               |	ТК_НалоговаяНакладная.УсловиеПродажи КАК УсловиеПродажи,
	               |	ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю КАК ТипПричиныНевыдачиПокупателю,
	               |	ТК_НалоговаяНакладная.ВключенаВЕдиныйРеестрНалоговыхНакладных КАК ВключенаВЕдиныйРеестрНалоговыхНакладных,
	               |	ТК_НалоговаяНакладная.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных КАК ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных,
	               |	ТК_НалоговаяНакладная.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_НалоговаяНакладная.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		СуммаВсего КАК СуммаВсего,
	               |		"""" КАК ДатаКорректировки,
	               |		НомерСтрокиНН КАК НомерСтрокиНН
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	ТК_НалоговаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		тзТовары = Выборка.Товары.Выгрузить();
		тзТовары.ЗаполнитьЗначения(Дата, "ДатаКорректировки");
		
		Товары.Загрузить(тзТовары);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ВозвратОтПокупателя(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(справочник.ХозОперации.Приложение2КНалоговойНакладной) КАК ХозОперация,
	               |	ТК_НалоговаяНакладная.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
	               |	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ТК_НалоговаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_НалоговаяНакладная.ТипЦен КАК ТипЦен,
	               |	ТК_НалоговаяНакладная.КурсДокумента КАК КурсДокумента,
	               |	ТК_НалоговаяНакладная.ТочкаДоставки КАК ТочкаДоставки,
	               |	ТК_НалоговаяНакладная.СкладКомпании КАК СкладКомпании,
	               |	ТК_НалоговаяНакладная.Ссылка КАК НалоговаяНакладная,
	               |	ТК_НалоговаяНакладная.КтоВыписалНалоговуюНакладную КАК КтоВыписалНалоговуюНакладную,
	               |	ТК_НалоговаяНакладная.ФормаРасчетов КАК ФормаРасчетов,
	               |	ТК_НалоговаяНакладная.УсловиеПродажи КАК УсловиеПродажи,
	               |	ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю КАК ТипПричиныНевыдачиПокупателю,
	               |	ТК_НалоговаяНакладная.ВключенаВЕдиныйРеестрНалоговыхНакладных КАК ВключенаВЕдиныйРеестрНалоговыхНакладных,
	               |	ТК_НалоговаяНакладная.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных КАК ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных,
	               |	ТК_НалоговаяНакладная.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерии
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя КАК ВозвратОтПокупателя
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |		ПО ВозвратОтПокупателя.ДокументОснование = ТК_НалоговаяНакладная.ДокументОснование
	               |ГДЕ
	               |	ВозвратОтПокупателя.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	               |	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_НалоговаяНакладнаяТовары.Коэффициент КАК Коэффициент,
	               |	ТК_НалоговаяНакладнаяТовары.Количество КАК Количество,
	               |	ТК_НалоговаяНакладнаяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_НалоговаяНакладнаяТовары.Цена КАК Цена,
	               |	ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ТК_НалоговаяНакладнаяТовары.Сумма КАК Сумма,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ТК_НалоговаяНакладнаяТовары.СуммаВсего КАК СуммаВсего,
	               |	"""" КАК ДатаКорректировки,
	               |	-ВозвратОтПокупателяТовары.Количество КАК ИзменениеКоличество,
	               |	-ВозвратОтПокупателяТовары.СуммаБезНалогов КАК ИзменениеСуммаБезНалогов,
	               |	-ВозвратОтПокупателяТовары.СуммаНДС КАК ИзменениеСуммаНДС,
	               |	ВЫБОР
	               |		КОГДА ТК_НалоговаяНакладнаяТовары.Количество = ВозвратОтПокупателяТовары.Количество
	               |			ТОГДА 103
	               |		ИНАЧЕ 102
	               |	КОНЕЦ КАК КодПричины,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН КАК НомерСтрокиНН,
	               |	ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН КАК НомерГруппы
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратОтПокупателя.Товары КАК ВозвратОтПокупателяТовары
	               |		ПО ТК_НалоговаяНакладнаяТовары.Ссылка.ДокументОснование = ВозвратОтПокупателяТовары.Ссылка.ДокументОснование
	               |			И ТК_НалоговаяНакладнаяТовары.Номенклатура = ВозвратОтПокупателяТовары.Номенклатура
	               |ГДЕ
	               |	ВозвратОтПокупателяТовары.Ссылка.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка1 = МассивРезультатов[0].Выбрать();
	
	Если Выборка1.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка1);
	КонецЕсли;
	
	тзТовары = МассивРезультатов[1].Выгрузить();
	тзТовары.ЗаполнитьЗначения(Дата, "ДатаКорректировки");
		
	Товары.Загрузить(тзТовары);
		
КонецПроцедуры

#КонецОбласти






#КонецЕсли
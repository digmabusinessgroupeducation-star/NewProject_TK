

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Проведение
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос = Новый Запрос;
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		//ХозОперация = ДополнительныеСвойства.ДляПроведения.ХозОперация;
		//ЭтоНачисление = ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.НачислениеКоммунальныхУслуг");
		
		ТекстыЗапроса = Новый СписокЗначений;
		ТекстЗапросаТаблицаПредыдущиеПоказанияСчетчиков(Запрос, ТекстыЗапроса, Регистры);
		
		Если ДополнительныеСвойства.ДляПроведения.ЭтоНачисление Тогда 
			ТекстЗапросаТаблицаНачисленияКоммунальныхУслуг(Запрос, ТекстыЗапроса, Регистры);
			ТекстЗапросаТаблицаЗатратыОбъектов(Запрос, ТекстыЗапроса, Регистры);
		КонецЕсли;
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
	КонецПроцедуры
	
	Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
		
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.Текст = "ВЫБРАТЬ
		|	НачислениеКоммунальныхУслуг.Дата КАК Период,
		|	НачислениеКоммунальныхУслуг.Ссылка КАК Ссылка,
		|	НачислениеКоммунальныхУслуг.ХозОперация КАК ХозОперация,
		|	НачислениеКоммунальныхУслуг.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	НачислениеКоммунальныхУслуг.МоментВремени КАК МоментВремени,
		|	НачислениеКоммунальныхУслуг.ОтчетныйМесяц КАК ОтчетныйМесяц
		|ИЗ
		|	Документ.НачислениеКоммунальныхУслуг КАК НачислениеКоммунальныхУслуг
		|ГДЕ
		|	НачислениеКоммунальныхУслуг.Ссылка = &Ссылка";
		
		Реквизиты = Запрос.Выполнить().Выбрать();
		Реквизиты.Следующий();
		
		Запрос.УстановитьПараметр("Период",Реквизиты.Период);
		Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
		Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
		Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ОтчетныйМесяц",Реквизиты.ОтчетныйМесяц);
		Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
		
		ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
		ДополнительныеСвойства.ДляПроведения.Вставить("ОтчетныйМесяц",Реквизиты.ОтчетныйМесяц);
		ДополнительныеСвойства.ДляПроведения.Вставить("ЭтоНачисление",Реквизиты.ХозОперация = Справочники.ХозОперации.НачислениеКоммунальныхУслуг);
		
	КонецПроцедуры
	
	Функция ТекстЗапросаТаблицаПредыдущиеПоказанияСчетчиков(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ПредыдущиеПоказанияСчетчиков";
		
		Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Период,
		|	НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		|	НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	НачислениеКоммунальныхУслугУслуги.Счетчик КАК Счетчик,
		|	НачислениеКоммунальныхУслугУслуги.ПоказанияТекущие КАК Показания
		|ИЗ
		|	Документ.НачислениеКоммунальныхУслуг.Показания КАК НачислениеКоммунальныхУслугУслуги
		|ГДЕ
		|	НачислениеКоммунальныхУслугУслуги.Ссылка = &Ссылка";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаТаблицаНачисленияКоммунальныхУслуг(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "НачисленияКоммунальныхУслуг";
		
		Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Период,
		|	НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		|	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
		|	НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	НачислениеКоммунальныхУслугУслуги.СубПотребитель КАК СубПотребитель,
		|	НачислениеКоммунальныхУслугУслуги.Тариф КАК Тариф,
		|	НачислениеКоммунальныхУслугУслуги.Потери КАК Потери,
		|	ЕСТЬNULL(ВложенныйЗапросПотребление.Потребление, 0) КАК ПотреблениеФакт,
		|	ЕСТЬNULL(ВложенныйЗапросПотребление.Потребление, 0) * НачислениеКоммунальныхУслугУслуги.Тариф КАК СуммаФакт,
		|	НачислениеКоммунальныхУслугУслуги.ПотреблениеАкт КАК ПотреблениеАкт,
		|	НачислениеКоммунальныхУслугУслуги.ПотреблениеИтого КАК ПотреблениеИтого,
		|	НачислениеКоммунальныхУслугУслуги.Начислено КАК Начислено,
		|	НачислениеКоммунальныхУслугУслуги.Абонплата КАК Абонплата,
		|	НачислениеКоммунальныхУслугУслуги.Предоплата КАК Предоплата,
		|	ВЫБОР
		|		КОГДА НачислениеКоммунальныхУслугУслуги.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
		|			ТОГДА -НачислениеКоммунальныхУслугУслуги.СуммаКОплате
		|		ИНАЧЕ НачислениеКоммунальныхУслугУслуги.СуммаКОплате
		|	КОНЕЦ КАК СуммаКОплате,
		|	НачислениеКоммунальныхУслугУслуги.Комиссия КАК Комиссия,
		|	НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц КАК ОтчетныйМесяц
		|ИЗ
		|	Документ.НачислениеКоммунальныхУслуг.Услуги КАК НачислениеКоммунальныхУслугУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НачислениеКоммунальныхУслугПоказания.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|			СУММА(НачислениеКоммунальныхУслугПоказания.Потребление) КАК Потребление
		|		ИЗ
		|			Документ.НачислениеКоммунальныхУслуг.Показания КАК НачислениеКоммунальныхУслугПоказания
		|		ГДЕ
		|			НачислениеКоммунальныхУслугПоказания.Ссылка = &Ссылка
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НачислениеКоммунальныхУслугПоказания.ТорговаяПлощадка) КАК ВложенныйЗапросПотребление
		|		ПО НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка = ВложенныйЗапросПотребление.ТорговаяПлощадка
		|ГДЕ
		|	НачислениеКоммунальныхУслугУслуги.Ссылка = &Ссылка
		|	И НачислениеКоммунальныхУслугУслуги.СубПотребитель = НЕОПРЕДЕЛЕНО";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаТаблицаЗатратыОбъектов(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ЗатратыОбъектов";
		
		Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Период,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		               |	НачислениеКоммунальныхУслугУслуги.СубПотребитель КАК ТорговаяПлощадка,
		               |	взОсн.ВидКоммунальнойУслуги КАК ВидКоммунальнойУслуги,
		               |	СУММА(НачислениеКоммунальныхУслугУслуги.СуммаЗатрат) КАК СуммаЗатрат,
		               |	МАКСИМУМ(НачислениеКоммунальныхУслугУслуги.ПотреблениеФакт) КАК ПотреблениеФакт,
		               |	СУММА(НачислениеКоммунальныхУслугУслуги.СуммаФакт) КАК СуммаФакт,
		               |	СУММА(НачислениеКоммунальныхУслугУслуги.Тариф) КАК Тариф,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц КАК ОтчетныйМесяц
		               |ИЗ
		               |	Документ.НачислениеКоммунальныхУслуг.Услуги КАК НачислениеКоммунальныхУслугУслуги
		               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Период,
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		               |			НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |			НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.ВидКоммунальнойУслуги КАК ВидКоммунальнойУслуги,
		               |			СУММА(НачислениеКоммунальныхУслугУслуги.СуммаЗатрат) КАК СуммаЗатрат,
		               |			МАКСИМУМ(НачислениеКоммунальныхУслугУслуги.ПотреблениеФакт) КАК ПотреблениеФакт,
		               |			СУММА(НачислениеКоммунальныхУслугУслуги.СуммаФакт) КАК СуммаФакт,
		               |			СУММА(НачислениеКоммунальныхУслугУслуги.Тариф) КАК Тариф,
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц КАК ОтчетныйМесяц
		               |		ИЗ
		               |			Документ.НачислениеКоммунальныхУслуг.Услуги КАК НачислениеКоммунальныхУслугУслуги
		               |		ГДЕ
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка = &Ссылка
		               |			И НачислениеКоммунальныхУслугУслуги.СубПотребитель = НЕОПРЕДЕЛЕНО
		               |		
		               |		СГРУППИРОВАТЬ ПО
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка.Дата,
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка,
		               |			НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка,
		               |			НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.ВидКоммунальнойУслуги,
		               |			НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц) КАК взОсн
		               |		ПО НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка = взОсн.ТорговаяПлощадка
		               |ГДЕ
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка = &Ссылка
		               |	И НачислениеКоммунальныхУслугУслуги.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.Дата,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка,
		               |	НачислениеКоммунальныхУслугУслуги.СубПотребитель,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц,
		               |	взОсн.ВидКоммунальнойУслуги
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	взОсн.Период,
		               |	взОсн.Ссылка,
		               |	взОсн.ТорговаяПлощадка,
		               |	взОсн.ВидКоммунальнойУслуги,
		               |	взОсн.СуммаЗатрат,
		               |	взОсн.ПотреблениеФакт,
		               |	взОсн.СуммаФакт,
		               |	взОсн.Тариф,
		               |	взОсн.ОтчетныйМесяц
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Период,
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		               |		НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка КАК ТорговаяПлощадка,
		               |		НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.ВидКоммунальнойУслуги КАК ВидКоммунальнойУслуги,
		               |		СУММА(НачислениеКоммунальныхУслугУслуги.СуммаЗатрат) КАК СуммаЗатрат,
		               |		МАКСИМУМ(НачислениеКоммунальныхУслугУслуги.ПотреблениеФакт) КАК ПотреблениеФакт,
		               |		СУММА(НачислениеКоммунальныхУслугУслуги.СуммаФакт) КАК СуммаФакт,
		               |		СУММА(НачислениеКоммунальныхУслугУслуги.Тариф) КАК Тариф,
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц КАК ОтчетныйМесяц
		               |	ИЗ
		               |		Документ.НачислениеКоммунальныхУслуг.Услуги КАК НачислениеКоммунальныхУслугУслуги
		               |	ГДЕ
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка = &Ссылка
		               |		И НачислениеКоммунальныхУслугУслуги.СубПотребитель = НЕОПРЕДЕЛЕНО
		               |	
		               |	СГРУППИРОВАТЬ ПО
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка.Дата,
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка,
		               |		НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка,
		               |		НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.ВидКоммунальнойУслуги,
		               |		НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц) КАК взОсн";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Процедура ОтразитьПредыдущиеПоказанияСчетчиков(ДополнительныеСвойства, Движения, Отказ) Экспорт
		
		Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПредыдущиеПоказанияСчетчиков;
		
		Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Движения.ПредыдущиеПоказанияСчетчиков.Записывать = Истина;
		Движения.ПредыдущиеПоказанияСчетчиков.Загрузить(Таблица);	
		
	КонецПроцедуры
	
	Процедура ОтразитьНачисленияКоммунальныхУслуг(ДополнительныеСвойства, Движения, Отказ) Экспорт
		
		Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаНачисленияКоммунальныхУслуг;
		
		Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Движения.НачисленияКоммунальныхУслуг.Записывать = Истина;
		Движения.НачисленияКоммунальныхУслуг.Загрузить(Таблица);	
		
	КонецПроцедуры
	
	Процедура ОтразитьЗатратыОбъектов(ДополнительныеСвойства, Движения, Отказ) Экспорт
		
		Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗатратыОбъектов;
		
		Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Движения.ЗатратыОбъектов.Записывать = Истина;
		Движения.ЗатратыОбъектов.Загрузить(Таблица);	
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	
	
	
	#Область ПрограммныйИнтерфейс
	
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		// Перемещение товаров
		КомандаПечати = КомандыПечати.Добавить(); 
		КомандаПечати.МенеджерПечати = "Документ.НачислениеКоммунальныхУслуг";
		КомандаПечати.Идентификатор = "СведенияОРасходеЭлектроэнергииХарьков";
		КомандаПечати.Представление = НСтр("ru = 'Сведения о расходе Харьковоблэнерго';uk='Відомості про витрату Харківобленерго'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
		
		//// Перемещение товаров 2 шт.
		//КомандаПечати = КомандыПечати.Добавить(); 
		//КомандаПечати.МенеджерПечати = "Документ.НачислениеКоммунальныхУслуг";
		//КомандаПечати.Идентификатор = "ПеремещениеТоваровДвойная";
		//КомандаПечати.Представление = НСтр("ru = 'Перемещение товаров 2 шт. на лист';uk='Переміщення товарів 2 шт. на лист'");
		//КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
		//
		//// Перемещение товаров Вендинг
		//КомандаПечати = КомандыПечати.Добавить(); 
		//КомандаПечати.МенеджерПечати = "Документ.НачислениеКоммунальныхУслуг";
		//КомандаПечати.Идентификатор = "ПеремещениеТоваровВендинг";
		//КомандаПечати.Представление = НСтр("ru = 'Перемещение товаров Вендинг';uk='Переміщення товарів Вендінг'");
		//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		//МассивУсловийРН = Новый Массив;
		//МассивУсловийРН.Добавить(Новый Структура("Реквизит, ВидСравнения, Значение", 
		//"СкладКомпании.ИД_Автомата",
		//ПредопределенноеЗначение("ВидСравнения.Больше"),
		//0));		
		////КомандаПечати.УсловияВидимости.Добавить(Новый Структура("Реквизит,ВидСравнения","СкладКомпании.ИД_Автомата",ВидСравненияКомпоновкиДанных.Заполнено)); 		
		//КомандаПечати.УсловияВидимости=МассивУсловийРН; 		
		
	КонецПроцедуры
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(ПодразделениеКомпании)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	
	#КонецОбласти	
	
	
	#Область Печать
	
	// Формирует печатные формы.
	//
	// Параметры:
	//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
	//  ПараметрыПечати - Структура - дополнительные настройки печати;
	//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
	//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
	//                                            представление - имя области, в которой был выведен объект (выходной параметр);
	//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если ТипЗнч(ПараметрыПечати) <> Тип("Структура") Тогда 
			ПараметрыПечати = Новый Структура;
		КонецЕсли;
		
		// Перемещение товаров
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СведенияОРасходеЭлектроэнергииХарьков") Тогда    
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СведенияОРасходеЭлектроэнергииХарьков",
			НСтр("ru = 'Сведения о расходе Харьковоблэнерго';uk='Відомості про витрату Харківобленерго'"), 
			ПечатьСведенияОРасходеЭлектроэнергииХарьков(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.НачислениеКоммунальныхУслуг.СведенияОРасходеЭлектроэнергииХарьков");
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПеремещениеТоваровДвойная") Тогда    
			
			ПараметрыПечати.Вставить("Порядок", "Сигареты");
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПеремещениеТоваровДвойная",
			НСтр("ru = 'Перемещение товаров 2 шт. на лист';uk='Переміщення товарів 2 шт. на лист'"), 
			ПечатьПеремещениеТоваровДвойная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваров");
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПеремещениеТоваровВендинг") Тогда    
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПеремещениеТоваровВендинг",
			НСтр("ru = 'Перемещение товаров Вендинг';uk='Переміщення товарів Вендінг'"), 
			ПечатьПеремещениеТоваровВендинг(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваровВендинг");
			
		КонецЕсли;	
		
	КонецПроцедуры
	
	// СведенияОРасходеЭлектроэнергииХарьков
	Функция ПечатьСведенияОРасходеЭлектроэнергииХарьков(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)  Экспорт
		
		усХарьковЭнергоСбыт = Справочники.КоммунальныеУслуги.НайтиПоКоду("000000012");
		кХарьковЭнергоСбыт = Справочники.Контрагенты.НайтиПоКоду("42206328     ");
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка КАК Ссылка,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.Дата КАК Дата,
		               |	НачислениеКоммунальныхУслугУслуги.Ссылка.ОтчетныйМесяц КАК ОтчетныйМесяц,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Договор КАК Договор,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Договор.Номер КАК НомерДоговора,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Договор.Организация КАК Организация,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Договор.Организация.Наименование КАК ОрганизацияНаименование,
		               |	НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Контрагент.Наименование КАК КонтрагентНаименование,
		               |	НачислениеКоммунальныхУслугПоказания.Счетчик КАК Счетчик,
		               |	НачислениеКоммунальныхУслугПоказания.Счетчик.ЗаводскойНомер КАК ЗаводскойНомер,
		               |	НачислениеКоммунальныхУслугПоказания.Счетчик.ЛицевойСчет КАК ЛицевойСчет,
		               |	НачислениеКоммунальныхУслугПоказания.ПоказанияПредыдущие КАК ПоказанияПредыдущие,
		               |	НачислениеКоммунальныхУслугПоказания.ПоказанияТекущие КАК ПоказанияТекущие,
		               |	НачислениеКоммунальныхУслугПоказания.Потребление КАК Потребление
		               |ИЗ
		               |	Документ.НачислениеКоммунальныхУслуг.Услуги КАК НачислениеКоммунальныхУслугУслуги
		               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачислениеКоммунальныхУслуг.Показания КАК НачислениеКоммунальныхУслугПоказания
		               |		ПО НачислениеКоммунальныхУслугУслуги.ТорговаяПлощадка = НачислениеКоммунальныхУслугПоказания.ТорговаяПлощадка
		               |			И НачислениеКоммунальныхУслугУслуги.Ссылка = НачислениеКоммунальныхУслугПоказания.Ссылка
		               |ГДЕ
		               |	НачислениеКоммунальныхУслугПоказания.Ссылка В(&МассивСсылок)
		               |	И НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга.Договор.Контрагент = &Контрагент
		               |	И НачислениеКоммунальныхУслугУслуги.КоммунальнаяУслуга В ИЕРАРХИИ(&КоммунальнаяУслуга)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ЛицевойСчет
		               |ИТОГИ
		               |	МАКСИМУМ(Ссылка),
		               |	МАКСИМУМ(Дата),
		               |	МАКСИМУМ(ОтчетныйМесяц),
		               |	СУММА(Потребление)
		               |ПО
		               |	Договор";
		
		
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		Запрос.УстановитьПараметр("КоммунальнаяУслуга", усХарьковЭнергоСбыт);	
		Запрос.УстановитьПараметр("Контрагент", кХарьковЭнергоСбыт);	
		
		Результатзапроса = Запрос.Выполнить();
		ВыборкаДоговор = Результатзапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СведенияОРасходеЭлектроэнергииХарьков";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		//КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		КодЯзыкаПечать = "ru";
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.НачислениеКоммунальныхУслуг.СведенияОРасходеЭлектроэнергииХарьков");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");	
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
		
		ПервыйДокумент = Истина;
		Пока ВыборкаДоговор.Следующий() Цикл
			
			СтруктураШапки = Новый Структура;
			СтруктураШапки.Вставить("Период", Формат(ВыборкаДоговор.ОтчетныйМесяц,"Л="+КодЯзыкаПечать+"; ДФ='MMMM yyyy'"));
			СтруктураШапки.Вставить("Организация", ВыборкаДоговор.ОрганизацияНаименование);		
			СтруктураШапки.Вставить("Адрес", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДоговор.Организация, "ЮридическийАдресОрганизации",, ТекущаяДата()));		
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДоговор);
			
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
			
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			нпп = 0;
			Выборка = ВыборкаДоговор.Выбрать();
			Пока Выборка.Следующий() Цикл
				нпп = нпп + 1;			
				
				СтруктураСтроки = Новый Структура("НомерДоговора, ЛицевойСчет, ЗаводскойНомер, ПоказанияТекущие, ПоказанияПредыдущие, Потребление");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, Выборка);			
				
				СтруктураСтроки.Вставить("нпп", нпп);
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
			КонецЦикла;
			
			
			ОбластьИтого.Параметры.Потребление = ВыборкаДоговор.Потребление;
			ТабличныйДокумент.Вывести(ОбластьИтого);		
			
			Руководитель = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель, ВыборкаДоговор.Организация, ВыборкаДоговор.Дата);
			
			СтруктурПодписи =  Новый Структура();
			СтруктурПодписи.Вставить("Директор", Руководитель);		
			СтруктурПодписи.Вставить("Поставщик", ВыборкаДоговор.КонтрагентНаименование);		
			
			ОбластьПодписи.Параметры.Заполнить(СтруктурПодписи);
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДоговор.Ссылка);
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
	КонецФункции
	
	// Перемещение товаров 2 шт.
	Функция ПечатьПеремещениеТоваровДвойная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)  Экспорт
		
		Порядок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
		ДатаПечати	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ДатаПечати", Неопределено);
		Бутыли = ТК_СправочникиПовтИсп.ЗначенияСвойства("ЕмкостьБутыля").ВыгрузитьКолонку("Объект");
		
		Если Порядок = "Сигареты" Тогда 
			СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
			|	ПорядокСигаретный,ТорговаяПлощадкаПолучатель";
		ИначеЕсли Порядок = "Пиво" Тогда 
			СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
			|	ПорядокПивной,ТорговаяПлощадкаПолучатель";
		Иначе
			СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
			|	Дата,ТорговаяПлощадкаПолучатель";
		КонецЕсли;		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПеремещениеТоваров.Ссылка КАК Ссылка,
		|	ПеремещениеТоваров.Номер КАК Номер,
		|	ПеремещениеТоваров.Дата КАК Дата,
		|	ПеремещениеТоваров.ХозОперация КАК ХозОперация,
		|	ПеремещениеТоваров.Организация КАК Организация,
		|	ПеремещениеТоваров.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоЕДРПОУ,
		|	ПеремещениеТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
		|	ПеремещениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ПеремещениеТоваров.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
		|	ПеремещениеТоваров.СкладКомпании КАК ПредставлениеОтправителя,
		|	ПеремещениеТоваров.СкладКомпанииПолучатель КАК ПредставлениеПолучателя,
		|	ПеремещениеТоваров.СуммаДокумента КАК СуммаДокумента,
		|	ПеремещениеТоваров.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваров.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки.Порядок, 0)
		|		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваров.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки.Порядок, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПорядокПивной,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваров.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки.ПорядокСигаретный, 0)
		|		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваров.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки.ПорядокСигаретный, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПорядокСигаретный,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|			ТОГДА ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки
		|		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваров.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки
		|		КОГДА ПеремещениеТоваров.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ПеремещениеТоваров.СкладКомпанииПолучатель.ТочкаДоставки
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ТочкаДоставки,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваров.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИЗФилиала)
		|			ТОГДА ПеремещениеТоваров.СкладКомпании.ТочкаДоставки
		|		КОГДА ПеремещениеТоваров.ДокументОснование ССЫЛКА Документ.ПеремещениеТоваров
		|				И ПеремещениеТоваров.ДокументОснование.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|			ТОГДА ВЫРАЗИТЬ(ПеремещениеТоваров.ДокументОснование КАК Документ.ПеремещениеТоваров).СкладКомпании.ТочкаДоставки
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АдресОтправителя,
		|	ПеремещениеТоваров.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура.Артикул КАК Артикул,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
		|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Количество КАК Количество,
		|		КоличествоБазовое КАК КоличествоБазовое,
		|		Цена КАК Цена,
		|		Сумма КАК СуммаВсего,
		|		ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			И НЕ ПеремещениеТоваров.Товары.Номенклатура В (&Бутыли) КАК ЭтоТара,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ПеремещениеТоваров.Товары.ЕдиницаИзмерения
		|			ИНАЧЕ ПеремещениеТоваров.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки
		|		КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ПеремещениеТоваров.Товары.Коэффициент
		|			ИНАЧЕ ЕСТЬNULL(ПеремещениеТоваров.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0)
		|		КОНЕЦ КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ПеремещениеТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|		КОНЕЦ КАК ОсновнаяЕдиницаИзмерения,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ЕСТЬNULL(ПеремещениеТоваров.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ПеремещениеТоваров.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков
		|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|		КОНЕЦ КАК ЕдиницаИзмеренияЯщиков,
		|		ВЫБОР
		|			КОГДА ПеремещениеТоваров.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|				ТОГДА ЕСТЬNULL(ПеремещениеТоваров.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ЕдиницаИзмеренияЯщиковКоэффициент
		|	) КАК Товары
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
		|ГДЕ
		|	ПеремещениеТоваров.Ссылка В(&МассивСсылок)" + СортировкаЗапроса;
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		Запрос.УстановитьПараметр("Бутыли", Бутыли);	
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПеремещениеТоваровДвойная";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваровДвойная");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовокВФилиал = Макет.ПолучитьОбласть("ЗаголовокВФилиал");	
		ОбластьЗаголовокТара	= Макет.ПолучитьОбласть("ЗаголовокТара");	
		ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
		ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
		ОбластьРазделитель		= Макет.ПолучитьОбласть("Разделитель");
		
		ПервыйДокумент = Истина;
		Пока ВыборкаДокументов.Следующий() Цикл
			
			ЭтоПеремещениеВФилиал = ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
			
			ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перемещение товаров №%1 от %2'; uk = 'Переміщення товарів №%1 від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(?(ЗначениеЗаполнено(ДатаПечати),ДатаПечати,ВыборкаДокументов.Дата), "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
			
			СтруктураШапки = Новый Структура;
			СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
			СтруктураШапки.Вставить("ТекстЗаголовкаТара", НСтр("ru = 'Возврат тары к '; uk = 'Повернення тари до '") + ЗаголовокЛок);		
			СтруктураШапки.Вставить("ПредставлениеПоставщика", "");		
			СтруктураШапки.Вставить("ПредставлениеОтправителя", "");
			//СтруктураШапки.Вставить("АдресОтправителя", "");						
			
			НомерМаршрута = 0;
			Если Порядок = "Пиво" Тогда 
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной/1000);
			Иначе
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный/100);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НомерМаршрута) Тогда 
				СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			СтруктураШапки.Вставить("АдресПоставщика", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.Организация, "ЮридическийАдресОрганизации",, ТекущаяДата()));		
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ПредставлениеПолучателя) Тогда
				СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ПредставлениеПолучателя);
			ИначеЕсли ЗначениеЗаполнено(ВыборкаДокументов.ТорговаяПлощадкаПолучатель) Тогда 
				СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ТорговаяПлощадкаПолучатель);	
			Иначе
				СтруктураШапки.Вставить("ПредставлениеПолучателя", "");						
			КонецЕсли;
			
			//Если ВыборкаДокументов.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал И ЗначениеЗаполнено(
			Если ЗначениеЗаполнено(ВыборкаДокументов.АдресОтправителя) Тогда 
				СтруктураШапки.Вставить("АдресОтправителя", ВыборкаДокументов.АдресОтправителя);
			ИначеЕсли ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда 
				СтруктураШапки.Вставить("АдресОтправителя", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадка,"ФактическийАдресТорговойПлощадки",,ТекущаяДата()));
			КонецЕсли;				
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда
				СтруктураШапки.Вставить("АдресПолучателя", ВыборкаДокументов.ТочкаДоставки);
			ИначеЕсли ЭтоПеремещениеВФилиал Тогда
				СтруктураШапки.Вставить("АдресПолучателя",УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадкаПолучатель,"ФактическийАдресТорговойПлощадки",,ТекущаяДата()));
			КонецЕсли;
			
			
			
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
			
			Если ЭтоПеремещениеВФилиал Тогда 
				
				ИДД = "";
				Если НЕ ПустаяСтрока(ВыборкаДокументов.ИДДокументаОтгрузки) Тогда 
					ИДД = ВыборкаДокументов.ИДДокументаОтгрузки;
				Иначе
					ИДД = Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор());
				КонецЕсли;
				
				Попытка
					ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(ИДД,1,125);
					ОбластьЗаголовокВФилиал.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
					ОбластьЗаголовокВФилиал.Рисунки.QRКод1.Картинка = Новый Картинка(ДанныеQRКода);
				Исключение КонецПопытки;
				
				ОбластьЗаголовокВФилиал.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаголовокВФилиал);
				
			Иначе
				
				ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаголовок);
				
			КонецЕсли;
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			нпп 			= 0;
			КоличествоМест 	= 0;
			СуммаВсего 		= 0;
			СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
			ЕстьТара 		= Ложь;
			
			ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				нпп = нпп + 1;			
				
				Если Не ЕстьТара И ЭтоПеремещениеВФилиал И ВыборкаТовары.ЭтоТара Тогда 
					ЕстьТара = Истина;
				КонецЕсли;
				
				СтруктураСтроки = Новый Структура("Номенклатура, Цена, СуммаВсего");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
				
				СтруктураСтроки.Вставить("НомерСтроки", нпп);
				
				стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
				СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
				СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
				
				КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
				ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
				ВыборкаТовары.Номенклатура,
				ВыборкаТовары.КоличествоБазовое,
				ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
				ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
				СтруктураИтоговМест);
				
				СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
			КонецЦикла;
			
			СтруктураПодвала = Новый Структура;	
			СтруктураПодвала.Вставить("Всего", СуммаВсего);		
			СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
			
			ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтого);		
			
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			Если ЕстьТара Тогда 
				
				ТабличныйДокумент.Вывести(ОбластьРазделитель);			
				ОбластьЗаголовокТара.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТара);
				ТабличныйДокумент.Вывести(ОбластьПоставщик);
				ТабличныйДокумент.Вывести(ОбластьПокупатель);
				ТабличныйДокумент.Вывести(ОбластьШапка);
				
				СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
				нпп = 0;
				ВыборкаТовары.Сбросить();
				Пока ВыборкаТовары.Следующий() Цикл 
					Если Не ВыборкаТовары.ЭтоТара Тогда 
						Продолжить;
					КонецЕсли;
					
					нпп = нпп + 1;
					
					СтруктураСтроки = Новый Структура("Номенклатура, КоличествоПредставление, Цена, СуммаВсего");
					ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "Номенклатура, Цена");
					СтруктураСтроки.Вставить("НомерСтроки", нпп);				
					
					стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
					СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
					
					КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
					ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
					ВыборкаТовары.Номенклатура,
					ВыборкаТовары.КоличествоБазовое,
					ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
					ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
					СтруктураИтоговМест);
					
					СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);									
					
					ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
					ТабличныйДокумент.Вывести(ОбластьСтрока);				
				КонецЦикла;			
				
				ОбластьИтого.Параметры.ВсегоМест = "";
				ОбластьИтого.Параметры.Всего = "";
				ТабличныйДокумент.Вывести(ОбластьИтого);
				ТабличныйДокумент.Вывести(ОбластьПодписи);
				
			КонецЕсли;
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
	КонецФункции
	
	// Перемещение товаров Вендинг
	Функция ПечатьПеремещениеТоваровВендинг(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)  Экспорт
		
		Порядок		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
		СсылкаТТН	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "СсылкаТТН", "");
		ДатаПечати	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ДатаПечати", Неопределено);
		//Бутыли = Новый СписокЗначений;
		//Бутыли.Добавить(Справочники.Номенклатура.НайтиПоКоду("D0783314"));		//	Бутыль 7л
		//Бутыли.Добавить(Справочники.Номенклатура.НайтиПоКоду("249990"));		//	Бутыль 18,9л Полифлекс
		Бутыли = ТК_СправочникиПовтИсп.ЗначенияСвойства("ЕмкостьБутыля").ВыгрузитьКолонку("Объект");
		
		
		//Если Порядок = "Сигареты" Тогда 
		//	СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		//	|	ПорядокСигаретный,ТорговаяПлощадкаПолучатель";
		//ИначеЕсли Порядок = "Пиво" Тогда 
		//	СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		//	|	ПорядокПивной,ТорговаяПлощадкаПолучатель";
		//ИначеЕсли Порядок = "ПакетДокументов" Тогда 
		//	СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		//	|	НомерСтроки";
		//Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	Дата,ТорговаяПлощадкаПолучатель";
		//КонецЕсли;		
		
		ОбработкаВендинг = Обработки.ВендингАвтоматы;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПеремещениеТоваровТовары.Ссылка КАК Ссылка,
		|	ПеремещениеТоваровТовары.Ссылка.Номер КАК Номер,
		|	ПеремещениеТоваровТовары.Ссылка.Дата КАК Дата,
		|	ПеремещениеТоваровТовары.Ссылка.ХозОперация КАК ХозОперация,
		|	ПеремещениеТоваровТовары.Ссылка.Организация КАК Организация,
		|	ПеремещениеТоваровТовары.Ссылка.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоЕДРПОУ,
		|	ПеремещениеТоваровТовары.Ссылка.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
		|	ПеремещениеТоваровТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ПеремещениеТоваровТовары.Ссылка.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель,
		|	ПеремещениеТоваровТовары.Ссылка.СкладКомпании КАК ПредставлениеОтправителя,
		|	ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель КАК ПредставлениеПолучателя,
		|	ПеремещениеТоваровТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	ПеремещениеТоваровТовары.Ссылка.ИДДокументаОтгрузки КАК ИДДокументаОтгрузки,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ТочкаДоставки.Порядок, 0)
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваровТовары.Ссылка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПеремещениеТоваровТовары.Ссылка.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки.Порядок, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПорядокПивной,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ТочкаДоставки.ПорядокСигаретный, 0)
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваровТовары.Ссылка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПеремещениеТоваровТовары.Ссылка.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки.ПорядокСигаретный, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПорядокСигаретный,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ТочкаДоставки
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
		|				И ПеремещениеТоваровТовары.Ссылка.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ПеремещениеТоваровТовары.Ссылка.ДокументОснование КАК Документ.ЗаказПокупателя).ТочкаДоставки
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель <> ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ТочкаДоставки
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ТочкаДоставки,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация <> ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИЗФилиала)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ТочкаДоставки
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ДокументОснование ССЫЛКА Документ.ПеремещениеТоваров
		|				И ПеремещениеТоваровТовары.Ссылка.ДокументОснование.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|			ТОГДА ВЫРАЗИТЬ(ПеремещениеТоваровТовары.Ссылка.ДокументОснование КАК Документ.ПеремещениеТоваров).СкладКомпании.ТочкаДоставки
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АдресОтправителя,
		|	ПеремещениеТоваровТовары.Номенклатура.Артикул КАК Артикул,
		|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПеремещениеТоваровТовары.Номенклатура.НаименованиеПолное КАК ТоварПредставление,
		|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ПеремещениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПеремещениеТоваровТовары.Количество КАК Количество,
		|	ПеремещениеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
		|	ПеремещениеТоваровТовары.Цена КАК Цена,
		|	ПеремещениеТоваровТовары.Сумма КАК СуммаВсего,
		|	ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|		И НЕ ПеремещениеТоваровТовары.Номенклатура В (&Бутыли) КАК ЭтоТара,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ПеремещениеТоваровТовары.ЕдиницаИзмерения
		|		ИНАЧЕ ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки
		|	КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ПеремещениеТоваровТовары.Коэффициент
		|		ИНАЧЕ ЕСТЬNULL(ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0)
		|	КОНЕЦ КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ПеремещениеТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ОсновнаяЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
		|	КОНЕЦ КАК ЕдиницаИзмеренияЯщиков,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
		|			ТОГДА ЕСТЬNULL(ПеремещениеТоваровТовары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЕдиницаИзмеренияЯщиковКоэффициент,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ВидАвтомата.Рядов
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ВидАвтомата.Рядов
		|	КОНЕЦ КАК Рядов,
		|	ВЫБОР
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ВидАвтомата.Ячеек
		|		КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|			ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ВидАвтомата.Ячеек
		|	КОНЕЦ КАК Ячеек,
		|	ЕСТЬNULL(ПланограммаАвтоматаСрезПоследних.Ячейка, 0) КАК Ячейка,
		|	ЦеныСрезПоследних.Цена КАК ЦенаРоз
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланограммаАвтомата.СрезПоследних КАК ПланограммаАвтоматаСрезПоследних
		|		ПО ПеремещениеТоваровТовары.Номенклатура = ПланограммаАвтоматаСрезПоследних.Номенклатура
		|			И ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры = ПланограммаАвтоматаСрезПоследних.Характеристика
		|			И (ВЫБОР
		|				КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|					ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпании
		|				КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|					ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель
		|			КОНЕЦ = ПланограммаАвтоматаСрезПоследних.Склад)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&НаДату, ) КАК ЦеныСрезПоследних
		|		ПО ПеремещениеТоваровТовары.Номенклатура = ЦеныСрезПоследних.Номенклатура
		|			И ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.Характеристика
		|			И (ВЫБОР
		|				КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровВФилиал)
		|					ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли
		|				КОГДА ПеремещениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПеремещениеТоваровИзФилиала)
		|					ТОГДА ПеремещениеТоваровТовары.Ссылка.СкладКомпанииПолучатель.ТипЦенРозничнойТорговли
		|			КОНЕЦ = ЦеныСрезПоследних.ТипЦен)
		|ГДЕ
		|	ПеремещениеТоваровТовары.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Ячейка
		|ИТОГИ
		|	МАКСИМУМ(ПорядокПивной),
		|	МАКСИМУМ(ПорядокСигаретный),
		|	МАКСИМУМ(ТочкаДоставки),
		|	МАКСИМУМ(АдресОтправителя),
		|	МАКСИМУМ(Рядов)
		|ПО
		|	Ссылка";// + СортировкаЗапроса;
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		Запрос.УстановитьПараметр("СсылкаТТН", СсылкаТТН);	
		Запрос.УстановитьПараметр("Бутыли", Бутыли);	
		Запрос.УстановитьПараметр("НаДату", КонецДня(ТекущаяДата()));
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПеремещениеТоваровВендинг";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПеремещениеТоваров.ПФ_MXL_ПеремещениеТоваровВендинг");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовокВФилиал = Макет.ПолучитьОбласть("ЗаголовокВФилиал");	
		ОбластьЗаголовокТара	= Макет.ПолучитьОбласть("ЗаголовокТара");	
		ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
		ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
		ОбластьРазделитель		= Макет.ПолучитьОбласть("Разделитель");
		
		ПервыйДокумент = Истина;
		Пока ВыборкаДокументов.Следующий() Цикл
			
			Если ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваров") Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Эта печатная форма предназначена только для филиалов (%1)'; uk = 'Ця друкована форма призначена лише для філій (%1)'"),
				ВыборкаДокументов.Ссылка));
				Продолжить;
			КонецЕсли;
			
			ЭтоПеремещениеВФилиал = ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал");
			
			Если ЭтоПеремещениеВФилиал И ВыборкаДокументов.ПредставлениеОтправителя.ВидАвтомата = ПредопределенноеЗначение("Справочник.ВидыВендингАвтоматов.ПустаяСсылка")
				ИЛИ НЕ ЭтоПеремещениеВФилиал И ВыборкаДокументов.ПредставлениеПолучателя.ВидАвтомата = ПредопределенноеЗначение("Справочник.ВидыВендингАвтоматов.ПустаяСсылка")Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Эта печатная форма предназначена только для Вендинга (%1)'; uk = 'Ця друкована форма призначена лише для Вендінга (%1)'"),
				ВыборкаДокументов.Ссылка));
				Продолжить;
			КонецЕсли;
			
			//	получаем остатки автомата
			СкладКомпании = ?(ЭтоПеремещениеВФилиал,ВыборкаДокументов.ПредставлениеОтправителя,ВыборкаДокументов.ПредставлениеПолучателя);
			Ответ = ОбработкаВендинг.ПолучитьДанные_GET("automat/"+Формат(СкладКомпании.ИД_Автомата,"ЧГ=0")+"/cell/");
			
			Если Ответ.КодСостояния > 399 Тогда
				ОбработкаВендинг.Ошибка404(Ответ,"GET");
				Продолжить;;
			КонецЕсли;
			
			ТаблицаОстатков = ОбработкаВендинг.ПолучитьТаблицаЗначений();
			
			Для каждого мСтрока Из Ответ.Данные Цикл
				стрОстатки = ТаблицаОстатков.Добавить();
				ЗаполнитьЗначенияСвойств(стрОстатки,мСтрока);
				стрОстатки.Артикул = Формат(мСтрока.article,"ЧГ=0");
			КонецЦикла;
			
			ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перемещение товаров №%1 от %2'; uk = 'Переміщення товарів №%1 від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(?(ЗначениеЗаполнено(ДатаПечати),ДатаПечати,ВыборкаДокументов.Дата), "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
			
			СтруктураШапки = Новый Структура;
			СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
			СтруктураШапки.Вставить("ТекстЗаголовкаТара", НСтр("ru = 'Возврат тары к '; uk = 'Повернення тари до '") + ЗаголовокЛок);		
			СтруктураШапки.Вставить("ПредставлениеПоставщика", "");		
			СтруктураШапки.Вставить("ПредставлениеОтправителя", "");
			//СтруктураШапки.Вставить("АдресОтправителя", "");						
			
			НомерМаршрута = 0;
			Если Порядок = "Пиво" Тогда 
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной/1000);
			Иначе
				НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный/100);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НомерМаршрута) Тогда 
				СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			СтруктураШапки.Вставить("АдресПоставщика", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.Организация, "ЮридическийАдресОрганизации",, ТекущаяДата()));		
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ПредставлениеПолучателя) Тогда
				СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ПредставлениеПолучателя);
			ИначеЕсли ЗначениеЗаполнено(ВыборкаДокументов.ТорговаяПлощадкаПолучатель) Тогда 
				СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ТорговаяПлощадкаПолучатель);	
			Иначе
				СтруктураШапки.Вставить("ПредставлениеПолучателя", "");						
			КонецЕсли;
			
			//Если ВыборкаДокументов.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровВФилиал И ЗначениеЗаполнено(
			Если ЗначениеЗаполнено(ВыборкаДокументов.АдресОтправителя) Тогда 
				СтруктураШапки.Вставить("АдресОтправителя", ВыборкаДокументов.АдресОтправителя);
			ИначеЕсли ВыборкаДокументов.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда 
				СтруктураШапки.Вставить("АдресОтправителя", УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадка,"ФактическийАдресТорговойПлощадки",,ТекущаяДата()));
			КонецЕсли;				
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ТочкаДоставки) Тогда
				СтруктураШапки.Вставить("АдресПолучателя", ВыборкаДокументов.ТочкаДоставки);
			ИначеЕсли ЭтоПеремещениеВФилиал Тогда
				СтруктураШапки.Вставить("АдресПолучателя",УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ВыборкаДокументов.ТорговаяПлощадкаПолучатель,"ФактическийАдресТорговойПлощадки",,ТекущаяДата()));
			КонецЕсли;
			
			
			
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
			
			//Если ЭтоПеремещениеВФилиал Тогда 
			//	
			//	ИДД = "";
			//	Если НЕ ПустаяСтрока(ВыборкаДокументов.ИДДокументаОтгрузки) Тогда 
			//		ИДД = ВыборкаДокументов.ИДДокументаОтгрузки;
			//	Иначе
			//		ИДД = Строка(ВыборкаДокументов.Ссылка.УникальныйИдентификатор());
			//	КонецЕсли;
			//	
			//	Попытка
			//		ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(ИДД,1,125);
			//		ОбластьЗаголовокВФилиал.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			//	Исключение КонецПопытки;
			//	
			//	ОбластьЗаголовокВФилиал.Параметры.Заполнить(СтруктураШапки);
			//	ТабличныйДокумент.Вывести(ОбластьЗаголовокВФилиал);
			//	
			//Иначе
			
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			//КонецЕсли;
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			нпп 			= 0;
			КоличествоМест 	= 0;
			СуммаВсего 		= 0;
			СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
			ЕстьТара 		= Ложь;
			
			тзРотацияВитрины = ОбработкаВендинг.ПолучитьТаблицаЗначений();
			
			ВыборкаТовары = ВыборкаДокументов.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				нпп = нпп + 1;			
				
				//	Проверка на новое МРЦ
				мОстатки = ТаблицаОстатков.НайтиСтроки(Новый Структура("Артикул,maxRetailPrice",ВыборкаТовары.Артикул,ВыборкаТовары.Характеристика.ЗначениеМРЦ * 100));
				
				ЭтоНовоеМРЦ = мОстатки.Количество() = 0;
				
				Если ЭтоНовоеМРЦ Тогда
					тзОстаткиМРЦ = ТаблицаОстатков.Скопировать(Новый Структура("Артикул",ВыборкаТовары.Артикул));
					тзОстаткиМРЦ.Сортировать("order Убыв");
					
					нСтрока = тзРотацияВитрины.Добавить();
					ЗаполнитьЗначенияСвойств(нСтрока,ВыборкаТовары);
					Если тзОстаткиМРЦ.Количество() > 0 Тогда
						ЗаполнитьЗначенияСвойств(нСтрока,тзОстаткиМРЦ[0],,"Номенклатура,Характеристика,Ячейка");
					КонецЕсли;
					нСтрока.СортировкаМРЦ = ВыборкаТовары.Характеристика.Сортировка;
				КонецЕсли;
				
				Если Не ЕстьТара И ЭтоПеремещениеВФилиал И ВыборкаТовары.ЭтоТара Тогда 
					ЕстьТара = Истина;
				КонецЕсли;
				
				СтруктураСтроки = Новый Структура("Номенклатура, Цена, СуммаВсего, ЦенаРоз");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
				
				СтруктураСтроки.Вставить("НомерСтроки", нпп);
				
				стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
				СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.Характеристика), ", " + СокрЛП(ВыборкаТовары.Характеристика), ""));
				СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
				
				КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
				ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
				ВыборкаТовары.Номенклатура,
				ВыборкаТовары.КоличествоБазовое,
				ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
				ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
				ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
				СтруктураИтоговМест);
				
				СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
				
				Если ВыборкаТовары.Ячейка = 0 Тогда
					СтруктураСтроки.Вставить("Ячейка", "");
					СтруктураСтроки.Вставить("_Ячейка","");
				Иначе
					Ячейка = ОбработкаВендинг.ИмяЯчейки(ВыборкаТовары.Рядов,ВыборкаТовары.Ячеек,ВыборкаТовары.Ячейка);
					СтруктураСтроки.Вставить("Ячейка", Ячейка);
					СтруктураСтроки.Вставить("_Ячейка","("+Прав("00"+ВыборкаТовары.Ячейка,3)+")");
				КонецЕсли;
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
				
				СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
			КонецЦикла;
			
			СтруктураПодвала = Новый Структура;	
			СтруктураПодвала.Вставить("Всего", СуммаВсего);		
			СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
			
			ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтого);		
			
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			Если ЕстьТара Тогда 
				
				ТабличныйДокумент.Вывести(ОбластьРазделитель);			
				ОбластьЗаголовокТара.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТара);
				ТабличныйДокумент.Вывести(ОбластьПоставщик);
				ТабличныйДокумент.Вывести(ОбластьПокупатель);
				ТабличныйДокумент.Вывести(ОбластьШапка);
				
				СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
				нпп = 0;
				ВыборкаТовары.Сбросить();
				Пока ВыборкаТовары.Следующий() Цикл 
					Если Не ВыборкаТовары.ЭтоТара Тогда 
						Продолжить;
					КонецЕсли;
					
					нпп = нпп + 1;
					
					СтруктураСтроки = Новый Структура("Номенклатура, КоличествоПредставление, Цена, СуммаВсего");
					ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "Номенклатура, Цена");
					СтруктураСтроки.Вставить("НомерСтроки", нпп);				
					
					стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
					СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.Характеристика), ", " + СокрЛП(ВыборкаТовары.Характеристика), ""));
					
					КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
					ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
					ВыборкаТовары.Номенклатура,
					ВыборкаТовары.КоличествоБазовое,
					ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
					ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
					ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
					СтруктураИтоговМест);
					
					СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);									
					
					ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
					ТабличныйДокумент.Вывести(ОбластьСтрока);				
				КонецЦикла;			
				
				ОбластьИтого.Параметры.ВсегоМест = "";
				ОбластьИтого.Параметры.Всего = "";
				ТабличныйДокумент.Вывести(ОбластьИтого);
				ТабличныйДокумент.Вывести(ОбластьПодписи);
				
			КонецЕсли;
			
			Если тзРотацияВитрины.Количество() > 0 Тогда
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				ОбластьЗаголовокРТ 		= Макет.ПолучитьОбласть("Заголовок");
				ОбластьШапкаТаблицыРТ	= Макет.ПолучитьОбласть("ШапкаТаблицыРТ");
				ОбластьСтрокаРТ 		= Макет.ПолучитьОбласть("СтрокаРТ");
				ОбластьИтогоРТ 			= Макет.ПолучитьОбласть("ИтогоРТ");
				
				ОбластьЗаголовокРТ.Параметры.ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ротация витрины к Перемещение товаров №%1 от %2'; uk = 'Ротація вітрини до Переміщення товарів №%1 від %2'", КодЯзыкаПечать),
				СокрЛП(ВыборкаДокументов.Номер),
				Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy"));
				
				ТабличныйДокумент.Вывести(ОбластьЗаголовокРТ);
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыРТ);
				
				НомерСтроки = 0;
				мНоменклатураДляЗаменыВитрины = ОбщегоНазначенияКлиентСервер.СвернутьМассив(тзРотацияВитрины.ВыгрузитьКолонку("Номенклатура"));
				
				Для Каждого стрНоменклатура Из мНоменклатураДляЗаменыВитрины Цикл
					тзНовоеМРЦ = тзРотацияВитрины.Скопировать(Новый Структура("Номенклатура",стрНоменклатура));
					тзНовоеМРЦ.Сортировать("СортировкаМРЦ Убыв");
					СтрокаЗаменыВитрины = тзНовоеМРЦ[0];
					
					НомерСтроки = 1 + НомерСтроки;
					ЯчейкаУ = ОбработкаВендинг.ИмяЯчейки(ВыборкаДокументов.Рядов,ВыборкаДокументов.Ячеек,СтрокаЗаменыВитрины.number);
					ЯчейкаВ = ОбработкаВендинг.ИмяЯчейки(ВыборкаДокументов.Рядов,ВыборкаДокументов.Ячеек,СтрокаЗаменыВитрины.Ячейка);
					СтруктураРотации = Новый Структура;
					СтруктураРотации.Вставить("НомерСтроки",НомерСтроки);
					СтруктураРотации.Вставить("Артикул",СтрокаЗаменыВитрины.Артикул);
					СтруктураРотации.Вставить("ТоварНаименование",СтрокаЗаменыВитрины.Номенклатура);
					СтруктураРотации.Вставить("Количество","1 шт");
					СтруктураРотации.Вставить("ЯчейкаУ",ЯчейкаУ);
					СтруктураРотации.Вставить("_ЯчейкаУ",?(СтрокаЗаменыВитрины.number=0,"","("+СтрокаЗаменыВитрины.number+")"));
					СтруктураРотации.Вставить("МРЦУ",СтрокаЗаменыВитрины.maxRetailPrice/100);
					СтруктураРотации.Вставить("ЯчейкаВ",ЯчейкаВ);
					СтруктураРотации.Вставить("_ЯчейкаВ","("+СтрокаЗаменыВитрины.Ячейка+")");
					СтруктураРотации.Вставить("МРЦВ",СтрокаЗаменыВитрины.Характеристика.ЗначениеМРЦ);
					ОбластьСтрокаРТ.Параметры.Заполнить(СтруктураРотации);
					ТабличныйДокумент.Вывести(ОбластьСтрокаРТ);
				КонецЦикла;
				
				ТабличныйДокумент.Вывести(ОбластьИтогоРТ);
				
			КонецЕсли;
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
	КонецФункции
	
	#КонецОбласти	
	
	
	
	
	
#КонецЕсли

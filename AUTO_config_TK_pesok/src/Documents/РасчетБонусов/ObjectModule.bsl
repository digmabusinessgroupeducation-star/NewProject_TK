#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь(); 
	Организация = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	

КонецПроцедуры


#КонецЕсли

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.НачислениеМаркетинга.Записывать = Истина;
	Движения.НачислениеМаркетинга.Очистить();
	
	Для Каждого ТекСтрокаНачисление Из Начисление Цикл
		Движение = Движения.НачислениеМаркетинга.Добавить();
		Движение.Период = Дата;
		Движение.УстановкаМаркетинга = ТекСтрокаНачисление.УстановкаМаркетинга;
		Движение.Контрагент = ТекСтрокаНачисление.Контрагент;
		Движение.ДоговорМаркетинга = ТекСтрокаНачисление.Договор;
		Движение.Менеджер = ТекСтрокаНачисление.Менеджер;
		Движение.Периодичность = ТекСтрокаНачисление.УстановкаМаркетинга.Периодичность; 
		Движение.СуммаРасчетная = ТекСтрокаНачисление.СуммаРасчетная;
		Движение.СуммаСогласованная = ТекСтрокаНачисление.СуммаСогласованная;
		Движение.НачалоРасчета = ТекСтрокаНачисление.НачалоРасчета;
		Движение.КонецРасчета = ТекСтрокаНачисление.КонецРасчета;
		Движение.АктОбОказанииУслуг = ТекСтрокаНачисление.АктОбОказанииУслуг;
	КонецЦикла;
	
	ргАналитикаНачисленияМаркетинга = Движения.АналитикаНачисленияМаркетинга;		
	ргАналитикаНачисленияМаркетинга.Записывать = Истина;
	ргАналитикаНачисленияМаркетинга.Очистить();
		
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	РасчетБонусовАналитикаРасчетов.НомерСтроки КАК НомерСтроки,
	                      |	РасчетБонусовНачисление.НачалоРасчета КАК Период,
	                      |	РасчетБонусовАналитикаРасчетов.УстановкаМаркетинга КАК УстановкаМаркетинга,
	                      |	РасчетБонусовНачисление.Договор КАК ДоговорМаркетинга,
	                      |	РасчетБонусовАналитикаРасчетов.Контрагент КАК Контрагент,
	                      |	РасчетБонусовАналитикаРасчетов.Организация КАК Организация,
	                      |	РасчетБонусовАналитикаРасчетов.ПодразделениеКомпании КАК ПодразделениеКомпании,
	                      |	РасчетБонусовАналитикаРасчетов.ТорговаяПлощадка КАК ТорговаяПлощадка,
	                      |	РасчетБонусовНачисление.Менеджер КАК Менеджер,
	                      |	РасчетБонусовАналитикаРасчетов.Номенклатура КАК Номенклатура,
	                      |	РасчетБонусовНачисление.ТипБонуса КАК ТипБонуса,
	                      |	РасчетБонусовАналитикаРасчетов.СуммаБазовая КАК СуммаБазовая,
	                      |	РасчетБонусовАналитикаРасчетов.СуммаРасчетная КАК СуммаРасчетная,
	                      |	РасчетБонусовНачисление.НачалоРасчета КАК НачалоРасчета,
	                      |	РасчетБонусовНачисление.КонецРасчета КАК КонецРасчета,
	                      |	РасчетБонусовАналитикаРасчетов.ИДУсловия КАК ИДУсловия
	                      |ИЗ
	                      |	Документ.РасчетБонусов.АналитикаРасчетов КАК РасчетБонусовАналитикаРасчетов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетБонусов.Начисление КАК РасчетБонусовНачисление
	                      |		ПО РасчетБонусовАналитикаРасчетов.Ссылка = РасчетБонусовНачисление.Ссылка
	                      |			И РасчетБонусовАналитикаРасчетов.УстановкаМаркетинга = РасчетБонусовНачисление.УстановкаМаркетинга
	                      |ГДЕ
	                      |	РасчетБонусовНачисление.Ссылка = &Ссылка
	                      |	И РасчетБонусовАналитикаРасчетов.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Движение = ргАналитикаНачисленияМаркетинга.Добавить();
		Движение.Активность = Истина;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;

	
КонецПроцедуры

Процедура ОбновитьСуммыТЧНачисления() Экспорт 			
	ТаблАналитики = АналитикаРасчетов.Выгрузить(,"УстановкаМаркетинга, ИДУсловия, ИДНачисления, СуммаБазовая, СуммаРасчетная");
	ТаблАналитики.Свернуть("УстановкаМаркетинга, ИДУсловия, ИДНачисления", "СуммаБазовая, СуммаРасчетная");
	
	Для Каждого Стр Из ТаблАналитики Цикл
		МассивСтрок = Расчеты.НайтиСтроки(Новый Структура("УстановкаМаркетинга, ИДУсловия", Стр.УстановкаМаркетинга, Стр.ИДУсловия));
		
		Для Каждого СтрМассива Из МассивСтрок Цикл
			СтрМассива.РасчетнаяБаза = Стр.СуммаБазовая;
			СтрМассива.СуммаРасчетная = Стр.СуммаРасчетная;
		КонецЦикла;
	КонецЦикла;
	
	ТаблСуммНачисления = ТаблАналитики.Скопировать();
	ТаблСуммНачисления.Свернуть("УстановкаМаркетинга, ИДНачисления", "СуммаРасчетная");
	
	Для Каждого Стр из ТаблСуммНачисления Цикл
		МассивСтрок = Начисление.НайтиСтроки(Новый Структура("УстановкаМаркетинга, ИДНачисления", Стр.УстановкаМаркетинга, Стр.ИДНачисления));
		
		Для Каждого СтрМассива Из МассивСтрок Цикл
			СтрМассива.СуммаРасчетная = Стр.СуммаРасчетная;			
		КонецЦикла;
	КонецЦикла;
	
	СписокУстановок = ТаблСуммНачисления.Скопировать();
	СписокУстановок.Свернуть("УстановкаМаркетинга");
	
	СуммыРасчетов = Расчеты.Выгрузить(,"УстановкаМаркетинга, СуммаРасчетная");
	СуммыРасчетов.Свернуть("УстановкаМаркетинга", "СуммаРасчетная");
	Для Каждого Стр Из СуммыРасчетов Цикл
		Если СписокУстановок.Найти(Стр.УстановкаМаркетинга, "УстановкаМаркетинга") <> Неопределено Тогда 
			Продолжить;	
		КонецЕсли;
		
		СтрУстановка = Начисление.Найти(Стр.УстановкаМаркетинга, "УстановкаМаркетинга");
		
		Если СтрУстановка<>Неопределено Тогда
			СтрУстановка.СуммаРасчетная = Стр.СуммаРасчетная;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры



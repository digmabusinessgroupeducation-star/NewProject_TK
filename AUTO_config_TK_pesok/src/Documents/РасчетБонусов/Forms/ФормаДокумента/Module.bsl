

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("РасчетБонусов"));

	РольПолная = РольДоступна("ПолныеПрава");
	РольПроверка = РольДоступна("ПроверкаРасчетаБонусов");
	РольУтверждение = РольДоступна("УтверждениеРасчетаБонусов");

	Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыАссортиментногоКомитета.Черновик);
	Если РольПолная ИЛИ РольПроверка ИЛИ Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Проверен Тогда
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыАссортиментногоКомитета.Проверен);
	КонецЕсли;
	Если РольПолная ИЛИ (РольУтверждение И Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Проверен) ИЛИ Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Утвержден Тогда
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыАссортиментногоКомитета.Утвержден);
	КонецЕсли;
	Если РольПолная ИЛИ Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Обработан Тогда
		Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыАссортиментногоКомитета.Обработан);
	КонецЕсли;
	
	Если РольПолная Тогда 
		Элементы.Статус.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.Статус.ТолькоПросмотр = (Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Утвержден И РольПроверка И НЕ РольУтверждение)
		ИЛИ НЕ (Объект.Статус <> Перечисления.СтатусыАссортиментногоКомитета.Обработан И (РольУтверждение ИЛИ РольПроверка));
	КонецЕсли;
	
	// Запрещено редактировать "обработанный" документ
	Если Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Обработан Тогда 
		ЭтаФорма.ТолькоПросмотр = НЕ РольПолная;
	ИначеЕсли Объект.Статус = Перечисления.СтатусыАссортиментногоКомитета.Утвержден Тогда
		ЭтаФорма.ТолькоПросмотр = НЕ (РольПолная Или НЕ РольПроверка ИЛИ РольУтверждение);
		Элементы.СоздатьАкт.Видимость = Истина;
		Элементы.НачислениеРасчитатьКомплект.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	РасчетРазницы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для Каждого нСтрока Из Объект.Начисление Цикл 
		нСтрока.Пометка = Ложь;
	КонецЦикла;
	
	ПересчитатьСуммуДокумента();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ТК_УправлениеДоступом
	
	УстановитьСостояниеДокумента();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПересчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура НачислениеУстановкаМаркетингаПриИзмененииНаСервере(Док, иСтроки)
	
	//заполнение строки Начисление с документа
	ТекСтрока = Объект.Начисление.НайтиПоИдентификатору(иСтроки);
	ЗаполнитьЗначенияСвойств(ТекСтрока,	Док);
	ТекСтрока.Договор           = Док.ДоговорВзаиморасчетов;
	ТекСтрока.НачалоРасчета     = ДобавитьМесяц(НачалоМесяца(ТекущаяДата()),-1);
	ТекСтрока.КонецРасчета      = КонецМесяца(ДобавитьМесяц(ТекущаяДата(),-1));
	ТекСтрока.ФормулировкаСчета = Док.ФормулировкаНачисления;
	ТекСтрока.ИДНачисления      = Новый УникальныйИдентификатор;
	ТекСтрока.Пометка 			= Истина;
	Если Док.СхемаРасчета = Перечисления.СхемыНачисления.ФиксированыйБонус Тогда
		ТекСтрока.СуммаРасчетная = Док.РазмерБонуса;;
	КонецЕсли; 
	ТекСтрока.Пометка 			= Истина;
	
	//ТекСтрока.АктОбОказанииУслуг = НайтиАкт(Док);
	
	//заполнение строки Расчеты с документа при добавлении
	рСтрока = Объект.Расчеты.Добавить();
	рСтрока.УстановкаМаркетинга = Док;
	рСтрока.Организация   = Док.Организация;
	рСтрока.Контрагент 	  = Док.Контрагент;
	рСтрока.НачалоРасчета = ДобавитьМесяц(НачалоМесяца(ТекущаяДата()),-1);
	рСтрока.КонецРасчета  = КонецМесяца(ДобавитьМесяц(ТекущаяДата(),-1));
	рСтрока.РазмерБонуса  = Док.РазмерБонуса;
	Если Док.СхемаРасчета = Перечисления.СхемыНачисления.ФиксированыйБонус Тогда
		рСтрока.ФиксированныйБонус = Истина;
		рСтрока.РазмерБонуса = Док.РазмерБонуса;
	КонецЕсли; 
	рСтрока.ИДУсловия = Новый УникальныйИдентификатор;
	
	РасчетРазницы();
	
КонецПроцедуры

&НаКлиенте
Процедура НачислениеУстановкаМаркетингаПриИзменении(Элемент)
	Док = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.УстановкаМаркетинга;
	иСтроки = ЭтаФорма.ТекущийЭлемент.ТекущаяСтрока;
	
	НачислениеУстановкаМаркетингаПриИзмененииНаСервере(Док, иСтроки);
КонецПроцедуры

&НаКлиенте
Процедура НачислениеПередУдалением(Элемент, Отказ)
	ТекДанные = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные;
	Док = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные.УстановкаМаркетинга;
	
	СписокРасчетов = Объект.Расчеты.НайтиСтроки(Новый Структура("УстановкаМаркетинга", Док));
	Для Каждого стр Из СписокРасчетов Цикл
		Объект.Расчеты.Удалить(стр);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекДанные.АктОбОказанииУслуг) Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		тПараметры = Новый Структура;
		тПараметры.Вставить("СтрокаНачисления", ТекДанные);
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУдалениеАкта", ЭтотОбъект, тПараметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Установить пометку на удаление на Акте об оказании услуг?'; ua = 'Встановити помітку на видалення для Акту надання послуг?'"), Режим, 0);
	КонецЕсли;

	УдалитьСтрокиАналитики(Док,Неопределено,ТекДанные.ИДНачисления);
	
КонецПроцедуры

&НаКлиенте
Процедура НачислениеПослеУдаления(Элемент)
	ПересчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура НачислениеПриАктивизацииСтроки(Элемент)
	    УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура НачислениеКонецРасчетаПриИзменении(Элемент)
	ТекСтрока = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные;
	
	МассивРасчетов = Объект.Расчеты.НайтиСтроки(Новый Структура("УстановкаМаркетинга", ТекСтрока.УстановкаМаркетинга));
		Если ТекСтрока <> Неопределено Тогда
			Для Каждого Стр Из МассивРасчетов Цикл
				Стр.КонецРасчета = ТекСтрока.КонецРасчета;
			КонецЦикла;
		КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачислениеНачалоРасчетаПриИзменении(Элемент)
	ТекСтрока = ЭтаФорма.ТекущийЭлемент.ТекущиеДанные;
	
	МассивРасчетов = Объект.Расчеты.НайтиСтроки(Новый Структура("УстановкаМаркетинга", ТекСтрока.УстановкаМаркетинга));
		Если ТекСтрока <> Неопределено Тогда
			Для Каждого Стр Из МассивРасчетов Цикл
				Стр.НачалоРасчета = ТекСтрока.НачалоРасчета;
			КонецЦикла;
		КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура НачислениеСуммаСогласованнаяПриИзменении(Элемент)
	ТекСтрока = Элементы.Начисление.ТекущиеДанные;
	Если ТекСтрока.СуммаСогласованная=0 Тогда 
		ТекСтрока.Разница = 0;
	Иначе	
		ТекСтрока.Разница = ТекСтрока.СуммаСогласованная - ТекСтрока.СуммаРасчетная;
	КонецЕсли;
	ПересчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура НачислениеСуммаРасчетнаяПриИзменении(Элемент)
	ТекСтрока = Элементы.Начисление.ТекущиеДанные;
	Если ТекСтрока.СуммаСогласованная=0 Тогда 
		ТекСтрока.Разница = 0;
	Иначе	
		ТекСтрока.Разница = ТекСтрока.СуммаСогласованная - ТекСтрока.СуммаРасчетная;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Для Каждого СтрокаМассива Из ВыбранноеЗначение Цикл
		НоваяСтрока = Объект.Начисление.Добавить();
		НоваяСтрока.УстановкаМаркетинга = СтрокаМассива;
		иСтроки = НоваяСтрока.ПолучитьИдентификатор();
		НачислениеУстановкаМаркетингаПриИзмененииНаСервере(СтрокаМассива, иСтроки);	
	КонецЦикла; 	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура РасчитатьБонусы(Команда)
	МассивСтрок = Объект.Начисление.НайтиСтроки(Новый Структура("Пометка", Истина));
	Текст = "";	
	
	Для Каждого нСтрока Из МассивСтрок Цикл
		Если НЕ нСтрока.СуммаРасчетная = 0 Тогда
			Текст = Текст+"Для строки "+нСтрока.НомерСтроки+" уже проведен расчет"+Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Текст="" Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Вы действительно хотите пересчитать значения?'; ua = 'Ви впевнені, що хочете перерахувати значення?'"), Режим, 0);
	Иначе 
		РасчитатьБонусыНаСервере(Ложь);
	КонецЕсли;	
	
	ПересчитатьСуммуДокумента();

	УстановитьОтбор();
КонецПроцедуры

&НаСервере
Процедура РасчитатьБонусыНаСервере(ОчиститьАналитику)
	
	МассивНачислений = Объект.Начисление.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если МассивНачислений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрНачисления Из МассивНачислений Цикл
		
		Если СтрНачисления.УстановкаМаркетинга.ПометкаУдаления Тогда
			ОбщегоНазначения.СообщитьПользователю("Документ "+СтрНачисления.УстановкаМаркетинга+" помечен на удаление, запрещено расчитывать бонусы!!!");
		Иначе
			МассивРасчетов = Объект.Расчеты.НайтиСтроки(Новый Структура("УстановкаМаркетинга", СтрНачисления.УстановкаМаркетинга));
			Для Каждого СтрРасчета Из МассивРасчетов Цикл
				РассчитатьБонусы(СтрНачисления, СтрРасчета, ОчиститьАналитику);
			КонецЦикла;	
		КонецЕсли; 
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура СоздатьАктНаСервере()
	
	МассивНачислений = Объект.Начисление.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если МассивНачислений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрНачисления Из МассивНачислений Цикл
		Если СтрНачисления.СуммаСогласованная = 0 Тогда
			ОбщегоНазначения.СообщитьПользователю("НЕ заполнена согласованная сумма для строки "+СтрНачисления.УстановкаМаркетинга);
		Иначе
			УдалитьАктОбОказанииУслуг(СтрНачисления.АктОбОказанииУслуг,СтрНачисления.ДокументСчет);
			
			СформироватьАкт(СтрНачисления);
		КонецЕсли;
	КонецЦикла;	
	
	РасчетРазницы();
	 
КонецПроцедуры

&НаКлиенте
Процедура СоздатьАкт(Команда)
	СоздатьАктНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФлажки(Команда)
	ЗаполнитьЗначенияФлажков(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	ЗаполнитьЗначенияФлажков(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	РасчитатьБонусыНаСервере(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНачисление(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
        Объект.Начисление.Очистить();
		Объект.Расчеты.Очистить();
		Объект.АналитикаРасчетов.Очистить();
    КонецЕсли;

	ЗаполнитьУстановкиНаСервере();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУстановкиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_УстановкаМаркетинга.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ТК_УстановкаМаркетинга КАК ТК_УстановкаМаркетинга
		|ГДЕ
		|	ТК_УстановкаМаркетинга.Проведен
		|	И ТК_УстановкаМаркетинга.Организация = &Организация
		|	И ТК_УстановкаМаркетинга.НачалоДействия <= &НаДату
		|	И ТК_УстановкаМаркетинга.КонецДействия >= &НаДату
		|
		|СГРУППИРОВАТЬ ПО
		|	ТК_УстановкаМаркетинга.Ссылка";
	
	Запрос.УстановитьПараметр("НаДату", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		ОбщегоНазначения.СообщитьПользователю("Нет данных для заполнения");
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = Объект.Начисление.Добавить();
			НоваяСтрока.УстановкаМаркетинга = ВыборкаДетальныеЗаписи.Ссылка;
			иСтроки = НоваяСтрока.ПолучитьИдентификатор();
			НачислениеУстановкаМаркетингаПриИзмененииНаСервере(ВыборкаДетальныеЗаписи.Ссылка, иСтроки);	
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУстановки(Команда)
	
	Если НЕ Объект.Начисление.Количество()=0 Тогда 
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ОчиститьНачисление", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Очистить все начисления перед заполнением?'; ua = 'Очистити всі нарахування перед заповненням?'"), Режим, 0);
	Иначе 
		ЗаполнитьУстановкиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодборНоменклатурыПослеЗакрытия", ЭтаФорма,"Подбор");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь); 
	ПараметрыФормы.Вставить("РежимВыбора", 		  Истина); 
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина); 
	//ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы); 
	
	ОткрытьФорму("Документ.ТК_УстановкаМаркетинга.ФормаВыбора",ПараметрыФормы,ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьКомплект(Команда)
	ЗадатьВопрос = Ложь;

	РазныйУчет = ПроверитьРегламментированныйУчет();
	
	Если РазныйУчет Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запрещено создавать консолидированный акт с разным видом учета");
		Возврат;
	КонецЕсли; 
	
	Для Каждого стр Из Объект.Начисление Цикл
		Если стр.Пометка Тогда
			Если ЗначениеЗаполнено(стр.АктОбОказанииУслуг) Тогда
				ЗадатьВопрос = Истина;
				Прервать;	
			КонецЕсли; 
		КонецЕсли; 	
	КонецЦикла;
	
	Если ЗадатьВопрос Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		
		Оповещение = Новый ОписаниеОповещения("УдалитьАкты", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Для формирования комплекта необходимо удалить сущестувующие документы. Установить пометку на удаление?'; ua = 'Для створення нових документів потрібно видалити старі. Встановити помытку на видалення?'"), Режим, 0);
	Иначе
		РасчитатьКомплектНаСервере();
	КонецЕсли; 
			

КонецПроцедуры

&НаКлиенте
Процедура УдалитьАкты(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для Каждого стр Из Объект.Начисление  Цикл
			Если стр.Пометка и ЗначениеЗаполнено(стр.АктОбОказанииУслуг) Тогда
				УдалитьАктОбОказанииУслуг(стр.АктОбОказанииУслуг,стр.ДокументСчет);
			КонецЕсли; 
		КонецЦикла; 	
		
		РасчитатьКомплектНаСервере();
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьРегламментированныйУчет() Экспорт
	
	Таблица = Объект.Начисление.Выгрузить(Новый структура("Пометка", Истина),"УстановкаМаркетинга");
	Таблица.Колонки.Добавить("РеглУчет");
	
	Для Каждого стр Из Таблица Цикл
		Стр.РеглУчет = Стр.УстановкаМаркетинга;
	КонецЦикла;  
	ТаблицаРегл = Таблица.Скопировать(,"РеглУчет");
	ТаблицаРегл.Свернуть("РеглУчет");
	Если ТаблицаРегл.Количество() = 1 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли; 
	
КонецФункции

&НаКлиенте
Процедура ПодборНоменклатурыПослеЗакрытия(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РасчетРазницы()
	
	Для Каждого стр Из Объект.Начисление Цикл
		Если стр.СуммаРасчетная = 0 Тогда
			Разница = "";
		Иначе
			Разница = стр.СуммаСогласованная - стр.СуммаРасчетная;
			стр.Разница = Формат(Разница, "ЧДЦ=2; ЧРД=,; ЧРГ=; ЧН=");
		КонецЕсли;
		
		Если НЕ стр.АктОбОказанииУслуг.Пустая() Тогда
			СуммаАкта = стр.АктОбОказанииУслуг.СуммаДокумента;
			РазницаСАктом = Формат(СуммаАкта - стр.СуммаСогласованная,"ЧДЦ=2; ЧН=");
		Иначе
			СуммаАкта = "";
			РазницаСАктом = "";
		КонецЕсли;
		
		стр.Пометка = Ложь;

	КонецЦикла; 
	
	
КонецФункции

&НаСервере
Функция ВозвратСсылка(Имя)
	Возврат Документы[Имя].ПустаяСсылка();
КонецФункции

&НаСервере
Функция ПолучитьСхему(Док)
	 Документ = Документы.ТК_УстановкаМаркетинга;
	Если Док.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтОплат Тогда
		Схема = Документ.ПолучитьМакет("ПроцентОтОплат");
	ИначеЕсли Док.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтПоставки Тогда
		Схема = Документ.ПолучитьМакет("ПроцентОтПоставок");
	КонецЕсли;
	
	Возврат Схема;
КонецФункции

&НаСервере
Функция РассчитатьБонусыСогласноНастроек(СтрокаНачисления, СтрокаРасчета, Ошибки = "") Экспорт
	
	ВсеОк = Истина;
	Сумма = 0;
	тРезультат = Новый ТаблицаЗначений;
	
	СхемаКомпоновкиДанных = ПолучитьСхему(СтрокаРасчета.УстановкаМаркетинга);	
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	нОтбор = СтрокаНачисления.УстановкаМаркетинга.ХранилищеОтборов.Получить();
	
	Если нОтбор = Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СтрокаРасчета.УстановкаМаркетинга.ХранилищеОтборов.Получить());
	КонецЕсли;
	
	СписокТП = Новый СписокЗначений;
	Тз = СтрокаРасчета.УстановкаМаркетинга.Площадки.Выгрузить();
	СписокТП.ЗагрузитьЗначения(Тз.ВыгрузитьКолонку("ТорговаяПлощадка"));
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", НачалоДня(СтрокаНачисления.НачалоРасчета));
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", КонецДня(СтрокаНачисления.КонецРасчета));
	//Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Контрагент", СтрокаРасчета.Контрагент);
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ТорговаяПлощадка", СписокТП);
	//Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Организация", СтрокаРасчета.Организация);
	//Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДоговорВзаиморасчетов", СтрокаНачисления.Договор);
	
	ЗначениеПараметра = Настройки.ПараметрыВывода.Элементы.Найти("ВертикальноеРасположениеОбщихИтогов");
	ЗначениеПараметра.Значение = РасположениеИтоговКомпоновкиДанных.Нет;
	ЗначениеПараметра.Использование = Истина;
	//
	//Если СтрокаРасчета.УстановкаМаркетинга.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтОплат Тогда
	//	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ХозОперация", Справочники.ХозОперации.БанковскаяВыпискаРасход);
	//КонецЕсли;
	
	//КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	УстановитьПривилегированныйРежим(Истина);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ПроцессорВывода.УстановитьОбъект(тРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Если тРезультат.Колонки.Найти("СуммаБазовая") <> Неопределено Тогда
		Сумма = тРезультат.Итог("СуммаБазовая");
	ИначеЕсли тРезультат.Колонки.Найти("Сумма") <> Неопределено Тогда
		Сумма = тРезультат.Итог("Сумма");
	КонецЕсли;
	
	Если Сумма = 0 Тогда 
		ВсеОк = Ложь;
		ОбщегоНазначения.СообщитьПользователю("Нет данных по расчету строки " + СтрокаНачисления.УстановкаМаркетинга);
	КонецЕсли;
	
	Если ВсеОк И тРезультат.Количество() > 0 И СтрокаРасчета.УстановкаМаркетинга.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтПоставки Тогда
		СтрокаРасчета.ДатаРасчета = СтрокаНачисления.КонецРасчета;
		СтрокаРасчета.РасчетнаяБаза = Сумма;
		тСуммаРасчетная = Окр(СтрокаРасчета.РазмерБонуса*Сумма/100,2,РежимОкругления.Окр15как20);
		СтрокаРасчета.СуммаРасчетная = тСуммаРасчетная;
		СтрокаНачисления.СуммаРасчетная = тСуммаРасчетная; 

		Для Каждого Стр Из тРезультат Цикл
			НоваяСтрока = Объект.АналитикаРасчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			
			ПоляЗаполнения = "УстановкаМаркетинга, ИДУсловия";
			Если НоваяСтрока.Контрагент.Пустая() Тогда
				ПоляЗаполнения = ПоляЗаполнения + ", Контрагент";
			КонецЕсли;			
			Если НоваяСтрока.Организация.Пустая() Тогда
				ПоляЗаполнения = ПоляЗаполнения + ", Организация";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета, ПоляЗаполнения);
			НоваяСтрока.СуммаРасчетная = НоваяСтрока.СуммаБазовая * (СтрокаРасчета.РазмерБонуса/100);
			
			ЗаполнитьСтрокуАналитикиИзНачисления(СтрокаНачисления, НоваяСтрока);
		КонецЦикла;
		
	ИначеЕсли ВсеОк И тРезультат.Количество() > 0 И СтрокаРасчета.УстановкаМаркетинга.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтОплат Тогда 
		СтрокаРасчета.ДатаРасчета = ТекущаяДата();
		СтрокаРасчета.РасчетнаяБаза = Сумма;
		тСуммаРасчетная = Окр(СтрокаРасчета.РазмерБонуса*Сумма/100,2,РежимОкругления.Окр15как20);
		СтрокаРасчета.СуммаРасчетная = тСуммаРасчетная;
		СтрокаНачисления.СуммаРасчетная = тСуммаРасчетная; 

	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВсеОк;
КонецФункции

&НаСервере
Процедура ЗаполнитьСтрокуАналитикиИзНачисления(СтрНачисления, СтрАналитики) Экспорт 
	ПоляЗаполнения = "ИДНачисления, ТипБонуса";
	
	Если СтрАналитики.Организация.Пустая() Тогда 
		ПоляЗаполнения = ПоляЗаполнения + ", Организация";
	КонецЕсли;
	
	Если СтрАналитики.Контрагент.Пустая() Тогда
		ПоляЗаполнения = ПоляЗаполнения + ", Контрагент";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрАналитики, СтрНачисления, ПоляЗаполнения);
КонецПроцедуры

&НаСервере
Процедура РассчитатьБонусы(ПараметрыНачисления, ПараметрыРасчета, ОчиститьАналитику = Ложь) Экспорт	
	
	ЕстьОшибки = Ложь;
	стрОшибки = "";
	ТекДата = ТекущаяДата();
		
	Если ОчиститьАналитику Тогда 
		УдалитьСтрокиАналитики(ПараметрыРасчета.УстановкаМаркетинга);			
	КонецЕсли;
	
	Если ПараметрыРасчета.ФиксированныйБонус Тогда
		ПараметрыРасчета.СуммаРасчетная = ПараметрыРасчета.РазмерБонуса;
		ПараметрыНачисления.СуммаРасчетная =  ПараметрыРасчета.РазмерБонуса;
	Иначе
		ЕстьОшибки = НЕ РассчитатьБонусыСогласноНастроек(ПараметрыНачисления, ПараметрыРасчета, стрОшибки);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьАкт(СтрНачисления)	

	ТекДата = ТекущаяДата();
	ДатаИсточника = СтрНачисления.КонецРасчета;
	
	НоваяДата = Дата(Год(ДатаИсточника), Месяц(ДатаИсточника), День(ДатаИсточника), Час(ТекДата), Минута(ТекДата), Секунда(ТекДата));
	
	Док = Документы.РеализацияТоваров.СоздатьДокумент();
	Док.Дата = КонецМесяца(СтрНачисления.КонецРасчета)-1;
	Док.ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг;
	Док.РегламентированныйУчет = СтрНачисления.УстановкаМаркетинга.РегламентированныйУчет;
	Док.ПодразделениеКомпании = СтрНачисления.УстановкаМаркетинга.ПодразделениеКомпании;//
	Док.Организация = СтрНачисления.Организация;
	Док.Автор = ПараметрыСеанса.АвторизованныйПользователь;
	Док.Менеджер = СтрНачисления.Менеджер;
	Док.Контрагент = СтрНачисления.Контрагент;
	Док.ДоговорВзаиморасчетов = СтрНачисления.Договор;
	Док.ВалютаДокумента = Док.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	Док.ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	Док.КурсДокумента = 1;
	Док.Комментарий = СтрНачисления.ФормулировкаСчета;
	Док.РасчетБонусов = Объект.Ссылка;
	Док.УстановкаМаркетинга = СтрНачисления.УстановкаМаркетинга;
	Док.ТорговаяПлощадка = ПолучитьТорговуюПлощадку(ДОк.ПодразделениеКомпании);
	
	Стр = Док.Товары.Добавить();
	Стр.Номенклатура = СтрНачисления.УстановкаМаркетинга.Услуга;
	Стр.Количество = 1;
	стр.КоличествоБазовое = 1;
	стр.Коэффициент = 1;
	Стр.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиЕдиницуИзмеренийПоБазовой(Стр.Номенклатура);
	стр.Цена =  СтрНачисления.СуммаСогласованная;
	Стр.ЦенаБезНалогов = СтрНачисления.СуммаСогласованная;
	Стр.СуммаВсего = СтрНачисления.СуммаСогласованная;
	Стр.Сумма = СтрНачисления.СуммаСогласованная;
	Стр.ТипБонуса = СтрНачисления.ТипБонуса;
	Стр.Содержание = СтрНачисления.Содержание;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");

	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, Неопределено);
	
	СтруктураОтбора = Новый Структура("УстановкаМаркетинга, ИДНачисления");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрНачисления);
	
	СтрокиАналитики = Объект.АналитикаРасчетов.НайтиСтроки(СтруктураОтбора);
	тзАналитики = Объект.АналитикаРасчетов.Выгрузить(СтрокиАналитики);
	
	ИтогСуммаРасчетная = тзАналитики.Итог("СуммаРасчетная");
	Для Каждого СтрАналитика Из тзАналитики Цикл 
		СтрРаспределениеМаркетинга = Док.РаспределениеМаркетинга.Добавить();
		СтрРаспределениеМаркетинга.Дата = КонецДня(СтрНачисления.КонецРасчета);
		СтрРаспределениеМаркетинга.Номенклатура = СтрАналитика.Номенклатура;
		СтрРаспределениеМаркетинга.ТорговаяПлощадка = СтрАналитика.ТорговаяПлощадка;
		СтрРаспределениеМаркетинга.СтавкаНДС = СтрАналитика.Номенклатура.СтавкаНДС;
		СтрРаспределениеМаркетинга.СуммаВсего = (СтрАналитика.СуммаРасчетная/ИтогСуммаРасчетная) * СтрНачисления.СуммаСогласованная;
	КонецЦикла;		
	
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
		ОбщегоНазначения.СообщитьПользователю("Проведен документ "+Док,Док.Ссылка);
	Исключение
		Попытка
			Док.Записать();
			ОбщегоНазначения.СообщитьПользователю("Записан документ "+Док,Док.Ссылка);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Ошибка  "+ОписаниеОшибки());
		КонецПопытки;		
	КонецПопытки;
	
	Если СтрНачисления.УстановкаМаркетинга.РегламентированныйУчет Тогда
		ДокСчет = Документы.СчетНаОплату.СоздатьДокумент();
		ДокСчет.Заполнить(Док.Ссылка);
		ДокСчет.Дата = КонецМесяца(СтрНачисления.КонецРасчета);
		Попытка
			ДокСчет.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю("Проведен документ "+ДокСчет,ДокСчет.Ссылка);
		Исключение
			Попытка
				ДокСчет.Записать();
				ОбщегоНазначения.СообщитьПользователю("Записан документ "+ДокСчет,ДокСчет.Ссылка);
			Исключение
				ОбщегоНазначения.СообщитьПользователю("Ошибка  "+ОписаниеОшибки());
			КонецПопытки;		
		КонецПопытки;
		
		СтрНачисления.ДокументСчет = ДокСчет.Ссылка;
	КонецЕсли;
			
	СтрНачисления.АктОбОказанииУслуг = Док.Ссылка;
	
КонецПроцедуры
	
&НаСервере
Процедура РасчитатьКомплектНаСервере()
	
	ТаблицаКомплеков = Объект.Начисление.Выгрузить(Новый Структура("Пометка",Истина));
	
	ТаблицаКонтрагентов = ТаблицаКомплеков.Скопировать(,"Договор");
	ТаблицаКонтрагентов.Свернуть("Договор");
	Для Каждого стр  Из ТаблицаКонтрагентов Цикл
		НайденыеСтроки = ТаблицаКомплеков.НайтиСтроки(Новый Структура("Договор",стр.Договор));
		
		Если НайденыеСтроки.Количество() > 0 Тогда
			СформироватьАктКомплект(НайденыеСтроки);
		КонецЕсли; 						
	КонецЦикла; 
	
	ПересчитатьСуммуДокумента();

КонецПроцедуры

&НаСервере
Процедура СформироватьАктКомплект(НайденыеСтроки)	

	ЕстьДокСчет = Ложь;

	ТаблицаУслуг = Новый ТаблицаЗначений;
	ТаблицаУслуг.Колонки.Добавить("Услуга"); 
	ТаблицаУслуг.Колонки.Добавить("СуммаСогласованная"); 
	ТаблицаУслуг.Колонки.Добавить("ТипБонуса"); 
	ТаблицаУслуг.Колонки.Добавить("УстановкаМаркетинга"); 
	
	Для Каждого стр Из НайденыеСтроки Цикл
		НовыйСтрока = ТаблицаУслуг.Добавить();
		НовыйСтрока.Услуга = стр.УстановкаМаркетинга.Услуга;
		НовыйСтрока.СуммаСогласованная = стр.СуммаСогласованная;
		НовыйСтрока.ТипБонуса = стр.ТипБонуса;
		НовыйСтрока.УстановкаМаркетинга = стр.УстановкаМаркетинга;
	КонецЦикла;
	ТаблицаУслуги = ТаблицаУслуг.Скопировать(,"Услуга,ТипБонуса,СуммаСогласованная");
	ТаблицаУслуги.Свернуть("Услуга,ТипБонуса","СуммаСогласованная");
	
	Док = Документы.РеализацияТоваров.СоздатьДокумент();
	Док.Дата = КонецМесяца(НайденыеСтроки[0].КонецРасчета)-1;
	Док.ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг;
	Док.РегламентированныйУчет = НайденыеСтроки[0].УстановкаМаркетинга.РегламентированныйУчет;
	Док.ПодразделениеКомпании = НайденыеСтроки[0].УстановкаМаркетинга.ПодразделениеКомпании;//
	Док.Организация = НайденыеСтроки[0].Организация;
	Док.Автор = ПараметрыСеанса.АвторизованныйПользователь;
	Док.Менеджер = НайденыеСтроки[0].Менеджер;
	Док.Контрагент = НайденыеСтроки[0].Контрагент;
	Док.ДоговорВзаиморасчетов = НайденыеСтроки[0].Договор;
	Док.ВалютаДокумента = Док.ДоговорВзаиморасчетов.ВалютаВзаиморасчетов;
	Док.ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	Док.КурсДокумента = 1;
	Док.Комментарий = НайденыеСтроки[0].ФормулировкаСчета;
	Док.РасчетБонусов = Объект.Ссылка;
	Док.ТорговаяПлощадка = ПолучитьТорговуюПлощадку(ДОк.ПодразделениеКомпании);
	
	Для Каждого СтрНачисления  Из ТаблицаУслуги Цикл
		
		Стр = Док.Товары.Добавить();
		Стр.Номенклатура = СтрНачисления.Услуга;
		Стр.Количество = 1;
		стр.КоличествоБазовое = 1;
		стр.Коэффициент = 1;
		Стр.ЕдиницаИзмерения = Справочники.ЕдиницыИзмерения.НайтиЕдиницуИзмеренийПоБазовой(Стр.Номенклатура);
		стр.Цена =  СтрНачисления.СуммаСогласованная;
		Стр.ЦенаБезНалогов = СтрНачисления.СуммаСогласованная;
		Стр.СуммаВсего = СтрНачисления.СуммаСогласованная;
		Стр.Сумма = СтрНачисления.СуммаСогласованная;
		Стр.ТипБонуса = СтрНачисления.ТипБонуса;
		
		СтруктураДействий = Новый Структура;
		
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, Неопределено);
		
		СтруктураОтбора = Новый Структура("УстановкаМаркетинга, ИДНачисления");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, стр);
		
		СтрокиАналитики = Объект.АналитикаРасчетов.НайтиСтроки(СтруктураОтбора);
		тзАналитики = Объект.АналитикаРасчетов.Выгрузить(СтрокиАналитики);
		
		ИтогСуммаРасчетная = тзАналитики.Итог("СуммаРасчетная");
		Для Каждого СтрАналитика Из тзАналитики Цикл 
			СтрРаспределениеМаркетинга = Док.РаспределениеМаркетинга.Добавить();
			СтрРаспределениеМаркетинга.Дата = КонецМесяца(НайденыеСтроки[0].КонецРасчета)-1;
			СтрРаспределениеМаркетинга.Номенклатура = СтрАналитика.Номенклатура;
			СтрРаспределениеМаркетинга.ТорговаяПлощадка = СтрАналитика.ТорговаяПлощадка;
			СтрРаспределениеМаркетинга.СтавкаНДС = СтрАналитика.Номенклатура.СтавкаНДС;
			СтрРаспределениеМаркетинга.СуммаВсего = (СтрАналитика.СуммаРасчетная/ИтогСуммаРасчетная) * СтрНачисления.СуммаСогласованная;
		КонецЦикла;		
	КонецЦикла; 

	Для Каждого стр Из ТаблицаУслуг Цикл
		НоваяСтрока = Док.РаспределениеУстоновокМаркетинга.Добавить();
		НоваяСтрока.УстановкаМаркетинга = стр.УстановкаМаркетинга;
	КонецЦикла; 
	
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
		ОбщегоНазначения.СообщитьПользователю("Проведен документ "+Док,Док.Ссылка);
	Исключение
		Попытка
			Док.Записать();
			ОбщегоНазначения.СообщитьПользователю("Записан документ "+Док,Док.Ссылка);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Ошибка  "+ОписаниеОшибки());
		КонецПопытки;		
	КонецПопытки;
	
	Если НайденыеСтроки[0].УстановкаМаркетинга.РегламентированныйУчет Тогда
		ДокСчет = Документы.СчетНаОплату.СоздатьДокумент();
		ДокСчет.Заполнить(Док.Ссылка);
		ДокСчет.Дата = КонецМесяца(НайденыеСтроки[0].КонецРасчета);
		Попытка
			ДокСчет.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю("Проведен документ "+ДокСчет,ДокСчет.Ссылка);
			ЕстьДокСчет = Истина;
		Исключение
			Попытка
				ДокСчет.Записать();
				ОбщегоНазначения.СообщитьПользователю("Записан документ "+ДокСчет,ДокСчет.Ссылка);
				ЕстьДокСчет = Истина;
			Исключение
				ОбщегоНазначения.СообщитьПользователю("Ошибка  "+ОписаниеОшибки());
			КонецПопытки;		
		КонецПопытки;
	КонецЕсли;
	
	Для Каждого Стр Из НайденыеСтроки Цикл
		НайтиСтроку = Объект.Начисление.НайтиПоИдентификатору(стр.номерСтроки-1);
		
		НайтиСтроку.АктОбОказанииУслуг = Док.Ссылка;
		
		Если ЕстьДокСчет Тогда
			НайтиСтроку.ДокументСчет = ДокСчет.Ссылка;
		КонецЕсли; 	
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиАналитики(УстановкаМаркетинга = Неопределено, ИДУсловия = Неопределено, ИДНачисления = Неопределено)  
	СтруктураОтбора = Новый Структура;
	
	Если УстановкаМаркетинга <> Неопределено Тогда
		СтруктураОтбора.Вставить("УстановкаМаркетинга", УстановкаМаркетинга);	
	КонецЕсли;
	
	Если ИДУсловия <> Неопределено Тогда
		СтруктураОтбора.Вставить("ИДУсловия", ИДУсловия);
	КонецЕсли;
	
	Если ИДНачисления <> Неопределено Тогда 
		СтруктураОтбора.Вставить("ИДНачисления", ИДНачисления);
	КонецЕсли;
	
	МассивСтрокАналитики = Объект.АналитикаРасчетов.НайтиСтроки(СтруктураОтбора);
	
	Для Каждого Стр Из МассивСтрокАналитики Цикл
		Объект.АналитикаРасчетов.Удалить(Стр);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначенияФлажков(Значение)
	
	Для Каждого СтрТовары Из Объект.Начисление Цикл 
		СтрТовары.Пометка = Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьАктОбОказанииУслуг(АктУслуг, Счет) Экспорт
	Если АктУслуг.Пустая() ИЛИ АктУслуг.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
		
	Попытка
		тОбъект = АктУслуг.ПолучитьОбъект();
		тОбъект.УстановитьПометкуУдаления(Истина);
	Исключение
		ОбщегоНазначения.СообщитьПользователю("Не удалось удалить """ + АктУслуг + """. " + Символы.ПС + "Описание ошибки: " + ОписаниеОшибки());
	КонецПопытки;
	
	Если НЕ Счет.пустая() Тогда
		Попытка
			тОбъект = Счет.ПолучитьОбъект();
			тОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось удалить """ + Счет + """. " + Символы.ПС + "Описание ошибки: " + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборРасчеты(УстановкаМаркетинга);
	
	Если ТипЗнч(УстановкаМаркетинга) <> Тип("ДокументСсылка.ТК_УстановкаМаркетинга") Тогда
		УстановкаМаркетинга = ВозвратСсылка("ТК_УстановкаМаркетинга");	
	КонецЕсли;
	
	тФиксированнаяСтруктура = Новый ФиксированнаяСтруктура("УстановкаМаркетинга", УстановкаМаркетинга);
	фРасчеты = Элементы.Расчеты;
	фРасчеты.ОтборСтрок = тФиксированнаяСтруктура;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборАналитика(СтрокаНачисления)	
	Если СтрокаНачисления = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	фАналитика = Элементы.АналитикаРасчетов;	
	
	тФиксированнаяСтруктура = Новый ФиксированнаяСтруктура("УстановкаМаркетинга", СтрокаНачисления.УстановкаМаркетинга);

	фАналитика.ОтборСтрок = тФиксированнаяСтруктура;
	
	Если НЕ СтрокаНачисления.УстановкаМаркетинга.Пустая() Тогда								
		тФиксированнаяСтруктура = Новый ФиксированнаяСтруктура("ИДНачисления", СтрокаНачисления.ИДНачисления);

		фАналитика.ОтборСтрок = тФиксированнаяСтруктура;
		
		фАналитика.ИзменятьСоставСтрок = Истина;
		фАналитика.ТолькоПросмотр = Ложь;
		
	Иначе
		тФиксированнаяСтруктура = Новый ФиксированнаяСтруктура("ИДНачисления", Новый УникальныйИдентификатор);

		фАналитика.ОтборСтрок = тФиксированнаяСтруктура;		
		
		фАналитика.ИзменятьСоставСтрок = Ложь;
		фАналитика.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция НайтиАкт(УстановкаМаркетинга) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	РеализацияТоваров.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ГДЕ
		|	РеализацияТоваров.Проведен
		|	И РеализацияТоваров.УстановкаМаркетинга = &УстановкаМаркетинга
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеализацияТоваров.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("УстановкаМаркетинга", УстановкаМаркетинга);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Документы.РеализацияТоваров.ПустаяСсылка();
	Иначе	
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка; 
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтбор()	
	
	ТекСтрока = Элементы.Начисление.ТекущиеДанные;
	
	Если ТекСтрока = Неопределено Тогда
		УстановкаМаркетинга = Неопределено;
	Иначе 
		УстановкаМаркетинга = ТекСтрока.УстановкаМаркетинга;
	КонецЕсли;
	
	УстановитьОтборРасчеты(УстановкаМаркетинга);
	УстановитьОтборАналитика(ТекСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = СокрЛП(Объект.Начисление.Итог("СуммаСогласованная"));
	ИтогСуммаРасчетная 	  = СокрЛП(Объект.Начисление.Итог("СуммаРасчетная")); 
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗакрытияВопросаУдалениеАкта(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	УдалитьАктОбОказанииУслуг(Параметры.СтрокаНачисления.АктОбОказанииУслуг,Параметры.СтрокаНачисления.ДокументСчет);

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	СтатусУтвержден = ПроверитьСтатусУтвержден(Объект.Статус);
	Если СтатусУтвержден Тогда
		Элементы.СоздатьАкт.Видимость = Истина;
		Элементы.НачислениеРасчитатьКомплект.Видимость = Истина;
	Иначе
		Элементы.СоздатьАкт.Видимость = Ложь;
		Элементы.НачислениеРасчитатьКомплект.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьСтатусУтвержден(Статус)
	
	Если Статус = Перечисления.СтатусыАссортиментногоКомитета.Утвержден Тогда
		Возврат Истина;
	Иначе
		Возврат ложь;
	КонецЕсли; 
	
КонецФункции 

&НаСервере
Функция ПолучитьТорговуюПлощадку(Подразделение)
	
	Если Подразделение.Код = "000000002" Тогда
		Возврат Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000110");
	ИначеЕсли Подразделение.Код = "000000008" Тогда
		Возврат Справочники.ТорговыеПлощадки.НайтиПоКоду("YY000118");
	ИначеЕсли Подразделение.Код = "AT000009" Тогда
		Возврат Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000285");
	ИначеЕсли Подразделение.Код = "AT000014" Тогда
		Возврат Справочники.ТорговыеПлощадки.НайтиПоКоду("GL000165");
	ИначеЕсли Подразделение.Код = "AT000010" Тогда
		Возврат Справочники.ТорговыеПлощадки.НайтиПоКоду("ХGL000252");
	Иначе
		Возврат Справочники.ТорговыеПлощадки.ПустаяСсылка();
	КонецЕсли; 
	
КонецФункции 

#КонецОбласти


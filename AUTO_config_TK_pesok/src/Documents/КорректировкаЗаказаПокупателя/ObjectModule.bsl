#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДата();
	ДокументОснование = Неопределено;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый"		, ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи"	, РежимЗаписи);
	
	/////Не регистрировать для выгрузки голобал центр
	//Если НЕ ТорговаяПлощадка.РегистрироватьТК Тогда
	//	ДополнительныеСвойства.Вставить("ОбменТК");
	//КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата
		|ИЗ
		|	Документ.КорректировкаЗаказаПокупателя КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);		
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И Не Отказ Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказПокупателя.Проведен КАК Проведен,
		|	ЗаказПокупателя.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.Ссылка = &Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыПокупателейОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыПокупателейОстатки.ЗаказаноОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ЗаказыПокупателей.Остатки(&НаДату, Заказ = &Заказ) КАК ЗаказыПокупателейОстатки
		|ГДЕ
		|	ЗаказыПокупателейОстатки.ЗаказаноОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервыТоваровОстатки.Номенклатура,
		|	РезервыТоваровОстатки.РезервОстаток
		|ИЗ
		|	РегистрНакопления.РезервыТоваров.Остатки(&НаДату, Заказ = &Заказ) КАК РезервыТоваровОстатки
		|ГДЕ
		|	РезервыТоваровОстатки.РезервОстаток > 0";
		
		ДатаЗапроса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "СтараяДатаДокумента", Дата);
		
		Запрос.УстановитьПараметр("НаДату", Новый Граница(ДатаЗапроса, ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("Заказ", ДокументОснование);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		РеквизитыЗаказа = МассивРезультатов[0].Выбрать();		
		Результат = МассивРезультатов[1];
		
		Если РеквизитыЗаказа.Следующий() Тогда
			Если РеквизитыЗаказа.ПометкаУдаления Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заказ ""%1"" помечен на удаление'; uk = 'Замовлення ""%1"" помічено на видалення'"),
				ДокументОснование),
				Ссылка,,,
				Отказ);
				
			ИначеЕсли Не РеквизитыЗаказа.Проведен Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заказ ""%1"" не проведен'; uk = 'Замовлення ""%1"" не проведене'"),
				ДокументОснование),
				Ссылка,,,
				Отказ);			
			КонецЕсли;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По заказу ""%1"" не удалось получить данные'; uk = 'По замовленню ""%1"" не вдалося отримати дані'"),
			ДокументОснование),
			Ссылка,,,
			Отказ);
		КонецЕсли;
		
		Если Результат.Пустой() И Не Отказ Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заказ ""%1"" помечен на удаление'; uk = 'Замовлення ""%1"" помічено на видалення'"),
			ДокументОснование),
			Ссылка,,,
			Отказ);
		КонецЕсли;
		
		Если Не Отказ Тогда 
			ДополнительныеСвойства.Вставить("ЗаказПокупателя", ДокументОснование);
		КонецЕсли;
	КонецЕсли;		
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);		
	Документы.КорректировкаЗаказаПокупателя.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаказыСервер.ОтразитьЗаказыПокупателей(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьРезервыТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УстановитьСтатусОчереди(ЭтотОбъект, ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ЗаказПросрочен", Ложь));
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	//Если ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателяВнутренний") Тогда 
	//	МассивНепроверяемыхРеквизитов.Добавить("Товары.Цена");
	//	МассивНепроверяемыхРеквизитов.Добавить("Товары.ЦенаБезНалогов");
	//КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ОбщегоНазначенияТК.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект,  ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата				  = ТекущаяДата();
	Автор 				  = Пользователи.ТекущийПользователь();
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ТорговаяПлощадка      = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяТорговаяПлощадка",,ТорговаяПлощадка);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Автор = Пользователи.ТекущийПользователь();
	ТипЦен = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ЗаказПокупателя.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ЗапросНайтиПодчиненныйПоТипу(Знач ДокСсылка,Знач ТипДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст =     "ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК ДокСсылка,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").Проведен
	|	КОНЕЦ КАК Проведен
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Док) КАК СвязанныеДокументы
	|ГДЕ
	|	СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"";
	Запрос.УстановитьПараметр("Док",ДокСсылка);
	
	Возврат Запрос;
	
КонецФункции	

Процедура УстановитьСтатусОчереди(ДокСсылка,ЗаказПросрочен)
	
	Если ЗаказПросрочен = Истина Тогда
		Статус = Перечисления.СтатусыСборкиРезервов.Просрочен;
	Иначе
		Статус = Перечисления.СтатусыСборкиРезервов.Отменен;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РезервированиеЗаказовПокупателя.Ссылка КАК Резерв,
	|	РезервированиеЗаказовПокупателя.ДокументОснование КАК ЗаказПокупателя
	|ПОМЕСТИТЬ Вт
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	|ГДЕ
	|	РезервированиеЗаказовПокупателя.ДокументОснование = &ДокСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.Ссылка,
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя.РаспределениеОтгрузки КАК РезервированиеЗаказовПокупателяРаспределениеОтгрузки
	|ГДЕ
	|	РезервированиеЗаказовПокупателяРаспределениеОтгрузки.ЗаказПокупателя.Ссылка = &ДокСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Вт.Резерв КАК Резерв,
	|	Вт.Резерв.ТорговаяПлощадка КАК ТорговаяПлощадка
	|ИЗ
	|	Вт КАК Вт
	|
	|СГРУППИРОВАТЬ ПО
	|	Вт.Резерв,
	|	Вт.Резерв.ТорговаяПлощадка";
	
	Запрос.УстановитьПараметр("ДокСсылка", ДокСсылка.ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ = ВыборкаДетальныеЗаписи.Резерв;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ЗаписьВОчереди.Статус = Статус;
			ЗаписьВОчереди.Записать(Истина);
		КонецЕсли;
		СтруктураСостояние = Новый  Структура("Состояние,ДатаПоследнегоИзменения,АвторИзменения,ТорговаяПлощадка"
		,Статус,ТекущаяДатаСеанса(),Пользователи.ТекущийПользователь().ФизическоеЛицо,ВыборкаДетальныеЗаписи.ТорговаяПлощадка);
		РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(ВыборкаДетальныеЗаписи.Резерв,СтруктураСостояние);
 	КонецЕсли; 
	
	
КонецПроцедуры 

#КонецОбласти




#Иначе

ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли
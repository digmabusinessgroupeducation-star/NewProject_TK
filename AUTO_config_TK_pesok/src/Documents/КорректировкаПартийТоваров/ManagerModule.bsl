#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
	ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровКорректировкаПартий(Запрос, ДополнительныеСвойства);	

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	Организация = Справочники.СкладыКомпании.ОрганизацияСклада(ДокументСсылка.СкладКомпании, ДокументСсылка.Дата);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	КорректировкаПартийТоваров.Дата КАК Период,
	               |	КорректировкаПартийТоваров.Ссылка КАК Ссылка,
	               |	КорректировкаПартийТоваров.ХозОперация КАК ХозОперация,
	               |	КорректировкаПартийТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	КорректировкаПартийТоваров.СкладКомпании КАК СкладКомпании
	               |ИЗ
	               |	Документ.КорректировкаПартийТоваров КАК КорректировкаПартийТоваров
	               |ГДЕ
	               |	КорректировкаПартийТоваров.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("ДатаДокумента", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании",Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("Период",Реквизиты.Период);

КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса =		
					"ВЫБРАТЬ
					|	Товары.Номенклатура КАК Номенклатура,
					|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
					|ПОМЕСТИТЬ ВтТаблицаТовары
					|ИЗ
					|	Документ.КорректировкаПартийТоваров.ПартииТоваровКомпании КАК Товары
					|ГДЕ
					|	Товары.Ссылка.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции



#КонецОбласти


// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");

	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
	НоваяСтрока.Период   	  = Ссылка.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
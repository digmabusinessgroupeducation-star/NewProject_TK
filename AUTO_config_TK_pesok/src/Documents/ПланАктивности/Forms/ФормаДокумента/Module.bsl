&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

&НаКлиенте
Перем НужноЗаполнитьНапоказ;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьСоответствиеПолейСвязи();
		//Если ЗначениеЗаполнено(Объект.СкладыКомпании)Тогда
		ЗаполнитьНапоказИзПланы();
		//КонецЕсли;	
	КонецЕсли;	
	
	Элементы.ТоварнаяМарка.Видимость = Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности");
	Элементы.ГруппаРасчет.Видимость = Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности");
	Элементы.СтраницаТЧПланы.Видимость = РольДоступна("ПолныеПрава");
	Элементы.ФормаВыполнениеПлановАктивности.Видимость = Объект.Проведен;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	НужноЗаполнитьНапоказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаполнитьСоответствиеПолейСвязи();
	
	ЗаполнитьНапоказИзПланы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЕстьДублиСкладов(Объект.Напоказ) Тогда
		Отказ = Истина;
		ПоказатьПредупреждениеОшибки();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства			
	
	Если МодифицированностьНапоказ Тогда
		ЗаполнитьПланыИзНапоказ(ТекущийОбъект);
	КонецЕсли;
	
	ТекущийОбъект.Напоказ.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если НужноЗаполнитьНапоказ Тогда
		ЗаполнитьНапоказИзПланы();
	КонецЕсли;
	Элементы.ФормаВыполнениеПлановАктивности.Видимость = Объект.Проведен;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	НужноЗаполнитьНапоказ = Ложь;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗагрузитьПланы(Команда)
	
	ЕстьОшибки = Ложь;
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Фильтр = "(*.xlsx)|*.xlsx|(*.xls)|*.xls|(*.ods)|*.ods";	
	ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл'; uk = 'Оберіть файл'");
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПродолжениеПомещенияФайлов", ЭтаФорма);
	
	НачатьПомещениеФайловНаСервер(ОповещениеЗавершения,,,ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеПомещенияФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт 
	
	Если ПомещенныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	МассивФайлов = Новый Массив;
	Для Каждого нФайл Из ПомещенныеФайлы Цикл 
		Если нФайл.ПомещениеФайлаОтменено Тогда 
			Продолжить;
		КонецЕсли;
		
		СтруктураФайла = Новый Структура;
		СтруктураФайла.Вставить("Адрес", нФайл.Адрес);
		СтруктураФайла.Вставить("Имя", нФайл.СсылкаНаФайл.Имя);
		СтруктураФайла.Вставить("Расширение", нФайл.СсылкаНаФайл.Расширение);
		
		МассивФайлов.Добавить(СтруктураФайла);
	КонецЦикла;	
	
	Если МассивФайлов.Количество() > 0 Тогда 
		ПрочитатьНаСервере(МассивФайлов);
		ЗаполнитьАктивность(Истина);
	Иначе 
		ПоказатьПредупреждение(, НСтр("ru = 'Нет выбранных файлов'; uk = 'Немає обраних файлів'"));
	КонецЕсли;
	Если ЕстьОшибки Тогда
		ПоказатьПредупреждениеОшибки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеПлановАктивности(Команда)
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрСсылка",Объект.Ссылка);
	ОткрытьФорму("Отчет.ВыполнениеПлановАктивности.Форма.ФормаОтчета", ПараметрыОткрытия);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьАктивность();
КонецПроцедуры

&НаКлиенте
Процедура ТоварнаяМаркаПриИзменении(Элемент)
	ЗаполнитьАктивность();
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	
	Элементы.ГруппаРасчет.Видимость = Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности");
	Элементы.ТоварнаяМарка.Видимость = Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности");
	ЗаполнитьАктивность();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоДействияПриИзменении(Элемент)
	ПериодДействияПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеДействияПриИзменении(Элемент)
	ПериодДействияПриИзменении();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНапоказ

&НаКлиенте
Процедура НапоказПриИзменении(Элемент)
	МодифицированностьНапоказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НапоказСкладКомпанииПриИзменении(Элемент)
	сКлад = Элементы.Напоказ.ТекущиеДанные.СкладКомпании;
	Если ЗначениеЗаполнено(сКлад) Тогда
		мСтрок = Объект.Напоказ.НайтиСтроки(Новый Структура("СкладКомпании",сКлад));
		Если мСтрок.Количество()>1 Тогда
			НомераСтрок = "";
			Для Каждого строка Из мСтрок Цикл
				НомераСтрок = НомераСтрок + строка.НомерСтроки + ",";	
			КонецЦикла;
			ПоказатьПредупреждение(,СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбранный склад /%1/ уже есть в документе, строки %2!!!'; uk = 'Обраний склад /%1/ вже є в документі, рядки %2!!!'"),
			сКлад, Лев(НомераСтрок,СтрДлина(НомераСтрок)-1)));
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Склад не может быть пустым!!!'; uk = 'Склад не може бути порожнім!!!'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НапоказСкладКомпанииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	спСкладов = ПолучитьСписокСкладовТЧ(Объект.Напоказ);
	спСкладов.Удалить(спСкладов.НайтиПоЗначению(Элементы.Напоказ.ТекущиеДанные.СкладКомпании));
	
	фиксНастройки = Новый НастройкиКомпоновкиДанных;
	
	эОтбор = фиксНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	эОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ССылка");
	эОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	эОтбор.ПравоеЗначение = спСкладов;
	эОтбор.Использование = Истина;
	
	эОтбор = фиксНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	эОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	эОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	эОтбор.ПравоеЗначение = Ложь;
	эОтбор.Использование = Истина;
	
	эОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФиксированныеНастройки", фиксНастройки);
	
	ОткрытьФорму("Справочник.СкладыКомпании.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

//	Загрузка планов из EXCEL
&НаСервере
Процедура ПрочитатьНаСервере(ПомещенныеФайлы)
	ТабДок = Новый ТабличныйДокумент;
	
	Для Каждого нФайл Из ПомещенныеФайлы Цикл 
		Попытка
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(нФайл.Адрес);
			ИмяФайла = ПолучитьИмяВременногоФайла(нФайл.Расширение);
			ДвоичныеДанные.Записать(ИмяФайла);
			
			ТабДок.Прочитать(ИмяФайла);
			
			УдалитьФайлы(ИмяФайла);
			
			ОбработатьФайл(нФайл.Имя,ТабДок);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка загрузки файла ''%1''
			|Описание ошибки: %2'; uk = 'Помилка завантаження файлу ''%1''
			|Опис помилки: %2'"),
			нФайл.Имя,
			ИнформацияОбОшибке().Описание);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);		
		КонецПопытки;
		
		ТабДок.Очистить();
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПериод(СтрокаПериод,ДатаДок)
	
	Период = Новый СтандартныйПериод;
	НачалоММДД = Сред(СтрокаПериод,СтрНайти(НРег(СтрокаПериод),"план")+5,5);
	КонецММДД = Прав(СтрокаПериод,СтрДлина(СтрокаПериод)-СтрНайти(СтрокаПериод,"-"));
	Период.ДатаНачала = Дата(НачалоММДД+"."+Формат(Год(ДатаДок),"ЧГ=0")+" 0:0:0");
	Период.ДатаОкончания = Дата(КонецММДД+"."+Формат(Год(ДатаДок),"ЧГ=0")+" 0:0:0");
	//Период.ДатаНачала = Дата(Формат(Год(Объект.Дата),"ЧГ=0")+СтрЗаменить(Прав(НачалоММДД,2),".","")+СтрЗаменить(Лев(НачалоММДД,2),".",""));
	//Период.ДатаОкончания = Дата(Формат(Год(Объект.Дата),"ЧГ=0")+СтрЗаменить(Прав(КонецММДД,2),".","")+СтрЗаменить(Лев(КонецММДД,2),".",""));
	Возврат Период;
КонецФункции

&НаСервереБезКонтекста
Процедура ПроверитьПериодыНаГод(ТаблицаПериодов);
	
	втВид = ТаблицаПериодов.Скопировать();
	втВид.Свернуть("ВидПериода");
	
	Для Каждого вид Из втВид Цикл
		ПредыдущееОкончание = Неопределено;
		Для Каждого строка Из ТаблицаПериодов.НайтиСтроки(Новый Структура("ВидПериода",вид.ВидПериода)) Цикл
			Если ЗначениеЗаполнено(ПредыдущееОкончание) И ПредыдущееОкончание > строка.ОкончаниеДействия Тогда
				строка.НачалоДействия = ДобавитьМесяц(строка.НачалоДействия,12);
				строка.ОкончаниеДействия = ДобавитьМесяц(строка.ОкончаниеДействия,12)
			Иначе
				Если строка.НачалоДействия > строка.ОкончаниеДействия Тогда
					строка.ОкончаниеДействия = ДобавитьМесяц(строка.ОкончаниеДействия,12);
				КонецЕсли;
			КонецЕсли;
			ПредыдущееОкончание = строка.ОкончаниеДействия;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьФайл(ФайлИсточник,ТабДок)
	
	СтрокаПериодов	= 2;
	СтрокаТоваров	= 4;
	СтрокаСкладов	= 5;
	
	КоличествоСтрок = ТабДок.ВысотаТаблицы;
	КоличествоКолонок = ТабДок.ШиринаТаблицы;
	
	ТоварНеНайден = Ложь;
	ЕстьОшибкаСклада = Ложь;
	
	СоответствиеТовары = Новый Соответствие;
	СоответствиеСклады = Новый Соответствие;
	
	СоответствиеТипов = Новый Соответствие;
	СоответствиеТипов.Вставить(Тип("СправочникСсылка.Номенклатура"),"А");
	СоответствиеТипов.Вставить(Тип("СправочникСсылка.ЦелевыеМарки"),"Г");
	СоответствиеТипов.Вставить(Тип("СправочникСсылка.ТоварныеМарки"),"М");
	
	ТаблицаПланов = Объект.Планы.Выгрузить().СкопироватьКолонки("СкладКомпании,ЦелеваяМарка,ВидПериода,НачалоДействия,ОкончаниеДействия,Количество,ПолеСвязи");
	
	ТаблицаМарок = Новый ТаблицаЗначений;
	ТаблицаМарок.Колонки.Добавить("ЦелеваяМарка");
	ТаблицаМарок.Колонки.Добавить("ПолеСвязи");
	
	ТаблицаПериодов = Новый ТаблицаЗначений;
	ТаблицаПериодов.Колонки.Добавить("Имя");
	ТаблицаПериодов.Колонки.Добавить("ВидПериода");
	ТаблицаПериодов.Колонки.Добавить("НачалоДействия");
	ТаблицаПериодов.Колонки.Добавить("ОкончаниеДействия");
	ТаблицаПериодов.Колонки.Добавить("ПолеСвязи");
	ТаблицаПериодов.Колонки.Добавить("Колонка");
	
	//	//	Найдем периоды	//	//
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		Период = СокрЛП(ТабДок.Область(СтрокаПериодов, НомерКолонки, СтрокаПериодов, НомерКолонки).Текст);
		Если СтрНайти(НРег(Период),"план") > 0 Тогда
			нСтрока = ТаблицаПериодов.Найти(Период,"Имя");
			Если нСтрока <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Период <%1> задублирован! Колонки: %2 и %3.'; uk = 'Період <%1> задублірован! Колонки %2 та %3.'"),
				Период,
				нСтрока.Колонка,
				НомерКолонки));
				ЕстьОшибки = Истина;
			КонецЕсли;
			стрПериода = ТаблицаПериодов.Добавить();
			стрПериода.Имя = Период;
			СтПериод = ПолучитьПериод(Период,Объект.Дата);
			стрПериода.НачалоДействия = СтПериод.ДатаНачала;
			стрПериода.ОкончаниеДействия = СтПериод.ДатаОкончания;
			стрПериода.Колонка = НомерКолонки;
			ДлительностьПериода = ?(СтПериод.ДатаОкончания>СтПериод.ДатаНачала,(СтПериод.ДатаОкончания-СтПериод.ДатаНачала+1)/86400,(ДобавитьМесяц(СтПериод.ДатаОкончания,12)-СтПериод.ДатаНачала+1)/86400);
			Если ДлительностьПериода > 27 Тогда
				стрПериода.ВидПериода = Перечисления.ВидыПериодаАктивности.Общий;
			ИначеЕсли ДлительностьПериода > 11 Тогда
				стрПериода.ВидПериода = Перечисления.ВидыПериодаАктивности.ПолМесяца;
			Иначе
				стрПериода.ВидПериода = Перечисления.ВидыПериодаАктивности.Неделя;
			КонецЕсли;
			ПолеСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1_%2_%3",
			стрПериода.ВидПериода,
			Формат(стрПериода.НачалоДействия,"ДФ=dd_MM_yyyy"),
			Формат(стрПериода.ОкончаниеДействия,"ДФ=dd_MM_yyyy"));
			стрПериода.ПолеСвязи = ПолеСвязи;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТаблицаПериодов) Тогда
		ПроверитьПериодыНаГод(ТаблицаПериодов);
		КонецОбластиКодовТовара = ?(ТаблицаПериодов.Количество() = 1,КоличествоКолонок,ТаблицаПериодов[1].Колонка - 1);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не найдена строка с периодами'; uk = 'Не знайден рядок з періодами'"));
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	//	//	Проверим товар	//	//
	Если ЗначениеЗаполнено(ТаблицаПериодов) Тогда
		Для Каждого стрПериод Из ТаблицаПериодов Цикл
			Для НомерКолонки = стрПериод.Колонка По КоличествоКолонок Цикл
				Марка = СокрЛП(ТабДок.Область(СтрокаТоваров, НомерКолонки, СтрокаТоваров, НомерКолонки).Текст);
				Если НомерКолонки <= КонецОбластиКодовТовара Тогда
					Артикул = СокрЛП(ТабДок.Область(СтрокаТоваров-1, НомерКолонки, СтрокаТоваров-1, НомерКолонки).Текст);
					Если ПустаяСтрока(Артикул) Тогда 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В колонке %1 незаполнен код товара ''%2'''; uk = 'У колонці %1 незаполнен код товару ''%2'''"),
						НомерКолонки, Марка));
						ТоварНеНайден = Истина;
						Если НомерКолонки = КонецОбластиКодовТовара Тогда
							Если СоответствиеТовары.Количество() = 0 Тогда
								ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнены артикула марок...'; uk = 'Не заповнені артикула марок ...'"));
								ЕстьОшибки = Истина;
							КонецЕсли;
							Прервать;				
						Иначе
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					Товар = Неопределено;
					Префикс = ВРег(Лев(Артикул,1));
					Если Префикс = "А" Тогда 
						Товар = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", Прав(Артикул,СтрДлина(Артикул)-1));
					ИначеЕсли Префикс = "Г" Тогда
						Товар = Справочники.ЦелевыеМарки.НайтиПоКоду(Прав(Артикул,СтрДлина(Артикул)-1));
					ИначеЕсли Префикс = "М" Тогда
						Товар = Справочники.ТоварныеМарки.НайтиПоКоду(Прав(Артикул,СтрДлина(Артикул)-1));
					Иначе
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В колонке %1 Некорректный префикс артикула ''%2'''; uk = 'У колонці %1 Некоректний префікс артикулу ''%2'''"),
						НомерКолонки, Артикул));
						ТоварНеНайден = Истина;
						Продолжить;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(Товар) Тогда
						ТоварНеНайден = Истина;
						
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В колонке %1 не найден товар с артикулом ''%2'''; uk = 'У колонці %1 не знайдений товар з артикулом ''%2'''"),
						НомерКолонки, Артикул));
					Иначе 
						СоответствиеТовары.Вставить(СокрЛП(ТабДок.Область(СтрокаТоваров, НомерКолонки, СтрокаТоваров, НомерКолонки).Текст), Товар);
						ПолеСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"Марка_%1_%2",
						Префикс,
						СокрЛП(Товар.Код));
						стрТМарок = ТаблицаМарок.Добавить();
						стрТМарок.ЦелеваяМарка = Товар;
						стрТМарок.ПолеСвязи = ПолеСвязи;
					КонецЕсли;
					Если НомерКолонки = КонецОбластиКодовТовара Тогда
						Если СоответствиеТовары.Количество() = 0 Тогда
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнены артикула марок...'; uk = 'Не заповнені артикула марок ...'"));
							ЕстьОшибки = Истина;
						КонецЕсли;
						Прервать;				
					КонецЕсли;
				Иначе
					
					Если СоответствиеТовары.Получить(Марка) = Неопределено Тогда
						ТоварНеНайден = Истина;
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В колонке %1 неадекватная марка сигарет ''%2'''; uk = 'У колонці %1 неадекватна марка сигарет ''%2'''"),
						НомерКолонки, Марка));
					КонецЕсли;
				КонецЕсли;
				Если СтрНайти(ТабДок.Область(СтрокаПериодов, НомерКолонки+1, СтрокаПериодов, НомерКолонки+1).Текст,"План") > 0 Тогда
					Прервать;				
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если ТоварНеНайден Или СоответствиеТовары.Количество() = 0 Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	//	//	Найдем колонку складов	//	//
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		Если СокрЛП(ТабДок.Область(СтрокаТоваров, НомерКолонки, СтрокаТоваров, НомерКолонки).Текст) = "№ ТТ" Тогда
			КолонкаСкладов = НомерКолонки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(КолонкаСкладов) Тогда
		//	//	Проверим склады	//	//
		Для НомерСтроки = СтрокаСкладов По КоличествоСтрок Цикл
			КодСклада = СокрЛП(ТабДок.Область(НомерСтроки, КолонкаСкладов, НомерСтроки, КолонкаСкладов).Текст);
			
			Склад = СоответствиеСклады.Получить(КодСклада);
			
			Если Склад <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1 дублируется склад (%2) %3'; uk = 'У рядку %1 дублюється склад (%2) %3'"),
				НомерСтроки, КодСклада, Склад));
				ЕстьОшибкаСклада = Истина;
			Иначе
				Если ПустаяСтрока(КодСклада) Тогда
					Склад = Справочники.СкладыКомпании.ПустаяСсылка();
					
					ИмяСклада = СокрЛП(ТабДок.Область(НомерСтроки, КолонкаСкладов+1, НомерСтроки, КолонкаСкладов+1).Текст); 
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В строке %1 не заполнен код склада ''%2'''; uk = 'У рядку %1 не заповнений код складу ''%2'''"),
					НомерСтроки, ИмяСклада));
					ЕстьОшибкаСклада = Истина;
				Иначе
					Склад = Справочники.СкладыКомпании.НайтиПоКоду(КодСклада);
					Если НЕ ЗначениеЗаполнено(Склад) Тогда
						ИмяСклада = СокрЛП(ТабДок.Область(НомерСтроки, КолонкаСкладов+1, НомерСтроки, КолонкаСкладов+1).Текст); 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В строке %1 не найден склад по коду ''%2'' - %3'; uk = 'У рядку %1 не знайдено склад за кодом ''%2'' - %3'"),
						НомерСтроки, КодСклада, ИмяСклада));
						ЕстьОшибкаСклада = Истина;
					КонецЕсли;
				КонецЕсли;
				СоответствиеСклады.Вставить(КодСклада,Склад);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не найдена колонка с заголовком ""№ ТТ""...'; uk = 'Не знайдена колонка із заголовком ""№ ТТ"" ...'"));
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибкаСклада Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	//	//	Погнали 	//	//
	Для НомерСтроки = СтрокаСкладов По КоличествоСтрок Цикл
		Для Каждого стрПериод Из ТаблицаПериодов Цикл
			Порция = стрПериод.Колонка + СоответствиеТовары.Количество() - 1;
			Для НомерКолонки = стрПериод.Колонка По Порция Цикл
				
				Количество = ТабДок.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).Текст;
				Если ПустаяСтрока(Количество) Или Количество = "0" Тогда
					Продолжить;
				КонецЕсли;
				
				КодСклада = СокрЛП(ТабДок.Область(НомерСтроки, КолонкаСкладов, НомерСтроки, КолонкаСкладов).Текст);
				Марка = СоответствиеТовары.Получить(СокрЛП(ТабДок.Область(СтрокаТоваров, НомерКолонки, СтрокаТоваров, НомерКолонки).Текст));
				
				ПолеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1_%2_%3",
				стрПериод.ВидПериода,
				Формат(стрПериод.НачалоДействия,"ДФ=dd_MM_yyyy"),
				Формат(стрПериод.ОкончаниеДействия,"ДФ=dd_MM_yyyy"));
				
				ПолеСвязи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1_Марка_%2_%3",
				ПолеПериода,
				СоответствиеТипов.Получить(ТипЗнч(Марка)),
				СокрЛП(Марка.Код));
				
				НоваяСтрока = ТаблицаПланов.Добавить();
				НоваяСтрока.СкладКомпании = СоответствиеСклады.Получить(КодСклада);
				НоваяСтрока.ЦелеваяМарка = Марка;
				НоваяСтрока.Количество = Количество;
				НоваяСтрока.ВидПериода = стрПериод.ВидПериода;
				НоваяСтрока.НачалоДействия = стрПериод.НачалоДействия;
				НоваяСтрока.ОкончаниеДействия = стрПериод.ОкончаниеДействия;
				НоваяСтрока.ПолеСвязи = ПолеСвязи;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Объект.Планы.Загрузить(ТаблицаПланов);
	Объект.Товары.Загрузить(ТаблицаМарок);
	Объект.Периоды.Загрузить(ТаблицаПериодов);
	Модифицированность = Истина;
	МодифицированностьНапоказ = Ложь;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Контрольная сумма - %1'; uk = 'Контрольна сума - %1'"),
	ТаблицаПланов.Итог("Количество")));
	
	Объект.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"Загружен %1 из файла: %2",
	ТекущаяДата(),
	ФайлИсточник);
	
	ЗаполнитьНапоказИзПланы();
	
КонецПроцедуры
//	Конец Загрузка планов из EXCEL

&НаСервере
Процедура ЗаполнитьСоответствиеПолейСвязи() 
	//Объект.Напоказ.Очистить();
	//Объект.Планы.Очистить();
	//Объект.Товары.Очистить();
КонецПроцедуры

&НаСервере
Процедура УстановитьСоставКолонокНапоказ() 
	
	КэшСвязейТовара = Новый Соответствие;
	КэшСвязейПериодов = Новый Соответствие;
	
	ДобавляемыеРеквизиты 	= Новый Массив;
	УдаляемыеРеквизиты		= Новый Массив;
	УдаляемыеПоляФормы 		= Новый Массив;
	
	КолонкиМарки 			= Элементы.НапоказГруппаПланы.ПодчиненныеЭлементы;
	Для Каждого элем Из КолонкиМарки Цикл
		УдаляемыеПоляФормы.Добавить(элем);
		Для Каждого элем Из элем.ПодчиненныеЭлементы Цикл
			УдаляемыеРеквизиты.Добавить(элем.ПутьКДанным);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого элем Из УдаляемыеПоляФормы Цикл
		Элементы.удалить(элем);
	КонецЦикла;
	
	ТипыРеквизита = Новый Массив;
	ТипыРеквизита.Добавить(Тип("Число"));
	ОписаниеТиповРеквизита = Новый ОписаниеТипов(ТипыРеквизита,,,Новый КвалификаторыЧисла(15, 0));						
	
	Для Каждого стрПериод Из Объект.Периоды Цикл
		Для Каждого стрТовары Из Объект.Товары Цикл
			ИмяНовогоРеквизита = стрПериод.ПолеСвязи+"_"+стрТовары.ПолеСвязи;		
			НовыйРеквизит = Новый РеквизитФормы(ИмяНовогоРеквизита, 
			ОписаниеТиповРеквизита,
			"Объект.Напоказ",
			ИмяНовогоРеквизита,
			Истина);
			
			КэшСвязейТовара.Вставить(ИмяНовогоРеквизита,стрТовары);
			КэшСвязейПериодов.Вставить(ИмяНовогоРеквизита,стрПериод);
			
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);	
		КонецЦикла;							
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 ИЛИ УдаляемыеРеквизиты.Количество() > 0 Тогда 		
		ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	КонецЕсли;
	
	Для Каждого стрПериод Из Объект.Периоды Цикл
		ИмяРеквизита = стрПериод.ПолеСвязи;
		ЗаголовокРеквизита = Строка(стрПериод.ВидПериода)+" "+стрПериод.Имя;
		
		НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ГруппаФормы"), Элементы.НапоказГруппаПланы);
		НовыйЭлемент.Заголовок = ЗаголовокРеквизита;
		НовыйЭлемент.Подсказка = НСтр("ru = '"+ ЗаголовокРеквизита +"'; uk = '"+ ЗаголовокРеквизита +"'");
		НовыйЭлемент.ОтображатьВШапке = Истина;
		НовыйЭлемент.Группировка = ГруппировкаКолонок.Горизонтальная;
		НовыйЭлемент.ГоризонтальноеПоложениеВШапке = ГоризонтальноеПоложениеЭлемента.Центр;
		
	КонецЦикла;
	
	Для Каждого НовыйРеквизит Из ДобавляемыеРеквизиты Цикл
		ИмяРеквизита = НовыйРеквизит.Имя;
		Марка = КэшСвязейТовара.Получить(ИмяРеквизита).ЦелеваяМарка;
		
		НовыйЭлемент = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Элементы[КэшСвязейПериодов.Получить(ИмяРеквизита).ПолеСвязи]);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;			
		НовыйЭлемент.ПутьКДанным = "Объект.Напоказ." + ИмяРеквизита;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "тзПланыКоличествоПриИзменении");
		НовыйЭлемент.Заголовок = СокрЛП(Марка);
		НовыйЭлемент.Подсказка = НСтр("ru = '"+ ЗаголовокРеквизита +"'; uk = '"+ ЗаголовокРеквизита +"'");
		НовыйЭлемент.Ширина = 5;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.МаксимальнаяШирина = 5;
		НовыйЭлемент.ТолькоПросмотр = Истина;
		НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
		НовыйЭлемент.АвтоВысотаЯчейки = Истина;
		НовыйЭлемент.ВысотаЗаголовка = 2;
		
		Если ТипЗнч(Марка) = Тип("СправочникСсылка.Номенклатура") Тогда
			НовыйЭлемент.Шрифт = Новый Шрифт ();
			НовыйЭлемент.ШрифтЗаголовка = Новый Шрифт ();
		Иначе
			НовыйЭлемент.Шрифт = Новый Шрифт (,,Истина);
			НовыйЭлемент.ШрифтЗаголовка = Новый Шрифт (,,Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНапоказИзПланы()	
	
	Объект.Напоказ.Очистить();
	
	УстановитьСоставКолонокНапоказ();
	
	ТаблицаПланов	= Объект.Планы.Выгрузить();
	ТаблицаПериодов = Объект.Периоды.Выгрузить();
	ТаблицаТоваров	= Объект.Товары.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗПланов",	ТаблицаПланов);
	Запрос.УстановитьПараметр("ТЗПериодов",	ТаблицаПериодов);
	Запрос.УстановитьПараметр("ТЗТоваров",	ТаблицаТоваров);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаПланов.СкладКомпании КАК СкладКомпании,
	|	ТаблицаПланов.ЦелеваяМарка КАК Марка,
	|	ТаблицаПланов.ВидПериода КАК ВидПериода,
	|	ТаблицаПланов.НачалоДействия КАК НачалоДействия,
	|	ТаблицаПланов.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ТаблицаПланов.Количество КАК Количество,
	|	ТаблицаПланов.ПолеСвязи КАК ПолеСвязи
	|ПОМЕСТИТЬ втПланов
	|ИЗ
	|	&ТЗПланов КАК ТаблицаПланов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПериодов.Имя КАК Имя,
	|	ТаблицаПериодов.ВидПериода КАК ВидПериода,
	|	ТаблицаПериодов.НачалоДействия КАК НачалоДействия,
	|	ТаблицаПериодов.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ТаблицаПериодов.ПолеСвязи КАК ПолеСвязи
	|ПОМЕСТИТЬ втПериодов
	|ИЗ
	|	&ТЗПериодов КАК ТаблицаПериодов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.ЦелеваяМарка КАК ЦелеваяМарка,
	|	ТаблицаТоваров.ПолеСвязи КАК ПолеСвязи
	|ПОМЕСТИТЬ втТоваров
	|ИЗ
	|	&ТЗТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПланов.СкладКомпании КАК СкладКомпании,
	|	ТаблицаПланов.Количество КАК Количество,
	|	ТаблицаПланов.ПолеСвязи КАК ПолеСвязи,
	|	ЕСТЬNULL(ДополнительныеСведения.Значение, """") КАК Куратор
	|ИЗ
	|	втПланов КАК ТаблицаПланов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО ТаблицаПланов.СкладКомпании = ДополнительныеСведения.Объект
	|			И (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкладКомпании
	|ИТОГИ
	|	МАКСИМУМ(Куратор)
	|ПО
	|	СкладКомпании
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ВыборкаСкладов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//	Запрос.Выполнить().выгрузить()
	Пока ВыборкаСкладов.Следующий() Цикл
		
		НоваяСтрока = Объект.Напоказ.Добавить();
		
		НоваяСтрока.СкладКомпании	= ВыборкаСкладов.СкладКомпании;
		НоваяСтрока.Куратор			= ВыборкаСкладов.Куратор;
		
		Выборка = ВыборкаСкладов.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока[Выборка.ПолеСвязи]	= Выборка.Количество;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПланыИзНапоказ(ТекОбъект) 
	
	ТаблицаСвязей = Новый ТаблицаЗначений;
	ТаблицаСвязей.Колонки.Добавить("ПолеСвязи");
	ТаблицаСвязей.Колонки.Добавить("Периоды");
	ТаблицаСвязей.Колонки.Добавить("ЦелеваяМарка");
	
	ТекОбъект.Планы.Очистить();
	
	Для Каждого стрПериод Из Объект.Периоды Цикл
		Для Каждого стрТовары Из Объект.Товары Цикл
			НоваяСтрока = ТаблицаСвязей.Добавить();
			НоваяСтрока.ПолеСвязи	= стрПериод.ПолеСвязи+"_"+стрТовары.ПолеСвязи;
			НоваяСтрока.Периоды		= стрПериод;
			НоваяСтрока.ЦелеваяМарка= стрТовары.ЦелеваяМарка;
		КонецЦикла;							
	КонецЦикла;
	
	Для Каждого строка Из Объект.Напоказ Цикл 
		Для Каждого стрСвязь Из ТаблицаСвязей Цикл 
			Количество = строка[стрСвязь.ПолеСвязи];
			Если Количество <> 0 Тогда 
				НоваяСтрока = ТекОбъект.Планы.Добавить();
				
				НоваяСтрока.СкладКомпании	= строка.СкладКомпании;
				НоваяСтрока.Количество		= Количество;
				НоваяСтрока.ПолеСвязи 		= стрСвязь.ПолеСвязи;
				НоваяСтрока.ЦелеваяМарка	= стрСвязь.ЦелеваяМарка;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, стрСвязь.Периоды,,"ПолеСвязи");
				
			КонецЕсли;			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьДублиСкладов(Знач ТЧ)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗПланов", ТЧ.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаПланов.СкладКомпании КАК СкладКомпании,
	|	ТаблицаПланов.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втПланов
	|ИЗ
	|	&ТЗПланов КАК ТаблицаПланов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПланов.СкладКомпании КАК СкладКомпании,
	|	КОЛИЧЕСТВО(ТаблицаПланов.СкладКомпании) КАК Колво
	|ПОМЕСТИТЬ Сгруппированные
	|ИЗ
	|	втПланов КАК ТаблицаПланов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПланов.СкладКомпании
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПланов.НомерСтроки КАК НомерСтроки,
	|	втПланов.СкладКомпании КАК СкладКомпании,
	|	Сгруппированные.Колво КАК Колво
	|ИЗ
	|	втПланов КАК втПланов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Сгруппированные КАК Сгруппированные
	|		ПО втПланов.СкладКомпании = Сгруппированные.СкладКомпании
	|ГДЕ
	|	(Сгруппированные.Колво > 1
	|			ИЛИ втПланов.СкладКомпании = ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка))
	|ИТОГИ
	|	МАКСИМУМ(Колво)
	|ПО
	|	СкладКомпании";
	
	Результат =  Запрос.Выполнить();
	
	Отказ = Не Результат.Пустой();	
	
	ВыборкаСкладов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСкладов.Следующий() Цикл
		
		НомераСтрок = "";
		Выборка = ВыборкаСкладов.Выбрать();
		Пока Выборка.Следующий() Цикл
			НомераСтрок = НомераСтрок + Выборка.НомерСтроки + ",";
		КонецЦикла;
		КакойСклад = ?(ЗначениеЗаполнено(ВыборкаСкладов.СкладКомпании),НСтр("ru = 'задублирован'; uk = 'задублірован'"),НСтр("ru = 'пустой'; uk = 'порожній'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'В строках %1 %2 скдад - %3'; uk = 'У рядках %1 %2 скдад - %3'"),
		Лев(НомераСтрок,СтрДлина(НомераСтрок)-1), КакойСклад, ВыборкаСкладов.СкладКомпании));
	КонецЦикла;
	
	//	Запрос.Выполнить().Выгрузить()
	
	
	Возврат Не Результат.Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокСкладовТЧ(Знач ТЧ)
	СпСкладов = Новый СписокЗначений;
	вт = ТЧ.Выгрузить(,"СкладКомпании");
	вт.Свернуть("СкладКомпании");
	СпСкладов.ЗагрузитьЗначения(вт.ВыгрузитьКолонку("СкладКомпании"));
	Возврат СпСкладов;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьАктивность(Загрузка = Ложь)
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности") Тогда
		Если НЕ Загрузка Тогда
			Объект.Активность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 %2. Волна № ",
			Формат(Год(Объект.Дата),"ЧГ=0"),
			СокрЛП(Объект.ТоварнаяМарка));
		КонецЕсли;
	Иначе
		Объект.Активность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"План на %1 %2",
		ПредставлениеПериода(Объект.НачалоДействия,КонецДня(Объект.ОкончаниеДействия),"ФП = Истина"),
		ПолучитьПодразделения());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждениеОшибки()
	ПоказатьПредупреждение(,НСтр("ru = 'Исправьте ошибки и повторите снова...'; uk = 'Виправте помилки і повторіть знову ...'"),,НСтр("ru = 'Внимание, ошибки!!!'; uk = 'Увага, помилки!!!'"));
КонецПроцедуры

&НаКлиенте
Процедура ПериодДействияПриИзменении();
	Если ЗначениеЗаполнено(Объект.НачалоДействия) И ЗначениеЗаполнено(Объект.ОкончаниеДействия) Тогда
		ЗаполнитьАктивность();
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ПолучитьПодразделения()
	
	ТаблицаНапоказ	= Объект.Напоказ.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗПланов",	ТаблицаНапоказ);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТаблицаПланов.СкладКомпании КАК Справочник.СкладыКомпании) КАК СкладКомпании
	|ПОМЕСТИТЬ втПланов
	|ИЗ
	|	&ТЗПланов КАК ТаблицаПланов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаПланов.СкладКомпании.Подразделение КАК Подразделение
	|ИЗ
	|	втПланов КАК ТаблицаПланов
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Список = Новый СписокЗначений;
	
	Список.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение"));
	Возврат Список;
КонецФункции



#КонецОбласти


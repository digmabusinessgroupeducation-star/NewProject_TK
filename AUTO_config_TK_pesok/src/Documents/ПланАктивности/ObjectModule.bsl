#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецПроцедуры
	
	Процедура ПриКопировании(ОбъектКопирования)
		Дата = ТекущаяДата();
		
		НачалоДействия = "";
		ОкончаниеДействия = "";
		Комментарий = "";
		Периоды.Очистить();
		Товары.Очистить();
		Планы.Очистить();
		ИнициализироватьДокумент();
	КонецПроцедуры
	
	#Область Прочее
	
	
	#КонецОбласти
	
	
	
	#Область Проведение
	
	Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
		Если ОбменДанными.Загрузка Тогда
			Возврат;
		КонецЕсли;
		
		
		ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
		ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
		ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение
			И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Док.Дата КАК Дата
			|ИЗ
			|	Документ.ПланАктивности КАК Док
			|ГДЕ
			|	Док.Ссылка = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			Результат = Запрос.Выполнить();
			
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
			
		Иначе
			ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);		
		КонецЕсли;
		
		
		
	КонецПроцедуры
	
	Процедура ПриЗаписи(Отказ)
		Если ОбменДанными.Загрузка Тогда
			Возврат;
		КонецЕсли;
		
		Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
			ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
		КонецЕсли;
		
		
	КонецПроцедуры
	
	Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
		ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
		
		ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
		
		Документы.ПланАктивности.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		
		ПроведениеСервер.ОтразитьПланАктивности(ДополнительныеСвойства, Движения, Отказ);
		
		ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
		ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
		
		ОценкаПроизводительности.ЗакончитьЗамерВремени("ПланАктивностиПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));
		
	КонецПроцедуры
	
	Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		Если НЕ ОбменДанными.Загрузка И ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности")  Тогда
			ПроверяемыеРеквизиты.Добавить("Расчет");	
		КонецЕсли;
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ИнициализацияИЗаполнение
	
	// Инициализирует документ
	//
	Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
		
		Автор =  Пользователи.ТекущийПользователь();
		Если ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности") Тогда
			Активность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 %2. Волна № ",
			Формат(Год(ТекущаяДата()),"ЧГ=0"),
			СокрЛП(ТоварнаяМарка));
		Иначе
			Активность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"План на %1",
			ПредставлениеПериода(НачалоДействия,КонецДня(ОкончаниеДействия),"ФП = Истина"));
		КонецЕсли;
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
			
		КонецЕсли;
		
		
	КонецПроцедуры // ИнициализироватьДокумент()
	
	
	
	#КонецОбласти
	
	
#КонецЕсли


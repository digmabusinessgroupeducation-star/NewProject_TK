&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЗаполнитьДопПараметры();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;	
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Контрагент = Объект.Контрагент;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ПоступлениеТоваров"));
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, "ТоварыЗаполнитьИзВходящего, ТоварыЗаполнитьЦеныИзВхДокумента,ТоварыРаспределитьДопРасходы", "Видимость", НЕ Объект.ИспользоватьЦеныИзПротоколов);

	
	УстановитьСвязиПараметровВыбораДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриЧтенииСозданииНаСервере();
	
	УстановитьСостояниеДокумента();
	
	Если Объект.ДополнительныеРасходы Тогда 
		//ПроверитьРаспределениеДопРасходов();
	КонецЕсли;
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.ДополнительныеРасходы Тогда
		ДопРасходыПересчитаны = Объект.ДопРасходы_Сумма = Объект.Товары.Итог("СуммаСпецСкидки")*-1;
		ВопросРаспределениеДопРасходов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ТК_УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	ПодразделениеКомпанииПриИзмененииНаСервере();
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаСервере
Процедура ПодразделениеКомпанииПриИзмененииНаСервере()
	//Запрос = Новый Запрос("ВЫБРАТЬ
	//|	ПодразделенияКомпании.ИспользоватьПротоколыСогласованныхЦен КАК ИспользоватьПротоколыСогласованныхЦен
	//|ИЗ
	//|	Справочник.ПодразделенияКомпании КАК ПодразделенияКомпании
	//|ГДЕ
	//|	ПодразделенияКомпании.Ссылка = &Ссылка
	//|	И ПодразделенияКомпании.ИспользоватьПротоколыСогласованныхЦен = ИСТИНА");
	//Запрос.УстановитьПараметр("Ссылка", Объект.ПодразделениеКомпании);
	//Выборка = Запрос.Выполнить().Выбрать();
	//
	//Если Выборка.Следующий() Тогда
	//	Объект.ИспользоватьЦеныИзПротоколов = Истина;
	//Иначе 
	//	Объект.ИспользоватьЦеныИзПротоколов = Ложь;
	//КонецЕсли;
	
	Объект.ИспользоватьЦеныИзПротоколов =  Объект.ПодразделениеКомпании.ИспользоватьПротоколыСогласованныхЦен И НЕ Объект.Контрагент.НеИспользоватьПротоколыЦен;
	
	ПерезаполнитьЦеныНаСервере();
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, ЭтаФорма.Объект);
	
КонецПроцедуры


&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация,Объект.ПодразделениеКомпании);
		Объект.ДоговорВзаиморасчетов = СтруктураДанные.Договор;
		Объект.ИспользоватьЦеныИзПротоколов =  СтруктураДанные.ИспользоватьЦеныИзПротоколов;
		ДоговорПередИзменением = Договор;
		Договор = Объект.ДоговорВзаиморасчетов;
		
		КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные);
	Иначе
		Объект.ДоговорВзаиморасчетов = Договор;
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорВзаиморасчетовПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Объект.ДоговорВзаиморасчетов = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ПодразделениеКомпании, Объект.ХозОперация, Объект.Дата);
	ОбработатьИзменениеДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	
	УстановитьСвязиПараметровВыбораДоговора();	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПриИзменении(Элемент)
	
	ЗаполнитьДопПараметрРЦТабак(Объект.ТорговаяПлощадка, ДопПараметры);
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура РегламентированныйУчетПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Контрагент,Организация,ТорговаяПлощадка,ТипСтавкиНДС",Контрагент,Объект.Организация,Объект.ТорговаяПлощадка,"Закупка"));
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина, Истина);
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,,Истина);
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,,Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБезНалоговПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуСНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,, Истина);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("ПересчитыватьПроцентСкидки", Истина));	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуВсегоНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)	
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,, Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаБезНалоговПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуИзСуммыБезНДС");	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,, Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);		
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПоставщикаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПоставщикаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,, Истина);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбработатьВыборНаСервере(ВыбранноеЗначение);         
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища) Экспорт
	
	
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина, Истина);
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	
	Для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Товары.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		
		
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПФПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги 

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовДополнительныеРасходы

&НаКлиенте
Процедура ДополнительныеРасходыПриИзменении(Элемент)
	
	УправлениеЭлементамиФормы();
	
	Если Не Объект.ДополнительныеРасходы Тогда 
		Объект.ДопРасходы_Контрагент = Неопределено;
		Объект.ДопРасходы_Договор = Неопределено;
		Объект.ДопРасходы_Содержание = "";
		Объект.ДопРасходы_СпособРаспределения = Неопределено;
		Объект.ДопРасходы_Сумма = 0;
		Объект.ДопРасходы_СуммаНДС = 0;
		
		Для Каждого Стр Из Объект.Товары Цикл 
			Стр.СуммаСпецСкидки = 0;
		КонецЦикла;
		
		ДопРасходыПересчитаны = Истина;	
	Иначе
		ДопРасходыПересчитаны = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРасходы_СпособРаспределенияПриИзменении(Элемент)
	
	ДопРасходыПересчитаны = Ложь;
	
	Если Объект.ДопРасходы_Сумма <> 0 Тогда 
		ВопросРаспределениеДопРасходов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРасходы_СуммаПриИзменении(Элемент)
	
	ДопРасходыПересчитаны = Ложь;
	
	ТекущаяСтрока = Новый Структура;
	ТекущаяСтрока.Вставить("СтавкаНДС", Объект.ДопРасходы_СтавкаНДС);
	ТекущаяСтрока.Вставить("СуммаНДС", Объект.ДопРасходы_СуммаНДС);
	ТекущаяСтрока.Вставить("Сумма", Объект.ДопРасходы_Сумма);
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "Сумма");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);	
	
	Объект.ДопРасходы_СуммаНДС = ТекущаяСтрока.СуммаНДС;
	
	Если Объект.ДопРасходы_Сумма <> 0 Тогда 
		ВопросРаспределениеДопРасходов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРасходы_СтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Новый Структура;
	ТекущаяСтрока.Вставить("СтавкаНДС", Объект.ДопРасходы_СтавкаНДС);
	ТекущаяСтрока.Вставить("СуммаНДС", Объект.ДопРасходы_СуммаНДС);
	ТекущаяСтрока.Вставить("Сумма", Объект.ДопРасходы_Сумма);
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "Сумма");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
	
	Объект.ДопРасходы_СуммаНДС = ТекущаяСтрока.СуммаНДС;
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПодборТовара(Команда)
	
	ПараметрыФормы = Новый Структура("СкладКомпании,НайтиПоТочномуСоответствию",Объект.СкладКомпании,Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора",ПараметрыФормы,Элементы.Товары,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьЦеныТоваров(Команда)
	ПерезаполнитьЦеныНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьЦеныНаСервере()
	
	СтруктураДействий = Новый Структура;
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,,Истина);
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения());
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦеныИзВхДокумента(Команда)	
	Если Не ЗначениеЗаполнено(Объект.ВхДокНомер) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнен реквизит ""Номер входящего документа""'; uk = 'Не заповнений реквізит ""Номер вхідного документу""'"), 
		Объект.Ссылка, 
		"ВхДокНомер");
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 Тогда 
		ЗаполнитьЦеныИзВхДокументаНаСервере();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьИзВходящего(Команда)
	Если Не ЗначениеЗаполнено(Объект.ВхДокНомер) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не заполнен реквизит ""Номер входящего документа""'; uk = 'Не заповнений реквізит ""Номер вхідного документу""'"), 
		Объект.Ссылка, 
		"ВхДокНомер");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИзВхДокументаНаСервере();
	
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьЦеныИзВхДокументаНаСервере()
	
	Документы.ПоступлениеТоваров.ЗаполнитьЦеныИзВходящегоДокумента(Объект);
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИзВхДокументаНаСервере()
	
	Документы.ПоступлениеТоваров.ЗаполнитьИзВходящегоДокумента(Объект);
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЦеныДляРЦНаСервере(ССылка)
	Массив = Новый Массив();
	Массив.Добавить(ССылка);
	ЦенообразованиеСервер.УстановкаЦенДляРЦ(Массив);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦеныДляРЦ(Команда)
	УстановитьЦеныДляРЦНаСервере(Объект.Ссылка);
КонецПроцедуры

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ПоступлениеТоваров.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка товаров из файла'; uk = 'Завантаження списка товарів із файлу'");		
	//ПараметрыЗагрузки.ДополнительныеПараметры = Новый структура;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных);
	
	РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина, Истина);	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));		
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл         
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовары = Объект.Товары.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.Количество = СтрокаТаблицы.Количество;
		
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", НоваяСтрокаТовары.ЕдиницаИзмерения);				
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, КэшированныеЗначения); 
		
		ТоварыДобавлены = Истина;
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


&НаКлиенте
Процедура РаспределитьДопРасходы(Команда)
	
	Если ПроверитьЗаполнение() Тогда 
		РаспределитьДопРасходыНаСервере();	
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура УправлениеЭлементамиФормы()	

	//Видимость элементов
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ТоварыГруппаКоличествоПоставщика");
	МассивВсехРеквизитов.Добавить("ТоварыРаспределитьДопРасходы");
	МассивВсехРеквизитов.Добавить("ГруппаДополнительныеРасходы");
	МассивВсехРеквизитов.Добавить("Транзитер");
	МассивВсехРеквизитов.Добавить("ФормаУстановитьЦеныДляРЦ");
	МассивВсехРеквизитов.Добавить("ТоварыСуммаНДС");
	МассивВсехРеквизитов.Добавить("СуммаНДС");
	
	МассивВидимыхРеквизитов = Новый Массив;
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваровТранзиты") Тогда 
		МассивВидимыхРеквизитов.Добавить("Транзитер");
	КонецЕсли;
	
	Если Объект.ДополнительныеРасходы Тогда 
		МассивВидимыхРеквизитов.Добавить("ТоварыРаспределитьДопРасходы");
		МассивВидимыхРеквизитов.Добавить("ГруппаДополнительныеРасходы");
	КонецЕсли;
	
	Если Объект.РегламентированныйУчет Тогда 
		МассивВидимыхРеквизитов.Добавить("ТоварыСуммаНДС");
		МассивВидимыхРеквизитов.Добавить("СуммаНДС");
	КонецЕсли;
	
	Если Объект.ИспользоватьЦеныИзПротоколов Тогда 
		МассивВидимыхРеквизитов.Добавить("ТоварыГруппаКоличествоПоставщика");
	КонецЕсли;
	
	Если ДопПараметры.ЭтоТорговаяПлоащдкаРЦТабак И Объект.Контрагент = ДопПараметры.КонтрагентЛТФ Тогда 
		МассивВидимыхРеквизитов.Добавить("ФормаУстановитьЦеныДляРЦ");
	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивВидимыхРеквизитов);			
	
	//Доступность элементов
	МассивЭлементовТолькоПросмотр = Новый Массив;
	
	Если Не ДопПараметры.РазрешитьРедактироватьЦены Тогда
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыЦена");
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыЦенаБезНалогов");
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыРасхождениеЦена");
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыРасхождениеКоличество");
		
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыСуммаБезНалогов");
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыСумма");
	КонецЕсли;

	Если Не ДопПараметры.РазрешитьРедактироватьСтавкуНДС  Тогда
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыСтавкаНДС");
		МассивЭлементовТолькоПросмотр.Добавить("ТоварыСуммаНДС");
	КонецЕсли;

	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементовТолькоПросмотр, "ТолькоПросмотр", Объект.ИспользоватьЦеныИзПротоколов);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));
	СтруктураДанные.Вставить("ТипЦен", ДоговорПоУмолчанию.ТипЦен);
	СтруктураДанные.Вставить("ИспользоватьЦеныИзПротоколов",Подразделение.ИспользоватьПротоколыСогласованныхЦен И НЕ Контрагент.НеИспользоватьПротоколыЦен);	
	
	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
	"ВалютаДокумента",
	Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
	"ВалютаРасчетовКурсКратность",
	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	СтруктураДанные.Вставить("ТипЦен", Договор.ТипЦен);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.ДоговорВзаиморасчетов;
	
	Если ДоговорПередИзменением <> Объект.ДоговорВзаиморасчетов Тогда
		
		ОчиститьОснованиеПриИзменениеКонтрагентаДоговора();
		
		ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением);
		
	Иначе
		
		Объект.ДоговорВзаиморасчетов = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()
	
	Объект.РаспределениеПоставки.Очистить();
	
КонецПроцедуры // ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()

&НаКлиенте
Процедура ОбновитьПодвалФормы()
	
	СуммаВсегоСНДС = Объект.Товары.Итог("СуммаВсего");
	СуммаНДС       = Объект.Товары.Итог("СуммаНДС");
	
КонецПроцедуры // ОбновитьПодвалФормы()

&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
КонецПроцедуры       

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект)
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект, "СогласованиеЦен")));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, РасхождениеКоличество = Ложь, РасхождениеЦена = Ложь)
	Если НЕ РасхождениеКоличество И НЕ РасхождениеЦена Тогда
		Возврат;	
	КонецЕсли;
	
	МассивПолейРасхождений = Новый Массив;
	Если РасхождениеКоличество Тогда
		МассивПолейРасхождений.Добавить(Новый Структура("Уменьшаемое, Вычетаемое, Разница",	"Количество" ,	"КоличествоПоставщика" ,	"РасхождениеКоличество"));
	КонецЕсли;
	
	Если РасхождениеЦена Тогда
		МассивПолейРасхождений.Добавить(Новый Структура("Уменьшаемое, Вычетаемое, Разница",	"Цена" 		 ,	"ЦенаПоставщика" 	   ,	"РасхождениеЦена"));
	КонецЕсли;
	
	СтруктураДействий.Вставить("ПересчитатьРазность", МассивПолейРасхождений);	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДопПараметры()
	
	Если ТипЗнч(ДопПараметры) <> Тип("Структура") Тогда 
		ДопПараметры = Новый Структура;
	КонецЕсли;
	
	Пользователь = Пользователи.ТекущийПользователь();
	КонтрагентЛТФ = Справочники.Контрагенты.НайтиПоКоду("36929041");	//	Марвел ТД (бывш. Львовская табачная фабрика ТД) ООО
	ТпРцТабак = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду("ХGL000295");	//	04 РЦ ТАБАК	ХGL000295
	Если ТпРцТабак = Неопределено Тогда
		ТпРцТабак = Справочники.ТорговыеПлощадки.Пустаяссылка();
	КонецЕсли;

	
	
	ДопПараметры.Вставить("КонтрагентЛТФ", КонтрагентЛТФ);
	ДопПараметры.Вставить("ТпРцТабак", ТпРцТабак);	
	ДопПараметры.Вставить("Пользователь", Пользователь);
	ДопПараметры.Вставить("РазрешитьРедактироватьЦены", ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь ,"РазрешитьРедактироватьЦены",Ложь));
	ДопПараметры.Вставить("РазрешитьРедактироватьСтавкуНДС", ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь ,"РазрешитьРедактироватьСтавкуНДС",Ложь));
	
	ЗаполнитьДопПараметрРЦТабак(Объект.ТорговаяПлощадка, ДопПараметры);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДопПараметрРЦТабак(ТорговаяПлощадка, ДопПараметры)
	
	Результат = Ложь;
	
	Если ТипЗнч(ТорговаяПлощадка) = Тип("СправочникСсылка.ТорговыеПлощадки") и ЗначениеЗаполнено(ТорговаяПлощадка) Тогда		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТорговыеПлощадки.Ссылка В ИЕРАРХИИ (&ТпРцТабак) КАК Результат
		|ИЗ
		|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
		|ГДЕ
		|	ТорговыеПлощадки.Ссылка = &ТорговаяПлощадка";
		
		Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадка);
		Запрос.УстановитьПараметр("ТпРцТабак", ДопПараметры.ТпРцТабак);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Результат = ВыборкаДетальныеЗаписи.Результат;
		КонецЕсли;;
		
		УстановитьПривилегированныйРежим(Ложь);
		//Результат = ТорговаяПлощадка.ПринадлежитЭлементу(ДопПараметры.ТпРцТабак);
	КонецЕсли;
	
	ДопПараметры.Вставить("ЭтоТорговаяПлоащдкаРЦТабак", Результат);
	
КонецПроцедуры

#Область ДополнительныеРасходы

&НаСервере
Процедура РаспределитьДопРасходыНаСервере(Отказ = Ложь)
	
	Документы.ПоступлениеТоваров.РаспределитьДопРасходы(Объект, Отказ);
	
	Если Не Отказ Тогда 
		ДопРасходыПересчитаны = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросРаспределениеДопРасходов()
	
	Если Объект.ДополнительныеРасходы И Не ДопРасходыПересчитаны Тогда 	
	
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВопросРаспределениеДопРасходовПродолжение", ЭтаФорма), 
			НСтр("ru = 'Пересчитать суммы распределения дополнительных расходов?'; uk = 'Перерахувати суми розподілу додаткових видатків?'"), 
			РежимДиалогаВопрос.ДаНет, 
			10, 
			КодВозвратаДиалога.Да, 
			НСтр("ru = 'Дополнительные расходы'; uk = 'Додаткові видатки'"), 
			КодВозвратаДиалога.Да);
			
	КонецЕсли;			
			
КонецПроцедуры

&НаКлиенте
Процедура ВопросРаспределениеДопРасходовПродолжение(Ответ, ДопРеквизиты) Экспорт 

	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда 
		РаспределитьДопРасходыНаСервере();
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти



#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьЗначениеМРЦ",
												Новый Структура("ХарактеристикаНоменклатуры", "ЗначениеМРЦ"));
		
	
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказателиПоступления(Форма)
	
	// Расчет итога по табличным частям "Товары" и "Услуги"
	КоллекцияТовары 					= Форма.Объект.Товары;
	КоллекцияУслуги						= Форма.Объект.Услуги;
	
	Форма.СуммаВсегоСНДС 				= КоллекцияТовары.Итог("СуммаВсего") + КоллекцияУслуги.Итог("Сумма");
	Форма.СуммаНДС 						= КоллекцияТовары.Итог("СуммаНДС") + КоллекцияУслуги.Итог("СуммаНДС");
	
	Если Форма.Объект.ИспользоватьЦеныИзПротоколов Тогда		
		ЕстьРасхождения = Ложь;
		Для Каждого Стр Из КоллекцияТовары Цикл
			Если Стр.РасхождениеКоличество <> 0 ИЛИ Стр.РасхождениеЦена <> 0 Тогда
				ЕстьРасхождения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;	
		
		Форма.ЕстьРасхождения = ЕстьРасхождения;
	Иначе
		Форма.ЕстьРасхождения = Ложь;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти



&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", 
	ЭтотОбъект);
	
	ПоказатьВводЗначения(
	Оповещение,
	, // пропускаем начальное значение
	"Введите штрих код",
	"Число"
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Результат, Параметры) Экспорт	
	
	Если Не Результат = Неопределено Тогда
		
		нНоменклатура = НайтиНомеклатуруПоШтрихкоду(Результат);
		
		Если нНоменклатура = Неопределено Тогда
		Иначе
			нСтрока = Объект.Товары.Добавить();
			нСтрока.Номенклатура = нНоменклатура;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", нСтрока.ХарактеристикаНоменклатуры);
			СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", нСтрока.ЕдиницаИзмерения);
			СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
			
			ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
			ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
			ДобавитьДействиеПересчетаРасхождений(СтруктураДействий, Истина, Истина);
			
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(нСтрока, СтруктураДействий, КэшированныеЗначения);
			
			РассчитатьИтоговыеПоказателиПоступления(ЭтаФорма);	
			
			
		КонецЕсли;
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиНомеклатуруПоШтрихкоду(Штрихкод)
	Возврат РегистрыСведений.ШтрихкодыНоменклатуры.НайтиТоварПоШтрихКоду(Штрихкод);
КонецФункции

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ОткрытьФорму("Справочник.Номенклатура.ФормаСписка",ПараметрыФормы,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвязиПараметровВыбораДоговора()
	
	МассивСвязей = Новый Массив;
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент");	
	МассивСвязей.Добавить(НоваяСвязь);
	
	Если Объект.ХозОперация <> ПредопределенноеЗначение("Справочник.ХозОперации.ПоступлениеТоваровТранзиты") Тогда 
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация");		
		МассивСвязей.Добавить(НоваяСвязь);
	КонецЕсли;
	
	Элементы.ДоговорВзаиморасчетов.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязей);
	
КонецПроцедуры


//&НаСервере
//Процедура ВидимостьКоманды_УстановкаЦенЛТФ();
//	
//	КонтрагентЛТФ = Справочники.Контрагенты.НайтиПоКоду("36929041");	//	Марвел ТД (бывш. Львовская табачная фабрика ТД) ООО
//	ТпРцТабак = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоКоду("ХGL000295");	//	04 РЦ ТАБАК	ХGL000295
//	Если ТпРцТабак = Неопределено Тогда
//		ТпРцТабак = Справочники.ТорговыеПлощадки.Пустаяссылка();
//	КонецЕсли;
//	
//	Элементы.ФормаУстановитьЦеныДляРЦ.Видимость = Объект.Контрагент = КонтрагентЛТФ И Объект.ТорговаяПлощадка.ПринадлежитЭлементу(ТпРцТабак);
//	
//КонецПроцедуры


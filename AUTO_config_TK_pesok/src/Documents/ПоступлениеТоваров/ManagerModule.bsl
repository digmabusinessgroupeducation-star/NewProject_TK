#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Проведение
	
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос = Новый Запрос;
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстыЗапроса = Новый СписокЗначений;
		
		Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда
			ПартионныйУчет.ТекстЗапросаВтТекущиеДвиженияПартииТоваров(ТекстыЗапроса);
			ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
			ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
			
		ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") Тогда
			
			ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры);	
			ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры);	
			ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
			
		Иначе
			ПартионныйУчет.ТекстЗапросаВтТекущиеДвиженияПартииТоваров(ТекстыЗапроса);
			ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
			ТекстЗапросаВтТаблицаЗаказы(Запрос, ТекстыЗапроса);
			
			ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры);	
			ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры);	
			ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
			
			///
			ТекстЗапросаТаблицаОстаткиОрганизации(Запрос, ТекстыЗапроса, Регистры);
			
			ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
			ПартионныйУчет.СравнитьДвиженияПартийТоваров(ДополнительныеСвойства);
			
			ЗаказыСервер.ПодготовитьТаблицуЗакрытияЗаказовПоставщику(Запрос, ДополнительныеСвойства);
			
		КонецЕсли;
		
		
	КонецПроцедуры
	
	Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
		
		
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;// Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваров.Дата КАК Период,
		|	ПоступлениеТоваров.Ссылка КАК Ссылка,
		|	ПоступлениеТоваров.ХозОперация КАК ХозОперация,
		|	ПоступлениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
		|	ПоступлениеТоваров.Организация КАК Организация,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация КАК ОрганизацияДоговора,
		|	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
		|	ПоступлениеТоваров.Контрагент КАК Контрагент,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
		|	ПоступлениеТоваров.ЗакрытиеЗаказовПоставщикам КАК ЗакрытиеЗаказов,
		|	ПоступлениеТоваров.МоментВремени КАК МоментВремени,
		|	ПоступлениеТоваров.ДополнительныеРасходы КАК ДополнительныеРасходы,
		|	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка = &Ссылка";
		Реквизиты = Запрос.Выполнить().Выбрать();
		Реквизиты.Следующий();
		
		Запрос.УстановитьПараметр("Период",Реквизиты.Период);
		Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
		Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
		Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
		Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
		Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
		Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
		Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
		Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
		Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
		Запрос.УстановитьПараметр("ОрганизацияДоговора",Реквизиты.ОрганизацияДоговора);
		Запрос.УстановитьПараметр("ДополнительныеРасходы",Реквизиты.ДополнительныеРасходы);
		Запрос.УстановитьПараметр("КурсДокумента", Реквизиты.КурсДокумента);
		
		Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
		ОтключитьХарактеристику = Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(Реквизиты.СкладКомпании, Реквизиты.Период);
		Запрос.УстановитьПараметр("ОтключитьХарактеристику", ОтключитьХарактеристику);
		
		ПроводитьПоПартиям = Ложь;
		Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") ИЛИ ПолучитьФункциональнуюОпцию("ФормироватьДвиженияПоПартиямПриПроведении") Тогда
			ПроводитьПоПартиям = Истина;
		КонецЕсли;
		Запрос.УстановитьПараметр("ПроводитьПоПартиям",ПроводитьПоПартиям);
		
		ПроводитьПоОстаткамОрганизации = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период);
		Запрос.УстановитьПараметр("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);
		
		ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",ПроводитьПоОстаткамОрганизации);
		ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);
		
		ДополнительныеСвойства.ДляПроведения.Вставить("ЗакрытиеЗаказов",Реквизиты.ЗакрытиеЗаказов);
		ДополнительныеСвойства.ДляПроведения.Вставить("Организация",Реквизиты.ОрганизацияДоговора);
		ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);
		
		ДополнительныеСвойства.ДляПроведения.Вставить("ДополнительныеРасходы", Реквизиты.ДополнительныеРасходы);
		
	КонецПроцедуры
	
	Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
		
		ИмяРегистра = "ВтТаблицаТовары";
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Ссылка КАК Ссылка,
		|	ПоступлениеТоваровТовары.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровТовары.Ссылка.Организация КАК Организация,
		|	ПоступлениеТоваровТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	ПоступлениеТоваровТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании КАК СкладКомпании,
		|	ПоступлениеТоваровТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА &ОтключитьХарактеристику
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры
		|	КОНЕЦ КАК ХарактеристикаНоменклатуры,
		|	ПоступлениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПоступлениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
		|	ПоступлениеТоваровТовары.Коэффициент КАК Коэффициент,
		|	ПоступлениеТоваровТовары.КоличествоБазовое КАК Количество,
		|	ПоступлениеТоваровТовары.Ссылка.КурсДокумента КАК КурсДокумента,
		|	ПоступлениеТоваровТовары.Цена КАК Цена,
		|	ПоступлениеТоваровТовары.Сумма КАК Сумма,
		|	ПоступлениеТоваровТовары.СуммаВсего КАК СуммаВсего,
		|	ПоступлениеТоваровТовары.СуммаНДС КАК СуммаНДС,
		|	ПоступлениеТоваровТовары.СуммаБезНалогов КАК СуммаБезНалогов,
		|	ПоступлениеТоваровТовары.Ссылка.Контрагент КАК Контрагент,
		|	ПоступлениеТоваровТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ПоступлениеТоваровТовары.Ссылка.ХозОперация КАК ХозОперация,
		|	ПоступлениеТоваровТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций,
		|	ПоступлениеТоваровТовары.СуммаСпецСкидки КАК СуммаСпецСкидки
		|ПОМЕСТИТЬ ВтТаблицаТовары
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка = &Ссылка
		|	И ПоступлениеТоваровТовары.КоличествоБазовое > 0";		
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		ИмяРегистра = "ВтТаблицаУслуги";
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПоступлениеТоваровУслуги.Ссылка КАК Ссылка,
		|	СУММА(ПоступлениеТоваровУслуги.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ПоступлениеТоваровУслуги.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВтТаблицаУслуги
		|ИЗ
		|	Документ.ПоступлениеТоваров.Услуги КАК ПоступлениеТоваровУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслуги.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслуги.Ссылка";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		ИмяРегистра = "ВтДополнительныеРасходы";
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПоступлениеТоваров.Ссылка КАК Ссылка,
		|	ПоступлениеТоваров.ДопРасходы_Контрагент КАК Контрагент,
		|	ПоступлениеТоваров.ДопРасходы_Договор КАК Договор,
		|	ПоступлениеТоваров.ДопРасходы_Сумма КАК Сумма,
		|	ПоступлениеТоваров.ДопРасходы_СуммаНДС КАК СуммаНДС,
		|	ПоступлениеТоваров.ДопРасходы_СтавкаНДС КАК СтавкаНДС
		|ПОМЕСТИТЬ ВтДополнительныеРасходы
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка = &Ссылка
		|	И &ДополнительныеРасходы";	
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		ИмяРегистра = "ВтПартииТоваровКомпании";
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ВЫБОР
		|		КОГДА ВтТаблицаТовары.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровКомиссия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
		|	КОНЕЦ КАК СтатусПартии,
		|	ВтТаблицаТовары.Ссылка КАК Партия,
		|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
		|	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
		|	ВтТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВтТаблицаТовары.Количество КАК Количество,
		|	ВЫРАЗИТЬ(ВтТаблицаТовары.СуммаВсего - ВтТаблицаТовары.СуммаСпецСкидки КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВтТаблицаТовары.СуммаВсего - ВтТаблицаТовары.СуммаСпецСкидки КАК ЧИСЛО(15, 2)) КАК СуммаБаз,
		|	ВЫБОР
		|		КОГДА ВтТаблицаТовары.РегламентированныйУчет
		|			ТОГДА ВЫРАЗИТЬ(ВтТаблицаТовары.СуммаНДС КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНДС
		|ПОМЕСТИТЬ втНовыеДвиженияПартийТоваров
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|ГДЕ
		|	&ПроводитьПоПартиям";
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаВтТаблицаЗаказы(Запрос, ТекстыЗапроса)
		
		ИмяРегистра = "ВтТаблицаЗаказы";
		ТекстЗапроса = "ВЫБРАТЬ
		|	Док.ДокументОснование КАК ЗаказПоставщику
		|ПОМЕСТИТЬ ВтТаблицаЗаказы
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|	И Док.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Док.ЗаказПоставщику
		|ИЗ
		|	Документ.ПоступлениеТоваров.РаспределениеПоставки КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|	И Док.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ОстаткиТоваровКомпании";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
		|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВтТаблицаТовары.Контрагент КАК Контрагент,
		|	ВтТаблицаТовары.Количество КАК Количество,
		|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	Функция ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
		ИмяРегистра = "РасчетыСПоставщиками";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности) КАК ВидОперации,
		|	&Период КАК Период,
		|	&ХозОперация КАК ХозОперация,
		|	ВтТаблицаТовары.Контрагент КАК Контрагент,
		|	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
		|	СУММА(ВтТаблицаТовары.СуммаВсего * &КурсДокумента) КАК СуммаБаз
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаТовары.Ссылка,
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
		|	ВтТаблицаТовары.Контрагент,
		|	ВтТаблицаТовары.РегламентированныйУчет
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВтТаблицаТовары.СуммаВсего) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности),
		|	&Период,
		|	&ХозОперация,
		|	&Контрагент,
		|	&РегламентированныйУчет,
		|	&ДоговорВзаиморасчетов,
		|	СУММА(ВтТаблицаУслуги.Сумма),
		|	СУММА(ВтТаблицаУслуги.Сумма * &КурсДокумента)
		|ИЗ
		|	ВтТаблицаУслуги КАК ВтТаблицаУслуги
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВтТаблицаУслуги.Сумма) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности),
		|	&Период,
		|	&ХозОперация,
		|	ВтДополнительныеРасходы.Контрагент,
		|	ЛОЖЬ,
		|	ВтДополнительныеРасходы.Договор,
		|	ВтДополнительныеРасходы.Сумма,
		|	ВтДополнительныеРасходы.Сумма * &КурсДокумента
		|ИЗ
		|	ВтДополнительныеРасходы КАК ВтДополнительныеРасходы
		|ГДЕ
		|	&ДополнительныеРасходы
		|	И ВтДополнительныеРасходы.Сумма <> 0"; 
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
		
		Возврат ТекстЗапроса;
	КонецФункции
	
	Функция ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры)
		ИмяРегистра = "РасчетыСПоставщикамиПоДокументам";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ВтТаблицаТовары.Ссылка КАК Сделка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности) КАК ВидОперации,
		|	&Период КАК Период,
		|	&ХозОперация КАК ХозОперация,
		|	ВтТаблицаТовары.Контрагент КАК Контрагент,
		|	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
		|	СУММА(ВтТаблицаТовары.СуммаВсего * &КурсДокумента) КАК СуммаБаз
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
		|	ВтТаблицаТовары.Контрагент,
		|	ВтТаблицаТовары.РегламентированныйУчет,
		|	ВтТаблицаТовары.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВтТаблицаТовары.Ссылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности),
		|	&Период,
		|	&ХозОперация,
		|	ВтТаблицаТовары.Контрагент,
		|	ЛОЖЬ,
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
		|	СУММА(ВтТаблицаТовары.СуммаСпецСкидки),
		|	СУММА(ВтТаблицаТовары.СуммаСпецСкидки)
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|ГДЕ
		|	ВтТаблицаТовары.РегламентированныйУчет = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаТовары.ДоговорВзаиморасчетов,
		|	ВтТаблицаТовары.Контрагент,
		|	ВтТаблицаТовары.Ссылка
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВтТаблицаТовары.СуммаСпецСкидки) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВтТаблицаУслуги.Ссылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности),
		|	&Период,
		|	&ХозОперация,
		|	&Контрагент,
		|	&РегламентированныйУчет,
		|	&ДоговорВзаиморасчетов,
		|	СУММА(ВтТаблицаУслуги.Сумма),
		|	СУММА(ВтТаблицаУслуги.Сумма * &КурсДокумента)
		|ИЗ
		|	ВтТаблицаУслуги КАК ВтТаблицаУслуги
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаУслуги.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВтДополнительныеРасходы.Ссылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности),
		|	&Период,
		|	&ХозОперация,
		|	ВтДополнительныеРасходы.Контрагент,
		|	ЛОЖЬ,
		|	ВтДополнительныеРасходы.Договор,
		|	ВтДополнительныеРасходы.Сумма,
		|	ВтДополнительныеРасходы.Сумма * &КурсДокумента
		|ИЗ
		|	ВтДополнительныеРасходы КАК ВтДополнительныеРасходы
		|ГДЕ
		|	&ДополнительныеРасходы";  
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
		
		Возврат ТекстЗапроса;
	КонецФункции
	
	Функция ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ПартииТоваровКомпании";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		
		ТекстЗапроса =" ВЫБРАТЬ
		|	втНовыеДвиженияПартийТоваров.ВидДвижения КАК ВидДвижения,
		|	втНовыеДвиженияПартийТоваров.Период КАК Период,
		|	втНовыеДвиженияПартийТоваров.СтатусПартии КАК СтатусПартии,
		|	втНовыеДвиженияПартийТоваров.Партия КАК Партия,
		|	втНовыеДвиженияПартийТоваров.Номенклатура КАК Номенклатура,
		|	втНовыеДвиженияПартийТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	втНовыеДвиженияПартийТоваров.СкладКомпании КАК СкладКомпании,
		|	втНовыеДвиженияПартийТоваров.ХозОперация КАК ХозОперация,
		|	втНовыеДвиженияПартийТоваров.СтавкаНДС КАК СтавкаНДС,
		|	втНовыеДвиженияПартийТоваров.Количество КАК Количество,
		|	втНовыеДвиженияПартийТоваров.Сумма КАК Сумма,
		|	втНовыеДвиженияПартийТоваров.СуммаБаз КАК СуммаБаз,
		|	втНовыеДвиженияПартийТоваров.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	втНовыеДвиженияПартийТоваров КАК втНовыеДвиженияПартийТоваров"; 
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
		
		Возврат ТекстЗапроса;	
	КонецФункции
	
	Функция ТекстЗапросаТаблицаОстаткиОрганизации(Запрос, ТекстыЗапроса, Регистры)
		ИмяРегистра = "ТаблицаПартииТоваровОрганизации";
		
		Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		КонецЕсли; 
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ВЫБОР
		|		КОГДА ВтТаблицаТовары.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровКомиссия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварПринятыйКомиссия)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный)
		|	КОНЕЦ КАК СтатусПартии,
		|	ВтТаблицаТовары.Ссылка КАК Партия,
		|	ВтТаблицаТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчета,
		|	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
		|	ВтТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ВтТаблицаТовары.Количество) КАК Количество,
		|	СУММА(ВтТаблицаТовары.СуммаВсего) КАК Сумма,
		|	СУММА(ВтТаблицаТовары.СуммаНДС) КАК СуммаНДС
		|ИЗ
		|	ВтТаблицаТовары КАК ВтТаблицаТовары
		|ГДЕ
		|	&ПроводитьПоОстаткамОрганизации
		|	И &РегламентированныйУчет
		|
		|СГРУППИРОВАТЬ ПО
		|	ВтТаблицаТовары.СтавкаНДС,
		|	ВтТаблицаТовары.Ссылка,
		|	ВтТаблицаТовары.ХозОперация,
		|	ВтТаблицаТовары.АналитикаУчетаНоменклатурыОрганизаций"; 
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
		
		Возврат ТекстЗапроса;	
	КонецФункции
	
	
	#КонецОбласти
	
	
	#Область Последовательность
	
	Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
		
		ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров,Взаиморасчеты,ПартииОрганизаций");	
		
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
		
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор   = Ссылка;
		НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
		НоваяСтрока.Период   	  = Ссылка.Дата;
		ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
		
		
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор        = Ссылка;
		НоваяСтрока.ДоговорКонтрагента = Ссылка.ДоговорВзаиморасчетов;
		НоваяСтрока.Период             = Ссылка.Дата;
		
		ТаблицыДляПоследовательности.Взаиморасчеты = ТаблицаПоследовательности;
		
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("Организация");
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор        = Ссылка;
		НоваяСтрока.Организация 	   = ДополнительныеСвойства.ДляПроведения.Организация;
		НоваяСтрока.Период             = Ссылка.Дата;
		
		ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;
		
		
		ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ПрограммныйИнтерфейс	
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		// Акт недопоставки 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
		КомандаПечати.Идентификатор = "АктНедопоставки";
		КомандаПечати.Представление = НСтр("ru = 'Акт недопоставки';uk='Акт недопостачання'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
		//МассивУсловий = Новый Массив;
		//МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ИспользоватьЦеныИзПротоколов", Истина, ВидСравнения.Равно));	
		////МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ЕстьРасхождения", Истина, ВидСравнения.Равно));	
		//КомандаПечати.УсловияВидимости = МассивУсловий;
		
		// Приходная накладная
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
		КомандаПечати.Идентификатор = "ПриходнаяНакладная";
		КомандаПечати.Представление = НСтр("ru = 'Приходная накладная'; uk = 'Прибуткова накладна'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
		
		// Приходная накладная Вендинг
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
		КомандаПечати.Идентификатор = "ПриходнаяНакладнаяВендинг";
		КомандаПечати.Представление = НСтр("ru = 'Приходная накладная Вендинг'; uk = 'Прибуткова накладна Вендінг'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
		
	КонецПроцедуры
	
	// Определяет список команд отчетов.
	//
	// Параметры:
	//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
	//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
	//
	Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
		
		КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
		Если КомандаОтчет <> Неопределено Тогда
			КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
		КонецЕсли;
		
		
	КонецПроцедуры
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(ПодразделениеКомпании)
		|	И ЗначениеРазрешено(ТорговаяПлощадка)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
	
	// Устанавливает параметры загрузки.
	//
	// Параметры:
	//  Параметры - Структура - параметры загрузки данных из файла.
	//
	Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
		
	КонецПроцедуры
	
	// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
	// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
	//
	// Параметры:
	//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
	//                               находятся загруженные данные из файла. Состав колонок:
	//     * Идентификатор - Число - порядковый номер строки;
	//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
	//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
	//                               являющейся копией табличной части документа, 
	//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
	//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
	//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
	//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
	//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
	//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
	//
	Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
		
		Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
		ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
		|			И (ДанныеДляСопоставления.Артикул <> """")
		|			И (НЕ спрНоменклатура.ЭтоГруппа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|ГДЕ
		|	НЕ ДанныеДляСопоставления.Идентификатор В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
		|	И ДанныеДляСопоставления.Штрихкод <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
		|	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
		|ИЗ
		|	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|ГДЕ
		|	НЕ ДанныеДляСопоставления.Идентификатор В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|		
		|				ОБЪЕДИНИТЬ ВСЕ
		|		
		|				ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
		|	И ДанныеДляСопоставления.Номенклатура <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
		|			И (НЕ спрНоменклатура.ЭтоГруппа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
		|	""Наименование"" КАК ПолеПоиска,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
		|ИЗ
		|	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
		|	""Артикул"",
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
		|	""Штрихкод"",
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
		
		Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
		
		ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
		ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
		Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
			Товар = Товары.Добавить();
			
			Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
			Товар.Количество = СтрокаТаблицы.Количество;
			
			нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
			Если нСтрока <> Неопределено Тогда 			
				Если нСтрока.Количество = 1 Тогда 
					
					Товар.Номенклатура = нСтрока.Ссылка;			
					
				ИначеЕсли нСтрока.Количество > 1 Тогда 
					
					ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
					ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
					ЗаписьОНеоднозначности.Колонка = "Номенклатура";
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;	
		
		ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
		
	КонецПроцедуры
	
	// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
	// 
	// Параметры:
	//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
	//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
	//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
	//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
	//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
	//
	Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
		
		Если ИмяКолонки = "Номенклатура" Тогда 
			Запрос = Новый Запрос;
			
			ТекстГде = "";
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
				ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
				Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
				ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
				Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	спрНоменклатура.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК спрНоменклатура
			|ГДЕ
			|	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл 
				СписокНеоднозначностей.Добавить(Выборка.Ссылка);
			КонецЦикла;		
		КонецЕсли;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
	
	
	Процедура ЗаполнитьЦеныИзВходящегоДокумента(Объект) Экспорт 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	РеализацияТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
		|	РеализацияТоваровТовары.Цена КАК Цена
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		РеализацияТоваров.Ссылка КАК Ссылка
		|	ИЗ
		|		Документ.РеализацияТоваров КАК РеализацияТоваров
		|	ГДЕ
		|		РеализацияТоваров.Номер = &Номер
		|		И РеализацияТоваров.Дата МЕЖДУ &НачДата И &КонДата) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|		ПО ВложенныйЗапрос.Ссылка = РеализацияТоваровТовары.Ссылка";
		
		ТекДата = ТекущаяДата();
		
		НачДата = ?(ЗначениеЗаполнено(Объект.ВхДокДата), НачалоДня(Объект.ВхДокДата), НачалоГода(ТекДата));
		КонДата = ?(ЗначениеЗаполнено(Объект.ВхДокДата), КонецДня(Объект.ВхДокДата), КонецДня(ТекДата));
		
		Запрос.УстановитьПараметр("Номер", СокрЛП(Объект.ВхДокНомер));
		Запрос.УстановитьПараметр("НачДата", НачДата);
		Запрос.УстановитьПараметр("КонДата", КонДата);
		
		ТаблицаЦен = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаЦен.Количество() = 0 Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не найден документ реализация %1 %2'; uk = 'Не знайдений документ реалізації %1 %2'"),
			"№" + СокрЛП(Объект.ВхДокНомер),
			?(ЗначениеЗаполнено(Объект.ВхДокДата), НСтр("ru = 'от'; uk = 'від'") + " " + Формат(Объект.ВхДокДата, "ДФ=dd.MM.yyyy"), "")),
			Объект.Ссылка);
			Возврат;
		КонецЕсли;
		
		УчетХарактеристикНаСкладе = Не Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(Объект.СкладКомпании, Объект.Дата);
		
		Если УчетХарактеристикНаСкладе Тогда 
			ТаблицаЦен.Индексы.Добавить("Номенклатура, ХарактеристикаНоменклатуры");
		Иначе
			ТаблицаЦен.Индексы.Добавить("Номенклатура");
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		//ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,,Истина);
		
		СтруктураКэшированныхЗначений = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		
		Для Каждого Стр Из Объект.Товары Цикл 		
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Номенклатура", Стр.Номенклатура);
			Если УчетХарактеристикНаСкладе Тогда 
				СтруктураОтбора.Вставить("ХарактеристикаНоменклатуры", Стр.ХарактеристикаНоменклатуры);
			КонецЕсли;
			
			Цена = 0;
			нСтроки = ТаблицаЦен.НайтиСтроки(СтруктураОтбора);		
			Если нСтроки.Количество() > 0 Тогда 
				Цена = нСтроки[0].Цена;
			КонецЕсли;
			
			Стр.Цена = Цена;
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, СтруктураКэшированныхЗначений);		
		КонецЦикла;
		
	КонецПроцедуры
	
	Процедура ЗаполнитьИзВходящегоДокумента(Объект) Экспорт 
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РеализацияТоваровТовары.Ссылка КАК Ссылка,
		|	РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
		|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	РеализацияТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	РеализацияТоваровТовары.Количество КАК Количество,
		|	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
		|	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
		|	РеализацияТоваровТовары.Цена КАК Цена,
		|	РеализацияТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
		|	РеализацияТоваровТовары.Сумма КАК Сумма,
		|	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
		|	РеализацияТоваровТовары.СтавкаНДС КАК СтавкаНДС,
		|	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
		|	РеализацияТоваровТовары.СуммаБезНалогов КАК СуммаБезНалогов,
		|	РеализацияТоваровТовары.Номенклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		РеализацияТоваров.Ссылка КАК Ссылка
		|	ИЗ
		|		Документ.РеализацияТоваров КАК РеализацияТоваров
		|	ГДЕ
		|		РеализацияТоваров.Номер = &Номер
		|		И РеализацияТоваров.Дата МЕЖДУ &НачДата И &КонДата) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|		ПО ВложенныйЗапрос.Ссылка = РеализацияТоваровТовары.Ссылка";
		
		ТекДата = ТекущаяДата();
		
		НачДата = ?(ЗначениеЗаполнено(Объект.ВхДокДата), НачалоДня(Объект.ВхДокДата), НачалоГода(ТекДата));
		КонДата = ?(ЗначениеЗаполнено(Объект.ВхДокДата), КонецДня(Объект.ВхДокДата), КонецДня(ТекДата));
		
		Запрос.УстановитьПараметр("Номер", СокрЛП(Объект.ВхДокНомер));
		Запрос.УстановитьПараметр("НачДата", НачДата);
		Запрос.УстановитьПараметр("КонДата", КонДата);
		
		тзТовары = Запрос.Выполнить().Выгрузить();
		
		Объект.Товары.Загрузить(тзТовары);
		
		УчетХарактеристикНаСкладе = Не Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(Объект.СкладКомпании, Объект.Дата);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
		//ДобавитьДействиеПересчетаРасхождений(СтруктураДействий,,Истина);
		
		СтруктураКэшированныхЗначений = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		
		
	КонецПроцедуры
	
	Процедура РаспределитьДопРасходы(Объект, Отказ = Ложь) Экспорт 
		
		СуммаРасходов 		= Объект.ДопРасходы_Сумма;
		СпособРаспределения = Объект.ДопРасходы_СпособРаспределения;
		
		Если СпособРаспределения = Перечисления.СпособыРаспределенияДопРасходов.ПоКоличеству Тогда 
			ИмяКолонки = "КоличествоБазовое";
		ИначеЕсли СпособРаспределения = Перечисления.СпособыРаспределенияДопРасходов.ПоСумме Тогда 
			ИмяКолонки = "СуммаВсего";
		Иначе	
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		Итог = Объект.Товары.Итог(ИмяКолонки);
		Если Итог = 0 Тогда 
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		НакопленнаяРазница = 0;
		Для Каждого Стр Из Объект.Товары Цикл 
			Коэффициент = Стр[ИмяКолонки] / Итог;
			
			СуммаДоОкругления = СуммаРасходов * Коэффициент;
			ОкругленнаяСумма = Окр(СуммаДоОкругления, 2);
			
			НакопленнаяРазница = НакопленнаяРазница + (ОкругленнаяСумма - СуммаДоОкругления);
			
			Стр.СуммаСпецСкидки = ОкругленнаяСумма * -1;
		КонецЦикла;
		
		Если НакопленнаяРазница <> 0 Тогда 
			Стр = Объект.Товары[Объект.Товары.Количество()-1];
			Стр.СуммаСпецСкидки = Стр.СуммаСпецСкидки + НакопленнаяРазница;
		КонецЕсли;
		
	КонецПроцедуры
	
	
	#Область ВыгрузкаEDI
	
	Функция СтруктураДокументаCOMDOC(ДокументСсылка) Экспорт
		
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваров.Номер КАК НомерДокумента,
		|	ПоступлениеТоваров.Дата КАК ДатаДокумента,
		|	ВЫБОР
		|		КОГДА ПоступлениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПоступлениеТоваров.ДокументОснование КАК Документ.ЗаказПоставщику).Номер, ПоступлениеТоваров.Дата)
		|		ИНАЧЕ ПоступлениеТоваров.Дата
		|	КОНЕЦ КАК НомерЗаказа,
		|	ВЫБОР
		|		КОГДА ПоступлениеТоваров.ДокументОснование ССЫЛКА Документ.ЗаказПоставщику
		|			ТОГДА ЕСТЬNULL(ВЫРАЗИТЬ(ПоступлениеТоваров.ДокументОснование КАК Документ.ЗаказПоставщику).Дата, ПоступлениеТоваров.Дата)
		|		ИНАЧЕ ПоступлениеТоваров.Дата
		|	КОНЕЦ КАК ДатаЗаказа,
		|	ПоступлениеТоваров.ВхДокНомер КАК ВхДокНомер,
		|	ПоступлениеТоваров.ВхДокДата КАК ВхДокДата,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация.НаименованиеПолное КАК ОрганизацияНаименование,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация.КодПоЕДРПОУ КАК ОрганизацияКодПоЕДРПОУ,
		|	ЕСТЬNULL(УчетнаяПолитикаОрганизацийСрезПоследних.ИННПлательщикаНДС, """") КАК ОрганизацияИНН,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация.GLN КАК ОрганизацияGLN,
		|	ПоступлениеТоваров.ТорговаяПлощадка.GLN КАК ТорговаяПлощадкаGLN,
		|	ПоступлениеТоваров.Контрагент.ИННПлательщикаНДС КАК КонтрагентИНН,
		|	ПоступлениеТоваров.Контрагент.ЮрФизЛицо КАК КонтрагентЮрФизЛицо,
		|	ПоступлениеТоваров.Контрагент.НаименованиеПолное КАК КонтрагентНаименование,
		|	ПоступлениеТоваров.Контрагент.КодПоЕДРПОУ КАК КонтрагентКодПоЕДРПОУ,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.Номер КАК ДоговорНомер,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов.ДатаНачалаДействия КАК ДоговорДатаНачала,
		|	ПоступлениеТоваров.Комментарий КАК Комментарий,
		|	ТК_ПараметрыОбменаEDI.GLN КАК КонтрагентGLN,
		|	ПоступлениеТоваров.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура.ОсновнойШтрихКод КАК ШтрихКод,
		|		Номенклатура.Артикул КАК Артикул,
		|		Номенклатура.Наименование2 КАК Наименование,
		|		ЕдиницаИзмерения.ЕдиницаПоКлассификатору КАК ЕдиницаИзмерения,
		|		Количество КАК Количество,
		|		ЦенаБезНалогов КАК ЦенаБезНалогов,
		|		Цена КАК Цена,
		|		ПоступлениеТоваров.Товары.Цена - ПоступлениеТоваров.Товары.ЦенаБезНалогов КАК НДС,
		|		СуммаБезНалогов КАК СуммаБезНалогов,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаВсего КАК СуммаВсего
		|	) КАК Товары
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ПараметрыОбменаEDI КАК ТК_ПараметрыОбменаEDI
		|		ПО ПоступлениеТоваров.Контрагент = ТК_ПараметрыОбменаEDI.Контрагент
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&НаДату, ) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|		ПО ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка = &Ссылка
		|	И ПоступлениеТоваров.Товары.Количество <> 0";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		Запрос.УстановитьПараметр("НаДату", ДокументСсылка.Дата);
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		СтруктураДокумента = Новый Структура;
		
		Пока Выборка.Следующий() Цикл
			
			СтруктураДокумента.Вставить("Дата",Выборка.ДатаДокумента);
			
			СтруктураДокумента.Вставить("НомерДокумента",СокрЛП(Выборка.НомерДокумента));
			СтруктураДокумента.Вставить("ДатаДокумента",Формат(Выборка.ДатаДокумента,"ДФ=yyyy-MM-dd"));
			
			СтруктураДокумента.Вставить("ТипДокумента","Прибуткова накладна");	
			СтруктураДокумента.Вставить("КодТипаДокумента","007");	
			
			СтруктураДокумента.Вставить("НомерЗаказа",СокрЛП(Выборка.НомерЗаказа));
			СтруктураДокумента.Вставить("ДатаЗаказа", Формат(Выборка.ДатаЗаказа,"ДФ=yyyy-MM-dd"));
			СтруктураДокумента.Вставить("АдресДоставки","");
			
			//СтруктураДокумента.Вставить("АдресДоставки",СокрЛП(Выборка.АдресДоставки));
			//СтруктураДокумента.Вставить("Коментарий",СокрЛП(Выборка.Комментарий));
			СтруктураДокумента.Вставить("Коментарий","");
			
			СтруктураДокумента.Вставить("ДокументОснованиеНомер",СокрЛП(Выборка.ВхДокНомер));
			СтруктураДокумента.Вставить("ДокументОснованиеДата",Формат(Выборка.ВхДокДата,"ДФ=yyyy-MM-dd"));
			
			СтруктураДокумента.Вставить("ДокументаОснованиеТип","Видаткова накладна");
			СтруктураДокумента.Вставить("ДокументОснованиеКодТип","006");
			
			СтруктураДокумента.Вставить("Покупатель","Покупець");
			СтруктураДокумента.Вставить("ВидОсобыОрганизации",?(Выборка.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо,"Юридична","Фізична"));
			СтруктураДокумента.Вставить("НазваниеОрганизации",СокрЛП(Выборка.ОрганизацияНаименование));
			СтруктураДокумента.Вставить("КодОКПООрганизации",СокрЛП(Выборка.ОрганизацияКодПоЕДРПОУ));
			СтруктураДокумента.Вставить("КодИННОрганизации",СокрЛП(Выборка.ОрганизацияИНН));
			СтруктураДокумента.Вставить("КодGLNОрганизации",СокрЛП(Выборка.ОрганизацияGLN));
			
			СтруктураДокумента.Вставить("Поставщик","Продавець");
			СтруктураДокумента.Вставить("ВидОсобыПоставщик",?(Выборка.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо,"Юридична","Фізична"));
			СтруктураДокумента.Вставить("НазваниеКонтрагента",СокрЛП(Выборка.КонтрагентНаименование));
			СтруктураДокумента.Вставить("КодОКПОКонтрагента", СокрЛП(Выборка.КонтрагентКодПоЕДРПОУ));
			СтруктураДокумента.Вставить("КодИННКонтрагента",СокрЛП(Выборка.КонтрагентИНН));
			СтруктураДокумента.Вставить("КодGLNКонтрагента",СокрЛП(Выборка.КонтрагентGLN));
			
			СтруктураДокумента.Вставить("ТочкаДоставки","");
			
			//СтруктураДокумента.Вставить("ТочкаДоставки",СокрЛП(Выборка.АдресДоставки));
			СтруктураДокумента.Вставить("ТочкаДоставкиGLN",СокрЛП(Выборка.ТорговаяПлощадкаGLN));
			СтруктураДокумента.Вставить("НомерДоговора",СокрЛП(Выборка.ДоговорНомер));
			СтруктураДокумента.Вставить("ДатаДоговора",Формат(Выборка.ДоговорДатаНачала,"ДФ=yyyy-MM-dd"));
			
			
			
			
		КонецЦикла;
		
		
		
		
		Возврат СтруктураДокумента
		
	КонецФункции
	
	#КонецОбласти	
	
	
	#КонецОбласти	
	
	
	#Область Печать
	
	// Формирует печатные формы.
	//
	// Параметры:
	//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
	//  ПараметрыПечати - Структура - дополнительные настройки печати;
	//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
	//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
	//                                            представление - имя области, в которой был выведен объект (выходной параметр);
	//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		// Акт недопоставки
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктНедопоставки") Тогда    
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"АктНедопоставки",
			НСтр("ru = 'Акт недопоставки';uk='Акт недопостачання'"), 
			ПечатьАктНедопоставки(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ПоступлениеТоваров.ПФ_MXL_АктПриемаПередачи");
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходнаяНакладная") Тогда 		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПриходнаяНакладная",
			НСтр("ru = 'Приходная накладная'; uk = 'Прибуткова накладна'"), 
			ПечатьПриходнаяНакладная(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ПоступлениеТоваров.ПФ_MXL_ПриходнаяНакладная");		
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходнаяНакладнаяВендинг") Тогда 		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПриходнаяНакладнаяВендинг",
			НСтр("ru = 'Приходная накладная Вендинг'; uk = 'Прибуткова накладна Вендінг'"), 
			ПечатьПриходнаяНакладнаяВендинг(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ПоступлениеТоваров.ПФ_MXL_ПриходнаяНакладнаяВендинг");		
		КонецЕсли;	
		
	КонецПроцедуры
	
	// Акт недопоставки
	Функция ПечатьАктНедопоставки(МассивОбъектов, ОбъектыПечати)Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваров.Ссылка КАК Ссылка,
		|	ПоступлениеТоваров.Номер КАК Номер,
		|	ПоступлениеТоваров.Дата КАК Дата,
		|	ПоступлениеТоваров.ДоговорВзаиморасчетов КАК ОснованиеПредставление,
		|	ПоступлениеТоваров.СкладКомпании КАК ПредставлениеСкладаМестоСоставления,
		|	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ПоступлениеТоваров.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
		|	ПоступлениеТоваров.Контрагент.НаименованиеПолное КАК ПредставлениеПокупателя,
		|	ПоступлениеТоваров.Организация КАК Поставщик,
		|	ПоступлениеТоваров.Контрагент КАК Контрагент,
		|	ТорговыеПлощадкиКонтактнаяИнформация.Представление КАК ПредставлениеСоставления,
		|	ПоступлениеТоваров.Товары.(
		|		НомерСтроки КАК нпп,
		|		Номенклатура.Артикул КАК Код,
		|		ВЫБОР
		|			КОГДА ПоступлениеТоваров.Товары.Номенклатура.Наименование2 = """"
		|				ТОГДА ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваров.Товары.Номенклатура)
		|			ИНАЧЕ ПоступлениеТоваров.Товары.Номенклатура.Наименование2
		|		КОНЕЦ КАК ТоварНаименование,
		|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|		Количество КАК ФактическиПринято,
		|		КоличествоПоставщика КАК ПоНакладной,
		|		РасхождениеКоличество КАК ОтклонениеПоНакладной,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Номенклатура.СтавкаНДС.Ставка КАК СтавкаНДС,
		|		ПричинаРасхождения КАК Причина
		|	) КАК Товары
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТорговыеПлощадки.КонтактнаяИнформация КАК ТорговыеПлощадкиКонтактнаяИнформация
		|		ПО ПоступлениеТоваров.ТорговаяПлощадка = ТорговыеПлощадкиКонтактнаяИнформация.Ссылка
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка В(&МассивСсылок)
		|	И (ПоступлениеТоваров.Товары.РасхождениеКоличество <> 0
		|			ИЛИ ПоступлениеТоваров.Товары.РасхождениеЦена <> 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваров.МоментВремени,
		|	ПоступлениеТоваров.Товары.НомерСтроки";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();	
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктНедопоставки";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_АктПриемаПередачи");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
		ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
		ОбластьМестоСоставления = Макет.ПолучитьОбласть("МестоСоставления");
		ОбластьСостав			= Макет.ПолучитьОбласть("Состав");
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаН");
		ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаН");
		//ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("ПодписиН");
		
		Соответствие = Новый Соответствие;
		Соответствие.Вставить(Перечисления.ТК_ПричиныНедовоза.Брак, "Невідповідність якості");
		Соответствие.Вставить(Перечисления.ТК_ПричиныНедовоза.Недовоз, "Недовіз");
		Соответствие.Вставить(Перечисления.ТК_ПричиныНедовоза.НетВЗаказе, "Відсутність в замовленні");
		Соответствие.Вставить(Перечисления.ТК_ПричиныНедовоза.Пересорт, "Пересорт");
		
		ПервыйДокумент = Истина;
		
		Пока ВыборкаДокументов.Следующий() Цикл
			
			СтруктураШапки = Новый Структура("ТекстЗаголовка, ПредставлениеПоставщика, ПредставлениеПокупателя,ПредставлениеСоставления");
			
			СтруктураШапки.ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Акт расхождений к расходной (товаро-транспортной) накладной №%1 от %2'; uk = 'Акт розбіжностей до видаткової (товарно-транспортної) накладної №%1 от %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
			
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов, "ПредставлениеПоставщика, ПредставлениеПокупателя,ПредставлениеСоставления");
			
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьМестоСоставления.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьМестоСоставления);
			
			ТабличныйДокумент.Вывести(ОбластьСостав);
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				Если ВыборкаТовары.ОтклонениеПоНакладной = 0 Тогда 
					Продолжить;
				КонецЕсли;
				СтруктураСтроки = Новый Структура("нпп, Код, ЕдиницаИзмерения, СтавкаНДС, ПоНакладной, ФактическиПринято, ОтклонениеПоНакладной, Причина");
				ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);
				
				СтруктураСтроки.Вставить("ТоварНаименование", ?(ЗначениеЗаполнено(ВыборкаТовары.ХарактеристикаНоменклатуры), СокрЛП(ВыборкаТовары.ТоварНаименование)+", "+ВыборкаТовары.ХарактеристикаНоменклатуры,СокрЛП(ВыборкаТовары.ТоварНаименование)));
				СтруктураСтроки.Вставить("Причина", Соответствие.Получить(ВыборкаТовары.Причина));
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;				
			
			//ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
			//ТабличныйДокумент.Вывести(ОбластьПодвал);
			
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
	КонецФункции
	
	// Приходная накладная
	Функция ПечатьПриходнаяНакладная(МассивОбъектов, ОбъектыПечати)Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваров.Ссылка КАК Ссылка,
		|	ПоступлениеТоваров.Номер КАК Номер,
		|	ПоступлениеТоваров.Дата КАК Дата,
		|	ВЫБОР
		|		КОГДА ПоступлениеТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)
		|			ТОГДА ПоступлениеТоваров.ДоговорВзаиморасчетов.Организация
		|		ИНАЧЕ ПоступлениеТоваров.Организация
		|	КОНЕЦ КАК Организация,
		|	ПоступлениеТоваров.Контрагент КАК Контрагент,
		|	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
		|	ПоступлениеТоваров.СкладКомпании.Розничный КАК СкладКомпанииРозничный,
		|	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
		|	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
		|	ПоступлениеТоваров.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦенгРозница,
		|	ПоступлениеТоваров.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.Артикул КАК Код,
		|		Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|		ХарактеристикаНоменклатуры КАК Характеристика,
		|		Количество КАК Количество,
		|		КоличествоБазовое КАК КоличествоБазовое,
		|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Цена КАК Цена,
		|		СуммаВсего КАК СуммаВсего,
		|		СуммаНДС КАК СуммаНДС,
		|		СуммаСкидки КАК СуммаСкидки
		|	) КАК Товары
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваров.МоментВремени,
		|	НомерСтроки";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПриходнаяНакладная";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_ПриходнаяНакладная");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
		ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
		ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьИтогоНДС			= Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");		
		ОбластьВалюта 			= Макет.ПолучитьОбласть("Валюта");
		ОбластьИтогоСкидка		= Макет.ПолучитьОбласть("ИтогоСкидка");
		
		ОбластьШапкаТаблицыР	= Макет.ПолучитьОбласть("ШапкаТаблицыР");
		ОбластьСтрокаР 			= Макет.ПолучитьОбласть("СтрокаР");
		ОбластьИтогоР 			= Макет.ПолучитьОбласть("ИтогоР");
		ОбластьСклад 			= Макет.ПолучитьОбласть("Склад");
		
		
		ПервыйДокумент = Истина;
		Пока ВыборкаДокументов.Следующий() Цикл
			ТекСсылка = ВыборкаДокументов.Ссылка;				
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			СтруктураШапки = Новый Структура;
			СтруктураШапки.Вставить("ТекстЗаголовка",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Поступление товаров №%1 от %2'; uk = 'Надходження товарів №%1 від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
			
			СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			СтруктураШапки.Вставить("ПредставлениеПолучателя", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			СтруктураШапки.Вставить("Склад", ВыборкаДокументов.СкладКомпании);
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ВалютаДокумента) Тогда 
				СтруктураШапки.Вставить("Валюта", "Валюта: " + СокрЛП(ВыборкаДокументов.ВалютаДокумента) + ?(ЗначениеЗаполнено(ВыборкаДокументов.КурсДокумента), "(" + Формат(ВыборкаДокументов.КурсДокумента, "ЧДЦ=3") + ")" , ""));
			КонецЕсли;
			
			тчТовары = ВыборкаДокументов.Товары.Выгрузить();
			
			СуммаВсего = тчТовары.Итог("СуммаВсего");
			СуммаСкидки = тчТовары.Итог("СуммаСкидки");
			СуммаНДС = тчТовары.Итог("СуммаНДС");
			
			СтруктураПодвала = Новый Структура;
			СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
			СтруктураПодвала.Вставить("ВсегоНДС", Формат(СуммаНДС, "ЧЦ=15; ЧДЦ=2"));
			СтруктураПодвала.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
			СтруктураПодвала.Вставить("ИтоговаяСтрока", 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1 на сумму %2 %3'; uk = 'Всього найменувань %1 на суму %2 %3'", КодЯзыкаПечать),
			Формат(тчТовары.Количество(), "ЧДЦ=0"),
			Формат(СуммаВсего, "ЧДЦ=2"),
			ВыборкаДокументов.ВалютаДокумента));
			
			ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
			СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);				
			
			СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
			
			Если СуммаСкидки > 0 Тогда 
				СтруктураПодвала.Вставить("ВсегоСкидка", СуммаСкидки);
			КонецЕсли;
			
			
			// Выводим данные в табличный документ
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			Если СтруктураШапки.Свойство("Валюта") Тогда 
				ОбластьВалюта.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьВалюта);
			КонецЕсли;
			
			Если ВыборкаДокументов.СкладКомпанииРозничный Тогда 
				
				ЗапросЦен = Новый Запрос;
				ЗапросЦен.Текст = "ВЫБРАТЬ
				|	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
				|	ЦеныСрезПоследних.Характеристика КАК Характеристика,
				|	ЦеныСрезПоследних.Цена КАК Цена
				|ИЗ
				|	РегистрСведений.Цены.СрезПоследних(
				|			&Дата,
				|			Номенклатура В (&мНоменклатура)
				|				И ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних";
				
				ЗапросЦен.УстановитьПараметр("Дата", ВыборкаДокументов.Дата);
				ЗапросЦен.УстановитьПараметр("мНоменклатура", тчТовары.ВыгрузитьКолонку("Номенклатура"));
				ЗапросЦен.УстановитьПараметр("ТипЦен", ВыборкаДокументов.ТипЦенгРозница);
				
				тчЦены = ЗапросЦен.Выполнить().Выгрузить();
				тчЦены.Индексы.Добавить("Номенклатура, Характеристика");
				
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыР);			
				Для Каждого Стр Из тчТовары Цикл 
					ПараметрыСтрки = Новый Структура("НомерСтроки, Код, Количество, КоличествоБазовое, ЕдиницаИзмерения, Цена, СуммаВсего");
					ЗаполнитьЗначенияСвойств(ПараметрыСтрки, Стр);				
					ПараметрыСтрки.Вставить("ТоварНаименование", СокрЛП(Стр.ТоварНаименование));
					
					нСтроки = тчЦены.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Стр.Номенклатура, Стр.Характеристика));
					Если нСтроки.Количество() > 0 Тогда 
						ПараметрыСтрки.Вставить("ЦенаРозничная", Формат(нСтроки[0].Цена, "ЧЦ=15; ЧДЦ=2"));
					КонецЕсли;
					
					ОбластьСтрокаР.Параметры.Заполнить(ПараметрыСтрки);
					ТабличныйДокумент.Вывести(ОбластьСтрокаР);
					
				КонецЦикла;
				
			Иначе
				
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
				Для Каждого Стр Из тчТовары Цикл 
					ПараметрыСтрки = Новый Структура("НомерСтроки, Код, Количество, КоличествоБазовое, ЕдиницаИзмерения, Цена, СуммаВсего");
					ЗаполнитьЗначенияСвойств(ПараметрыСтрки, Стр);
					ПараметрыСтрки.Вставить("ТоварНаименование", СокрЛП(Стр.ТоварНаименование));
					ОбластьСтрока.Параметры.Заполнить(ПараметрыСтрки);
					ТабличныйДокумент.Вывести(ОбластьСтрока);				
				КонецЦикла;
				
			КонецЕсли;
			
			Если СтруктураПодвала.Свойство("ВсегоСкидка") Тогда 
				ОбластьИтогоСкидка.Параметры.Заполнить(СтруктураПодвала);
				ТабличныйДокумент.Вывести(ОбластьИтогоСкидка);
			ИначеЕсли ВыборкаДокументов.СкладКомпанииРозничный Тогда 
				ОбластьИтогоР.Параметры.Заполнить(СтруктураПодвала);
				ТабличныйДокумент.Вывести(ОбластьИтогоР);			
			Иначе
				ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
				ТабличныйДокумент.Вывести(ОбластьИтого);
			КонецЕсли;
			
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
			
			ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
			
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ТекСсылка);
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
		
	КонецФункции
	
	// Приходная накладная Вендинг
	Функция ПечатьПриходнаяНакладнаяВендинг(МассивОбъектов, ОбъектыПечати)
		
		ОбработкаВендинг = Обработки.ВендингАвтоматы;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПоступлениеТоваровТовары.Ссылка КАК Ссылка,
		|	ПоступлениеТоваровТовары.Ссылка.Номер КАК Номер,
		|	ПоступлениеТоваровТовары.Ссылка.Дата КАК Дата,
		|	ВЫБОР
		|		КОГДА ПоступлениеТоваровТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)
		|			ТОГДА ПоступлениеТоваровТовары.Ссылка.ДоговорВзаиморасчетов.Организация
		|		ИНАЧЕ ПоступлениеТоваровТовары.Ссылка.Организация
		|	КОНЕЦ КАК Организация,
		|	ПоступлениеТоваровТовары.Ссылка.Контрагент КАК Контрагент,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании КАК СкладКомпании,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании.Розничный КАК СкладКомпанииРозничный,
		|	ПоступлениеТоваровТовары.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
		|	ПоступлениеТоваровТовары.Ссылка.КурсДокумента КАК КурсДокумента,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦенгРозница,
		|	ПоступлениеТоваровТовары.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровТовары.Номенклатура.Артикул КАК Артикул,
		|	ПоступлениеТоваровТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ПоступлениеТоваровТовары.Количество КАК Количество,
		|	ПоступлениеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
		|	ПоступлениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПоступлениеТоваровТовары.Цена КАК Цена,
		|	ПоступлениеТоваровТовары.СуммаВсего КАК СуммаВсего,
		|	ПоступлениеТоваровТовары.СуммаНДС КАК СуммаНДС,
		|	ПоступлениеТоваровТовары.СуммаСкидки КАК СуммаСкидки,
		|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК ЦенаРозничная,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании.ВидАвтомата.Рядов КАК Рядов,
		|	ПоступлениеТоваровТовары.Ссылка.СкладКомпании.ВидАвтомата.Ячеек КАК Ячеек,
		|	ЕСТЬNULL(ПланограммаАвтоматаСрезПоследних.Ячейка, 0) КАК Ячейка
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПланограммаАвтомата.СрезПоследних КАК ПланограммаАвтоматаСрезПоследних
		|		ПО ПоступлениеТоваровТовары.Ссылка.СкладКомпании = ПланограммаАвтоматаСрезПоследних.Склад
		|			И ПоступлениеТоваровТовары.Номенклатура = ПланограммаАвтоматаСрезПоследних.Номенклатура
		|			И ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры = ПланограммаАвтоматаСрезПоследних.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&НаДату, ) КАК ЦеныСрезПоследних
		|		ПО ПоступлениеТоваровТовары.Номенклатура = ЦеныСрезПоследних.Номенклатура
		|			И ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.Характеристика
		|			И ПоступлениеТоваровТовары.Ссылка.СкладКомпании.ТипЦенРозничнойТорговли = ЦеныСрезПоследних.ТипЦен
		|ГДЕ
		|	ПоступлениеТоваровТовары.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	Ячейка
		|ИТОГИ ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		Запрос.УстановитьПараметр("НаДату", КонецДня(ТекущаяДата()));
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПриходнаяНакладнаяВендинг";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_ПриходнаяНакладнаяВендинг");
		Макет.КодЯзыка = КодЯзыкаПечать;
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
		ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
		ОбластьШапкаТаблицы		= Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьИтогоНДС			= Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");		
		ОбластьВалюта 			= Макет.ПолучитьОбласть("Валюта");
		ОбластьИтогоСкидка		= Макет.ПолучитьОбласть("ИтогоСкидка");
		
		ОбластьШапкаТаблицыР	= Макет.ПолучитьОбласть("ШапкаТаблицыР");
		ОбластьСтрокаР 			= Макет.ПолучитьОбласть("СтрокаР");
		ОбластьИтогоР 			= Макет.ПолучитьОбласть("ИтогоР");
		ОбластьСклад 			= Макет.ПолучитьОбласть("Склад");
		
		
		ПервыйДокумент = Истина;
		Пока ВыборкаДокументов.Следующий() Цикл
			
			Если ВыборкаДокументов.СкладКомпании.ВидАвтомата = ПредопределенноеЗначение("Справочник.ВидыВендингАвтоматов.ПустаяСсылка") Тогда
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Эта печатная форма предназначена только для Вендинга (%1)'; uk = 'Ця друкована форма призначена лише для Вендінга (%1)'"),
				ВыборкаДокументов.Ссылка));
				Продолжить;
			КонецЕсли;
			
			//	получаем остатки автомата
			Ответ = ОбработкаВендинг.ПолучитьДанные_GET("automat/"+Формат(ВыборкаДокументов.СкладКомпании.ИД_Автомата,"ЧГ=0")+"/cell/");
			
			Если Ответ.КодСостояния > 399 Тогда
				ОбработкаВендинг.Ошибка404(Ответ,"GET");
				Продолжить;;
			КонецЕсли;
			
			ТаблицаОстатков = ОбработкаВендинг.ПолучитьТаблицаЗначений();
			
			Для каждого мСтрока Из Ответ.Данные Цикл
				стрОстатки = ТаблицаОстатков.Добавить();
				ЗаполнитьЗначенияСвойств(стрОстатки,мСтрока);
				стрОстатки.Артикул = Формат(мСтрока.article,"ЧГ=0");
			КонецЦикла;
			
			ТекСсылка = ВыборкаДокументов.Ссылка;				
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			СтруктураШапки = Новый Структура;
			СтруктураШапки.Вставить("ТекстЗаголовка",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Поступление товаров №%1 от %2'; uk = 'Надходження товарів №%1 від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy")));
			
			СтруктураШапки.Вставить("ПредставлениеПоставщика", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			СтруктураШапки.Вставить("ПредставлениеПолучателя", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));
			СтруктураШапки.Вставить("Склад", ВыборкаДокументов.СкладКомпании);
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.ВалютаДокумента) Тогда 
				СтруктураШапки.Вставить("Валюта", "Валюта: " + СокрЛП(ВыборкаДокументов.ВалютаДокумента) + ?(ЗначениеЗаполнено(ВыборкаДокументов.КурсДокумента), "(" + Формат(ВыборкаДокументов.КурсДокумента, "ЧДЦ=3") + ")" , ""));
			КонецЕсли;
			
			// Выводим данные в табличный документ
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПоставщик);
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			Если СтруктураШапки.Свойство("Валюта") Тогда 
				ОбластьВалюта.Параметры.Заполнить(СтруктураШапки);
				ТабличныйДокумент.Вывести(ОбластьВалюта);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыР);			
			
			нпп 			= 0;
			СуммаВсего 		= 0;
			СуммаСкидки		= 0;
			СуммаНДС		= 0;
			
			тзРотацияВитрины = ОбработкаВендинг.ПолучитьТаблицаЗначений();
			
			ВыборкаТовары = ВыборкаДокументов.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				нпп = нпп + 1;
				
				//	Проверка на новое МРЦ
				мОстатки = ТаблицаОстатков.НайтиСтроки(Новый Структура("Артикул,maxRetailPrice",ВыборкаТовары.Артикул,ВыборкаТовары.Характеристика.ЗначениеМРЦ * 100));
				
				ЭтоНовоеМРЦ = мОстатки.Количество() = 0;
				
				Если ЭтоНовоеМРЦ Тогда
					тзОстаткиМРЦ = ТаблицаОстатков.Скопировать(Новый Структура("Артикул",ВыборкаТовары.Артикул));
					тзОстаткиМРЦ.Сортировать("order Убыв");
					
					нСтрока = тзРотацияВитрины.Добавить();
					ЗаполнитьЗначенияСвойств(нСтрока,ВыборкаТовары);
					Если тзОстаткиМРЦ.Количество() > 0 Тогда
						ЗаполнитьЗначенияСвойств(нСтрока,тзОстаткиМРЦ[0],,"Номенклатура,Характеристика,Ячейка");
					КонецЕсли;
					нСтрока.СортировкаМРЦ = ВыборкаТовары.Характеристика.Сортировка;
				КонецЕсли;
				
				ПараметрыСтроки = Новый Структура("Артикул, Количество, КоличествоБазовое, ЕдиницаИзмерения, Цена, СуммаВсего, ЦенаРозничная");
				ЗаполнитьЗначенияСвойств(ПараметрыСтроки, ВыборкаТовары);				
				ПараметрыСтроки.Вставить("НомерСтроки", нпп);
				//ПараметрыСтроки.Вставить("ТоварНаименование", СокрЛП(ВыборкаТовары.ТоварНаименование));
				ПараметрыСтроки.Вставить("ТоварНаименование", СокрЛП(ВыборкаТовары.ТоварНаименование) + ?(Не ПустаяСтрока(ВыборкаТовары.Характеристика), ", " + СокрЛП(ВыборкаТовары.Характеристика), ""));
				
				Если ВыборкаТовары.Ячейка = 0 Тогда
					ПараметрыСтроки.Вставить("Ячейка", "");
					ПараметрыСтроки.Вставить("_Ячейка","");
				Иначе
					Ячейка = ОбработкаВендинг.ИмяЯчейки(ВыборкаТовары.Рядов,ВыборкаТовары.Ячеек,ВыборкаТовары.Ячейка);
					ПараметрыСтроки.Вставить("Ячейка", Ячейка);
					ПараметрыСтроки.Вставить("_Ячейка","("+Прав("00"+ВыборкаТовары.Ячейка,3)+")");
				КонецЕсли;
				
				ОбластьСтрокаР.Параметры.Заполнить(ПараметрыСтроки);
				ТабличныйДокумент.Вывести(ОбластьСтрокаР);
				
				СуммаВсего	= СуммаВсего + ВыборкаТовары.СуммаВсего;
				СуммаСкидки	= СуммаСкидки + ВыборкаТовары.СуммаСкидки;
				СуммаНДС	= СуммаНДС + ВыборкаТовары.СуммаНДС;
				
			КонецЦикла;
			СтруктураПодвала = Новый Структура;
			СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
			СтруктураПодвала.Вставить("ВсегоНДС", Формат(СуммаНДС, "ЧЦ=15; ЧДЦ=2"));
			СтруктураПодвала.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
			СтруктураПодвала.Вставить("ИтоговаяСтрока", 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1 на сумму %2 %3'; uk = 'Всього найменувань %1 на суму %2 %3'", КодЯзыкаПечать),
			Формат(ВыборкаТовары.Количество(), "ЧДЦ=0"),
			Формат(СуммаВсего, "ЧДЦ=2"),
			ВыборкаДокументов.ВалютаДокумента));
			
			ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
			СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);				
			
			СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
			
			Если СуммаСкидки > 0 Тогда 
				СтруктураПодвала.Вставить("ВсегоСкидка", СуммаСкидки);
			КонецЕсли;
			
			Если СтруктураПодвала.Свойство("ВсегоСкидка") Тогда 
				ОбластьИтогоСкидка.Параметры.Заполнить(СтруктураПодвала);
				ТабличныйДокумент.Вывести(ОбластьИтогоСкидка);
				//ИначеЕсли ВыборкаДокументов.СкладКомпанииРозничный Тогда 
				//	ОбластьИтогоР.Параметры.Заполнить(СтруктураПодвала);
				//	ТабличныйДокумент.Вывести(ОбластьИтогоР);			
				//Иначе
				//	ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
				//	ТабличныйДокумент.Вывести(ОбластьИтого);
			КонецЕсли;
			
			ОбластьИтогоР.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоР);			
			
			
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
			
			ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
			
			ТабличныйДокумент.Вывести(ОбластьПодписи);
			
			
			Если тзРотацияВитрины.Количество() > 0 Тогда
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				ОбластьЗаголовокРТ 		= Макет.ПолучитьОбласть("Заголовок");
				ОбластьШапкаТаблицыРТ	= Макет.ПолучитьОбласть("ШапкаТаблицыРТ");
				ОбластьСтрокаРТ 		= Макет.ПолучитьОбласть("СтрокаРТ");
				ОбластьИтогоРТ 			= Макет.ПолучитьОбласть("ИтогоРТ");
				
				ОбластьЗаголовокРТ.Параметры.ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ротация витрины к Поступлению товаров №%1 от %2'; uk = 'Ротація вітрини до Надходження товарів №%1 від %2'", КодЯзыкаПечать),
				СокрЛП(ВыборкаДокументов.Номер),
				Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy"));
				
				ТабличныйДокумент.Вывести(ОбластьЗаголовокРТ);
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыРТ);
				
				НомерСтроки = 0;
				мНоменклатураДляЗаменыВитрины = ОбщегоНазначенияКлиентСервер.СвернутьМассив(тзРотацияВитрины.ВыгрузитьКолонку("Номенклатура"));
				
				Для Каждого стрНоменклатура Из мНоменклатураДляЗаменыВитрины Цикл
					тзНовоеМРЦ = тзРотацияВитрины.Скопировать(Новый Структура("Номенклатура",стрНоменклатура));
					тзНовоеМРЦ.Сортировать("СортировкаМРЦ Убыв");
					СтрокаЗаменыВитрины = тзНовоеМРЦ[0];
					
					НомерСтроки = 1 + НомерСтроки;
					ЯчейкаУ = ОбработкаВендинг.ИмяЯчейки(ВыборкаДокументов.Рядов,ВыборкаДокументов.Ячеек,СтрокаЗаменыВитрины.number);
					ЯчейкаВ = ОбработкаВендинг.ИмяЯчейки(ВыборкаДокументов.Рядов,ВыборкаДокументов.Ячеек,СтрокаЗаменыВитрины.Ячейка);
					СтруктураРотации = Новый Структура;
					СтруктураРотации.Вставить("НомерСтроки",НомерСтроки);
					СтруктураРотации.Вставить("Артикул",СтрокаЗаменыВитрины.Артикул);
					СтруктураРотации.Вставить("ТоварНаименование",СтрокаЗаменыВитрины.Номенклатура);
					СтруктураРотации.Вставить("Количество","1 шт");
					СтруктураРотации.Вставить("ЯчейкаУ",ЯчейкаУ);
					СтруктураРотации.Вставить("_ЯчейкаУ",?(СтрокаЗаменыВитрины.number=0,"","("+СтрокаЗаменыВитрины.number+")"));
					СтруктураРотации.Вставить("МРЦУ",СтрокаЗаменыВитрины.maxRetailPrice/100);
					СтруктураРотации.Вставить("ЯчейкаВ",ЯчейкаВ);
					СтруктураРотации.Вставить("_ЯчейкаВ","("+СтрокаЗаменыВитрины.Ячейка+")");
					СтруктураРотации.Вставить("МРЦВ",СтрокаЗаменыВитрины.Характеристика.ЗначениеМРЦ);
					ОбластьСтрокаРТ.Параметры.Заполнить(СтруктураРотации);
					ТабличныйДокумент.Вывести(ОбластьСтрокаРТ);
				КонецЦикла;
				
				ТабличныйДокумент.Вывести(ОбластьИтогоРТ);
				
			КонецЕсли;
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ТекСсылка);
			
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
		
	КонецФункции
	
	
	#КонецОбласти	
	
	
	
	
#Иначе
	
#КонецЕсли
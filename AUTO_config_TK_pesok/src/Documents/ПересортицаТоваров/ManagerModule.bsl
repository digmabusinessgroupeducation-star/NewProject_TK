#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровПересортицаТоваров(Запрос, ДополнительныеСвойства);
	    ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);

	Иначе
		
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		Запрос.Текст = "";
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровПересортицаТоваров(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПересортицаТоваров.Дата КАК Период,
	               |	ПересортицаТоваров.Номер КАК Номер,
	               |	ПересортицаТоваров.Ссылка КАК Ссылка,
	               |	ПересортицаТоваров.ХозОперация КАК ХозОперация,
	               |	ПересортицаТоваров.Организация КАК Организация,
	               |	ПересортицаТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПересортицаТоваров.СкладКомпании КАК СкладКомпании,
	               |	ПересортицаТоваров.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	               |	ПересортицаТоваров.МоментВремени КАК МоментВремени,
	               |	ПересортицаТоваров.ДокументОснование КАК ДокументОснование
	               |ИЗ
	               |	Документ.ПересортицаТоваров КАК ПересортицаТоваров
	               |ГДЕ
	               |	ПересортицаТоваров.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация", Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("СкладКомпании", Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("ДокументОснование", Реквизиты.ДокументОснование);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("Дата", Реквизиты.Период);
	ДополнительныеСвойства.ДляПроведения.Вставить("Ссылка", Реквизиты.Ссылка);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости", Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);

КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)	

	ХозОперация = Неопределено;
	Запрос.Параметры.Свойство("ХозОперация", ХозОперация);
	
	ИмяРегистра = "ВтТаблицаТовары";
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПересортицаТоваровТовары.Ссылка КАК Ссылка,
	               |	ПересортицаТоваровТовары.Ссылка.Организация КАК Организация,
	               |	ПересортицаТоваровТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПересортицаТоваровТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ПересортицаТоваровТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПересортицаТоваровТовары.Ссылка.СкладКомпании КАК СкладКомпании,
	               |	ПересортицаТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	ПересортицаТоваровТовары.НоменклатураРасход КАК НоменклатураРасход,
	               |	ПересортицаТоваровТовары.НоменклатураПриход КАК НоменклатураПриход,
	               |	ПересортицаТоваровТовары.ХарактеристикаНоменклатурыРасход КАК ХарактеристикаНоменклатурыРасход,
	               |	ПересортицаТоваровТовары.ХарактеристикаНоменклатурыПриход КАК ХарактеристикаНоменклатурыПриход,
	               |	ПересортицаТоваровТовары.ЕдиницаИзмеренияРасход КАК ЕдиницаИзмеренияРасход,
	               |	ПересортицаТоваровТовары.ЕдиницаИзмеренияПриход КАК ЕдиницаИзмеренияПриход,
	               |	ПересортицаТоваровТовары.КоличествоРасход КАК КоличествоРасход,
	               |	ПересортицаТоваровТовары.КоличествоПриход КАК КоличествоПриход,
	               |	ПересортицаТоваровТовары.КоличествоБазовоеРасход КАК КоличествоБазовоеРасход,
	               |	ПересортицаТоваровТовары.КоличествоБазовоеПриход КАК КоличествоБазовоеПриход,
	               |	ПересортицаТоваровТовары.КоэффициентРасход КАК КоэффициентРасход,
	               |	ПересортицаТоваровТовары.КоэффициентПриход КАК КоэффициентПриход,
	               |	ПересортицаТоваровТовары.ЦенаРасход КАК ЦенаРасход,
	               |	ПересортицаТоваровТовары.ЦенаПриход КАК ЦенаПриход,
	               |	ПересортицаТоваровТовары.СуммаРасход КАК СуммаРасход,
	               |	ПересортицаТоваровТовары.СуммаПриход КАК СуммаПриход,
	               |	ПересортицаТоваровТовары.Партия КАК Партия,
	               |	ПересортицаТоваровТовары.ОстатокРасход КАК ОстатокРасход,
	               |	ПересортицаТоваровТовары.ОстатокПриход КАК ОстатокПриход
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	               |ГДЕ
	               |	ПересортицаТоваровТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
	               |	ВтТаблицаТовары.НоменклатураРасход КАК Номенклатура,
	               |	ВтТаблицаТовары.ХарактеристикаНоменклатурыРасход КАК ХарактеристикаНоменклатуры,
	               |	ВтТаблицаТовары.КоличествоБазовоеРасход КАК Количество,
	               |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	               |	ВтТаблицаТовары.НомерСтроки КАК ПорядокСтрок
	               |ИЗ
	               |	ВтТаблицаТовары КАК ВтТаблицаТовары
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	ВтТаблицаТовары.СкладКомпании,
	               |	ВтТаблицаТовары.НоменклатураПриход,
	               |	ВтТаблицаТовары.ХарактеристикаНоменклатурыПриход,
	               |	ВтТаблицаТовары.КоличествоБазовоеПриход,
	               |	ВтТаблицаТовары.ХозОперация,
	               |	ВтТаблицаТовары.НомерСтроки
	               |ИЗ
	               |	ВтТаблицаТовары КАК ВтТаблицаТовары
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПорядокСтрок,
	               |	ВидДвижения УБЫВ";
 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти	


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");

	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
	НоваяСтрока.Период   	  = Ссылка.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Пересортица
    КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ПересортицаТоваров";
    КомандаПечати.Идентификатор = "Пересортица";
    КомандаПечати.Представление = НСтр("ru = 'Пересортица'; uk = 'Пересортиця'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Переоценка оприходованных товаров
    КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ПересортицаТоваров";
    КомандаПечати.Идентификатор = "ПереоценкаОприходованныхТоваров";
    КомандаПечати.Представление = НСтр("ru = 'Переоценка оприходованнных товаров'; uk = 'Переоцінка оприбуткованих товарів'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Пересортица") Тогда // Пересортица
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Пересортица",
		НСтр("ru = 'Пересортица'; uk = 'Пересортиця'"), 
		ПечатьПересортица(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ПересортицаТоваров.ПФ_MXL_Пересортица");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПереоценкаОприходованныхТоваров") Тогда// Переоценка оприходованных товаров
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПереоценкаОприходованныхТоваров",
		НСтр("ru = 'Переоценка оприходованнных товаров'; uk = 'Переоцінка оприбуткованих товарів'"), 
		ПечатьПереоценка(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ПересортицаТоваров.ПФ_MXL_Переоценка");
				
	КонецЕсли;	
	
	
КонецПроцедуры


// Пересортица
Функция ПечатьПересортица(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПересортицаТоваров.Ссылка КАК Ссылка,
	               |	ПересортицаТоваров.Дата КАК Дата,
	               |	ПересортицаТоваров.Номер КАК Номер,
	               |	ПересортицаТоваров.ХозОперация КАК ХозОперация,
	               |	ПересортицаТоваров.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		НоменклатураРасход.Артикул КАК КодРасход,
	               |		НоменклатураРасход КАК НоменклатураРасход,
	               |		НоменклатураПриход.Артикул КАК КодПриход,
	               |		НоменклатураПриход КАК НоменклатураПриход,
	               |		ХарактеристикаНоменклатурыРасход КАК ХарактеристикаРасход,
	               |		ХарактеристикаНоменклатурыПриход КАК ХарактеристикаПриход,
	               |		ЕдиницаИзмеренияРасход КАК ЕдиницаИзмеренияРасход,
	               |		ЕдиницаИзмеренияПриход КАК ЕдиницаИзмеренияПриход,
	               |		КоличествоРасход КАК КоличествоРасход,
	               |		КоличествоПриход КАК КоличествоПриход,
	               |		ЦенаРасход КАК ЦенаРозничнаяРасход,
	               |		ЦенаПриход КАК ЦенаРозничная,
	               |		СуммаРасход КАК СуммаРозничнаяРасход,
	               |		СуммаПриход КАК СуммаРозничная
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ПересортицаТоваров КАК ПересортицаТоваров
	               |ГДЕ
	               |	ПересортицаТоваров.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Пересортица";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПересортицаТоваров.ПФ_MXL_Пересортица");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		текХозОперация = ВыборкаДокументов.ХозОперация;
		НаименованиеХозОперации = "";
		
		Если текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаТоваров") Тогда 
			НаименованиеХозОперации = "ru = 'Пересортица товаров №%1 от %2'; uk = 'Пересортиця товарів №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеретариваниеТоваров") Тогда 
			НаименованиеХозОперации = "ru = 'Перетаривание товаров №%1 от %2'; uk = 'Перетарювання товарів №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаПоЗакрытиюСмены") Тогда 
			НаименованиеХозОперации = "ru = 'Пересортица товаров по закрытию смены №%1 от %2'; uk = 'Пересортиця товарів по закриттю зміни №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПонижениеСортности") Тогда 
			НаименованиеХозОперации = "ru = 'Понижение сортности №%1 от %2'; uk = 'Зниження сортності №%1 від %2'";
		КонецЕсли;	
						
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр(НаименованиеХозОперации, КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));		
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		 
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
						
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
										
		ТабличныйДокумент.Вывести(ОбластьШапка);
				
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		
		нпп = 0;
		РасходИтого = 0;
		ПриходИтого = 0;
		
		Пока ВыборкаТовары.Следующий() Цикл			
			нпп = нпп + 1;
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
			
			Если ЗначениеЗаполнено(ВыборкаТовары.ХарактеристикаРасход) Тогда 
				ОбластьСтрока.Параметры.НоменклатураРасход = СокрЛП(ВыборкаТовары.НоменклатураРасход) + ", " + СокрЛП(ВыборкаТовары.ХарактеристикаРасход);
			КонецЕсли;			
			
			Если ЗначениеЗаполнено(ВыборкаТовары.ХарактеристикаПриход) Тогда 				
				ОбластьСтрока.Параметры.НоменклатураПриход = СокрЛП(ВыборкаТовары.НоменклатураПриход) + ", " + СокрЛП(ВыборкаТовары.ХарактеристикаПриход);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			РасходИтого = РасходИтого + ВыборкаТовары.СуммаРозничнаяРасход;
			ПриходИтого = ПриходИтого + ВыборкаТовары.СуммаРозничная;
		КонецЦикла;
				
		ОбластьПодписи.Параметры.СуммаРозничнаяРасходИтог = РасходИтого;
		ОбластьПодписи.Параметры.СуммаРозничнаяПриходИтог = ПриходИтого;
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Переоценка оприходованных товаров
Функция ПечатьПереоценка(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПересортицаТоваров.Ссылка КАК Ссылка,
	               |	ПересортицаТоваров.Дата КАК Дата,
	               |	ПересортицаТоваров.Номер КАК Номер,
	               |	ПересортицаТоваров.ХозОперация КАК ХозОперация,
	               |	ПересортицаТоваров.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		НоменклатураПриход.Артикул КАК Код,
	               |		НоменклатураПриход КАК НоменклатураПриход,
	               |		ХарактеристикаНоменклатурыПриход КАК ХарактеристикаПриход,
	               |		ЕдиницаИзмеренияПриход КАК ЕдиницаИзмерения,
	               |		КоличествоПриход КАК Количество,
	               |		ЦенаРасход КАК ЦенаСтарая,
	               |		ЦенаПриход КАК Цена,
	               |		СуммаРасход КАК СуммаРозничнаяРасход,
	               |		СуммаПриход КАК СуммаРозничная,
	               |		ВЫБОР
	               |			КОГДА ЕСТЬNULL(ПересортицаТоваров.Товары.ЦенаРасход, 0) > 0
	               |				ТОГДА ВЫРАЗИТЬ(100 * (ЕСТЬNULL(ПересортицаТоваров.Товары.ЦенаПриход, 0) - ПересортицаТоваров.Товары.ЦенаРасход) / ПересортицаТоваров.Товары.ЦенаРасход КАК ЧИСЛО(5, 2))
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ПроцентНаценки
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ПересортицаТоваров КАК ПересортицаТоваров
	               |ГДЕ
	               |	ПересортицаТоваров.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Пересортица";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПересортицаТоваров.ПФ_MXL_Переоценка");		
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		текХозОперация = ВыборкаДокументов.ХозОперация;
		НаименованиеХозОперации = "";
		
		Если текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаТоваров") Тогда 
			НаименованиеХозОперации = "ru = 'Пересортица товаров №%1 от %2'; uk = 'Пересортиця товарів №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеретариваниеТоваров") Тогда 
			НаименованиеХозОперации = "ru = 'Перетаривание товаров №%1 от %2'; uk = 'Перетарювання товарів №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаПоЗакрытиюСмены") Тогда 
			НаименованиеХозОперации = "ru = 'Пересортица товаров по закрытию смены №%1 от %2'; uk = 'Пересортиця товарів по закриттю зміни №%1 від %2'";
		ИначеЕсли текХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПонижениеСортности") Тогда 
			НаименованиеХозОперации = "ru = 'Понижение сортности №%1 от %2'; uk = 'Зниження сортності №%1 від %2'";
		КонецЕсли;	
						
		ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр(НаименованиеХозОперации, КодЯзыкаПечать),
								СокрЛП(ВыборкаДокументов.Номер),
								Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));		
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		 
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
						
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
										
		ТабличныйДокумент.Вывести(ОбластьШапка);
				
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		
		Пока ВыборкаТовары.Следующий() Цикл			
			
			СтруктураСтроки = Новый Структура("НомерСтроки, Код, ЕдиницаИзмерения, ЦенаСтарая, ПроцентНаценки, Цена, Количество");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);
			
			текНоменклатура = СокрЛП(Строка(ВыборкаТовары.НоменклатураПриход));
			Если ЗначениеЗаполнено(ВыборкаТовары.ХарактеристикаПриход) Тогда 				
				текНоменклатура = текНоменклатура + ", " + СокрЛП(ВыборкаТовары.ХарактеристикаПриход);
			КонецЕсли;
			
			СтруктураСтроки.Вставить("Номенклатура", текНоменклатура);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецОбласти	






#КонецЕсли
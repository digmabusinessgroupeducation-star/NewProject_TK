#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда	
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		ЗаполнитьДокументПеремещениеТоваровВФилиал(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВозвратОтПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииВозвратаОтПокупателя(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
	Для Каждого Стр Из Товары Цикл 
		Стр.Партия = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	Автор 				  = Пользователи.ТекущийПользователь();
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
КонецПроцедуры
	

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.ПересортицаТоваров КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);	
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
	КонецЕсли;


	
КонецПроцедуры


Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;

	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ПересортицаТоваров.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ПересортицаТоваров.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);

	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ПересортицаТоваровПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

	
КонецПроцедуры


Процедура ЗаполнитьДокументПеремещениеТоваровВФилиал(ДанныеЗаполненеия)
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаТоваров");
	
	Автор =  Пользователи.ТекущийПользователь();
	КурсДокумента         = 1;
	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	               |	ПеремещениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	               |	ПеремещениеТоваров.СкладКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПеремещениеТоваров.Товары.(
	               |		Номенклатура КАК НоменклатураРасход,
	               |		Номенклатура КАК НоменклатураПриход,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыРасход,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмеренияРасход,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмеренияПриход,
	               |		Коэффициент КАК КоэффициентРасход,
	               |		Количество КАК КоличествоРасход,
	               |		КоличествоБазовое КАК КоличествоБазовоеРасход,
	               |		Количество КАК КоличествоПриход,
	               |		КоличествоБазовое КАК КоличествоБазовоеПриход
	               |	) КАК Товары,
	               |	ПеремещениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПеремещениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании
	               |ИЗ
	               |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	               |ГДЕ
	               |	ПеремещениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполненеия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		ТипЦен = Справочники.СкладыКомпании.ТипЦенРозничный(Выборка.СкладКомпании);
		Организация = Справочники.СкладыКомпании.ОрганизацияСклада(Выборка.СкладКомпании);
		
		Если ПодразделениеКомпании.Пустая() Тогда 
			ПодразделениеКомпании = Выборка.ПодразделениеКомпании;
		КонецЕсли;				
		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();				
		
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВозвратаОтПокупателя(ДанныеЗаполнения)
	
	ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПересортицаТоваров");	
	Автор =  Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВозвратОтПокупателя.Ссылка КАК ДокументОснование,
	               |	ВозвратОтПокупателя.Организация КАК Организация,
	               |	ВозвратОтПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратОтПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВозвратОтПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ВозвратОтПокупателя.ТипЦен КАК ТипЦен,
	               |	ВозвратОтПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратОтПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ВозвратОтПокупателя.ДокументОснование КАК ДокументПродажи,
	               |	ВозвратОтПокупателя.Товары.(
	               |		Номенклатура КАК НоменклатураРасход,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыРасход,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмеренияРасход,
	               |		Количество КАК КоличествоРасход,
	               |		КоличествоБазовое КАК КоличествоБазовоеРасход,
	               |		Коэффициент КАК КоэффициентРасход
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВозвратОтПокупателя КАК ВозвратОтПокупателя
	               |ГДЕ
	               |	ВозвратОтПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Товары.Очистить();
				
		ВыборкаТовары = Выборка.Товары.Выбрать();						
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);			
		КонецЦикла;		
	КонецЕсли;	
	
КонецПроцедуры


#КонецЕсли



#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом
	
	УстановитьСостояниеДокумента();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ТК_УстановкаМаркетинга"));
	
	ЗаполнитьДеревоТП();
	ЗаполнитьОтборы();

	Если НЕ ЗначениеЗаполнено(Объект.Менеджер) Тогда
		Объект.Менеджер = Пользователи.АвторизованныйПользователь().ФизическоеЛицо;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если  ТекущийОбъект.СхемаРасчета <> Перечисления.СхемыНачисления.ФиксированыйБонус Тогда
		ТекущийОбъект.ХранилищеОтборов = Новый ХранилищеЗначения(КомпоновщикНастроек.ПолучитьНастройки(), Новый СжатиеДанных(9));
	КонецЕсли;
	
	ТекущийОбъект.Площадки.Очистить();
	
	тДерево = РеквизитФормыВЗначение("ДеревоТорговыхПлощадок");
	ОбходДерева(тДерево, ТекущийОбъект);
	
	Если ЗначениеЗаполнено(Отборы) Тогда
        ТекущийОбъект.ДополнительныеСвойства.Вставить("Отборы", Отборы);
    КонецЕсли;
	

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ТК_УправлениеДоступом
	
	УстановитьСостояниеДокумента();

КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ИнициализироватьНастройки();
	Элементы.КомпоновщикНастроекНастройкиОтбор.Доступность = Истина;
	
	Если  Объект.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтПоставки Тогда 
		СхемаКомпоновкиДанных = Документы.ТК_УстановкаМаркетинга.ПолучитьМакет("ПроцентОтПоставок");
	ИначеЕсли  Объект.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтОплат Тогда
		СхемаКомпоновкиДанных = Документы.ТК_УстановкаМаркетинга.ПолучитьМакет("ПроцентОтОплат");
	Иначе
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
		Элементы.КомпоновщикНастроекНастройкиОтбор.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Функция получитьзначениеПеречисления (имя)
	возврат Перечисления.СхемыНачисления[имя];	
КонецФункции

&НаСервере
Процедура ОбновитьКоличествоТорговыхПлощадок()
	Попытка
		КолвоТП = ЭтаФорма.ДеревоТорговыхПлощадок.Строки.НайтиСтроки(Новый Структура("ТорговаяПлощадкаВыбрана", Истина), Истина).Количество();
		ЭтаФорма.ДеревоТорговыхПлощадок.Колонки["ТорговаяПлощадка"].ТекстШапки = "Торговые площадки" + ?(КолвоТП = 0, "", " (" + КолвоТП + ")");
	Исключение
		Возврат;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ОбходДерева(тДерево, ТекущийОбъект);
		
	Для Каждого СтрДерева Из тДерево.Строки Цикл
		Если СтрДерева.ТорговаяПлощадкаВыбрана Тогда
			НоваяСтрока = ТекущийОбъект.Площадки.Добавить();
			НоваяСтрока.ТорговаяПлощадка = СтрДерева.ТорговаяПлощадка;
		КонецЕсли;
					
		Если СтрДерева.Строки.Количество()>0 Тогда
			ОбходДерева(СтрДерева, ТекущийОбъект);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоТП()
	
	ВыбранныеТП = Новый СписокЗначений;
	Для Каждого стр из Объект.Площадки Цикл
		ВыбранныеТП.Добавить(стр.ТорговаяПлощадка);
	КонецЦикла;
	
	ДеревоТП = Новый ДеревоЗначений;
	
	 //создаем колонки
	МассивТипов = Новый Массив(1); МассивТипов[0]=Тип("СправочникСсылка.ТорговыеПлощадки");
	ДеревоТП.Колонки.Добавить("ТорговаяПлощадка", Новый ОписаниеТипов(МассивТипов),"Торговые площадки",35);
	МассивТипов[0]=Тип("Булево");
	ДеревоТП.Колонки.Добавить("ТорговаяПлощадкаВыбрана", Новый ОписаниеТипов(МассивТипов));

	// заполняем строки
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТорговыеПлощадки.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВыбранныеТорговыеПлощадки
	               |ИЗ
	               |	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	               |ГДЕ
	               |	ТорговыеПлощадки.Ссылка В(&ВыбранныеТП)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТорговыеПлощадки.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТорговыеПлощадки.Родитель КАК РодительТорговойПлощадки,
	               |	ТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
	               |	ВЫБОР
	               |		КОГДА ВыбранныеТорговыеПлощадки.Ссылка ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК ТорговаяПлощадкаВыбрана
	               |ИЗ
	               |	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВыбранныеТорговыеПлощадки КАК ВыбранныеТорговыеПлощадки
	               |		ПО (ВыбранныеТорговыеПлощадки.Ссылка = ТорговыеПлощадки.Ссылка)
	               |ГДЕ
	               |	НЕ ТорговыеПлощадки.ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТорговаяПлощадка ИЕРАРХИЯ УБЫВ
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ВыбранныеТП", ВыбранныеТП);
	Выборка = Запрос.Выполнить().Выбрать();
		
	СоответствиеРодителей = Новый Соответствие;
	// добавляем строки в дерево. Иерархически.
	Пока Выборка.Следующий() Цикл
		Если СоответствиеРодителей[Выборка.РодительТорговойПлощадки] = Неопределено Тогда
			ТекСтрока = ДеревоТП.Строки.Добавить();
		Иначе
			ТекСтрока = СоответствиеРодителей[Выборка.РодительТорговойПлощадки].Строки.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ТекСтрока, Выборка);
		СоответствиеРодителей.Вставить(Выборка.ТорговаяПлощадка, ТекСтрока);
	КонецЦикла;
	ЗначениеВРеквизитФормы(ДеревоТП, "ДеревоТорговыхПлощадок");

КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОтборы()
	
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда    
		тОтборы = Параметры.ЗначениеКопирования;
	Иначе
		тОтборы = РеквизитФормыВЗначение("Объект");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СхемаРасчета) Тогда 
		Если  Объект.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтПоставки Тогда 
			СхемаКомпоновкиДанных = Документы.ТК_УстановкаМаркетинга.ПолучитьМакет("ПроцентОтПоставок");
		ИначеЕсли  Объект.СхемаРасчета = Перечисления.СхемыНачисления.ПроцентОтОплат Тогда
			СхемаКомпоновкиДанных = Документы.ТК_УстановкаМаркетинга.ПолучитьМакет("ПроцентОтОплат");
		Иначе
			Элементы.КомпоновщикНастроекНастройкиОтбор.Доступность = Ложь;
			Возврат;
		КонецЕсли;
		
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));	
		
		НастройкиОтборов = тОтборы.ХранилищеОтборов.Получить();

		Попытка
			КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтборов);
		Исключение
			КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановкаФлажков(ЭлементКоллекции, ЗначениеПометки)

    ПодчинЭлементы = ЭлементКоллекции.ПолучитьЭлементы();
    Для Каждого ТекЭлемент Из ПодчинЭлементы Цикл
        ТекЭлемент.ТорговаяПлощадкаВыбрана = ЗначениеПометки;
        УстановкаФлажков(ТекЭлемент, ТекЭлемент.ТорговаяПлощадкаВыбрана);
    КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция УстановленноДляВсех(ЭлементКоллекции)

    СоседниеЭлементы =
        ЭлементКоллекции.ПолучитьРодителя().ПолучитьЭлементы();
    Для Каждого ТекЭлемент Из СоседниеЭлементы Цикл
        Если ТекЭлемент.ТорговаяПлощадкаВыбрана <> ЭлементКоллекции.ТорговаяПлощадкаВыбрана Тогда
            Возврат Ложь;
        КонецЕсли;
    КонецЦикла;
    Возврат Истина;

КонецФункции   

&НаКлиенте
Процедура ДеревоТорговыхПлощадокТорговаяПлощадкаВыбранаПриИзменении(Элемент)
	
	ИДТекущейСтроки = Элементы.ДеревоТорговыхПлощадок.ТекущаяСтрока;

    Если ИДТекущейСтроки <> Неопределено Тогда

        ЭлементКоллекции = ЭтаФорма.ДеревоТорговыхПлощадок.НайтиПоИдентификатору(
            ИДТекущейСтроки);

        Если ЭлементКоллекции.ТорговаяПлощадкаВыбрана = 2 Тогда
            ЭлементКоллекции.ТорговаяПлощадкаВыбрана = 0;
        КонецЕсли;

        УстановкаФлажков(ЭлементКоллекции, ЭлементКоллекции.ТорговаяПлощадкаВыбрана);

        Родитель = ЭлементКоллекции.ПолучитьРодителя();
        Пока Родитель <> Неопределено Цикл
            Родитель.ТорговаяПлощадкаВыбрана = ?(УстановленноДляВсех(ЭлементКоллекции),
                ЭлементКоллекции.ТорговаяПлощадкаВыбрана, 2);
            ЭлементКоллекции = Родитель;
            Родитель = ЭлементКоллекции.ПолучитьРодителя();
        КонецЦикла;

    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СхемаРасчетаПриИзменении(Элемент)
	
	ИнициализироватьНастройки();

КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()

	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзТекстовогоПоляПродолжение", ЭтаФорма, Неопределено);
	
	ПоказатьВводСтроки(
		ОписаниеОповещения, 
		"", 
		НСтр("ru = 'Введите артикула товаров с новой строки или через запятую'; uk = 'Введіть артикули товарів з нової строки або через кому'"),
		0,
		Истина);
		
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТекстовогоПоляПродолжение(Текст, ДопПараметры) Экспорт 
	
	Если Не ЗначениеЗаполнено(Текст) Тогда 
		Возврат;
	КонецЕсли;

	ЗагрузитьИзТекстовогоПоляНаСервере(Текст);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзТекстовогоПоляНаСервере(Текст)
	
	НовТекст = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(",.-;", Текст, Символы.ПС);	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(НовТекст, Символы.ПС);
	СписокНоменклатуры = Новый СписокЗначений;
	
	Для Каждого Стр Из МассивСтрок Цикл 
		текСтр = СокрЛП(Стр);
		
		Если ПустаяСтрока(текСтр) Тогда 
			Продолжить;
		ИначеЕсли Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(текСтр) Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Неверный формат артикула ""%1""'; uk = 'Невірний формат артикулу ""%1""'"),
					текСтр));
		Иначе
			Номенклатура = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", текСтр);	
			Если Не Номенклатура.Пустая() Тогда 
				НоваяСтрока = СписокНоменклатуры.Добавить(Номенклатура);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не найдена номенклатура с артикулом ""%1""'; uk = 'Не знайдена номенклатура з артикулом ""%1""'"),
						текСтр));					
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьОтборСКД("Номенклатура", ВидСравненияКомпоновкиДанных.ВСписке, СписокНоменклатуры);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСКД(ИмяПоля, ВидОтбора, Значение)
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ОтборыКомпановщика = КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для Каждого Стр Из ОтборыКомпановщика Цикл
		Если Стр.ЛевоеЗначение = ПолеОтбора Тогда
			Отбор = Стр;
		КонецЕсли;
	КонецЦикла;
	Если Отбор = Неопределено Тогда
		Отбор = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение  = ПолеОтбора;
	КонецЕсли;
	
	Если Значение <> Неопределено Тогда
		Отбор.ВидСравнения   = ВидОтбора;
		Отбор.Использование  = Истина;
		Отбор.ПравоеЗначение  = Значение;
	Иначе
		Отбор.Использование  = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




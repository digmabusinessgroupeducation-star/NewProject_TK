#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ПрограммныйИнтерфейс
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		// Склады из  Изменение ассортимента
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.ИзменениеАссортимента";
		КомандаПечати.Идентификатор = "ИзменениеАссортиментаСклады";
		КомандаПечати.Представление = НСтр("ru = 'Список складов';uk='Список складів'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
		КомандаПечати.СписокФорм = "ФормаДокумента";
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ДляВызоваИзДругихПодсистем
	// СтандартныеПодсистемы.УправлениеДоступом
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(ПодразделениеКомпании)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	#КонецОбласти
	
	
	
	
	
	
	
	#Область Проведение
	
	Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
		
		ИсточникиДанных = Новый Соответствие;
		
		Возврат ИсточникиДанных; 
		
	КонецФункции
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
		
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		Запрос = Новый Запрос;
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстыЗапроса = Новый СписокЗначений;
		ТекстЗапросаТаблицаЦеныНоменклатуры(Запрос, ТекстыЗапроса, Регистры);
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
	КонецПроцедуры
	
	Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		
	КонецПроцедуры
	
	Функция ТекстЗапросаТаблицаЦеныНоменклатуры(Запрос, ТекстыЗапроса, Регистры)
		
		ИмяРегистра = "ОграничениеАссортимента";
		
		Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
			Возврат "";
		КонецЕсли; 
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ИзменениеАссортиментаТовары.Ссылка.ДатаВводаВАссортимент КАК Период,
		|	ИзменениеАссортиментаТорговыеПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ИзменениеАссортиментаТовары.Номенклатура КАК Номенклатура,
		|	ИзменениеАссортиментаТовары.БазовыйАссортимент КАК БазовыйАссортимент,
		|	ИзменениеАссортиментаТовары.ВидАссортимента КАК ВидАссортимента,
		|	ВЫБОР
		|		КОГДА ИзменениеАссортиментаТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВводТоваровВАссортимент)
		|			ТОГДА 1
		|		КОГДА ИзменениеАссортиментаТовары.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.РаспродажаСПоследующимВыводомИзАссортимента)
		|			ТОГДА 2
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Состояние
		|ИЗ
		|	Документ.ИзменениеАссортимента.Товары КАК ИзменениеАссортиментаТовары,
		|	Документ.ИзменениеАссортимента.ТорговыеПлощадки КАК ИзменениеАссортиментаТорговыеПлощадки
		|ГДЕ
		|	ИзменениеАссортиментаТовары.Ссылка = &Ссылка
		|	И ИзменениеАссортиментаТорговыеПлощадки.Ссылка = &Ссылка
		|	И НЕ ИзменениеАссортиментаТовары.Номенклатура = ЗНАЧЕНИЕ(справочник.Номенклатура.ПустаяСсылка)";
		
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	
	// Процедура компонует текст запроса, выполняет запрос и выгружает результаты запроса в таблицы
	//
	// Параметры:
	//	Запрос				- Запрос - запрос, параметры которого предварительно установлены.
	//	ТекстыЗапроса		- Список значений - в списке перечислены тексты запросов и их имена.
	//	Таблицы				- Структура - структура в которую будут помещены полученные таблицы для движений.
	//	ДобавитьРазделитель	- Булево - Истина, если нужно добавить разделитель ";" между запросами.
	//	ДобавлятьСловоТаблица	- Булево - Истина, если к имени таблицы движений нужно вначало добавить слово "Таблица"
	//
	Процедура ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, Таблицы, ДобавитьРазделитель = Ложь, ДобавлятьСловоТаблица = Истина, ТолькоОтмеченные=Ложь) Экспорт
		
		ТаблицыЗапроса = ОбщегоНазначенияТК.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, ДобавитьРазделитель);
		
		// Помещение результатов запроса в таблицы
		Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл
			
			ИмяТаблицы = ТекстЗапроса.Представление;
			
			Если Не ПустаяСтрока(ИмяТаблицы) И (Не ТолькоОтмеченные Или ТекстЗапроса.Пометка) Тогда
				
				Если ДобавлятьСловоТаблица Тогда
					// Таблицы для проведения должны начинаться с "Таблица"
					Если НЕ СтрНачинаетсяС(ИмяТаблицы, "Таблица") Тогда
						ИмяТаблицы = "Таблица" + ИмяТаблицы;
					КонецЕсли;
				КонецЕсли;
				
				Таблицы.Вставить(ИмяТаблицы, ТаблицыЗапроса[ТекстЗапроса.Представление]);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	
	
	#Область Печать
	
	// Формирует печатные формы.
	//
	// Параметры:
	//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
	//  ПараметрыПечати - Структура - дополнительные настройки печати;
	//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
	//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
	//                                            представление - имя области, в которой был выведен объект (выходной параметр);
	//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		//	ИзменениеАссортимента
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИзменениеАссортиментаСклады") Тогда    
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ИзменениеАссортиментаСклады",
			НСтр("ru = 'Изменение ассортимента';uk='Зміна асортименту'"),
			ПечатьИзменениеАссортимента(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ИзменениеАссортимента.ПФ_MXL_ИзменениеАссортиментаСклады");
		КонецЕсли;	
		//ПечатьИзменениеАссортимента(МассивОбъектов, ОбъектыПечати);
	КонецПроцедуры
	
	//	ИзменениеАссортимента
	Функция ПечатьИзменениеАссортимента(МассивОбъектов, ОбъектыПечати)	Экспорт
		
		Док = МассивОбъектов[0];
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ИзменениеАссортимента";
		
		ТабличныйДокумент.ТолькоПросмотр = Истина;
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИзменениеАссортимента.ПФ_MXL_ИзменениеАссортиментаСклады");
		
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		ОбластьПодразделение 	= Макет.ПолучитьОбласть("Подразделение");
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
		ОбластьАвтор			= Макет.ПолучитьОбласть("Автор");
		ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
		
		ПервыйДокумент = Истина;
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 №%2 с %3'; uk = '%1 №%2 с %3'", КодЯзыкаПечать),
		СокрЛП(Док.ХозОперация),
		СокрЛП(Док.Номер),
		Формат(Док.ДатаВводаВАссортимент, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ПредставлениеПодразделения", Док.ПодразделениеКомпании);
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПодразделение.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПодразделение);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп		= 0;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СкладыКомпании.Код КАК КодСклада,
		|	СкладыКомпании.Ссылка КАК Склад,
		|	СкладыКомпании.Маршрут КАК Маршрут,
		|	СкладыКомпании.АссортиментнаяМатрица КАК Матрица,
		|	ИзменениеАссортиментаТорговыеПлощадки.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	ИзменениеАссортиментаТорговыеПлощадки.ТорговаяПлощадка.Код КАК ПлощадкаКод
		|ИЗ
		|	Документ.ИзменениеАссортимента.ТорговыеПлощадки КАК ИзменениеАссортиментаТорговыеПлощадки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
		|		ПО ИзменениеАссортиментаТорговыеПлощадки.ТорговаяПлощадка = СкладыКомпании.ТорговаяПлощадка
		|			И (СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка))
		|ГДЕ
		|	ИзменениеАссортиментаТорговыеПлощадки.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Ссылка", Док);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("КодСклада, Склад, Маршрут");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаДетальныеЗаписи);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("ПредставлениеПлощадки", СокрЛП(ВыборкаДетальныеЗаписи.ТорговаяПлощадка)+", "+СокрЛП(ВыборкаДетальныеЗаписи.ПлощадкаКод));
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		СтруктураИтого = Новый Структура;	
		СтруктураИтого.Вставить("Всего", нпп);		
		
		ОбластьИтого.Параметры.Заполнить(СтруктураИтого);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		ОбластьАвтор.Параметры.Автор = Док.Автор;
		ТабличныйДокумент.Вывести(ОбластьАвтор);
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Док);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции
	
	#КонецОбласти	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	
#КонецЕсли
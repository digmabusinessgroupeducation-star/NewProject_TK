
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Элементы.ОтборХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ЗаказПокупателя"));
	
		// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, "Список");
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
		
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ПериодВыборки = Неопределено;
	Если ПараметрыОтбора.Свойство("ПериодВыборки", ПериодВыборки) Тогда 
		
		ЭлементыОтбора = Список.Отбор.Элементы;
		
		ЭлементОтбораДанных = Неопределено;
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			Если ЭлементОтбора.Представление = "ОтборПериод" Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
			
			Если ЭлементОтбораДанных = Неопределено Тогда
				ГруппаОтборПериод = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтборПериод.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли; 
				ГруппаОтборПериод.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
				ГруппаОтборПериод.Использование = Истина;
				ГруппаОтборПериод.Представление = "ОтборПериод";
			Иначе
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Истина;
			КонецЕсли;	
			
			ГруппаДатаСортировки = ГруппаОтборПериод.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаДатаСортировки.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ; 
			ГруппаДатаСортировки.Использование = Истина;
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаНачала) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаНачала;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаОкончания;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;
			
			Если ГруппаДатаСортировки.Элементы.Количество() = 0 Тогда 
				ГруппаОтборПериод.Элементы.Удалить(ГруппаДатаСортировки);
			КонецЕсли;
			
		Иначе
			
			Если ЭлементОтбораДанных <> Неопределено Тогда
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Для Каждого текОтбор Из ПараметрыОтбора Цикл 		
		ИмяРеквизита = текОтбор.Ключ;
		Значение = текОтбор.Значение;
		
		Если текОтбор.Ключ = "ПериодВыборки" Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, 
			ИмяРеквизита, 
			Значение, 
			ВидСравненияКомпоновкиДанных.Равно,
			, 
			ЗначениеЗаполнено(Значение));
		
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыборкиПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("ПериодВыборки", ПериодВыборки));	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("Организация", ОтборОрганизация));	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеКомпанииПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("ПодразделениеКомпании", ОтборПодразделениеКомпании));	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("Контрагент", ОтборКонтрагент));	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("Автор", ОтборАвтор));
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	тПериодВыборки 	= Настройки.Получить("ПериодВыборки");
	тОрганизация 	= Настройки.Получить("ОтборОрганизация");
	тПодразделение 	= Настройки.Получить("ОтборПодразделениеКомпании");
	тКонтрагент		= Настройки.Получить("ОтборКонтрагент");
	тАвтор			= Настройки.Получить("ОтборАвтор");
	
	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(тПериодВыборки) Тогда 
		СтруктураОтбора.Вставить("ПериодВыборки", тПериодВыборки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(тОрганизация) Тогда 
		СтруктураОтбора.Вставить("Организация", тОрганизация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(тПодразделение) Тогда 
		СтруктураОтбора.Вставить("ПодразделениеКомпании", тПодразделение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(тКонтрагент) Тогда 
		СтруктураОтбора.Вставить("Контрагент", тКонтрагент);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(тАвтор) Тогда 
		СтруктураОтбора.Вставить("Автор", тАвтор);
	КонецЕсли;
	
	УстановитьОтборСписка(Список, СтруктураОтбора);
	
КонецПроцедуры


#КонецОбласти

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура Подключаемый_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиент.ОткрытьОтчетПоПроблемамИзСписка(ЭтотОбъект, "Список", Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
		// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПриПолученииДанныхНаСервере(Настройки, Строки);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

КонецПроцедуры

&НаСервере
Процедура ЗарезервироватьНаСервере()
	
	ИспользоватьОбъединениеРезервированиеЗаказов = ПолучитьФункциональнуюОпцию("ИспользоватьОбъединениеРезервированиеЗаказов");
	
	МассивСтрок = Элементы.Список.ВыделенныеСтроки;
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьОбъединениеРезервированиеЗаказов Тогда
		РезервироватьМассивСписка(МассивСтрок);	
	Иначе
		для Каждого  СтрЗаказ Из МассивСтрок Цикл 
			РезервироватьСтрокуСписка(СтрЗаказ);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарезервировать(Команда)
	ЗарезервироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура РезервироватьМассивСписка(МассивИзСписка)
	
	
	
	//// проверка на отгрузку
	//Запрос = ЗапросНайтиПодчиненныйПоТипу(СтрокаСписка,?(ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(СтрокаСписка.Контрагент),"ПеремещениеТоваров","РеализацияТоваров"));
	//Рез = Запрос.Выполнить();
	//
	//Если Не Рез.Пустой() Тогда
	//	Выборка = Рез.Выбрать();
	//	Выборка.Следующий();
	//	Если Выборка.Проведен Тогда
	//		Возврат;
	//	КонецЕсли;
	//КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка.СкладКомпании КАК СкладКомпании,
	|	ЕСТЬNULL(ДополнительныеСведения.Значение, ЛОЖЬ) КАК Попачка,
	|	ЗаказПокупателя.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказПокупателя.Ссылка.ТочкаДоставки КАК ТочкаДоставки,
	|	ЗаказПокупателя.Ссылка КАК Заказ,
	|	НЕ ТК_ПараметрыОбменаEDI.Контрагент ЕСТЬ NULL КАК EDI
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО ЗаказПокупателя.Ссылка = ДополнительныеСведения.Объект
	|			И (ДополнительныеСведения.Свойство.Имя = ""Попачечно_b9a6d6a63dc74ae1a8f7d6a75ea25654"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ПараметрыОбменаEDI КАК ТК_ПараметрыОбменаEDI
	|		ПО ЗаказПокупателя.Контрагент = ТК_ПараметрыОбменаEDI.Контрагент
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&Ссылка)
	|ИТОГИ ПО
	|	СкладКомпании,
	|	Попачка,
	|	Контрагент,
	|	ТочкаДоставки";
	
	Запрос.УстановитьПараметр("Ссылка", МассивИзСписка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаСкладКомпании = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСкладКомпании.Следующий() Цикл
		// Вставить обработку выборки ВыборкаСкладКомпании
		
		ВыборкаПопачка = ВыборкаСкладКомпании.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПопачка.Следующий() Цикл
			// Вставить обработку выборки ВыборкаПопачка
			
			ВыборкаКонтрагент = ВыборкаПопачка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаКонтрагент.Следующий() Цикл
				// Вставить обработку выборки ВыборкаКонтрагент
				
				ВыборкаТочкаДоставки = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаТочкаДоставки.Следующий() Цикл
					// Вставить обработку выборки ВыборкаТочкаДоставки
					
					ПорцияЗаказов = Новый Массив;
					ВыборкаДетальныеЗаписи = ВыборкаТочкаДоставки.Выбрать();
					
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						// Вставить обработку выборки ВыборкаДетальныеЗаписи
						Если ВыборкаДетальныеЗаписи.EDI Тогда
							СформироватьРезерв(ВыборкаДетальныеЗаписи.Заказ);
						Иначе
							ПорцияЗаказов.Добавить(ВыборкаДетальныеЗаписи.Заказ);
						КонецЕсли;
					КонецЦикла;
					
					Если ПорцияЗаказов.Количество() > 0 Тогда
						СформироватьРезерв(ПорцияЗаказов);
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РезервироватьСтрокуСписка(СтрокаСписка)
	
	
	
	// проверка на отгрузку
	Запрос = ЗапросНайтиПодчиненныйПоТипу(СтрокаСписка,?(ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(СтрокаСписка.Контрагент),"ПеремещениеТоваров","РеализацияТоваров"));
	Рез = Запрос.Выполнить();
	
	Если Не Рез.Пустой() Тогда
		Выборка = Рез.Выбрать();
		Выборка.Следующий();
		Если Выборка.Проведен Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	
	
	
	
	Запрос = ЗапросНайтиПодчиненныйПоТипу(СтрокаСписка,"РезервированиеЗаказовПокупателя");
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		дОбъектРезерв = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
	Иначе
		ВыборкаРезерв = Рез.Выбрать();
		ВыборкаРезерв.Следующий();
		дОбъектРезерв = ВыборкаРезерв.ДокСсылка.ПолучитьОбъект();
		Если ВыборкаРезерв.Проведен Тогда
			дОбъектРезерв.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		ИначеЕсли ВыборкаРезерв.ПометкаУдаления Тогда
			дОбъектРезерв.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;
	КонецЕсли;
	
	дОбъектРезерв.Заполнить(СтрокаСписка);	
	дОбъектРезерв.Дата = ТекущаяДатаСеанса();
	Если дОбъектРезерв.ЭтоНовый() Тогда
		дОбъектРезерв.УстановитьНовыйНомер();
	КонецЕсли;
	
	
	ОбработкаТабличнойЧастиСервер.СортироватьТЧ(дОбъектРезерв.Товары);
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад",дОбъектРезерв.СкладКомпании);
	ЭлементБлокировки.ИсточникДанных  =  дОбъектРезерв.Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура","Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика","ХарактеристикаНоменклатуры");
	Блокировка.Заблокировать();
	
	Документы.РезервированиеЗаказовПокупателя.УдалитьСтрокиБезОстатка(дОбъектРезерв);
	Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(дОбъектРезерв);
	Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(дОбъектРезерв);
	//Проверим квоты после заполнения характеристик, может быть еще уменьшено кол-во
	Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(дОбъектРезерв);
	
	
	Если дОбъектРезерв.Товары.Количество() = 0 Тогда
		ОтменитьТранзакцию();
		Возврат;
	КонецЕсли;		
	
	
	дОбъектРезерв.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный); 
	ОбщегоНазначения.СообщитьПользователю("Создан документ "+дОбъектРезерв.Ссылка);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРезерв(Заказы)
	
	ОбъединениеРезервов = ТипЗнч(Заказы) = Тип("Массив");
	
	РезервыПорцииЗаказов = Новый Массив;
	
	Если ОбъединениеРезервов Тогда
		Для Каждого СтрЗаказ Из Заказы Цикл 
			Запрос = ЗапросНайтиПодчиненныйПоТипу(СтрЗаказ,"РезервированиеЗаказовПокупателя");
			Рез = Запрос.Выполнить();
			Если НЕ Рез.Пустой() Тогда
				ВыборкаРезерв = Рез.Выбрать();
				Пока ВыборкаРезерв.Следующий() Цикл
					РезервыПорцииЗаказов.Добавить(ВыборкаРезерв.ДокСсылка);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Запрос = ЗапросНайтиПодчиненныйПоТипу(Заказы,"РезервированиеЗаказовПокупателя");
		Рез = Запрос.Выполнить();
		Если НЕ Рез.Пустой() Тогда
			ВыборкаРезерв = Рез.Выбрать();
			Пока ВыборкаРезерв.Следующий() Цикл
				РезервыПорцииЗаказов.Добавить(ВыборкаРезерв.ДокСсылка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	КоличествоРезервыПорцииЗаказов = РезервыПорцииЗаказов.Количество();
	Если КоличествоРезервыПорцииЗаказов = 0 Тогда
		дОбъектРезерв = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
	Иначе
		Для Индекс = 0 по РезервыПорцииЗаказов.ВГраница() Цикл 
			Если Индекс = РезервыПорцииЗаказов.ВГраница() Тогда
				дОбъектРезерв = РезервыПорцииЗаказов[Индекс].ПолучитьОбъект();
				Если дОбъектРезерв.Проведен Тогда
					дОбъектРезерв.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				ИначеЕсли дОбъектРезерв.ПометкаУдаления Тогда
					дОбъектРезерв.УстановитьПометкуУдаления(Ложь);
				КонецЕсли;
			Иначе
				Если НЕ РезервыПорцииЗаказов[Индекс].ПометкаУдаления Тогда
					дОбъектРезерв = РезервыПорцииЗаказов[Индекс].ПолучитьОбъект();
					дОбъектРезерв.УстановитьПометкуУдаления(Истина);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	дОбъектРезерв.Заполнить(Заказы);	
	дОбъектРезерв.Дата = ТекущаяДатаСеанса();
	Если дОбъектРезерв.ЭтоНовый() Тогда
		дОбъектРезерв.УстановитьНовыйНомер();
	КонецЕсли;
	
	ОбработкаТабличнойЧастиСервер.СортироватьТЧ(дОбъектРезерв.Товары);
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Склад",дОбъектРезерв.СкладКомпании);
	ЭлементБлокировки.ИсточникДанных  =  дОбъектРезерв.Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура","Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика","ХарактеристикаНоменклатуры");
	Блокировка.Заблокировать();
	
	Документы.РезервированиеЗаказовПокупателя.УдалитьСтрокиБезОстатка(дОбъектРезерв);
	Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(дОбъектРезерв);
	Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(дОбъектРезерв);
	//Проверим квоты после заполнения характеристик, может быть еще уменьшено кол-во
	Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(дОбъектРезерв);
	
	Если дОбъектРезерв.Товары.Количество() = 0 Тогда
		ОтменитьТранзакцию();
		Возврат;
	КонецЕсли;		
	
	//тзЗаказыПокупателей = дОбъектРезерв.Товары.Выгрузить(,"ЗаказПокупателя");//.Скопировать(,"ЗаказПокупателя");
	//тзЗаказыПокупателей.Свернуть("ЗаказПокупателя");
	//Для Каждого СтрЗаказ Из тзЗаказыПокупателей Цикл
	//	стр = дОбъектРезерв.РаспределениеОтгрузки.Добавить();
	//	ЗаполнитьЗначенияСвойств(стр,СтрЗаказ);
	//КонецЦикла;
	
	дОбъектРезерв.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный);
	ОбщегоНазначения.СообщитьПользователю("Создан документ "+дОбъектРезерв.Ссылка);
	
	
	ЗафиксироватьТранзакцию();
	
	
	
	
	
КонецПроцедуры

&НаСервере
Функция ЗапросНайтиПодчиненныйПоТипу(Знач ДокСсылка,Знач ТипДокумента)
	Запрос = Новый Запрос;
	Запрос.Текст =     "ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК ДокСсылка,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").Проведен
	|	КОНЕЦ КАК Проведен
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Док) КАК СвязанныеДокументы
	|ГДЕ
	|	СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"";
	Запрос.УстановитьПараметр("Док",ДокСсылка);
	
	Возврат Запрос;
	
КонецФункции


// Конец СтандартныеПодсистемы.КонтрольВеденияУчета



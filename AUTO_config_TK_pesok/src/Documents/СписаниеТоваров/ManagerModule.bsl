#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровСписаниеТоваров(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);

	Иначе
		
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		//ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваров(Запрос, ДополнительныеСвойства);		
		//ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВозвратПоставщику(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровСписаниеТоваров(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеТоваров.Дата КАК Период,
	               |	СписаниеТоваров.Ссылка КАК Ссылка,
	               |	СписаниеТоваров.ХозОперация КАК ХозОперация,
	               |	СписаниеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	СписаниеТоваров.Организация КАК Организация,
	               |	СписаниеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	СписаниеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	СписаниеТоваров.СкладКомпании КАК СкладКомпании,
	               |	СписаниеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	СписаниеТоваров.КурсДокумента КАК КурсДокумента,
	               |	СписаниеТоваров.МоментВремени КАК МоментВремени,
	               |	СписаниеТоваров.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	               |	ВЫБОР
	               |		КОГДА СписаниеТоваров.СкладКомпании = ЗНАЧЕНИЕ(Справочник.СкладыКомпании.ПустаяСсылка)
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ СписаниеТоваров.СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(Справочник.ВидыСкладов.ТНП)
	               |	КОНЕЦ КАК ОтправительТНП
	               |ИЗ
	               |	Документ.СписаниеТоваров КАК СписаниеТоваров
	               |ГДЕ
	               |	СписаниеТоваров.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени",Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
		
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента", Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости",Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПолучательТНП",Ложь);
	ДополнительныеСвойства.ДляПроведения.Вставить("ОтправительТНП",Реквизиты.ОтправительТНП);

КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СписаниеТоваровТовары.Ссылка КАК Ссылка,
	               |	СписаниеТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
	               |	СписаниеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СписаниеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	СписаниеТоваровТовары.Коэффициент КАК Коэффициент,
	               |	СписаниеТоваровТовары.КоличествоБазовое КАК Количество,
	               |	СписаниеТоваровТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	СписаниеТоваровТовары.Партия КАК Партия,
	               |	СписаниеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ЛОЖЬ КАК Возврат,
	               |	СписаниеТоваровТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
	               |ГДЕ
	               |	СписаниеТоваровТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	&СкладКомпании КАК СкладКомпании,
				  |	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				  |	ВтТаблицаТовары.КоличествоБазовое КАК Количество,
	              |	&ХозОперация КАК ХозОперация
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти
	

#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = ДополнительныеСвойства.ДляПроведения.СкладКомпании;
	НоваяСтрока.Период   	  = ДополнительныеСвойства.ДляПроведения.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт списания товаров
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.СписаниеТоваров";
    КомандаПечати.Идентификатор = "АктСписанияТоваров";
    КомандаПечати.Представление = НСтр("ru = 'Акт списания товаров'; uk = 'Акт списання товарів'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Акт списания товаров (Розница)
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.СписаниеТоваров";
    КомандаПечати.Идентификатор = "АктСписанияТоваровРозница";
    КомандаПечати.Представление = НСтр("ru = 'Акт списания товаров (Розница)'; uk = 'Акт списання товарів (Роздріб)'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
		
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

Процедура ЗаполнитьХарактеристикиПоОстаткамПартий(ДокументОбъект) Экспорт 
	
	Если Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ДокументОбъект.СкладКомпании, ДокументОбъект.Дата) Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблЧастьТовары = ДокументОбъект.Товары;
	
	Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("МоментВремени", ДокументОбъект.МоментВремени());
	Запрос.УстановитьПараметр("Склад", ДокументОбъект.СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("тчТовары", ТаблЧастьТовары.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	тчТовары.НомерСтроки КАК НомерСтроки,
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	тчТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	тчТовары.Коэффициент КАК Коэффициент,
	               |	тчТовары.Количество КАК Количество
	               |ПОМЕСТИТЬ вт_Товары
	               |ИЗ
	               |	&тчТовары КАК тчТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МИНИМУМ(вт_Товары.НомерСтроки) КАК нСтр,
	               |	вт_Товары.Номенклатура КАК Номенклатура,
	               |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
	               |	вт_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	вт_Товары.Коэффициент КАК Коэффициент,
	               |	СУММА(вт_Товары.Количество) КАК Количество,
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ) КАК ХарактеристикиИспользуются
	               |ПОМЕСТИТЬ сверн_Товары
	               |ИЗ
	               |	вт_Товары КАК вт_Товары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт_Товары.Номенклатура,
	               |	вт_Товары.ЕдиницаИзмерения,
	               |	вт_Товары.Коэффициент,
	               |	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры.Сортировка КАК Сортировка,
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
	               |ПОМЕСТИТЬ вт_Остатки
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	               |			,
	               |			СкладКомпании = &Склад
	               |				И Номенклатура В
	               |					(ВЫБРАТЬ
	               |						сверн_Товары.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						сверн_Товары КАК сверн_Товары
	               |					ГДЕ
	               |						сверн_Товары.ХарактеристикиИспользуются)) КАК ОстаткиТоваровКомпанииОстатки
	               |ГДЕ
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт_Остатки.Номенклатура КАК Номенклатура,
	               |	вт_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	вт_Остатки.КоличествоОстаток КАК КолВоСвободно
	               |ИЗ
	               |	вт_Остатки КАК вт_Остатки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	вт_Остатки.Номенклатура,
	               |	вт_Остатки.Сортировка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	сверн_Товары.нСтр КАК нСтр,
	               |	сверн_Товары.Номенклатура КАК Номенклатура,
	               |	сверн_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	сверн_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	сверн_Товары.Коэффициент КАК Коэффициент,
	               |	сверн_Товары.Количество КАК Количество,
	               |	сверн_Товары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
	               |ИЗ
	               |	сверн_Товары КАК сверн_Товары
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	нСтр";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	тзХарактеристики = МассивРезультатов[3].Выгрузить();
	тзХарактеристики.Индексы.Добавить("Номенклатура");
	
	тзТовары = МассивРезультатов[4].Выгрузить();
	
	ТаблЧастьТовары.Очистить();
	
	Для Каждого стрТовары Из тзТовары Цикл 
		текКоэффициент = ?(стрТовары.Коэффициент = 0, 1, стрТовары.Коэффициент);
		КолВоСписатьБаз = стрТовары.Количество * текКоэффициент;
		
		нСтроки = тзХарактеристики.НайтиСтроки(Новый Структура("Номенклатура", стрТовары.Номенклатура));	
		Для Каждого стрХарактеристика Из нСтроки Цикл 
			текСвободно = Цел(стрХарактеристика.КолВоСвободно / текКоэффициент) * текКоэффициент;
			Если текСвободно < 1 Тогда 
				Продолжить;
			КонецЕсли;
			
			КолВоСписать = Цел(Мин(КолВоСписатьБаз, текСвободно)/текКоэффициент);
			Если КолВоСписать > 0 Тогда 
				НоваяСтрока = ТаблЧастьТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
				НоваяСтрока.ХарактеристикаНоменклатуры = стрХарактеристика.ХарактеристикаНоменклатуры;
				НоваяСтрока.Количество = КолВоСписать;
				
				КолВоСписатьБаз = КолВоСписатьБаз - КолВоСписать * текКоэффициент;
				стрХарактеристика.КолВоСвободно = текСвободно - КолВоСписать * текКоэффициент;
			КонецЕсли;
			
			Если КолВоСписатьБаз < 1 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;			
		
		Если КолВоСписатьБаз > 0 Тогда 
			КолВоСписать = Цел(КолВоСписатьБаз/текКоэффициент);
			Если КолВоСписать > 0 Тогда 
				НоваяСтрока = ТаблЧастьТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
				НоваяСтрока.Количество = КолВоСписать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоОстаткамНаСкладе(ДокументОбъект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	тчТовары = ДокументОбъект.Товары;
	тчТовары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Склад", ДокументОбъект.СкладКомпании);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоБазовое,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) > 0
	               |			ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	               |		ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
	               |	КОНЕЦ КАК Количество
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(&Дата, СкладКомпании = &Склад) КАК ОстаткиТоваровКомпанииОстатки
	               |ГДЕ
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Номенклатура ИЕРАРХИЯ,
	               |	ХарактеристикаНоменклатуры";
	
	тчТовары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Акт списания товаров
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСписанияТоваров") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"АктСписанияТоваров",
		НСтр("ru = 'Акт списания товаров'; uk = 'Акт списання товарів'"), 
		ПечатьАктСписанияТоваров(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.СписаниеТоваров.ПФ_MXL_АктСписанияТоваров");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктСписанияТоваровРозница") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"АктСписанияТоваровРозница",
		НСтр("ru = 'Акт списания товаров (Розница)'; uk = 'Акт списання товарів (Роздріб)'"), 
		ПечатьАктСписанияТоваровРозница(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.СписаниеТоваров.ПФ_MXL_АктСписанияТоваров");
		
	КонецЕсли;	

	
КонецПроцедуры

Функция ПечатьАктСписанияТоваров(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Регистратор КАК Регистратор,
	               |	ВложенныйЗапрос.Номенклатура.Артикул КАК Код,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК Характеристика,
	               |	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВложенныйЗапрос.Количество КАК Количество,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Количество <> 0
	               |			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 3))
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЦенаРозничная,
	               |	ВложенныйЗапрос.Сумма КАК Сумма
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ПартииТоваровКомпании.Регистратор КАК Регистратор,
	               |		ПартииТоваровКомпании.Регистратор.МоментВремени КАК МоментВремени,
	               |		МИНИМУМ(ПартииТоваровКомпании.НомерСтроки) КАК НомерСтроки,
	               |		ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	               |		ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ПартииТоваровКомпании.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		СУММА(ВЫБОР
	               |				КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |					ТОГДА ПартииТоваровКомпании.Количество
	               |				ИНАЧЕ ПартииТоваровКомпании.Количество * -1
	               |			КОНЕЦ) КАК Количество,
	               |		СУММА(ВЫБОР
	               |				КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |					ТОГДА ПартииТоваровКомпании.Сумма
	               |				ИНАЧЕ ПартииТоваровКомпании.Сумма * -1
	               |			КОНЕЦ) КАК Сумма
	               |	ИЗ
	               |		РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	               |	ГДЕ
	               |		ПартииТоваровКомпании.Регистратор В(&МассивСсылок)
	               |		И ПартииТоваровКомпании.Активность
	               |		И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ПартииТоваровКомпании.Регистратор,
	               |		ПартииТоваровКомпании.Номенклатура,
	               |		ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	               |		ПартииТоваровКомпании.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |		ПартииТоваровКомпании.Регистратор.МоментВремени) КАК ВложенныйЗапрос
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВложенныйЗапрос.МоментВремени,
	               |	ВложенныйЗапрос.НомерСтроки
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписаниеТоваров.Ссылка КАК Ссылка,
	               |	СписаниеТоваров.Дата КАК Дата,
	               |	СписаниеТоваров.Номер КАК Номер,
	               |	СписаниеТоваров.Организация КАК Организация,
	               |	СписаниеТоваров.Организация.НаименованиеПолное КАК ОрганизацияПредставление,
	               |	СписаниеТоваров.ТорговаяПлощадка КАК ПодразделениеКомпании,
	               |	СписаниеТоваров.СкладКомпании КАК СкладКомпании,
	               |	СписаниеТоваров.ВалютаДокумента КАК ВалютаДокумента
	               |ИЗ
	               |	Документ.СписаниеТоваров КАК СписаниеТоваров
	               |ГДЕ
	               |	СписаниеТоваров.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументов = МассивРезультатов[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ТаблицаДанныеШапки = МассивРезультатов[1].Выгрузить();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктСписанияТоваров";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеТоваров.ПФ_MXL_АктСписанияТоваров");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьРеквизиты 		= Макет.ПолучитьОбласть("РеквизитыОрганизации");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл		
		текДокумент = ВыборкаДокументов.Регистратор;
		текСумма 	= ВыборкаДокументов.Сумма;
		
		МассивРеквизитов = ТаблицаДанныеШапки.НайтиСтроки(Новый Структура("Ссылка", текДокумент));
		Если МассивРеквизитов.Количество() > 0 Тогда 
			текРеквизитыШапки = МассивРеквизитов[0];
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Ошибка печати документа <" + текДокумент + ">! Обратитесь к разработчику...");
			Продолжить;
		КонецЕсли;
								
		СтруктураШапки = Новый Структура("Дата, Номер, Организация, ОрганизацияПредставление, ПодразделениеКомпании, СкладКомпании");		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, текРеквизитыШапки);
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Списание товаров №%1 от %2'; uk = 'Списання товарів №%1 от %2'", КодЯзыкаПечать),
							СокрЛП(СтруктураШапки.Номер),
							Формат(СтруктураШапки.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
							
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);			
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьРеквизиты.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьРеквизиты);
		                              		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;
				
		ВыборкаТовары = ВыборкаДокументов.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Код, Количество, ЕдиницаИзмерения, ЦенаРозничная, Сумма");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			НоменклатураПредставление = ?(Не ПустаяСтрока(ВыборкаТовары.НаименованиеПолное), ВыборкаТовары.НаименованиеПолное, Строка(ВыборкаТовары.Номенклатура));			
			СтруктураСтроки.Вставить("НоменклатураПредставление", НоменклатураПредставление + ?(ЗначениеЗаполнено(ВыборкаТовары.Характеристика), ", " + СокрЛП(ВыборкаТовары.Характеристика), ""));
						
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ИтогоКоличество", Формат(Количество, "ЧДЦ=2; ЧН="));
		СтруктураПодвала.Вставить("Всего", Формат(текСумма, "ЧДЦ=2; ЧН="));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(текСумма, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		ИтоговаяСтрока = НСтр("ru = 'Всего наименований %нпп% на сумму %сумма%'; uk = 'Всього найменувань %нпп% на суму %сумма%'");
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%нпп%", Строка(нпп));
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%сумма%", Формат(текСумма, "ЧДЦ=2; ЧН=") + " " + СокрЛП(текРеквизитыШапки.ВалютаДокумента));
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		

		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, текДокумент);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции

Функция ПечатьАктСписанияТоваровРозница(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Регистратор КАК Регистратор,
	               |	ВложенныйЗапрос.Номенклатура.Артикул КАК Код,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК Характеристика,
	               |	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВложенныйЗапрос.Количество КАК Количество,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Количество <> 0
	               |			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 3))
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЦенаРозничная,
	               |	ВложенныйЗапрос.Сумма КАК Сумма
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ПартииТоваровКомпании.Регистратор КАК Регистратор,
	               |		ПартииТоваровКомпании.Регистратор.МоментВремени КАК МоментВремени,
	               |		МИНИМУМ(ПартииТоваровКомпании.НомерСтроки) КАК НомерСтроки,
	               |		ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	               |		ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ПартииТоваровКомпании.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		СУММА(ВЫБОР
	               |				КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |					ТОГДА ПартииТоваровКомпании.Количество
	               |				ИНАЧЕ ПартииТоваровКомпании.Количество * -1
	               |			КОНЕЦ) КАК Количество,
	               |		СУММА(ВЫБОР
	               |				КОГДА ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |					ТОГДА ПартииТоваровКомпании.Сумма
	               |				ИНАЧЕ ПартииТоваровКомпании.Сумма * -1
	               |			КОНЕЦ) КАК Сумма
	               |	ИЗ
	               |		РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	               |	ГДЕ
	               |		ПартииТоваровКомпании.Регистратор В(&МассивСсылок)
	               |		И ПартииТоваровКомпании.Активность
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ПартииТоваровКомпании.Регистратор,
	               |		ПартииТоваровКомпании.Номенклатура,
	               |		ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	               |		ПартииТоваровКомпании.Номенклатура.ОсновнаяЕдиницаИзмерения,
	               |		ПартииТоваровКомпании.Регистратор.МоментВремени) КАК ВложенныйЗапрос
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВложенныйЗапрос.МоментВремени,
	               |	ВложенныйЗапрос.НомерСтроки
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СписаниеТоваров.Ссылка КАК Ссылка,
	               |	СписаниеТоваров.Дата КАК Дата,
	               |	СписаниеТоваров.Номер КАК Номер,
	               |	СписаниеТоваров.Организация КАК Организация,
	               |	СписаниеТоваров.Организация.НаименованиеПолное КАК ОрганизацияПредставление,
	               |	СписаниеТоваров.ТорговаяПлощадка КАК ПодразделениеКомпании,
	               |	СписаниеТоваров.СкладКомпании КАК СкладКомпании,
	               |	СписаниеТоваров.ВалютаДокумента КАК ВалютаДокумента
	               |ИЗ
	               |	Документ.СписаниеТоваров КАК СписаниеТоваров
	               |ГДЕ
	               |	СписаниеТоваров.Ссылка В(&МассивСсылок)";
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеТоваров.Ссылка КАК Ссылка,
	               |	СписаниеТоваров.Дата КАК Дата,
	               |	СписаниеТоваров.Номер КАК Номер,
	               |	СписаниеТоваров.Организация КАК Организация,
	               |	СписаниеТоваров.ТорговаяПлощадка КАК ПодразделениеКомпании,
	               |	СписаниеТоваров.СкладКомпании КАК СкладКомпании,
	               |	СписаниеТоваров.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	               |	СписаниеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	СписаниеТоваров.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Код,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	               |		ХарактеристикаНоменклатуры КАК Характеристика,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое
	               |	) КАК Товары,
	               |	СписаниеТоваров.МоментВремени КАК МоментВремени
	               |ИЗ
	               |	Документ.СписаниеТоваров КАК СписаниеТоваров
	               |ГДЕ
	               |	СписаниеТоваров.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать(); 
	
	ЗапросЦены = Новый Запрос;
	ЗапросЦены.Текст = "ВЫБРАТЬ
	                   |	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	                   |	ЦеныСрезПоследних.Характеристика КАК Характеристика,
	                   |	ЦеныСрезПоследних.Цена КАК Цена
	                   |ИЗ
	                   |	РегистрСведений.Цены.СрезПоследних(
	                   |			&Момент,
	                   |			ТипЦен = &ТипЦен
	                   |				И (Номенклатура, Характеристика) В (&ТаблицаНоменклатуры)) КАК ЦеныСрезПоследних";

		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктСписанияТоваровРозница";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеТоваров.ПФ_MXL_АктСписанияТоваров");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьРеквизиты 		= Макет.ПолучитьОбласть("РеквизитыОрганизации");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл											
		СтруктураШапки = Новый Структура("Дата, Номер, Организация, ПодразделениеКомпании, СкладКомпании");		
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		СтруктураШапки.Вставить("ОрганизацияПредставление", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Истина, Истина, Истина,,,, КодЯзыкаПечать));
			
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Списание товаров №%1 от %2'; uk = 'Списання товарів №%1 от %2'", КодЯзыкаПечать),
							СокрЛП(СтруктураШапки.Номер),
							Формат(СтруктураШапки.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
							
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);			
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьРеквизиты.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьРеквизиты);
		                              		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		ТаблицаТовары = ВыборкаДокументов.Товары.Выгрузить();
		
		ТаблицаНоменклатуры = ТаблицаТовары.Скопировать();
		ТаблицаНоменклатуры.Свернуть("Номенклатура, Характеристика");
		
		ЗапросЦены.УстановитьПараметр("Момент", ВыборкаДокументов.МоментВремени);
		ЗапросЦены.УстановитьПараметр("ТипЦен", ВыборкаДокументов.ТипЦен);
		ЗапросЦены.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
		
		ТаблицаЦен = ЗапросЦены.Выполнить().Выгрузить();
		ТаблицаЦен.Индексы.Добавить("Номенклатура, Характеристика");
		
		нпп 			= 0;
		Количество 		= 0;
		СуммаВсего		= 0;		
				
		Для Каждого ВыборкаТовары Из ТаблицаТовары Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Код, Количество, ЕдиницаИзмерения");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			НоменклатураПредставление = ?(Не ПустаяСтрока(ВыборкаТовары.НаименованиеПолное), ВыборкаТовары.НаименованиеПолное, Строка(ВыборкаТовары.Номенклатура));			
			СтруктураСтроки.Вставить("НоменклатураПредставление", НоменклатураПредставление + ?(ЗначениеЗаполнено(ВыборкаТовары.Характеристика), ", " + СокрЛП(ВыборкаТовары.Характеристика), ""));			
			
			СтрокиЦены = ТаблицаЦен.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", ВыборкаТовары.Номенклатура, ВыборкаТовары.Характеристика));			
			Если СтрокиЦены.Количество() > 0 Тогда 
				Цена = СтрокиЦены[0].Цена;
			Иначе
				Цена = 0;
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ЦенаРозничная", Цена);
			текСумма = ВыборкаТовары.КоличествоБазовое * Цена;
			
			СтруктураСтроки.Вставить("Сумма", текСумма);
			
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаВсего = СуммаВсего + текСумма;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ИтогоКоличество", Формат(Количество, "ЧДЦ=2; ЧН="));
		СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧДЦ=2; ЧН="));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		ИтоговаяСтрока = НСтр("ru = 'Всего наименований %нпп% на сумму %сумма%'; uk = 'Всього найменувань %нпп% на суму %сумма%'");
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%нпп%", Строка(нпп));
		ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%сумма%", Формат(СуммаВсего, "ЧДЦ=2; ЧН=") + " " + СокрЛП(ВыборкаДокументов.ВалютаДокумента));
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		

		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции


#КонецОбласти	

	
	
#КонецЕсли
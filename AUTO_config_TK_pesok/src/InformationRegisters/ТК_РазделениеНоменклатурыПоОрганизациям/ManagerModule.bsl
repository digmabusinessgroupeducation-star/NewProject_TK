
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем


#КонецОбласти


Функция ПолучитьОрганизациюСпискаНоменклатуры(ОрганизацияПоУмолчанию, СписокНоменклатуры,ТорговаяПлощадка = Неопределено)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат	= Новый Соответствие;
	Если СписокНоменклатуры.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	
	Для Каждого Номенклатура Из СписокНоменклатуры Цикл
		Если Номенклатура = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("Организация",ОрганизацияПоУмолчанию);
		СтруктураВозврата.Вставить("Номенклатура",Неопределено);
		СтруктураВозврата.Вставить("ТорговаяПлощадка",Неопределено);
		

		Результат.Вставить(Номенклатура, СтруктураВозврата);
	КонецЦикла;
	
	МассивТорговаяПлощадка = Новый Массив();
	МассивТорговаяПлощадка.Добавить(Справочники.ТорговыеПлощадки.ПустаяСсылка());

	Если ЗначениеЗаполнено(ТорговаяПлощадка) Тогда
		МассивТорговаяПлощадка.Добавить(ТорговаяПлощадка);
	КонецЕсли;
		
	СоответствиеЭлементовИГрупп = ОбщегоНазначенияТКВызовСервера.ПолучитьСписокВышеСтоящихГруппЭлементов(СписокНоменклатуры);
	
	ТаблицаИерархии = Новый ТаблицаЗначений;
	ТаблицаИерархии.Колонки.Добавить("Элемент",		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаИерархии.Колонки.Добавить("Родитель",	Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаИерархии.Колонки.Добавить("Уровень",		ОбщегоНазначенияТК.ПолучитьОписаниеТиповЧисла(10, 0));
	
	Для каждого Номенклатура Из СписокНоменклатуры Цикл
		
		НоваяСтрока = ТаблицаИерархии.Добавить();
		НоваяСтрока.Элемент		= Номенклатура;
		НоваяСтрока.Родитель	= Номенклатура;
		
		СписокГрупп = СоответствиеЭлементовИГрупп.Получить(Номенклатура);
		Если СписокГрупп = Неопределено Тогда
			НоваяСтрока.Уровень = 1;
			Продолжить;
		КонецЕсли;
		
		КоличествоВышеСтоящихГрупп = СписокГрупп.Количество();
		
		НоваяСтрока.Уровень = КоличествоВышеСтоящихГрупп + 1;
		
		Для Индекс = 1 По КоличествоВышеСтоящихГрупп Цикл
			НоваяСтрока = ТаблицаИерархии.Добавить();
			НоваяСтрока.Элемент	= Номенклатура;
			НоваяСтрока.Родитель= СписокГрупп[Индекс - 1];
			НоваяСтрока.Уровень	= КоличествоВышеСтоящихГрупп - Индекс + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	НоваяСтрока = ТаблицаИерархии.Добавить();	// Корень
	
	МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИерархии, "Родитель", Истина);

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаИерархии",	ТаблицаИерархии);
	Запрос.УстановитьПараметр("МассивНоменклатуры",	ОбщегоНазначенияТКВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивНоменклатуры));
	Запрос.УстановитьПараметр("МассивТорговаяПлощадка",	МассивТорговаяПлощадка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИерархии.Элемент КАК Элемент,
	|	ТаблицаИерархии.Родитель КАК Родитель,
	|	ТаблицаИерархии.Уровень КАК Уровень
	|ПОМЕСТИТЬ ВТТаблицаИерархии
	|ИЗ
	|	&ТаблицаИерархии КАК ТаблицаИерархии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Родитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИерархии.Элемент КАК Номенклатура,
	|	ТаблицаИерархии.Родитель КАК Родитель,
	|	ТаблицаИерархии.Уровень КАК Уровень,
	|	ТК_РазделениеНоменклатурыПоОрганизациям.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_РазделениеНоменклатурыПоОрганизациям.Номенклатура КАК НоменклатураОрганизация,
	|	ТК_РазделениеНоменклатурыПоОрганизациям.Организация КАК Организация
	|ИЗ
	|	ВТТаблицаИерархии КАК ТаблицаИерархии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТК_РазделениеНоменклатурыПоОрганизациям КАК ТК_РазделениеНоменклатурыПоОрганизациям
	|		ПО ТаблицаИерархии.Родитель = ТК_РазделениеНоменклатурыПоОрганизациям.Номенклатура
	|ГДЕ
	|	ТК_РазделениеНоменклатурыПоОрганизациям.Номенклатура В(&МассивНоменклатуры)
	|	И ТК_РазделениеНоменклатурыПоОрганизациям.ТорговаяПлощадка В(&МассивТорговаяПлощадка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Уровень УБЫВ,
	|	ТорговаяПлощадка УБЫВ";

	ТаблицаОрганизации = Запрос.Выполнить().Выгрузить();
	ТаблицаОрганизации.Индексы.Добавить("Номенклатура");
	
	ОтборНоменклатура = Новый Структура("Номенклатура");
	
	ПустаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();

	Для Каждого Номенклатура Из СписокНоменклатуры Цикл
		СтруктураВозврата = Результат.Получить(Номенклатура);

		 ОтборНоменклатура.Номенклатура = Номенклатура;
		 
		 НайденныеСтроки = ТаблицаОрганизации.НайтиСтроки(ОтборНоменклатура);
		 Если НайденныеСтроки.Количество() > 0 Тогда
			 нСтока = НайденныеСтроки[0];
			 Если ЗначениеЗаполнено(нСтока.Организация)Тогда
				СтруктураВозврата.Организация = нСтока.Организация;  
			 КонецЕсли;
					 
			 ЗаполнитьЗначенияСвойств(СтруктураВозврата, НайденныеСтроки[0],,"Организация");
			 СтруктураВозврата.Номенклатура = нСтока.НоменклатураОрганизация;
		 Иначе
			 
			 ОтборНоменклатура.Номенклатура = ПустаяНоменклатура;
			 
			 НайденныеСтроки = ТаблицаОрганизации.НайтиСтроки(ОтборНоменклатура);
			 Если НайденныеСтроки.Количество() > 0 Тогда
				 
				 нСтока = НайденныеСтроки[0];
				 Если ЗначениеЗаполнено(нСтока.Организация)Тогда
					 СтруктураВозврата.Организация = нСтока.Организация;  
				 КонецЕсли;
								 
				 ЗаполнитьЗначенияСвойств(СтруктураВозврата, нСтока,,"Организация");

				 СтруктураВозврата.Номенклатура = нСтока.НоменклатураОрганизация;
			 КонецЕсли;
			 
		 КонецЕсли;

	КонецЦикла;
	
	Возврат Результат;

	
КонецФункции

#КонецОбласти



#КонецЕсли


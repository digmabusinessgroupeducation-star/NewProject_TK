

Функция СформироватьСтруктуруЗаписиРезультатов() Экспорт
	СтруктураЗаписиРезультатов = Новый Структура("Дата, ИдентификаторОбработки, КлючОбработки, Комментарий, СообщенияОбОшибках, РегистрироватьРезультаты", ТекущаяДата(), "", "", "", "", Истина);
	Возврат СтруктураЗаписиРезультатов;
КонецФункции

Функция ПолучитьВремяВыполнения(ДатаСтарта) Экспорт
	
	Секунд = ТекущаяДата() - ДатаСтарта;
	
	Если Секунд > 0 Тогда
		Возврат "[" + Формат(Дата('00010101000000') + Секунд, "ДФ='mm:ss'") + "]";
	Иначе
		Возврат "[zero]";
	КонецЕсли;
	
КонецФункции

 
Процедура ДобавитьКомментарий(Комментарий, СтруктураЗаписи, СтатусКомментария = Неопределено) Экспорт
	
	Если НЕ СтруктураЗаписи.РегистрироватьРезультаты = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если СтатусКомментария = СтатусСообщения.ОченьВажное Тогда
		ПрефиксКомментария = " !!! ";
	ИначеЕсли СтатусКомментария = СтатусСообщения.Важное Тогда
		ПрефиксКомментария = "  !  ";
	ИначеЕсли СтатусКомментария = СтатусСообщения.Информация Тогда
		ПрефиксКомментария = "  i  ";
	Иначе
		ПрефиксКомментария = "     ";
	КонецЕсли;
	
	ПрефиксКомментарияДатой = "" + Формат(ТекущаяДата(), "ДЛФ=T") + ПрефиксКомментария;
	
	Комментарий = ПрефиксКомментарияДатой + Комментарий;
	Комментарий = СтрЗаменить(Комментарий, Символы.ПС, Символы.ПС + ПрефиксКомментарияДатой);
	СтруктураЗаписи.Комментарий = СтруктураЗаписи.Комментарий + Комментарий + Символы.ПС;
	
КонецПроцедуры


Процедура ЗаписатьЖурналВыполненияОбработок(СтруктураЗаписи, Успешный) Экспорт
	
	Если НЕ СтруктураЗаписи.РегистрироватьРезультаты = Истина Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ТК_РезультатыВыполненияОбмена.СоздатьНаборЗаписей();
//	НаборЗаписей.Отбор.Дата.Установить(СтруктураЗаписи.Дата);
	НаборЗаписей.Отбор.ИдентификаторОбработки.Установить(СтруктураЗаписи.ИдентификаторОбработки);
	НаборЗаписей.Отбор.КлючОбработки.Установить(СтруктураЗаписи.КлючОбработки);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, СтруктураЗаписи);
	Запись.Успешный = Успешный;
	
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура СообщитьОбОшибке(СообщениеОбОшибке, СтруктураЗаписи, СтатусСообщенияОбОшибке = Неопределено) Экспорт
	
	Если НЕ СтруктураЗаписи.РегистрироватьРезультаты = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьКомментарий(СообщениеОбОшибке, СтруктураЗаписи, СтатусСообщенияОбОшибке);
	СтруктураЗаписи.СообщенияОбОшибках = СтруктураЗаписи.СообщенияОбОшибках + СообщениеОбОшибке + Символы.ПС;
	
КонецПроцедуры


Процедура ЗафиксироватьРезультатЖурналеВыполненияОбработок(СтруктураЗаписи, Успешный, Отказ = Ложь) Экспорт

	Если НЕ СтруктураЗаписи.РегистрироватьРезультаты = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ЗаписатьЖурналВыполненияОбработок(СтруктураЗаписи, Успешный);
	Исключение
		Отказ = Истина;
		СообщитьОбОшибке(ОписаниеОшибки(), СтруктураЗаписи, СтатусСообщения.ОченьВажное);
	КонецПопытки;

КонецПроцедуры

Процедура ОчиститьУстаревшиеЗаписиРегистра(ВыборкаДетальныеЗаписи)Экспорт
	
	ПараметрыОтбора = ПараметрыОтбораРегистра();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора,ВыборкаДетальныеЗаписи);
		ОчиститьЗаписиРегистра(ПараметрыОтбора);
	КонецЦикла;
	
КонецПроцедуры


Функция ПараметрыОтбораРегистра()
	
	ПоляОтбора = Новый Структура("ИдентификаторОбработки,КлючОбработки");
	
	Возврат ПоляОтбора;
	
КонецФункции

Функция НаборЗаписейРегистраПоПараметрамОтбора(ПараметрыОтбора)
	
	НаборЗаписей = РегистрыСведений.ТК_РезультатыВыполненияОбмена.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	Для Каждого ЭлементОтбора Из НаборЗаписей.Отбор Цикл
		ЗначениеОтбора = Неопределено;
		Если ПараметрыОтбора.Свойство(ЭлементОтбора.Имя, ЗначениеОтбора) Тогда
			ЭлементОтбора.Установить(ЗначениеОтбора);
		КонецЕсли;
	КонецЦикла;	
	
	Возврат НаборЗаписей;
	
КонецФункции

Процедура ОчиститьЗаписиРегистра(ПараметрыОтбора)
	
	НаборЗаписей = НаборЗаписейРегистраПоПараметрамОтбора(ПараметрыОтбора);
	НаборЗаписей.Записать();
	
КонецПроцедуры


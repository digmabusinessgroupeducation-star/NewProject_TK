#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// Функция получает элемент справочника - ключ аналитики учета номенклатураы.
//
// Параметры:
//	ПараметрыАналитики - Выборка или Структура с полями "Статья, Номенклатура, Характеристика".
//
// Возвращаемое значение:
//	СправочникСсылка.ТК_АналитикаУчетаОстаткиТНП - Созданный элемент справочника
//
Функция ЗначениеКлючаАналитики(ПараметрыАналитики) Экспорт

	МенеджерЗаписи = ПолучитьМенеджерЗаписи(ПараметрыАналитики);
	
	Если МенеджерЗаписи <> Неопределено Тогда
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран()
		 И Не ЗначениеЗаполнено(МенеджерЗаписи.КлючАналитики) Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;

		Если МенеджерЗаписи.Выбран() И ЗначениеЗаполнено(МенеджерЗаписи.КлючАналитики) Тогда
			Результат = МенеджерЗаписи.КлючАналитики;
		Иначе
			Результат = СоздатьКлючАналитики(ПараметрыАналитики);
		КонецЕсли;

		Возврат Результат;
	КонецЕсли;

КонецФункции
	
// Функция получает элемент справочника - ключ аналитики учета.
//
// Параметры:
//	ПараметрыАналитики - Выборка или Структура  с полями "Организация, Номенклатура, Характеристика".
//
// Возвращаемое значение:
//	СправочникСсылка.ТК_КлючиАналитикиУчетаНоменклатурыОрганизаций - Созданный элемент справочника
//
Функция СоздатьКлючАналитики(ПараметрыАналитики) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыАналитики.Номенклатура) Тогда
		МенеджерЗаписи = ПолучитьМенеджерЗаписи(ПараметрыАналитики);
		
		Если МенеджерЗаписи <> Неопределено Тогда

			// Создание нового ключа аналитики.
			СправочникОбъект = Справочники.ТК_КлючиАналитикиОстаткиТНП.СоздатьЭлемент();
			СправочникОбъект.Наименование = ПолучитьПолноеНаименованиеКлючаАналитики(МенеджерЗаписи);
			ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыАналитики, "СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры,Статья");
			СправочникОбъект.Записать();

			Результат = СправочникОбъект.Ссылка;

			МенеджерЗаписи.КлючАналитики = Результат;
			МенеджерЗаписи.Записать(Ложь);

			Возврат Результат;
			
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьВКоллекции(Коллекция) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(ТекстЗначенияКлючейАналитикиВКоллекции());
	Запрос.УстановитьПараметр("Коллекция", Коллекция);
	//Запрос.УстановитьПараметр("СкладКомпании",СкладКомпании);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.АналитикаУчетаНоменклатуры) Тогда
			КлючАналитики = ЗначениеКлючаАналитики(Выборка)
		Иначе
			КлючАналитики = Выборка.АналитикаУчетаНоменклатуры;
		КонецЕсли;
		Коллекция[Выборка.Индекс].АналитикаУчетаНоменклатуры = КлючАналитики;
	КонецЦикла;
КонецПроцедуры





Функция ПолучитьПолноеНаименованиеКлючаАналитики(МенеджерЗаписи)

	Возврат СокрЛП(МенеджерЗаписи.Номенклатура) + "; " 
		+ ?(ЗначениеЗаполнено(МенеджерЗаписи.ХарактеристикаНоменклатуры), СокрЛП(МенеджерЗаписи.ХарактеристикаНоменклатуры) + "; ", "")
		+ СокрЛП(МенеджерЗаписи.СкладКомпании)+ "; "
		+ СокрЛП(МенеджерЗаписи.Статья);

КонецФункции


Функция ПолучитьМенеджерЗаписи(ПараметрыАналитики)
	
	// В параметрах аналитики могут быть не все свойства
	СтруктураАналитики = Новый Структура("СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры,Статья");
	ЗаполнитьЗначенияСвойств(СтруктураАналитики, ПараметрыАналитики);
	Если НЕ ЗначениеЗаполнено(СтруктураАналитики.СкладКомпании)
	 И НЕ ЗначениеЗаполнено(СтруктураАналитики.Номенклатура)
	 И НЕ ЗначениеЗаполнено(СтруктураАналитики.ХарактеристикаНоменклатуры)
	  И НЕ ЗначениеЗаполнено(СтруктураАналитики.Статья) Тогда 
		Возврат Неопределено
	Иначе 
		МенеджерЗаписи = РегистрыСведений.ТК_АналитикаУчетаОстаткиТНП.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыАналитики, "СкладКомпании, Номенклатура, ХарактеристикаНоменклатуры,Статья");
		ПроверитьЗаполнениеПоляСкладКомпании(МенеджерЗаписи);
		Возврат МенеджерЗаписи;
	КонецЕсли;
	
КонецФункции

Процедура ПроверитьЗаполнениеПоляСкладКомпании(МенеджерЗаписиАналитикаУчетаНоменклатуры)

	Если МенеджерЗаписиАналитикаУчетаНоменклатуры.СкладКомпании = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Ошибочное значение парамера ""СкладКомпании""';uk='Помилкове значення параметру ""СкладКомпании""'");
	КонецЕсли;

КонецПроцедуры



Функция ТекстЗначенияКлючейАналитикиВКоллекции()
	ТекстЗапроса =     "ВЫБРАТЬ
	|	Коллекция.НомерСтроки - 1 КАК Индекс,
	|	Коллекция.СкладКомпании КАК СкладКомпании,
	|	Коллекция.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Коллекция.Номенклатура КАК Номенклатура,
	|	Коллекция.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Коллекция.Статья КАК Статья
	|ПОМЕСТИТЬ Коллекция
	|ИЗ
	|	&Коллекция КАК Коллекция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Коллекция.Индекс КАК Индекс,
	|	Коллекция.СкладКомпании КАК СкладКомпании,
	|	Коллекция.Номенклатура КАК Номенклатура,
	|	Коллекция.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Коллекция.Статья КАК Статья,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	Коллекция КАК Коллекция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_АналитикаУчетаОстаткиТНП КАК Аналитика
	|		ПО (Аналитика.Номенклатура = Коллекция.Номенклатура)
	|			И (Аналитика.ХарактеристикаНоменклатуры = Коллекция.ХарактеристикаНоменклатуры)
	|			И (Аналитика.СкладКомпании = Коллекция.СкладКомпании)
	|			И (Аналитика.Статья = Коллекция.Статья)
	|ГДЕ
	|	(Аналитика.КлючАналитики ЕСТЬ NULL
	|			ИЛИ Аналитика.КлючАналитики <> Коллекция.АналитикаУчетаНоменклатуры
	|			ИЛИ Аналитика.КлючАналитики = ЗНАЧЕНИЕ(Справочник.ТК_КлючиАналитикиОстаткиТНП.ПустаяСсылка))";
	
	
	
	Возврат ТекстЗапроса;
КонецФункции




	
	#КонецОбласти

	
	
	
	
	
#КонецЕсли
Процедура ОчиститьКонтекстСеанса(ИдентификаторЧата) Экспорт
	
	НаборЗаписей = РегистрыСведений.ТелеграмКонтекстСеансов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЧата.Установить(ИдентификаторЧата);
	НаборЗаписей.Записать();
	
КонецПроцедуры


Процедура ИзменитьПараметрКонтекстаСеанса(ИдентификаторЧата, Параметр, Действие, Значение) Экспорт
	
	НаборЗаписей = РегистрыСведений.ТелеграмКонтекстСеансов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЧата.Установить(ИдентификаторЧата);
	НаборЗаписей.Отбор.Параметр.Установить(Параметр);
	Если Действие = Перечисления.ТелеграмДействияСКонтекстом.Установить Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдентификаторЧата = ИдентификаторЧата;
		НоваяЗапись.Параметр = Параметр;
		НоваяЗапись.Значение = Значение;
	ИначеЕсли Действие = Перечисления.ТелеграмДействияСКонтекстом.ОчиститьЗначение Тогда
		НаборЗаписей.Очистить();
	КонецЕсли;
	НаборЗаписей.Записать();
	
КонецПроцедуры


Функция ПолучитьКонтекстСеанса(ИдентификаторЧата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЧата", ИдентификаторЧата);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Параметр КАК Параметр,
	|	ТелеграмКонтекстСеансов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.ИдентификаторЧата = &ИдентификаторЧата";
	ТаблицаКонтекста = Запрос.Выполнить().Выгрузить();
	Возврат ТелеграмСервер.ПреобразоватьТаблицуКонтекстаВСоответствие(ТаблицаКонтекста);
	
КонецФункции


Функция ПолучитьЗначение(ИдентификаторЧата, ИмяПараметра) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЧата", ИдентификаторЧата);
	Запрос.УстановитьПараметр("ИмяПараметра", ИмяПараметра);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.ИдентификаторЧата = &ИдентификаторЧата
	|	И ТелеграмКонтекстСеансов.Параметр.Наименование = &ИмяПараметра";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Значение;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗначениеПоПараметру(ИдентификаторЧата, Параметр) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЧата", ИдентификаторЧата);
	Запрос.УстановитьПараметр("Параметр", Параметр);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТелеграмКонтекстСеансов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
	|ГДЕ
	|	ТелеграмКонтекстСеансов.ИдентификаторЧата = &ИдентификаторЧата
	|	И ТелеграмКонтекстСеансов.Параметр = &Параметр";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Значение;
	КонецЕсли;
	
КонецФункции



Функция УстановитьЗначение(ИдентификаторЧата, ИмяПараметра, Значение) Экспорт
	
	Параметр = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.НайтиПоНаименованию(ИмяПараметра, Истина);
	Если Параметр = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПустаяСсылка() Тогда
		ТекстИсключения = СтрШаблон("При установке контекста сеанса не удалось найти параметр контекста %1", ИмяПараметра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ТелеграмКонтекстСеансов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторЧата = ИдентификаторЧата;
	МенеджерЗаписи.Параметр = Параметр;
	МенеджерЗаписи.Значение = Значение;
	МенеджерЗаписи.Записать();
	
КонецФункции

Функция ИД_ЧатаПользователя(Пользователь)Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТелеграмКонтекстСеансов.ИдентификаторЧата КАК ИдентификаторЧата
		|ИЗ
		|	РегистрСведений.ТелеграмКонтекстСеансов КАК ТелеграмКонтекстСеансов
		|ГДЕ
		|	ТелеграмКонтекстСеансов.Параметр = &Параметр
		|	И ТелеграмКонтекстСеансов.Значение = &Значение";
	
	Запрос.УстановитьПараметр("Значение", Пользователь);
	Запрос.УстановитьПараметр("Параметр", ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	
	ВыборкаДетальныеЗаписи.Следующий();	

	Возврат ВыборкаДетальныеЗаписи.ИдентификаторЧата;
	
	
КонецФункции

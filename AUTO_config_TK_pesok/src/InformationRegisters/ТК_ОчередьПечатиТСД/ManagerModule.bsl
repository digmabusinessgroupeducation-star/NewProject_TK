


Функция СформироватьСтруктуруЗаписи() Экспорт
	СтруктураЗаписи = Новый Структура("Дата, ТорговаяПлощадка,СкладКомпании, Макет, Статус, Хранилище, Ошибки, Копий", ТекущаяДатаСеанса(),"", "", "", 0, "", "",1);
	Возврат СтруктураЗаписи;
КонецФункции


Процедура ЗаписатьОчередьПечати(СтруктураЗаписи) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ТК_ОчередьПечатиТСД.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Дата.Установить(СтруктураЗаписи.Дата);
	НаборЗаписей.Отбор.ТорговаяПлощадка.Установить(СтруктураЗаписи.ТорговаяПлощадка);
	
	Если СтруктураЗаписи.Свойство("СкладКомпании") Тогда
		НаборЗаписей.Отбор.СкладКомпании.Установить(СтруктураЗаписи.СкладКомпании);
	КонецЕсли;
	
	НаборЗаписей.Отбор.Макет.Установить(СтруктураЗаписи.Макет);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, СтруктураЗаписи);
	
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры



Функция НаборЗаписейРегистраПоПараметрамОтбора(ПараметрыОтбора)
	
	НаборЗаписей = РегистрыСведений.ТК_ОчередьПечатиТСД.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	Для Каждого ЭлементОтбора Из НаборЗаписей.Отбор Цикл
		ЗначениеОтбора = Неопределено;
		Если ПараметрыОтбора.Свойство(ЭлементОтбора.Имя, ЗначениеОтбора) Тогда
			ЭлементОтбора.Установить(ЗначениеОтбора);
		КонецЕсли;
	КонецЦикла;	
	
	Возврат НаборЗаписей;
	
КонецФункции

Функция ПараметрыОтбораРегистра()
	
	ПоляОтбора = Новый Структура("Дата,ТорговаяПлощадка,СкладКомпании,Макет");
	
	Возврат ПоляОтбора;
	
КонецФункции

Процедура ОчиститьЗаписиРегистра(ПараметрыОтбора)
	
	НаборЗаписей = НаборЗаписейРегистраПоПараметрамОтбора(ПараметрыОтбора);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ОчиститьУстаревшиеЗаписиРегистра(ВыборкаДетальныеЗаписи)Экспорт
	
	ПараметрыОтбора = ПараметрыОтбораРегистра();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора,ВыборкаДетальныеЗаписи);
		ОчиститьЗаписиРегистра(ПараметрыОтбора);
	КонецЦикла;
	
КонецПроцедуры

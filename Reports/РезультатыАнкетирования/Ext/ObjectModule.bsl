
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
	ПолучитьДанныеЗУП = Настройки.ДополнительныеСвойства.КлючВарианта = "БланкНачисленийПоАнкетам";
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);	
	
	НачалоПериода = МакетКомпоновки.ЗначенияПараметров.Найти("НачалоПериода").Значение;
	КонецПериода  = МакетКомпоновки.ЗначенияПараметров.Найти("КонецПериода").Значение;	
	
	тзСотрудники = Новый ТаблицаЗначений;
	тзСотрудники.Колонки.Добавить("КодОбъекта", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	тзСотрудники.Колонки.Добавить("КодСотрудник", ОбщегоНазначения.ОписаниеТипаСтрока(12));
	тзСотрудники.Колонки.Добавить("ДатаПриход", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	тзСотрудники.Колонки.Добавить("ДатаУход", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	тзСотрудники.Колонки.Добавить("Сотрудник", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	тзСотрудники.Колонки.Добавить("ПредставлениеСотрудникЗУП", ОбщегоНазначения.ОписаниеТипаСтрока(80));
	
	Если ПолучитьДанныеЗУП Тогда 
		Результат = Отчеты.РезультатыАнкетирования.ПолучитьWSСсылкуЗарплатаАттика();
		
		Если Результат.Успешно Тогда		
			Ответ = Результат.WS.getRBO(НачалоПериода, КонецПериода);		
			ТаблицаЗначений = Ответ.Получить(); 
			
			Если ТаблицаЗначений.Количество() > 0 Тогда 					
				Для Каждого Стр Из ТаблицаЗначений Цикл 
					НоваяСтрока = тзСотрудники.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "КодОбъекта,КодСотрудник,ДатаПриход,ДатаУход,Сотрудник");
					НоваяСтрока.ПредставлениеСотрудникЗУП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1) %2", СокрЛП(НоваяСтрока.КодСотрудник), СокрЛП(НоваяСтрока.Сотрудник));
				КонецЦикла;
			Иначе
				ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Нет данных по табелям за период с %1 по %2'; uk = 'Немає даних по табелям за період з %1 по %2'"),
						Формат(НачалоПериода, "ДФ=dd.MM.yyyy"),
						Формат(КонецПериода, "ДФ=dd.MM.yyyy")));
			КонецЕсли;
		Иначе
			Сообщение = НСтр("ru = 'Ошибка подключения к базе ""Зарплата Аттика УУ""'; uk = 'Помилка підключення до бази ""Зарплата Аттіка УУ""'");
			Если Результат.Свойство("Сообщение") Тогда 
				Сообщение = Сообщение + Символы.ПС + Результат.Сообщение;
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(Сообщение);
		КонецЕсли;	
	КонецЕсли;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("тзСотрудники", тзСотрудники);
	Запрос.Текст = "ВЫБРАТЬ
	               |	тзСотрудники.КодОбъекта КАК КодОбъекта,
	               |	тзСотрудники.КодСотрудник КАК КодСотрудник,
	               |	тзСотрудники.Сотрудник КАК Сотрудник,
	               |	тзСотрудники.ДатаПриход КАК ДатаПриход,
	               |	тзСотрудники.ДатаУход КАК ДатаУход,
				   |	тзСотрудники.ПредставлениеСотрудникЗУП КАК ПредставлениеСотрудникЗУП
	               |ПОМЕСТИТЬ втДанныеЗУП
	               |ИЗ
	               |	&тзСотрудники КАК тзСотрудники";
	
	Запрос.Выполнить();
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки,,, МенеджерВТ); 
	
	ДокументРезультат.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

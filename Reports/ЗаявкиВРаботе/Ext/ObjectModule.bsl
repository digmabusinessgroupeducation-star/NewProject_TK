
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ПрограммныйИнтерфейс
	
	
	#КонецОбласти
	
	#Область ОбработчикиСобытий
	
	Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		СтандартнаяОбработка = Ложь;
		
		СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
		
		Настройки = КомпоновщикНастроек.ПолучитьНастройки(); 
		
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);		
		
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		Период = МакетКомпоновки.ЗначенияПараметров.Найти("Период").Значение;
		
		ЗаполнитьДанныеИзВнешнегоИсточника(МенеджерВТ, Период.ДатаНачала, Период.ДатаОкончания);
		
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных; 
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки,,, МенеджерВТ); 
		
		ДокументРезультат.Очистить();
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент; 
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат); 
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	#Область СлужебныеПроцедурыИФункции
	
	Процедура ЗаполнитьДанныеИзВнешнегоИсточника(МенеджерВТ, НачалоПериода, КонецПериода)
		
		тзВнешниеДанные = Новый ТаблицаЗначений;
		тзВнешниеДанные.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
		тзВнешниеДанные.Колонки.Добавить("ID", ОбщегоНазначения.ОписаниеТипаСтрока(30));
		тзВнешниеДанные.Колонки.Добавить("СрокВыполнения", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
		тзВнешниеДанные.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10));
		тзВнешниеДанные.Колонки.Добавить("Статус", Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыСервисДеск"));
		тзВнешниеДанные.Колонки.Добавить("Услуга", Новый ОписаниеТипов("СправочникСсылка.УслугиСервисДеск"));
		
		ПараметрыДляЗапроса = ПланыОбмена.ОбменFirestore.НастройкиУзла("DBG");
		
		ЗаполнитьТаблицуДокументами(тзВнешниеДанные, ПараметрыДляЗапроса, НачалоПериода);
		ЗаполнитьТаблицуДокументами(тзВнешниеДанные, ПараметрыДляЗапроса, КонецПериода);
		тзВнешниеДанные.ЗаполнитьЗначения(1, "Количество");
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.УстановитьПараметр("тзВнешниеДанные", тзВнешниеДанные);	
		Запрос.Текст = "ВЫБРАТЬ
		|	тзВнешниеДанные.Период КАК Период,
		|	тзВнешниеДанные.ID КАК ID,
		|	тзВнешниеДанные.СрокВыполнения КАК Срок,
		|	тзВнешниеДанные.Статус КАК Статус,
		|	тзВнешниеДанные.Услуга КАК Услуга,
		|	тзВнешниеДанные.Количество КАК Количество
		|ПОМЕСТИТЬ ВнешниеДанные
		|ИЗ
		|	&тзВнешниеДанные КАК тзВнешниеДанные";
		
		Запрос.Выполнить();
		
	КонецПроцедуры
	
	Функция ЗаполнитьТаблицуДокументами(тзЗаявки, ПараметрыДляЗапроса, НаДату, nextPageToken = "")
		НаДатуВМиллисекундах = (УниверсальноеВремя(НаДату) - Дата(1970,1,1)) * 1000;
		СоединениеFire = Обработки.СервисДеск.СоединениеFire(ПараметрыДляЗапроса.Service_CloudFirestoreAPI);
		АдресРесурса = Обработки.СервисДеск.ПодготовитьАдресРесурса("POST", ПараметрыДляЗапроса.Project_ID, "", , , ":runQuery");
		ТелоЗапроса = Обработки.СервисДеск.ПодготовитьТелоЗапроса(ПараметрыДляЗапроса, СоединениеFire, "ЗаявкиВРаботеНаДату", НаДатуВМиллисекундах);
		
		ОтветHTTP = Обработки.СервисДеск.СоздатьДокумент_POST(ПараметрыДляЗапроса, СоединениеFire, АдресРесурса, ТелоЗапроса);
		Если ОтветHTTP.КодСостояния = 200 Тогда
			JSONСтрока = ОтветHTTP.ПолучитьТелоКакСтроку();
			СтруктураОтвета = Обработки.СервисДеск.Из_JSON(JSONСтрока);
			Для Каждого Документ Из СтруктураОтвета Цикл
				Если Документ.Свойство("document") Тогда
					нСтрока = тзЗаявки.Добавить();
					нСтрока.Период = НаДату;
					Поля = Документ.document.fields;
					нСтрока.ID = ГуидПоля(Документ.document.name);
					нСтрока.СрокВыполнения = МестноеВремя(Дата(1970,1,1) + Поля.datePlaneCompletion.integerValue/1000);
					нСтрока.Статус =  Перечисления.СтатусыСервисДеск[Число(Поля.status.integerValue)];
					нСтрока.Услуга = Справочники.УслугиСервисДеск.ПолучитьСсылку(Новый УникальныйИдентификатор(ГуидПоля(Поля.categoryRef.referenceValue)));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		СоединениеFire = Неопределено;
	КонецФункции
	
	Функция ГуидПоля(name)
		мCтрок = СтрРазделить(name,"/");
		ГуидПоля = мCтрок[мCтрок.Количество()-1];
		Возврат ГуидПоля;
	КонецФункции
	
	
	#КонецОбласти
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

Функция СформироватьОтчет(Волна,Отчет,Запись = Ложь)	Экспорт
	
	Макет = ПолучитьМакет(Отчет);
	
	ХозОперация = Новый СписокЗначений;
	ХозОперация.Добавить(Справочники.ХозОперации.РеализацияТоваровРозница);
	ХозОперация.Добавить(Справочники.ХозОперации.ЗакрытиеСмены);
	ХозОперация.Добавить(Справочники.ХозОперации.ЗакрытиеСменыВозврат);
	
	Если Волна = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПланАктивности.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПланАктивности КАК ПланАктивности
		|ГДЕ
		|	ПланАктивности.Проведен
		|	И ПланАктивности.ХозОперация = &ХозОперация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка УБЫВ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Если Отчет = "Выполнение" Тогда
			Запрос.УстановитьПараметр("ХозОперация", ПредопределенноеЗначение("Справочник.ХозОперации.План"));
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"1","2");
		Иначе
			Запрос.УстановитьПараметр("ХозОперация", ПредопределенноеЗначение("Справочник.ХозОперации.ПланПоАктивности"));
		КонецЕсли;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Волна = ВыборкаДетальныеЗаписи.Ссылка;
			
			Если Волна.Дата >= Дата(2022,1,1) Тогда
				ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоКоду("D0009000");	//	Табачные изделия
			Иначе
				ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоКоду("D0001002");	//	Сигареты
			КонецЕсли;
			
			Если Отчет = "Ленточки" Тогда
				Книга = Отчет_Ленточки(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
			ИначеЕсли Отчет = "Премия" Тогда
				Книга = Отчет_Премия(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
			ИначеЕсли Отчет = "Итого" Тогда
				Книга = Отчет_Итого(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
			ИначеЕсли Отчет = "Выполнение" Тогда
				Книга = Отчет_Выполнение(Волна,Макет,ХозОперация);
			КонецЕсли;
			
			Если Запись Тогда
				Записать(Книга,Отчет,Волна.Активность);	
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если Волна.Дата >= Дата(2022,1,1) Тогда
			ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоКоду("D0009000");	//	Табачные изделия
		Иначе
			ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоКоду("D0001002");	//	Сигареты
		КонецЕсли;
		
		Если Отчет = "Ленточки" Тогда
			Книга = Отчет_Ленточки(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
		ИначеЕсли Отчет = "Премия" Тогда
			Книга = Отчет_Премия(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
		ИначеЕсли Отчет = "Итого" Тогда
			Книга = Отчет_Итого(Волна,Макет,ХозОперация,ГруппаНоменклатуры);
		ИначеЕсли Отчет = "Выполнение" Тогда
			Книга = Отчет_Выполнение(Волна,Макет,ХозОперация);
		КонецЕсли;
		
	КонецЕсли;
	
	
	//Если Запись Тогда
	//	Записать(Книга,Отчет);	
	//КонецЕсли;
	
	Возврат Книга;
	
КонецФункции

Функция Отчет_Ленточки(Волна,Макет,ХозОперация,ГруппаНоменклатуры)
	
	Книга = Новый ПакетОтображаемыхДокументов;
	
	облДанныеСклад = Макет.ПолучитьОбласть("Данные|Склад");
	облДанныеПериод = Макет.ПолучитьОбласть("Данные|Период");
	
	мПериод = Волна.Периоды.НайтиСтроки(Новый Структура("ВидПериода",Перечисления.ВидыПериодаАктивности.Неделя));
	Если мПериод.Количество() = 0 Тогда
		Возврат Книга;
	КонецЕсли;
	
	Для Каждого Марка Из Волна.Товары Цикл
		Если ТипЗнч(Марка.ЦелеваяМарка) = Тип("СправочникСсылка.Номенклатура") Тогда
			Продолжить;;
		КонецЕсли;
		
		ТабДок = Новый ТабличныйДокумент;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПланАктивности.СкладКомпании.Код КАК Код,
		|	ПланАктивности.СкладКомпании КАК СкладКомпании,
		|	ДополнительныеСведения.Значение КАК Куратор,
		|	ПланАктивности.НачалоДействия КАК НачалоДействия,
		|	ПланАктивности.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ПланАктивности.ЦелеваяМарка КАК ЦелеваяМарка,
		|	ПланАктивности.Количество КАК План
		|ИЗ
		|	РегистрСведений.ПланАктивности КАК ПланАктивности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО ПланАктивности.СкладКомпании = ДополнительныеСведения.Объект
		|			И (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
		|ГДЕ
		|	ПланАктивности.Регистратор = &Регистратор
		|	И ПланАктивности.ЦелеваяМарка = &ЦелеваяМарка
		|	И ПланАктивности.ВидПериода = &ВидПериода
		|	И ПланАктивности.Количество <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СкладКомпании
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("ВидПериода", Перечисления.ВидыПериодаАктивности.Неделя);
		Запрос.УстановитьПараметр("Регистратор", Волна);
		Запрос.УстановитьПараметр("ЦелеваяМарка", Марка.ЦелеваяМарка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		План = РезультатЗапроса.Выгрузить();
		
		Склады = План.Скопировать(,"Код,СкладКомпании,Куратор");
		Склады.Свернуть("Код,СкладКомпании,Куратор");
		
		Для Каждого склад Из Склады Цикл
			облДанныеСклад.Параметры.Заполнить(склад);
			облДанныеСклад.Параметры.Марка = Марка.ЦелеваяМарка;
			
			ТабДок.Вывести(облДанныеСклад);
			
			
			Для Каждого Период Из мПериод Цикл
				
				Запрос = Новый Запрос;
				Если ТипЗнч(Марка.ЦелеваяМарка) = Тип("СправочникСсылка.ТоварныеМарки") Тогда
					
					Запрос.Текст = 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ПродажиОбороты.СкладКомпании КАК СкладКомпании,
					|	ПродажиОбороты.Номенклатура.ТоварнаяМарка.Родитель КАК Марка,
					|	СУММА(ПродажиОбороты.КоличествоОборот) КАК Факт
					|ИЗ
					|	РегистрНакопления.Продажи.Обороты(
					|			&ДатаНачала,
					|			&ДатаОкончания,
					|			,
					|			СкладКомпании В (&СкладКомпании)
					|				И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
					|				И Номенклатура.ТоварнаяМарка В ИЕРАРХИИ (&Марка)
					|				И ХозОперация В (&ХозОперация)) КАК ПродажиОбороты
					|
					|СГРУППИРОВАТЬ ПО
					|	ПродажиОбороты.СкладКомпании,
					|	ПродажиОбороты.Номенклатура.ТоварнаяМарка.Родитель";
					
				Иначе
					Запрос.Текст = 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ПродажиОбороты.СкладКомпании КАК СкладКомпании,
					|	ЦелевыеМаркиТовары.Ссылка КАК Марка,
					|	СУММА(ПродажиОбороты.КоличествоОборот) КАК Факт
					|ИЗ
					|	Справочник.ЦелевыеМарки.Товары КАК ЦелевыеМаркиТовары
					|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(
					|				&ДатаНачала,
					|				&ДатаОкончания,
					|				,
					|				СкладКомпании В (&СкладКомпании)
					|					И ХозОперация В (&ХозОперация)) КАК ПродажиОбороты
					|		ПО ЦелевыеМаркиТовары.Номенклатура = ПродажиОбороты.Номенклатура
					|ГДЕ
					|	ЦелевыеМаркиТовары.Ссылка = &Марка
					|
					|СГРУППИРОВАТЬ ПО
					|	ПродажиОбороты.СкладКомпании,
					|	ЦелевыеМаркиТовары.Ссылка";
					
				КонецЕсли;
				
				Запрос.УстановитьПараметр("Марка", Марка.ЦелеваяМарка);
				Запрос.УстановитьПараметр("ХозОперация", ХозОперация);
				Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
				Запрос.УстановитьПараметр("ДатаНачала", Период.НачалоДействия);
				Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Период.ОкончаниеДействия));
				Запрос.УстановитьПараметр("СкладКомпании", Склад.СкладКомпании);
				//Запрос.УстановитьПараметр("СкладКомпании", Склады.ВыгрузитьКолонку("СкладКомпании"));
				
				РезультатЗапроса = Запрос.Выполнить();
				
				Факт = РезультатЗапроса.Выгрузить();
				
				//Для Каждого склад Из Склады Цикл
				//	облДанныеСклад.Параметры.Заполнить(склад);
				//	облДанныеСклад.Параметры.Марка = Марка.ЦелеваяМарка;
				//	
				//	ТабДок.Вывести(облДанныеСклад);
				
				
				мПлан = План.НайтиСтроки(Новый Структура("СкладКомпании,НачалоДействия,ОкончаниеДействия",склад.СкладКомпании,Период.НачалоДействия,Период.ОкончаниеДействия));
				мФакт = Факт.НайтиСтроки(Новый Структура("СкладКомпании",склад.СкладКомпании));
				ЗначениеПлан = ?(мПлан.Количество()>0,мПлан[0].План,0);
				ЗначениеФакт = ?(мФакт.Количество()>0,мФакт[0].Факт,0);
				
				облДанныеПериод.Параметры.Период = ПредставлениеПериода(Период.НачалоДействия,КонецДня(Период.ОкончаниеДействия));;
				облДанныеПериод.Параметры.План = ЗначениеПлан;
				облДанныеПериод.Параметры.Факт = ЗначениеФакт;
				облДанныеПериод.Параметры.Разница = ЗначениеФакт - ЗначениеПлан;
				ТабДок.Присоединить(облДанныеПериод);
				
				
			КонецЦикла;
		КонецЦикла;
		
		ТабДок.ФиксацияСлева = 2;
		
		Лист = Книга.Состав.Добавить();
		Лист.Данные = ПоместитьВоВременноеХранилище(ТабДок);
		Лист.Наименование = СокрЛП(Марка.ЦелеваяМарка);
		
	КонецЦикла;
	
	Возврат Книга;
	
КонецФункции

Функция Отчет_Премия(Волна,Макет,ХозОперация,ГруппаНоменклатуры)
	
	тзПремий = Волна.Расчет.Выгрузить();
	тзПремий.Сортировать("Порог Убыв");
	
	Книга = Новый ПакетОтображаемыхДокументов;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланАктивности.НачалоДействия КАК НачалоДействия,
	|	КОНЕЦПЕРИОДА(ПланАктивности.ОкончаниеДействия, ДЕНЬ) КАК ОкончаниеДействия,
	|	ПланАктивности.ЦелеваяМарка КАК Марка
	|ИЗ
	|	РегистрСведений.ПланАктивности КАК ПланАктивности
	|ГДЕ
	|	ПланАктивности.Регистратор = &Регистратор
	|	И ПланАктивности.ВидПериода = &ВидПериода
	|	И ПланАктивности.ЦелеваяМарка ССЫЛКА Справочник.ТоварныеМарки";
	
	Запрос.УстановитьПараметр("Регистратор", Волна);
	Запрос.УстановитьПараметр("ВидПериода", Перечисления.ВидыПериодаАктивности.ПолМесяца);
	
	РезультатЗапроса = Запрос.Выполнить();
	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаПериод = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПериод.Следующий() Цикл
		Период = ПредставлениеПериода(ВыборкаПериод.НачалоДействия,ВыборкаПериод.ОкончаниеДействия,"ФП=Истина");
		ТабДок = Новый ТабличныйДокумент;
		
		//облЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		облШапка = Макет.ПолучитьОбласть("Шапка");
		облДанные = Макет.ПолучитьОбласть("Данные");
		
		облШапка.Параметры.Период = Период;
		ТабДок.Вывести(ОблШапка);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПланАктивности.СкладКомпании.Код КАК Код,
		|	ПланАктивности.СкладКомпании КАК Склад,
		|	ЕСТЬNULL(ДополнительныеСведения.Значение, """") КАК Куратор,
		|	ПланАктивности.Количество КАК План,
		|	СУММА(ЕСТЬNULL(ПродажиОбороты.КоличествоОборот, 0)) КАК Факт
		|ИЗ
		|	РегистрСведений.ПланАктивности КАК ПланАктивности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи.Обороты(
		|				&ДатаНачала,
		|				&ДатаОкончания,
		|				,
		|				ХозОперация В (&ХозОперация)
		|					И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
		|					И Номенклатура.ТоварнаяМарка В ИЕРАРХИИ (&ТоварнаяМарка)) КАК ПродажиОбороты
		|		ПО ПланАктивности.СкладКомпании = ПродажиОбороты.СкладКомпании
		|			И ПланАктивности.ЦелеваяМарка = ПродажиОбороты.Номенклатура.ТоварнаяМарка.Родитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
		|			И ПланАктивности.СкладКомпании = ДополнительныеСведения.Объект
		|ГДЕ
		|	ПланАктивности.ВидПериода = &ВидПериода
		|	И ПланАктивности.НачалоДействия = &ДатаНачала
		|	И ПланАктивности.ОкончаниеДействия = &ДатаОкончанияП
		|	И ПланАктивности.ЦелеваяМарка ССЫЛКА Справочник.ТоварныеМарки
		|
		|СГРУППИРОВАТЬ ПО
		|	ПланАктивности.СкладКомпании.Код,
		|	ПланАктивности.СкладКомпании,
		|	ПланАктивности.Количество,
		|	ЕСТЬNULL(ДополнительныеСведения.Значение, """")
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("ДатаНачала", ВыборкаПериод.НачалоДействия);
		Запрос.УстановитьПараметр("ДатаОкончания", ВыборкаПериод.ОкончаниеДействия);
		Запрос.УстановитьПараметр("ДатаОкончанияП", НачалоДня(ВыборкаПериод.ОкончаниеДействия));
		Запрос.УстановитьПараметр("ВидПериода", Перечисления.ВидыПериодаАктивности.ПолМесяца);
		Запрос.УстановитьПараметр("ТоварнаяМарка", ВыборкаПериод.Марка);
		Запрос.УстановитьПараметр("ХозОперация", ХозОперация);
		Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Процент = ?(Выборка.План = 0,0,Выборка.Факт / Выборка.План);
			Премия = 0;
			Для Каждого строка Из тзПремий Цикл
				Если Процент >= строка.Порог Тогда
					Премия = строка.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			облДанные.Параметры.Заполнить(Выборка);
			облДанные.Параметры.Процент = Процент;
			облДанные.Параметры.Премия = Премия;
			ТабДок.Вывести(облДанные);
		КонецЦикла;
		
		ТабДок.ФиксацияСверху = 2;
		ТабДок.ФиксацияСлева = 2;
		
		Лист = Книга.Состав.Добавить();
		Лист.Данные = ПоместитьВоВременноеХранилище(ТабДок);
		Лист.Наименование = СокрЛП(Период);
		
	КонецЦикла;
	
	Возврат Книга;
	
КонецФункции

Функция Отчет_Итого(Волна,Макет,ХозОперация,ГруппаНоменклатуры)
	
	Книга = Новый ПакетОтображаемыхДокументов;
	
	
	Группировки = Новый СписокЗначений;
	Группировки.Добавить("ПланЗначение","План");
	Группировки.Добавить("ФактЗначение","Факт");
	Группировки.Добавить("ПроцентЗначение","Выполнение в %");
	
	ОтделениеКиева = Новый СписокЗначений;	//	ТС Buffet
	ОтделениеКиева.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("000000008"),"Киев",Истина);
	ОтделениеКиева.Добавить(Справочники.ПодразделенияКомпании.НайтиПоКоду("000000008"),"Аттика",Ложь);
	
	мПериодТотал = Волна.Периоды.НайтиСтроки(Новый Структура("ВидПериода",Перечисления.ВидыПериодаАктивности.Общий));
	Если мПериодТотал.Количество() > 0 Тогда
		ДатаНачала = мПериодТотал[0].НачалоДействия;
		ДатаОкончания = мПериодТотал[0].ОкончаниеДействия;
	Иначе
		Возврат Книга;
	КонецЕсли;
	
	Для Каждого Киев Из ОтделениеКиева Цикл
		
		Имя = Киев.Представление + " " + мПериодТотал[0].Имя;
		
		ТабДок = Новый ТабличныйДокумент;
		
		облГруппаСклад	= Макет.ПолучитьОбласть("Группа|Склад");
		облГруппаМарка	= Макет.ПолучитьОбласть("Группа|Марка");
		облШапкаСклад	= Макет.ПолучитьОбласть("Шапка|Склад");
		облШапкаМарка	= Макет.ПолучитьОбласть("Шапка|Марка");
		облСтрокаСклад	= Макет.ПолучитьОбласть("Строка|Склад");
		облСтрокаМарка	= Макет.ПолучитьОбласть("Строка|Марка");
		
		облГруппаСклад.Параметры.Период = ПредставлениеПериода(ДатаНачала,КонецДня(ДатаОкончания));
		ТабДок.Вывести(облГруппаСклад);
		
		Для Каждого группа Из Группировки Цикл
			облГруппаМарка.Параметры.Группа = группа.Представление;
			Лево = ТабДок.Присоединить(облГруппаМарка).Лево;
			Для колонка = 2 По Волна.Товары.количество() Цикл
				облГруппаМарка.Параметры.Группа = "";
				Право = ТабДок.Присоединить(облГруппаМарка).Лево;
			КонецЦикла;
			ТабДок.Область(1,Лево,1,Право).Объединить();
		КонецЦикла;
		
		ТабДок.Вывести(облШапкаСклад);
		
		Для Каждого группа Из Группировки Цикл
			Для Каждого марка Из Волна.Товары Цикл
				облШапкаМарка.Параметры.Марка = Марка.ЦелеваяМарка;
				ВыведеннаяОбласть = ТабДок.Присоединить(облШапкаМарка);
				Если ТипЗнч(Марка.ЦелеваяМарка) = Тип("СправочникСсылка.Номенклатура") Тогда
					ВыведеннаяОбласть.Шрифт = Новый Шрифт(,,Ложь);
				ИначеЕсли ТипЗнч(Марка.ЦелеваяМарка) = Тип("СправочникСсылка.ЦелевыеМарки") Тогда
					ВыведеннаяОбласть.ЦветФона = WebЦвета.БледноБирюзовый;
				ИначеЕсли ТипЗнч(Марка.ЦелеваяМарка) = Тип("СправочникСсылка.ТоварныеМарки") Тогда
					ВыведеннаяОбласть.ЦветТекста = WebЦвета.Красный;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланАктивности.СкладКомпании КАК СкладКомпании
		|ПОМЕСТИТЬ Склады
		|ИЗ
		|	РегистрСведений.ПланАктивности КАК ПланАктивности
		|ГДЕ
		|	ПланАктивности.Регистратор = &Ссылка
		|	И ПланАктивности.ВидПериода = &ВидПериода
		|	И ПланАктивности.СкладКомпании.Подразделение В ИЕРАРХИИ(&Подразделение)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦелевыеМаркиТовары.Ссылка КАК Марка,
		|	ЦелевыеМаркиТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ Марки
		|ИЗ
		|	Документ.ПланАктивности.Товары КАК ПланАктивностиТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЦелевыеМарки.Товары КАК ЦелевыеМаркиТовары
		|		ПО ПланАктивностиТовары.ЦелеваяМарка = ЦелевыеМаркиТовары.Ссылка
		|ГДЕ
		|	ПланАктивностиТовары.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Номенклатура.ТоварнаяМарка.Родитель,
		|	Номенклатура.Ссылка
		|ИЗ
		|	Документ.ПланАктивности.Товары КАК ПланАктивностиТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ПланАктивностиТовары.ЦелеваяМарка = Номенклатура.ТоварнаяМарка.Родитель
		|ГДЕ
		|	ПланАктивностиТовары.Ссылка = &Ссылка
		|	И Номенклатура.Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПланАктивностиТовары.ЦелеваяМарка,
		|	ПланАктивностиТовары.ЦелеваяМарка
		|ИЗ
		|	Документ.ПланАктивности.Товары КАК ПланАктивностиТовары
		|ГДЕ
		|	ПланАктивностиТовары.Ссылка = &Ссылка
		|	И ПланАктивностиТовары.ЦелеваяМарка ССЫЛКА Справочник.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПланАктивности.СкладКомпании КАК СкладКомпании,
		|	ПланАктивности.ЦелеваяМарка КАК Марка,
		|	ПланАктивности.Количество КАК ПланЗначение
		|ПОМЕСТИТЬ План
		|ИЗ
		|	РегистрСведений.ПланАктивности КАК ПланАктивности
		|ГДЕ
		|	ПланАктивности.Регистратор = &Ссылка
		|	И ПланАктивности.ВидПериода = &ВидПериода
		|	И ПланАктивности.СкладКомпании.Подразделение В ИЕРАРХИИ(&Подразделение)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродажиОбороты.СкладКомпании КАК СкладКомпании,
		|	Марки.Марка КАК Марка,
		|	СУММА(ПродажиОбороты.КоличествоОборот) КАК ФактЗначение
		|ПОМЕСТИТЬ Факт
		|ИЗ
		|	РегистрНакопления.Продажи.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			,
		|			ХозОперация В (&ХозОперация)
		|				И Номенклатура В ИЕРАРХИИ (&ГруппаНоменклатуры)
		|				И Номенклатура.ТоварнаяМарка В ИЕРАРХИИ
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						Марки.Марка КАК Марка
		|					ИЗ
		|						Марки КАК Марки)
		|				И СкладКомпании В
		|					(ВЫБРАТЬ
		|						Склады.СкладКомпании КАК СкладКомпании
		|					ИЗ
		|						Склады КАК Склады)) КАК ПродажиОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Марки КАК Марки
		|		ПО ПродажиОбороты.Номенклатура = Марки.Номенклатура
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажиОбороты.СкладКомпании,
		|	Марки.Марка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	План.СкладКомпании КАК СкладКомпании,
		|	План.Марка КАК Марка,
		|	План.ПланЗначение КАК ПланЗначение,
		|	Факт.ФактЗначение КАК ФактЗначение,
		|	ВЫБОР
		|		КОГДА План.ПланЗначение = 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(Факт.ФактЗначение, 0) / План.ПланЗначение * 100
		|	КОНЕЦ КАК ПроцентЗначение
		|ПОМЕСТИТЬ Итого
		|ИЗ
		|	План КАК План
		|		ЛЕВОЕ СОЕДИНЕНИЕ Факт КАК Факт
		|		ПО План.СкладКомпании = Факт.СкладКомпании
		|			И План.Марка = Факт.Марка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Итого.СкладКомпании.Код КАК Код,
		|	Итого.СкладКомпании КАК Склад,
		|	Итого.Марка КАК Марка,
		|	ДополнительныеСведения.Значение КАК Куратор,
		|	Итого.ПланЗначение КАК ПланЗначение,
		|	Итого.ФактЗначение КАК ФактЗначение,
		|	Итого.ПроцентЗначение КАК ПроцентЗначение
		|ИЗ
		|	Итого КАК Итого
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО Итого.СкладКомпании = ДополнительныеСведения.Объект
		|			И (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		
		Если Киев.Пометка Тогда
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"И ПланАктивности.СкладКомпании.Подразделение В ИЕРАРХИИ(&Подразделение)","И НЕ ПланАктивности.СкладКомпании.Подразделение В ИЕРАРХИИ (&Подразделение)");	
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Ссылка", Волна);
		Запрос.УстановитьПараметр("ХозОперация", ХозОперация);
		Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
		Запрос.УстановитьПараметр("Подразделение", Киев.Значение);
		Запрос.УстановитьПараметр("ВидПериода", Перечисления.ВидыПериодаАктивности.Общий);
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		//	РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам)
		
		ТЗ = РезультатЗапроса.Выгрузить();
		Склады = ТЗ.Скопировать(,"Код,Склад,Куратор");
		Склады.Свернуть("Код,Склад,Куратор");
		
		Для Каждого склад Из Склады Цикл
			облСтрокаСклад.Параметры.Заполнить(склад);
			ТабДок.Вывести(облСтрокаСклад);
			
			Для Каждого группа Из Группировки Цикл
				Для Каждого марка Из Волна.Товары Цикл
					мСтрок = ТЗ.НайтиСтроки(Новый Структура("Склад,Марка",склад.Склад,марка.ЦелеваяМарка));
					облСтрокаМарка.Параметры.значение = ?(мСтрок.Количество()>0,мСтрок[0][группа.Значение],0);
					Если группа.Значение = "ПроцентЗначение" Тогда
						Формат = "ЧДЦ=2";
					Иначе
						Формат = "";
					КонецЕсли;
					ТабДок.Присоединить(облСтрокаМарка).Формат = Формат;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		ТабДок.ФиксацияСверху = 2;
		ТабДок.ФиксацияСлева = 2;
		
		Лист = Книга.Состав.Добавить();
		Лист.Данные = ПоместитьВоВременноеХранилище(ТабДок);
		Лист.Наименование = Имя;
		
	КонецЦикла;
	Возврат Книга;
	
КонецФункции

Функция Отчет_Выполнение(Волна,Макет,ХозОперация)
	
	Книга = Новый ПакетОтображаемыхДокументов;
	
	Листы = Новый СписокЗначений;
	Листы.Добавить("Кураторы","Кураторы",Истина);
	Листы.Добавить("Направления","Направления",Истина);
	
	мПериодТотал = Волна.Периоды.НайтиСтроки(Новый Структура("ВидПериода",Перечисления.ВидыПериодаАктивности.Общий));
	Если мПериодТотал.Количество() > 0 Тогда
		ДатаНачала = НачалоДня(мПериодТотал[0].НачалоДействия);
		ДатаОкончания = КонецДня(мПериодТотал[0].ОкончаниеДействия);
	Иначе
		Возврат Книга;
	КонецЕсли;
	
	МесяцДатаНачала		= НачалоДня(ДобавитьМесяц(ДатаНачала,-1));
	МесяцДатаОкончания	= КонецМесяца(ДобавитьМесяц(ДатаОкончания,-1));
	ГодДатаНачала		= НачалоДня(ДобавитьМесяц(ДатаНачала,-12));
	ГодДатаОкончания	= КонецМесяца(ДобавитьМесяц(ДатаОкончания,-12));
	
	Период		= ПредставлениеПериода(ДатаНачала,ДатаОкончания);
	МесяцПрошлый= ПредставлениеПериода(МесяцДатаНачала,МесяцДатаОкончания);
	ГодПрошлый	= ПредставлениеПериода(ГодДатаНачала,ГодДатаОкончания);
	
	облШапкаСклад	= Макет.ПолучитьОбласть("Шапка|Склад");
	облДанныеСклад	= Макет.ПолучитьОбласть("Данные|Склад");
	
	Для Каждого Лист Из Листы Цикл
		
		Имя = Лист.Представление;
		
		ТабДок = Новый ТабличныйДокумент;
		
		Если Имя = "Кураторы" Тогда
			облШапкаДоп		= Макет.ПолучитьОбласть("Шапка|Чеки");
			облДанныеДоп	= Макет.ПолучитьОбласть("Данные|Чеки");
			облШапкаДоп.Параметры.ГодПрошлый = ГодПрошлый;
			облШапкаДоп.Параметры.Период = Период;
			
			ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ПланАктивности.СкладКомпании.Подразделение КАК Подразделение
			|ПОМЕСТИТЬ План
			|ИЗ
			|	РегистрСведений.ПланАктивности КАК ПланАктивности
			|ГДЕ
			|	ПланАктивности.Регистратор = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ПланАктивности.СкладКомпании.Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПланАктивности.СкладКомпании КАК СкладКомпании,
			|	СУММА(ПланАктивности.Количество) КАК ПланЗначение,
			|	СУММА(0) КАК ФактЗначение,
			|	СУММА(0) КАК ФактМесяцПрошлый,
			|	СУММА(0) КАК ФактГодПрошлый
			|ПОМЕСТИТЬ Факт
			|ИЗ
			|	РегистрСведений.ПланАктивности КАК ПланАктивности
			|ГДЕ
			|	ПланАктивности.Регистратор = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ПланАктивности.СкладКомпании
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПродажиОбороты.СкладКомпании,
			|	СУММА(0),
			|	СУММА(ПродажиОбороты.СуммаОборот),
			|	СУММА(0),
			|	СУММА(0)
			|ИЗ
			|	РегистрНакопления.Продажи.Обороты(
			|			&ДатаНачала,
			|			&ДатаОкончания,
			|			,
			|			ХозОперация В (&ХозОперация)
			|				И (СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйзал)
			|					ИЛИ СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйпавильон))
			|				И СкладКомпании.Подразделение В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						План.Подразделение КАК Подразделение
			|					ИЗ
			|						План КАК План)) КАК ПродажиОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ПродажиОбороты.СкладКомпании
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПродажиОбороты.СкладКомпании,
			|	СУММА(0),
			|	СУММА(0),
			|	СУММА(ПродажиОбороты.СуммаОборот),
			|	СУММА(0)
			|ИЗ
			|	РегистрНакопления.Продажи.Обороты(
			|			&МесяцДатаНачала,
			|			&МесяцДатаОкончания,
			|			,
			|			ХозОперация В (&ХозОперация)
			|				И (СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйзал)
			|					ИЛИ СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйпавильон))
			|				И СкладКомпании.Подразделение В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						План.Подразделение КАК Подразделение
			|					ИЗ
			|						План КАК План)) КАК ПродажиОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ПродажиОбороты.СкладКомпании
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПродажиОбороты.СкладКомпании,
			|	СУММА(0),
			|	СУММА(0),
			|	СУММА(0),
			|	СУММА(ПродажиОбороты.СуммаОборот)
			|ИЗ
			|	РегистрНакопления.Продажи.Обороты(
			|			&ГодДатаНачала,
			|			&ГодДатаОкончания,
			|			,
			|			ХозОперация В (&ХозОперация)
			|				И (СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйзал)
			|					ИЛИ СкладКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйпавильон))
			|				И СкладКомпании.Подразделение В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						План.Подразделение КАК Подразделение
			|					ИЗ
			|						План КАК План)) КАК ПродажиОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ПродажиОбороты.СкладКомпании
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Факт.СкладКомпании КАК СкладКомпании,
			|	СУММА(Факт.ФактЗначение) КАК ФактЗначение,
			|	СУММА(Факт.ФактМесяцПрошлый) КАК ФактМесяцПрошлый,
			|	СУММА(Факт.ФактГодПрошлый) КАК ФактГодПрошлый,
			|	СУММА(Факт.ПланЗначение) КАК ПланЗначение
			|ПОМЕСТИТЬ СвернПланФакт
			|ИЗ
			|	Факт КАК Факт
			|
			|СГРУППИРОВАТЬ ПО
			|	Факт.СкладКомпании
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СвернПланФакт.СкладКомпании.Код КАК Код,
			|	СвернПланФакт.СкладКомпании.Ссылка КАК Склад,
			|	ДополнительныеСведения.Значение КАК Куратор,
			|	СвернПланФакт.ПланЗначение КАК ПланЗначение,
			|	СвернПланФакт.ФактЗначение КАК ФактЗначение,
			|	СвернПланФакт.ФактМесяцПрошлый КАК ФактМесяцПрошлый,
			|	СвернПланФакт.ФактГодПрошлый КАК ФактГодПрошлый,
			|	Чеки.ЧекиЗначение КАК ЧекиЗначение,
			|	Чеки.ЧекиПрошлыйГод КАК ЧекиПрошлыйГод
			|ИЗ
			|	СвернПланФакт КАК СвернПланФакт
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|		ПО (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
			|			И СвернПланФакт.СкладКомпании = ДополнительныеСведения.Объект
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ВложенныйЗапрос.СкладКомпании КАК СкладКомпании,
			|			СУММА(ВложенныйЗапрос.ЧекиЗначение) КАК ЧекиЗначение,
			|			СУММА(ВложенныйЗапрос.ЧекиПрошлыйГод) КАК ЧекиПрошлыйГод
			|		ИЗ
			|			(ВЫБРАТЬ
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.СкладКомпании КАК СкладКомпании,
			|				СУММА(ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КоличествоЧеков) КАК ЧекиЗначение,
			|				СУММА(0) КАК ЧекиПрошлыйГод
			|			ИЗ
			|				Документ.ЗакрытиеСмены.СнятыеКассы КАК ЗакрытиеСменыСнятыеКассы
			|			ГДЕ
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|				И ЗакрытиеСменыСнятыеКассы.Ссылка.Проведен
			|			
			|			СГРУППИРОВАТЬ ПО
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.СкладКомпании
			|			
			|			ОБЪЕДИНИТЬ ВСЕ
			|			
			|			ВЫБРАТЬ
			|				РеализацияТоваров.СкладКомпании,
			|				КОЛИЧЕСТВО(РеализацияТоваров.Ссылка),
			|				СУММА(0)
			|			ИЗ
			|				Документ.РеализацияТоваров КАК РеализацияТоваров
			|			ГДЕ
			|				РеализацияТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|				И РеализацияТоваров.Проведен
			|				И РеализацияТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.РеализацияТоваровРозница)
			|				И РеализацияТоваров.ДоговорВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.ПродажаПоКартам)
			|			
			|			СГРУППИРОВАТЬ ПО
			|				РеализацияТоваров.СкладКомпании
			|			
			|			ОБЪЕДИНИТЬ ВСЕ
			|			
			|			ВЫБРАТЬ
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.СкладКомпании,
			|				СУММА(0),
			|				СУММА(ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КоличествоЧеков)
			|			ИЗ
			|				Документ.ЗакрытиеСмены.СнятыеКассы КАК ЗакрытиеСменыСнятыеКассы
			|			ГДЕ
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.Дата МЕЖДУ &ГодДатаНачала И &ГодДатаОкончания
			|				И ЗакрытиеСменыСнятыеКассы.Ссылка.Проведен
			|			
			|			СГРУППИРОВАТЬ ПО
			|				ЗакрытиеСменыСнятыеКассы.Ссылка.СкладКомпании
			|			
			|			ОБЪЕДИНИТЬ ВСЕ
			|			
			|			ВЫБРАТЬ
			|				РеализацияТоваров.СкладКомпании,
			|				СУММА(0),
			|				КОЛИЧЕСТВО(РеализацияТоваров.Ссылка)
			|			ИЗ
			|				Документ.РеализацияТоваров КАК РеализацияТоваров
			|			ГДЕ
			|				РеализацияТоваров.Дата МЕЖДУ &ГодДатаНачала И &ГодДатаОкончания
			|				И РеализацияТоваров.Проведен
			|				И РеализацияТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.РеализацияТоваровРозница)
			|				И РеализацияТоваров.ДоговорВзаиморасчетов.ВидДоговора = ЗНАЧЕНИЕ(Справочник.ВидыДоговоров.ПродажаПоКартам)
			|			
			|			СГРУППИРОВАТЬ ПО
			|				РеализацияТоваров.СкладКомпании) КАК ВложенныйЗапрос
			|		
			|		СГРУППИРОВАТЬ ПО
			|			ВложенныйЗапрос.СкладКомпании) КАК Чеки
			|		ПО СвернПланФакт.СкладКомпании = Чеки.СкладКомпании
			|
			|УПОРЯДОЧИТЬ ПО
			|	Склад
			|АВТОУПОРЯДОЧИВАНИЕ";
		Иначе
			облШапкаДоп		= Макет.ПолучитьОбласть("Шапка|Направление");
			облДанныеДоп	= Макет.ПолучитьОбласть("Данные|Направление");
			
			ТекстЗапроса =		
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СкладыКомпании.Ссылка КАК СкладКомпании
			|ПОМЕСТИТЬ Склады
			|ИЗ
			|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|		ПланАктивности.СкладКомпании.Подразделение КАК Подразделение
			|	ИЗ
			|		РегистрСведений.ПланАктивности КАК ПланАктивности
			|	ГДЕ
			|		ПланАктивности.Регистратор = &Ссылка) КАК ВложенныйЗапрос
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
			|		ПО ВложенныйЗапрос.Подразделение = СкладыКомпании.Подразделение
			|ГДЕ
			|	(СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйзал)
			|			ИЛИ СкладыКомпании.ВидСклада = ЗНАЧЕНИЕ(справочник.ВидыСкладов.торговыйпавильон))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВложенныйЗапрос.Направление КАК Направление,
			|	ВложенныйЗапрос.СкладКомпании КАК СкладКомпании,
			|	СУММА(ВложенныйЗапрос.ПланЗначение) КАК ПланЗначение,
			|	СУММА(ВложенныйЗапрос.ФактЗначение) КАК ФактЗначение,
			|	СУММА(ВложенныйЗапрос.ФактМесяцПрошлый) КАК ФактМесяцПрошлый,
			|	СУММА(ВложенныйЗапрос.ФактГодПрошлый) КАК ФактГодПрошлый
			|ПОМЕСТИТЬ ПланФакт
			|ИЗ
			|	(ВЫБРАТЬ
			|		ПланАктивности.ЦелеваяМарка.Наименование КАК Направление,
			|		ПланАктивности.СкладКомпании КАК СкладКомпании,
			|		СУММА(ПланАктивности.Количество) КАК ПланЗначение,
			|		0 КАК ФактЗначение,
			|		0 КАК ФактМесяцПрошлый,
			|		0 КАК ФактГодПрошлый
			|	ИЗ
			|		РегистрСведений.ПланАктивности КАК ПланАктивности
			|	ГДЕ
			|		ПланАктивности.Регистратор = &Ссылка
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ПланАктивности.СкладКомпании,
			|		ПланАктивности.ЦелеваяМарка.Наименование
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ВложенныйЗапрос_Тек.Направление,
			|		ВложенныйЗапрос_Тек.СкладКомпании,
			|		0,
			|		СУММА(ВложенныйЗапрос_Тек.ФактЗначение),
			|		СУММА(0),
			|		СУММА(0)
			|	ИЗ
			|		(ВЫБРАТЬ
			|			ВЫБОР
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Табак)
			|					ТОГДА ""Табак""
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Напитки)
			|					ТОГДА ""Напитки""
			|				ИНАЧЕ ""Прочее""
			|			КОНЕЦ КАК Направление,
			|			ПродажиОбороты.СкладКомпании КАК СкладКомпании,
			|			ПродажиОбороты.СуммаОборот КАК ФактЗначение
			|		ИЗ
			|			РегистрНакопления.Продажи.Обороты(
			|					&ДатаНачала,
			|					&ДатаОкончания,
			|					,
			|					ХозОперация В (&ХозОперация)
			|						И СкладКомпании В
			|							(ВЫБРАТЬ
			|								Склады.СкладКомпании КАК СкладКомпании
			|							ИЗ
			|								Склады КАК Склады)) КАК ПродажиОбороты) КАК ВложенныйЗапрос_Тек
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ВложенныйЗапрос_Тек.Направление,
			|		ВложенныйЗапрос_Тек.СкладКомпании
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ВложенныйЗапрос_Месяц.Направление,
			|		ВложенныйЗапрос_Месяц.СкладКомпании,
			|		0,
			|		СУММА(0),
			|		СУММА(ВложенныйЗапрос_Месяц.ФактЗначение),
			|		СУММА(0)
			|	ИЗ
			|		(ВЫБРАТЬ
			|			ВЫБОР
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Табак)
			|					ТОГДА ""Табак""
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Напитки)
			|					ТОГДА ""Напитки""
			|				ИНАЧЕ ""Прочее""
			|			КОНЕЦ КАК Направление,
			|			ПродажиОбороты.СкладКомпании КАК СкладКомпании,
			|			ПродажиОбороты.СуммаОборот КАК ФактЗначение
			|		ИЗ
			|			РегистрНакопления.Продажи.Обороты(
			|					&МесяцДатаНачала,
			|					&МесяцДатаОкончания,
			|					,
			|					ХозОперация В (&ХозОперация)
			|						И СкладКомпании В
			|							(ВЫБРАТЬ
			|								Склады.СкладКомпании КАК СкладКомпании
			|							ИЗ
			|								Склады КАК Склады)) КАК ПродажиОбороты) КАК ВложенныйЗапрос_Месяц
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ВложенныйЗапрос_Месяц.Направление,
			|		ВложенныйЗапрос_Месяц.СкладКомпании
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ВложенныйЗапрос_Год.Направление,
			|		ВложенныйЗапрос_Год.СкладКомпании,
			|		0,
			|		СУММА(0),
			|		СУММА(0),
			|		СУММА(ВложенныйЗапрос_Год.ФактЗначение)
			|	ИЗ
			|		(ВЫБРАТЬ
			|			ВЫБОР
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Табак)
			|					ТОГДА ""Табак""
			|				КОГДА ПродажиОбороты.Номенклатура В ИЕРАРХИИ (&Напитки)
			|					ТОГДА ""Напитки""
			|				ИНАЧЕ ""Прочее""
			|			КОНЕЦ КАК Направление,
			|			ПродажиОбороты.СкладКомпании КАК СкладКомпании,
			|			ПродажиОбороты.СуммаОборот КАК ФактЗначение
			|		ИЗ
			|			РегистрНакопления.Продажи.Обороты(
			|					&ГодДатаНачала,
			|					&ГодДатаОкончания,
			|					,
			|					ХозОперация В (&ХозОперация)
			|						И СкладКомпании В
			|							(ВЫБРАТЬ
			|								Склады.СкладКомпании КАК СкладКомпании
			|							ИЗ
			|								Склады КАК Склады)) КАК ПродажиОбороты) КАК ВложенныйЗапрос_Год
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ВложенныйЗапрос_Год.Направление,
			|		ВложенныйЗапрос_Год.СкладКомпании) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.Направление,
			|	ВложенныйЗапрос.СкладКомпании
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПланФакт.СкладКомпании.Код КАК Код,
			|	ПланФакт.СкладКомпании КАК Склад,
			|	ПланФакт.Направление КАК Направление,
			|	ДополнительныеСведения.Значение КАК Куратор,
			|	ПланФакт.ПланЗначение КАК ПланЗначение,
			|	ПланФакт.ФактЗначение КАК ФактЗначение,
			|	ПланФакт.ФактМесяцПрошлый КАК ФактМесяцПрошлый,
			|	ПланФакт.ФактГодПрошлый КАК ФактГодПрошлый
			|ИЗ
			|	ПланФакт КАК ПланФакт
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
			|		ПО ПланФакт.СкладКомпании = ДополнительныеСведения.Объект
			|			И (ДополнительныеСведения.Свойство.Имя = ""КураторТТ"")
			|
			|УПОРЯДОЧИТЬ ПО
			|	Склад
			|АВТОУПОРЯДОЧИВАНИЕ";
		КонецЕсли;
		
		облШапкаСклад.Параметры.Период = Период;
		облШапкаСклад.Параметры.МесяцПрошлый = МесяцПрошлый;
		облШапкаСклад.Параметры.ГодПрошлый = ГодПрошлый;
		ТабДок.Вывести(облШапкаСклад);
		
		ТабДок.Присоединить(облШапкаДоп);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр("Ссылка", Волна);
		Запрос.УстановитьПараметр("ХозОперация", ХозОперация);
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		Запрос.УстановитьПараметр("МесяцДатаНачала", МесяцДатаНачала);
		Запрос.УстановитьПараметр("МесяцДатаОкончания", МесяцДатаОкончания);
		Запрос.УстановитьПараметр("ГодДатаНачала", ГодДатаНачала);
		Запрос.УстановитьПараметр("ГодДатаОкончания", ГодДатаОкончания);
		Запрос.УстановитьПараметр("Табак", Справочники.ЦелевыеМарки.НайтиПоКоду("000000008").Товары.ВыгрузитьКолонку("Номенклатура"));
		Запрос.УстановитьПараметр("Напитки", Справочники.ЦелевыеМарки.НайтиПоКоду("000000006").Товары.ВыгрузитьКолонку("Номенклатура"));
		
		РезультатЗапроса = Запрос.Выполнить();
		//	РезультатЗапроса.Выгрузить()
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			//Для Каждого склад Из Склады Цикл
			облДанныеСклад.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(облДанныеСклад);
			облДанныеДоп.Параметры.Заполнить(Выборка);
			ТабДок.Присоединить(облДанныеДоп);
		КонецЦикла;
		
		ТабДок.ФиксацияСверху = 2;
		ТабДок.ФиксацияСлева = 2;
		
		Лист = Книга.Состав.Добавить();
		Лист.Данные = ПоместитьВоВременноеХранилище(ТабДок);
		Лист.Наименование = Имя;
		
	КонецЦикла;
	Возврат Книга;
	
КонецФункции

Процедура Записать(Объект,Представление,Активность)
	ПутьХранения = "\\space.digma\office\departments\Служба_Аналитики\Отчеты Аналитика\";
	ИмяФайла = СокрЛП(Активность) + " " + Представление + " " + Формат(ТекущаяДата(),"ДФ=yyyy-MM-dd") +".XlsX";
	Если Представление = "Выполнение" Тогда
		//ИмяФайла = "План-факт " + Формат(ТекущаяДата(),"ДФ=yyyy-MM-dd") + " " + Представление +".XlsX";
		Каталог = "План-факт" + "\";
	Иначе
		//ИмяФайла = "ФМУ План-факт " + Формат(ТекущаяДата(),"ДФ=yyyy-MM-dd") + " " + Представление +".XlsX";
		Каталог = "ФМУ План-факт" + "\";
	КонецЕсли;
	СоздатьКаталог(ПутьХранения + Каталог);
	
	ПолноеИмяФайла = ПутьХранения + Каталог + ИмяФайла;
	
	Попытка
		Если ТипЗнч(Объект) = Тип("ПакетОтображаемыхДокументов") Тогда
			Объект.Записать(ПолноеИмяФайла, ТипФайлаПакетаОтображаемыхДокументов.XLSX);
		Иначе
			Объект.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		КонецЕсли;
	Исключение
		ВсЁОк = Ложь;
		ТекстОшибки = " Не удалось создать файл! " + ОписаниеОшибки();
		Сообщить(ТекстОшибки);
	КонецПопытки;
КонецПроцедуры

//"ВЫБРАТЬ
//|	ЦелевыеМаркиТовары.Номенклатура КАК Группы,
//|	ЦелевыеМаркиТовары.Ссылка КАК Направление
//|ПОМЕСТИТЬ Направления
//|ИЗ
//|	Документ.ПланАктивности.Товары КАК ПланАктивностиТовары
//|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЦелевыеМарки.Товары КАК ЦелевыеМаркиТовары
//|		ПО ПланАктивностиТовары.ЦелеваяМарка = ЦелевыеМаркиТовары.Ссылка
//|ГДЕ
//|	ПланАктивностиТовары.Ссылка = &Ссылка
//|;
//|
//|////////////////////////////////////////////////////////////////////////////////
//|ВЫБРАТЬ
//|	ПродажиОбороты.СкладКомпании КАК СкладКомпании,
//|	ПродажиОбороты.Номенклатура КАК Номенклатура,
//|	ПродажиОбороты.КоличествоОборот КАК КоличествоОборот
//|ИЗ
//|	РегистрНакопления.Продажи.Обороты(&дата1, &дата2, , СкладКомпании = &СкладКомпании) КАК ПродажиОбороты
//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Направления КАК Направление
//|		ПО (ПродажиОбороты.Номенклатура В ИЕРАРХИИ
//|				(ВЫБРАТЬ
//|					Направление.Номенклатура КАК Номенклатура
//|				ИЗ
//|					Направление КАК Направление))
//|;
//|
//|////////////////////////////////////////////////////////////////////////////////
//|ВЫБРАТЬ
//|	Направления.Группы КАК Группы,
//|	Направления.Направление КАК Направление,
//|	Номенклатура.Ссылка КАК Ссылка
//|ИЗ
//|	Направления КАК Направления
//|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
//|		ПО (Номенклатура.Ссылка В ИЕРАРХИИ
//|				(ВЫБРАТЬ
//|					Направления.Группы КАК Группы
//|				ИЗ
//|					Направления КАК Направления))"

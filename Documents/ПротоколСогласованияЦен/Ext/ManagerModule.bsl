#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	Реквизиты = ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
//	ДополнитьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства,Реквизиты);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДополнительныеСвойства.ДляПроведения.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПротоколСогласованияЦенАкция") Тогда
		ТекстЗапросаТаблицаСогласованиеЦенАкция(Запрос, ТекстыЗапроса, Регистры);
	Иначе
		ТекстЗапросаТаблицаСогласованиеЦен(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	ТекстЗапросаТаблицаЦеныКонтрагентов(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

КонецПроцедуры

Функция ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПротоколСогласованияЦен.Дата КАК Период,
	               |	ПротоколСогласованияЦен.Ссылка КАК Ссылка,
	               |	ПротоколСогласованияЦен.ХозОперация КАК ХозОперация,
	               |	ПротоколСогласованияЦен.Организация КАК Организация,
	               |	ПротоколСогласованияЦен.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПротоколСогласованияЦен.Контрагент КАК Контрагент,
	               |	ПротоколСогласованияЦен.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПротоколСогласованияЦен.ВалютаДокумента КАК ВалютаДокумента
	               |ИЗ
	               |	Документ.ПротоколСогласованияЦен КАК ПротоколСогласованияЦен
	               |ГДЕ
	               |	ПротоколСогласованияЦен.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	
	Возврат Реквизиты;	

	
КонецФункции

//Процедура ДополнитьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства,Реквизиты)
//	
//	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);
//	
//КонецПроцедуры



Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ПротоколСогласованияЦенТовары.Ссылка КАК Ссылка,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ПротоколСогласованияЦенТовары.НомерСтроки КАК НомерСтроки,
	               |	ПротоколСогласованияЦенТовары.Ссылка.Организация КАК Организация,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПротоколСогласованияЦенТовары.Ссылка.Контрагент КАК Контрагент,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПротоколСогласованияЦенТовары.Номенклатура КАК Номенклатура,
	               |	ПротоколСогласованияЦенТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,	             
	               |	ПротоколСогласованияЦенТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ТипЦен КАК ТипЦен,
	               |	ПротоколСогласованияЦенТовары.Цена КАК Цена,
	               |	ПротоколСогласованияЦенТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ПроцентОтклонения КАК ПроцентОтклонения,
	               |	ПротоколСогласованияЦенТовары.Ссылка.ДатаНачалаДействия КАК ДатаНачалаДействия,
	               |	ПротоколСогласованияЦенТовары.Ссылка.СрокДействия КАК СрокДействия,
	               |	ПротоколСогласованияЦенТовары.Ссылка.КурсДокумента КАК КурсДокумента
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ПротоколСогласованияЦен.Товары КАК ПротоколСогласованияЦенТовары
	               |ГДЕ
	               |	ПротоколСогласованияЦенТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСогласованиеЦен(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СогласованиеЦен";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ	              
	              |	ВтТаблицаТовары.ДатаНачалаДействия КАК Период,
	              |	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
				  |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
				  |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
				  |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,				  
				  |	ВтТаблицаТовары.Цена КАК Цена,
	              |	ВтТаблицаТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
				  |	ВтТаблицаТовары.СрокДействия КАК СрокДействия,
				  |	ВтТаблицаТовары.ПроцентОтклонения КАК ПроцентОтклонения
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСогласованиеЦенАкция(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СогласованиеЦенАкция";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ	              
	              |	ВтТаблицаТовары.ДатаНачалаДействия КАК Период,
	              |	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
				  |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
				  |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
				  |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,				  
				  |	ВтТаблицаТовары.Цена КАК Цена,
	              |	ВтТаблицаТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
				  |	ВтТаблицаТовары.СрокДействия КАК СрокДействия,
				  |	ВтТаблицаТовары.ПроцентОтклонения КАК ПроцентОтклонения
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ТекстЗапросаТаблицаЦеныКонтрагентов(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ЦеныКонтрагентов";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ВтТаблицаТовары.ДатаНачалаДействия КАК Период,
	              |	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ТипЦен КАК ТипЦен,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	              |	ВтТаблицаТовары.Цена КАК Цена
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |ГДЕ
	              |	ВтТаблицаТовары.Цена <> 0"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;	
КонецФункции

#КонецОбласти
	
	
#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти
	
	
	
	
	
	
	
	
	
	
	
	
	
	
	

#КонецЕсли

#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("НачислениеКоммунальныхУслуг"));
	
	ЗаполнитьСписокВыбораПериодов(Объект, Элементы);
	
	ЭтоНачисление = Объект.ХозОперация = Справочники.ХозОперации.НачислениеКоммунальныхУслуг;
	
	Если ЭтоНачисление Тогда
		Если ОбменФронтПовтИсп.ЭтоПодразделениеАттика(Объект.ПодразделениеКомпании) Тогда
			СформироватьДеревоАттика();
		Иначе
			СформироватьДеревоУслуг();
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Показания.ТолькоПросмотр = ЭтоНачисление;	
	
	Ответственные();
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	//// СтандартныеПодсистемы.УправлениеДоступом
	//УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	//// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЭтоНачисление Тогда
		ЗаписатьДеревоУслугВТЧУслуги();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	ЭтоНачисление = Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.НачислениеКоммунальныхУслуг");
	Элементы.Показания.ТолькоПросмотр = ЭтоНачисление;	
	УправлениеЭлементамиФормы();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги


&НаКлиенте
Процедура ДеревоУслугПриИзменении(Элемент)
	СтрокаДеревоУслугПриИзменении(Элемент.ТекущаяСтрока,Элемент.ТекущийЭлемент.Имя);
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура КомандаРазвернутьУзлыДерева(Команда)
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоУслуг",,Истина)
КонецПроцедуры

&НаКлиенте
Процедура КомандаСвернутьУзлыДерева(Команда)
	КоллекцияЭлементовДерева = ДеревоУслуг.ПолучитьЭлементы();
	//Свернуть дерево 
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоУслуг.Свернуть(ИдентификаторСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаРазвернутьДо2Уровня(Команда)
	КоллекцияЭлементовДерева = ДеревоУслуг.ПолучитьЭлементы();
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Элементы.ДеревоУслуг.Развернуть(ИдентификаторСтроки);
		КоллекцияСтрокиДерева = Строка.ПолучитьЭлементы();
		Для Каждого СтрокаСтроки Из КоллекцияСтрокиДерева Цикл
			ИдентификаторСтроки = СтрокаСтроки.ПолучитьИдентификатор();
			Элементы.ДеревоУслуг.Свернуть(ИдентификаторСтроки);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьДеревоУслуг(Команда)
	Если ПроверитьЗаполнение() Тогда
		КомандаЗаполнитьДеревоУслугНаСервере();
		СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтаФорма,"ДеревоУслуг",,Истина);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
	МодульПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаСервере
Процедура Ответственные()
	
	ОтветственныйСписокВыбора = Элементы.Ответственный.СписокВыбора;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	КоммунальныеУслуги.Ответственный КАК Ответственный
	|ИЗ
	|	Справочник.КоммунальныеУслуги КАК КоммунальныеУслуги
	|ГДЕ
	|	НЕ КоммунальныеУслуги.ЭтоГруппа
	|	И НЕ КоммунальныеУслуги.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоммунальныеУслуги.Ответственный.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОтветственныйСписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Ответственный,?(ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ответственный),"","По всем, без отбора"));
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		Ответственный = Пользователи.ТекущийПользователь().ФизическоеЛицо;
		Если ОтветственныйСписокВыбора.НайтиПоЗначению(Ответственный) <> Неопределено Тогда 
			Объект.Ответственный = Ответственный;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ГруппаТЧ.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
	МассивЭлементов = Новый Массив();
	
	МассивЭлементов.Добавить("ГруппаУслуги");
	МассивЭлементов.Добавить("ПоказанияПоказанияПредыдущие");
	МассивЭлементов.Добавить("ПоказанияПотребление");
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементов,"Видимость",ЭтоНачисление);
	
	Элементы.ГруппаТабДок.Видимость = Ложь;
	ИмяПоляДерева = "ДеревоУслугПотреблениеФакт";
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораПериодов(Объект, Элементы)
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
	
	СписокПериодов = Элементы.ОтчетныйМесяц.СписокВыбора;
	СписокПериодов.Очистить();
	
	Для Ит = -3 По 3 Цикл 
		ТекДата = НачалоМесяца(ДобавитьМесяц(ДатаДокумента, Ит));
		СписокПериодов.Добавить(ТекДата, Формат(ТекДата, "ДФ='MMMM yyyy'"));
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаДерева(ЭтоАттика, ЕстьОтветственный)
	
	Если ЭтоАттика Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	тзПоказания.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	тзПоказания.Счетчик КАК Счетчик,
		|	тзПоказания.ПоказанияПредыдущие КАК ПоказанияПредыдущие,
		|	тзПоказания.ПоказанияТекущие КАК ПоказанияТекущие,
		|	тзПоказания.Потребление КАК Потребление
		|ПОМЕСТИТЬ втПоказания
		|ИЗ
		|	&тзПоказания КАК тзПоказания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КоммунальныеУслугиПотребители.Ссылка КАК КоммунальнаяУслуга,
		|	ВЫРАЗИТЬ(КоммунальныеУслугиПотребители.Потребитель КАК Справочник.ТорговыеПлощадки) КАК ТорговаяПлощадка,
		|	ТорговыеПлощадкиПотребители.Потребитель КАК СубПотребитель,
		|	КоммунальныеУслугиСубы.Ссылка КАК СубУслуга,
		|	ИСТИНА КАК Поле1
		|ПОМЕСТИТЬ втПотребители
		|ИЗ
		|	Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиСубы
		|		ПО ТорговыеПлощадкиПотребители.Потребитель = КоммунальныеУслугиСубы.Потребитель
		|			И (ТорговыеПлощадкиПотребители.Потребитель ССЫЛКА Справочник.ТочкиДоставки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
		|		ПО ТорговыеПлощадкиПотребители.Ссылка = КоммунальныеУслугиПотребители.Потребитель
		|ГДЕ
		|	ТорговыеПлощадкиПотребители.Ссылка.ПодразделениеКомпании В(&ПодразделениеКомпании)
		|	И КоммунальныеУслугиПотребители.Ссылка.Ответственный В ИЕРАРХИИ(&Ответственный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоммунальныеУслугиПотребители.Ссылка,
		|	КоммунальныеУслугиПотребители.Потребитель,
		|	NULL,
		|	NULL,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
		|ГДЕ
		|	КоммунальныеУслугиПотребители.Потребитель.ПодразделениеКомпании В(&ПодразделениеКомпании)
		|	И НЕ КоммунальныеУслугиПотребители.Потребитель В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ТорговыеПлощадкиПотребители.Потребитель КАК Потребитель
		|				ИЗ
		|					Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
		|				ГДЕ
		|					ТорговыеПлощадкиПотребители.Ссылка.ПодразделениеКомпании В (&ПодразделениеКомпании))
		|	И КоммунальныеУслугиПотребители.Ссылка.Ответственный В ИЕРАРХИИ(&Ответственный)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	втПотребители.КоммунальнаяУслуга.Родитель КАК КомУслугаРодитель,
		|	втПотребители.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	втПотребители.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
		|	втПотребители.СубПотребитель КАК СубПотребитель,
		|	втПотребители.СубУслуга КАК КоммунальнаяУслугаСуб,
		|	ЕСТЬNULL(АбонплатыКоммунальныхУслугСрезПоследних.Абонплата, 0) КАК Абонплата,
		|	ЕСТЬNULL(ТарифыКоммунальныхУслугСрезПоследних.Тариф, 0) КАК Тариф,
		|	ЕСТЬNULL(взПотребление.Потребление, 0) КАК ПотреблениеПоСчетчику,
		|	ЕСТЬNULL(ТарифыКоммунальныхУслугСрезПоследних.Тариф, 0) * ЕСТЬNULL(взПотребление.Потребление, 0) КАК Начислено,
		|	ВЫБОР
		|		КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
		|			ТОГДА 8
		|		КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ИндексКартинки,
		|	ТИПЗНАЧЕНИЯ(втПотребители.СубПотребитель) КАК ТипСуба,
		|	ВЫБОР
		|		КОГДА втПотребители.СубПотребитель ЕСТЬ NULL
		|			ТОГДА втПотребители.ТорговаяПлощадка.Код
		|		ИНАЧЕ ВЫБОР
		|				КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
		|					ТОГДА ВЫРАЗИТЬ(втПотребители.СубПотребитель КАК Справочник.ТорговыеПлощадки).Код
		|				КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
		|					ТОГДА ВЫРАЗИТЬ(втПотребители.СубПотребитель КАК Справочник.ТочкиДоставки).Global_ID
		|				ИНАЧЕ ""----""
		|			КОНЕЦ
		|	КОНЕЦ КАК КодПотребителя,
		|	втПотребители.ТорговаяПлощадка.Код КАК ТорговаяПлощадкаКод
		|ИЗ
		|	втПотребители КАК втПотребители
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыКоммунальныхУслуг.СрезПоследних(&Период, ) КАК ТарифыКоммунальныхУслугСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.КоммунальнаяУслуга
		|				ИНАЧЕ втПотребители.СубУслуга
		|			КОНЕЦ = ТарифыКоммунальныхУслугСрезПоследних.КоммунальнаяУслуга)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АбонплатыКоммунальныхУслуг.СрезПоследних(&Период, ) КАК АбонплатыКоммунальныхУслугСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.КоммунальнаяУслуга
		|				ИНАЧЕ втПотребители.СубУслуга
		|			КОНЕЦ = АбонплатыКоммунальныхУслугСрезПоследних.КоммунальнаяУслуга)
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			втПоказания.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|			СУММА(втПоказания.Потребление) КАК Потребление
		|		ИЗ
		|			втПоказания КАК втПоказания
		|		
		|		СГРУППИРОВАТЬ ПО
		|			втПоказания.ТорговаяПлощадка) КАК взПотребление
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубПотребитель ЕСТЬ NULL
		|					ТОГДА втПотребители.ТорговаяПлощадка
		|				ИНАЧЕ втПотребители.СубПотребитель
		|			КОНЕЦ = взПотребление.ТорговаяПлощадка)
		|ГДЕ
		|	НЕ втПотребители.ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПотребители.СубПотребитель КАК СубПотребитель
		|				ИЗ
		|					втПотребители КАК втПотребители
		|				ГДЕ
		|					НЕ втПотребители.СубПотребитель ЕСТЬ NULL)
		|
		|УПОРЯДОЧИТЬ ПО
		|	КомУслугаРодитель,
		|	втПотребители.ТорговаяПлощадка.Наименование,
		|	ТипСуба
		|ИТОГИ
		|	МИНИМУМ(ИндексКартинки)
		|ПО
		|	КомУслугаРодитель,
		|	ТорговаяПлощадка,
		|	КоммунальнаяУслуга";
		
		Если Не ЕстьОтветственный Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И КоммунальныеУслугиПотребители.Ссылка.Ответственный В ИЕРАРХИИ(&Ответственный)","");
		КонецЕсли;	
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КоммунальныеУслугиПотребители.Ссылка КАК КоммунальнаяУслуга,
		|	КоммунальныеУслугиПотребители.Потребитель КАК ТорговаяПлощадка,
		|	NULL КАК СубПотребитель,
		|	NULL КАК СубУслуга,
		|	ЛОЖЬ КАК Суб
		|ПОМЕСТИТЬ втПотребители
		|ИЗ
		|	Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
		|ГДЕ
		|	КоммунальныеУслугиПотребители.Потребитель.ПодразделениеКомпании В(&ПодразделениеКомпании)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КоммунальныеУслугиПотребители.Ссылка,
		|	ТорговыеПлощадкиПотребители.Ссылка,
		|	ТорговыеПлощадкиПотребители.Потребитель,
		|	КоммунальныеУслугиПотребителиСуб.Ссылка,
		|	ИСТИНА
		|ИЗ
		|	Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребителиСуб
		|		ПО ТорговыеПлощадкиПотребители.Потребитель = КоммунальныеУслугиПотребителиСуб.Потребитель
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
		|		ПО ТорговыеПлощадкиПотребители.Ссылка = КоммунальныеУслугиПотребители.Потребитель
		|ГДЕ
		|	(ВЫРАЗИТЬ(КоммунальныеУслугиПотребителиСуб.Потребитель КАК Справочник.ТорговыеПлощадки).ПодразделениеКомпании В (&ПодразделениеКомпании)
		|			ИЛИ КоммунальныеУслугиПотребителиСуб.Потребитель ССЫЛКА Справочник.ТочкиДоставки)
		|	И ТорговыеПлощадкиПотребители.Ссылка.ПодразделениеКомпании В(&ПодразделениеКомпании)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	втПотребители.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
		|	втПотребители.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	втПотребители.СубПотребитель КАК СубПотребитель,
		|	втПотребители.СубУслуга КАК КоммунальнаяУслугаСуб,
		|	ПривязкаСчетчиковСрезПоследних.Счетчик КАК Счетчик,
		|	ЕСТЬNULL(АбонплатыКоммунальныхУслугСрезПоследних.Абонплата, 0) КАК Абонплата,
		|	ЕСТЬNULL(ТарифыКоммунальныхУслугСрезПоследних.Тариф, 0) КАК Тариф
		|ПОМЕСТИТЬ втСоСчетчиками
		|ИЗ
		|	втПотребители КАК втПотребители
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкаСчетчиков.СрезПоследних(&Период, ) КАК ПривязкаСчетчиковСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.ТорговаяПлощадка
		|				ИНАЧЕ втПотребители.СубПотребитель
		|			КОНЕЦ = ПривязкаСчетчиковСрезПоследних.ТорговаяПлощадка)
		|			И (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.КоммунальнаяУслуга.ВидКоммунальнойУслуги
		|				ИНАЧЕ втПотребители.СубУслуга.ВидКоммунальнойУслуги
		|			КОНЕЦ = ПривязкаСчетчиковСрезПоследних.Счетчик.ВидКоммунальнойУслуги)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыКоммунальныхУслуг.СрезПоследних(&Период, ) КАК ТарифыКоммунальныхУслугСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.КоммунальнаяУслуга
		|				ИНАЧЕ втПотребители.СубУслуга
		|			КОНЕЦ = ТарифыКоммунальныхУслугСрезПоследних.КоммунальнаяУслуга)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АбонплатыКоммунальныхУслуг.СрезПоследних(&Период, ) КАК АбонплатыКоммунальныхУслугСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втПотребители.СубУслуга ЕСТЬ NULL
		|					ТОГДА втПотребители.КоммунальнаяУслуга
		|				ИНАЧЕ втПотребители.СубУслуга
		|			КОНЕЦ = АбонплатыКоммунальныхУслугСрезПоследних.КоммунальнаяУслуга)
		|ГДЕ
		|	НЕ втПотребители.ТорговаяПлощадка В
		|				(ВЫБРАТЬ
		|					втПотребители.СубПотребитель КАК СубПотребитель
		|				ИЗ
		|					втПотребители КАК втПотребители
		|				ГДЕ
		|					НЕ втПотребители.СубПотребитель ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСоСчетчиками.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
		|	втСоСчетчиками.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	втСоСчетчиками.СубПотребитель КАК СубПотребитель,
		|	втСоСчетчиками.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
		|	втСоСчетчиками.Счетчик КАК Счетчик,
		|	втСоСчетчиками.Абонплата КАК Абонплата,
		|	втСоСчетчиками.Тариф КАК Тариф,
		|	ЕСТЬNULL(ТекущиеПоказанияСчетчиковСрезПоследних.Показания, 0) КАК ПоказанияТекущие,
		|	ЕСТЬNULL(ПредыдущиеПоказанияСчетчиковСрезПоследних.Показания, 0) КАК ПоказанияПредыдущие,
		|	ВЫБОР
		|		КОГДА втСоСчетчиками.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
		|			ТОГДА 8
		|		КОГДА втСоСчетчиками.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК ИндексКартинки,
		|	втСоСчетчиками.ТорговаяПлощадка.Код КАК КодПотребителя,
		|	ВЫБОР
		|		КОГДА втСоСчетчиками.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
		|			ТОГДА ВЫРАЗИТЬ(втСоСчетчиками.СубПотребитель КАК Справочник.ТорговыеПлощадки).Код
		|		КОГДА втСоСчетчиками.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
		|			ТОГДА ВЫРАЗИТЬ(втСоСчетчиками.СубПотребитель КАК Справочник.ТочкиДоставки).Global_ID
		|		ИНАЧЕ ""----""
		|	КОНЕЦ КАК КодСуба
		|ИЗ
		|	втСоСчетчиками КАК втСоСчетчиками
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеПоказанияСчетчиков.СрезПоследних(&Период, ) КАК ТекущиеПоказанияСчетчиковСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втСоСчетчиками.СубПотребитель ЕСТЬ NULL
		|					ТОГДА втСоСчетчиками.ТорговаяПлощадка
		|				ИНАЧЕ втСоСчетчиками.СубПотребитель
		|			КОНЕЦ = ТекущиеПоказанияСчетчиковСрезПоследних.ТорговаяПлощадка)
		|			И втСоСчетчиками.Счетчик = ТекущиеПоказанияСчетчиковСрезПоследних.Счетчик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредыдущиеПоказанияСчетчиков.СрезПоследних(&МоментВремени, ) КАК ПредыдущиеПоказанияСчетчиковСрезПоследних
		|		ПО (ВЫБОР
		|				КОГДА втСоСчетчиками.СубПотребитель ЕСТЬ NULL
		|					ТОГДА втСоСчетчиками.ТорговаяПлощадка
		|				ИНАЧЕ втСоСчетчиками.СубПотребитель
		|			КОНЕЦ = ПредыдущиеПоказанияСчетчиковСрезПоследних.ТорговаяПлощадка)
		|			И втСоСчетчиками.Счетчик = ПредыдущиеПоказанияСчетчиковСрезПоследних.Счетчик
		|
		|УПОРЯДОЧИТЬ ПО
		|	КоммунальнаяУслуга,
		|	ТорговаяПлощадка
		|ИТОГИ
		|	МИНИМУМ(ИндексКартинки)
		|ПО
		|	ТорговаяПлощадка,
		|	КоммунальнаяУслуга,
		|	СубПотребитель
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
КонецФункции
&НаСервереБезКонтекста
Функция ТекстЗапросаПоказания(ЭтоАттика, ЕстьОтветственный)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(КоммунальныеУслугиПотребители.Потребитель КАК Справочник.ТорговыеПлощадки) КАК ТорговаяПлощадка
	|ПОМЕСТИТЬ втПотребителиОсн
	|ИЗ
	|	Справочник.КоммунальныеУслуги.Потребители КАК КоммунальныеУслугиПотребители
	|ГДЕ
	|	КоммунальныеУслугиПотребители.Потребитель.ПодразделениеКомпании В(&ПодразделениеКомпании)
	|	И КоммунальныеУслугиПотребители.Ссылка.Ответственный В ИЕРАРХИИ(&Ответственный)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПотребителиОсн.ТорговаяПлощадка КАК ТорговаяПлощадка
	|ПОМЕСТИТЬ втПотребители
	|ИЗ
	|	втПотребителиОсн КАК втПотребителиОсн
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТорговыеПлощадкиПотребители.Потребитель КАК Справочник.ТорговыеПлощадки)
	|ИЗ
	|	Справочник.ТорговыеПлощадки.Потребители КАК ТорговыеПлощадкиПотребители
	|ГДЕ
	|	ТорговыеПлощадкиПотребители.Потребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|	И ТорговыеПлощадкиПотребители.Ссылка В
	|			(ВЫБРАТЬ
	|				втПотребителиОсн.ТорговаяПлощадка КАК ТорговаяПлощадка
	|			ИЗ
	|				втПотребителиОсн КАК втПотребителиОсн)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПотребители.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПривязкаСчетчиковСрезПоследних.Счетчик КАК Счетчик
	|ПОМЕСТИТЬ втСоСчетчиками
	|ИЗ
	|	втПотребители КАК втПотребители
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПривязкаСчетчиков.СрезПоследних(&Период, ) КАК ПривязкаСчетчиковСрезПоследних
	|		ПО втПотребители.ТорговаяПлощадка = ПривязкаСчетчиковСрезПоследних.ТорговаяПлощадка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втСоСчетчиками.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	втСоСчетчиками.Счетчик КАК Счетчик,
	|	ЕСТЬNULL(ПредыдущиеПоказанияСчетчиковСрезПоследних.Показания, 0) КАК ПоказанияПредыдущие,
	|	ЕСТЬNULL(ТекущиеПоказанияСчетчиковСрезПоследних.Показания, 0) КАК ПоказанияТекущие,
	|	ЕСТЬNULL(ТекущиеПоказанияСчетчиковСрезПоследних.Показания, 0) - ЕСТЬNULL(ПредыдущиеПоказанияСчетчиковСрезПоследних.Показания, 0) КАК Разность,
	|	9999999999 КАК Потребление
	|ИЗ
	|	втСоСчетчиками КАК втСоСчетчиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеПоказанияСчетчиков.СрезПоследних(&Период, ) КАК ТекущиеПоказанияСчетчиковСрезПоследних
	|		ПО втСоСчетчиками.Счетчик = ТекущиеПоказанияСчетчиковСрезПоследних.Счетчик
	|			И втСоСчетчиками.ТорговаяПлощадка = ТекущиеПоказанияСчетчиковСрезПоследних.ТорговаяПлощадка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредыдущиеПоказанияСчетчиков.СрезПоследних(&МоментВремени, ) КАК ПредыдущиеПоказанияСчетчиковСрезПоследних
	|		ПО втСоСчетчиками.Счетчик = ПредыдущиеПоказанияСчетчиковСрезПоследних.Счетчик
	|			И втСоСчетчиками.ТорговаяПлощадка = ПредыдущиеПоказанияСчетчиковСрезПоследних.ТорговаяПлощадка
	|
	|УПОРЯДОЧИТЬ ПО
	|	втСоСчетчиками.ТорговаяПлощадка.Наименование";
	
	Если Не ЕстьОтветственный Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И КоммунальныеУслугиПотребители.Ссылка.Ответственный В ИЕРАРХИИ(&Ответственный)","");
	КонецЕсли;	
	
	Возврат ТекстЗапроса;
КонецФункции

&НаСервере
Функция ЗаполнитьПоказания(ПодразделениеКомпании,Ответственный,Период,МоментВремени,ЭтоАттика)
	Объект.Показания.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоказания(ЭтоАттика,ЗначениеЗаполнено(Ответственный));
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	тзПоказания = Запрос.Выполнить().Выгрузить();
	Для Каждого Счетчик Из тзПоказания Цикл
		Счетчик.Потребление = Потребление(Счетчик.ПоказанияТекущие, Счетчик.ПоказанияПредыдущие);
		ЗаполнитьЗначенияСвойств(Объект.Показания.Добавить(),Счетчик);
		//нСтрока = Объект.Показания.Добавить();
		//ЗаполнитьЗначенияСвойств(нСтрока,Счетчик);
	КонецЦикла;
	
	Возврат тзПоказания; 
	
КонецФункции

&НаСервере
Процедура КомандаЗаполнитьДеревоУслугНаСервере()
	ЭтоАттика = ОбменФронтПовтИсп.ЭтоПодразделениеАттика(Объект.ПодразделениеКомпании);
	ПодразделениеКомпании = ОбменФронтПовтИсп.МассивПодразделений(Объект.ПодразделениеКомпании);
	Ответственный = Объект.Ответственный;
	Период = Объект.ОтчетныйМесяц;
	Если Параметры.Ключ.Пустая() Тогда
		МоментВремени = КонецМесяца(Период);
	Иначе
		МоментВремени = Новый Граница(Объект.Ссылка.МоментВремени(), ВидГраницы.Исключая);
	КонецЕсли;
	
	тзПоказания = ЗаполнитьПоказания(ПодразделениеКомпании,Ответственный,Период,МоментВремени,ЭтоАттика);
	
	_ДеревоУслуг = РеквизитФормыВЗначение("ДеревоУслуг");
	_ДеревоУслуг.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДерева(ЭтоАттика,ЗначениеЗаполнено(Ответственный));
	Запрос.УстановитьПараметр("тзПоказания", тзПоказания);
	Запрос.УстановитьПараметр("Период", КонецМесяца(Период));
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.УстановитьПараметр("ПодразделениеКомпании", ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();
	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаКомУслугаРодитель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКомУслугаРодитель.Следующий() Цикл
		стрУслуга = _ДеревоУслуг.Строки.Добавить();
		стрУслуга.Потребитель			= ВыборкаКомУслугаРодитель.КомУслугаРодитель;
		стрУслуга.ИндексКартинки		= ВыборкаКомУслугаРодитель.ИндексКартинки;
		стрУслуга.ИндексПотребителя		= 1;
		стрУслугаПотреблениеФакт		= 0;
		стрУслугаСуммаФакт				= 0;
		стрУслугаПотреблениеАкт			= 0;
		стрУслугаПотреблениеИтого		= 0;
		стрУслугаНачислено				= 0;
		стрУслугаАбонплата				= 0;
		стрУслугаСуммаКОплате			= 0;
		
		ВыборкаПотребитель = ВыборкаКомУслугаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПотребитель.Следующий() Цикл
			стрПотребитель = стрУслуга.Строки.Добавить();
			стрПотребитель.Потребитель			= ВыборкаПотребитель.ТорговаяПлощадка;
			стрПотребитель.ИндексКартинки		= ВыборкаПотребитель.ИндексКартинки;
			стрПотребитель.КодПотребителя		= ВыборкаПотребитель.ТорговаяПлощадкаКод;
			стрПотребитель.ИндексПотребителя	= 2;
			стрПотребительПотреблениеФакт		= 0;
			стрПотребительСуммаФакт				= 0;
			стрПотребительТариф					= 0;
			стрПотребительАбонплата				= 0;
			
			нВ = 0;
			ЕстьНашСуб = Ложь;
			ВыборкаКомУслуга = ВыборкаПотребитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			КоличествоВыборкаКомУслуга = ВыборкаКомУслуга.Количество();
			Пока ВыборкаКомУслуга.Следующий() Цикл
				нВ = 1 + нВ;
				мСубы = Новый Массив;
				
				Выборка = ВыборкаКомУслуга.Выбрать();
				КоличествоВыборка = Выборка.Количество();
				Пока Выборка.Следующий() Цикл
					ЭтоСуб = ЗначениеЗаполнено(Выборка.СубПотребитель);
					ЭтоЧужойСуб = Строка(Выборка.ТипСуба) = "Точки доставки";
					ЕстьНашСуб = ЕстьНашСуб ИЛИ (ЭтоСуб И НЕ ЭтоЧужойСуб);
					Если ЭтоЧужойСуб Тогда
						КоммунальнаяУслуга = Выборка.КоммунальнаяУслугаСуб;
					ИначеЕсли ЭтоСуб Тогда 
						КоммунальнаяУслуга = "";
					Иначе
						КоммунальнаяУслуга = Выборка.КоммунальнаяУслуга;
					КонецЕсли;
					ПотреблениеФакт = Выборка.ПотреблениеПоСчетчику;
					Если КоличествоВыборкаКомУслуга = 1 И Не ЭтоСуб Тогда
						стрПотребитель.КоммунальнаяУслуга	= Выборка.КоммунальнаяУслуга;
						стрПотребитель.ТорговаяПлощадка		= Выборка.ТорговаяПлощадка;
						стрПотребитель.СубПотребитель		= Выборка.СубПотребитель;
						стрПотребитель.КоммунальнаяУслугаСуб= Выборка.КоммунальнаяУслугаСуб;
						стрПотребитель.Тариф				= Выборка.Тариф;
						стрПотребитель.ПотреблениеФакт		= ПотреблениеФакт;
						стрПотребитель.СуммаФакт			= стрПотребитель.ПотреблениеФакт * Выборка.Тариф;
						стрПотребитель.ПотреблениеАкт		= ПотреблениеФакт;
						стрПотребитель.ПотреблениеИтого		= стрПотребитель.ПотреблениеАкт;
						стрПотребитель.Начислено			= стрПотребитель.ПотреблениеАкт * Выборка.Тариф;
						стрПотребитель.Абонплата			= Выборка.Абонплата;
						стрПотребитель.СуммаКОплате			= стрПотребитель.Начислено + стрПотребитель.Абонплата;
						стрПотребитель.СуммаЗатрат			= стрПотребитель.СуммаКОплате;
						стрПотребитель.ПотреблениеПоСчетчику= ПотреблениеФакт;
						
						стрПотребительПотреблениеФакт		= стрПотребительПотреблениеФакт + ПотреблениеФакт;
						стрПотребительСуммаФакт				= стрПотребительСуммаФакт + стрПотребитель.СуммаФакт;
						стрПотребительАбонплата				= стрПотребительАбонплата + стрПотребитель.Абонплата;
						стрУслугаПотреблениеАкт				= стрУслугаПотреблениеАкт	+ ПотреблениеФакт;
						Продолжить;
					Иначе
						Если ЭтоСуб И нВ <> КоличествоВыборкаКомУслуга Тогда
							Продолжить;
						КонецЕсли;
						стрСубПотребитель = стрПотребитель.Строки.Добавить();
						стрСубПотребитель.КоммунальнаяУслуга	= КоммунальнаяУслуга;
						стрСубПотребитель.ТорговаяПлощадка		= Выборка.ТорговаяПлощадка;
						стрСубПотребитель.КоммунальнаяУслугаСуб	= Выборка.КоммунальнаяУслугаСуб;
						стрСубПотребитель.СубПотребитель		= Выборка.СубПотребитель;
						стрСубПотребитель.Потребитель			= ?(ЭтоСуб,Выборка.СубПотребитель,Выборка.ТорговаяПлощадка);
						стрСубПотребитель.КодПотребителя		= Выборка.КодПотребителя;
						стрСубПотребитель.ПотреблениеФакт		= ПотреблениеФакт;
						стрСубПотребитель.Тариф					= ?(ЭтоСуб И НЕ ЭтоЧужойСуб И КоличествоВыборкаКомУслуга > 1,стрПотребительТариф,Выборка.Тариф);
						стрСубПотребитель.СуммаФакт				= стрСубПотребитель.ПотреблениеФакт * стрСубПотребитель.Тариф;
						стрСубПотребитель.ИндексКартинки		= Выборка.ИндексКартинки;
						стрСубПотребитель.ИндексПотребителя		= Выборка.ИндексКартинки;
						стрСубПотребитель.ПотреблениеПоСчетчику	= ПотреблениеФакт;
						
						
						Если ЭтоСуб Тогда
							стрПотребительПотреблениеФакт		= стрПотребительПотреблениеФакт - ПотреблениеФакт;
							стрПотребительСуммаФакт				= стрПотребительСуммаФакт - стрСубПотребитель.СуммаФакт;
							
							Если ТипЗнч(Выборка.СубПотребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") Тогда
								мСубы.Добавить(стрСубПотребитель);
							КонецЕсли;
						Иначе
							стрСубПотребитель.ПотреблениеАкт	= ПотреблениеФакт;
							стрСубПотребитель.ПотреблениеИтого	= стрСубПотребитель.ПотреблениеАкт;
							стрСубПотребитель.Начислено			= стрСубПотребитель.ПотреблениеАкт * Выборка.Тариф;
							стрСубПотребитель.Абонплата			= Выборка.Абонплата;
							стрСубПотребитель.СуммаКОплате		= стрСубПотребитель.Начислено + стрСубПотребитель.Абонплата;
							стрСубПотребитель.СуммаЗатрат		= стрСубПотребитель.СуммаКОплате;
							
							стрПотребительСуммаФакт			= стрПотребительСуммаФакт + стрСубПотребитель.СуммаФакт;
							стрПотребительТариф				= стрПотребительТариф + Выборка.Тариф;
							стрПотребительАбонплата			= стрПотребительАбонплата + Выборка.Абонплата;
							
							Если нВ = КоличествоВыборкаКомУслуга Тогда
								стрПотребительПотреблениеФакт = стрПотребительПотреблениеФакт + ПотреблениеФакт;
								
								стрПотребитель.ПотреблениеАкт	= ПотреблениеФакт;
								стрПотребитель.ПотреблениеИтого	= стрПотребитель.ПотреблениеАкт;
								стрПотребитель.Начислено		= стрПотребитель.ПотреблениеАкт * стрПотребительТариф;
								стрПотребитель.Абонплата		= стрПотребительАбонплата;
								стрПотребитель.СуммаКОплате		= стрПотребитель.Начислено + стрПотребитель.Абонплата;
								стрПотребитель.СуммаЗатрат		= стрПотребитель.СуммаКОплате;
								стрПотребитель.ПотреблениеПоСчетчику= ПотреблениеФакт;
								
								стрУслугаПотреблениеАкт		= стрУслугаПотреблениеАкт + ПотреблениеФакт;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Если КоличествоВыборкаКомУслуга <> 1 Тогда
				стрПотребитель.Тариф	= стрПотребительТариф;
			КонецЕсли;
			Если ЭтоСуб ИЛИ КоличествоВыборкаКомУслуга <> 1 Тогда
				стрПотребитель.ПотреблениеФакт	= стрПотребительПотреблениеФакт;
				стрПотребитель.СуммаФакт		= стрПотребительСуммаФакт;
				стрПотребитель.ЕстьНашСуб		= ЕстьНашСуб;
			КонецЕсли;
			
			стрУслугаПотреблениеИтого	= стрУслугаПотреблениеАкт;
			стрУслугаНачислено			= стрУслугаНачислено		+ стрПотребитель.Начислено;
			стрУслугаАбонплата			= стрУслугаАбонплата		+ стрПотребительАбонплата;
			стрУслугаСуммаКОплате		= стрУслугаСуммаКОплате		+ стрПотребитель.СуммаКОплате;
			стрУслугаПотреблениеФакт	= стрУслугаПотреблениеАкт;
			стрУслугаСуммаФакт			= стрУслугаНачислено;
			
			Если ЭтоСуб Тогда
				иПотреблениеФакт = стрПотребитель.ПотреблениеФакт;
				иСуммаЗатрат = стрПотребитель.СуммаЗатрат;	
				Для Каждого Суб Из мСубы Цикл
					иПотреблениеФакт = иПотреблениеФакт + Суб.ПотреблениеФакт;
				КонецЦикла;
				Для Каждого Суб Из мСубы Цикл
					Суб.СуммаЗатрат = ?(иПотреблениеФакт = 0,0,иСуммаЗатрат * Суб.ПотреблениеФакт / иПотреблениеФакт);
				КонецЦикла;
				стрПотребитель.СуммаЗатрат = ?(иПотреблениеФакт = 0,0,иСуммаЗатрат * стрПотребитель.ПотреблениеФакт / иПотреблениеФакт);
				
				Если КоличествоВыборкаКомУслуга <> 1 Тогда
					Для инд = 1 ПО КоличествоВыборкаКомУслуга Цикл
						ТекСтрока = стрПотребитель.Строки[инд - 1];
						ТекСтрока.ПотреблениеФакт = стрПотребительПотреблениеФакт;
						ТекСтрока.СуммаФакт = стрПотребительПотреблениеФакт * ТекСтрока.Тариф;
						//ТекСтрока.ПотреблениеФакт = 0;
						//ТекСтрока.СуммаФакт = 0;
						ТекСтрока.СуммаЗатрат = ?(стрПотребитель.СуммаКОплате = 0,0,ТекСтрока.СуммаКОплате * стрПотребитель.СуммаЗатрат / стрПотребитель.СуммаКОплате);
						ТекСтрока.ЕстьНашСуб = ЕстьНашСуб;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		стрУслуга.ПотреблениеФакт		= стрУслугаПотреблениеФакт;
		стрУслуга.СуммаФакт				= стрУслугаСуммаФакт;
		стрУслуга.Тариф					= ?(стрУслугаПотреблениеФакт = 0,0,стрУслугаСуммаФакт / стрУслугаПотреблениеФакт);
		стрУслуга.ПотреблениеАкт		= стрУслугаПотреблениеАкт;
		стрУслуга.ПотреблениеИтого		= стрУслугаПотреблениеИтого;
		стрУслуга.Начислено				= стрУслугаНачислено;
		стрУслуга.Абонплата				= стрУслугаАбонплата;
		стрУслуга.СуммаКОплате			= стрУслугаСуммаКОплате;
		стрУслуга.СуммаЗатрат			= стрУслугаСуммаКОплате;
		стрУслуга.ПотреблениеПоСчетчику	= стрУслугаПотреблениеАкт;
		стрУслуга.ПотреблениеПоСчетчику	= стрУслугаПотреблениеАкт;
	КонецЦикла;
	
	//Элементы.ДеревоУслугПотреблениеФакт.ТекстПодвала	= _ДеревоУслуг.Строки.Итог("ПотреблениеФакт");
	//Элементы.ДеревоУслугСуммаФакт.ТекстПодвала			= _ДеревоУслуг.Строки.Итог("СуммаФакт");
	//Элементы.ДеревоУслугПотреблениеАкт.ТекстПодвала		= _ДеревоУслуг.Строки.Итог("ПотреблениеАкт");
	//Элементы.ДеревоУслугПотери.ТекстПодвала				= _ДеревоУслуг.Строки.Итог("Потери");
	//Элементы.ДеревоУслугПотреблениеИтого.ТекстПодвала	= _ДеревоУслуг.Строки.Итог("ПотреблениеИтого");
	//Элементы.ДеревоУслугНачислено.ТекстПодвала			= _ДеревоУслуг.Строки.Итог("Начислено");
	//Элементы.ДеревоУслугАбонплата.ТекстПодвала			= _ДеревоУслуг.Строки.Итог("Абонплата");
	//Элементы.ДеревоУслугПредоплата.ТекстПодвала			= _ДеревоУслуг.Строки.Итог("Предоплата");
	//Элементы.ДеревоУслугКомиссия.ТекстПодвала			= _ДеревоУслуг.Строки.Итог("Комиссия");
	//Элементы.ДеревоУслугСуммаКОплате.ТекстПодвала		= _ДеревоУслуг.Строки.Итог("СуммаКОплате");
	//
	//ссссс = _ДеревоУслуг.Строки.Вставить(0);
	//ссссс.ИндексПотребителя = 1;
	//ссссс.ИндексКартинки = -1;
	
	ЗначениеВРеквизитФормы(_ДеревоУслуг, "ДеревоУслуг");
КонецПроцедуры

&НаСервереБезКонтекста
Функция Потребление(ПоказанияТекущие, ПоказанияПредыдущие)
	Если ПоказанияПредыдущие > ПоказанияТекущие Тогда
		РазрядностьСчетчика = СтрДлина(Формат(ПоказанияПредыдущие,"ЧДЦ=0; ЧГ=0"));
		МаксПоказания = Pow(10,РазрядностьСчетчика);
		Потребление = МаксПоказания - ПоказанияПредыдущие + ПоказанияТекущие;
	Иначе
		Потребление = ПоказанияТекущие - ПоказанияПредыдущие;
	КонецЕсли;	
	
	Возврат Потребление; 
КонецФункции

&НаСервере
Процедура СформироватьДеревоАттика()
	
	_ДеревоУслуг = РеквизитФормыВЗначение("ДеревоУслуг");
	_ДеревоУслуг.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	тзПоказания.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	тзПоказания.Счетчик КАК Счетчик,
	|	тзПоказания.ПоказанияПредыдущие КАК ПоказанияПредыдущие,
	|	тзПоказания.ПоказанияТекущие КАК ПоказанияТекущие,
	|	тзПоказания.Потребление КАК Потребление
	|ПОМЕСТИТЬ втПоказания
	|ИЗ
	|	&тзПоказания КАК тзПоказания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ТЧ.КоммунальнаяУслуга КАК Справочник.КоммунальныеУслуги) КАК КоммунальнаяУслуга,
	|	ВЫРАЗИТЬ(ТЧ.ТорговаяПлощадка КАК Справочник.ТорговыеПлощадки) КАК ТорговаяПлощадка,
	|	ТЧ.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|	ТЧ.СубПотребитель КАК СубПотребитель,
	|	ТЧ.Тариф КАК Тариф,
	|	ТЧ.ПотреблениеФакт КАК ПотреблениеФакт,
	|	ТЧ.СуммаФакт КАК СуммаФакт,
	|	ТЧ.ПотреблениеАкт КАК ПотреблениеАкт,
	|	ТЧ.Потери КАК Потери,
	|	ТЧ.ПотреблениеИтого КАК ПотреблениеИтого,
	|	ТЧ.Начислено КАК Начислено,
	|	ТЧ.Абонплата КАК Абонплата,
	|	ТЧ.Предоплата КАК Предоплата,
	|	ТЧ.СуммаКОплате КАК СуммаКОплате,
	|	ТЧ.Комиссия КАК Комиссия,
	|	ТЧ.СуммаЗатрат КАК СуммаЗатрат
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЧ КАК ТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Доноры.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|	Субы.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	Субы.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|	Субы.СубПотребитель КАК СубПотребитель,
	|	Субы.Тариф КАК Тариф,
	|	0 КАК ПотреблениеФакт,
	|	0 КАК СуммаФакт,
	|	Субы.ПотреблениеАкт КАК ПотреблениеАкт,
	|	Субы.Потери КАК Потери,
	|	Субы.ПотреблениеИтого КАК ПотреблениеИтого,
	|	Субы.Начислено КАК Начислено,
	|	Субы.Абонплата КАК Абонплата,
	|	Субы.Предоплата КАК Предоплата,
	|	Субы.СуммаКОплате КАК СуммаКОплате,
	|	Субы.Комиссия КАК Комиссия,
	|	Субы.СуммаЗатрат КАК СуммаЗатрат,
	|	Субы.ПотреблениеФакт КАК ПотреблениеФактСуб,
	|	Субы.СуммаФакт КАК СуммаФактСуб
	|ПОМЕСТИТЬ втПотребители
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|		ВТ.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|		ВТ.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|		ВТ.СубПотребитель КАК СубПотребитель,
	|		ВТ.Тариф КАК Тариф,
	|		ВТ.ПотреблениеФакт КАК ПотреблениеФакт,
	|		ВТ.СуммаФакт КАК СуммаФакт,
	|		ВТ.ПотреблениеАкт КАК ПотреблениеАкт,
	|		ВТ.Потери КАК Потери,
	|		ВТ.ПотреблениеИтого КАК ПотреблениеИтого,
	|		ВТ.Начислено КАК Начислено,
	|		ВТ.Абонплата КАК Абонплата,
	|		ВТ.Предоплата КАК Предоплата,
	|		ВТ.СуммаКОплате КАК СуммаКОплате,
	|		ВТ.Комиссия КАК Комиссия,
	|		ВТ.СуммаЗатрат КАК СуммаЗатрат
	|	ИЗ
	|		ВТ КАК ВТ
	|	ГДЕ
	|		ВТ.СубПотребитель <> НЕОПРЕДЕЛЕНО) КАК Субы
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|			ВТ.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|			ВТ.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|			ВТ.СубПотребитель КАК СубПотребитель,
	|			ВТ.Тариф КАК Тариф,
	|			ВТ.ПотреблениеФакт КАК ПотреблениеФакт,
	|			ВТ.СуммаФакт КАК СуммаФакт,
	|			ВТ.ПотреблениеАкт КАК ПотреблениеАкт,
	|			ВТ.Потери КАК Потери,
	|			ВТ.ПотреблениеИтого КАК ПотреблениеИтого,
	|			ВТ.Начислено КАК Начислено,
	|			ВТ.Абонплата КАК Абонплата,
	|			ВТ.Предоплата КАК Предоплата,
	|			ВТ.СуммаКОплате КАК СуммаКОплате,
	|			ВТ.Комиссия КАК Комиссия,
	|			ВТ.СуммаЗатрат КАК СуммаЗатрат
	|		ИЗ
	|			ВТ КАК ВТ
	|		ГДЕ
	|			ВТ.СубПотребитель = НЕОПРЕДЕЛЕНО) КАК Доноры
	|		ПО Субы.ТорговаяПлощадка = Доноры.ТорговаяПлощадка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ.КоммунальнаяУслуга,
	|	ВТ.ТорговаяПлощадка,
	|	ВТ.КоммунальнаяУслугаСуб,
	|	ВТ.СубПотребитель,
	|	ВТ.Тариф,
	|	ВТ.ПотреблениеФакт,
	|	ВТ.СуммаФакт,
	|	ВТ.ПотреблениеАкт,
	|	ВТ.Потери,
	|	ВТ.ПотреблениеИтого,
	|	ВТ.Начислено,
	|	ВТ.Абонплата,
	|	ВТ.Предоплата,
	|	ВТ.СуммаКОплате,
	|	ВТ.Комиссия,
	|	ВТ.СуммаЗатрат,
	|	0,
	|	0
	|ИЗ
	|	ВТ КАК ВТ
	|ГДЕ
	|	ВТ.СубПотребитель = НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втПотребители.КоммунальнаяУслуга.Родитель КАК КомУслугаРодитель,
	|	втПотребители.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	втПотребители.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|	втПотребители.СубПотребитель КАК СубПотребитель,
	|	втПотребители.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|	втПотребители.Тариф КАК Тариф,
	|	втПотребители.ПотреблениеФакт КАК ПотреблениеФакт,
	|	втПотребители.СуммаФакт КАК СуммаФакт,
	|	втПотребители.ПотреблениеАкт КАК ПотреблениеАкт,
	|	втПотребители.Потери КАК Потери,
	|	втПотребители.ПотреблениеИтого КАК ПотреблениеИтого,
	|	втПотребители.Начислено КАК Начислено,
	|	втПотребители.Абонплата КАК Абонплата,
	|	втПотребители.Предоплата КАК Предоплата,
	|	втПотребители.СуммаКОплате КАК СуммаКОплате,
	|	втПотребители.Комиссия КАК Комиссия,
	|	втПотребители.СуммаЗатрат КАК СуммаЗатрат,
	|	ВЫБОР
	|		КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|			ТОГДА 8
	|		КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
	|			ТОГДА 5
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА втПотребители.СубПотребитель = НЕОПРЕДЕЛЕНО
	|			ТОГДА втПотребители.ТорговаяПлощадка.Код
	|		ИНАЧЕ ВЫБОР
	|				КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|					ТОГДА ВЫРАЗИТЬ(втПотребители.СубПотребитель КАК Справочник.ТорговыеПлощадки).Код
	|				КОГДА втПотребители.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
	|					ТОГДА ВЫРАЗИТЬ(втПотребители.СубПотребитель КАК Справочник.ТочкиДоставки).Global_ID
	|				ИНАЧЕ ""----""
	|			КОНЕЦ
	|	КОНЕЦ КАК КодПотребителя,
	|	ТИПЗНАЧЕНИЯ(втПотребители.СубПотребитель) КАК ТипСуба,
	|	ЕСТЬNULL(взПотребление.Потребление, 0) КАК ПотреблениеПоСчетчику,
	|	втПотребители.ПотреблениеФактСуб КАК ПотреблениеФактСуб,
	|	втПотребители.СуммаФактСуб КАК СуммаФактСуб,
	|	втПотребители.ТорговаяПлощадка.Код КАК ТорговаяПлощадкаКод
	|ИЗ
	|	втПотребители КАК втПотребители
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			втПоказания.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|			СУММА(втПоказания.Потребление) КАК Потребление
	|		ИЗ
	|			втПоказания КАК втПоказания
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втПоказания.ТорговаяПлощадка) КАК взПотребление
	|		ПО (ВЫБОР
	|				КОГДА втПотребители.СубПотребитель = НЕОПРЕДЕЛЕНО
	|					ТОГДА втПотребители.ТорговаяПлощадка
	|				ИНАЧЕ втПотребители.СубПотребитель
	|			КОНЕЦ = взПотребление.ТорговаяПлощадка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КомУслугаРодитель,
	|	втПотребители.ТорговаяПлощадка.Наименование,
	|	ТипСуба
	|ИТОГИ
	|	МАКСИМУМ(ПотреблениеФакт),
	|	СУММА(СуммаФакт),
	|	МАКСИМУМ(ПотреблениеАкт),
	|	МАКСИМУМ(Потери),
	|	МАКСИМУМ(ПотреблениеИтого),
	|	СУММА(Начислено),
	|	СУММА(Абонплата),
	|	СУММА(Предоплата),
	|	СУММА(СуммаКОплате),
	|	СУММА(Комиссия),
	|	МИНИМУМ(ИндексКартинки),
	|	МАКСИМУМ(ПотреблениеПоСчетчику),
	|	МАКСИМУМ(ПотреблениеФактСуб),
	|	СУММА(СуммаФактСуб)
	|ПО
	|	КомУслугаРодитель,
	|	ТорговаяПлощадка,
	|	КоммунальнаяУслуга";
	
	Запрос.УстановитьПараметр("ТЧ", Объект.Услуги.Выгрузить());
	Запрос.УстановитьПараметр("тзПоказания", Объект.Показания.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаКомУслугаРодитель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКомУслугаРодитель.Следующий() Цикл
		стрУслуга = _ДеревоУслуг.Строки.Добавить();
		стрУслуга.Потребитель			= ВыборкаКомУслугаРодитель.КомУслугаРодитель;
		стрУслуга.ИндексКартинки		= ВыборкаКомУслугаРодитель.ИндексКартинки;
		стрУслуга.ИндексПотребителя		= 1;
		ЗаполнитьЗначенияСвойств(стрУслуга,ВыборкаКомУслугаРодитель,"Начислено,Абонплата,Предоплата,СуммаКОплате,Комиссия");
		
		стрУслугаПотреблениеФактСуб		= 0;
		стрУслугаСуммаФактСуб			= 0;
		стрУслугаСуммаЗатрат			= 0;
		
		ВыборкаПотребитель = ВыборкаКомУслугаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПотребитель.Следующий() Цикл
			стрПотребитель = стрУслуга.Строки.Добавить();
			стрПотребитель.Потребитель			= ВыборкаПотребитель.ТорговаяПлощадка;
			стрПотребитель.КодПотребителя		= ВыборкаПотребитель.ТорговаяПлощадкаКод;
			стрПотребитель.ИндексКартинки		= ВыборкаПотребитель.ИндексКартинки;
			стрПотребитель.ИндексПотребителя	= 2;
			ЗаполнитьЗначенияСвойств(стрПотребитель,ВыборкаПотребитель,,"ТорговаяПлощадка,КодПотребителя");
			
			стрПотребительТариф					= 0;
			стрПотребительСуммаЗатратСуб		= 0;
			нВ = 0;
			ЕстьНашСуб = Ложь;
			ВыборкаКомУслуга = ВыборкаПотребитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			КоличествоВыборкаКомУслуга = ВыборкаКомУслуга.Количество();
			Пока ВыборкаКомУслуга.Следующий() Цикл
				нВ = 1 + нВ;
				мСубы = Новый Массив;
				
				Выборка = ВыборкаКомУслуга.Выбрать();
				КоличествоВыборка = Выборка.Количество();
				Пока Выборка.Следующий() Цикл
					ЭтоСуб = ЗначениеЗаполнено(Выборка.СубПотребитель);
					ЭтоЧужойСуб = Строка(Выборка.ТипСуба) = "Точки доставки";
					ЕстьНашСуб = ЕстьНашСуб ИЛИ (ЭтоСуб И НЕ ЭтоЧужойСуб);
					Если ЭтоЧужойСуб Тогда
						КоммунальнаяУслуга = Выборка.КоммунальнаяУслугаСуб;
					ИначеЕсли ЭтоСуб Тогда 
						КоммунальнаяУслуга = "";
					Иначе
						КоммунальнаяУслуга = Выборка.КоммунальнаяУслуга;
					КонецЕсли;
					ПотреблениеФакт = Выборка.ПотреблениеПоСчетчику;
					Если КоличествоВыборкаКомУслуга = 1 И Не ЭтоСуб Тогда
						ЗаполнитьЗначенияСвойств(стрПотребитель,Выборка,"ТорговаяПлощадка,КодПотребителя,КоммунальнаяУслуга,СуммаЗатрат,Тариф");
						Продолжить;
					Иначе
						Если ЭтоСуб И нВ <> КоличествоВыборкаКомУслуга Тогда
							Продолжить;
						КонецЕсли;
						
						стрСубПотребитель = стрПотребитель.Строки.Добавить();
						ЗаполнитьЗначенияСвойств(стрСубПотребитель,Выборка);
						
						стрСубПотребитель.КоммунальнаяУслуга	= КоммунальнаяУслуга;
						стрСубПотребитель.Потребитель			= ?(ЭтоСуб,Выборка.СубПотребитель,Выборка.ТорговаяПлощадка);
						стрСубПотребитель.ИндексПотребителя		= Выборка.ИндексКартинки;
						стрСубПотребитель.ПотреблениеФакт		= ?(ЭтоСуб,Выборка.ПотреблениеФактСуб,Выборка.ПотреблениеФакт);
						стрСубПотребитель.СуммаФакт				= ?(ЭтоСуб,Выборка.СуммаФактСуб,Выборка.СуммаФакт);
						
						Если ЭтоСуб Тогда
							стрУслугаПотреблениеФактСуб		= стрУслугаПотреблениеФактСуб +?(ЭтоЧужойСуб,0,Выборка.ПотреблениеФактСуб);
							стрУслугаСуммаФактСуб			= стрУслугаСуммаФактСуб +?(ЭтоЧужойСуб,0,Выборка.СуммаФактСуб);
							стрПотребительСуммаЗатратСуб	= стрПотребительСуммаЗатратСуб +?(ЭтоЧужойСуб,0,Выборка.СуммаЗатрат);
						Иначе
							стрПотребительТариф				= стрПотребительТариф + Выборка.Тариф;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Если КоличествоВыборкаКомУслуга <> 1 Тогда
				стрПотребитель.Тариф	= стрПотребительТариф;
				стрПотребитель.СуммаЗатрат		= стрПотребитель.Строки.Итог("СуммаЗатрат") - ?(ЭтоСуб,стрПотребительСуммаЗатратСуб,0);		
				Если ЭтоСуб Тогда
					Для инд = 1 ПО КоличествоВыборкаКомУслуга Цикл
						ТекСтрока = стрПотребитель.Строки[инд - 1];
						ТекСтрока.ЕстьНашСуб = ЕстьНашСуб;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			Если ЭтоСуб ИЛИ КоличествоВыборкаКомУслуга <> 1 Тогда
				стрПотребитель.ЕстьНашСуб		= ЕстьНашСуб;
			КонецЕсли;
			
		КонецЦикла;
		
		стрУслуга.ПотреблениеФакт		= стрУслуга.Строки.Итог("ПотреблениеФакт") + стрУслугаПотреблениеФактСуб;
		стрУслуга.СуммаФакт				= стрУслуга.Строки.Итог("СуммаФакт") + стрУслугаСуммаФактСуб;
		стрУслуга.Тариф					= ?(стрУслуга.ПотреблениеФакт = 0,0,стрУслуга.СуммаФакт / стрУслуга.ПотреблениеФакт);
		стрУслуга.ПотреблениеАкт		= стрУслуга.Строки.Итог("ПотреблениеАкт");
		стрУслуга.ПотреблениеИтого		= стрУслуга.Строки.Итог("ПотреблениеИтого");
		стрУслуга.Потери				= стрУслуга.Строки.Итог("Потери");
		стрУслуга.СуммаЗатрат			= стрУслуга.Строки.Итог("СуммаЗатрат") + стрУслугаСуммаЗатрат;
		стрУслуга.ПотреблениеПоСчетчику	= стрУслуга.Строки.Итог("ПотреблениеПоСчетчику");
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(_ДеревоУслуг, "ДеревоУслуг");
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоУслуг()
	
	_ДеревоУслуг = РеквизитФормыВЗначение("ДеревоУслуг");
	_ДеревоУслуг.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|	ВЫРАЗИТЬ(ТЧ.ТорговаяПлощадка КАК Справочник.ТорговыеПлощадки) КАК ТорговаяПлощадка,
	|	ТЧ.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|	ТЧ.СубПотребитель КАК СубПотребитель,
	|	ТЧ.Тариф КАК Тариф,
	|	ТЧ.Начислено КАК Начислено,
	|	ТЧ.ПотреблениеФакт КАК ПотреблениеФакт,
	|	ТЧ.СуммаФакт КАК СуммаФакт,
	|	ТЧ.ПотреблениеАкт КАК ПотреблениеАкт,
	|	ТЧ.Потери КАК Потери,
	|	ТЧ.ПотреблениеИтого КАК ПотреблениеИтого,
	|	ТЧ.Абонплата КАК Абонплата,
	|	ТЧ.Предоплата КАК Предоплата,
	|	ТЧ.Комиссия КАК Комиссия,
	|	ТЧ.СуммаКОплате КАК СуммаКОплате
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЧ КАК ТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.КоммунальнаяУслуга КАК КоммунальнаяУслуга,
	|	ВТ.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ВТ.КоммунальнаяУслугаСуб КАК КоммунальнаяУслугаСуб,
	|	ВТ.СубПотребитель КАК СубПотребитель,
	|	ВТ.Тариф КАК Тариф,
	|	ВТ.Начислено КАК Начислено,
	|	ВТ.ПотреблениеФакт КАК ПотреблениеФакт,
	|	ВТ.СуммаФакт КАК СуммаФакт,
	|	ВТ.ПотреблениеАкт КАК ПотреблениеАкт,
	|	ВТ.Потери КАК Потери,
	|	ВТ.ПотреблениеИтого КАК ПотреблениеИтого,
	|	ВТ.Абонплата КАК Абонплата,
	|	ВТ.Предоплата КАК Предоплата,
	|	ВТ.Комиссия КАК Комиссия,
	|	ВТ.СуммаКОплате КАК СуммаКОплате,
	|	ВЫБОР
	|		КОГДА ВТ.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|			ТОГДА 8
	|		КОГДА ВТ.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
	|			ТОГДА 5
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВТ.ТорговаяПлощадка.Код КАК КодПотребителя,
	|	ВЫБОР
	|		КОГДА ВТ.СубПотребитель ССЫЛКА Справочник.ТорговыеПлощадки
	|			ТОГДА ВЫРАЗИТЬ(ВТ.СубПотребитель КАК Справочник.ТорговыеПлощадки).Код
	|		КОГДА ВТ.СубПотребитель ССЫЛКА Справочник.ТочкиДоставки
	|			ТОГДА ВЫРАЗИТЬ(ВТ.СубПотребитель КАК Справочник.ТочкиДоставки).Global_ID
	|		ИНАЧЕ ""----""
	|	КОНЕЦ КАК КодСуба
	|ИЗ
	|	ВТ КАК ВТ
	|ИТОГИ
	|	МИНИМУМ(ИндексКартинки)
	|ПО
	|	ТорговаяПлощадка,
	|	КоммунальнаяУслуга";
	
	Запрос.УстановитьПараметр("ТЧ", Объект.Услуги.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаКомУслуга = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКомУслуга.Следующий() Цикл
		стрУслуга = _ДеревоУслуг.Строки.Добавить();
		стрУслуга.Потребитель			= ВыборкаКомУслуга.КоммунальнаяУслуга;
		стрУслуга.ИндексПотребителя		= 1;
		стрУслуга.ИндексКартинки		= ВыборкаКомУслуга.ИндексКартинки;
		
		ВыборкаПотребитель = ВыборкаКомУслуга.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПотребитель.Следующий() Цикл
			стрПотребитель = стрУслуга.Строки.Добавить();
			стрПотребитель.Потребитель			= ВыборкаПотребитель.ТорговаяПлощадка;
			стрПотребитель.ИндексКартинки		= ВыборкаПотребитель.ИндексКартинки;
			стрПотребитель.ИндексПотребителя		= 2;
			
			ВыборкаСуб = ВыборкаПотребитель.Выбрать();
			Пока ВыборкаСуб.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаСуб.СубПотребитель) Тогда
					стрСубПотребитель = стрПотребитель.Строки.Добавить();
					стрСубПотребитель.Потребитель			= ВыборкаСуб.СубПотребитель;
					//стрСубПотребитель.Счетчик				= ВыборкаСуб.Счетчик;
					//стрСубПотребитель.ПоказанияПредыдущие	= ВыборкаСуб.ПоказанияПредыдущие;
					//стрСубПотребитель.ПоказанияТекущие		= ВыборкаСуб.ПоказанияТекущие;
					//стрСубПотребитель.ПотреблениеФакт		= ВыборкаСуб.ПоказанияТекущие - ВыборкаСуб.ПоказанияПредыдущие;
					стрСубПотребитель.Тариф					= ВыборкаСуб.Тариф;
					стрСубПотребитель.СуммаФакт				= ВыборкаСуб.ПотреблениеФакт * ВыборкаСуб.Тариф;
					стрСубПотребитель.КодПотребителя		= ВыборкаСуб.КодСуба;
					стрСубПотребитель.ИндексКартинки		= ВыборкаСуб.ИндексКартинки;
					стрСубПотребитель.ИндексПотребителя		= ВыборкаСуб.ИндексКартинки;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(_ДеревоУслуг, "ДеревоУслуг");
КонецПроцедуры


&НаСервере
Процедура СтрокаДеревоУслугПриИзменении(ТекущаяСтрока,ИмяКолонки)
	
	ТекущиеДанные = ДеревоУслуг.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если ИмяКолонки = "ДеревоУслугПотреблениеФакт" Тогда
		ТекущиеПотреблениеФакт = ТекущиеДанные.ПотреблениеФакт;
		ТекущиеСуммаФакт = ТекущиеПотреблениеФакт * ТекущиеДанные.Тариф; 
		ТекущиеДанные.СуммаФакт = ТекущиеСуммаФакт;
		
		СубПотреблениеФакт = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		РодительПотреблениеПоСчетчику = РодительТекущиеДанные.ПотреблениеПоСчетчику;
		РодительСуммаФактБыло = РодительТекущиеДанные.СуммаФакт;
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И НЕ ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				СубПотреблениеФакт = СубПотреблениеФакт + Строка.ПотреблениеФакт;
			КонецЕсли;
		КонецЦикла;
		РодительПотреблениеФакт = РодительПотреблениеПоСчетчику - СубПотреблениеФакт - ТекущиеПотреблениеФакт; 
		РодительТекущиеДанные.ПотреблениеФакт = РодительПотреблениеФакт; 
		РодительСуммаФактСтало = РодительПотреблениеФакт * РодительТекущиеДанные.Тариф;
		РодительТекущиеДанные.СуммаФакт = РодительСуммаФактСтало;
		
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				Строка.ПотреблениеФакт = РодительПотреблениеФакт;
				Строка.СуммаФакт = РодительПотреблениеФакт * Строка.Тариф
			КонецЕсли;
		КонецЦикла;
		
		РодительРодительТекущиеДанные = РодительТекущиеДанные.ПолучитьРодителя();
		РодительРодительТекущиеДанные.ПотреблениеФакт = РодительРодительТекущиеДанные.ПотреблениеПоСчетчику - ТекущиеПотреблениеФакт; 
		РодительРодительТекущиеДанные.СуммаФакт = РодительРодительТекущиеДанные.СуммаФакт - РодительСуммаФактБыло + РодительСуммаФактСтало;
		СтрокаДеревоУслугПриИзменении(РодительТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугСуммаЗатрат");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугПотреблениеАкт" Тогда
		ЭлементыТекущиеДанные = ТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыТекущиеДанные Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				Строка.ПотреблениеАкт = ТекущиеДанные.ПотреблениеАкт;
			КонецЕсли;
		КонецЦикла;
		
		рПотреблениеАкт = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рПотреблениеАкт = рПотреблениеАкт + Строка.ПотреблениеАкт;
		КонецЦикла;
		РодительТекущиеДанные.ПотреблениеАкт = рПотреблениеАкт;
		СтрокаДеревоУслугПриИзменении(ТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугПотреблениеИтогоИНачислено");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугПотери" Тогда
		ЭлементыТекущиеДанные = ТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыТекущиеДанные Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				Строка.Потери = ТекущиеДанные.Потери;
			КонецЕсли;
		КонецЦикла;
		рПотери = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рПотери = рПотери + Строка.Потери;
		КонецЦикла;
		РодительТекущиеДанные.Потери = рПотери;
		СтрокаДеревоУслугПриИзменении(ТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугПотреблениеИтогоИНачислено");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугАбонплата" Тогда
		рАбонплата = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рАбонплата = рАбонплата + Строка.Абонплата;
		КонецЦикла;
		РодительТекущиеДанные.Абонплата = рАбонплата;
		Если ТекущиеДанные.ИндексПотребителя = -1 Тогда
			ррАбонплата = 0;
			РодительРодителя = РодительТекущиеДанные.ПолучитьРодителя();
			ЭлементыРодительРодителя = РодительРодителя.ПолучитьЭлементы();
			Для Каждого Строка ИЗ ЭлементыРодительРодителя Цикл
				ррАбонплата = ррАбонплата + Строка.Абонплата;
			КонецЦикла;
			РодительРодителя.Предоплата = ррАбонплата;
		КонецЕсли;
		СтрокаДеревоУслугПриИзменении(ТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугСуммаКОплате");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугПредоплата" Тогда
		рПредоплата = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рПредоплата = рПредоплата + Строка.Предоплата;
		КонецЦикла;
		РодительТекущиеДанные.Предоплата = рПредоплата;
		Если ТекущиеДанные.ИндексПотребителя = -1 Тогда
			ррПредоплата = 0;
			РодительРодителя = РодительТекущиеДанные.ПолучитьРодителя();
			ЭлементыРодительРодителя = РодительРодителя.ПолучитьЭлементы();
			Для Каждого Строка ИЗ ЭлементыРодительРодителя Цикл
				ррПредоплата = ррПредоплата + Строка.Предоплата;
			КонецЦикла;
			РодительРодителя.Предоплата = ррПредоплата;
		КонецЕсли;
		СтрокаДеревоУслугПриИзменении(ТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугСуммаКОплате");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугКомиссия" Тогда
		рКомиссия = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рКомиссия = рКомиссия + Строка.Комиссия;
		КонецЦикла;
		РодительТекущиеДанные.Комиссия = рКомиссия;
		Если ТекущиеДанные.ИндексПотребителя = -1 Тогда
			ррКомиссия = 0;
			РодительРодителя = РодительТекущиеДанные.ПолучитьРодителя();
			ЭлементыРодительРодителя = РодительРодителя.ПолучитьЭлементы();
			Для Каждого Строка ИЗ ЭлементыРодительРодителя Цикл
				ррКомиссия = ррКомиссия + Строка.Комиссия;
			КонецЦикла;
			РодительРодителя.Комиссия = ррКомиссия;
		КонецЕсли;
		СтрокаДеревоУслугПриИзменении(?(ТекущиеДанные.ИндексПотребителя = 2,ТекущиеДанные.ПолучитьИдентификатор(),РодительТекущиеДанные.ПолучитьИдентификатор()),"ДеревоУслугСуммаЗатрат");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугПотреблениеИтогоИНачислено" Тогда
		ТекущиеДанныеНачисленоБыло = ТекущиеДанные.Начислено;
		ТекущиеДанные.ПотреблениеИтого = ТекущиеДанные.ПотреблениеАкт + ТекущиеДанные.Потери;
		ТекущиеДанные.Начислено = ТекущиеДанные.ПотреблениеИтого * ТекущиеДанные.Тариф;
		ЭлементыТекущиеДанные = ТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыТекущиеДанные Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				Строка.ПотреблениеИтого = ТекущиеДанные.ПотреблениеИтого;
				Строка.Начислено = ТекущиеДанные.ПотреблениеИтого * Строка.Тариф;
			КонецЕсли;
		КонецЦикла;
		
		рПотреблениеАкт = 0;
		рПотери = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рПотреблениеАкт = рПотреблениеАкт + Строка.ПотреблениеАкт;
			рПотери = рПотери + Строка.Потери;
		КонецЦикла;
		рПотреблениеИтого = рПотреблениеАкт + рПотери;
		РодительТекущиеДанные.ПотреблениеАкт = рПотреблениеАкт; 
		РодительТекущиеДанные.Потери = рПотери;
		РодительТекущиеДанные.ПотреблениеИтого = рПотреблениеИтого;
		РодительТекущиеДанные.Начислено = РодительТекущиеДанные.Начислено - ТекущиеДанныеНачисленоБыло + ТекущиеДанные.Начислено;
		СтрокаДеревоУслугПриИзменении(ТекущиеДанные.ПолучитьИдентификатор(),"ДеревоУслугСуммаКОплате");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугСуммаКОплате" Тогда
		ТекущиеДанные.СуммаКОплате = ТекущиеДанные.Начислено + ТекущиеДанные.Абонплата + ТекущиеДанные.Предоплата;
		ЭлементыТекущиеДанные = ТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыТекущиеДанные Цикл
			Если ТипЗнч(Строка.Потребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") И ЗначениеЗаполнено(Строка.КоммунальнаяУслуга) Тогда
				Строка.СуммаКОплате = Строка.Начислено + Строка.Абонплата + Строка.Предоплата;
			КонецЕсли;
		КонецЦикла;
		
		рНачислено = 0;
		рАбонплата = 0;
		рПредоплата = 0;
		РодительТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
		ЭлементыРодитель = РодительТекущиеДанные.ПолучитьЭлементы();
		Для Каждого Строка ИЗ ЭлементыРодитель Цикл
			рНачислено = рНачислено + Строка.Начислено;
			рАбонплата = рАбонплата + Строка.Абонплата;
			рПредоплата = рПредоплата + Строка.Предоплата;
		КонецЦикла;
		рСуммаКОплате = рНачислено + рАбонплата + рПредоплата;
		РодительТекущиеДанные.Абонплата = рАбонплата; 
		РодительТекущиеДанные.Предоплата = рПредоплата;
		РодительТекущиеДанные.СуммаКОплате = рСуммаКОплате;
		СтрокаДеревоУслугПриИзменении(?(ТекущиеДанные.ИндексПотребителя = 2,ТекущиеДанные.ПолучитьИдентификатор(),РодительТекущиеДанные.ПолучитьИдентификатор()),"ДеревоУслугСуммаЗатрат");
		
	ИначеЕсли ИмяКолонки = "ДеревоУслугСуммаЗатрат" Тогда
		ЕстьНашСуб = ТекущиеДанные.ЕстьНашСуб;
		Ур1 = ТекущиеДанные.ПолучитьРодителя();
		Ур1.СуммаЗатрат = Ур1.СуммаКОплате + Ур1.Комиссия;
		Строки = ТекущиеДанные.ПолучитьЭлементы();
		
		Если ЕстьНашСуб Тогда
			мСубы = Новый Массив;
			иСуммаЗатрат = ТекущиеДанные.СуммаКОплате + ТекущиеДанные.Комиссия;	
			мСубы.Добавить(ТекущиеДанные);
		Иначе
			ТекущиеДанные.СуммаЗатрат = ТекущиеДанные.СуммаКОплате + ТекущиеДанные.Комиссия;
		КонецЕсли;
		
		Для Каждого Строка ИЗ Строки Цикл
			Если ЕстьНашСуб Тогда
				Если ТипЗнч(Строка.СубПотребитель) = Тип("СправочникСсылка.ТорговыеПлощадки") Тогда
					мСубы.Добавить(Строка);
				КонецЕсли;
			Иначе
				Строка.СуммаЗатрат = Строка.СуммаКОплате + Строка.Комиссия;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьНашСуб Тогда
			иПотреблениеФакт = 0;
			Для Каждого Суб Из мСубы Цикл
				иПотреблениеФакт = иПотреблениеФакт + Суб.ПотреблениеФакт;
			КонецЦикла;
			Для Каждого Суб Из мСубы Цикл
				Суб.СуммаЗатрат = иСуммаЗатрат * Суб.ПотреблениеФакт / иПотреблениеФакт;
			КонецЦикла;
			Для Каждого Строка ИЗ Строки Цикл
				Если Строка.ЕстьНашСуб Тогда
					Строка.СуммаЗатрат = (Строка.СуммаКОплате + Строка.Комиссия) * ТекущиеДанные.СуммаЗатрат / (ТекущиеДанные.СуммаКОплате + ТекущиеДанные.Комиссия)	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДеревоУслугВТЧУслуги()
	
	Объект.Услуги.Очистить();
	
	Для Каждого СтрокаДерева Из ДеревоУслуг.ПолучитьЭлементы() Цикл 
		ЗаписатьСтрокуДерева(Объект.Услуги, СтрокаДерева);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСтрокуДерева(ТЧ,СтрокаДерева)
	
	// проверим, а надо ли записывать
	Если ЗначениеЗаполнено(СтрокаДерева.ТорговаяПлощадка) Тогда
		НоваяСтрока = ТЧ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаДерева);
		//Если ЗначениеЗаполнено(СтрокаДерева.СубПотребитель) Тогда
		//	НоваяСтрока.КоммунальнаяУслуга = СтрокаДерева.ПолучитьРодителя().КоммунальнаяУслуга;	
		//КонецЕсли;
	КонецЕсли;
	
	// подчиненные строки
	Для Каждого СтрокаСтрокиДерева Из СтрокаДерева.ПолучитьЭлементы() Цикл 
		ЗаписатьСтрокуДерева(ТЧ, СтрокаСтрокиДерева);
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьТабДок(Команда)
	Элементы.ГруппаТабДок.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТабДок;
	ТабДок.Область("R1C3:R1C3").Текст = Элементы.ИмяПоляДерева.СписокВыбора.НайтиПоЗначению(ИмяПоляДерева).Представление;
КонецПроцедуры

&НаСервере
Процедура КомандаЗагрузитьВыбранноеПолеНаСервере()
	ИмяКолонкиДерева = Элементы.ИмяПоляДерева.СписокВыбора.НайтиПоЗначению(ИмяПоляДерева).Представление;
	ПЗ = Новый ПостроительЗапроса;
	ПЗ.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область());
	//ПЗ.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	//ПЗ.ЗаполнитьНастройки();
	ПЗ.Выполнить();
	ТЗ = ПЗ.Результат.Выгрузить();
	
	Ур1 = ДеревоУслуг.ПолучитьЭлементы();
	Для Каждого стрУр1 из Ур1 Цикл
		Ур2 = стрУр1.ПолучитьЭлементы();
		Для Каждого стрУр2 из Ур2 Цикл
			Если ИмяПоляДерева = "ДеревоУслугПотреблениеФакт" Тогда
				Ур3 = стрУр2.ПолучитьЭлементы();
				Для Каждого стрУр3 из Ур3 Цикл
					строкаТЗ = ТЗ.Найти(Строка(стрУр3.Потребитель.Global_ID),"Код"); 
					Если строкаТЗ <> Неопределено Тогда
						стрУр3[ИмяКолонкиДерева] = строкаТЗ[ИмяКолонкиДерева];
						СтрокаДеревоУслугПриИзменении(стрУр3.ПолучитьИдентификатор(),ИмяПоляДерева);
						ТЗ.Удалить(строкаТЗ);
						Если ТЗ.Количество() = 0 Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				строкаТЗ = ТЗ.Найти(стрУр2.Потребитель.Код,"Код"); 
				Если строкаТЗ <> Неопределено Тогда
					стрУр2[ИмяКолонкиДерева] = строкаТЗ[ИмяКолонкиДерева];
					СтрокаДеревоУслугПриИзменении(стрУр2.ПолучитьИдентификатор(),ИмяПоляДерева);
					ТЗ.Удалить(строкаТЗ);
					Если ТЗ.Количество() = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Если ТЗ.Количество() > 0 Тогда
		Для Каждого стр из ТЗ Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не найден /%1/ - %2  / %3 /'; uk = 'Не знайдений /%1/ - %2  / %3 /'"),
			стр.Код,
			стр.Объект,
			стр[ИмяКолонкиДерева]));
		КонецЦикла;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Готово!!!");
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьВыбранноеПоле(Команда)
	КомандаЗагрузитьВыбранноеПолеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИмяПоляДереваПриИзменении(Элемент)
	ТабДок.Область("R1C3:R1C3").Текст = Элементы.ИмяПоляДерева.СписокВыбора.НайтиПоЗначению(ИмяПоляДерева).Представление;
КонецПроцедуры





#КонецОбласти








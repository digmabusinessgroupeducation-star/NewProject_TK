#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьДаннымиПоУмолчаниюИзДоговора(Договор) Экспорт 
		
	УстановитьПривилегированныйРежим(Истина);
	
	МассивИменСвойств = Новый Массив;
	МассивИменСвойств.Добавить("ТТН_Заказчик");
	МассивИменСвойств.Добавить("ТТН_Перевозчик");
	МассивИменСвойств.Добавить("ТТН_Водитель");
	МассивИменСвойств.Добавить("ТТН_Автомобиль");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("МассивИменСвойств", МассивИменСвойств);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Свойство.Имя КАК СвойствоИмя,
	               |	ДополнительныеСведения.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Объект = &Договор
	               |	И ДополнительныеСведения.Свойство.Имя В(&МассивИменСвойств)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураРеквизитов = Новый Структура("Заказчик,Перевозчик,Водитель,Автомобиль,НомерПрав");
	
	Пока Выборка.Следующий() Цикл 
		Если Выборка.СвойствоИмя = "ТТН_Заказчик" Тогда 
			СтруктураРеквизитов.Заказчик = Выборка.Значение;
		ИначеЕсли Выборка.СвойствоИмя = "ТТН_Перевозчик" Тогда 
			СтруктураРеквизитов.Перевозчик = Выборка.Значение;
		ИначеЕсли Выборка.СвойствоИмя = "ТТН_Автомобиль" Тогда 
			СтруктураРеквизитов.Автомобиль = Выборка.Значение;
		ИначеЕсли Выборка.СвойствоИмя = "ТТН_Водитель" Тогда 
			СтруктураРеквизитов.Водитель = Выборка.Значение;

			Если ТипЗнч(Выборка.Значение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
				НомерПрав = Справочники.ФизическиеЛица.ПолучитьНомерВодительскогоУдостоверения(Выборка.Значение, Дата);
							
				Если ЗначениеЗаполнено(НомерПрав) Тогда 
					СтруктураРеквизитов.НомерПрав = НомерПрав;
				КонецЕсли;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураРеквизитов);
	
КонецПроцедуры


#КонецОбласти
	
Процедура ПриКопировании(ОбъектКопирования)
	ДокументОснование = Неопределено;
	ИнициализироватьДокумент();
КонецПроцедуры


#Область Проведение

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	

КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_НалоговаяНакладная") Тогда 
		ЗаполнитьДокументНаОснованииНН(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		ЗаполнитьДокументНаОснованииРеализации(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
		ЗаполнитьДокументНаОснованииВыпускПродукцииСРеализацией(ДанныеЗаполнения);
	Иначе 
		ИнициализироватьДокумент(ДанныеЗаполнения);	
	КонецЕсли;	
	
КонецПроцедуры


// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ХозОперация           = Справочники.ХозОперации.ТТН;
	ПунктПолучатель = "Согласно накладной";
	Если ЗначениеЗаполнено(Организация) Тогда
		Заказчик = Организация.Контрагент;
		Отправитель = Заказчик.НаименованиеПолное;
	КонецЕсли;
	ТорговаяПлощадка       = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяТорговаяПлощадка");	

	ПунктОтправитель = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ТорговаяПлощадка, УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("ФактическийАдресОбъекта"),ТекущаяДата());
	Статус = Перечисления.СтатусыТТН.Черновик;
	
	//Отправитель = 
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
	КонецЕсли;
	
	
КонецПроцедуры // ИнициализироватьДокумент()

Процедура ЗаполнитьДокументНаОснованииНН(Знач ЗаказОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ТТН) КАК ХозОперация,
	               |	ТК_НалоговаяНакладная.Ссылка КАК Документ,
	               |	ТК_НалоговаяНакладная.Дата КАК Дата,
	               |	ТК_НалоговаяНакладная.ОтветственныйОтправитель КАК ОтветственныйОтправитель,
	               |	ТК_НалоговаяНакладная.ОтветственныйПолучатель КАК ОтветственныйПолучатель,
	               |	ТК_НалоговаяНакладная.ДолжностьОтветственногоОтправителя КАК ДолжностьОтветственногоОтправителя,
	               |	ТК_НалоговаяНакладная.ДолжностьОтветственногоПолучателя КАК ДолжностьОтветственногоПолучателя,
	               |	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
	               |	ТК_НалоговаяНакладная.Организация.НаименованиеПолное КАК Отправитель,
	               |	""Автоперевезення"" КАК ВидПеревозки,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	ТК_НалоговаяНакладная.Ссылка = &ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ТК_НалоговаяНакладнаяТовары.Количество * ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения.Вес) КАК Масса
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	               |ГДЕ
	               |	ТК_НалоговаяНакладнаяТовары.Ссылка.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ЗаказОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[0].Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьДаннымиПоУмолчаниюИзДоговора(Выборка.ДоговорВзаиморасчетов);
	
	ВыборкаТовары = МассивРезультатов[1].Выбрать();
	ВыборкаТовары.Следующий();
	НоваяСтрока = Источники.Добавить();
	НоваяСтрока.Документ = Выборка.Документ;
	НоваяСтрока.Масса = ВыборкаТовары.Масса;
	
	Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииРеализации(Знач РеализацияОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ТТН) КАК ХозОперация,
	               |	РеализацияТоваров.Ссылка КАК Документ,
	               |	РеализацияТоваров.Дата КАК Дата,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.Организация.НаименованиеПолное КАК Отправитель,
	               |	""Автоперевезення"" КАК ВидПеревозки,
	               |	РеализацияТоваров.Организация КАК Организация,
	               |	РеализацияТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РеализацияТоваров.СкладКомпании.НаименованиеПредставление КАК ПунктОтправитель,
	               |	РеализацияТоваров.Организация.Контрагент КАК Заказчик,
	               |	РеализацияТоваров.ТочкаДоставки.Наименование КАК ПунктПолучатель,
	               |	РеализацияТоваров.ПометкаУдаления КАК ПометкаУдаления,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка = &ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(РеализацияТоваровТовары.КоличествоБазовое * РеализацияТоваровТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес) КАК Масса
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", РеализацияОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[0].Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	ЗаполнитьДаннымиПоУмолчаниюИзДоговора(Выборка.ДоговорВзаиморасчетов);
	
	ВыборкаТовары = МассивРезультатов[1].Выбрать();
	ВыборкаТовары.Следующий();
	НоваяСтрока = Источники.Добавить();
	НоваяСтрока.Документ = Выборка.Документ;
	НоваяСтрока.Масса = ВыборкаТовары.Масса;
		
	Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВыпускПродукцииСРеализацией(Знач РеализацияОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ТТН) КАК ХозОперация,
	               |	ВыпускПродукции.Ссылка КАК Документ,
	               |	ВыпускПродукции.Дата КАК Дата,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.Организация.НаименованиеПолное КАК Отправитель,
	               |	""Автоперевезення"" КАК ВидПеревозки,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВыпускПродукции.СкладКомпании.НаименованиеПредставление КАК ПунктОтправитель,
	               |	ВыпускПродукции.Организация.Контрагент КАК Заказчик,
	               |	ВыпускПродукции.ТочкаДоставки.Наименование КАК ПунктПолучатель,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &ДокументОснование
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СУММА(ВыпускПродукцииПродукция.КоличествоБазовое * ВыпускПродукцииПродукция.Номенклатура.ОсновнаяЕдиницаИзмерения.Вес) КАК Масса
	               |ИЗ
	               |	Документ.ВыпускПродукции.Продукция КАК ВыпускПродукцииПродукция
	               |ГДЕ
	               |	ВыпускПродукцииПродукция.Ссылка.Ссылка = &ДокументОснование
	               |	И ВыпускПродукцииПродукция.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)";
	
	Запрос.УстановитьПараметр("ДокументОснование", РеализацияОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[0].Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ЗаполнитьДаннымиПоУмолчаниюИзДоговора(Выборка.ДоговорВзаиморасчетов);
	
	ВыборкаТовары = МассивРезультатов[1].Выбрать();
	ВыборкаТовары.Следующий();
	НоваяСтрока = Источники.Добавить();
	НоваяСтрока.Документ = Выборка.Документ;
	НоваяСтрока.Масса = ВыборкаТовары.Масса;
		
	Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры


#КонецОбласти

	
#КонецЕсли


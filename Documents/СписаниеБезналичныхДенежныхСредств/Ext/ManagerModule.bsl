#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры


#КонецОбласти	
	




#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);

	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ХозОперация = ДополнительныеСвойства.ДляПроведения.ХозОперация;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры);
	//ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
	//ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);					
	
	ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПоставщиками(Запрос, ДополнительныеСвойства);
	ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);
	
	ПартионныйУчет.ПодготовитьТаблицуРасчетыСПоставщиками(Запрос, ДополнительныеСвойства);
	ПартионныйУчет.ПодготовитьТаблицуРасчетыСПокупателями(Запрос, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеБезналичныхДенежныхСредств.Дата КАК Период,
	               |	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Ссылка,
	               |	СписаниеБезналичныхДенежныхСредств.ХозОперация КАК ХозОперация,
	               |	СписаниеБезналичныхДенежныхСредств.Организация КАК Организация,
	               |	СписаниеБезналичныхДенежныхСредств.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	СписаниеБезналичныхДенежныхСредств.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	СписаниеБезналичныхДенежныхСредств.ВалютаДокумента КАК ВалютаДокумента,
	               |	СписаниеБезналичныхДенежныхСредств.МоментВремени КАК МоментВремени,
	               |	СписаниеБезналичныхДенежныхСредств.КурсДокумента КАК КурсДокумента,
	               |	СписаниеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
	               |	СписаниеБезналичныхДенежныхСредств.ДокументОснование КАК Сделка,
	               |	СписаниеБезналичныхДенежныхСредств.БанковскийСчет КАК БанковскийСчет
	               |ИЗ
	               |	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	               |ГДЕ
	               |	СписаниеБезналичныхДенежныхСредств.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", 			Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", 		Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", 			Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("КурсДокумента", 		Реквизиты.КурсДокумента);
	Запрос.УстановитьПараметр("ВалютаДокумента",	Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("БанковскийСчет",		Реквизиты.БанковскийСчет);
	Запрос.УстановитьПараметр("ХозОперация", 		Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет", Истина);	
	Запрос.УстановитьПараметр("ВидДоговораПокупка", ПредопределенноеЗначение("Перечисление.ВидыДоговоров.Покупка"));
	Запрос.УстановитьПараметр("ВидДоговораПродажа",	ПредопределенноеЗначение("Перечисление.ВидыДоговоров.Продажа"));
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("МоментВремени", Реквизиты.МоментВремени);
	ДополнительныеСвойства.ДляПроведения.Вставить("СуммаДокумента", Реквизиты.СуммаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента", Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("ВалютаДокумента",Реквизиты.ВалютаДокумента);	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет", Истина);
	
КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.СтатьяДДС КАК СтатьяДДС,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Контрагент КАК Контрагент,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.Организация КАК Организация,
	               |	ВЫБОР
	               |		КОГДА СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ТорговаяПлощадка <> ЗНАЧЕНИЕ(справочник.торговыеплощадки.пустаяссылка)
	               |			ТОГДА СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ТорговаяПлощадка
	               |		ИНАЧЕ СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка.ТорговаяПлощадка
	               |	КОНЕЦ КАК ТорговаяПлощадка,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВЫБОР
	               |		КОГДА СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ДоговорВзаиморасчетов.ВидДоговора.ВидДоговора = &ВидДоговораПокупка
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК РасчетСПоставщиком,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Сумма КАК Сумма,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Сделка КАК Сделка,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.НазначениеПлатежа КАК НазначениеПлатежа,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.НомерДок КАК НомерДок,
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.АвтоЗакрытиеСделок КАК АвтоЗакрытиеСделок
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа
	               |ГДЕ
	               |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка = &Ссылка";
				   
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&ВалютаДокумента КАК Валюта,
	               |	&БанковскийСчет КАК СтруктурнаяЕдиница,
	               |	СУММА(ВтТаблицаТовары.Сумма) КАК Сумма,
	               |	&ХозОперация КАК ХозОперация,
	               |	ВтТаблицаТовары.СтатьяДДС КАК СтатьяДДС
	               |ИЗ
	               |	ВтТаблицаТовары КАК ВтТаблицаТовары
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаТовары.СтатьяДДС"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ИСТИНА КАК РегламентированныйУчет,
	              |	СУММА(ВтТаблицаТовары.Сумма) КАК Сумма,
	              |	СУММА(ВтТаблицаТовары.Сумма * &КурсДокумента) КАК СуммаБаз,
	              |	&ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |ГДЕ
	              |	ВтТаблицаТовары.ВидДоговора <> &ВидДоговораПродажа
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПокупателями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ИСТИНА КАК РегламентированныйУчет,
	              |	СУММА(ВтТаблицаТовары.Сумма) КАК Сумма,
	              |	СУММА(ВтТаблицаТовары.Сумма * &КурсДокумента) КАК СуммаБаз,
	              |	&ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |ГДЕ
	              |	ВтТаблицаТовары.ВидДоговора = &ВидДоговораПродажа
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти
	

#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("Взаиморасчеты");	
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	&Дата КАК Период,
	                      |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка КАК Регистратор,
	                      |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.ДоговорВзаиморасчетов КАК ДоговорКонтрагента
	                      |ИЗ
	                      |	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа
	                      |ГДЕ
	                      |	СписаниеБезналичныхДенежныхСредствРасшифровкаПлатежа.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", ДополнительныеСвойства.ДляПроведения.Дата); 
		
	ТаблицыДляПоследовательности.Взаиморасчеты = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти	
	
	
	
#КонецЕсли
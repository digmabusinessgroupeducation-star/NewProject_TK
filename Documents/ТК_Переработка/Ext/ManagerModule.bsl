#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	

	
#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТК_Переработка.Дата КАК Период,
	|	ТК_Переработка.Ссылка КАК Ссылка,
	|	ТК_Переработка.ХозОперация КАК ХозОперация,
	|	ТК_Переработка.Организация КАК Организация,
	|	ТК_Переработка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_Переработка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ТК_Переработка.СкладКомпании КАК СкладКомпании,
	|	ТК_Переработка.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ТК_Переработка.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	|	ТК_Переработка.Услуга_Контрагент КАК Контрагент,
	|	ТК_Переработка.Услуга_ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ТК_Переработка.Услуги.(
	|		Сумма КАК Сумма
	|	) КАК Услуги
	|ИЗ
	|	Документ.ТК_Переработка КАК ТК_Переработка
	|ГДЕ
	|	ТК_Переработка.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	тчУслуги = Реквизиты.Услуги.Выгрузить();
	ЕстьУслуги = тчУслуги.Количество() > 0 
				И тчУслуги.Итог("Сумма") <> 0 
				И ЗначениеЗаполнено(Реквизиты.Контрагент)
				И ЗначениеЗаполнено(Реквизиты.ДоговорВзаиморасчетов);	
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("МоментВремени",                            Новый МоментВремени(Реквизиты.Период,ДокументСсылка));
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("ХозОперация",							  Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",					  Реквизиты.РегламентированныйУчет);
	
	Запрос.УстановитьПараметр("Контрагент",								  Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов", 					  Реквизиты.ДоговорВзаиморасчетов);	
		
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка", 		Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", 			Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости", 	Реквизиты.ТипЦенСебестоимости);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",	Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",			Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("ЕстьУслуги",	 			ЕстьУслуги);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент",	 			Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов",	Реквизиты.ДоговорВзаиморасчетов);
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ЕстьУслуги = ДополнительныеСвойства.ДляПроведения.ЕстьУслуги;
	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
		
		// Исполнение запроса и выгрузка полученных таблиц для движений.
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровПереработка(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") И ЕстьУслуги Тогда
		
		ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
	Иначе
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры, ДополнительныеСвойства.ДляПроведения.ХозОперация);
		
		Если ЕстьУслуги Тогда 
			ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
			ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры);
		КонецЕсли;
		
		// Исполнение запроса и выгрузка полученных таблиц для движений.
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровПереработка(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);				
	КонецЕсли;
	
КонецПроцедуры


Функция ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаПродукция";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ПереработкаПродукция.НомерСтроки КАК НомерСтроки,
	               |	ТК_ПереработкаПродукция.Номенклатура КАК Номенклатура,
	               |	ТК_ПереработкаПродукция.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_ПереработкаПродукция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ПереработкаПродукция.Коэффициент КАК Коэффициент,
	               |	ТК_ПереработкаПродукция.Количество КАК Количество,
	               |	ТК_ПереработкаПродукция.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_ПереработкаПродукция.Цена КАК Цена,
	               |	ТК_ПереработкаПродукция.Сумма КАК Сумма,
	               |	0 КАК СуммаАкцизногоНалога,
	               |	ТК_ПереработкаПродукция.СтавкаНДС КАК СтавкаНДС,
	               |	ТК_ПереработкаПродукция.СуммаНДС КАК СуммаНДС
	               |ПОМЕСТИТЬ ВтТаблицаПродукция
	               |ИЗ
	               |	Документ.ТК_Переработка.Продукция КАК ТК_ПереработкаПродукция
	               |ГДЕ
	               |	ТК_ПереработкаПродукция.Ссылка = &Ссылка";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	
	ИмяРегистра = "ВтТаблицаМатериалы";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ПереработкаМатериалы.НомерСтроки КАК НомерСтроки,
	               |	ТК_ПереработкаМатериалы.Номенклатура КАК Номенклатура,
	               |	ТК_ПереработкаМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ПереработкаМатериалы.Коэффициент КАК Коэффициент,
	               |	ТК_ПереработкаМатериалы.Количество КАК Количество,
	               |	ТК_ПереработкаМатериалы.КоличествоБазовое КАК КоличествоБазовое
	               |ПОМЕСТИТЬ ВтТаблицаМатериалы
	               |ИЗ
	               |	Документ.ТК_Переработка.Материалы КАК ТК_ПереработкаМатериалы
	               |ГДЕ
	               |	ТК_ПереработкаМатериалы.Ссылка = &Ссылка";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	
	ИмяРегистра = "ВтТаблицаУслуги";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ПереработкаУслуги.Номенклатура КАК Номенклатура,
	               |	СУММА(ТК_ПереработкаУслуги.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ТК_ПереработкаУслуги.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ВтТаблицаУслуги
	               |ИЗ
	               |	Документ.ТК_Переработка.Услуги КАК ТК_ПереработкаУслуги
	               |ГДЕ
	               |	ТК_ПереработкаУслуги.Ссылка = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТК_ПереработкаУслуги.Номенклатура";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстыЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры, ХозОперация)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаПродукция", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&Период КАК Период,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	ВтТаблицаМатериалы.Номенклатура КАК Номенклатура,
	               |	СУММА(ВтТаблицаМатериалы.КоличествоБазовое) КАК Количество,
	               |	&ХозОперация КАК ХозОперация
	               |ИЗ
	               |	ВтТаблицаМатериалы КАК ВтТаблицаМатериалы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаМатериалы.Номенклатура
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |                             
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	               |	&Период,
	               |	&СкладКомпании,
	               |	ВтТаблицаПродукция.Номенклатура,
	               |	СУММА(ВтТаблицаПродукция.КоличествоБазовое),
	               |	&ХозОперация
	               |ИЗ
	               |	ВтТаблицаПродукция КАК ВтТаблицаПродукция
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВтТаблицаПродукция.Номенклатура"; 					
	
	        		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	&РегламентированныйУчет КАК РегламентированныйУчет,
	               |	&Контрагент КАК Контрагент,
	               |	СУММА(ВтТаблицаУслуги.Сумма) КАК Сумма,
	               |	СУММА(ВтТаблицаУслуги.Сумма) КАК СуммаБаз,
	               |	&ХозОперация КАК ХозОперация,
	               |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности) КАК ВидОперации
	               |ИЗ
	               |	ВтТаблицаУслуги КАК ВтТаблицаУслуги";
	
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаРасчетаСПоставщикамиПоДокументам(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПоставщикамиПоДокументам";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	&ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	&РегламентированныйУчет КАК РегламентированныйУчет,
	               |	&Контрагент КАК Контрагент,
	               |	&Ссылка КАК Сделка,
	               |	СУММА(ВтТаблицаУслуги.Сумма) КАК Сумма,
	               |	СУММА(ВтТаблицаУслуги.Сумма) КАК СуммаБаз,
	               |	&ХозОперация КАК ХозОперация,
	               |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеКредиторскойЗадолженности) КАК ВидОперации
	               |ИЗ
	               |	ВтТаблицаУслуги КАК ВтТаблицаУслуги";  
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции


#КонецОбласти	


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = ДополнительныеСвойства.ДляПроведения.СкладКомпании;
	НоваяСтрока.Период   	  = ДополнительныеСвойства.ДляПроведения.Дата;
	
	ТаблицыДляПоследовательности.Вставить("ПартииТоваров", ТаблицаПоследовательности);
		
	Если ДополнительныеСвойства.ДляПроведения.ЕстьУслуги Тогда
		ТаблицаПоследовательности = Новый ТаблицаЗначений;
		ТаблицаПоследовательности.Колонки.Добавить("Период");
		ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
		ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");
		
		НоваяСтрока = ТаблицаПоследовательности.Добавить();
		НоваяСтрока.Регистратор        = Ссылка;
		НоваяСтрока.ДоговорКонтрагента = ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
		НоваяСтрока.Период             = ДополнительныеСвойства.ДляПроведения.Дата;
		
		ТаблицыДляПоследовательности.Вставить("Взаиморасчеты", ТаблицаПоследовательности);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности", ТаблицыДляПоследовательности);
	
КонецПроцедуры


#КонецОбласти



#Область Печать




#КонецОбласти	






#КонецЕсли
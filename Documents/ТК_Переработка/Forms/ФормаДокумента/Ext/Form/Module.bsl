&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
			
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ТК_Переработка"));
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
	ЗаполнитьСебестоимостьПереработки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
		
	УстановитьСостояниеДокумента();
	
	ЗаполнитьСебестоимостьПереработки();
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыПродукция

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Продукция.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Услуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы", "Количество");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалы

&НаКлиенте
Процедура МатериалыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Материалы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура МатериалыЕдиницаИзмеренияПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Материалы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоПриИзменении(Элемент)
		
	ТекущаяСтрока = Элементы.Материалы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));

	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаКлиенте
Процедура УправлениеЭлементамиФормы()

	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Себестоимость");
	МассивВсехРеквизитов.Добавить("СебестоимостьНДС");
	МассивВсехРеквизитов.Добавить("СебестоимостьБезНДС");
	МассивВсехРеквизитов.Добавить("МатериалыСебестоимость");	
	
	МассивВидимыхРеквизитов = Новый Массив;
	
	Если Себестоимость <> 0 Тогда 
		МассивВидимыхРеквизитов.Добавить("Себестоимость");
		МассивВидимыхРеквизитов.Добавить("МатериалыСебестоимость");		
	КонецЕсли;
	
	Если СебестоимостьНДС <> 0 Тогда 
		МассивВидимыхРеквизитов.Добавить("СебестоимостьНДС");
		МассивВидимыхРеквизитов.Добавить("СебестоимостьБезНДС");
	КонецЕсли;

	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивВидимыхРеквизитов);	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСебестоимостьПереработки()
	
	Если Не Объект.Проведен Тогда 
		Себестоимость = 0;
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_ПереработкаМатериалы.НомерСтроки КАК НомерСтроки,
	               |	ТК_ПереработкаМатериалы.Номенклатура КАК Номенклатура,
	               |	ТК_ПереработкаМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ПереработкаМатериалы.Коэффициент КАК Коэффициент,
	               |	ТК_ПереработкаМатериалы.Количество КАК Количество,
	               |	ТК_ПереработкаМатериалы.КоличествоБазовое КАК КоличествоБазовое
	               |ПОМЕСТИТЬ втМатериалы
	               |ИЗ
	               |	&тзМатериалы КАК ТК_ПереработкаМатериалы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	               |	СУММА(ПартииТоваровКомпании.Количество) КАК Количество,
	               |	СУММА(ПартииТоваровКомпании.Сумма) КАК Сумма,
	               |	ВЫБОР
	               |		КОГДА СУММА(ПартииТоваровКомпании.Количество) <> 0
	               |			ТОГДА СУММА(ПартииТоваровКомпании.Сумма) / СУММА(ПартииТоваровКомпании.Количество)
	               |		ИНАЧЕ СУММА(ПартииТоваровКомпании.Сумма)
	               |	КОНЕЦ КАК УсредненнаяЦена,
	               |	ВЫБОР
	               |		КОГДА СУММА(ПартииТоваровКомпании.Количество) <> 0
	               |			ТОГДА СУММА(ПартииТоваровКомпании.СуммаНДС) / СУММА(ПартииТоваровКомпании.Количество)
	               |		ИНАЧЕ СУММА(ПартииТоваровКомпании.СуммаНДС)
	               |	КОНЕЦ КАК СреднийНДС
	               |ПОМЕСТИТЬ втСебестоимость
	               |ИЗ
	               |	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	               |ГДЕ
	               |	ПартииТоваровКомпании.Регистратор = &Регистратор
	               |	И ПартииТоваровКомпании.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПартииТоваровКомпании.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втМатериалы.НомерСтроки КАК НомерСтроки,
	               |	втМатериалы.Номенклатура КАК Номенклатура,
	               |	втМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	втМатериалы.Коэффициент КАК Коэффициент,
	               |	втМатериалы.Количество КАК Количество,
	               |	втМатериалы.КоличествоБазовое КАК КоличествоБазовое,
	               |	втМатериалы.КоличествоБазовое * ЕСТЬNULL(втСебестоимость.УсредненнаяЦена, 0) КАК Себестоимость,
	               |	втМатериалы.КоличествоБазовое * ЕСТЬNULL(втСебестоимость.СреднийНДС, 0) КАК СебестоимостьНДС
	               |ИЗ
	               |	втМатериалы КАК втМатериалы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втСебестоимость КАК втСебестоимость
	               |		ПО втМатериалы.Номенклатура = втСебестоимость.Номенклатура";
	
	Запрос.УстановитьПараметр("тзМатериалы", Объект.Материалы.Выгрузить());
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	
	Объект.Материалы.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Себестоимость 		= Объект.Материалы.Итог("Себестоимость") + Объект.Услуги.Итог("Сумма");
	СебестоимостьНДС 	= Объект.Материалы.Итог("СебестоимостьНДС") + Объект.Услуги.Итог("СуммаНДС");
	СебестоимостьБезНДС = Себестоимость - СебестоимостьНДС;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры


#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ТорговаяПлощадкаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТорговаяПлощадка) Тогда
		Результат = РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(ТекущаяДата(), Новый Структура("ТорговаяПлощадка", Объект.ТорговаяПлощадка));
		Если НЕ Результат.Количество()=0 Тогда
			Объект.Организация = Результат[0].Организация;
		КонецЕсли;
		
		Объект.СкладКомпании = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Объект.ТорговаяПлощадка);		
		
		//Если ЗначениеЗаполнено(Объект.СкладКомпании) Тогда 
		//	Если Объект.СкладКомпании.Розничный Тогда 
		//		Объект.ТипЦенИтогов = Справочники.СкладыКомпании.ТипЦенРозничный(Объект.СкладКомпании);	
		//	Иначе
		//		Объект.ТипЦенИтогов = Справочники.СкладыКомпании.ТипЦенСебестоимости(Объект.СкладКомпании);	
		//	КонецЕсли;
		//КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПриИзменении(Элемент)
	ТорговаяПлощадкаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СкладКомпанииПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Объект.ТорговаяПлощадка) Тогда
		Объект.ТорговаяПлощадка = Объект.СкладКомпании.ТорговаяПлощадка;
		Результат = РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(ТекущаяДата(), Новый Структура("ТорговаяПлощадка", Объект.ТорговаяПлощадка));
		Если НЕ Результат.Количество()=0 Тогда
			Объект.Организация = Результат[0].Организация;
		КонецЕсли;
		//Объект.ТипЦенИтогов = Объект.СкладКомпании.ТипЦенСебестоимости;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	СкладКомпанииПриИзмененииНаСервере();
КонецПроцедуры



#КонецОбласти
 


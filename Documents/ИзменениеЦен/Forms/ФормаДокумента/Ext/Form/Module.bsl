&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	

	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ИзменениеЦен"));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	УстановитьСостояниеДокумента();
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);

	УстановитьСостояниеДокумента();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);	
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", Объект.ТипЦен));
	СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);	
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	//СтруктураДействий.Вставить("ПроверитьНовуюЦенуПоИндикативу");	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаБазоваяПриИзменении(Элемент)	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры	

&НаКлиенте
Процедура ТоварыПроцентНаценкиПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоваяЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьПроцентНаценкиИзНовойЦены");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ПроцентНаценкиПриИзменении(Элемент)
	
	ТекстСообщения = НСтр("ru='Установить процент наценки?'; uk='Встановити відсоток націнки?'");
	
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить("Отмена", НСтр("ru='Отмена'; uk='Відмінити'"));
	СписокКнопок.Добавить("ДляВсех", НСтр("ru='Для всех'; uk='Для всіх'"));
	СписокКнопок.Добавить("ТолькоПустым", НСтр("ru='Только пустым'; uk='Тільки для пустих'"));
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьПроцентыВТЧПоУстановленному", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстСообщения, СписокКнопок, 10, "Отмена",, "Отмена");
	
КонецПроцедуры

&НаКлиенте
Процедура БазовыйТипЦенПриИзменении(Элемент)
		
	СтруктураДействий = Новый Структура;
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенПриИзменении(Элемент)
	
	ПараметрыОкргуления = ПолучитьПараметрыОкргуленияТипаЦен(Объект.ТипЦен);
	ТипЦенВариантОкругления = ПараметрыОкргуления.ВариантОкругления;
	ТипЦенТочность = ПараметрыОкргуления.Точность;
	ПравилоРасчета = ПараметрыОкргуления.ПравилоРасчета;

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", Объект.ТипЦен));
	ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения);
	
	
КонецПроцедуры


#КонецОбласти	

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");


КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
        И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
        СвойстваВыполнитьОтложеннуюИнициализацию();
        УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
    КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаКлиенте
Процедура ОкруглятьДоПриИзменении(Элемент)
	
	Если Объект.Товары.Количество() > 0 Тогда 
		ТекстСообщения = НСтр("ru='Пересчитать колонку ""Новая цена"" кратно %ОкруглятьДо%?'; uk='Перерахувати колонку ""Нова ціна"" кратно %ОкруглятьДо%?'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОкруглятьДо%", Объект.ОкруглятьДо);
		
		Оповещение = Новый ОписаниеОповещения("ОкруглятьДоПриИзмененииПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет, 10, КодВозвратаДиалога.Нет,, КодВозвратаДиалога.Нет);
	КонецЕсли;		
		
КонецПроцедуры

&НаКлиенте
Процедура ОкруглятьДоПриИзмененииПродолжение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт 
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда 
		СтруктураДействий = Новый Структура;
		
		СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
		СтруктураДействий.Вставить("ПересчитатьПроцентНаценкиИзНовойЦены");
		Если Объект.ОкруглятьДо = 0.9 Тогда
			тОкруглятьДо = 1
		Иначе
			тОкруглятьДо = Объект.ОкруглятьДо;
		КонецЕсли;
			
		ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, тОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
		
		ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения);
		
		Если Объект.ОкруглятьДо = 0.9 Тогда
			Для Каждого тСтр Из Объект.Товары Цикл
				тСтр.НоваяЦена = тСтр.НоваяЦена-0.1;		
			КонецЦикла;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПересчитатьПроцентНаценкиИзНовойЦены");
			ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьПоРекомендованнымНаценкам(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда 
		
		ТекстСообщения = НСтр("ru='Установить процент наценки из рекомендованных?'; uk='Встановити відсоток націнки із рекомендованих?'");	
		
		Оповещение = Новый ОписаниеОповещения("ПересчитатьПоРекомендованнымНаценкамПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет, 10, КодВозвратаДиалога.Нет,, КодВозвратаДиалога.Нет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦенуМРЦ(Команда)
	ЗаполнитьЦенуПоМРЦ();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦенуМРЦ5(Команда)
	ЗаполнитьЦенуПоМРЦ(5);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТоварыБезОстатка(Команда)
	Если Объект.Товары.Количество() > 0 Тогда 
		УдалитьТоварыБезОстаткаНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокиСниженияЦены(Команда)
	
	МассивИндексовНаУдаление = Новый Массив;
	
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.НоваяЦена < Стр.СтараяЦена Тогда 
			МассивИндексовНаУдаление.Добавить(Стр);
		КонецЕсли;
	КонецЦикла;
			
	Если МассивИндексовНаУдаление.Количество() > 0 Тогда 
		Для Каждого Инд Из МассивИндексовНаУдаление Цикл 
			Объект.Товары.Удалить(Инд);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТовары(Команда)
	Объект.Товары.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСохраненияРезультата", ЭтаФорма.УникальныйИдентификатор);
	
	Оповещение = Новый ОписаниеОповещения("ПодборНоменклатурыЗавершение", ЭтаФорма, ПараметрыФормы);
	
	ОткрытьФорму("Документ.ИзменениеЦен.Форма.ФормаПодбораТовара", ПараметрыФормы, ЭтаФорма, Истина,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьТоварыПоБазовомуТЦНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЦеныСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ЦеныСрезПоследних.Характеристика КАК ХарактеристикаНоменклатуры,
	               |	ЦеныСрезПоследних.Цена КАК НоваяЦена
	               |ИЗ
	               |	РегистрСведений.Цены.СрезПоследних(&Дата, ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних";	
	
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ТипЦен", Объект.БазовыйТипЦен);
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	Объект.Товары.Загрузить(тзРезультат);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыПоБазовомуТЦПродолжение(Ответ, ДопПараметры) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьТоварыПоБазовомуТЦНаСервере();
	КонецЕсли;		
	
	СтруктураДействий = Новый Структура;
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);	
	СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", Объект.ТипЦен));
	СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки", ПравилоРасчета);	
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования", Новый Структура("Номенклатура","ГруппаЦенообразования"));
	
	Для Каждого ТекущаяСтрока Из Объект.Товары Цикл 
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыПоБазовомуТЦ(Команда)
	
	Если Объект.БазовыйТипЦен.Пустая() Тогда 
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьТоварыПоБазовомуТЦПродолжение", ЭтаФорма, Неопределено);
		
		ПоказатьВопрос(
			ОписаниеОповещения, 
			НСтр("ru = 'Будет очищена табличная часть ""Товары"".
                  |Вы уверены, что хотите продолжить?'; uk = 'Буде очищена таблична частина ""Товари"".
                  |Ви впевнені, що бажаєте продовжити?'"),
			РежимДиалогаВопрос.ДаНет,
			10,
			КодВозвратаДиалога.Нет,
			НСтр("ru = 'Очистить Товары'; uk = 'Очистити Товари'"),
			КодВозвратаДиалога.Нет);
	Иначе	
		ЗаполнитьТоварыПоБазовомуТЦПродолжение(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ИзменениеЦен.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка товаров из файла'; uk = 'Завантаження списка товарів із файлу'");		
	//ПараметрыЗагрузки.ДополнительныеПараметры = Новый структура;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	
	
	МассивОформляемыхПолей = Новый Массив;
	МассивОформляемыхПолей.Добавить("ТоварыНомерСтроки");
	МассивОформляемыхПолей.Добавить("ТоварыНоменклатураАртикул");
	МассивОформляемыхПолей.Добавить("ТоварыНоменклатура");
	МассивОформляемыхПолей.Добавить("ТоварыНоваяЦена");
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Для Каждого Поле Из МассивОформляемыхПолей Цикл 
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[Поле].Имя);
	КонецЦикла;

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.НоваяЦена");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СтараяЦена");
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет);
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	Для Каждого Поле Из МассивОформляемыхПолей Цикл 
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[Поле].Имя);
	КонецЦикла;

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.НоваяЦена");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СтараяЦена");
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);	
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий)
	
	ПараметрыПолученияЦены = Новый Структура;
	ПараметрыПолученияЦены.Вставить("Дата", Объект.Дата);
	ПараметрыПолученияЦены.Вставить("ТипЦен", Объект.БазовыйТипЦен);
	ПараметрыПолученияЦены.Вставить("ГраницаИсклюая", Истина);
	
	ПараметрыСтаройЦены = Новый Структура;
	ПараметрыСтаройЦены.Вставить("ИмяРегистра", "Цены");
	ПараметрыСтаройЦены.Вставить("Дата", Объект.Дата);
	ПараметрыСтаройЦены.Вставить("ТипЦен", Объект.ТипЦен);
	ПараметрыСтаройЦены.Вставить("ГраницаИсклюая", Истина);
	
	СтруктураПолейДляЗаполненияЦен = Новый Структура;			
	СтруктураПолейДляЗаполненияЦен.Вставить("ЦенаБазовая", ПараметрыПолученияЦены);	
	СтруктураПолейДляЗаполненияЦен.Вставить("СтараяЦена", ПараметрыСтаройЦены);	
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);		
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, ОкруглятьДо, ВариантОкругления, Точность)
	
	ПараметрыОкргуления = Новый Структура;
	Если ЗначениеЗаполнено(ОкруглятьДо) Тогда 
		ПараметрыОкргуления.Вставить("ОкруглятьДо", ОкруглятьДо);
	ИначеЕсли ЗначениеЗаполнено(ВариантОкругления) Тогда 
		ПараметрыОкргуления.Вставить("ВариантОкругления", ВариантОкругления);
		ПараметрыОкргуления.Вставить("Точность", Точность);
	Иначе
		ПараметрыОкргуления.Вставить("ОкруглятьДо", 0);
	КонецЕсли;
	
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуСУчетомОкругления", ПараметрыОкргуления);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПроцентыВТЧПоУстановленному(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	ДляВсех 		= ОтветНаВопрос = "ДляВсех";
	ТолькоПустым 	= ОтветНаВопрос = "ТолькоПустым";
	
	Если НЕ (ТолькоПустым ИЛИ ДляВсех) Тогда
		ПроцентНаценки = 0;
		Возврат;
	КонецЕсли;
		
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);	
	
	Для Каждого СтрТЧ ИЗ Объект.Товары Цикл
		Если ТолькоПустым И ЗначениеЗаполнено(СтрТЧ.ПроцентНаценки) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрТЧ.ПроцентНаценки = ПроцентНаценки;			
		ОбработкаТабличнойЧастиКлиентСервер.ПересчитатьНовуюЦенуИПроцентНаценки(СтрТЧ, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьПоРекомендованнымНаценкамПродолжение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		//ПересчитатьПоРекомендованнымНаценкамНаСервере();
		
		СтруктураДействий = Новый Структура;	
		СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
		СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);	
		ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
  		
		ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения);
	КонецЕсли;
	
КонецПроцедуры                                       

&НаСервере
Процедура ОбработатьТЧНаСервере(СтруктураДействий, КэшированныеЗначения)
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьБазовыеЦеныВТЧНаСервере(ПараметрыЗаполнения, СтруктураДействий, КэшированныеЗначения)	
	ЦенообразованиеСервер.ЗаполнитьЦеныПоТипуЦенВТЧ(Объект.Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦенуПоМРЦ(ПроцентНаценки = 0)		
		
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Не Стр.ХарактеристикиИспользуются
			ИЛИ Стр.ХарактеристикаНоменклатуры.Пустая() Тогда 
			
			Продолжить;	
		КонецЕсли;
		
		ЗначениеМРЦ = Стр.ХарактеристикаНоменклатуры.ЗначениеМРЦ;
		Если ПроцентНаценки > 0 Тогда 
			ЗначениеМРЦ = Цел(ЗначениеМРЦ * (ПроцентНаценки/100 + 1) * 100)/100;	
		КонецЕсли;
		Стр.НоваяЦена = ЗначениеМРЦ;
	КонецЦикла;			
	
КонецПроцедуры

&НаСервере
Процедура УдалитьТоварыБезОстаткаНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	тчТовары = Объект.Товары.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("тчТовары", тчТовары);
	Запрос.Текст = "ВЫБРАТЬ
	               |	тчТовары.НомерСтроки КАК НомерСтроки,
	               |	тчТовары.Номенклатура КАК Номенклатура,
	               |	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	тчТовары.ЦенаБазовая КАК ЦенаБазовая,
	               |	тчТовары.ПроцентНаценки КАК ПроцентНаценки,
	               |	тчТовары.НоваяЦена КАК НоваяЦена,
	               |	тчТовары.СтараяЦена КАК СтараяЦена,
	               |	тчТовары.РекомендуемыйПроцентНаценкиНоменклатура КАК РекомендуемыйПроцентНаценкиНоменклатура,
	               |	тчТовары.РекомендуемыйПроцентНаценкиРодитель КАК РекомендуемыйПроцентНаценкиРодитель,
	               |	тчТовары.РекомендуемыйПроцентНаценкиТип КАК РекомендуемыйПроцентНаценкиТип,
	               |	тчТовары.РекомендуемыйПроцентНаценкиГруппаЦенообразования КАК РекомендуемыйПроцентНаценкиГруппаЦенообразования,
	               |	тчТовары.ОкруглятьДо КАК ОкруглятьДо,
	               |	тчТовары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
	               |ПОМЕСТИТЬ втТовары
	               |ИЗ
	               |	&тчТовары КАК тчТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ОстаткиТоваровКомпанииОстатки.КоличествоОстаток) КАК КоличествоОстаток
	               |ПОМЕСТИТЬ втОстатки
	               |ИЗ
	               |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	               |			,
	               |			Номенклатура В
	               |				(ВЫБРАТЬ
	               |					втТовары.Номенклатура КАК Номенклатура
	               |				ИЗ
	               |					втТовары КАК втТовары)) КАК ОстаткиТоваровКомпанииОстатки
	               |ГДЕ
	               |	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	               |	ОстаткиТоваровКомпанииОстатки.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втТовары.Номенклатура КАК Номенклатура,
	               |	втТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	втТовары.ЦенаБазовая КАК ЦенаБазовая,
	               |	втТовары.ПроцентНаценки КАК ПроцентНаценки,
	               |	втТовары.НоваяЦена КАК НоваяЦена,
	               |	втТовары.РекомендуемыйПроцентНаценкиНоменклатура КАК РекомендуемыйПроцентНаценкиНоменклатура,
	               |	втТовары.РекомендуемыйПроцентНаценкиРодитель КАК РекомендуемыйПроцентНаценкиРодитель,
	               |	втТовары.РекомендуемыйПроцентНаценкиТип КАК РекомендуемыйПроцентНаценкиТип,
	               |	втТовары.РекомендуемыйПроцентНаценкиГруппаЦенообразования КАК РекомендуемыйПроцентНаценкиГруппаЦенообразования,
	               |	втТовары.ОкруглятьДо КАК ОкруглятьДо,
	               |	втТовары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	               |	втОстатки.КоличествоОстаток КАК КоличествоОстаток,
	               |	втТовары.СтараяЦена КАК СтараяЦена
	               |ИЗ
	               |	втТовары КАК втТовары
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	               |		ПО втТовары.Номенклатура = втОстатки.Номенклатура
	               |			И втТовары.ХарактеристикаНоменклатуры = втОстатки.ХарактеристикаНоменклатуры";
	
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Объект.Товары.Загрузить(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтарыеЦены()
	
	ПараметрыСтаройЦены = Новый Структура;
	ПараметрыСтаройЦены.Вставить("ИмяРегистра", "Цены");
	ПараметрыСтаройЦены.Вставить("Дата", Объект.Дата);
	ПараметрыСтаройЦены.Вставить("ТипЦен", Объект.ТипЦен);
	ПараметрыСтаройЦены.Вставить("ГраницаИсклюая", Истина);
	
	СтруктураПолейДляЗаполненияЦен = Новый Структура;			
	СтруктураПолейДляЗаполненияЦен.Вставить("СтараяЦена", ПараметрыСтаройЦены);	
	
	СтруктураДействий = Новый Структура("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);		
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборНоменклатурыЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("АдресРезультата") Тогда 
		ПолучитьРезультатНаСервере(Результат);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьРезультатНаСервере(Результат)
	
	Попытка
		ТаблицаРезультат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;

	Попытка
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	Исключение КонецПопытки;
	
	ЗаменятьТовары = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ЗаменятьТовары", Ложь);
	
	Если ЗаменятьТовары Тогда 
		Объект.Товары.Очистить();
	КонецЕсли;
	
	
	СтруктураДействий = Новый Структура;		
	СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", Объект.ТипЦен));
	СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);	
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ПроверитьНовуюЦенуПоИндикативу");

	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);
	
	ПараметрыПолученияЦены = Новый Структура;
	ПараметрыПолученияЦены.Вставить("Дата", Объект.Дата);
	ПараметрыПолученияЦены.Вставить("ТипЦен", Объект.БазовыйТипЦен);
	ПараметрыПолученияЦены.Вставить("ГраницаИсклюая", Истина);
	
	ПараметрыСтаройЦены = Новый Структура;
	ПараметрыСтаройЦены.Вставить("ИмяРегистра", "Цены");
	ПараметрыСтаройЦены.Вставить("Дата", Объект.Дата);
	ПараметрыСтаройЦены.Вставить("ТипЦен", Объект.ТипЦен);
	ПараметрыСтаройЦены.Вставить("ГраницаИсклюая", Истина);
	
	СтруктураПолейДляЗаполненияЦен = Новый Структура;			
	СтруктураПолейДляЗаполненияЦен.Вставить("ЦенаБазовая", ПараметрыПолученияЦены);	
	СтруктураПолейДляЗаполненияЦен.Вставить("СтараяЦена", ПараметрыСтаройЦены);	
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);		

	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для Каждого Стр Из ТаблицаРезультат Цикл 
		Если Не ЗаменятьТовары Тогда 			
			нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", Стр.Номенклатура, Стр.ХарактеристикаНоменклатуры));
			Если нСтроки.Количество() > 0 Тогда 
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр, "Номенклатура, ХарактеристикаНоменклатуры, ХарактеристикиИспользуются");
		
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", НоваяСтрока.ХарактеристикаНоменклатуры);	
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыОкргуленияТипаЦен()
	
	ПараметрыОкргуления = ПолучитьПараметрыОкргуленияТипаЦен(Объект.ТипЦен);
	ТипЦенВариантОкругления = ПараметрыОкргуления.ВариантОкругления;
	ТипЦенТочность = ПараметрыОкргуления.Точность;
	ПравилоРасчета = ПараметрыОкргуления.ПравилоРасчета;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыОкргуленияТипаЦен(Знач ТипЦен)	
	
	Результат = Новый Структура("ВариантОкругления, Точность,ПравилоРасчета", Неопределено, 0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТипыЦен.ВариантОкругления КАК ВариантОкругления,
	               |	ТипыЦен.Точность КАК Точность,
	               |	ТипыЦен.ПравилоРасчета КАК ПравилоРасчета
	               |ИЗ
	               |	Справочник.ТипыЦен КАК ТипыЦен
	               |ГДЕ
	               |	ТипыЦен.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ТипЦен);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьСоциальныйТовар(Номенклатура)	
	Социальный = Справочники.Номенклатура.ТоварСоциальный(Номенклатура);
	
	Возврат Социальный;
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьВариантОкругления()	
	ВариантОкругления = Перечисления.ВариантыОкругления.ВсегдаВПользуКлиента;
	
	Возврат ВариантОкругления;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
    
    Если АдресЗагруженныхДанных = Неопределено Тогда 
        Возврат;
    КонецЕсли;
    
    ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных)
    
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	СтруктураДействий = Новый Структура;
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);
	ДобавитьДействиеПересчетаЦеныСОкруглением(СтруктураДействий, Объект.ОкруглятьДо, ТипЦенВариантОкругления, ТипЦенТочность);

	СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", Объект.ТипЦен));
	СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
	СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ПересчитатьПроцентНаценкиИзНовойЦены");
//	СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ПравилоРасчета);	
	СтруктураДействий.Вставить("ПроверитьНовуюЦенуПоИндикативу");

	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл         
	    Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда 
	        Продолжить;
	    КонецЕсли;
	    
	    НоваяСтрокаТовары = Объект.Товары.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.НоваяЦена = СтрокаТаблицы.НоваяЦена;
		НоваяСтрокаТовары.ХарактеристикаНоменклатуры = СтрокаТаблицы.ХарактеристикаНоменклатуры;
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, КэшированныеЗначения); 
		
		ТоварыДобавлены = Истина;
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
	    Модифицированность = Истина;
	КонецЕсли;
    
КонецПроцедуры

#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	ЗаполнитьПараметрыОкргуленияТипаЦен();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
											
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьГруппуЦенообразования",	
											Новый Структура("Номенклатура","ГруппаЦенообразования"));											
												
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
		Если Не Объект.ТипЦен.Пустая() Тогда 
			ЗаполнитьСтарыеЦены();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		Социальный = ПроверитьСоциальныйТовар(СтрокаТовары.Номенклатура);
		Если Социальный тогда 
			СтрокаТовары.ПроцентНаценки = 9.99;
			НоваяЦена = СтрокаТовары.ЦенаБазовая * 1.0999;		
			СтрокаТовары.НоваяЦена=(Цел(НоваяЦена * Pow(10,ТипЦенТочность))) / Pow(10,ТипЦенТочность);
				
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
	
#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИзменениеЦен") Тогда 
		Возврат;
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПротоколСогласованияЦен") Тогда 
		ЗаполнитьДокументНаОснованииПротоколаСогласованныхЦен(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПротоколаСогласованныхЦен(Знач ПротоколОснование)
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ИзменениеЦен) КАК ХозОперация,
		               |	ПротоколСогласованияЦен.Ссылка КАК ДокументОснование,
		               |	ПротоколСогласованияЦен.Организация КАК Организация,
		               |	ПротоколСогласованияЦен.ПодразделениеКомпании КАК ПодразделениеКомпании,
		               |	ПротоколСогласованияЦен.ТипЦен КАК БазовыйТипЦен,
		               |	ПротоколСогласованияЦен.ДатаНачалаДействия КАК ДатаНачалаДействия,
		               |	ПротоколСогласованияЦен.Товары.(
		               |		Номенклатура КАК Номенклатура,
		               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		               |		Цена КАК ЦенаБазовая
		               |	) КАК Товары,
		               |	ПротоколСогласованияЦен.ВалютаДокумента КАК ВалютаДокумента,
		               |	ПротоколСогласованияЦен.КурсДокумента КАК КурсДокумента
		               |ИЗ
		               |	Документ.ПротоколСогласованияЦен КАК ПротоколСогласованияЦен
		               |ГДЕ
		               |	ПротоколСогласованияЦен.Ссылка = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", ПротоколОснование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда 
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
			ТипЦен = Справочники.ТипыЦен.НайтиПоРеквизиту("БазовыйТипЦен", ПротоколОснование.ТипЦен);
			Товары.Очистить();
			
			ВыборкаТовары = Выборка.Товары.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл 
				НоваяСтрока = Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			КонецЦикла;
		КонецЕсли;
		КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		ОкруглятьДо =  1 / Pow(10, ТипЦен.Точность);
		
		ПараметрыОкргуления = Новый Структура;
		ПараметрыОкргуления.Вставить("ОкруглятьДо", ОкруглятьДо);
		ПараметрыОкргуления.Вставить("ВариантОкругления", ТипЦен.ВариантОкругления);
		ПараметрыОкргуления.Вставить("Точность",  ТипЦен.Точность);

		ПараметрыСтаройЦены = Новый Структура;
		ПараметрыСтаройЦены.Вставить("ИмяРегистра", "Цены");
		ПараметрыСтаройЦены.Вставить("Дата", ТекущаяДата());
		ПараметрыСтаройЦены.Вставить("ТипЦен", ТипЦен);
		ПараметрыСтаройЦены.Вставить("ГраницаИсклюая", Истина);
		
		СтруктураПолейДляЗаполненияЦен = Новый Структура;			
		СтруктураПолейДляЗаполненияЦен.Вставить("СтараяЦена", ПараметрыСтаройЦены);	
				
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ЗаполнитьРекомендованныеНаценки", Новый Структура("ТипЦен", ТипЦен));
		СтруктураДействий.Вставить("ЗаполнитьПроцентНаценкиИзРекомендованных");
		СтруктураДействий.Вставить("ПересчитатьНовуюЦенуПоПроцентуНаценки",ТипЦен.ПравилоРасчета);	
		СтруктураДействий.Вставить("ПересчитатьНовуюЦенуСУчетомОкругления", ПараметрыОкргуления);

		
		ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Товары, СтруктураДействий, КэшированныеЗначения);
	     	
		Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)	
		
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ИзменениеЦен.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЦенообразованиеСервер.ОтразитьЦены(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ДокументОснование = Неопределено;
	ДатаНачалаДействия = ТекущаяДата() + (60*30);	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТовары из Товары Цикл
		
		МинимальнаяРозничнаяЦена = ЦенообразованиеСервер.ПолучитьМинимальнуюРозничнуюЦену(СтрокаТовары.Номенклатура);
		Если МинимальнаяРозничнаяЦена <> Неопределено и СтрокаТовары.НоваяЦена < МинимальнаяРозничнаяЦена Тогда
			
			ОписаниеОшибки = НСтр("ru='Для строки [НомерСтроки] номенклатуры [Номенклатура] новая цена установлена ниже минимальной [МинимальнаяЦена]'; uk='Для рядка [НомерСтроки] номенклатури [Номенклатура] нова ціна встановлена нижче мінімальної [МинимальнаяЦена]'");
			сПараметры = Новый Структура("НомерСтроки,Номенклатура,МинимальнаяЦена",СтрокаТовары.НомерСтроки, СтрокаТовары.Номенклатура,МинимальнаяРозничнаяЦена);
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ОписаниеОшибки, сПараметры);
			
			
			Индекс = Товары.Индекс(СтрокаТовары);
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОписаниеОшибки;
			Сообщение.Поле = "Товары["+Индекс+"].НоваяЦена";
			Сообщение.КлючДанных = Ссылка;
			Сообщение.ПутьКДанным = "Объект";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Социальный = Справочники.Номенклатура.ТоварСоциальный(СтрокаТовары.Номенклатура);
		Если Социальный и СтрокаТовары.ПроцентНаценки > 10 Тогда
			Индекс = Товары.Индекс(СтрокаТовары);
			ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'На социальный товар установлена наценка больше допустимой'; uk = 'На соціальний товар встановлена націнка більше рекомендованої'"),Ссылка,
			"Товары["+Индекс+"].ПроцентНаценки","Объект",Истина
			);	
		КонецЕсли;
		
	КонецЦикла;
	
	//Проверка Даты начала действия
	Если ДатаНачалаДействия < ТекущаяДата() Тогда 
		
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		Если Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"НеКонтролироватьДатуНачалаДействияИзмененияЦен", Ложь) Тогда 
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Запрещено проводить Изменение цен с датой начала действия в заднем числе'; uk = 'Заборонено проводити Зміну цін з датою почтаку дії у задньому числі'"),
			ЭтотОбъект,"ДатаНачалаДействия"
			);
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры



#КонецОбласти

	
	
	
	
	
	

#Область ИнициализацияИЗаполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор				  = Пользователи.ТекущийПользователь();
	ДатаНачалаДействия    = НачалоДня(ТекущаяДатаСеанса());
	ХозОперация	 		  = ПредопределенноеЗначение("Справочник.ХозОперации.ИзменениеЦен");
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);	
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

	
	
	
	
	
	
	
	
		
#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаЦены(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИзменениеЦенТовары.Ссылка КАК Ссылка,
	               |	ИзменениеЦенТовары.НомерСтроки КАК НомерСтроки,
	               |	ИзменениеЦенТовары.Номенклатура КАК Номенклатура,
	               |	ИзменениеЦенТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ИзменениеЦенТовары.ЦенаБазовая КАК ЦенаБазовая,
	               |	ИзменениеЦенТовары.ПроцентНаценки КАК ПроцентНаценки,
	               |	ИзменениеЦенТовары.НоваяЦена КАК НоваяЦена,
	               |	ИзменениеЦенТовары.Ссылка.Дата КАК Дата,
	               |	ИзменениеЦенТовары.Ссылка.Автор КАК Автор,
	               |	ИзменениеЦенТовары.Ссылка.Организация КАК Организация,
	               |	ИзменениеЦенТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ИзменениеЦенТовары.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	               |	ИзменениеЦенТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	ИзменениеЦенТовары.Ссылка.ТипЦен КАК ТипЦен,
	               |	ИзменениеЦенТовары.Ссылка.БазовыйТипЦен КАК БазовыйТипЦен,
	               |	ИзменениеЦенТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ИзменениеЦенТовары.Ссылка.ДатаНачалаДействия КАК ДатаНачалаДействия
	               |ИЗ
	               |	Документ.ИзменениеЦен.Товары КАК ИзменениеЦенТовары
	               |ГДЕ
	               |	ИзменениеЦенТовары.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.ДатаНачалаДействия);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТипЦен", ДокументСсылка.ТипЦен);
	Запрос.УстановитьПараметр("Граница", Новый Граница(ДокументСсылка.ДатаНачалаДействия, ВидГраницы.Исключая));
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", Реквизиты.ПодразделениеКомпании);

КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ИзменениеЦенТовары.Ссылка КАК Ссылка,
	               |	ИзменениеЦенТовары.Ссылка.ХозОперация КАК ХозОперация,
	               //|	ИзменениеЦенТовары.НомерСтроки КАК НомерСтроки,
	               |	ИзменениеЦенТовары.Ссылка.Организация КАК Организация,
	               |	ИзменениеЦенТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ИзменениеЦенТовары.Номенклатура КАК Номенклатура,
	               |	ИзменениеЦенТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ИзменениеЦенТовары.Ссылка.ТипЦен КАК ТипЦен,
	               |	ИзменениеЦенТовары.Ссылка.БазовыйТипЦен КАК БазовыйТипЦен,
	               |	ИзменениеЦенТовары.НоваяЦена КАК Цена,
	               |	ИзменениеЦенТовары.ЦенаБазовая КАК ЦенаБазовая,
	               |	ИзменениеЦенТовары.ПроцентНаценки КАК ПроцентНаценки,
	               |	ИзменениеЦенТовары.Ссылка.ДатаНачалаДействия КАК ДатаНачалаДействия,
	               |	ИзменениеЦенТовары.Ссылка.КурсДокумента КАК КурсДокумента
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ИзменениеЦен.Товары КАК ИзменениеЦенТовары
	               |ГДЕ
	               |	ИзменениеЦенТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЦены(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Цены";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ВтТаблицаТовары.ДатаНачалаДействия КАК Период,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ТипЦен КАК ТипЦен,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	              |	ВтТаблицаТовары.Цена КАК Цена,
	              |	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК ЦенаСтарая
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&Граница, ТипЦен = &ТипЦен) КАК ЦеныСрезПоследних
	              |		ПО ВтТаблицаТовары.Номенклатура = ЦеныСрезПоследних.Номенклатура
	              |			И ВтТаблицаТовары.ХарактеристикаНоменклатуры = ЦеныСрезПоследних.Характеристика
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.ТипЦен,
	              |	ВтТаблицаТовары.Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры,
	              |	ВтТаблицаТовары.Цена,
	              |	ВтТаблицаТовары.ДатаНачалаДействия,
	              |	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0)"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;	
КонецФункции

#КонецОбласти
		
	
#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Изменение цен
    КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ИзменениеЦен";
    КомандаПечати.Идентификатор = "ПФ_ИзменениеЦен";
    КомандаПечати.Представление = НСтр("ru = 'Изменение цен'; uk = 'Зміна цін'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК НоваяЦена
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
	
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.НоваяЦена = СтрокаТаблицы.НоваяЦена;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.НоваяЦена = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;
				Если Не ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНоменклатуры) Тогда 
					знчМРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(СтрокаТаблицы.ХарактеристикаНоменклатуры, "МРЦ", "")));
				Иначе
					знчМРЦ = 0;
				КонецЕсли;
				Товар.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ВернутьХарактеристикуПоЗначениюМРЦ(нСтрока.Ссылка,знчМРЦ);

			ИначеЕсли нСтрока.НоваяЦена > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
	
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК спрНоменклатура
		|ГДЕ
		|	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Изменение цен
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_ИзменениеЦен") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_ИзменениеЦен",
		НСтр("ru = 'Изменение цен'; uk = 'Зміна цін'"), 
		ПечатьИзменениеЦен(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ИзменениеЦен.ПФ_MXL_ИзменениеЦен");
	КонецЕсли;	
	
КонецПроцедуры

// Изменение цен
Функция ПечатьИзменениеЦен(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Цены.Регистратор.Дата КАК Дата,
	               |	Цены.Регистратор.Номер КАК Номер,
	               |	Цены.Регистратор КАК Ссылка,
	               |	Цены.ТипЦен КАК ПредставлениеТипаЦен,
	               |	Цены.Регистратор.ВалютаДокумента КАК ПредставлениеВалюты,
	               |	Цены.Номенклатура КАК Номенклатура,
	               |	Цены.Характеристика КАК Характеристика,
	               |	Цены.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	Цены.Цена КАК Цена,
	               |	Цены.ЦенаСтарая КАК ЦенаСтарая,
	               |	Цены.НомерСтроки КАК НомерСтроки,
	               |	Цены.Номенклатура.Артикул КАК Код
	               |ИЗ
	               |	РегистрСведений.Цены КАК Цены
	               |ГДЕ
	               |	Цены.Регистратор В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Цены.МоментВремени
	               |ИТОГИ
	               |	МАКСИМУМ(Дата),
	               |	МАКСИМУМ(Номер),
	               |	МАКСИМУМ(ПредставлениеТипаЦен),
	               |	МАКСИМУМ(ПредставлениеВалюты)
	               |ПО
	               |	Ссылка";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ИзменениеЦен";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИзменениеЦен.ПФ_MXL_ИзменениеЦен");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьТипЦен 			= Макет.ПолучитьОбласть("ТипЦен");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Изменение цен №%1 от %2'; uk = 'Зміна цін №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
				
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		 
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
						
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ЗаголовокЛок;
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
						
		ОбластьТипЦен.Параметры.Заполнить(ВыборкаДокументов);
		ТабличныйДокумент.Вывести(ОбластьТипЦен);
				
		ТабличныйДокумент.Вывести(ОбластьШапка);
				
		ВыборкаТовары = ВыборкаДокументов.Выбрать();
		нпп = 0;
		Пока ВыборкаТовары.Следующий() Цикл			
			нпп = нпп + 1;
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
			
			Если Не ПустаяСтрока(ВыборкаТовары.Характеристика) Тогда 
				стрНоменклатура = СокрЛП(ВыборкаТовары.Номенклатура) + ", " + СокрЛП(ВыборкаТовары.Характеристика);
				ОбластьСтрока.Параметры.Номенклатура = стрНоменклатура;
			КонецЕсли;			
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
		КонецЦикла;
				
		ОбластьПодвал.Параметры.КоличествоПозиций = нпп;
		ТабличныйДокумент.Вывести(ОбластьПодвал);					
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


#КонецОбласти	
	
	
	
	
	

	
	
	
	
	
	
	
	
	
	
	
	

#КонецЕсли
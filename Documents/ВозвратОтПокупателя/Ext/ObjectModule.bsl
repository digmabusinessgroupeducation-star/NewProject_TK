#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		ЗаполнитьДокументНаОснованииРеализацияТоваров(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВозвратПоставщику") Тогда 
		ЗаполнитьДокументНаОснованииВозвратПоставщику(ДанныеЗаполнения);	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
		ЗаполнитьДокументНаОснованииВыпускПродукции(ДанныеЗаполнения);	
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда 
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	Для Каждого Стр Из Товары Цикл 
		Стр.ДокументПродажи = Неопределено;
		Стр.Партия = Неопределено;
	КонецЦикла;
	
	GuidСистемыСоздания = "";
	ПроведенПоКарте = Ложь;
	Карта = Неопределено;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
		///Не регистрировать для выгрузки голобал центр
	Если НЕ ТорговаяПлощадка.РегистрироватьТК Тогда
		ДополнительныеСвойства.Вставить("ОбменТК");
	КонецЕсли;
	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.СкладКомпании КАК СкладКомпании,
		|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов
		|ИЗ
		|	Документ.ВозвратОтПокупателя КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
		ДополнительныеСвойства.Вставить("СтарыйДоговорДокумента", Выборка.ДоговорВзаиморасчетов);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияДоговораПоследовательности", Ложь);
	КонецЕсли;

	

	СуммаДокумента = Товары.Итог("СуммаВсего");
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияДоговораПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовИзменен", ДоговорВзаиморасчетов <> ДополнительныеСвойства.СтарыйДоговорДокумента);
	КонецЕсли;

КонецПроцедуры



Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ВозвратОтПокупателя.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ВозвратОтПокупателя.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьВозвратнаяТара(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);	
	
	ЗапасыСервер.ОтразаитьПродажиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	
	ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателями(ДополнительныеСвойства, Движения, Отказ);
	ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателямиПоДокументам(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);	

	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ВозвратОтПокупателяПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)	
	
	МассивНепроверяемыхРеквизитов = Новый Массив;	
	
	Если НЕ РегламентированныйУчет Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДС");
	КонецЕсли;
	
	
	//Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
	//	
	//	Для Каждого СтрТабл Из Товары Цикл 
	//		АдресОшибки = НСтр("ru=' в строке %НомерСтроки% списка ""Товары""';uk=' у рядку %НомерСтроки% списку ""Товари""'");
	//		АдресОшибки =  СтрЗаменить(АдресОшибки, "%НомерСтроки%", СтрТабл.НомерСтроки);
	//		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрТабл.НомерСтроки, "ДокументПродажи");
	//						
	//		Если Не ЗначениеЗаполнено(СтрТабл.ДокументПродажи) И Не СтрТабл.Себестоимость > 0 Тогда
	//			ТекстОшибки = НСтр("ru = 'Не заполнены колонки ""Документ продажи"" и ""Себестоимость""'; uk = 'Не заповнені колнки ""Документ продажу"" та ""Собівартість""'");
	//			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, Поле,,Отказ);
	//		КонецЕсли;
	//						
	//	КонецЦикла;
	//	
	//КонецЕсли;				
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	//ПродажиСервер.ПроверитьКорректностьВозвращаемыхТоваров(ЭтотОбъект, "Товары", Отказ);
	
КонецПроцедуры



#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументНаОснованииРеализацияТоваров(Знач РеализацияОснование)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Автор КАК Автор,
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ВозвратТоваровОтПокупателя) КАК ХозОперация,
	               |	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РеализацияТоваров.Организация КАК Организация,
	               |	РеализацияТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РеализацияТоваров.СкладКомпании КАК СкладКомпании,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	РеализацияТоваров.КурсДокумента КАК КурсДокумента,
	               |	РеализацияТоваров.ТипЦен КАК ТипЦен,
	               |	РеализацияТоваров.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваров.Ссылка КАК ДокументОснование,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка = &ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РеализацияТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	РеализацияТоваровТовары.Количество КАК Количество,
	               |	РеализацияТоваровТовары.Коэффициент КАК Коэффициент,
	               |	РеализацияТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	РеализацияТоваровТовары.Цена КАК Цена,
	               |	РеализацияТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	РеализацияТоваровТовары.Сумма КАК Сумма,
	               |	РеализацияТоваровТовары.СуммаВсего КАК СуммаВсего,
	               |	РеализацияТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	               |	РеализацияТоваровТовары.СуммаНДС КАК СуммаНДС,
	               |	РеализацияТоваровТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	РеализацияТоваровТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	РеализацияТоваровТовары.СуммаСкидки КАК СуммаСкидки
	               |ИЗ
	               |	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	               |ГДЕ
	               |	РеализацияТоваровТовары.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", РеализацияОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;
	
	Если ТаблицаТоваров.Количество() Тогда 
		Товары.Загрузить(ТаблицаТоваров);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВозвратПоставщику(Знач ВозвратПоставщику)
	
	Дата = ТекущаяДата();	
	Товары.Очистить();
	
	ИнициализироватьДокумент();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.ВозвратТоваровОтПокупателя) КАК ХозОперация,
	               |	ВозвратПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВозвратПоставщику.Организация.Контрагент КАК Контрагент,
	               |	ВозвратПоставщику.Контрагент КАК КонтрагентПолучатель,
	               |	ВозвратПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратПоставщику.КурсДокумента КАК КурсДокумента,
	               |	ВозвратПоставщику.СкладКомпании.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВозвратПоставщику.Ссылка КАК ДокументОснование
	               |ИЗ
	               |	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	               |ГДЕ
	               |	ВозвратПоставщику.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартииТоваровКомпании.Номенклатура КАК Номенклатура,
	               |	ПартииТоваровКомпании.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ПартииТоваровКомпании.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ПартииТоваровКомпании.Количество) * -1 КАК Количество,
	               |	СУММА(ПартииТоваровКомпании.СуммаБаз) * -1 КАК СуммаБаз,
	               |	ВЫБОР
	               |		КОГДА СУММА(ПартииТоваровКомпании.Количество) <> 0
	               |			ТОГДА СУММА(ПартииТоваровКомпании.СуммаБаз) * -1 / (СУММА(ПартииТоваровКомпании.Количество) * -1)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СебестоимостьЕд
	               |ПОМЕСТИТЬ втПартии
	               |ИЗ
	               |	РегистрНакопления.ПартииТоваровКомпании КАК ПартииТоваровКомпании
	               |ГДЕ
	               |	ПартииТоваровКомпании.Регистратор ССЫЛКА Документ.ВозвратПоставщику
	               |	И ПартииТоваровКомпании.Регистратор = &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПартииТоваровКомпании.Номенклатура,
	               |	ПартииТоваровКомпании.ХарактеристикаНоменклатуры,
	               |	ПартииТоваровКомпании.СтавкаНДС
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Номенклатура,
	               |	ХарактеристикаНоменклатуры
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВозвратПоставщикуТовары.Номенклатура КАК Номенклатура,
	               |	ВозвратПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВозвратПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВозвратПоставщикуТовары.Количество КАК Количество,
	               |	ВозвратПоставщикуТовары.Коэффициент КАК Коэффициент,
	               |	ВозвратПоставщикуТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ВозвратПоставщикуТовары.Цена КАК Цена,
	               |	ВозвратПоставщикуТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ВозвратПоставщикуТовары.Сумма КАК Сумма,
	               |	ВозвратПоставщикуТовары.Сумма КАК СуммаВсего,
	               |	ВозвратПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ВозвратПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	               |	ВозвратПоставщикуТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ЕСТЬNULL(втПартии.СебестоимостьЕд, 0) * ВозвратПоставщикуТовары.КоличествоБазовое КАК Себестоимость
	               |ИЗ
	               |	Документ.ВозвратПоставщику.Товары КАК ВозвратПоставщикуТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втПартии КАК втПартии
	               |		ПО ВозвратПоставщикуТовары.Номенклатура = втПартии.Номенклатура
	               |			И ВозвратПоставщикуТовары.ХарактеристикаНоменклатуры = втПартии.ХарактеристикаНоменклатуры
	               |			И ВозвратПоставщикуТовары.СтавкаНДС = втПартии.СтавкаНДС
	               |ГДЕ
	               |	ВозвратПоставщикуТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ВозвратПоставщику);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ТаблицаТовары = МассивРезультатов[2].Выгрузить();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);		
		Организация = Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(ВыборкаШапка.КонтрагентПолучатель);
		
		МенеджерСправочника = Справочники.ДоговорыКонтрагентов;		
		СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Ссылка, ХозОперация);
		ДоговорВзаиморасчетов = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(
										Контрагент, 
										Организация, 
										ПодразделениеКомпании, 
										СписокВидовДоговоров, 
										?(ЗначениеЗаполнено(ВалютаДокумента), ВалютаДокумента, Неопределено), 
										Дата);
		
		Товары.Загрузить(ТаблицаТовары);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВыпускПродукции(Знач ВыпускПродукции)
	
	Дата 		= ТекущаяДатаСеанса();
	ХозОперация = Справочники.ХозОперации.ВозвратТоваровОтПокупателя;
	
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.КурсДокумента КАК КурсДокумента,
	               |	ВыпускПродукции.ТипЦен КАК ТипЦен,
	               |	ВыпускПродукции.Ссылка КАК ДокументОснование,
	               |	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВыпускПродукции.Продукция.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		Коэффициент КАК Коэффициент,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		Ссылка КАК ДокументПродажи,
	               |		Ссылка КАК Партия
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &Ссылка
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)";
	
	Запрос.УстановитьПараметр("Ссылка", ВыпускПродукции);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		ТаблицаТовары = Выборка.Товары.Выгрузить();
		Товары.Загрузить(ТаблицаТовары);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
	СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	

КонецПроцедуры


#КонецОбласти


#КонецЕсли

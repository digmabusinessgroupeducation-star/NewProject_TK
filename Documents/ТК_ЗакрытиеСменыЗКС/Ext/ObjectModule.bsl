#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда



#Область ПрограммныйИнтерфейс



#КонецОбласти


#Область ОбработчикиСобытий


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗакрытиеСмены") Тогда
		ЗаполнитьДокументНаОснованииЗакрытияСмены(ДанныеЗаполнения);		
	Иначе	
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	

	СуммаДокумента = Товары.Итог("СуммаВсего");
	СуммаЭкваер    = ОплатаПлатежнымиКартами.Итог("Сумма");
	СуммаСкидки    = Товары.Итог("СуммаСкидки"); 
	
	мВозвраты = Товары.НайтиСтроки(Новый Структура("Возврат",Истина));
	тСуммаВозврат = 0;
	Для Каждого СтрВоз Из мВозвраты Цикл 
		 тСуммаВозврат = тСуммаВозврат +  -СтрВоз.СуммаВсего
	КонецЦикла;
	СуммаВозврат =  тСуммаВозврат;
	
	
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,Организация);
		
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрытиеСмены.Дата КАК Дата,
		|	ЗакрытиеСмены.Организация КАК Организация
		|ИЗ
		|	Документ.ТК_ЗакрытиеСменыЗКС КАК ЗакрытиеСмены
		|ГДЕ
		|	ЗакрытиеСмены.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);

	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;

	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", Организация <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;


КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	//ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);	
	Документы.ТК_ЗакрытиеСменыЗКС.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ТК_ЗакрытиеСменыЗКС.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСервер.ОтразитьПогрешностиДокументов(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьРозничныеПродажиПодакцизныхТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПотребностиТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьПотребностьОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	//ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);

	//ОценкаПроизводительности.ЗакончитьЗамерВремени("ЗакрытиеСменыПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

КонецПроцедуры


#КонецОбласти


Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Массив.Добавить(Движения.ОстаткиОрганизации);
		
	КонецЕсли;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры


#Область ИнициализацияИЗаполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	РегламентированныйУчет = Истина;
	
КонецПроцедуры // ИнициализироватьДокумент()


Процедура ЗаполнитьДокументНаОснованииЗакрытияСмены(ДанныеЗаполнения)		
	
	Товары.Очистить();
	ОплатаПлатежнымиКартами.Очистить();
	
	ХозОперация = Справочники.ХозОперации.ЗакрытиеСменыОрганизации;
	СписыватьОстатки = Ложь;
	РегламентированныйУчет = Истина;
	ДокументОснование = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗакрытиеСмены.Ссылка КАК ДокументОснование,
	               |	ЗакрытиеСмены.Дата КАК Дата,
	               |	ЗакрытиеСмены.Организация КАК Организация,
	               |	ЗакрытиеСмены.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗакрытиеСмены.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗакрытиеСмены.СкладКомпании КАК СкладКомпании,
	               |	ЗакрытиеСмены.ТипЦен КАК ТипЦен,
	               |	ЗакрытиеСмены.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗакрытиеСмены.КурсДокумента КАК КурсДокумента
	               |ИЗ
	               |	Документ.ЗакрытиеСмены КАК ЗакрытиеСмены
	               |ГДЕ
	               |	ЗакрытиеСмены.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗакрытиеСменыТовары.Номенклатура КАК Номенклатура,
	               |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыТовары.Количество КАК Количество,
	               |	ЗакрытиеСменыТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗакрытиеСменыТовары.Коэффициент КАК Коэффициент,
	               |	ЗакрытиеСменыТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ЗакрытиеСменыТовары.Цена КАК Цена,
	               |	ЗакрытиеСменыТовары.Сумма КАК Сумма,
	               |	ЗакрытиеСменыТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ЗакрытиеСменыТовары.СуммаНДС КАК СуммаНДС,
	               |	ЗакрытиеСменыТовары.СуммаАкцизногоНалога КАК СуммаАкцизногоНалога,
	               |	ЗакрытиеСменыТовары.СуммаВсего КАК СуммаВсего,
	               |	ЗакрытиеСменыТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ЗакрытиеСменыТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ЗакрытиеСменыТовары.Возврат КАК Возврат,
	               |	ЗакрытиеСменыТовары.КлассификаторПродажи КАК КлассификаторПродажи
	               |ИЗ
	               |	Документ.ЗакрытиеСмены.Товары КАК ЗакрытиеСменыТовары
	               |ГДЕ
	               |	ЗакрытиеСменыТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.КассаККМ КАК КассаККМ,
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.КодАвторизации КАК КодАвторизации,
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.НомерПлатежнойКарты КАК НомерПлатежнойКарты,
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.Сумма КАК Сумма
	               |ИЗ
	               |	Документ.ЗакрытиеСмены.ОплатаПлатежнымиКартами КАК ЗакрытиеСменыОплатаПлатежнымиКартами
	               |ГДЕ
	               |	ЗакрытиеСменыОплатаПлатежнымиКартами.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена КАК КассоваяСмена,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.Дата КАК Дата,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.Номер КАК НомерДокумента,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.Организация КАК Организация,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КассаККМ КАК КассаККМ,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.Кассир КАК Кассир,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.НачалоКассовойСмены КАК НачалоКассовойСмены,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.ОкончаниеКассовойСмены КАК ОкончаниеКассовойСмены,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.НомерСменыККМ КАК НомерСменыККМ,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.ДатаZотчета КАК ДатаZотчета,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.НомерZотчета КАК НомерZотчета,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.СуммаДокумента КАК СуммаДокумента,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.СуммаВозврат КАК СуммаВозврат,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.СуммаСкидки КАК СуммаСкидки,
	               |	ЗакрытиеСменыСнятыеКассы.КассоваяСмена.КоличествоЧеков КАК КоличествоЧеков
	               |ИЗ
	               |	Документ.ЗакрытиеСмены.СнятыеКассы КАК ЗакрытиеСменыСнятыеКассы
	               |ГДЕ
	               |	ЗакрытиеСменыСнятыеКассы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();	
	
	ВыборкаРеквизиты 	= МассивРезультатов[0].Выбрать();	
	тзТовары 			= МассивРезультатов[1].Выгрузить();
	тзПлатежныеКарты	= МассивРезультатов[2].Выгрузить();
	вбКассоваяСмена		= МассивРезультатов[3].Выбрать();	
	
	Если ВыборкаРеквизиты.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаРеквизиты);	
	КонецЕсли;
	
	Товары.Загрузить(тзТовары);
	ОплатаПлатежнымиКартами.Загрузить(тзПлатежныеКарты);
	
	Если вбКассоваяСмена.Следующий() Тогда 
		Если Не КассоваяСмена.Пустая() Тогда
			СменаЗКС = КассоваяСмена.ПолучитьОбъект();
			Если СменаЗКС.ПометкаУдаления Тогда 
				СменаЗКС.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;			
		Иначе
			СменаЗКС = Документы.ТК_КассоваяСменаЗКС.СоздатьДокумент();			
			СменаЗКС.Номер = вбКассоваяСмена.НомерДокумента;
		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(СменаЗКС, вбКассоваяСмена);
		
		СменаЗКС.СуммаЭкваер = ОплатаПлатежнымиКартами.Итог("Сумма");
		
		Попытка
			СменаЗКС.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());			
			СменаЗКС.Записать(РежимЗаписиДокумента.Запись);
		КонецПопытки;
		
		КассоваяСмена = СменаЗКС.Ссылка;
	КонецЕсли;
	
КонецПроцедуры
	

#КонецОбласти


#КонецЕсли

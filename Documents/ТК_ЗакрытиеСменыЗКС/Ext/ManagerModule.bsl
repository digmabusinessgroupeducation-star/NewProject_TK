#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);

	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаПогрешностиДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРозничныеПродажиПодакцизныхТоваров(Запрос, ТекстыЗапроса, Регистры);
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);	
	
	Если ДополнительныеСвойства.ДляПроведения.ПроводитьПоОстаткамОрганизации Тогда	
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровОрганизации(Запрос, ДополнительныеСвойства);
	КонецЕсли;
	

	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.ВалютаДокумента КАК Валюта,
	|	ДанныеДокумента.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ДанныеДокумента.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ДанныеДокумента.СкладКомпании КАК СкладКомпании,
	|	ДанныеДокумента.КурсДокумента КАК КурсДокумента,
	|	ДанныеДокумента.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ДанныеДокумента.СписыватьОстатки КАК СписыватьОстатки
	|ИЗ
	|	Документ.ТК_ЗакрытиеСменыЗКС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Валюта",                                   Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);

	Запрос.УстановитьПараметр("МоментВремени",                         ДополнительныеСвойства.ДляПроведения.Момент);
	ПроводитьПоОстаткамОрганизации = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период) И Реквизиты.СписыватьОстатки;
	Запрос.УстановитьПараметр("ПроводитьПоОстаткамОрганизации", ПроводитьПоОстаткамОрганизации);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",ПроводитьПоОстаткамОрганизации);	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);	
//	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",Реквизиты.ХозОперация);	
		
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЗакрытиеСменыТовары.НомерСтроки КАК НомерСтроки,
	               |	ЗакрытиеСменыТовары.Номенклатура КАК Номенклатура,
	               |	ЗакрытиеСменыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗакрытиеСменыТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗакрытиеСменыТовары.Коэффициент КАК Коэффициент,
	               |	ЗакрытиеСменыТовары.КоличествоБазовое КАК Количество,
	               |	ЗакрытиеСменыТовары.Цена КАК Цена,
	               |	ЗакрытиеСменыТовары.Сумма КАК Сумма,
	               |	ЗакрытиеСменыТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ЗакрытиеСменыТовары.СуммаНДС КАК СуммаНДС,
	               |	ЗакрытиеСменыТовары.СуммаАкцизногоНалога КАК СуммаАкцизногоНалога,
	               |	ЗакрытиеСменыТовары.СуммаВсего КАК СуммаВсего,
	               |	ЗакрытиеСменыТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ЗакрытиеСменыТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ЗакрытиеСменыТовары.Возврат КАК Возврат,
	               |	ЗакрытиеСменыТовары.КлассификаторПродажи КАК КлассификаторПродажи,
	               |	ЗакрытиеСменыТовары.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования,
	               |	ВЫБОР
	               |		КОГДА ЗакрытиеСменыТовары.КлассификаторПродажи = ЗНАЧЕНИЕ(Справочник.КлассификаторПродаж.ПустаяСсылка)
	               |			ТОГДА 100000
	               |		ИНАЧЕ ЗакрытиеСменыТовары.КлассификаторПродажи.Код
	               |	КОНЕЦ КАК Порядок,
	               |	ЗакрытиеСменыТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_ЗакрытиеСменыЗКС.Товары КАК ЗакрытиеСменыТовары
	               |ГДЕ
	               |	ЗакрытиеСменыТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПогрешностиДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПогрешностиДокументов";
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	&Организация КАК Организация,
	               |	&ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВложенныйЗапрос.ГруппаЦенообразования КАК ГруппаЦенообразования,
	               |	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	               |	СУММА(ВложенныйЗапрос.СуммаВсего) КАК Сумма,
	               |	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	               |	СУММА(ВложенныйЗапрос.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога,
	               |	ВЫРАЗИТЬ(СУММА(ВложенныйЗапрос.СуммаВсего) КАК ЧИСЛО(32, 2)) КАК ОкрСумма,
	               |	ВЫРАЗИТЬ(СУММА(ВложенныйЗапрос.СуммаНДС) КАК ЧИСЛО(32, 2)) КАК ОкрСуммаНДС,
	               |	ВЫРАЗИТЬ(СУММА(ВложенныйЗапрос.СуммаАкцизногоНалога) КАК ЧИСЛО(32, 2)) КАК ОкрСуммаАкцизногоНалога
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	               |		ВтТаблицаТовары.ГруппаЦенообразования КАК ГруппаЦенообразования,
	               |		ВтТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	               |		СУММА(ВтТаблицаТовары.СуммаВсего) КАК СуммаВсего,
	               |		СУММА(ВтТаблицаТовары.СуммаНДС) КАК СуммаНДС,
	               |		СУММА(ВтТаблицаТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога,
	               |		ВЫБОР
	               |			КОГДА СУММА(ВтТаблицаТовары.СуммаВсего) < 0
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ ЛОЖЬ
	               |		КОНЕЦ КАК Возврат
	               |	ИЗ
	               |		ВтТаблицаТовары КАК ВтТаблицаТовары
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ВтТаблицаТовары.Номенклатура,
	               |		ВтТаблицаТовары.ГруппаЦенообразования,
	               |		ВтТаблицаТовары.СтавкаНДС) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.ГруппаЦенообразования,
	               |	ВложенныйЗапрос.СтавкаНДС,
	               |	ВложенныйЗапрос.Возврат
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ВложенныйЗапрос.СуммаВсего) <> 0"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРозничныеПродажиПодакцизныхТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РозничныеПродажиПодакцизныхТоваров";
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	&Организация КАК Организация,
	               |	&ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ВложенныйЗапрос.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.СуммаАкцизногоНалога < 0
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗакрытиеСменыВозврат)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗакрытиеСмены)
	               |	КОНЕЦ КАК ХозОперация
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	               |		ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		СУММА(ВтТаблицаТовары.СуммаАкцизногоНалога) КАК СуммаАкцизногоНалога
	               |	ИЗ
	               |		ВтТаблицаТовары КАК ВтТаблицаТовары
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ВтТаблицаТовары.Номенклатура,
	               |		ВтТаблицаТовары.ХарактеристикаНоменклатуры) КАК ВложенныйЗапрос
	               |ГДЕ
	               |	ВложенныйЗапрос.СуммаАкцизногоНалога <> 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Номенклатура,
	               |	ВложенныйЗапрос.ХарактеристикаНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.СуммаАкцизногоНалога < 0
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗакрытиеСменыВозврат)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗакрытиеСмены)
	               |	КОНЕЦ"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции


#КонецОбласти





	
#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииОрганизаций");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("Организация");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.Организация 	   = Ссылка.Организация;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти



#КонецЕсли

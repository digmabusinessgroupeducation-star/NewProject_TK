#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий		
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПоступленияТоваров(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПокупателя") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПеремещения(ДанныеЗаполнения);
		
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДата();
	
	ДокументОснование = Неопределено;
	GuidСистемыСоздания = "";
	Карта = Неопределено;
	ПроведенПоКарте = Ложь;
	
	РаспределениеОтгрузки.Очистить();
	РаспределениеМаркетинга.Очистить();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	СуммаДокумента = Товары.Итог("СуммаВсего");	
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	Док.СкладКомпании КАК СкладКомпании
		|ИЗ
		|	Документ.РеализацияТоваров КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
		ДополнительныеСвойства.Вставить("СтарыйДоговорДокумента", Выборка.ДоговорВзаиморасчетов);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияДоговораПоследовательности", Ложь);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		// Выполнить контроль квот  -- перенес в проверку заполнения
				
		Если НЕ ДополнительныеСвойства.Свойство("ОбменТК") 
			И НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "РазрешитьОтрицательныеОстатки", Ложь) 
			И НЕ ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда
			ВыполнитьКонтрольОстатков(Отказ,РежимПроведения)
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияДоговораПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовИзменен", ДоговорВзаиморасчетов <> ДополнительныеСвойства.СтарыйДоговорДокумента);
	КонецЕсли;
	
	//ОтправитьУведомление(); //Svat задача Лозенко 27,05,2024

	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Не ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		
		ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
		
		ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
		Документы.РеализацияТоваров.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		Документы.РеализацияТоваров.ИнициалзироватьДанныеПоследовательности(Ссылка, ДополнительныеСвойства);
		
		ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОстаткиТоваровКомпании") Тогда
			ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);	
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровКомпании") Тогда
			ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПриходованиеТоваров") Тогда
			ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПродажиТоваровКомпании") Тогда
			ЗапасыСервер.ОтразаитьПродажиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПокупателями") Тогда
			ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателями(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПокупателямиПоДокументам") Тогда
			ВзаиморасчетыСервер.ОтразитьРасчетыСПокупателямиПоДокументам(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗаказыПокупателей") Тогда
			ЗаказыСервер.ОтразитьЗаказыПокупателей(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРезервыТоваров") Тогда
			ЗаказыСервер.ОтразитьРезервыТоваров(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаВозвратнаяТара") Тогда
			ЗапасыСервер.ОтразитьВозвратнаяТара(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;		
		
		
		ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);	
		
		УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);
		
		ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
		
		ОценкаПроизводительности.ЗакончитьЗамерВремени("РеализацияТоваровПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	Если НЕ ДоговорВзаиморасчетов.НеПроверятьПросроченнуюЗадолженность Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДатаПроверки = ?(ЭтоНовый(),КонецДня(ТекущаяДатаСеанса()),Дата);
		ОтсрочкаПлатежа =	Справочники.ДоговорыКонтрагентов.СрокОплатыПоДоговору(ДатаПроверки,ДоговорВзаиморасчетов);
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаЗапроса",ДатаПроверки);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		//Запрос.УстановитьПараметр("РегламентированныйУчет",РегламентированныйУчет);
		//Запрос.УстановитьПараметр("СрокОплатыЗадолженности",?(РегламентированныйУчет,ОтсрочкаПлатежа.Ф1,ОтсрочкаПлатежа.Ф2));
		Запрос.УстановитьПараметр("СрокОплатыЗадолженности_Ф1",ОтсрочкаПлатежа.Ф1);
		Запрос.УстановитьПараметр("СрокОплатыЗадолженности_Ф2",ОтсрочкаПлатежа.Ф2);
		
		//Запрос.Текст = "ВЫБРАТЬ
		//|	СУММА(РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток) КАК СуммаОстаток
		//|ИЗ
		//|	РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(
		//|			&ДатаЗапроса,
		//|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		//|				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПокупателямиПоДокументамОстатки
		//|ГДЕ
		//|	ВЫБОР
		//|			КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, &СрокОплатыЗадолженности + 1) <= НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ)
		//|				ТОГДА ИСТИНА
		//|			ИНАЧЕ ЛОЖЬ
		//|		КОНЕЦ";
		Запрос.Текст = "ВЫБРАТЬ
		|	СУММА(РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток) КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(&ДатаЗапроса, ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК РасчетыСПокупателямиПоДокументамОстатки
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, ВЫБОР
		|						КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет = ИСТИНА
		|							ТОГДА &СрокОплатыЗадолженности_Ф1
		|						ИНАЧЕ &СрокОплатыЗадолженности_Ф2
		|					КОНЕЦ + 1) <= НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			СуммаДолга = 0;
			Пока Выборка.Следующий() Цикл 
				СуммаДолга = ?(ЗначениеЗаполнено(Выборка.СуммаОстаток),Выборка.СуммаОстаток,0);
			КонецЦикла;
			
			Если СуммаДолга > 10 Тогда
				Отказ = Истина;
				ТекстСообщения = "ВНИМАНИЕ! Обнаружено превышение срока оплаты долга контрагента "+Строка(Контрагент);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения); 
				
				
				Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
					ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
				КонецЕсли;
				ДополнительныеСвойства.РезультатОбработки.ДобавитьСтроку(ТекстСообщения);
				
				
			КонецЕсли;
			
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	//Проверка заполнения ТочкиДоставки
	Если НЕ ЗначениеЗаполнено(ТочкаДоставки) И Не ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда 
		
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		Если Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь,"НеКонтролироватьЗаполнениеТД", Истина) Тогда 
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Запрещено проводить Реализацию товаров с пустой точкой доставки'; uk = 'Заборонено проводити документ з пустою точкою доставки'"),
			ЭтотОбъект,"ТочкаДоставки"
			);
		КонецЕсли;
	КонецЕсли;
	
	//Проверка квот
	Пользователь 					= Пользователи.ТекущийПользователь();
	КонтролироватьКвоты				= Не ДоговорВзаиморасчетов.НеКонтролироватьКвоты И Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"НеКонтролироватьОтгрузкиПоКвотам",Ложь);			
	
	Если КонтролироватьКвоты  И Не ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда 
		ЭтоВнутреннийКонтрагент 		= ЗначениеНастроекПовтИсп.ЭтоВнутреннийКонтрагент(Контрагент);
		КонтролироватьКвотыВнКонтр 		= Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"ОтключитьКонтрольКвотДляВнутреннихКонтрагентов",Ложь);	
		
		Если Не ЭтоВнутреннийКонтрагент ИЛИ ЭтоВнутреннийКонтрагент И КонтролироватьКвотыВнКонтр Тогда 
			ВыполнитьКонтрольКвот(Отказ);	
		КонецЕсли;
		
	КонецЕсли;

	Если ХозОперация = Справочники.ХозОперации.АктОбОказанииУслуг Тогда
		МассивУдалить = Новый Массив;
		МассивУдалить.Добавить("ТорговаяПлощадка");
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивУдалить);
	КонецЕсли;	
	
	//Данченко
	//// Выполнить контроль разделения Номенклатуры по Организациям Vov41k 08.01.2023
	//Если ЗначениеНастроекПовтИсп.ДоступноРазделениеТоваровПоОрганизациям(ТорговаяПлощадка) Тогда 
	//	ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ);
	//КонецЕсли;		

		
КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументНаОснованииПоступленияТоваров(Знач ПоступлениеОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Хозоперации.РеализацияТоваров) КАК ХозОперация,
	|	ПоступлениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
	|	ПоступлениеТоваров.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	|	ПоступлениеТоваров.Ссылка КАК ДокументОснование,
	|	ПоступлениеТоваров.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		СтавкаНДС КАК СтавкаНДС
	|	) КАК Товары
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ПоступлениеОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",Организация,ТорговаяПлощадка,"Реализация"));
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Товары, СтруктураДействий, Неопределено);
	
	Автор =  Пользователи.ТекущийПользователь();
	Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, СкладКомпании, Дата);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииЗаказаПокупателя(Знач ЗаказПокупателя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Ссылка КАК ДокументОснование,
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.РеализацияТоваров) КАК ХозОперация,
	               |	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПокупателя.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПокупателя.ТипЦен КАК ТипЦен,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	               |	ЗаказПокупателя.Менеджер КАК Менеджер
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	ЗаказПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЗаказПокупателяТовары.Количество КАК Количество,
	               |	ЗаказПокупателяТовары.Коэффициент КАК Коэффициент,
	               |	ЗаказПокупателяТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ЗаказПокупателяТовары.Цена КАК Цена,
	               |	ЗаказПокупателяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ЗаказПокупателяТовары.Сумма КАК Сумма,
	               |	ЗаказПокупателяТовары.СуммаВсего КАК СуммаВсего,
	               |	ЗаказПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ЗаказПокупателяТовары.СуммаНДС КАК СуммаНДС,
	               |	ЗаказПокупателяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ЗаказПокупателяТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ЗаказПокупателяТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ЗаказПокупателяТовары.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования
	               |ИЗ
	               |	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	               |ГДЕ
	               |	ЗаказПокупателяТовары.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервыТоваровОстатки.Номенклатура КАК Номенклатура,
	               |	РезервыТоваровОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РезервыТоваровОстатки.Характеристика КАК ХарактеристикаНоменклатуры,
	               |	РезервыТоваровОстатки.РезервОстаток КАК Количество,
	               |	РезервыТоваровОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	               |	РезервыТоваровОстатки.РезервОстаток КАК КоличествоБазовое,
	               |	0 КАК Цена,
	               |	0 КАК ЦенаБезНалогов,
	               |	0 КАК Сумма,
	               |	0 КАК СуммаВсего,
	               |	РезервыТоваровОстатки.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	               |	0 КАК СуммаНДС,
	               |	0 КАК СуммаБезНалогов,
	               |	0 КАК ПроцентСкидки,
	               |	0 КАК СуммаСкидки,
	               |	РезервыТоваровОстатки.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования
	               |ИЗ
	               |	РегистрНакопления.РезервыТоваров.Остатки(&НаДату, Заказ = &Ссылка) КАК РезервыТоваровОстатки";
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказПокупателя);
	Запрос.УстановитьПараметр("Склад", ЗаказПокупателя.СкладКомпании);
	Если ЭтоНовый() Тогда
		НаДату = КонецДня(ТекущаяДата())
	Иначе
		НаДату = МоментВремени();
	КонецЕсли;
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаТоваровРезерв = МассивРезультатов[2].Выгрузить();
	Автор =  Пользователи.ТекущийПользователь();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;
	
	Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, СкладКомпании, Дата);
	
	//Если ТаблицаТоваров.Количество() Тогда 
	//	Товары.Загрузить(ТаблицаТоваров);
	//КонецЕсли;
	ТаблицаТоваров.Колонки.Добавить("ТипЦен");
	ТаблицаТоваровРезерв.Колонки.Добавить("ТипЦен");
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(ЭтотОбъект));
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ЭтотОбъект,"Цены"));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	//СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",ЭтотОбъект.Организация,ЭтотОбъект.ТорговаяПлощадка,"Реализация"));
	
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	ТаблицаТоваровДкумент =  ?(ТаблицаТоваровРезерв.Количество() = 0,ТаблицаТоваров,ТаблицаТоваровРезерв);
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ТаблицаТоваровДкумент, СтруктураДействий, Неопределено);
	
	Товары.Загрузить(ТаблицаТоваровДкумент);
	
	//РаспределениеОтгрузки.Очистить();
	//НовСтрока = РаспределениеОтгрузки.Добавить();
	//НовСтрока.ЗаказПокупателя = ЗаказПокупателя;
	
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПеремещения(Знач Перемещение)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Справочник.Хозоперации.РеализацияТоваров) КАК ХозОперация,
	|	ПеремещениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ПеремещениеТоваров.Организация КАК Организация,
	|	ПеремещениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПеремещениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПеремещениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПеремещениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПеремещениеТоваров.КурсДокумента КАК КурсДокумента,
	|	ПеремещениеТоваров.ТипЦен КАК ТипЦен,
	|	ПеремещениеТоваров.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПеремещениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПеремещениеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПеремещениеТоваровТовары.Количество КАК Количество,
	|	ПеремещениеТоваровТовары.Коэффициент КАК Коэффициент,
	|	ПеремещениеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	ПеремещениеТоваровТовары.Цена КАК Цена,
	|	ПеремещениеТоваровТовары.Сумма КАК Сумма,
	|	ПеремещениеТоваровТовары.СуммаВсего КАК СуммаВсего,
	|	ПеремещениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	|	ПеремещениеТоваровТовары.СуммаНДС КАК СуммаНДС,
	|	ПеремещениеТоваровТовары.Номенклатура.ГруппаЦенообразования КАК ГруппаЦенообразования
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Перемещение);
	
	Если ЭтоНовый() Тогда
		НаДату = КонецДня(ТекущаяДата())
	Иначе
		НаДату = МоментВремени();
	КонецЕсли;
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();	
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	Автор =  Пользователи.ТекущийПользователь();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;
	
	Ответственный = ОтветственныеЛица.ПодобратьОтветственноеЛицоОрганизации(Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаВыпискуДокументов, СкладКомпании, Дата);
	
	//КонецЕсли;
	ТаблицаТоваров.Колонки.Добавить("ТипЦен");
	ТаблицаТоваров.Колонки.Добавить("ЦенаБезНалогов");
	ТаблицаТоваров.Колонки.Добавить("СуммаБезНалогов");
	ТаблицаТоваров.Колонки.Добавить("СуммаСкидки");
	ТаблицаТоваров.Колонки.Добавить("ПроцентСкидки");
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(ЭтотОбъект));
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ЭтотОбъект,"Цены"));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	//СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре",Новый Структура("Организация,ТорговаяПлощадка,ТипСтавкиНДС",ЭтотОбъект.Организация,ЭтотОбъект.ТорговаяПлощадка,"Реализация"));
	
	//	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ТаблицаТоваров, СтруктураДействий, Неопределено);
	
	Товары.Загрузить(ТаблицаТоваров);
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
	СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);
	Ответственный = СкладКомпании.Ответственный;
	
	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонтрольКвот

Процедура ВыполнитьКонтрольКвот(Отказ = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ДополнительныеСвойства.Вставить("ВидКвоты", УправлениеСвойствами.ЗначениеСвойства(ТочкаДоставки, "ВидКвотыОтгрузки"));
	ДополнительныеСвойства.Вставить("ВидКвотыФМУ", УправлениеСвойствами.ЗначениеСвойства(ТочкаДоставки, "ВидКвотыОтгрузкиФМУ"));
	ДополнительныеСвойства.Вставить("Область", ЗначениеНастроекПовтИсп.ПолучитьОбластьТочкиДоставки(ТочкаДоставки));
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ПродажиСервер.ТаблицаОграниченийПоКвотамОтгрузки(Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	СообщитьОбОшибкахОграниченийКвот(МенеджерВременныхТаблиц, Отказ);
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&ХозОперация КАК ХозОперация,
	|	&Организация КАК Организация,
	|	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	&СкладКомпании КАК СкладКомпании,
	|	&Контрагент КАК Контрагент,
	|	&ТочкаДоставки КАК ТочкаДоставки
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).Производитель КАК Производитель,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаТоваров.КоличествоБазовое КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	Запрос.УстановитьПараметр("Дата",								Дата);
	Запрос.УстановитьПараметр("Организация",						Организация);
	Запрос.УстановитьПараметр("СкладКомпании",						СкладКомпании);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",				ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент",							Контрагент);
	Запрос.УстановитьПараметр("ТочкаДоставки",						ТочкаДоставки);
	Запрос.УстановитьПараметр("ХозОперация",						ХозОперация);
	Запрос.УстановитьПараметр("ТаблицаТоваров",						Товары.Выгрузить());	
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Процедура СообщитьОбОшибкахОграниченийКвот(МенеджерВременныхТаблиц, ЕстьОшибки) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПревышенияПоКвотам.Номенклатура КАК Номенклатура,
	|	ПревышенияПоКвотам.Производитель КАК Производитель,
	|	ПревышенияПоКвотам.Количество КАК Количество,
	|	ПревышенияПоКвотам.текПроцент КАК текПроцент,
	|	ПревышенияПоКвотам.Порог КАК Порог,
	|	ПревышенияПоКвотам.Процент КАК Процент,
	|	ПревышенияПоКвотам.ИтогоПоПроизводителю КАК ИтогоПоПроизводителю,
	|	ПревышенияПоКвотам.ПревышенПорог КАК ПревышенПорог,
	|	ПревышенияПоКвотам.ПроверятьПревышениеПроцента КАК ПроверятьПревышениеПроцента,
	|	ПревышенияПоКвотам.ПревышенПроцент КАК ПревышенПроцент
	|ИЗ
	|	ПревышенияПоКвотам КАК ПревышенияПоКвотам";
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;	
	
	ОбщегоНазначения.СообщитьПользователю(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'В документе превышена квота по отгрузке на точку доставки %1'; uk = 'У документі перевищена квота по відвантаженню на точку доставки %1'"),
	Строка(ТочкаДоставки)),
	ЭтотОбъект,
	,
	,
	ЕстьОшибки);
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 
		МаксКоличество = Макс(Цел(((Выборка.ИтогоПоПроизводителю - Выборка.Количество)* Выборка.Процент)/(100 - Выборка.Процент)), Выборка.Порог);
		
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Превышена квота по <%1> текущее кол-во: %2 (%3%), макс. кол-во: %4%5'; uk = 'Перевищена квота по <%1> поточна кіль-ть: %2 (%3%), макс. кіль-ть: %4%5'"),
		Выборка.Номенклатура,
		Выборка.Количество,
		Выборка.текПроцент,
		МаксКоличество,
		?(Выборка.ПроверятьПревышениеПроцента И Выборка.ПревышенПроцент,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '; установленная квота - %1%'; uk = '; встановлена квота - %1%'"),
		Выборка.Процент),
		"")
		),
		ЭтотОбъект); 
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

Процедура СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц)
	
	Если ТаблицаОшибок.Количество()> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Перемещение превышает остаток товара компании на складе %1 %2 %3';uk='Переміщення перевищує залишок товару компанії на складі %1 %2 %3'"),
		СкладКомпании);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстСообщения,
		ЭтотОбъект);	
		
		Для Каждого СтрокаТаблицы Из ТаблицаОшибок Цикл 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Номенклатура: %1, недостаточно %2 %3';uk='Номенклатура: %1, недостатньо %2 %3'"),
			НоменклатураКлиентСервер.ПредставлениеНоменклатуры(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.ХарактеристикаНоменклатуры),
			СтрокаТаблицы.Количество,
			СтрокаТаблицы.ЕдиницаИзмерения);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Ссылка);
			
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ВыполнитьКонтрольОстатков(Отказ,РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ЗапасыСервер.УстановитьБлокировкуОстатоковТоваровКомпании(МенеджерВременныхТаблиц);
	
	Если РежимПроведения = РежимПроведенияДокумента.Оперативный тогда
		ДатаКонтроля =  ТекущаяДатаСеанса();
	Иначе
		ДатаКонтроля =  Дата;
	КонецЕсли;
	
	ЗапасыСервер.ТаблицаОстатковТоваровКомпании(Ссылка, СкладКомпании, ДатаКонтроля, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	ТаблицаОшибок = ЗапасыСервер.ТаблицаОшибокОстатковТоваров();
	ЗапасыСервер.ЗаполнитьТаблицуОшибок(МенеджерВременныхТаблиц,ДополнительныеСвойства,ТаблицаОшибок,Отказ);	
	СообщитьОбОшибках(ТаблицаОшибок,МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ)
	
	СписокНоменклатуры = Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	
	СоответствиеНоменклатуры = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(
								Организация,
								СписокНоменклатуры,
								ТорговаяПлощадка);
								
	Для Каждого Стр Из СоответствиеНоменклатуры Цикл 
		Если Стр.Значение.Организация <> Организация Тогда 
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номенклатура: %1 закреплена за организацией ""%2""'; uk = 'Номенклатура: %1 закріплена за організацією ""%2""'"),
					Стр.Ключ,
					Стр.Значение.Организация),
				ЭтотОбъект,
				"Товары");
				
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтправитьУведомление()
	
	Если ЭтотОбъект.СкладКомпании.Код = "AC000021" И ЭтотОбъект.ТочкаДоставки.ПорядокСигаретный = 99900 Тогда
		
		ЭлАдрес = "zhuravel.denis1999@gmail.com";
		
		МассивДанных = Новый Массив;        
		МассивДанных.Добавить(Метаданные.Документы.РеализацияТоваров);
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("ФормаСписка", МассивДанных);
		
		мОбъектыПечати = Новый Массив();
		мОбъектыПечати.Добавить(ЭтотОбъект.Ссылка);
		
		НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
		НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.XLSX);
		
		ПечатнаяФорма = ВРег("Расходная накладная (Без шапки)");
		ДокументНапечатан = Ложь;
		Для Каждого Команда Из КомандыПечати Цикл 
			
			Если ВРег(Команда.Представление) = ПечатнаяФорма Тогда
				тзПечатныеФормы = УправлениеПечатью.НапечататьВФайл(Команда,мОбъектыПечати,НастройкиСохранения);  
				ДокументНапечатан = Истина;
				Прервать;
			КонецЕсли;    
		КонецЦикла;
		
		Если ДокументНапечатан Тогда
			Если тзПечатныеФормы.Количество() = 0 Тогда Возврат; КонецЕсли;
			
			ДД = тзПечатныеФормы[0].ДвоичныеДанные;  
			ИмяФайла = тзПечатныеФормы[0].ИмяФайла;
			АдресВХ =    ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор());
			МассивВложения = Новый Массив;
			СтруктураВложения = Новый Структура;
			СтруктураВложения.Вставить("Представление",ИмяФайла);
			СтруктураВложения.Вставить("АдресВоВременномХранилище",АдресВХ);
			СтруктураВложения.Вставить("Кодировка","");
			СтруктураВложения.Вставить("Идентификатор","");
			МассивВложения.Добавить(СтруктураВложения);
			
			ПараметрыПисьма = Новый Структура;
			ПараметрыПисьма.Вставить("Кому",ЭлАдрес);
			ПараметрыПисьма.Вставить("Тема","Накладная");
			ПараметрыПисьма.Вставить("Тело",ИмяФайла);
			ПараметрыПисьма.Вставить("Вложения",МассивВложения);
			
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
			РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти
	
	
#КонецЕсли

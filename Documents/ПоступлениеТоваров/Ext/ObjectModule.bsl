#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий				
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПоставщику") Тогда 
		ЗаполнитьДокументНаОснованииЗаказаПоставщику(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВозвратПоставщику") Тогда 
		ЗаполнитьДокументНаОснованииВозвратаПоставщику(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		ЗаполнитьДокументНаОснованииРеализацииТоваров(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
		ЗаполнитьДокументНаОснованииВыпускаСРеализацией(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДата();
	
	ДокументОснование = Неопределено;
	ВхДокНомер = "";
	ВхДокДата = Неопределено;
	ИДДокументаОтгрузки = "";
	НомерRB = "";
	СтатусRB = Неопределено;
	ИспользоватьЦеныИзПротоколов =  ПодразделениеКомпании.ИспользоватьПротоколыСогласованныхЦен И НЕ Контрагент.НеИспользоватьПротоколыЦен;

	РаспределениеПоставки.Очистить();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	СуммаДокумента = Товары.Итог("СуммаВсего") + Услуги.Итог("Сумма");
	
	ОрганизацияАналитики = Организация;
	Если ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровТранзиты Тогда
		ОрганизацияАналитики = ДоговорВзаиморасчетов.Организация;
	КонецЕсли;
	
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,ОрганизацияАналитики);


	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен И НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.СкладКомпании КАК СкладКомпании,
		|	Док.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА Док.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)
		|			ТОГДА Док.ДоговорВзаиморасчетов.Организация
		|		ИНАЧЕ Док.Организация
		|	КОНЕЦ КАК Организация
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента", Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
		ДополнительныеСвойства.Вставить("СтарыйДоговорДокумента", Выборка.ДоговорВзаиморасчетов);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);	
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияДоговораПоследовательности", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияДоговораПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("ДоговорВзаиморасчетовИзменен", ДоговорВзаиморасчетов <> ДополнительныеСвойства.СтарыйДоговорДокумента);
	КонецЕсли;
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ОрганизацияПроверки = Организация;
		Если ХозОперация = Справочники.ХозОперации.ПоступлениеТоваровТранзиты Тогда
			ОрганизацияПроверки = ДоговорВзаиморасчетов.Организация;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", ОрганизацияПроверки <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;

	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ОбменДанными.Загрузка И Не Проведен И ЗначениеЗаполнено(СкладКомпании) Тогда
		спНомеровСтрок = ОбщегоНазначенияТК.ПроверитьЗаполнениеХарактеристикиТЧ(Дата, СкладКомпании, Товары);	
		Если ЗначениеЗаполнено(спНомеровСтрок) Тогда
			Отказ = Истина;	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Не заполнена характеристика в строках - "+спНомеровСтрок,ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	Если ДополнительныеРасходы Тогда 
		ПроверяемыеРеквизиты.Добавить("ДопРасходы_Контрагент");
		ПроверяемыеРеквизиты.Добавить("ДопРасходы_Договор");
		ПроверяемыеРеквизиты.Добавить("ДопРасходы_СпособРаспределения");
		ПроверяемыеРеквизиты.Добавить("ДопРасходы_Сумма");
		ПроверяемыеРеквизиты.Добавить("ДопРасходы_СтавкаНДС");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ПоступлениеТоваров.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ПоступлениеТоваров.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
    ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);

	
	ВзаиморасчетыСервер.ОтразитьРасчетыСПоставщиками(ДополнительныеСвойства, Движения, Отказ);
	ВзаиморасчетыСервер.ОтразитьРасчетыСПоставщикамиПоДокументам(ДополнительныеСвойства, Движения, Отказ);
	
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗаказыПоставщикам") Тогда
		ЗаказыСервер.ОтразитьЗаказыПоставщика(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОтправитьУведомление();
	
КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор =  Пользователи.ТекущийПользователь();  ЗакрытиеЗаказовПоставщикам = 1;
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
	СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
	РегламентированныйУчет = Истина;
	Если ЗначениеЗаполнено(СкладКомпании) Тогда
		ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(СкладКомпании);		
	КонецЕсли;
	
	
КонецПроцедуры


Процедура ЗаполнитьДокументНаОснованииЗаказаПоставщику(ЗаказОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ЗаказПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЗаказПоставщикуТранзиты)
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХозОперации.ПоступлениеТоваровТранзиты)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Хозоперации.ПоступлениеТоваров)
	               |	КОНЕЦ КАК ХозОперация,
	               |	ЗаказПоставщику.Организация КАК Организация,
	               |	ЗаказПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЗаказПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ЗаказПоставщику.Контрагент КАК Контрагент,
	               |	ЗаказПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ЗаказПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ЗаказПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	ЗаказПоставщику.КурсДокумента КАК КурсДокумента,
	               |	ЗаказПоставщику.ТипЦен КАК ТипЦен,
	               |	ЗаказПоставщику.Ссылка КАК ДокументОснование,
	               |	ЗаказПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ЗаказПоставщику.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		Коэффициент КАК Коэффициент,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Сумма КАК Сумма,
	               |		СуммаБезНалогов КАК СуммаБезНалогов
	               |	) КАК Товары,
	               |	2 КАК ЗакрытиеЗаказовПоставщикам
	               |ИЗ
	               |	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	               |ГДЕ
	               |	ЗаказПоставщику.Ссылка = &ДокументОснование";
		
	Запрос.УстановитьПараметр("ДокументОснование", ЗаказОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		ИспользоватьЦеныИзПротоколов = ПодразделениеКомпании.ИспользоватьПротоколыСогласованныхЦен;

		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			Если ИспользоватьЦеныИзПротоколов Тогда
				НоваяСтрока.КоличествоПоставщика = НоваяСтрока.Количество;
			КонецЕсли;
			
			СтруктураДействий = Новый Структура;
			
			СтруктураДействий.Вставить("ЗаполнитьЦены", Новый Структура("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(ЭтотОбъект, "СогласованиеЦен")));

			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
			
		КонецЦикла;
	КонецЕсли;
	
	Распределение = ЭтотОбъект.РаспределениеПоставки.Добавить();	
	Распределение.ЗаказПоставщику = ЗаказОснование;
	
	Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВозвратаПоставщику(ВозвратОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ВозвратПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщику)
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.Хозоперации.ПоступлениеТоваров)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Хозоперации.ПоступлениеТоваровТранзиты)
	               |	КОНЕЦ КАК ХозОперация,
	               |	2 КАК ЗакрытиеЗаказовПоставщикам,
	               |	ВозвратПоставщику.Организация КАК Организация,
	               |	ВозвратПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВозвратПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВозвратПоставщику.Контрагент КАК Контрагент,
	               |	ВозвратПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратПоставщику.КурсДокумента КАК КурсДокумента,
	               |	ВозвратПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ВозвратПоставщику.ТипЦен КАК ТипЦен,
	               |	ВозвратПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратПоставщику.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	               |ГДЕ
	               |	ВозвратПоставщику.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ВозвратОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Товары.Очистить();
		
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			
			СтруктураДействий = Новый Структура;
			
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));	
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, Неопределено);
			
		КонецЦикла;
	КонецЕсли;
	
	Автор =  Пользователи.ТекущийПользователь();
	
КонецПроцедуры


Процедура ЗаполнитьДокументНаОснованииРеализацииТоваров(Реализация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваров.Организация КАК ПоставщикОрганизация,
	               |	РеализацияТоваров.Контрагент КАК ПокупательКонтрагент,
	               |	РеализацияТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РеализацияТоваров.Организация.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	РеализацияТоваров.КурсДокумента КАК КурсДокумента,
	               |	РеализацияТоваров.Номер КАК ВхДокНомер,
	               |	РеализацияТоваров.Дата КАК ВхДокДата,
	               |	РеализацияТоваров.Ссылка КАК ДокументОснование,
	               |	РеализацияТоваров.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		Коэффициент КАК Коэффициент,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Реализация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		ХозОперация = Справочники.ХозОперации.ПоступлениеТоваров;
		
		Организация = Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(Выборка.ПокупательКонтрагент);
		
		СкладКомпании = Справочники.СкладыКомпании.ПолучитьСкладПоТочкеДоставки(Выборка.ТочкаДоставки);
		ПодразделениеКомпании = СкладКомпании.Подразделение;
		ТорговаяПлощадка = СкладКомпании.ТорговаяПлощадка;		
		
		МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
		
		СписокВидовДоговора = Новый СписокЗначений;
		СписокВидовДоговора.Добавить(Справочники.ВидыДоговоров.Поставка);
		СписокВидовДоговора.Добавить(Справочники.ВидыДоговоров.ПоставкаПереводДолга);

		ДоговорВзаиморасчетов = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, ПодразделениеКомпании, СписокВидовДоговора, , ТекущаяДата());
				
		Товары.Очистить();
		Товары.Загрузить(Выборка.Товары.Выгрузить());
	КонецЕсли;		
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииВыпускаСРеализацией(ВыпускПродукции)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВыпускПродукции.Организация КАК ПоставщикОрганизация,
	               |	ВыпускПродукции.Контрагент КАК ПокупательКонтрагент,
	               |	ВыпускПродукции.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВыпускПродукции.Организация.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.КурсДокумента КАК КурсДокумента,
	               |	ВыпускПродукции.Номер КАК ВхДокНомер,
	               |	ВыпускПродукции.Дата КАК ВхДокДата,
	               |	ВыпускПродукции.Ссылка КАК ДокументОснование,
	               |	ВыпускПродукции.Продукция.(
	               |		Номенклатура КАК Номенклатура,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Коэффициент КАК Коэффициент,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки
	               |	) КАК Продукция
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &Ссылка
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)";
	
	Запрос.УстановитьПараметр("Ссылка", ВыпускПродукции);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		ХозОперация = Справочники.ХозОперации.ПоступлениеТоваров;
		
		Организация = Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(Выборка.ПокупательКонтрагент);
		
		СкладКомпании = Справочники.СкладыКомпании.ПолучитьСкладПоТочкеДоставки(Выборка.ТочкаДоставки);		
		ПодразделениеКомпании = СкладКомпании.Подразделение;		
		ТорговаяПлощадка = СкладКомпании.ТорговаяПлощадка;		
		
		МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
		
		СписокВидовДоговора = Новый СписокЗначений;
		СписокВидовДоговора.Добавить(Справочники.ВидыДоговоров.Поставка);
		СписокВидовДоговора.Добавить(Справочники.ВидыДоговоров.ПоставкаПереводДолга);

		ДоговорВзаиморасчетов = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, ПодразделениеКомпании, СписокВидовДоговора, , ТекущаяДата());
				
		Товары.Очистить();
		Товары.Загрузить(Выборка.Продукция.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьУведомление()
	
	Если ЭтотОбъект.СкладКомпании.Код = "AC000021" или ЭтотОбъект.СкладКомпании.Код = "AC000027" Тогда
		
		ЭлАдрес = "Dmitriy.Ovcharov@digma.ua; kaydanovzzz@gmail.com";
		
		МассивДанных = Новый Массив;        
		МассивДанных.Добавить(Метаданные.Документы.ПоступлениеТоваров);
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("ФормаСписка", МассивДанных);
		
		мОбъектыПечати = Новый Массив();
		мОбъектыПечати.Добавить(ЭтотОбъект.Ссылка);
		
		НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
		НастройкиСохранения.ФорматыСохранения.Добавить(ТипФайлаТабличногоДокумента.XLSX);
		
		ПечатнаяФорма = ВРег("Приходная накладная");
		ДокументНапечатан = Ложь;
		Для Каждого Команда Из КомандыПечати Цикл 
			
			Если ВРег(Команда.Представление) = ПечатнаяФорма Тогда
				тзПечатныеФормы = УправлениеПечатью.НапечататьВФайл(Команда,мОбъектыПечати,НастройкиСохранения);  
				ДокументНапечатан = Истина;
				Прервать;
			КонецЕсли;    
		КонецЦикла;
		
		Если ДокументНапечатан Тогда
			Если тзПечатныеФормы.Количество() = 0 Тогда Возврат; КонецЕсли;
			
			ДД = тзПечатныеФормы[0].ДвоичныеДанные;  
			ИмяФайла = тзПечатныеФормы[0].ИмяФайла;
			АдресВХ =    ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор());
			МассивВложения = Новый Массив;
			СтруктураВложения = Новый Структура;
			СтруктураВложения.Вставить("Представление",ИмяФайла);
			СтруктураВложения.Вставить("АдресВоВременномХранилище",АдресВХ);
			СтруктураВложения.Вставить("Кодировка","");
			СтруктураВложения.Вставить("Идентификатор","");
			МассивВложения.Добавить(СтруктураВложения);
			
			ПараметрыПисьма = Новый Структура;
			ПараметрыПисьма.Вставить("Кому",ЭлАдрес);
			ПараметрыПисьма.Вставить("Тема","Накладная");
			ПараметрыПисьма.Вставить("Тело",ИмяФайла);
			ПараметрыПисьма.Вставить("Вложения",МассивВложения);
			
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
			РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции


#КонецОбласти




#КонецЕсли



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда	
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВозвратПоставщику(Запрос, ДополнительныеСвойства);
		//ПартионныйУчет.ПодготовитьТаблицуСписанияОприходованияТоваров(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоВзаиморасчетам") Тогда
		
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПоставщиками(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуРасчетыСПоставщиками(Запрос, ДополнительныеСвойства);
		
	ИначеЕсли ДополнительныеСвойства.Свойство("ПроведениеПоОстаткамОрганизации") Тогда
		
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровОрганизации(Запрос, ДополнительныеСвойства);
		
	Иначе
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		//ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры);	
		
		ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровВозвратПоставщику(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровОрганизации(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуОприходованияТоваров(Запрос, ДополнительныеСвойства);
			
		ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПоставщиками(Запрос, ДополнительныеСвойства);
		ПартионныйУчет.ПодготовитьТаблицуРасчетыСПоставщиками(Запрос, ДополнительныеСвойства);
	КонецЕсли;
	

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВозвратПоставщику.Дата КАК Период,
	               |	ВозвратПоставщику.Ссылка КАК Ссылка,
	               |	ВозвратПоставщику.ХозОперация КАК ХозОперация,
	               |	ВозвратПоставщику.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ВозвратПоставщику.Организация КАК Организация,
	               |	ВозвратПоставщику.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВозвратПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ВозвратПоставщику.Контрагент КАК Контрагент,
	               |	ВозвратПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратПоставщику.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВозвратПоставщику.СуммаДокумента КАК СуммаДокумента,
	               |	ВозвратПоставщику.КурсДокумента КАК КурсДокумента,
	               |	ВозвратПоставщику.ДокументОснование КАК ДокументОснование,
	               |	ВозвратПоставщику.МоментВремени КАК МоментВремени,
	               |	ВозвратПоставщику.СкладКомпании.ТипЦенСебестоимости КАК ТипЦенСебестоимости,
	               |	ВозвратПоставщику.ДоговорВзаиморасчетов.Организация КАК ОрганизацияДоговора
	               |ИЗ
	               |	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	               |ГДЕ
	               |	ВозвратПоставщику.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", 				Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", 			Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", 				Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",			Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",	Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",		Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",			Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("Контрагент",				Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",	Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",		Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ЗаполненДокументОснование", ЗначениеЗаполнено(Реквизиты.ДокументОснование));
	Запрос.УстановитьПараметр("ОрганизацияДоговора",	Реквизиты.ОрганизацияДоговора);

	ПроводитьПоПартиям = Ложь;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") ИЛИ ПолучитьФункциональнуюОпцию("ФормироватьДвиженияПоПартиямПриПроведении") Тогда
		ПроводитьПоПартиям = Истина;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПроводитьПоПартиям",ПроводитьПоПартиям);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент", Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов", Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("СуммаДокумента", Реквизиты.СуммаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента", Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("Сделка", Реквизиты.ДокументОснование);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка", Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("Организация", Реквизиты.ОрганизацияДоговора);

	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	
	ПроводитьПоОстаткамОрганизации = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",ПроводитьПоОстаткамОрганизации);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ТипЦенСебестоимости", Реквизиты.ТипЦенСебестоимости);
	
КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВозвратПоставщикуТовары.Ссылка КАК Ссылка,
	               |	ВозвратПоставщикуТовары.Номенклатура КАК Номенклатура,
	               |	ВозвратПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВозвратПоставщикуТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ВозвратПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ВозвратПоставщикуТовары.Коэффициент КАК Коэффициент,
	               |	ВозвратПоставщикуТовары.КоличествоБазовое КАК Количество,
	               |	ВозвратПоставщикуТовары.Цена КАК Цена,
	               |	ВозвратПоставщикуТовары.Сумма КАК Сумма,
	               |	ВозвратПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	               |	ВозвратПоставщикуТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ВЫБОР
	               |		КОГДА &ЗаполненДокументОснование
	               |			ТОГДА ВозвратПоставщикуТовары.Ссылка.ДокументОснование
	               |		ИНАЧЕ ВозвратПоставщикуТовары.Партия
	               |	КОНЕЦ КАК Партия,
	               |	ВозвратПоставщикуТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	&Контрагент КАК Контрагент,
	               |	&ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратПоставщикуТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ВозвратПоставщику.Товары КАК ВозвратПоставщикуТовары
	               |ГДЕ
	               |	ВозвратПоставщикуТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	&СкладКомпании КАК СкладКомпании,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
				  |	&Контрагент КАК Контрагент,
				  |	ВтТаблицаТовары.Количество КАК Количество,
	              |	&ХозОперация КАК ХозОперация
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетаСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности) КАК ВидОперации,
	              |	&Период КАК Период,
	              |	&ХозОперация КАК ХозОперация,
	              |	&Контрагент КАК Контрагент,
	              |	&РегламентированныйУчет КАК РегламентированныйУчет,
	              |	&ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	СУММА(ВтТаблицаТовары.Сумма) КАК Сумма,
	              |	СУММА(ВтТаблицаТовары.Сумма * ВтТаблицаТовары.КурсДокумента) КАК СуммаБаз
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.Ссылка"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;
КонецФункции


#КонецОбласти
	

#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииТоваров,Взаиморасчеты,ПартииОрганизаций");	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");

	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = Ссылка.СкладКомпании;
	НоваяСтрока.Период   	  = Ссылка.Дата;
	ТаблицыДляПоследовательности.ПартииТоваров = ТаблицаПоследовательности;
	
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");

	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.ДоговорКонтрагента = Ссылка.ДоговорВзаиморасчетов;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.Взаиморасчеты = ТаблицаПоследовательности;

	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("Организация");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.Организация 	   = ДополнительныеСвойства.ДляПроведения.Организация;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;

	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	                                            
	// Возврат поставщику с партиями
    КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ВозвратПоставщику";
    КомандаПечати.Идентификатор = "ВозвратПоставщикуСПартиями";
    КомандаПечати.Представление = НСтр("ru = 'Возврат поставщику с партиями'; uk = 'Повернення постачальнику з партіями'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	
	КомандаПечати = КомандыПечати.Добавить(); 
    КомандаПечати.МенеджерПечати = "Документ.ВозвратПоставщику";
    КомандаПечати.Идентификатор = "ВозвратПоставщикуСПартиямиДвойная";
    КомандаПечати.Представление = НСтр("ru = 'Возврат поставщику с партиями 2 шт. на лист'; uk = 'Повернення постачальнику з партіями 2 шт. на лист'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПартииВТЧТовары(Объект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	тзТовары = Объект.Товары.Выгрузить();
	тзТоварыСверн = тзТовары.Скопировать();
	
	тзТоварыСверн.Свернуть("Номенклатура, ХарактеристикаНоменклатуры");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	Товары.Номенклатура КАК Номенклатура
	               |ПОМЕСТИТЬ ТаблицаТоваров
	               |ИЗ
	               |	&Товары КАК Товары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ОстаткиПартий.Партия КАК Партия,
	               |	ЕСТЬNULL(ОстаткиПартий.КоличествоОстаток, 0) КАК КвоОстаток,
	               |	ЕСТЬNULL(ОстаткиПартий.СуммаОстаток, 0) КАК СуммаОстаток
	               |ИЗ
	               |	ТаблицаТоваров КАК ТаблицаТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровКомпании.Остатки(
	               |				&МоментВремени,
	               |				СкладКомпании = &Склад
	               |					И (Номенклатура, ХарактеристикаНоменклатуры) В
	               |						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |							ТаблицаТоваров.Номенклатура КАК Номенклатура,
	               |							ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	               |						ИЗ
	               |							ТаблицаТоваров КАК ТаблицаТоваров)
	               |					И Партия ССЫЛКА Документ.ПоступлениеТоваров
	               |					И ВЫРАЗИТЬ(Партия КАК Документ.ПоступлениеТоваров).ДоговорВзаиморасчетов = &Договор
	               |					И ВЫРАЗИТЬ(Партия КАК Документ.ПоступлениеТоваров).РегламентированныйУчет = &РеглУчет) КАК ОстаткиПартий
	               |		ПО ТаблицаТоваров.Номенклатура = ОстаткиПартий.Номенклатура
	               |			И ТаблицаТоваров.ХарактеристикаНоменклатуры = ОстаткиПартий.ХарактеристикаНоменклатуры
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОстаткиПартий.Партия.Дата";
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Товары", тзТоварыСверн);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Запрос.УстановитьПараметр("МоментВремени", Объект.Ссылка.МоментВремени());
	Иначе
		Запрос.УстановитьПараметр("МоментВремени",ТекущаяДата());
	КонецЕсли;

	Запрос.УстановитьПараметр("Склад", Объект.СкладКомпании);
	//Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	//Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Объект.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("РеглУчет", Объект.РегламентированныйУчет);

	тзПартии = Запрос.Выполнить().Выгрузить();
	
	тзТовары.Очистить();
	
	Для Каждого стрТовары Из Объект.Товары Цикл
		КвоСписать = стрТовары.Количество;
		МассивПартии = тзПартии.НайтиСтроки(Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", стрТовары.Номенклатура, стрТовары.ХарактеристикаНоменклатуры));
		
		Для Каждого стрПартии Из МассивПартии Цикл						
			Если стрПартии.КвоОстаток <= 0 Тогда 
				Продолжить;
			КонецЕсли;
			
			КвоСписываем = Мин(КвоСписать, стрПартии.КвоОстаток/стрТовары.Коэффициент);
			НовСтрТовары = тзТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрТовары, стрТовары);
			ЗаполнитьЗначенияСвойств(НовСтрТовары, стрПартии);			
			
			НовСтрТовары.Партия = стрПартии.Партия;
			НовСтрТовары.Количество = КвоСписываем;			
			
			Если стрПартии.КвоОстаток = 0 Тогда 
				НовСтрТовары.Сумма 	 = стрПартии.СуммаОстаток;
			Иначе
				НовСтрТовары.Сумма	 = стрПартии.СуммаОстаток * (КвоСписываем*стрТовары.Коэффициент/стрПартии.КвоОстаток);			
				НовСтрТовары.Цена	 = стрПартии.СуммаОстаток / стрПартии.КвоОстаток;
			КонецЕсли;
						
			СтруктураДействий = Новый Структура;			
			
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС");			
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НовСтрТовары, СтруктураДействий, Неопределено);
			
			стрПартии.КвоОстаток = стрПартии.КвоОстаток - КвоСписываем*стрТовары.Коэффициент;
			стрПартии.СуммаОстаток = стрПартии.СуммаОстаток - НовСтрТовары.Сумма;
			КвоСписать = КвоСписать - КвоСписываем;
			
			Если КвоСписать = 0 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если КвоСписать <> 0 Тогда 
			НовСтрТовары = тзТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрТовары, стрТовары);
			НовСтрТовары.Партия = "";
			НовСтрТовары.Количество = КвоСписать;
			
			СтруктураДействий = Новый Структура;			
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");			
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НовСтрТовары, СтруктураДействий, Неопределено);
		КонецЕсли;
	КонецЦикла;
	
	Объект.Товары.Загрузить(тзТовары);
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
		
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Возврат поставщику с партиями
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВозвратПоставщикуСПартиями") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ВозвратПоставщикуСПартиями",
		НСтр("ru = 'Возврат поставщику с партиями'; uk = 'Повернення постачальнику з партіями'"), 
		ПечатьВозвратПоставщикуСПартиями(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ВозвратПоставщику.ПФ_MXL_ВозвратПоставщикуСПартиями");
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВозвратПоставщикуСПартиямиДвойная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ВозвратПоставщикуСПартиямиДвойная",
		НСтр("ru = 'Возврат поставщику с партиями 2 шт. на лист'; uk = 'Повернення постачальнику з партіями 2 шт. на лист'"), 
		ПечатьВозвратПоставщикуСПартиямиДвойная(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ВозвратПоставщику.ПФ_MXL_НакладнаяДвойная");
	
	КонецЕсли;	
	
КонецПроцедуры

// Возврат поставщику с партиями
Функция ПечатьВозвратПоставщикуСПартиями(МассивОбъектов, ОбъектыПечати)Экспорт

	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВозвратПоставщику.Ссылка КАК Ссылка,
	               |	ВозвратПоставщику.Номер КАК Номер,
	               |	ВозвратПоставщику.Дата КАК Дата,
	               |	ВозвратПоставщику.Контрагент КАК Контрагент,
	               |	ВозвратПоставщику.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ВЫБОР
	               |		КОГДА ВозвратПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)
	               |			ТОГДА ВозвратПоставщику.ДоговорВзаиморасчетов.Организация
	               |		ИНАЧЕ ВозвратПоставщику.Организация
	               |	КОНЕЦ КАК Организация,
	               |	ВЫБОР
	               |		КОГДА ВозвратПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)
	               |			ТОГДА ВозвратПоставщику.ДоговорВзаиморасчетов.Организация.НаименованиеПолное
	               |		ИНАЧЕ ВозвратПоставщику.Организация.НаименованиеПолное
	               |	КОНЕЦ КАК ОрганизацияНаименованиеПолное,
	               |	ВозвратПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ВозвратПоставщику.СкладКомпании.Код КАК КодСклада,
	               |	ВозвратПоставщику.СкладКомпании.НаименованиеПредставление КАК СкладКомпанииПредставление,
	               |	ВозвратПоставщику.СкладКомпании.ТочкаДоставки КАК СкладКомпанииТочкаДоставки,
	               |	ВозвратПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратПоставщику.СуммаДокумента КАК СуммаДокумента,
	               |	ВозвратПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратПоставщику.ДокументОснование КАК ДокументОснование,
	               |	ВозвратПоставщику.ДокументОснование.ВхДокНомер КАК ВхДокНомер,
	               |	ВозвратПоставщику.ДокументОснование.ВхДокДата КАК ВхДокДата,
	               |	ВозвратПоставщику.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Сумма КАК СуммаВсего,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмерения,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиков,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.ЕдиницаИзмерения
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.ЕдиницаИзмерения.Коэффициент
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваров
	               |					И ВозвратПоставщику.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	               |				ТОГДА ВозвратПоставщику.ДокументОснование.ВхДокНомер
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Партия.ВхДокНомер
	               |		КОНЕЦ КАК ВхДокНомер,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваров
	               |					И ВозвратПоставщику.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	               |				ТОГДА ВозвратПоставщику.ДокументОснование.ВхДокДата
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Партия.ВхДокДата
	               |		КОНЕЦ КАК ВхДокДата
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	               |ГДЕ
	               |	ВозвратПоставщику.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВозвратПоставщикуСПартиями";
	
	ТабличныйДокумент.ПолеСверху = 10;
	ТабличныйДокумент.ПолеСлева  = 10;
	ТабличныйДокумент.ПолеСнизу  = 10;
	ТабличныйДокумент.ПолеСправа = 10;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратПоставщику.ПФ_MXL_ВозвратПоставщикуСПартиями");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьАдрес	 		= Макет.ПолучитьОбласть("Адрес");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьСклад	 		= Макет.ПолучитьОбласть("Склад");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("ПодписиКратко");
	ОбластьШтрихКод			= Макет.ПолучитьОбласть("ШтрихКод");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Накладная на возврат товара №%1 от %2'; uk = 'Накладна на повернення товару №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
						
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ВыборкаДокументов.КонтрагентНаименованиеПолное);
		СтруктураШапки.Вставить("Поставщик", ВыборкаДокументов.Контрагент);
		СтруктураШапки.Вставить("Адрес", ПолучитьАдресКонтарагента(ВыборкаДокументов.Контрагент));		
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ОрганизацияНаименованиеПолное);
		СтруктураШапки.Вставить("Получатель", ВыборкаДокументов.Организация);
		СтруктураШапки.Вставить("АдресДоставки", ПолучитьАдресОрганизации(ВыборкаДокументов.Организация));		
		СтруктураШапки.Вставить("ОснованиеПредставление", ВыборкаДокументов.ДоговорВзаиморасчетов);
		СтруктураШапки.Вставить("ПредставлениеСклада", ?(ЗначениеЗаполнено(ВыборкаДокументов.СкладКомпанииТочкаДоставки), ВыборкаДокументов.СкладКомпанииТочкаДоставки, ВыборкаДокументов.СкладКомпании));				
		СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", СокрЛП(ВыборкаДокументов.КодСклада) + ", " + СокрЛП(ВыборкаДокументов.СкладКомпанииПредставление));				
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		 
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
				
		ИДДокумента = ВыборкаДокументов.Ссылка.УникальныйИдентификатор();
		
		ОбластьШтрихКод.Параметры.ИДДокументаОтгрузки = ИДДокумента;
		
		Попытка			
			ПараметрыШтрихКода = Новый Структура;
			ПараметрыШтрихКода.Вставить("ТипКода", 10);
			ПараметрыШтрихКода.Вставить("Высота", 60);
			ПараметрыШтрихКода.Вставить("Ширина", 800);
			ПараметрыШтрихКода.Вставить("Штрихкод", ИДДокумента);
			ПараметрыШтрихКода.Вставить("ОтображатьТекст", Ложь);
			ПараметрыШтрихКода.Вставить("ВертикальноеВыравнивание", 2);
			ПараметрыШтрихКода.Вставить("Масштабировать", Истина);
			ПараметрыШтрихКода.Вставить("СохранятьПропорции", Ложь);			
			
			ДанныеШтрихКода = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			
			ОбластьШтрихКод.Рисунки.ШК.Картинка = ДанныеШтрихКода;
			
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
		ТабличныйДокумент.Вывести(ОбластьШтрихКод);		
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьАдрес.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьАдрес);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ОбластьСклад.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСклад);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;
		СуммаНДС		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();		
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
						
			СтруктураСтроки = Новый Структура("Номенклатура, ВхДокНомер, Цена");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "Номенклатура, ВхДокНомер, Цена");			
			СтруктураСтроки.Вставить("СуммаВсего", Формат(ВыборкаТовары.СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("ВхДокДата", Формат(ВыборкаТовары.ВхДокДата, "ДФ=dd.MM.yy"));
			стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
			СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
												ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
													ВыборкаТовары.Номенклатура, 
													ВыборкаТовары.КоличествоБазовое, 
													ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
													ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент), 
												СтруктураИтоговМест);
											
			СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
					
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);		
		СтруктураПодвала.Вставить("ВсегоБезНДС", СуммаВсего - СуммаНДС);		
		СтруктураПодвала.Вставить("ВсегоКво", Строка(Количество) + " шт.");
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
				
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПечатьВозвратПоставщикуСПартиямиДвойная(МассивОбъектов, ОбъектыПечати)Экспорт

	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВозвратПоставщику.Ссылка КАК Ссылка,
	               |	ВозвратПоставщику.Номер КАК Номер,
	               |	ВозвратПоставщику.Дата КАК Дата,
	               |	ВозвратПоставщику.Контрагент КАК Контрагент,
	               |	ВозвратПоставщику.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ВЫБОР
	               |		КОГДА ВозвратПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)
	               |			ТОГДА ВозвратПоставщику.ДоговорВзаиморасчетов.Организация
	               |		ИНАЧЕ ВозвратПоставщику.Организация
	               |	КОНЕЦ КАК Организация,
	               |	ВЫБОР
	               |		КОГДА ВозвратПоставщику.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВозвратТоваровПоставщикуТранзиты)
	               |			ТОГДА ВозвратПоставщику.ДоговорВзаиморасчетов.Организация.НаименованиеПолное
	               |		ИНАЧЕ ВозвратПоставщику.Организация.НаименованиеПолное
	               |	КОНЕЦ КАК ОрганизацияНаименованиеПолное,
	               |	ВозвратПоставщику.СкладКомпании КАК СкладКомпании,
	               |	ВозвратПоставщику.СкладКомпании.Код КАК КодСклада,
	               |	ВозвратПоставщику.СкладКомпании.НаименованиеПредставление КАК СкладКомпанииПредставление,
	               |	ВозвратПоставщику.СкладКомпании.ТочкаДоставки КАК СкладКомпанииТочкаДоставки,
	               |	ВозвратПоставщику.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВозвратПоставщику.СуммаДокумента КАК СуммаДокумента,
	               |	ВозвратПоставщику.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВозвратПоставщику.ДокументОснование КАК ДокументОснование,
	               |	ВозвратПоставщику.ДокументОснование.ВхДокНомер КАК ВхДокНомер,
	               |	ВозвратПоставщику.ДокументОснование.ВхДокДата КАК ВхДокДата,
	               |	ВозвратПоставщику.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		СуммаНДС КАК СуммаНДС,
	               |		Сумма КАК СуммаВсего,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмерения,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков
	               |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмерения.ПустаяСсылка)
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиков,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.ЕдиницаИзмерения
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Тара)
	               |				ТОГДА ВозвратПоставщику.Товары.ЕдиницаИзмерения.Коэффициент
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент
	               |		КОНЕЦ КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваров
	               |					И ВозвратПоставщику.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	               |				ТОГДА ВозвратПоставщику.ДокументОснование.ВхДокНомер
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Партия.ВхДокНомер
	               |		КОНЕЦ КАК ВхДокНомер,
	               |		ВЫБОР
	               |			КОГДА ВозвратПоставщику.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваров
	               |					И ВозвратПоставщику.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	               |				ТОГДА ВозвратПоставщику.ДокументОснование.ВхДокДата
	               |			ИНАЧЕ ВозвратПоставщику.Товары.Партия.ВхДокДата
	               |		КОНЕЦ КАК ВхДокДата
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВозвратПоставщику КАК ВозвратПоставщику
	               |ГДЕ
	               |	ВозвратПоставщику.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		       
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВозвратПоставщикуСПартиямиДвойная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратПоставщику.ПФ_MXL_НакладнаяДвойная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьАдрес	 		= Макет.ПолучитьОбласть("Адрес");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьСклад	 		= Макет.ПолучитьОбласть("Склад");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("ПодписиКратко");
	ОбластьШтрихКод			= Макет.ПолучитьОбласть("ШтрихКод");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Накладная на возврат товара №%1 от %2'; uk = 'Накладна на повернення товару №%1 від %2'", КодЯзыкаПечать),
							СокрЛП(ВыборкаДокументов.Номер),
							Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
						
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ВыборкаДокументов.КонтрагентНаименованиеПолное);
		СтруктураШапки.Вставить("Поставщик", ВыборкаДокументов.Контрагент);
		СтруктураШапки.Вставить("Адрес", ПолучитьАдресКонтарагента(ВыборкаДокументов.Контрагент));		
		СтруктураШапки.Вставить("ПредставлениеПолучателя", ВыборкаДокументов.ОрганизацияНаименованиеПолное);
		СтруктураШапки.Вставить("Получатель", ВыборкаДокументов.Организация);
		СтруктураШапки.Вставить("АдресДоставки", ПолучитьАдресОрганизации(ВыборкаДокументов.Организация));		
		СтруктураШапки.Вставить("ОснованиеПредставление", ВыборкаДокументов.ДоговорВзаиморасчетов);
		СтруктураШапки.Вставить("ПредставлениеСклада", ?(ЗначениеЗаполнено(ВыборкаДокументов.СкладКомпанииТочкаДоставки), ВыборкаДокументов.СкладКомпанииТочкаДоставки, ВыборкаДокументов.СкладКомпании));				
		СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", СокрЛП(ВыборкаДокументов.КодСклада) + ", " + СокрЛП(ВыборкаДокументов.СкладКомпанииПредставление));				
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		 
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
				
		ИДДокумента = ВыборкаДокументов.Ссылка.УникальныйИдентификатор();
		
		ОбластьШтрихКод.Параметры.ИДДокументаОтгрузки = ИДДокумента;
		
		Попытка			
			ПараметрыШтрихКода = Новый Структура;
			ПараметрыШтрихКода.Вставить("ТипКода", 10);
			ПараметрыШтрихКода.Вставить("Высота", 60);
			ПараметрыШтрихКода.Вставить("Ширина", 800);
			ПараметрыШтрихКода.Вставить("Штрихкод", ИДДокумента);
			ПараметрыШтрихКода.Вставить("ОтображатьТекст", Ложь);
			ПараметрыШтрихКода.Вставить("ВертикальноеВыравнивание", 2);
			ПараметрыШтрихКода.Вставить("Масштабировать", Истина);
			ПараметрыШтрихКода.Вставить("СохранятьПропорции", Ложь);			
			
			ДанныеШтрихКода = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			
			ОбластьШтрихКод.Рисунки.ШК.Картинка = ДанныеШтрихКода;
			ОбластьШтрихКод.Рисунки.ШК1.Картинка = ДанныеШтрихКода;
			
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
		ТабличныйДокумент.Вывести(ОбластьШтрихКод);		
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьАдрес.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьАдрес);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		ОбластьСклад.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьСклад);
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;
		СуммаНДС		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();		
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
						
			СтруктураСтроки = Новый Структура("Номенклатура, ВхДокНомер, Цена");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары, "Номенклатура, ВхДокНомер, Цена");			
			СтруктураСтроки.Вставить("СуммаВсего", Формат(ВыборкаТовары.СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("ВхДокДата", Формат(ВыборкаТовары.ВхДокДата, "ДФ=dd.MM.yy"));
			стрНоменклатура = СокрЛП(ВыборкаТовары.Артикул) +  " " + СокрЛП(ВыборкаТовары.ТоварПредставление);						
			СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
												ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
													ВыборкаТовары.Номенклатура, 
													ВыборкаТовары.КоличествоБазовое, 
													ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
													ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент), 
												СтруктураИтоговМест);
											
			СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
					
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);		
		СтруктураПодвала.Вставить("ВсегоБезНДС", СуммаВсего - СуммаНДС);		
		СтруктураПодвала.Вставить("ВсегоКво", Строка(Количество) + " шт.");
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
				
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПолучитьАдресОрганизации(Док)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление
		|ИЗ
		|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
		|ГДЕ
		|	ОрганизацииКонтактнаяИнформация.Ссылка.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Док);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Адрес = "";
		ОбщегоНазначения.СообщитьПользователю("Для организации "+ Док+" НЕ заполнен адрес организации, поле оставлено пустым!!!");
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Адрес = ВыборкаДетальныеЗаписи.Представление;
	КонецЕсли;
	Возврат Адрес;
	
КонецФункции

Функция ПолучитьАдресКонтарагента(Док)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтрагентыКонтактнаяИнформация.Представление КАК Представление
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
		|ГДЕ
		|	КонтрагентыКонтактнаяИнформация.Ссылка.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Док);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Адрес = "";
		ОбщегоНазначения.СообщитьПользователю("Для контрагента "+ Док+" НЕ заполнен адрес, поле оставлено пустым!!!");
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Адрес = ВыборкаДетальныеЗаписи.Представление;
	КонецЕсли;
	Возврат Адрес;
	
КонецФункции


#КонецОбласти	

	
	
#КонецЕсли
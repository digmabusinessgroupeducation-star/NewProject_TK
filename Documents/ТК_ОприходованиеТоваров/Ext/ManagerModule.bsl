#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	

	
#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТК_ОприходованиеТоваров.Дата КАК Период,
	|	ТК_ОприходованиеТоваров.Ссылка КАК Ссылка,
	|	ТК_ОприходованиеТоваров.ХозОперация КАК ХозОперация,
	|	ТК_ОприходованиеТоваров.Организация КАК Организация,
	|	ТК_ОприходованиеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ТК_ОприходованиеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ТК_ОприходованиеТоваров.СкладКомпании КАК СкладКомпании,
	|	ТК_ОприходованиеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ТК_ОприходованиеТоваров.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.ТК_ОприходованиеТоваров КАК ТК_ОприходованиеТоваров
	|ГДЕ
	|	ТК_ОприходованиеТоваров.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	Запрос.УстановитьПараметр("МоментВремени",                            Новый МоментВремени(Реквизиты.Период,ДокументСсылка));
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("ХозОперация",							  Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",					  Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Контрагент",								  Реквизиты.Контрагент);
	
	ПроводитьПоПартиям = Ложь;
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") ИЛИ ПолучитьФункциональнуюОпцию("ФормироватьДвиженияПоПартиямПриПроведении") Тогда
		ПроводитьПоПартиям = Истина;
	КонецЕсли;
	Запрос.УстановитьПараметр("ПроводитьПоПартиям", ПроводитьПоПартиям);
		
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("ТорговаяПлощадка", 		Реквизиты.ТорговаяПлощадка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СкладКомпании", 			Реквизиты.СкладКомпании);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет",	Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация",			Реквизиты.ХозОперация);
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ДополнительныеСвойства.Свойство("ПроведениеПоПартиям") Тогда
		ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
	Иначе
		ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	// Исполнение запроса и выгрузка полученных таблиц для движений.
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТК_ОприходованиеТоваровТовары.НомерСтроки КАК НомерСтроки,
	               |	ТК_ОприходованиеТоваровТовары.Номенклатура КАК Номенклатура,
	               |	ТК_ОприходованиеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ТК_ОприходованиеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ТК_ОприходованиеТоваровТовары.Коэффициент КАК Коэффициент,
	               |	ТК_ОприходованиеТоваровТовары.Количество КАК Количество,
	               |	ТК_ОприходованиеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ТК_ОприходованиеТоваровТовары.Цена КАК Цена,
	               |	ТК_ОприходованиеТоваровТовары.Сумма КАК Сумма,
	               |	ТК_ОприходованиеТоваровТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_ОприходованиеТоваров.Товары КАК ТК_ОприходованиеТоваровТовары
	               |ГДЕ
	               |	ТК_ОприходованиеТоваровТовары.Ссылка = &Ссылка";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	&Период КАК Период,
	               |	&СкладКомпании КАК СкладКомпании,
	               |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	               |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВтТаблицаТовары.КоличествоБазовое КАК Количество,
	               |	&ХозОперация КАК ХозОперация,
	               |	&Контрагент КАК Контрагент
	               |ИЗ
	               |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПартииТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	&СкладКомпании КАК СкладКомпании,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	              |	&Ссылка КАК Партия,
	              |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	              |	ВтТаблицаТовары.КоличествоБазовое КАК Количество,
	              |	ВтТаблицаТовары.Сумма КАК Сумма,
	              |	ВтТаблицаТовары.Сумма КАК СуммаБаз,
	              |	0 КАК СуммаНДС,
	              |	&ХозОперация КАК ХозОперация,
	              |	ВтТаблицаТовары.СтавкаНДС КАК СтавкаНДС
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |ГДЕ
	              |	&ПроводитьПоПартиям"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);	
	
	Возврат ТекстЗапроса;	
КонецФункции


#КонецОбласти	


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
	
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("СкладКомпании");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор   = Ссылка;
	НоваяСтрока.СкладКомпании = ДополнительныеСвойства.ДляПроведения.СкладКомпании;
	НоваяСтрока.Период   	  = ДополнительныеСвойства.ДляПроведения.Дата;
	
	ТаблицыДляПоследовательности.Вставить("ПартииТоваров", ТаблицаПоследовательности);
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры


#КонецОбласти



#Область Печать




#КонецОбласти	






#КонецЕсли
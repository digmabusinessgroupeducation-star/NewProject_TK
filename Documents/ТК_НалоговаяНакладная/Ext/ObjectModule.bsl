
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПроверитьСхемуНалообложения()
	
	СхемаНалогообложения = Справочники.Контрагенты.ПолучитьСхемуНалообложения(Контрагент, Дата);
	Если СхемаНалогообложения = Перечисления.СхемыНалогообложения.НеПлательщик Тогда
		ТипПричиныНевыдачиПокупателю = 2 
	Иначе
		ТипПричиныНевыдачиПокупателю =0;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаполнитьРуководителемОрганизации(Организация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&Дата, ) КАК ОтветственныеЛицаОрганизацийСрезПоследних
		|ГДЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|	И ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Справочники.ФизическиеЛица.ПустаяСсылка();
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.ФизическоеЛицо;
	КонецЕсли;
	
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТК_ЗаказПокупателяКазначейсвто") Тогда
		ЗаполнитьДокументНаОсновании_ТК_ЗаказПокупателяКазначейсвто(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваров") Тогда 
		ЗаполнитьДокументНаОсновании_РеализацияТоваров(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ВыпускПродукции") Тогда 
		ЗаполнитьДокументНаОсновании_ВыпускПродукции(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование = Неопределено;
	НомерРН = "";
	НомерДляБухгалтерии = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
		
	РегистрыСведений.ТК_АналитикаУчетаНоменклатурыОрганизаций.ЗаполнитьВКоллекции(Товары,Организация);

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И НЕ ЭтоНовый() И Проведен Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Док.Дата КАК Дата,
		|	Док.Организация КАК Организация
		|ИЗ
		|	Документ.ТК_НалоговаяНакладная КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтараяОрганизацияДокумента", Выборка.Организация);
	Иначе
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед",Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияОрганизацииПоследовательности", Ложь);
	КонецЕсли;
	
	
	
	СуммаДокумента = Товары.Итог("СуммаВсего");
	СуммаНДСДокумента = Товары.Итог("СуммаНДС");
	    		
	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения И Не Отказ Тогда 
		Если ЭтоНовый() Тогда 
			УстановитьНовыйНомер();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		тНомерДляБухгалтерии = ОбщегоНазначенияТК.ПолучитьНомерНалоговогоДокументаДляБухгалтерии(Дата, Номер, Организация);
		тНомерРН = ОбщегоНазначенияТК.ИзменитьНомерРН(Дата, Номер, Организация, ПодразделениеКомпании);
		тКтоВыписалНалоговуюНакладную = ЗаполнитьРуководителемОрганизации(Организация);
		ПроверитьСхемуНалообложения();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если НЕ ЗначениеЗаполнено(КтоВыписалНалоговуюНакладную) или КтоВыписалНалоговуюНакладную<>тКтоВыписалНалоговуюНакладную Тогда
			КтоВыписалНалоговуюНакладную = тКтоВыписалНалоговуюНакладную;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(НомерДляБухгалтерии) ИЛИ НомерДляБухгалтерии<>тНомерДляБухгалтерии Тогда 
			НомерДляБухгалтерии = тНомерДляБухгалтерии;
		КонецЕсли;
		
		Если РН = Истина и Не ЗначениеЗаполнено(НомерРН) Тогда 
			НомерРН = тНомерРН;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТипЦен) Тогда 
			ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(СкладКомпании);
		КонецЕсли;
	КонецЕсли;
	
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;

	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияОрганизацииПоследовательности") Тогда  
		ДополнительныеСвойства.Вставить("ОрганизацияИзменена", Организация <> ДополнительныеСвойства.СтараяОрганизацияДокумента);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ТК_НалоговаяНакладная.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.ТК_НалоговаяНакладная.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	Документы.ТК_НалоговаяНакладная.ПроверитьДоговорПоставка(Ссылка);

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПартииТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьОстаткиОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПотребностиТоваровОрганизации") Тогда
		ЗапасыСервер.ОтразитьПотребностьОрганизации(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("НалоговаяНакладнаяПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

	
КонецПроцедуры








Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа или ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорВзаиморасчетов");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата				  = ТекущаяДата();
	Автор				  = Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	ИспользоватьЦеныИзПротоколов = ЗначениеНастроекПовтИсп.ПолучитьНастройкуПодразделения(ПодразделениеКомпании);
	СкладКомпании = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
	ХозОперация			  = ПредопределенноеЗначение("Справочник.ХозОперации.НалоговаяНакладная");
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ТК_ЗаказПокупателяКазначейсвто(ДанныеЗаполнения)
	
	Автор = ДанныеЗаполнения.Автор;
	ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
	ДоговорВзаиморасчетов = ДанныеЗаполнения.ДоговорВзаиморасчетов;
	Комментарий = ДанныеЗаполнения.Комментарий;
	Контрагент = ДанныеЗаполнения.Контрагент;
	КурсДокумента = ДанныеЗаполнения.КурсДокумента;
	Организация = ДанныеЗаполнения.Организация;
	ПодразделениеКомпании = ДанныеЗаполнения.ПодразделениеКомпании;
	ДокументОснование = ДанныеЗаполнения.Ссылка;
	СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	ТипЦен = ДанныеЗаполнения.ТипЦен;
	ХозОперация = ДанныеЗаполнения.ХозОперация;
	Для Каждого ТекСтрокаДополнительныеРеквизиты Из ДанныеЗаполнения.ДополнительныеРеквизиты Цикл
		НоваяСтрока = ДополнительныеРеквизиты.Добавить();
		НоваяСтрока.Значение = ТекСтрокаДополнительныеРеквизиты.Значение;
		НоваяСтрока.Свойство = ТекСтрокаДополнительныеРеквизиты.Свойство;
		НоваяСтрока.ТекстоваяСтрока = ТекСтрокаДополнительныеРеквизиты.ТекстоваяСтрока;
	КонецЦикла;
	Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаТовары.ЕдиницаИзмерения;
		НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
		НоваяСтрока.КоличествоБазовое = ТекСтрокаТовары.КоличествоБазовое;
		НоваяСтрока.Коэффициент = ТекСтрокаТовары.Коэффициент;
		НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
		НоваяСтрока.ПроцентСкидки = ТекСтрокаТовары.ПроцентСкидки;
		НоваяСтрока.СтавкаНДС = ТекСтрокаТовары.СтавкаНДС;
		НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
		НоваяСтрока.СуммаБезНалогов = ТекСтрокаТовары.СуммаБезНалогов;
		НоваяСтрока.СуммаВсего = ТекСтрокаТовары.СуммаВсего;
		НоваяСтрока.СуммаНДС = ТекСтрокаТовары.СуммаНДС;
		НоваяСтрока.СуммаСкидки = ТекСтрокаТовары.СуммаСкидки;
		НоваяСтрока.ХарактеристикаНоменклатуры = ТекСтрокаТовары.ХарактеристикаНоменклатуры;
		НоваяСтрока.Цена = ТекСтрокаТовары.Цена;
		НоваяСтрока.ЦенаБезНалогов = ТекСтрокаТовары.ЦенаБезНалогов;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_РеализацияТоваров(ДанныеЗаполнения)
	
	Дата = ТекущаяДата();
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВЫБОР
	               |		КОГДА РеализацияТоваров.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.АктОбОказанииУслуг)
	               |			ТОГДА ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяАктУслуг)
	               |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладная)
	               |	КОНЕЦ КАК ХозОперация,
	               |	РеализацияТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РеализацияТоваров.Организация КАК Организация,
	               |	РеализацияТоваров.Контрагент КАК Контрагент,
	               |	РеализацияТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РеализацияТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	РеализацияТоваров.ТипЦен КАК ТипЦен,
	               |	РеализацияТоваров.ТочкаДоставки КАК ТочкаДоставки,
	               |	РеализацияТоваров.СкладКомпании КАК СкладКомпании,
	               |	РеализацияТоваров.КурсДокумента КАК КурсДокумента,
	               |	РеализацияТоваров.Ссылка КАК ДокументОснование,
	               |	РеализацияТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ЛОЖЬ КАК РН,
	               |	РеализацияТоваров.Товары.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		СуммаВсего КАК СуммаВсего,
	               |		НомерСтроки КАК НомерСтрокиНН,
	               |		Содержание КАК Содержание
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.РеализацияТоваров КАК РеализацияТоваров
	               |ГДЕ
	               |	РеализацияТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		Товары.Загрузить(Выборка.Товары.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОсновании_ВыпускПродукции(ДанныеЗаполнения)
	
	Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ВыпускПродукции.Дата КАК Дата,
	               |	ВыпускПродукции.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ВыпускПродукции.Организация КАК Организация,
	               |	ВыпускПродукции.Контрагент КАК Контрагент,
	               |	ВыпускПродукции.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ВыпускПродукции.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВыпускПродукции.ТипЦен КАК ТипЦен,
	               |	ВыпускПродукции.ТочкаДоставки КАК ТочкаДоставки,
	               |	ВыпускПродукции.СкладКомпании КАК СкладКомпании,
	               |	ВыпускПродукции.КурсДокумента КАК КурсДокумента,
	               |	ВыпускПродукции.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ВыпускПродукции.Продукция.(
	               |		Номенклатура КАК Номенклатура,
	               |		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Коэффициент КАК Коэффициент,
	               |		Количество КАК Количество,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		Цена КАК Цена,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		СуммаВсего КАК СуммаВсего,
	               |		НомерСтроки КАК НомерСтрокиНН
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ВыпускПродукции КАК ВыпускПродукции
	               |ГДЕ
	               |	ВыпускПродукции.Ссылка = &Ссылка
	               |	И ВыпускПродукции.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ВыпускПродукцииСРеализацией)
	               |	И ВыпускПродукции.РегламентированныйУчет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);		
		
		Дата 				= ТекущаяДатаСеанса();
		ХозОперация 		= Справочники.ХозОперации.НалоговаяНакладная;
		ДокументОснование 	= ДанныеЗаполнения;
		РН 					= Ложь;
		
		Товары.Загрузить(Выборка.Товары.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#КонецЕсли
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);

	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	ПартионныйУчет.ПодготовитьТаблицуСписанияПартийТоваровОрганизации(Запрос, ДополнительныеСвойства);
	

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Док.Дата КАК Период,
	               |	Док.Ссылка КАК Ссылка,
	               |	Док.ХозОперация КАК ХозОперация,
	               |	Док.МоментВремени КАК МоментВремени
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК Док
	               |ГДЕ
	               |	Док.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", 				Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", 			Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", 				Реквизиты.Ссылка);

	РегистрацияПоследовательности = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период);
	
	Если Реквизиты.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа или Реквизиты.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая Тогда
		ПроводитьПоОстаткамОрганизации = Ложь;	
	Иначе
		ПроводитьПоОстаткамОрганизации = РегистрацияПоследовательности;
	КонецЕсли;
	
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация); 	
	ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",РегистрацияПоследовательности);

КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДокТовары.Ссылка КАК Ссылка,
	               |	ДокТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ДокТовары.КоличествоБазовое КАК Количество,
	               |	ДокТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная.Товары КАК ДокТовары
	               |ГДЕ
	               |	ДокТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииОрганизаций");	


	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("Организация");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.Организация 	   = Ссылка.Организация;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;

	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	МассивУсловийРН = Новый Массив;
	МассивУсловийРН.Добавить(Новый Структура("Реквизит, ВидСравнения, Значение", 
												"РН",
												ПредопределенноеЗначение("ВидСравнения.Равно"),
												Истина));		
												
	//// Налоговая накладная
	//КомандаПечати = КомандыПечати.Добавить();
	//КомандаПечати.МенеджерПечати = "Документ.ТК_НалоговаяНакладная";
	//КомандаПечати.Идентификатор = "НалоговаяНакладная";
	//КомандаПечати.Представление = НСтр("ru = 'Налоговая накладная'; uk = 'Податкова накладна'");
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
												
	// Расходная накладная к НН
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ТК_НалоговаяНакладная";
    КомандаПечати.Идентификатор = "РасходнаяНакладнаяНН";
    КомандаПечати.Представление = НСтр("ru = 'Расходная накладная к НН'; uk = 'Видаткова накладна до ПН'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
	КомандаПечати.УсловияВидимости = МассивУсловийРН;
	
	// Расходная накладная к НН (Казн)
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ТК_НалоговаяНакладная";
    КомандаПечати.Идентификатор = "РасходнаяНакладнаяННКазн";
    КомандаПечати.Представление = НСтр("ru = 'Расходная накладная к НН Казначейство'; uk = 'Видаткова накладна до ПН Казн'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 
	КомандаПечати.УсловияВидимости = МассивУсловийРН;

	
	// Расходная накладная к НН (+Артикула)
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ТК_НалоговаяНакладная";
    КомандаПечати.Идентификатор = "РасходнаяНакладнаяННСАртикулами";
    КомандаПечати.Представление = НСтр("ru = 'Расходная накладная к НН (с артикулами)'; uk = 'Видаткова накладна до ПН (з артикулами)'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.УсловияВидимости = МассивУсловийРН;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Функция ПолучитьНомерДокумента(Дата, НомерДляБухгалтерии) Экспорт 
	ТекНомерНН = "";
	ТекНомерФилиала = "";
	МесячныйПрефиксВНомереДляБух = ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата);
	ТекНомерННСФилиалом = ?(Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) = 0, НомерДляБухгалтерии, Прав(НомерДляБухгалтерии, СтрДлина(НомерДляБухгалтерии) - Найти(НомерДляБухгалтерии, МесячныйПрефиксВНомереДляБух) - 1)); 

	ПозицияСлеша = Найти(ТекНомерННСФилиалом, "/");
	Если ПозицияСлеша = 0 Тогда
		ТекНомерНН = ТекНомерННСФилиалом;
	Иначе
		ТекНомерНН = Лев(ТекНомерННСФилиалом, ПозицияСлеша - 1);
		ТекНомерФилиала = Сред(ТекНомерННСФилиалом, ПозицияСлеша + 1, СтрДлина(ТекНомерННСФилиалом)-ПозицияСлеша);			
	КонецЕсли;
	
	Возврат ТекНомерНН;
КонецФункции

Функция ПолучитьМесячныйПрефиксНалоговыхДокументов(Дата)

	НомерМесяца = Месяц(Дата);
	Индекс = НомерМесяца*3 - 2;
	
	Возврат Сред("Ян|Фв|Мр|Ап|Ма|Ин|Ил|Ав|Сн|Ок|Но|Дк", Индекс, 2);	

КонецФункции

Процедура ПроверитьДоговорПоставка(Док) Экспорт
	Внутренний = Справочники.Контрагенты.ЭтоВнутреннийКонтрагент(Док.Контрагент);
	
	Если Док.РН = Ложь и НЕ Внутренний Тогда 
		Возврат;
	КонецЕсли;
		
	МассивДоговоров = Новый Массив;
	МассивДоговоров.Добавить(Справочники.ВидыДоговоров.Поставка);

	ДоговорПоставка= Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(
							Док.Контрагент,
							Док.Организация,
							Док.ПодразделениеКомпании,
							МассивДоговоров,,ТекущаяДата(),
							Док.Номер);
								
	Если ДоговорПоставка = Справочники.ДоговорыКонтрагентов.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю("НЕТ Договора поставки для данного договора!!!");
	КонецЕсли;
		
КонецПроцедуры
#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НалоговаяНакладная") Тогда 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"НалоговаяНакладная",
			НСтр("ru = 'Налоговая накладная'; uk = 'Податкова накладна'"), 
			ПечатьНалоговаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.ТК_НалоговаяНакладная.ПФ_MXL_UK_НалоговаяНакладная2018Альбом");		
	
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяННКазн") Тогда   // Расходная накладная к НН Казн
		
		ПараметрыПечати.Вставить("ШрифтПечати", Ложь);

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РасходнаяНакладнаяННКазн",
			НСтр("ru = 'Расходная накладная к НН Казн'; uk = 'Видаткова накладна до ПН Казн'"), 
			ПечатьРасходнаяНакладнаяНН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяНН") Тогда   // Расходная накладная к НН 
		
		ПараметрыПечати.Вставить("ШрифтПечати", Истина);

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РасходнаяНакладнаяНН",
			НСтр("ru = 'Расходная накладная к НН'; uk = 'Видаткова накладна до ПН'"), 
			ПечатьРасходнаяНакладнаяНН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");

		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладнаяННСАртикулами") Тогда   // Расходная накладная к НН (+Артикула)
		
		ПараметрыПечати.Вставить("ВыводитьАртикула", Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РасходнаяНакладнаяННСАртикулами",
			НСтр("ru = 'Расходная накладная к НН (с артикулами)'; uk = 'Видаткова накладна до ПН (з артикулами)'"), 
			ПечатьРасходнаяНакладнаяНН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
			,
			"Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
		
	КонецЕсли;	
	
КонецПроцедуры


// Налоговая накладная 
Функция ПечатьНалоговаяНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда 
		Порядок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ПорядокПечати", "");
		ВыводитьАртикула = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьАртикула", Ложь);
	Иначе
		Порядок = "";
		ВыводитьАртикула = Ложь;
	КонецЕсли;
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ПорядокСигаретный";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ПорядокПивной";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ТК_НалоговаяНакладная.МоментВремени";
	КонецЕсли;	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТК_НалоговаяНакладнаяТовары.Ссылка КАК Ссылка,
	|	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТК_НалоговаяНакладнаяТовары.Номенклатура.КодУКТВЭД
	|		КОГДА ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры.УКТВЭД = ЗНАЧЕНИЕ(справочник.КлассификаторУКТВЭД.ПустаяСсылка)
	|			ТОГДА ТК_НалоговаяНакладнаяТовары.Номенклатура.КодУКТВЭД
	|		ИНАЧЕ ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры.УКТВЭД
	|	КОНЕЦ КАК НоменклатураКодУКТВЭД,
	|	ЕСТЬNULL(ТК_НалоговаяНакладнаяТовары.Номенклатура.СтранаПроисхождения.Код, 0) КАК КодСтраны,
	|	ТК_НалоговаяНакладнаяТовары.Номенклатура.Наименование2 КАК НаименованиеНалоговая,
	|	ТК_НалоговаяНакладнаяТовары.Номенклатура.Наименование КАК Наименованиие,
	|	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТК_НалоговаяНакладнаяТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК КодЕдиницыИзмерения,
	|	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
	|	ТК_НалоговаяНакладнаяТовары.СтавкаНДС.Ставка КАК чСтавкаНДС,
	|	ТК_НалоговаяНакладнаяТовары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ТК_НалоговаяНакладнаяТовары.СуммаСкидки = 0
	|			ТОГДА ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов
	|		КОГДА ТК_НалоговаяНакладнаяТовары.КоличествоБазовое > 0
	|			ТОГДА (ТК_НалоговаяНакладнаяТовары.СуммаВсего - ТК_НалоговаяНакладнаяТовары.СуммаНДС) / ТК_НалоговаяНакладнаяТовары.КоличествоБазовое
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаБезНалогов,
	|	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ТК_НалоговаяНакладнаяТовары.СуммаСкидки = 0
	|			ТОГДА ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов
	|		ИНАЧЕ ТК_НалоговаяНакладнаяТовары.СуммаВсего - ТК_НалоговаяНакладнаяТовары.СуммаНДС
	|	КОНЕЦ КАК СуммаБезНалогов,
	|	ТК_НалоговаяНакладнаяТовары.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
	|ГДЕ
	|	ТК_НалоговаяНакладнаяТовары.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.Сумма7) КАК Сумма7,
	|	СУММА(ВложенныйЗапрос.Сумма20) КАК Сумма20,
	|	СУММА(ВложенныйЗапрос.ИтогНДС) КАК ИтогНДС,
	|	СУММА(ВложенныйЗапрос.ИтогНДС7) КАК ИтогНДС7,
	|	СУММА(ВложенныйЗапрос.ИтогНДС20) КАК ИтогНДС20
	|ПОМЕСТИТЬ втИтоги
	|ИЗ
	|	(ВЫБРАТЬ
	|		втТовары.Ссылка КАК Ссылка,
	|		СУММА(втТовары.СуммаБезНалогов) КАК Сумма,
	|		СУММА(втТовары.СуммаНДС) КАК ИтогНДС,
	|		0 КАК Сумма20,
	|		0 КАК ИтогНДС20,
	|		0 КАК Сумма7,
	|		0 КАК ИтогНДС7
	|	ИЗ
	|		втТовары КАК втТовары
	|	
	|	СГРУППИРОВАТЬ ПО
	|		втТовары.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втТовары_20.Ссылка,
	|		0,
	|		0,
	|		СУММА(втТовары_20.СуммаБезНалогов),
	|		СУММА(втТовары_20.СуммаНДС),
	|		0,
	|		0
	|	ИЗ
	|		втТовары КАК втТовары_20
	|	ГДЕ
	|		втТовары_20.чСтавкаНДС = 20
	|	
	|	СГРУППИРОВАТЬ ПО
	|		втТовары_20.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втТовары_7.Ссылка,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СУММА(втТовары_7.СуммаБезНалогов),
	|		СУММА(втТовары_7.СуммаНДС)
	|	ИЗ
	|		втТовары КАК втТовары_7
	|	ГДЕ
	|		втТовары_7.чСтавкаНДС = 7
	|	
	|	СГРУППИРОВАТЬ ПО
	|		втТовары_7.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТК_НалоговаяНакладная.Ссылка КАК Ссылка,
	|	ТК_НалоговаяНакладная.Дата КАК Дата,
	|	ТК_НалоговаяНакладная.Номер КАК Номер,
	|	ТК_НалоговаяНакладная.Организация КАК Организация,
	|	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
	|	ТК_НалоговаяНакладная.КтоВыписалНалоговуюНакладную КАК КтоВыписалНалоговуюНакладную,
	|	ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю КАК ТипПричиныНевыдачиПокупателю,
	|	ТК_НалоговаяНакладная.ВключенаВЕдиныйРеестрНалоговыхНакладных КАК ВключенаВЕдиныйРеестрНалоговыхНакладных,
	|	ТК_НалоговаяНакладная.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных КАК ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных,
	|	ЕСТЬNULL(втИтоги.Сумма, 0) КАК Сумма,
	|	ЕСТЬNULL(втИтоги.Сумма7, 0) КАК Сумма7,
	|	ЕСТЬNULL(втИтоги.Сумма20, 0) КАК Сумма20,
	|	ЕСТЬNULL(втИтоги.ИтогНДС, 0) КАК ИтогНДС,
	|	ЕСТЬNULL(втИтоги.ИтогНДС7, 0) КАК ИтогНДС7,
	|	ЕСТЬNULL(втИтоги.ИтогНДС20, 0) КАК ИтогНДС20,
	|	ТК_НалоговаяНакладная.ХозОперация КАК ХозОперация
	|ИЗ
	|	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИтоги КАК втИтоги
	|		ПО (втИтоги.Ссылка = ТК_НалоговаяНакладная.Ссылка)
	|ГДЕ
	|	ТК_НалоговаяНакладная.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТК_НалоговаяНакладная.МоментВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Ссылка КАК Ссылка,
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.НоменклатураКодУКТВЭД КАК НоменклатураКодУКТВЭД,
	|	втТовары.КодСтраны КАК КодСтраны,
	|	втТовары.НаименованиеНалоговая КАК НаименованиеНалоговая,
	|	втТовары.Наименованиие КАК Наименованиие,
	|	втТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	втТовары.КодЕдиницыИзмерения КАК КодЕдиницыИзмерения,
	|	втТовары.СтавкаНДС КАК СтавкаНДС,
	|	втТовары.чСтавкаНДС КАК чСтавкаНДС,
	|	втТовары.Количество КАК Количество,
	|	втТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	|	втТовары.СуммаНДС КАК СуммаНДС,
	|	втТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	|	втТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	втТовары КАК втТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	втТовары.Ссылка,
	|	втТовары.НомерСтроки";
	               
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаДокументов = МассивРезультатов[2].Выбрать();
	ТаблицаТовары = МассивРезультатов[3].Выгрузить();	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_НалоговаяНакладная";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТК_НалоговаяНакладная.ПФ_MXL_UK_НалоговаяНакладная2018Альбом");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьПробел	 		= Макет.ПолучитьОбласть("Пробел");
	ОбластьОригинал			= Макет.ПолучитьОбласть("Оригинал");
	ОбластьЗаголовок	 	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка	 		= Макет.ПолучитьОбласть("Шапка");
	ОбластьРазделI	 		= Макет.ПолучитьОбласть("РазделI");
	ОбластьПодвал	 		= Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("");
		СтруктураШапки.Вставить("");
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
						
		
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;		
КонецФункции

// Расходная накладная к НН
Функция ПечатьРасходнаяНакладнаяНН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Шрифт   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ШрифтПечати", Ложь);
	
	Договор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Договор", Неопределено);
	НомерРНН= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Номер", Ложь);
	
	нПредставлениеСкладаМестоСоставления = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ПредставлениеСкладаМестоСоставления", Неопределено);
	нАдресДоставки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "АдресДоставки", Неопределено);
	нОтгрузил      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Отгрузил", Неопределено);
	нПолучил       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Получил", Неопределено);
	
	
	
	
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда 
		Порядок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ПорядокПечати", "");
		ВыводитьАртикула = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьАртикула", Ложь);
	Иначе
		Порядок = "";
		ВыводитьАртикула = Ложь;
	КонецЕсли;
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ПорядокСигаретный";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ПорядокПивной";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
	               			|	ТК_НалоговаяНакладная.МоментВремени";
	КонецЕсли;		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТК_НалоговаяНакладная.Ссылка КАК Ссылка,
	               |	ТК_НалоговаяНакладная.Организация КАК Организация,
	               |	ТК_НалоговаяНакладная.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	               |	ТК_НалоговаяНакладная.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ТК_НалоговаяНакладная.СкладКомпании КАК ПредставлениеСкладаМестоСоставления,
	               |	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
	               |	ТК_НалоговаяНакладная.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	               |	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов.НаименованиеДляПечати КАК ОснованиеПредставление,
	               |	ТК_НалоговаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
	               |	ТК_НалоговаяНакладная.СуммаДокумента КАК СуммаДокумента,
	               |	ТК_НалоговаяНакладная.ТочкаДоставки КАК ТочкаДоставки,
	               |	ТК_НалоговаяНакладная.ТочкаДоставки.Представление КАК АдресДоставки,
	               |	ТК_НалоговаяНакладная.Дата КАК Дата,
	               |	ТК_НалоговаяНакладная.Номер КАК Номер,
	               |	ТК_НалоговаяНакладная.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура.Артикул КАК Артикул,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.Представление КАК ТоварПредставление,
	               |		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		ХарактеристикаНоменклатуры.Наименование КАК ХарактеристикаНоменклатуры,
	               |		Количество КАК Количество,
	               |		Коэффициент КАК Коэффициент,
	               |		КоличествоБазовое КАК КоличествоБазовое,
	               |		ВЫРАЗИТЬ(ТК_НалоговаяНакладная.Товары.Цена КАК ЧИСЛО(15, 2)) КАК ЦенаПредставление,
	               |		ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |		Сумма КАК Сумма,
	               |		СуммаВсего КАК СуммаВсего,
	               |		СтавкаНДС КАК СтавкаНДС,
	               |		СуммаНДС КАК СуммаНДС,
	               |		СуммаБезНалогов КАК СуммаБезНалогов,
	               |		ПроцентСкидки КАК ПроцентСкидки,
	               |		СуммаСкидки КАК СуммаСкидки,
	               |		Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	               |		ЕСТЬNULL(ТК_НалоговаяНакладная.Товары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |		Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	               |		ЕСТЬNULL(ТК_НалоговаяНакладная.Товары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |		Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	               |		ЕСТЬNULL(ТК_НалоговаяНакладная.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |		Номенклатура.Наименование2 КАК НаименованиеДляНалоговой
	               |	) КАК Товары,
	               |	ЕСТЬNULL(ТК_НалоговаяНакладная.ТочкаДоставки.Порядок, 0) КАК ПорядокПивной,
	               |	ЕСТЬNULL(ТК_НалоговаяНакладная.ТочкаДоставки.ПорядокСигаретный, 0) КАК ПорядокСигаретный,
	               |	ТК_НалоговаяНакладная.ДолжностьОтветственногоОтправителя КАК ДолжностьОтветственногоОтправителя,
	               |	ТК_НалоговаяНакладная.ДолжностьОтветственногоПолучателя КАК ДолжностьОтветственногоПолучателя,
	               |	ТК_НалоговаяНакладная.ОтветственныйОтправитель КАК ОтветственныйОтправитель,
	               |	ТК_НалоговаяНакладная.ОтветственныйПолучатель КАК ОтветственныйПолучатель,
	               |	ТК_НалоговаяНакладная.РН КАК РН,
	               |	ТК_НалоговаяНакладная.НомерРН КАК НомерРН
	               |ИЗ
	               |	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
	               |ГДЕ
	               |	ТК_НалоговаяНакладная.Ссылка В(&МассивСсылок)" + СортировкаЗапроса + ", НомерСтроки";
	               
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
		
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РасходнаяНакладнаяНН";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваров.ПФ_MXL_РасходнаяНакладная");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("ПокупательКазн");
	//ОбластьМестоСоставления = Макет.ПолучитьОбласть("Склад");	
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТара");
	Если Шрифт Тогда
		ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаТара");
	Иначе 
		ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаТара1");
	КонецЕсли;
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	ОбластьСертификаты		= Макет.ПолучитьОбласть("Сертификаты");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		РН = ВыборкаДокументов.РН;
		ДатаДействия = '20220601000000';
		Если РН и ВыборкаДокументов.Дата > ДатаДействия Или НомерРНН Тогда
			Номер = СокрЛП(ВыборкаДокументов.НомерРН);
		Иначе						
			Номер = СокрЛП(ВыборкаДокументов.Номер);
		КонецЕсли;
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Расходная накладная №%1 от %2'; uk = 'Видаткова накладна №%1 від %2'", КодЯзыкаПечать),
			Номер,
			Формат(ВыборкаДокументов.Дата, НСтр("ru = 'Л=ru; ДЛФ=DD'; uk = 'Л=uk; ДЛФ=DD'", КодЯзыкаПечать)));

		
		СтруктураШапки = Новый Структура;
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		СтруктураШапки.Вставить("АдресДоставки", "");
		СтруктураШапки.Вставить("ОснованиеМесто", "");
		СтруктураШапки.Вставить("ОснованиеПредставлениеМесто", "");
		СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", "");
		СтруктураШапки.Вставить("ПредставлениеЗаказ", "");
		СтруктураШапки.Вставить("ПредставлениеРезерва", "");		
		СтруктураШапки.Вставить("Основание", НСтр("ru = 'Основание'; uk = 'Підстава'",КодЯзыкаПечать));
		СтруктураШапки.Вставить("ОснованиеПредставление", "");
				
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		
		Если Договор <> Неопределено Тогда
			СтруктураШапки.Вставить("ОснованиеПредставление", Договор.НаименованиеДляПечати);
		КонецЕсли;
		
		Если нАдресДоставки <> Неопределено Тогда 
			СтруктураШапки.Вставить("АдресДоставки", нАдресДоставки);
		КонецЕсли;	
		
		Если нПредставлениеСкладаМестоСоставления <> Неопределено Тогда 
			СтруктураШапки.Вставить("ПредставлениеСкладаМестоСоставления", нПредставлениеСкладаМестоСоставления);
		КонецЕсли;	
		
		СтруктураШапки.Вставить("Поставщик", ВыборкаДокументов.Организация);
		СтруктураШапки.Вставить("ПредставлениеПоставщика", ?(ЗначениеЗаполнено(ВыборкаДокументов.ОрганизацияНаименованиеПолное), СокрЛП(ВыборкаДокументов.ОрганизацияНаименованиеПолное), Строка(ВыборкаДокументов.Организация)));
		СтруктураШапки.Вставить("РеквизитыПоставщика", Справочники.Организации.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Организация, Ложь, Истина, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));

		СтруктураШапки.Вставить("ПредставлениеПолучателя", ?(ЗначениеЗаполнено(ВыборкаДокументов.КонтрагентНаименованиеПолное), СокрЛП(ВыборкаДокументов.КонтрагентНаименованиеПолное), строка(ВыборкаДокументов.Контрагент)));
		СтруктураШапки.Вставить("РеквизитыПолучателя", Справочники.Контрагенты.ПолучитьПредставлениеДляПечати(ВыборкаДокументов.Контрагент, Ложь, Истина, Истина, Истина, ВыборкаДокументов.Дата, КодЯзыкаПечать));		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		                              
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		//ОбластьМестоСоставления.Параметры.Заполнить(СтруктураШапки);
		//ТабличныйДокумент.Вывести(ОбластьМестоСоставления);
		
		ОбластьШапка.Параметры.ИмяКолонкиТовар = НСтр("ru = 'Товар'; uk = 'Товар'", КодЯзыкаПечать);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;		
		СуммаНДС 		= 0;
		СуммаВсего 		= 0;
		СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл 
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, СуммаВсего");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			СтруктураСтроки.Вставить("ЦенаПредставление", Формат(ВыборкаТовары.ЦенаПредставление, "ЧЦ=15; ЧДЦ=2"));
			
			стрНоменклатура = ?(ВыводитьАртикула, СокрЛП(ВыборкаТовары.Артикул) +  " ", "") + СокрЛП(ВыборкаТовары.НаименованиеДляНалоговой);						
			СтруктураСтроки.Вставить("ТоварПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			СтруктураСтроки.Вставить("КоличествоПредставление", Строка(ВыборкаТовары.Количество) + " " + Строка(ВыборкаТовары.ЕдиницаИзмерения));
			
			КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
												ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
													ВыборкаТовары.Номенклатура,
													ВыборкаТовары.КоличествоБазовое,
													ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
													ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
													ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
												СтруктураИтоговМест);

			СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Количество = Количество + ВыборкаТовары.Количество;
			СуммаНДС = СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего = СуммаВсего + ВыборкаТовары.СуммаВсего;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("ВсегоБезНДС", СуммаВсего - СуммаНДС);
		СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);
		СтруктураПодвала.Вставить("Всего", Формат(СуммаВсего, "ЧЦ=15; ЧДЦ=2"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);
		СтруктураПодвала.Вставить("ВсегоКво", Количество);
		СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
					
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);		
		
		Отправитель = ?(ТипЗнч(ВыборкаДокументов.ОтветственныйОтправитель) = Тип("СправочникСсылка.ФизическиеЛица"), ВыборкаДокументов.ОтветственныйОтправитель.Наименование,ВыборкаДокументов.ОтветственныйОтправитель);
		Получатель =  ?(ТипЗнч(ВыборкаДокументов.ОтветственныйПолучатель)  = Тип("СправочникСсылка.ФизическиеЛица"), ВыборкаДокументов.ОтветственныйПолучатель.Наименование,ВыборкаДокументов.ОтветственныйПолучатель);
		Отгрузил = ВыборкаДокументов.ДолжностьОтветственногоОтправителя+", "+ОбщегоНазначенияТК.ФамилияИнициалы(Отправитель);								
		Получил	= ВыборкаДокументов.ДолжностьОтветственногоПолучателя+", "+ОбщегоНазначенияТК.ФамилияИнициалы(Получатель);																		

		СтруктураПодписи = Новый Структура;
		
		Если нОтгрузил = Неопределено Тогда
			СтруктураПодписи.Вставить("Отгрузил", Отгрузил);
		Иначе
			СтруктураПодписи.Вставить("Отгрузил", нОтгрузил);
		КонецЕсли;
		
		Если нОтгрузил = Неопределено Тогда
			СтруктураПодписи.Вставить("Получил", Получил);
		Иначе
			СтруктураПодписи.Вставить("Получил", нПолучил);	
		КонецЕсли;
		
		ОбластьПодписи.Параметры.Заполнить(СтруктураПодписи);

		ТабличныйДокумент.Вывести(ОбластьПодписи);
		ТабличныйДокумент.Вывести(ОбластьСертификаты);
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


Функция КодИсточникаНалоговогоНомера(ЗНАЧ НалоговыйНомер, ПлательщикЮрлицо) Экспорт
	
	НалоговыйНомер = СокрЛП(НалоговыйНомер);
	ДлинаНомера = СтрДлина(НалоговыйНомер);
	
	Результат = "";
	
	Если ДлинаНомера > 0 Тогда
		
		Если ПлательщикЮрлицо Тогда
			
			Если ДлинаНомера = 9 Тогда
				//спецплательщик
				Результат = 3;
			ИначеЕсли ДлинаНомера = 8 Тогда
				//ЕДРПОУ
				Результат = 1;
			КонецЕсли;
			
		Иначе
			
			Если ДлинаНомера = 10 Тогда
				//ДРФО
				Результат = 2;
			Иначе
				//паспорт
				Результат = 4;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


Функция СтруктураНалоговогоДокумента(ДокументСсылка,ТолькоСтруктуру)Экспорт
	
	СтруктураПоказателей = Новый Структура();
	
	СтруктураПоказателей.Вставить("НалоговыйДокумент",   	Новый Структура());	// данные налоговой накладной
	СтруктураПоказателей.НалоговыйДокумент.Вставить("R",   	Новый ТаблицаЗначений());  // данные табличной части
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G3S");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G32");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G33");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G4");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G4S");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G105_2S");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G5");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G6");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G008");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G009");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G010");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G011");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G11_10");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("ROWNUM");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G11_10Регл");
	СтруктураПоказателей.НалоговыйДокумент.R.Колонки.Добавить("G11_10Документ");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТК_НалоговаяНакладная.Дата КАК Дата,
		|	ТК_НалоговаяНакладная.Организация КАК Организация,
		|	ТК_НалоговаяНакладная.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ТК_НалоговаяНакладная.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|				ИЛИ ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая)
		|			ТОГДА ""Неплатник""
		|		КОГДА ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА ""Неплатник""
		|		ИНАЧЕ ТК_НалоговаяНакладная.Контрагент.НаименованиеПолное
		|	КОНЕЦ КАК НазваниеКонтрагента,
		|	ТК_НалоговаяНакладная.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
		|	ТК_НалоговаяНакладная.ВалютаДокумента КАК ВалютаДокумента,
		|	ТК_НалоговаяНакладная.ТипЦен КАК ТипЦен,
		|	ТК_НалоговаяНакладная.КтоВыписалНалоговуюНакладную КАК КтоВыписалНалоговуюНакладную,
		|	ТК_НалоговаяНакладная.НомерДляБухгалтерии КАК НомерДляБухгалтерии,
		|	ВЫБОР
		|		КОГДА ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|				ИЛИ ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая)
		|			ТОГДА ""100000000000""
		|		КОГДА ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА ""100000000000""
		|		ИНАЧЕ ТК_НалоговаяНакладная.Контрагент.ИННПлательщикаНДС
		|	КОНЕЦ КАК КодКонтрагента,
		|	ВЫБОР
		|		КОГДА ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|				ИЛИ ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая)
		|			ТОГДА """"
		|		КОГДА ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 2
		|				ИЛИ ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю = 11
		|			ТОГДА """"
		|		ИНАЧЕ ТК_НалоговаяНакладная.Контрагент.КодПоЕДРПОУ
		|	КОНЕЦ КАК ЕДРПОУКонтрагента,
		|	ВЫБОР
		|		КОГДА ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа)
		|				ИЛИ ТК_НалоговаяНакладная.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТК_НалоговаяНакладная.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|	КОНЕЦ КАК ЮрлицоКонтрагент,
		|	ТК_НалоговаяНакладная.СуммаДокумента КАК СуммаДокумента,
		|	ТК_НалоговаяНакладная.СуммаНДСДокумента КАК СуммаНДСДокумента,
		|	ТК_НалоговаяНакладная.Номер КАК Номер,
		|	ТК_НалоговаяНакладная.ТипПричиныНевыдачиПокупателю КАК ТипНеВыдачи,
		|	ТК_НалоговаяНакладная.ХозОперация КАК ХозОперация,
		|	ТК_НалоговаяНакладная.КтоВыписалНалоговуюНакладную.ФИО КАК КтоВыписалНалоговуюФио
		|ИЗ
		|	Документ.ТК_НалоговаяНакладная КАК ТК_НалоговаяНакладная
		|ГДЕ
		|	ТК_НалоговаяНакладная.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТК_НалоговаяНакладнаяТовары.НомерСтроки КАК НомерСтроки,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТК_НалоговаяНакладнаяТовары.Коэффициент КАК Коэффициент,
		|	ТК_НалоговаяНакладнаяТовары.КоличествоБазовое КАК Количество,
		|	ТК_НалоговаяНакладнаяТовары.Цена КАК Цена,
		|	ТК_НалоговаяНакладнаяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
		|	ТК_НалоговаяНакладнаяТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТК_НалоговаяНакладнаяТовары.СуммаНДС КАК СуммаНДС,
		|	ТК_НалоговаяНакладнаяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
		|	ТК_НалоговаяНакладнаяТовары.Сумма КАК Сумма,
		|	ТК_НалоговаяНакладнаяТовары.ПроцентСкидки КАК ПроцентСкидки,
		|	ТК_НалоговаяНакладнаяТовары.СуммаСкидки КАК СуммаСкидки,
		|	ТК_НалоговаяНакладнаяТовары.СуммаВсего КАК СуммаВсего,
		|	ТК_НалоговаяНакладнаяТовары.Содержание КАК Содержание,
		|	ТК_НалоговаяНакладнаяТовары.НомерСтрокиНН КАК НомерСтрокиНН,
		|	ТК_НалоговаяНакладнаяТовары.ДатаОтгрузкиОплаты КАК ДатаОтгрузкиОплаты,
		|	ТК_НалоговаяНакладнаяТовары.СтавкаНДС.Ставка КАК НДССтавка,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура.БазоваяЕдиницаИзмерения.НаименованиеПолное КАК ЕдиницаИзмерения,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ВЫБОР
		|		КОГДА ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ТК_НалоговаяНакладнаяТовары.Номенклатура.КодУКТВЭД
		|		КОГДА ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры.УКТВЭД = ЗНАЧЕНИЕ(справочник.КлассификаторУКТВЭД.ПустаяСсылка)
		|			ТОГДА ТК_НалоговаяНакладнаяТовары.Номенклатура.КодУКТВЭД
		|		ИНАЧЕ ТК_НалоговаяНакладнаяТовары.ХарактеристикаНоменклатуры.УКТВЭД
		|	КОНЕЦ КАК КодУКТВЭД,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура.Наименование2 КАК НоменклатураНаименование2,
		|	ТК_НалоговаяНакладнаяТовары.Номенклатура.СтранаПроисхождения <> &Украина КАК Импортированный,
		|	ЕСТЬNULL(ДополнительныеСведения.Значение, """") КАК КодУслуги
		|ИЗ
		|	Документ.ТК_НалоговаяНакладная.Товары КАК ТК_НалоговаяНакладнаяТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО ТК_НалоговаяНакладнаяТовары.Номенклатура = ДополнительныеСведения.Объект
		|			И (ДополнительныеСведения.Свойство.Имя = ""КодУслуги"")
		|ГДЕ
		|	ТК_НалоговаяНакладнаяТовары.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Украина", Справочники.СтраныМира.НайтиПоКоду("804"));

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ШапкаДокумента = РезультатЗапроса[0].Выбрать();
	ШапкаДокумента.Следующий();
	СтруктураПоказателей.Вставить("Дата",ШапкаДокумента.Дата);
	СтруктураПоказателей.Вставить("Номер",ШапкаДокумента.Номер);
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	ТаблицаТовары.Колонки.Добавить("ЦенаБезНДСРегл");
	ТаблицаТовары.Колонки.Добавить("СуммаБезНДСРегл");
	ТаблицаТовары.Колонки.Добавить("СуммаНДСРегл");
	ТаблицаТовары.Колонки.Добавить("СуммаСНДСРегл");
	
    БезНДС = Ложь;
	Если ТаблицаТовары.Итог("СуммаНДС") = 0 Тогда
		БезНДС = Истина;
	КонецЕсли;
	
	КопТабТовары = ТаблицаТовары.Скопировать(,"ХарактеристикаНоменклатуры");
	КопТабТовары.Свернуть("ХарактеристикаНоменклатуры");
	Если КопТабТовары.Количество() > 0 и ЗначениеЗаполнено(КопТабТовары[0].ХарактеристикаНоменклатуры) и НЕ БезНДС Тогда
		МРЦ = "6";
	Иначе 
		МРЦ = " ";
	КонецЕсли;
		
	Если БезНДС Тогда 
		Для Каждого СтрокаТаблицы Из ТаблицаТовары Цикл 
			
			СтрокаТаблицы.СуммаСНДСРегл   =  СтрокаТаблицы.СуммаВсего; //СтрокаТаблицы.СуммаБезНалогов + СтрокаТаблицы.СуммаНДС;
			СтрокаТаблицы.СуммаНДСРегл    =  СтрокаТаблицы.СуммаНДС;
			СтрокаТаблицы.СуммаБезНДСРегл =  СтрокаТаблицы.СуммаБезНалогов;
			СтрокаТаблицы.ЦенаБезНДСРегл  =  СтрокаТаблицы.ЦенаБезНалогов;
			
		КонецЦикла;
	Иначе
		Для Каждого СтрокаТаблицы Из ТаблицаТовары Цикл 
			
			СтрокаТаблицы.СуммаСНДСРегл   =  СтрокаТаблицы.СуммаВсего; //СтрокаТаблицы.СуммаБезНалогов + СтрокаТаблицы.СуммаНДС;
			
			//СтрокаТаблицы.СуммаНДСРегл    =  СтрокаТаблицы.СуммаНДС;			
			//СтрокаТаблицы.СуммаБезНДСРегл =  Окр(СтрокаТаблицы.СуммаСНДСРегл - СтрокаТаблицы.СуммаНДСРегл, 2);
			
			//Vov41k 10.08.2023
			СтрокаТаблицы.СуммаБезНДСРегл =  Окр(СтрокаТаблицы.СуммаВсего - СтрокаТаблицы.СуммаНДС, 2);
			СтрокаТаблицы.СуммаНДСРегл    =  СтрокаТаблицы.СуммаБезНДСРегл * СтрокаТаблицы.НДССтавка / 100; 
						
			СтрокаТаблицы.ЦенаБезНДСРегл  = 	СтрокаТаблицы.СуммаБезНДСРегл / ?(СтрокаТаблицы.Количество = 0, 1, СтрокаТаблицы.Количество);
			
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаСумм = Новый ТаблицаЗначений;
	ТаблицаСумм.Колонки.Добавить("НДССтавка");
	ТаблицаСумм.Колонки.Добавить("СуммаБезНДС");
	ТаблицаСумм.Колонки.Добавить("СуммаБезНДСРегл");
	ТаблицаСумм.Колонки.Добавить("СуммаБезНДСРазница");
	ТаблицаСумм.Колонки.Добавить("СуммаНДС");
	ТаблицаСумм.Колонки.Добавить("СуммаВсего");
	
	ИтогНДС    = Окр(ТаблицаТовары.Итог("СуммаНДС"), 2);
	ВсегоСумма = Окр(ТаблицаТовары.Итог("СуммаВсего"), 2);	
	
	Если ИтогНДС = 0 Тогда
		БезНДС = Истина;
	Иначе
		БезНДС = Ложь;
	КонецЕсли;
	
	//НДС 7%
	ВТ7 = ТаблицаТовары.Скопировать(Новый Структура("НДССтавка", 7), "СуммаБезНДСРегл, СуммаНДС, СуммаВсего");		
	Если ВТ7.Количество() > 0 и не БезНДС Тогда 
		ВТ7.Свернуть(,"СуммаБезНДСРегл, СуммаНДС, СуммаВсего");
		
		нСтрокаСтавка = ТаблицаСумм.Добавить();
		нСтрокаСтавка.НДССтавка = 7;
		нСтрокаСтавка.СуммаВсего = Окр(ВТ7[0].СуммаВсего, 2);
		нСтрокаСтавка.СуммаНДС = Окр(ВТ7[0].СуммаНДС, 2);
		нСтрокаСтавка.СуммаБезНДС = нСтрокаСтавка.СуммаВсего - нСтрокаСтавка.СуммаНДС;
		нСтрокаСтавка.СуммаБезНДСРегл = Окр(ВТ7[0].СуммаБезНДСРегл, 2);
		нСтрокаСтавка.СуммаБезНДСРазница = нСтрокаСтавка.СуммаБезНДС - нСтрокаСтавка.СуммаБезНДСРегл;
	КонецЕсли;
	
	//НДС 20%
	ВТ20 = ТаблицаТовары.Скопировать(Новый Структура("НДССтавка", 20), "СуммаБезНДСРегл, СуммаНДС, СуммаВсего");
	Если ВТ20.Количество() > 0 и не БезНДС Тогда 
		
		нСтрокаСтавка = ТаблицаСумм.Добавить();
		нСтрокаСтавка.НДССтавка = 20;
		нСтрокаСтавка.СуммаВсего = ВсегоСумма - ТаблицаСумм.Итог("СуммаВсего"); 
		нСтрокаСтавка.СуммаНДС = ИтогНДС - ТаблицаСумм.Итог("СуммаНДС");
		нСтрокаСтавка.СуммаБезНДС = нСтрокаСтавка.СуммаВсего - нСтрокаСтавка.СуммаНДС;
		нСтрокаСтавка.СуммаБезНДСРегл = ТаблицаТовары.Скопировать(Новый Структура("НДССтавка", 20), "СуммаБезНДСРегл").Итог("СуммаБезНДСРегл");		
		нСтрокаСтавка.СуммаБезНДСРазница = нСтрокаСтавка.СуммаБезНДС - нСтрокаСтавка.СуммаБезНДСРегл;
	КонецЕсли;

	//НДС 0
	ВТ0 = ТаблицаТовары.Скопировать(Новый Структура("НДССтавка", 0), "СуммаБезНДСРегл, СуммаНДС, СуммаВсего");
	Если ВТ0.Количество() > 0 Тогда 
		
		нСтрокаСтавка = ТаблицаСумм.Добавить();
		нСтрокаСтавка.НДССтавка = 903;
		нСтрокаСтавка.СуммаВсего = ВсегоСумма - ТаблицаСумм.Итог("СуммаВсего"); 
		нСтрокаСтавка.СуммаНДС = 0;
		нСтрокаСтавка.СуммаБезНДС = нСтрокаСтавка.СуммаВсего;
		нСтрокаСтавка.СуммаБезНДСРегл = ТаблицаТовары.Скопировать(Новый Структура("НДССтавка", 0), "СуммаБезНДСРегл").Итог("СуммаБезНДСРегл");		
		нСтрокаСтавка.СуммаБезНДСРазница = нСтрокаСтавка.СуммаБезНДС;
	КонецЕсли;

	Для Каждого СтрокаСтавки Из ТаблицаСумм Цикл 
		Если СтрокаСтавки.СуммаБезНДСРазница = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		МассивТоваров = ТаблицаТовары.НайтиСтроки(Новый Структура("НДССтавка", СтрокаСтавки.НДССтавка));				
		КолВоТоваров = МассивТоваров.Количество();
		Если КолВоТоваров = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		Если КолВоТоваров > Цел(СтрокаСтавки.СуммаБезНДСРазница*100) Тогда 
			ШагКорр = ?(СтрокаСтавки.СуммаБезНДСРазница>0, 0.01, -0.01);
		Иначе
			ШагКорр = ОбщегоНазначенияТККлиентСервер.ОкруглитьКратно(СтрокаСтавки.СуммаБезНДСРазница / КолВоТоваров, 0.01);
		КонецЕсли;
		
		Ит = 0;
		Пока СтрокаСтавки.СуммаБезНДСРазница <> 0 Цикл 
			СтрокаТовары = МассивТоваров.Получить(Ит);
			СтрокаТовары.СуммаБезНДСРегл = СтрокаТовары.СуммаБезНДСРегл + ШагКорр;			
			СтрокаТовары.СуммаНДСРегл = СтрокаТовары.СуммаБезНДСРегл * СтрокаТовары.НДССтавка / 100; //Vov41k 10.08.2023
						
			СтрокаСтавки.СуммаБезНДСРазница = СтрокаСтавки.СуммаБезНДСРазница - ШагКорр;
			
			Если Ит = КолВоТоваров-1 Тогда 
				Если нСтрокаСтавка.СуммаБезНДСРазница <> 0 Тогда 
					СтрокаТовары.СуммаБезНДСРегл = СтрокаТовары.СуммаБезНДСРегл + СтрокаСтавки.СуммаБезНДСРазница;
					СтрокаТовары.СуммаНДСРегл = СтрокаТовары.СуммаБезНДСРегл * СтрокаТовары.НДССтавка / 100; //Vov41k 10.08.2023
					
					СтрокаСтавки.СуммаБезНДСРазница = 0;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
			
			Ит = Ит + 1;
		КонецЦикла;
	КонецЦикла;
	
	
	//// Шапка НДС 20%
	//СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G7", СуммаБезНДС20);	
	//СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G7", НДС20);
	//
	//// Шапка НДС 7%
	//Если НДС7 > 0 Тогда 
	//	СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G109", СуммаБезНДС7);	
	//	СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G109", НДС7);			
	//КонецЕсли;
		
	Для Каждого СтрокаСтавки Из ТаблицаСумм Цикл 
		
		Если СтрокаСтавки.НДССтавка = 20  Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G7", СтрокаСтавки.СуммаБезНДС);	
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G7", СтрокаСтавки.СуммаНДС);
			
		ИначеЕсли СтрокаСтавки.НДССтавка = 14 Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G14", СтрокаСтавки.СуммаБезНДС);	
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G14", СтрокаСтавки.СуммаНДС);	
			
		ИначеЕсли СтрокаСтавки.НДССтавка = 7 Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G109", СтрокаСтавки.СуммаБезНДС);	
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G109", СтрокаСтавки.СуммаНДС);		
			
		ИначеЕсли СтрокаСтавки.НДССтавка = 902 Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G8", СтрокаСтавки.СуммаБезНДС);

		ИначеЕсли СтрокаСтавки.НДССтавка = 901 Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G9", СтрокаСтавки.СуммаБезНДС);			
			
		ИначеЕсли СтрокаСтавки.НДССтавка = 903 Тогда
			
			СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G10", СтрокаСтавки.СуммаБезНДС);	
			
		КонецЕсли;
		
	КонецЦикла;
		
	СуммаВозвратнойТары = 0;
	
	СтруктураПоказателей.НалоговыйДокумент.Вставить("R04G11",ВсегоСумма);	
	СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G11",ИтогНДС);	
	СтруктураПоказателей.НалоговыйДокумент.Вставить("R02G11",СуммаВозвратнойТары);	
	
	РеквизитыОрганизации = Справочники.Организации.ПолучитьРеквизитыОрганизации(КонецДня(ШапкаДокумента.Дата),ШапкаДокумента.Организация);
	
	ИсточникКодаПокупателя = КодИсточникаНалоговогоНомера(ШапкаДокумента.ЕДРПОУКонтрагента, ШапкаДокумента.ЮрлицоКонтрагент);
	ИсточникКодаПродавца   = КодИсточникаНалоговогоНомера(РеквизитыОрганизации.КодПоЕДРПОУ, РеквизитыОрганизации.ЮрЛицо);
	
	СтруктураПоказателей.НалоговыйДокумент.Вставить("R01G1", "");
	СтруктураПоказателей.НалоговыйДокумент.Вставить("TIN", СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));			
	Если БезНДС Тогда
		СтруктураПоказателей.НалоговыйДокумент.Вставить("R03G10S",	"Без ПДВ");
	КонецЕсли;
	
	Если ШапкаДокумента.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажа или ШапкаДокумента.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяРозничнаяПродажаНеОблагаемая Тогда
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HORIG1",	"1");
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HTYPR",	"11");
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HKB","");
	ИначеЕсли ШапкаДокумента.ТипНеВыдачи = 2 ИЛИ  ШапкаДокумента.ТипНеВыдачи = 11 Тогда
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HORIG1",	"1");
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HTYPR",	Формат(ШапкаДокумента.ТипНеВыдачи,"ЧЦ=2; ЧВН="));
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HKB","");
	Иначе
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HORIG1",	"");
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HTYPR",	"");
		СтруктураПоказателей.НалоговыйДокумент.Вставить("HKB",		СокрЛП(ИсточникКодаПокупателя));
	КонецЕсли;
	
	ЧастиИмя =   ФизическиеЛицаКлиентСервер.ЧастиИмени(ШапкаДокумента.КтоВыписалНалоговуюФио);
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HFILL",	ШапкаДокумента.Дата);
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HNUM",		ПолучитьНомерДокумента(ШапкаДокумента.Дата, ШапкаДокумента.НомерДляБухгалтерии));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HNUM1",	МРЦ);
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HNUM2",	"");
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HFBUY",	"");
	//
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HNAMESEL",	СокрЛП(ШапкаДокумента.ОрганизацияНаименованиеПолное));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HNAMEBUY",	СокрЛП(ШапкаДокумента.НазваниеКонтрагента));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HKSEL",	СокрЛП(РеквизитыОрганизации.ИНН));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HKBUY",	СокрЛП(ШапкаДокумента.КодКонтрагента));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HBOS",		""+ЧастиИмя.Имя +" "+ЧастиИмя.Фамилия+"");
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HKBOS",	СокрЛП(РеквизитыОрганизации.РуководительКод));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HTINSEL",	СокрЛП(РеквизитыОрганизации.КодПоЕДРПОУ));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HTINBUY",	СокрЛП(ШапкаДокумента.ЕДРПОУКонтрагента));
	СтруктураПоказателей.НалоговыйДокумент.Вставить("HKS",		СокрЛП(ИсточникКодаПродавца));

	СтруктураПоказателей.НалоговыйДокумент.Вставить("R003G10S",	"");	

	СуммаНДСРасчет = 0;
	
	Для каждого СтрокаТаблицы Из ТаблицаТовары Цикл
		
		СтрокаСтруктуры = СтруктураПоказателей.НалоговыйДокумент.R.Добавить();
		
		СтрокаСтруктуры.G32  		= ?(СтрокаТаблицы.Импортированный,"1","");
		
		Если ШапкаДокумента.ХозОперация = Справочники.ХозОперации.НалоговаяНакладнаяАктУслуг Тогда
			СтрокаСтруктуры.G4S     	= "послуга";
			СтрокаСтруктуры.G33  		= СтрокаТаблицы.КодУслуги;
		    СтрокаСтруктуры.G3S 		= ?(Не ПустаяСтрока(СтрокаТаблицы.Содержание), СтрокаТаблицы.Содержание, "");
			СтрокаСтруктуры.G105_2S 	= "";
	        СтрокаСтруктуры.G4  		= "";
		Иначе
			СтрокаСтруктуры.G33  		= "";
		    СтрокаСтруктуры.G3S 		= ?(Не ПустаяСтрока(СтрокаТаблицы.НоменклатураНаименование2), СтрокаТаблицы.НоменклатураНаименование2, СтрокаТаблицы.НоменклатураНаименование);
			СтрокаСтруктуры.G4  		= СтрЗаменить(СокрЛП(СтрокаТаблицы.КодУКТВЭД), " ", "");
			СтрокаСтруктуры.G4S     	= СокрЛП(СтрокаТаблицы.ЕдиницаИзмерения);
			СтрокаСтруктуры.G105_2S 	= СокрЛП(СтрокаТаблицы.ЕдиницаИзмеренияКод);
		КонецЕсли; 
		
		
		СтрокаСтруктуры.G5 			= Формат(СтрокаТаблицы.Количество,"ЧЦ=27; ЧДЦ=12; ЧРД=.; ЧН=; ЧГ=");
		СтрокаСтруктуры.G6 			= Формат(СтрокаТаблицы.ЦенаБезНДСРегл,"ЧЦ=27; ЧДЦ=12; ЧРД=.; ЧН=; ЧГ=");
		Если БезНДС Тогда
			СтрокаСтруктуры.G008  		= "903";
			СтрокаСтруктуры.G009  		= "14060554";
		Иначе
			СтрокаСтруктуры.G008  		= Формат(СтрокаТаблицы.НДССтавка,"ЧЦ=2; ЧН=");
			СтрокаСтруктуры.G009  		= "";
			СтрокаСтруктуры.G11_10  	= Формат(СтрокаТаблицы.СуммаНДСРегл,  "ЧЦ=19; ЧДЦ=6; ЧРД=.; ЧН=; ЧГ=");
		КонецЕсли;
		СтрокаСтруктуры.G010  		= Формат(СтрокаТаблицы.СуммаБезНДСРегл,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=");
		СтрокаСтруктуры.G011  		= "";
		
		
		РасчСуммаНДС6Зн = Окр(НДСОбщегоНазначенияКлиентСервер.РассчитатьСуммуНДС(СтрокаТаблицы.СуммаБезНДСРегл, Ложь, СтрокаТаблицы.НДССтавка), 6 );

		СтрокаСтруктуры.G11_10Регл     = РасчСуммаНДС6Зн;
		СтрокаСтруктуры.G11_10Документ = СтрокаТаблицы.СуммаНДСРегл;

		
		
		СтрокаСтруктуры.ROWNUM		= СтрокаТаблицы.НомерСтроки;
			
		СуммаНДСРасчет = СуммаНДСРасчет + РасчСуммаНДС6Зн;
	КонецЦикла;
	
	СтруктураПоказателей.Вставить("СуммаНДС_Расчет",СуммаНДСРасчет);
	СтруктураПоказателей.Вставить("СуммаНДС_Документ",ИтогНДС);

	Если ТолькоСтруктуру Тогда
		Возврат СтруктураПоказателей;
	КонецЕсли;
	
	
	ОбъектXML = Новый ЗаписьXML;
	ОбъектXML.УстановитьСтроку("windows-1251");
	ОбъектXML.ЗаписатьОбъявлениеXML();	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLAR");
	//Схема = "J1201010.xsd";
	Схема = "J1201015.xsd"; //Vov41k 02.08.2023
	
	ОбъектXML.ЗаписатьНачалоАтрибута("xmlns:xsi"); 
	ОбъектXML.ЗаписатьТекст("http://www.w3.org/2001/XMLSchema-instance"); 
	ОбъектXML.ЗаписатьКонецАтрибута();
	
	ОбъектXML.ЗаписатьНачалоАтрибута("xsi:noNamespaceSchemaLocation");
	ОбъектXML.ЗаписатьТекст(Схема);
	ОбъектXML.ЗаписатьКонецАтрибута();
	
	Схема = СтрЗаменить(Схема, ".xsd", "");
	
	///DECLARHEAD	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLARHEAD");		
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.TIN, "TIN");
	ЗаписатьXML(ОбъектXML, Лев(Схема, 3), "C_DOC");
	ЗаписатьXML(ОбъектXML, Сред(Схема, 4, 3), "C_DOC_SUB");
	ЗаписатьXML(ОбъектXML, Прав(Схема, 2), "C_DOC_VER");
	
	//ЗаписатьXML(ОбъектXML, Прав(Схема, 1), "C_DOC_TYPE");
	//ЗаписатьXML(ОбъектXML, "1242", "C_DOC_CNT");
	
	//Vov41k 02.08.2023
	ЗаписатьXML(ОбъектXML, "0", "C_DOC_TYPE");
	ЗаписатьXML(ОбъектXML, "1", "C_DOC_CNT");	
	
	ЗаписатьXML(ОбъектXML, Месяц(СтруктураПоказателей.Дата),"PERIOD_MONTH");
	ЗаписатьXML(ОбъектXML, 1,"PERIOD_TYPE");
	ЗаписатьXML(ОбъектXML, Год(СтруктураПоказателей.Дата), "PERIOD_YEAR");
	ЗаписатьXML(ОбъектXML, 1,"C_DOC_STAN");
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.Дата,"ДФ=ddMMyyyy"), "D_FILL");
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARHEAD	

	
	
	///DECLARBODY	
	ОбъектXML.ЗаписатьНачалоЭлемента("DECLARBODY");
	Если БезНДС Тогда
		ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.R03G10S, "R03G10S");	
	КонецЕсли;
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HORIG1, "HORIG1");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HTYPR, "HTYPR");	
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.НалоговыйДокумент.HFILL,"ДФ=ddMMyyyy"), "HFILL");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HNUM, "HNUM");	//номер документа
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HNUM1, "HNUM1");	//номер документа
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HNAMESEL, "HNAMESEL");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HNAMEBUY, "HNAMEBUY");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HKSEL, "HKSEL");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HTINSEL, "HTINSEL");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HKS, "HKS");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HKBUY, "HKBUY");
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HKB, "HKB");

	Если ЗначениеЗаполнено(СтруктураПоказателей.НалоговыйДокумент.HTINBUY) Тогда
		 ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HTINBUY, "EDR_POK");
	КонецЕсли;
		
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.НалоговыйДокумент.R04G11,"ЧРД=.; ЧГ=0"), "R04G11");	
	ЗаписатьXML(ОбъектXML, Формат(СтруктураПоказателей.НалоговыйДокумент.R03G11,"ЧРД=.; ЧГ=0"), "R03G11");	
	//20
	Если СтруктураПоказателей.НалоговыйДокумент.Свойство("R03G7") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.НалоговыйДокумент.R03G7,"ЧРД=.; ЧГ=0"), "R03G7");//НДС20
	КонецЕсли;
	Если СтруктураПоказателей.НалоговыйДокумент.Свойство("R01G7") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.НалоговыйДокумент.R01G7,"ЧРД=.; ЧГ=0"), "R01G7");//БНДС20	
	КонецЕсли;
	//7
	Если СтруктураПоказателей.НалоговыйДокумент.Свойство("R03G109") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.НалоговыйДокумент.R03G109,"ЧРД=.; ЧГ=0"), "R03G109");//НДС7	
	КонецЕсли;
	Если СтруктураПоказателей.НалоговыйДокумент.Свойство("R01G109") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.НалоговыйДокумент.R01G109,"ЧРД=.; ЧГ=0"), "R01G109");//БНДС7	
	КонецЕсли;	
	//0
	Если СтруктураПоказателей.НалоговыйДокумент.Свойство("R01G10") Тогда
		ЗаписатьXML(ОбъектXML,  Формат(СтруктураПоказателей.НалоговыйДокумент.R01G10,"ЧРД=.; ЧГ=0"), "R01G10");//БНДС0	
	КонецЕсли;	

	
	СоответствиеКолонок = Новый Соответствие;
	СоответствиеКолонок.Вставить("RXXXXG3S",	"G3S");
	СоответствиеКолонок.Вставить("RXXXXG32", 	"G32");
	СоответствиеКолонок.Вставить("RXXXXG33", 	"G33");
	СоответствиеКолонок.Вставить("RXXXXG4", 	"G4");

	СоответствиеКолонок.Вставить("RXXXXG008", 	"G008");
	Если БезНДС Тогда 
		СоответствиеКолонок.Вставить("RXXXXG009", 	"G009");
	Иначе
		СоответствиеКолонок.Вставить("RXXXXG11_10", "G11_10");
	КонецЕсли;
	СоответствиеКолонок.Вставить("RXXXXG010", 	"G010");
	//Новые
	СоответствиеКолонок.Вставить("RXXXXG5", 	"G5");
	СоответствиеКолонок.Вставить("RXXXXG6", 	"G6");
	СоответствиеКолонок.Вставить("RXXXXG4S", 	"G4S");
	СоответствиеКолонок.Вставить("RXXXXG105_2S","G105_2S");	
	
	Для Каждого Параметр Из СоответствиеКолонок Цикл
		Номер = 0;
		Для Каждого Стр Из СтруктураПоказателей.НалоговыйДокумент.R Цикл					
			Значение = Неопределено;
			
			Номер = Номер + 1;
			ОбъектXML.ЗаписатьНачалоЭлемента(Параметр.Ключ);											
			ОбъектXML.ЗаписатьАтрибут("ROWNUM", Формат(Номер, "ЧРД=.; ЧГ=0"));
			ОбъектXML.ЗаписатьТекст(Стр[Параметр.Значение]);						
			ОбъектXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
	КонецЦикла;		
	
	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HBOS, "HBOS");	
	ЗаписатьXML(ОбъектXML, СтруктураПоказателей.НалоговыйДокумент.HKBOS, "HKBOS");	
	
	ОбъектXML.ЗаписатьКонецЭлемента(); ///DECLARBODY	
	ОбъектXML.ЗаписатьКонецЭлемента(); //DECLAR
	
	СтрXML = ОбъектXML.Закрыть();	
	
	
	ПотокДанных = Новый ПотокВПамяти();
	
	ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных,"windows-1251");
	ЗаписьДанных.ЗаписатьСтроку(СтрXML,"windows-1251");
	ЗаписьДанных.Закрыть();
	
	ДВ = ПотокДанных.ЗакрытьИПолучитьДвоичныеДанные();
	СтруктураПоказателей.Вставить("ДанныеXML",ДВ);

	СтруктураПоказателей.Вставить("ТекстXML",СтрXML);
	СтруктураПоказателей.Вставить("ИмяФайла",СокрЛП(Строка(ШапкаДокумента.Организация)) + "_" + Формат(ШапкаДокумента.Дата, "ДФ=yyyyMMdd")+"_"+ШапкаДокумента.Номер+"_НН.xml"); 
	
	Возврат СтруктураПоказателей;

КонецФункции

Функция СформироватьНалоговую(МассивДокументов,ТолькоСтруктуру=Ложь)Экспорт
	
	МассивВозврата = Новый Массив;
	
	
	Для Индекс = 0 ПО МассивДокументов.ВГраница() Цикл
		
		Документ = МассивДокументов[Индекс];
		
		СтруктруаДокумента = СтруктураНалоговогоДокумента(Документ,ТолькоСтруктуру);
		МассивВозврата.Добавить(Новый Структура("Документ,Выгрузка",Документ,СтруктруаДокумента));
		
	КонецЦикла;
	
	
	Возврат МассивВозврата;
	
КонецФункции


#КонецОбласти	


#КонецЕсли
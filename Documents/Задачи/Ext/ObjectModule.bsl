#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем НовыйСтатусЗаявки Экспорт;





Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	
	Если Не ОбменДанными.Загрузка Тогда
		
		ДополнительныеСвойства.Вставить("СтатусЗаявки",   ?(ЭтоНовый(),Неопределено, Статус));

		
		Если НовыйСтатусЗаявки <> Неопределено Тогда
			Статус = НовыйСтатусЗаявки;	
			ДатаЗавершения = ТекущаяДатаСеанса();
		Иначе
			
			Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Если НЕ Статус = Перечисления.СтатусыПроцессов.Завершено Тогда
					Статус = Перечисления.СтатусыПроцессов.Завершено;
				
				КонецЕсли;
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Если ЗначениеЗаполнено(ТекущийИсполнитель)Тогда
					Статус = Перечисления.СтатусыПроцессов.Выполняется;
				Иначе
					Статус = Перечисления.СтатусыПроцессов.Открыт;
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(ТекущийИсполнитель) И Статус = Перечисления.СтатусыПроцессов.Открыт Тогда
				Статус = Перечисления.СтатусыПроцессов.Выполняется;
				ДатаВзята = ТекущаяДатаСеанса();
			ИначеЕсли НЕ ЗначениеЗаполнено(ТекущийИсполнитель) И Статус = Перечисления.СтатусыПроцессов.Выполняется Тогда
				Статус = Перечисления.СтатусыПроцессов.Открыт;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.СтатусЗаявки<>Статус Тогда
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей = РегистрыСведений.ТК_СтатусыЗадач.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Задача.Установить(Ссылка);
		НаборЗаписей.Отбор.Период.Установить(ТекущаяДатаСеанса());

		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Задача = Ссылка;
		НоваяЗапись.Период = ТекущаяДатаСеанса();
		НоваяЗапись.Статус = Статус;
		НаборЗаписей.Записать();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	
КонецПроцедуры


	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ЭтоНовый() Тогда
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Переписка.Очистить();
	Статус = Перечисления.СтатусыПроцессов.Открыт;
	ТекущийИсполнитель = Неопределено;
КонецПроцедуры


#Область ИнициализацияИЗаполнение

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор				  = Пользователи.ТекущийПользователь();
	Инициатор			  = Пользователи.ТекущийПользователь();
	Статус                = Перечисления.СтатусыПроцессов.Открыт;

	ДатаСоздания = НачалоДня(ТекущаяДатаСеанса());
	ХозОперация	 		  = ПредопределенноеЗначение("Справочник.ХозОперации.Задача");
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	
КонецПроцедуры



#КонецОбласти

НовыйСтатусЗаявки = Неопределено;

#КонецЕсли



///Телеграм функции с заявками
Функция СоздатьЗадачу(НакопленныеДанные,ПараметрыЗаявки = Неопределено)Экспорт
	УстановитьПривилегированныйРежим(Истина);
	КонтекстСеанса = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьКонтекстСеанса(НакопленныеДанные.ИдентификаторЧата);	
	ПараметрПользовательИнициализирован = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован");
	
	
	ДокОбъект = Документы.Задачи.СоздатьДокумент();
	ДокОбъект.Дата = ТекущаяДата();
	ДокОбъект.ДатаСоздания		 = ТекущаяДата();
	ДокОбъект.Статус	    	 = Перечисления.СтатусыПроцессов.Открыт;
	ДокОбъект.Инициатор          = КонтекстСеанса[ПараметрПользовательИнициализирован];
	
	
	ПараметрНоваяЗаявкаЗаголовок = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("НоваяЗаявкаЗаголовок");
	ПараметрНоваяЗаявкаОписание  = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("НоваяЗаявкаОписание");
	ПараметрНоваяЗаявкаОбъект    = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("НоваяЗаявкаОбъект");
	ПараметрВводТочки 		     = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ВводТочки");


	
	Если ПараметрыЗаявки = Неопределено Тогда
		//ДокОбъект.Подразделение      = КонтекстСеанса[ПараметрПользовательИнициализирован].Подразделение;
		ДокОбъект.Объект             = КонтекстСеанса[ПараметрНоваяЗаявкаОбъект];
		ДокОбъект.Подразделение      = ДокОбъект.Объект.ПодразделениеКомпании; 
		ДокОбъект.Наименование       = КонтекстСеанса[ПараметрНоваяЗаявкаЗаголовок];
		ДокОбъект.Описание           = КонтекстСеанса[ПараметрНоваяЗаявкаОписание];
		ДокОбъект.Записать();

	Иначе
		ДокОбъект.Объект             = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоGlobal_ID(Число(ПараметрыЗаявки[0]));
		ДокОбъект.Подразделение      = ДокОбъект.Объект.ПодразделениеКомпании; 
		ДокОбъект.Наименование       = ПараметрыЗаявки[1];
		ДокОбъект.Описание           = ПараметрыЗаявки[2];

		Если ПараметрыЗаявки.Количество() = 4 Тогда
			Попытка
				КодКатегори = 	Число(ПараметрыЗаявки[3]);
				Категория = Справочники.ТК_КатегорииЗаявок.НайтиКатегориюПоКоду(КодКатегори);
				Если НЕ Категория = Неопределено Тогда	
					ДокОбъект.Категория = Категория;
					ДокОбъект.КрайнийСрокУстранения = ДокОбъект.ДатаСоздания + Категория.СрокВыполнения *60*60*24
				КонецЕсли;
			Исключение
				
			КонецПопытки;
		КонецЕсли;
		
		ДокОбъект.Записать();
		
	КонецЕсли;
	
	ДействиеКонтекста = Перечисления.ТелеграмДействияСКонтекстом.ОчиститьЗначение;
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(НакопленныеДанные.ИдентификаторСообщения,ПараметрНоваяЗаявкаЗаголовок,ДействиеКонтекста,"");
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(НакопленныеДанные.ИдентификаторСообщения,ПараметрНоваяЗаявкаОписание,ДействиеКонтекста,"");
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(НакопленныеДанные.ИдентификаторСообщения,ПараметрНоваяЗаявкаОбъект,ДействиеКонтекста,"");
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(НакопленныеДанные.ИдентификаторСообщения,ПараметрВводТочки,ДействиеКонтекста,"");

	
	Возврат ДокОбъект.Ссылка.Номер;
	
КонецФункции

Функция СоздатьЗадачуПоФото(НакопленныеДанные)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстСеанса = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьКонтекстСеанса(НакопленныеДанные.ИдентификаторЧата);	
	ПараметрВводТочки 		     = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ВводТочки");
	
	
	Сообщение = НакопленныеДанные.Сообщение;
	текстВозврата ="";
	Если ТипЗнч(Сообщение) = Тип("ОбъектXDTO") Тогда
		КоллекцияСвойств = Сообщение.Свойства();
		Если КоллекцияСвойств.Получить("caption") <> Неопределено Тогда
			ТекстЗаявки  = Сообщение.caption;
		Иначе
			ТекстЗаявки ="";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекстЗаявки) Тогда
			Возврат "Укажите текст заявки!";
		ИначеЕсли  СтрНайти(ТекстЗаявки,":") >1 Тогда	
			
			Массив =	СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТекстЗаявки,":",,Истина);
			Если Массив.Количество()>=3 Тогда
				
				Попытка
					Точка = Справочники.ТорговыеПлощадки.НайтиТорговуюПлощадкуПоGlobal_ID(Число(Массив[0]));
				Исключение
					Точка = Неопределено 
				КонецПопытки;
				
				Если Точка = Неопределено Тогда
					текстВозврата = "В справочнике нет магазина с номером "+Массив[0]+Символы.ПС;
				КонецЕсли;
				Если ПустаяСтрока(Массив[1]) Тогда
					текстВозврата = текстВозврата + "Укажите название задачи!"+Символы.ПС;
				КонецЕсли;
				Если ПустаяСтрока(Массив[2]) Тогда
					текстВозврата = текстВозврата + "Укажите описание задачи!";
				КонецЕсли;
				
				Если ПустаяСтрока(текстВозврата) Тогда
					///Создаем задачу
					
					ПараметрПользовательИнициализирован = ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован");
					
					
					ДокОбъект = Документы.Задачи.СоздатьДокумент();
					ДокОбъект.Дата = ТекущаяДата();
					ДокОбъект.ДатаСоздания		 = ТекущаяДата();
					ДокОбъект.Статус	    	 = Перечисления.СтатусыПроцессов.Открыт;
					ДокОбъект.Инициатор          = КонтекстСеанса[ПараметрПользовательИнициализирован];
					
					ДокОбъект.Объект             = Точка;
					ДокОбъект.Подразделение      = ДокОбъект.Объект.ПодразделениеКомпании; 
					ДокОбъект.Наименование       = Массив[1];
					ДокОбъект.Описание           = Массив[2];
					Если Массив.Количество() = 4 Тогда
						Попытка
							КодКатегори = 	Число(Массив[3]);
							Категория = Справочники.ТК_КатегорииЗаявок.НайтиКатегориюПоКоду(КодКатегори);
							Если НЕ Категория = Неопределено Тогда	
								ДокОбъект.Категория = Категория;
								ДокОбъект.КрайнийСрокУстранения = ДокОбъект.ДатаСоздания + Категория.СрокВыполнения *60*60*24
							КонецЕсли;
						Исключение
							
						КонецПопытки;
					КонецЕсли;
										
					ДокОбъект.Записать();
					
					ДобавитьФайл(ДокОбъект.Ссылка,НакопленныеДанные);
					
					текстВозврата = "Создана заявка № "+ДокОбъект.Номер;
					
				КонецЕсли;
			Иначе	
				текстВозврата ="Ошибочное описание заявки!";
			КонецЕсли;
		Иначе
			текстВозврата ="Ошибочное описание заявки!";
		КонецЕсли;
		
	Иначе
		текстВозврата ="Ошибочное описание заявки!";
	КонецЕсли;	
	
	ДействиеКонтекста = Перечисления.ТелеграмДействияСКонтекстом.ОчиститьЗначение;
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(НакопленныеДанные.ИдентификаторСообщения,ПараметрВводТочки,ДействиеКонтекста,"");
	
	
	Возврат текстВозврата;
	
КонецФункции


Функция ДобавитьФайл(ДокСсылка,НакопленныеДанные)
	
	УстановитьПривилегированныйРежим(Истина);
	
	РасширениеБезТочки = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(НакопленныеДанные.ЗагруженныйФайл);

	Если ВРег(РасширениеБезТочки) = ВРег("jpg") Тогда
		
		
		Файл = Новый Файл(НакопленныеДанные.ЗагруженныйФайл);
		Размер = Файл.Размер()/1024/1024;
		Если Размер >2 Тогда
			Возврат  "Файл не загружен!"+ Символы.ПС + "Превышение размера файла";
		КонецЕсли;
		
		ДвоичныеДанные = Новый ДвоичныеДанные(НакопленныеДанные.ЗагруженныйФайл);
		АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		АдресВременногоХранилищаТекста = "";
		
		
		ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов = ДокСсылка;
		ПараметрыФайла.ИмяБезРасширения = Файл.ИмяБезРасширения;
		ПараметрыФайла.РасширениеБезТочки = РасширениеБезТочки;
		ПараметрыФайла.ВремяИзмененияУниверсальное = Неопределено;
		
		ФайлРезультат = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла,АдресВременногоХранилищаФайла,АдресВременногоХранилищаТекста);
		
		ДокОбъект =  ДокСсылка.ПолучитьОбъект();
		ДокОбъект.ФайлКартинки = ФайлРезультат;
		ДокОбъект.Записать();
		
		УдалитьФайлы(НакопленныеДанные.ЗагруженныйФайл);
		ТекстВозврата = "Добавлен файл к задаче: "+ДокСсылка.Номер;
		
	Иначе
		ТекстВозврата = "Файл не загружен!"+ Символы.ПС + "Возможно загружать только фотографии формата jpg";
	КонецЕсли;
	
	Возврат ТекстВозврата;
	
КонецФункции


Функция ПрикрепитьФото(НакопленныеДанные)Экспорт
	
	ТелеграмСервер.ЗагрузитьФайл(НакопленныеДанные);
	ПросмотрЗаявки = НакопленныеДанные.Контекст.Получить(ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПросмотрЗаявки"));
	НомерДокумента  = Число(ПросмотрЗаявки);
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);

	
	ТекстВозврата = ДобавитьФайл(ДокСсылка,НакопленныеДанные);
	
	Возврат ТекстВозврата;
	
КонецФункции


Функция ФайлыДокумента(НакопленныеДанные) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	//ПросмотрЗаявки = НакопленныеДанные.Контекст.Получить(ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПросмотрЗаявки"));
	НомерДокумента  = Число(НакопленныеДанные.ОтветКонтекстнойКлавиатуры);
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗадачиПрисоединенныеФайлы.Наименование КАК Наименование,
	|	ЗадачиПрисоединенныеФайлы.Расширение КАК Расширение,
	|	ДвоичныеДанныеФайлов.ДвоичныеДанныеФайла КАК ФайлХранилище
	|ИЗ
	|	Справочник.ЗадачиПрисоединенныеФайлы КАК ЗадачиПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДвоичныеДанныеФайлов КАК ДвоичныеДанныеФайлов
	|		ПО ЗадачиПрисоединенныеФайлы.Ссылка = ДвоичныеДанныеФайлов.Файл
	|ГДЕ
	|	ЗадачиПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
	|	И ЗадачиПрисоединенныеФайлы.ПометкаУдаления = ЛОЖЬ");
	Запрос.УстановитьПараметр("ВладелецФайла",ДокСсылка);
	
	МассивСообщений = Новый Массив;
	
	Рез = Запрос.Выполнить();
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		ВходнойФайлСтруктура = Новый Структура;
		ВходнойФайлСтруктура.Вставить("ВидИсточника", "ДвоичныеДанные");
		ВходнойФайлСтруктура.Вставить("Источник", Выборка.ФайлХранилище.Получить());
		ВходнойФайлСтруктура.Вставить("ИмяФайла", Выборка.Наименование);
		
		Сообщение = Новый Структура;
		Сообщение.Вставить("ВходнойФайлСтруктура", ВходнойФайлСтруктура);
		
		МассивСообщений.Добавить(Сообщение);
		
	КонецЦикла;
	
	Возврат МассивСообщений;
	
	//ОтправитьФотографию(Сообщение, НакопленныеДанные);	
	
КонецФункции


Функция ЭтоЗадачаПользователя(Знач НомерДокумента,Знач Пользователь)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задачи.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Номер = &Номер
	|	И Задачи.ТекущийИсполнитель = &ТекущийИсполнитель";
	
	Запрос.УстановитьПараметр("Номер", Число(НомерДокумента));
	Запрос.УстановитьПараметр("ТекущийИсполнитель", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	
КонецФункции

Функция КоличестовЗаявокПользователя(ИдентификаторЧата,	Статус =  Неопределено,Исполнитель = Истина)Экспорт
	
	Если Статус = Неопределено Тогда
		Статус = Перечисления.СтатусыПроцессов.Выполняется;
	КонецЕсли;
	
	
	ЗначениеКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьЗначениеПоПараметру(ИдентификаторЧата,ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован"));
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Задачи.Ссылка) КАК КоличествоЗаявок
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Статус = &Статус
	|	И Задачи."+?(Исполнитель,"ТекущийИсполнитель","Инициатор")+" = &ЗначениеКонтекста
	|	И Задачи.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ЗначениеКонтекста", ЗначениеКонтекста);
	Запрос.УстановитьПараметр("Статус", Статус);
	РезультатЗапроса = Запрос.Выполнить();
	 // Перечисления.СтатусыПроцессов.НаКонтроле
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.КоличествоЗаявок;
	
КонецФункции

Функция КоличестовАктивныхЗаявок()Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Задачи.Ссылка) КАК КоличествоЗаявок
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Статус = &Статус
	|	И Задачи.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыПроцессов.Открыт);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.КоличествоЗаявок;
	
КонецФункции


Функция ПолучитьКонтекстнуюКлавиатуруПоЗаявкамПользователя(ИдентификаторЧата = Неопределено,	Статус =  Неопределено)Экспорт
	
	Если Статус = Неопределено Тогда
		Статус = Перечисления.СтатусыПроцессов.Выполняется;
	КонецЕсли;


	//ПараметрКонтекстаСсылка = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("Пользователь инициализирован");
	 ЗначениеКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьЗначение(ИдентификаторЧата,"ПользовательИнициализирован");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задачи.Номер КАК Номер,
	|	Задачи.Дата КАК Дата,
	|	Задачи.Наименование КАК Наименование,
	|	Задачи.Объект КАК Объект,
	|	Задачи.Объект.Global_ID КАК НомерТочки
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Статус = &Статус
	|	И Задачи.ТекущийИсполнитель = &ТекущийИсполнитель
	|	И Задачи.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ТекущийИсполнитель", ЗначениеКонтекста);
	Запрос.УстановитьПараметр("Статус", Статус);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивСтрок = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивКнопокСтроки = Новый Массив;
		НомерТочки = Формат(ВыборкаДетальныеЗаписи.НомерТочки,"ЧЦ=15; ЧГ=");
		НомерЗадачи = Формат(ВыборкаДетальныеЗаписи.Номер,"ЧЦ=15; ЧГ=");
		Кнопка = Новый Структура;
		
		Кнопка.Вставить("text", "т.т. "+НомерТочки+" "+Символы.ПС+Строка(ВыборкаДетальныеЗаписи.Объект)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Наименование));
		Кнопка.Вставить("callback_data", НомерЗадачи);
		МассивКнопокСтроки.Добавить(Кнопка);
		
		МассивСтрок.Добавить(МассивКнопокСтроки);
		
	КонецЦикла;
	
	
	
	
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", МассивСтрок);
	
	Возврат Клавиатура
	
	
КонецФункции

Функция ПолучитьКонтекстнуюКлавиатуруПоЗаявкамКуратора(ИдентификаторЧата = Неопределено,	Статус =  Неопределено)Экспорт
	
	Если Статус = Неопределено Тогда
		Статус = Перечисления.СтатусыПроцессов.Выполняется;
	КонецЕсли;


	//ПараметрКонтекстаСсылка = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("Пользователь инициализирован");
	 ЗначениеКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьЗначение(ИдентификаторЧата,"ПользовательИнициализирован");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задачи.Номер КАК Номер,
	|	Задачи.Дата КАК Дата,
	|	Задачи.Наименование КАК Наименование,
	|	Задачи.Объект КАК Объект,
	|	Задачи.Объект.Global_ID КАК НомерТочки
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Статус = &Статус
	|	И Задачи.Инициатор = &Инициатор
	|	И Задачи.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Инициатор", ЗначениеКонтекста);
	Запрос.УстановитьПараметр("Статус", Статус);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивСтрок = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивКнопокСтроки = Новый Массив;
		НомерТочки = Формат(ВыборкаДетальныеЗаписи.НомерТочки,"ЧЦ=15; ЧГ=");
		НомерЗадачи = Формат(ВыборкаДетальныеЗаписи.Номер,"ЧЦ=15; ЧГ=");
		Кнопка = Новый Структура;
		
		Кнопка.Вставить("text", "т.т. "+НомерТочки+" "+Символы.ПС+Строка(ВыборкаДетальныеЗаписи.Объект)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Наименование));
		Кнопка.Вставить("callback_data", НомерЗадачи);
		МассивКнопокСтроки.Добавить(Кнопка);
		
		МассивСтрок.Добавить(МассивКнопокСтроки);
		
	КонецЦикла;
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", МассивСтрок);
	
	Возврат Клавиатура
	
	
КонецФункции




Функция ПолучитьКонтекстнуюКлавиатуруПоОткрытымЗаявкам()Экспорт
	

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 50 
	|	Задачи.Номер КАК Номер,
	|	Задачи.Дата КАК Дата,
	|	Задачи.Наименование КАК Наименование,
	|	Задачи.Объект КАК Объект,
	|	Задачи.Объект.Global_ID КАК НомерТочки
	|ИЗ
	|	Документ.Задачи КАК Задачи
	|ГДЕ
	|	Задачи.Статус = &Статус
	|	И Задачи.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер";
	
	//Запрос.УстановитьПараметр("Ответственный", Пользователь);
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыПроцессов.Открыт);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивСтрок = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивКнопокСтроки = Новый Массив;
		
		НомерТочки = Формат(ВыборкаДетальныеЗаписи.НомерТочки,"ЧЦ=15; ЧГ=");
		НомерЗадачи = Формат(ВыборкаДетальныеЗаписи.Номер,"ЧЦ=15; ЧГ=");
		
		Кнопка = Новый Структура;
		Кнопка.Вставить("text", Формат(ВыборкаДетальныеЗаписи.Номер,"ЧЦ=15; ЧГ=")+":"+НомерТочки+"-"+Символы.ПС+Строка(ВыборкаДетальныеЗаписи.Объект)+" "+СокрЛП(ВыборкаДетальныеЗаписи.Наименование));
		Кнопка.Вставить("callback_data", НомерЗадачи);
		МассивКнопокСтроки.Добавить(Кнопка);
		
		МассивСтрок.Добавить(МассивКнопокСтроки);
		
	КонецЦикла;
	
	
	
	
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", МассивСтрок);

	Возврат Клавиатура
	
	
КонецФункции

Функция ПолучитьОписаниеЗаявки(НомерЗаявки)Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	Задачи.Ссылка КАК Ссылка,
	              |	Задачи.Дата КАК Дата,
	              |	Задачи.Объект КАК Объект,
	              |	ВЫРАЗИТЬ(Задачи.Описание КАК СТРОКА(250)) КАК Описание,
	              |	Задачи.КрайнийСрокУстранения КАК КрайнийСрокУстранения,
	              |	Задачи.Инициатор КАК Инициатор
	              |ИЗ
	              |	Документ.Задачи КАК Задачи
	              |ГДЕ
	              |	Задачи.Номер = &Номер";
	Запрос.УстановитьПараметр("Номер",Число(НомерЗаявки));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СообщениеОтвет ="Заявка №"+НомерЗаявки +" не найдена!";
	Иначе
		
		ВидТел = УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("ТелефонПользователя");

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СообщениеОтвет = "Заявка №"+НомерЗаявки+" от "+Формат(Выборка.Дата,"ДФ=yyyy-MM-dd")+Символы.ПС+
		"Объект " + Строка(Выборка.Объект)+Символы.ПС+
		"Инициатор "+ Строка(Выборка.Инициатор)+" тел."+УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Выборка.Инициатор,ВидТел)+Символы.ПС+
		"Описание "+ СокрЛП(Выборка.Описание)+Символы.ПС+
		"Исполнить до "+Формат(Выборка.КрайнийСрокУстранения,"ДФ=yyyy-MM-dd");
		
		ТекстКомментарий = "";
		
		ТаблицаПереписка = 	Выборка.Ссылка.Переписка;
		Для Каждого ТекущаяСтрока Из ТаблицаПереписка Цикл 
			ТекстКомментарий  = ТекстКомментарий + Строка(ТекущаяСтрока.Пользователь) + "-"+ТекущаяСтрока.Текст + Символы.ПС;	
		КонецЦикла;
		Если Не ПустаяСтрока(ТекстКомментарий) Тогда
			СообщениеОтвет = СообщениеОтвет + Символы.ПС+ТекстКомментарий;
		КонецЕсли;
	КонецЕсли;
	
	Возврат   СообщениеОтвет;
КонецФункции

Функция УстановитьОтветственногоИзКонтекста(ИдентификаторЧата,ДатаВыполнения = Неопределено)Экспорт
	СтруктураКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьКонтекстСеанса(ИдентификаторЧата);
	
	ПолученСписокАктивныхЗаявок = СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПолученСписокАктивныхЗаявок")];
	ПросмотрЗаявки 				= СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПросмотрЗаявки")];
	ПользовательИнициализирован = СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПользовательИнициализирован")];
	
	Если ПользовательИнициализирован = Неопределено Тогда
		Возврат "Необходима авторизация, перейдите на statr";
	КонецЕсли;

	Если ПолученСписокАктивныхЗаявок = Неопределено Тогда
		Возврат "Необходимо получить список заявок";
	КонецЕсли;
	Если  ПросмотрЗаявки = Неопределено Тогда
		Возврат "Необходимо указать заявку";
	КонецЕсли;
	
	НомерДокумента  = Число(ПросмотрЗаявки);
	
	
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	ДокОбъект.ТекущийИсполнитель =  ПользовательИнициализирован;
	Если ТипЗнч(ДатаВыполнения) = Тип("Дата") Тогда
		ДокОбъект.КрайнийСрокУстранения = ДатаВыполнения;
	КонецЕсли;
	
	ДокОбъект.Записать();
	
	УстановитьПривилегированныйРежим(Истина);
	пПользовательВводитДатуВыполненияЗаявки = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПользовательВводитДатуВыполненияЗаявки");
	пПросмотрЗаявки = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПросмотрЗаявки");
	пПолученСписокАктивныхЗаявок = ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПолученСписокАктивныхЗаявок");

	ДействиеКонтекста = Перечисления.ТелеграмДействияСКонтекстом.ОчиститьЗначение;
	
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(ИдентификаторЧата,пПользовательВводитДатуВыполненияЗаявки,ДействиеКонтекста,"");
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(ИдентификаторЧата,пПросмотрЗаявки,ДействиеКонтекста,"");
	РегистрыСведений.ТелеграмКонтекстСеансов.ИзменитьПараметрКонтекстаСеанса(ИдентификаторЧата,пПолученСписокАктивныхЗаявок,ДействиеКонтекста,"");

	
	
	Возврат "Заявка № "+ПросмотрЗаявки+" взята";

КонецФункции

Функция ПодготовитьОтчетПоЗаявкамИнициатора(ИдентификаторЧата) Экспорт
	
	СтруктураКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ПолучитьКонтекстСеанса(ИдентификаторЧата);
	ПользовательИнициализирован = СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ПользовательИнициализирован")];
	
	
	Если ПользовательИнициализирован = Неопределено Тогда
		Возврат "Необходима авторизация, перейдите на statr";
	КонецЕсли;
	
	ОтчетПоТочке =  СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("ЗапросОтчетаПоТочке")];
	Если ОтчетПоТочке = Неопределено Тогда
		ОтчетПоТочке = Ложь;
	КонецЕсли;
	
	ОбъектНайден =  СтруктураКонтекста[ПланыВидовХарактеристик.ТелеграмПараметрыКонтекста.ПолучитьПараметрПоИмени("НайденОбъект")];
	
	
	
	тСообщения ="";
	СформироватьОтчетОтправитьПользователю(ПользовательИнициализирован,ОтчетПоТочке,ОбъектНайден,тСообщения);
	Если ПустаяСтрока(тСообщения) Тогда
		Возврат "Отчет отправлен";
	Иначе
		тОшибки ="Не возможно отправить отчет!"+Символы.ПС+тСообщения;
		Возврат тОшибки;
	КонецЕсли;
	
КонецФункции



Функция ВыполнитьЗаявкуПользователя(НакопленныеДанные)Экспорт
	
	ПросмотрЗаявки = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	
	НомерДокумента = Число(ПросмотрЗаявки);
	
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	
	Если НакопленныеДанные.Свойство("Сообщение") И НЕ НакопленныеДанные.Сообщение.Свойства().Получить("location") = Неопределено Тогда 
		ДокОбъект.Широта  = НакопленныеДанные.Сообщение.location.latitude;
		ДокОбъект.Долгота = НакопленныеДанные.Сообщение.location.longitude;
	КонецЕсли;
	ДокОбъект.НовыйСтатусЗаявки = Перечисления.СтатусыПроцессов.НаКонтроле;
	ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	Возврат "Заявка № "+ПросмотрЗаявки+" выполнена";
	
	
КонецФункции


Функция ПодтвердитьЗаявкуПользователя(НакопленныеДанные)Экспорт
	
	ПросмотрЗаявки = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	
	НомерДокумента = Число(ПросмотрЗаявки);
	
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат "Заявка № "+ПросмотрЗаявки+" выполнена";
	
	
КонецФункции

Функция ОтправитьНаДоработку(НакопленныеДанные)Экспорт
	
	ПросмотрЗаявки = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	
	НомерДокумента = Число(ПросмотрЗаявки);
	
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	ДокОбъект.НовыйСтатусЗаявки = Перечисления.СтатусыПроцессов.Выполняется;
	ДокОбъект.Записать();
	
	Возврат "Заявка № "+ПросмотрЗаявки+" отправлена на доработку";
	
	
КонецФункции

Функция ДобавитьКомментарий(НакопленныеДанные)Экспорт
	
	ПросмотрЗаявки = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	
	НомерДокумента = Число(ПросмотрЗаявки);
	
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(НомерДокумента);
	ДокОбъект = ДокСсылка.ПолучитьОбъект();
	нСтр = ДокОбъект.Переписка.Добавить();
	нСтр.Дата = ТекущаяДатаСеанса();
	нСтр.Пользователь = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПользовательИнициализирован")];
	нСтр.Текст = НакопленныеДанные.ТекстСообщения;
	ДокОбъект.Записать();
	
	Возврат "Комментарий добавлен";
	
	
КонецФункции

Процедура СформироватьОтчетОтправитьПользователю(Пользователь,ЭтоОтчетПоТочке,ОбъектНайден,ТекстОшибок)Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ВидКонтакта = УправлениеКонтактнойИнформацией.ВидКонтактнойИнформацииПоИмени("EmailПользователя");
	
	Кому = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Пользователь, ВидКонтакта, ТекущаяДатаСеанса(), Истина);
	Если Не ЗначениеЗаполнено(Кому) Тогда
		ТекстОшибок = "Нет адерса получателя";
		Возврат;
	КонецЕсли;
	
	
	
	СхемаКомпоновкиДанных = Документы.Задачи.ПолучитьМакет("СтатусыЗаявок");
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию); 
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериода", НачалоДня(ТекущаяДата())-60*60*24*180);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КонецПериода", КонецДня(ТекущаяДата()));	
	Если НЕ ЭтоОтчетПоТочке Тогда 
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Инициатор", Пользователь);
	Иначе
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОбъектЗадачи", ОбъектНайден);
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Расшифровка       = Новый ДанныеРасшифровкиКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроек.Настройки,Расшифровка);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,Расшифровка);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	
	
	Поток = Новый ПотокВПамяти();
	ДокументРезультат.Записать(Поток,ТипФайлаТабличногоДокумента.XLSX);
	ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
	СтруктураВложения = Новый Структура;
	СтруктураВложения.Вставить("Представление", "Отчет по статусу размещенных заявок.XLSX");
	СтруктураВложения.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилище(ДвоичныеДанные));
	СтруктураВложения.Вставить("Кодировка","");
	СтруктураВложения.Вставить("Идентификатор","");

	МассивВложений = Новый Массив;
	МассивВложений.Добавить(СтруктураВложения);
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Кому", Кому);
	ПараметрыПисьма.Вставить("Тема", "Статусы заявок");
	ПараметрыПисьма.Вставить("Тело", "Статусы заявок");
	ПараметрыПисьма.Вставить("Вложения",МассивВложений);
	
	Попытка
		Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),ПараметрыПисьма);
		РеОтпр = РаботаСПочтовымиСообщениями.ОтправитьПисьмо(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(),Письмо);	
	Исключение
		ТекстОшибок = ОписаниеОшибки();
	КонецПопытки;

	
КонецПроцедуры

Функция ИдентификаторЧатаИнициатораЗаявки(НакопленныеДанные)Экспорт
	
	ПросмотрЗаявки = НакопленныеДанные.КонтекстСеанса[ТелеграмСерверПовтИсп.ПараметрКонтекстаПоИмени("ПросмотрЗаявки")];
	ДокСсылка =  Документы.Задачи.НайтиПоНомеру(Число(ПросмотрЗаявки));
	ЗначениеКонтекста = РегистрыСведений.ТелеграмКонтекстСеансов.ИД_ЧатаПользователя(ДокСсылка.Инициатор);
	

	Возврат ЗначениеКонтекста	
КонецФункции


#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Подразделение)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти



#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	НомерRB = Неопределено;
	ТипИнвентаризации = Неопределено;
	ЗадачаТСД = Неопределено;
	Товары.Очистить();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	
	Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
		ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
	КонецЕсли;

	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И Не ЭтоНовый() И Проведен Тогда 
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	Инвентаризация.Дата КАК Дата,
		                      |	Инвентаризация.СкладКомпании КАК СкладКомпании
		                      |ИЗ
		                      |	Документ.Инвентаризация КАК Инвентаризация
		                      |ГДЕ
		                      |	Инвентаризация.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();		
		Выборка.Следующий();
		ДополнительныеСвойства.Вставить("СтараяДатаДокумента",  Выборка.Дата);
		ДополнительныеСвойства.Вставить("СтарыйСкладДокумента", Выборка.СкладКомпании);
	Иначе		
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Ложь);
		ДополнительныеСвойства.Вставить("ПроверкаИзмененияСкладаПоследовательности", Ложь);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		текМомент = Новый МоментВремени(Дата, Ссылка);
		
		ПараметрыЗаполненияОстатков = Новый Структура;
		ПараметрыЗаполненияОстатков.Вставить("Момент", текМомент);
		ПараметрыЗаполненияОстатков.Вставить("СкладКомпании", СкладКомпании);
		ПараметрыЗаполненияОстатков.Вставить("Команда", "ПересчитатьКоличествоКнижн");
		
		Документы.Инвентаризация.ЗаполнитьТоварыПоОстаткамНаСкладе(Товары, ПараметрыЗаполненияОстатков);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДатаДокументаСдвинутаВперед") Тогда    
		ДополнительныеСвойства.Вставить("ДатаДокументаСдвинутаВперед", Дата > ДополнительныеСвойства.СтараяДатаДокумента);
	КонецЕсли;

	Если НЕ ДополнительныеСвойства.Свойство("ПроверкаИзмененияСкладаПоследовательности") Тогда  
	   	ДополнительныеСвойства.Вставить("СкладКомпанииИзменен", СкладКомпании <> ДополнительныеСвойства.СтарыйСкладДокумента);
	КонецЕсли;

КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	Если ОбщегоНазначенияТК.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект,ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры
		

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();

	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.Инвентаризация.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	Документы.Инвентаризация.ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСервер.ОтразитьОстаткиТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);	
	ЗапасыСервер.ОтразаитьПартииТоваровКомпании(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразаитьПриходованиеТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);	

	УправлениеПоследовательностями.ЗарегистрироватьДокументВПоследовательностях(ДополнительныеСвойства,ПринадлежностьПоследовательностям,Отказ);

	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ИнвентаризацияПроведениеНаСервере",ВремяНачала,,Строка(Ссылка));

КонецПроцедуры

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьДокументНаОснованииПоступленияТоваров(Знач ПоступлениеОснование)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоступлениеТоваров.Автор КАК Автор,
	               |	ЗНАЧЕНИЕ(Справочник.Хозоперации.РеализацияТоваров) КАК ХозОперация,
	               |	ПоступлениеТоваров.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	ПоступлениеТоваров.Организация КАК Организация,
	               |	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
	               |	ПоступлениеТоваров.Контрагент КАК Контрагент,
	               |	ПоступлениеТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	               |	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
	               |	ПоступлениеТоваров.ТипЦен КАК ТипЦен,
	               |	ПоступлениеТоваров.СуммаДокумента КАК СуммаДокумента,
	               |	ПоступлениеТоваров.Ссылка КАК ДокументОснование
	               |ИЗ
	               |	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	               |ГДЕ
	               |	ПоступлениеТоваров.Ссылка = &ДокументОснование
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	               |	ПоступлениеТоваровТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ПоступлениеТоваровТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ПоступлениеТоваровТовары.Количество КАК Количество,
	               |	ПоступлениеТоваровТовары.Коэффициент КАК Коэффициент,
	               |	ПоступлениеТоваровТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	ПоступлениеТоваровТовары.Цена КАК Цена,
	               |	ПоступлениеТоваровТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	               |	ПоступлениеТоваровТовары.Сумма КАК Сумма,
	               |	ПоступлениеТоваровТовары.СуммаВсего КАК СуммаВсего,
	               |	ПоступлениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ПоступлениеТоваровТовары.СуммаНДС КАК СуммаНДС,
	               |	ПоступлениеТоваровТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	ПоступлениеТоваровТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ПоступлениеТоваровТовары.СуммаСкидки КАК СуммаСкидки
	               |ИЗ
	               |	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	               |ГДЕ
	               |	ПоступлениеТоваровТовары.Ссылка = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ПоступлениеОснование);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = МассивРезультатов[0].Выбрать();
	ТаблицаТоваров = МассивРезультатов[1].Выгрузить();
	
	Если ВыборкаШапка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);
	КонецЕсли;
	
	Если ТаблицаТоваров.Количество() Тогда 
		Товары.Загрузить(ТаблицаТоваров);
	КонецЕсли;
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор 				  = Пользователи.ТекущийПользователь();
	ХозОперация			  = ПредопределенноеЗначение("Справочник.ХозОперации.ИнвентаризацияТоваров");

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("Товары") Тогда
			  Товары.Загрузить(ДанныеЗаполнения.Товары);
		КонецЕсли;
		
	Иначе
		Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
		ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
		СкладКомпании 		  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);	
		ТорговаяПлощадка  	  = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(СкладКомпании);	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#КонецЕсли

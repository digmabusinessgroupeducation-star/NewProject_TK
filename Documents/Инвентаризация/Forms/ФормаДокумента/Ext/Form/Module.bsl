&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", Элементы.ГруппаДополнительныеРеквизиты.Имя);
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("Инвентаризация"));
	
	Если Параметры.Ключ.Пустая() Тогда 
		ПолучитьНастройкиКомпоновщикаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриЧтенииСозданииНаСервере();
	
	//Отборы
	СхемаКомпоновкиДанных = ПолучитьОбщийМакет("СхемаКомпоновкиОтбораНоменклатуры");	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));	
	
	НастройкиОтборов = ТекущийОбъект.ХранилищеОтборов.Получить();
	ЗагрузитьНастройкиПоУмолчанию = НастройкиОтборов = Неопределено;
	
	УстановитьСостояниеДокумента();
	
	Если Не ЗагрузитьНастройкиПоУмолчанию Тогда 
		Попытка
			КомпоновщикНастроек.ЗагрузитьНастройки(ТекущийОбъект.ХранилищеОтборов.Получить());
		Исключение
			ЗагрузитьНастройкиПоУмолчанию = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если ЗагрузитьНастройкиПоУмолчанию Тогда 
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
	    ОбновитьЭлементыДополнительныхРеквизитов();
	    УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	тзТовары = Объект.Товары.Выгрузить();
	тзТовары.Колонки.Добавить("Дубли");
	тзТовары.ЗаполнитьЗначения(1,"Дубли");
	тзТовары.Свернуть("Номенклатура,ХарактеристикаНоменклатуры","Дубли");
	
	
	для Каждого стТзТовары Из тзТовары Цикл 
		Если стТзТовары.Дубли > 1 Тогда
			Отказ = Истина;
			МассивСтрок = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура,ХарактеристикаНоменклатуры",стТзТовары.Номенклатура,стТзТовары.ХарактеристикаНоменклатуры));
			Для Каждого нСтрока Из МассивСтрок Цикл 
				СтрокаСообщения = "ВНИМАНИЕ одинаковые строки "+нСтрока.НомерСтроки+" товар "+ Строка(нСтрока.Номенклатура) + " " +?(нСтрока.ХарактеристикиИспользуются,Строка(нСтрока.ХарактеристикаНоменклатуры),"");
				ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения,,ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Товары", нСтрока.НомерСтроки, "Номенклатура"),,Истина);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЭтотОбъект.Модифицированность Тогда
		СортироватьТЧ_НаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ТекущийОбъект.ХранилищеОтборов = Новый ХранилищеЗначения(КомпоновщикНастроек.ПолучитьНастройки(), Новый СжатиеДанных(9));
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	// СтандартныеПодсистемы.Свойства
	Если ЭтотОбъект.ПараметрыСвойств.Свойство(ТекущаяСтраница.Имя)
        И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
        СвойстваВыполнитьОтложеннуюИнициализацию();
        УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
    КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	УстановитьСостояниеДокумента();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеКомпанииПриИзмененииНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиОтборПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Объект.Комментарий) Тогда 
		Возврат;
	КонецЕсли;
	
	ПредставлениеОтбора = КомпоновщикНастроек.Настройки.Отбор;
	
	Если Не ПустаяСтрока(ПредставлениеОтбора) Тогда 
		Объект.Комментарий = ПредставлениеОтбора;
	Иначе
		Объект.Комментарий = "";
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	ТекНоменклатура = ТекущаяСтрока.Номенклатура;
	
	СтруктураДействий = Новый Структура;
	
	Если ТекНоменклатура.Пустая() Тогда 
		ТекущаяСтрока.ХарактеристикаНоменклатуры = Неопределено;
		ТекущаяСтрока.ЕдиницаИзмерения = Неопределено;
		ТекущаяСтрока.Коэффициент = 0;
		ТекущаяСтрока.КоличествоКнижн = 0;
	Иначе
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
		ДобавитьДействиеЗаполненияКнижногоКоличества(СтруктураДействий);	
	КонецЕсли;
	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	ДобавитьДействиеЗаполненияКнижногоКоличества(СтруктураДействий);	
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий);	
		
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;

	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоФактПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;

	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СброситьОтборы(Команда)
	ПолучитьНастройкиКомпоновщикаПоУмолчанию();	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФакт(Команда)	
	
	СтруктураДействий = Новый Структура;
	ДобавитьДействиеПересчетаРасхождений(СтруктураДействий);		
	
	Для Каждого Стр Из Объект.Товары Цикл 
		Если Стр.КоличествоФакт <> 0 Тогда 
			Стр.КоличествоФакт = 0;
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(Стр, СтруктураДействий, КэшированныеЗначения);	
		КонецЕсли;
	КонецЦикла;		
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
    УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация, Подразделение)
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Подразделение, Объект.ХозОперация, Дата);
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("Договор",ДоговорПоУмолчанию);
	СтруктураДанные.Вставить("ВалютаДокумента",ДоговорПоУмолчанию.ВалютаВзаиморасчетов);
	СтруктураДанные.Вставить("ВалютаРасчетовКурсКратность",	РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаВзаиморасчетов)));

	Возврат СтруктураДанные;
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, Подразделение, ВидОперации, НаДату)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, Подразделение, СписокВидовДоговоров, , НаДату);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, Договор)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаДокумента",
		Договор.ВалютаВзаиморасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов))
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()
		
	
	
КонецПроцедуры // ОчиститьОснованиеПриИзменениеКонтрагентаДоговора()

&НаКлиенте
Процедура ОбновитьПодвалФормы()
	
	СуммаВсегоСНДС = Объект.Товары.Итог("СуммаВсего");
	СуммаНДС       = Объект.Товары.Итог("СуммаНДС");
	
КонецПроцедуры // ОбновитьПодвалФормы()

&НаКлиенте
Процедура КонтрагентПриИзмененииФрагмент(ДоговорПередИзменением, СтруктураДанные)
	
	Объект.ВалютаДокумента       = СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговораФрагмент(ДоговорПередИзменением)
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ДоговорВзаиморасчетов);
	
	ВалютаРасчетовПередИзменением = ВалютаДокумента;
	ВалютаДокумента = СтруктураДанные.ВалютаДокумента;
	Объект.ВалютаДокумента =  СтруктураДанные.ВалютаДокумента;
	Если ЗначениеЗаполнено(Объект.ДоговорВзаиморасчетов) Тогда 
		Объект.КурсДокумента       = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс=0,1,СтруктураДанные.ВалютаРасчетовКурсКратность.Курс)/?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	КонецЕсли;
	
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеПересчетаРасхождений(СтруктураДействий)	
	
	МассивПолейРасхождений = Новый Массив;
	
	СтруктураПолейРасхождений = Новый Структура;
	СтруктураПолейРасхождений.Вставить("Уменьшаемое", 	"КоличествоФакт");
	СтруктураПолейРасхождений.Вставить("Вычетаемое", 	"КоличествоКнижн");
	СтруктураПолейРасхождений.Вставить("Разница", 		"Количество");
	
	МассивПолейРасхождений.Добавить(СтруктураПолейРасхождений);		
	
	СтруктураДействий.Вставить("ПересчитатьРазность", МассивПолейРасхождений);	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДействиеЗаполненияКнижногоКоличества(СтруктураДействий)
	
	СтруктураПараметровОстатков = Новый Структура;
	СтруктураПараметровОстатков.Вставить("Дата", Новый МоментВремени(Объект.Дата, Объект.Ссылка));
	СтруктураПараметровОстатков.Вставить("СкладКомпании", Объект.СкладКомпании);	
	СтруктураПараметровОстатков.Вставить("ИмяПоляОстатка", "КоличествоКнижн");
	
	СтруктураДействий.Вставить("ЗаполнитьОстаток", СтруктураПараметровОстатков);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаСкладеНаКлиенте(Команда)
	
	Если Объект.СкладКомпании.Пустая() Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Поле = "СкладКомпании";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Склад""'; uk = 'Не заповнене поле ""Склад""'");
		
		Сообщение.Сообщить();
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Команда", Команда.Имя);
		
		ПроцедураПродолжение = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамНаСкладеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		Если Объект.Товары.Количество() > 0 Тогда 
			ТекстВопроса = НСтр("ru = 'Таблица ""Товары"" будет очищена. Продолжить?'; uk = 'Таблиця ""Товари"" буде очищена. Продовжити?'");
			ПоказатьВопрос(ПроцедураПродолжение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ВыполнитьОбработкуОповещения(ПроцедураПродолжение, КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКоличествоКнижн(Команда)
	
	Если Объект.СкладКомпании.Пустая() Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Поле = "СкладКомпании";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Текст = НСтр("ru = 'Не заполнено поле ""Склад""'; uk = 'Не заповнене поле ""Склад""'");
		
		Сообщение.Сообщить();
	Иначе
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Команда", Команда.Имя);
		
		ПроцедураПродолжение = Новый ОписаниеОповещения("ПересчитатьКоличествоКнижнЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		Если Объект.Товары.Количество() > 0 Тогда 
			ТекстВопроса = НСтр("ru = 'Кол-во книжн. будет перезаполнено. Продолжить?'; uk = 'Кількість кніжн. буде заповнити. Продовжити?'");
			ПоказатьВопрос(ПроцедураПродолжение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ВыполнитьОбработкуОповещения(ПроцедураПродолжение, КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаСкладеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ЗаполнитьПоОстаткамНаСкладеНаСервере(ДополнительныеПараметры);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамНаСкладеНаСервере(ДополнительныеПараметры)
	
	тчТовары = Объект.Товары;
	тчТовары.Очистить();
	
	ДополнительныеПараметры.Вставить("тчТовары", тчТовары);
	ДополнительныеПараметры.Вставить("СкладКомпании", Объект.СкладКомпании);	
	ДополнительныеПараметры.Вставить("НачалоИнвентаризации", Объект.НачалоИнвентаризации);
	
	Если Не ПустаяСтрока(Строка(КомпоновщикНастроек.Настройки.Отбор)) И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Команда", "ПересчитатьКоличествоКнижн") <> "ПересчитатьКоличествоКнижн" Тогда 
		ДополнительныеПараметры.Вставить("КомпоновщикНастроек", КомпоновщикНастроек);
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда 
		Если ЗначениеЗаполнено(Объект.Дата) Тогда 
			ДополнительныеПараметры.Вставить("Момент", КонецДня(Объект.Дата));
		Иначе
			ДополнительныеПараметры.Вставить("Момент", КонецДня(ТекущаяДата()));
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить("Момент", Объект.Ссылка.МоментВремени());
	КонецЕсли;
	
	Документы.Инвентаризация.ЗаполнитьТоварыПоОстаткамНаСкладе(тчТовары, ДополнительныеПараметры);
	
КонецПроцедуры


&НаКлиенте
Процедура ПересчитатьКоличествоКнижнЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ПересчитатьКоличествоКнижнНаСервере(ДополнительныеПараметры);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПересчитатьКоличествоКнижнНаСервере(ДополнительныеПараметры)
	тчТовары = Объект.Товары;
	
	//ДополнительныеПараметры.Вставить("тчТовары", тчТовары);
	ДополнительныеПараметры.Вставить("СкладКомпании", Объект.СкладКомпании);	
	ДополнительныеПараметры.Вставить("Команда", "ПересчитатьКоличествоКнижн");
	Если Объект.Ссылка.Пустая() Тогда 
		Если ЗначениеЗаполнено(Объект.Дата) Тогда 
			ДополнительныеПараметры.Вставить("Момент", КонецДня(Объект.Дата));
		Иначе
			ДополнительныеПараметры.Вставить("Момент", КонецДня(ТекущаяДата()));
		КонецЕсли;
	Иначе
		ДополнительныеПараметры.Вставить("Момент", Объект.Ссылка.МоментВремени());
	КонецЕсли;
	
	Документы.Инвентаризация.ЗаполнитьТоварыПоОстаткамНаСкладе(тчТовары, ДополнительныеПараметры);

КонецПроцедуры

&НаСервере
Процедура СортироватьТЧ_НаСервере()
	ОбработкаТабличнойЧастиСервер.СортироватьТЧ(Объект.Товары,"Инвентаризация");
КонецПроцедуры


&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры




#КонецОбласти


#Область РаботаСБуферомОбмена

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));

//	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакАртикул",
//											Новый Структура("Номенклатура", "Артикул"));
//	
												
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборыНоменклатуры()
	
	//НастройкиОтборов = Объект.отбор 
	ЗаполнитьОтборНоменклатурыПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборНоменклатурыПоУмолчанию()
	

КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройкиКомпоновщикаПоУмолчанию()
	
	СхемаКомпоновкиДанных = ПолучитьОбщийМакет("СхемаКомпоновкиОтбораНоменклатуры");	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор)));	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);			

КонецПроцедуры

&НаСервере
Процедура СоздатьПересортицуНаСервере()
	Док = Документы.ПересортицаТоваров.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(Док, Объект);
	Док.УстановитьНовыйНомер();
	Док.Дата = Объект.Дата -1;
	Док.ХозОперация = Справочники.ХозОперации.ПересортицаТоваров;
	Док.ВалютаДокумента = Справочники.Валюты.НайтиПоКоду("980");
	Док.РегламентированныйУчет = Истина;
	
	Номенклатура = Справочники.Номенклатура.НайтиПоКоду("D0009000");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияТовары.Номенклатура КАК НоменклатураРасход,
		|	ИнвентаризацияТовары.Номенклатура КАК НоменклатураПриход,
		|	ИнвентаризацияТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыПриход,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмеренияРасход,
		|	ИнвентаризацияТовары.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПриход,
		|	ИнвентаризацияТовары.Коэффициент КАК КоэффициентРасход,
		|	ИнвентаризацияТовары.Коэффициент КАК КоэффициентПриход,
		|	ИнвентаризацияТовары.КоличествоФакт КАК КоличествоРасход,
		|	ИнвентаризацияТовары.КоличествоФакт КАК КоличествоПриход,
		|	ИнвентаризацияТовары.Количество КАК Количество,
		|	ИнвентаризацияТовары.КоличествоФакт * ИнвентаризацияТовары.Коэффициент КАК КоличествоБазовоеРасход,
		|	ИнвентаризацияТовары.КоличествоФакт * ИнвентаризацияТовары.Коэффициент КАК КоличествоБазовоеПриход
		|ИЗ
		|	Документ.Инвентаризация.Товары КАК ИнвентаризацияТовары
		|ГДЕ
		|	ИнвентаризацияТовары.Ссылка.Ссылка = &Ссылка
		|	И ИнвентаризацияТовары.Номенклатура В ИЕРАРХИИ(&Номенклатура)
		|	И ИнвентаризацияТовары.Количество > 0
		|	И ИнвентаризацияТовары.ХарактеристикаНоменклатуры <> &ХарактеристикаНоменклатуры
		|
		|УПОРЯДОЧИТЬ ПО
		|	НоменклатураПриход";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Документ не создан!!! Нет данных для заполнения документа!!!");
	Иначе
		Док.Товары.Загрузить(РезультатЗапроса);
		Попытка
			Док.Записать(РежимЗаписиДокумента.Проведение);
			ОбщегоНазначения.СообщитьПользователю("Документ проведен "+Док.Ссылка);
		Исключение
			Док.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначения.СообщитьПользователю("Документ записан "+Док.Ссылка);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПересортицу(Команда)
	СоздатьПересортицуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТорговаяПлощадкаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ТорговаяПлощадка) Тогда
		Результат = РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(ТекущаяДата(), Новый Структура("ТорговаяПлощадка", Объект.ТорговаяПлощадка));
		Если НЕ Результат.Количество()=0 Тогда
			Объект.Организация = Результат[0].Организация;
		КонецЕсли;
		
		Объект.СкладКомпании = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Объект.ТорговаяПлощадка);		
		
		Если ЗначениеЗаполнено(Объект.СкладКомпании) Тогда 
			Если Объект.СкладКомпании.Розничный Тогда 
				Объект.ТипЦенИтогов = Справочники.СкладыКомпании.ТипЦенРозничный(Объект.СкладКомпании);	
			Иначе
				Объект.ТипЦенИтогов = Справочники.СкладыКомпании.ТипЦенСебестоимости(Объект.СкладКомпании);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПриИзменении(Элемент)
	ТорговаяПлощадкаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СкладКомпанииПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Объект.ТорговаяПлощадка) Тогда
		Объект.ТорговаяПлощадка = Объект.СкладКомпании.ТорговаяПлощадка;
		Результат = РегистрыСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних(ТекущаяДата(), Новый Структура("ТорговаяПлощадка", Объект.ТорговаяПлощадка));
		Если НЕ Результат.Количество()=0 Тогда
			Объект.Организация = Результат[0].Организация;
		КонецЕсли;
		Объект.ТипЦенИтогов = Объект.СкладКомпании.ТипЦенСебестоимости;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	СкладКомпанииПриИзмененииНаСервере();
КонецПроцедуры


#КонецОбласти
 


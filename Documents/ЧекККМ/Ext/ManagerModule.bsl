#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДокТовары.НомерСтроки КАК НомерСтроки,
	               |	ДокТовары.Номенклатура КАК Номенклатура,
	               |	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ДокТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ДокТовары.Коэффициент КАК Коэффициент,
	               |	ДокТовары.КоличествоБазовое КАК Количество,
	               |	ДокТовары.Цена КАК Цена,
	               |	ДокТовары.Сумма КАК Сумма,
	               |	ДокТовары.СтавкаНДС КАК СтавкаНДС,
	               |	ДокТовары.СуммаНДС КАК СуммаНДС,
	               |	ДокТовары.СуммаАкцизногоНалога КАК СуммаАкцизногоНалога,
	               |	ДокТовары.СуммаВсего КАК СуммаВсего,
	               |	ДокТовары.СуммаСкидки КАК СуммаСкидки,
	               |	ДокТовары.ПроцентСкидки КАК ПроцентСкидки,
	               |	ДокТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	ДокТовары.Ссылка.СкладКомпании КАК СкладКомпании
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.ЧекККМ.Товары КАК ДокТовары
	               |ГДЕ
	               |	ДокТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДенежныеСредстваВКассахККМ(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                            КАК Ссылка,
	|	ДанныеДокумента.Организация                       КАК Организация,
	|	ДанныеДокумента.Дата                              КАК Период,
	|	ДанныеДокумента.ВалютаДокумента                   КАК Валюта,
	|	ДанныеДокумента.ТорговаяПлощадка                  КАК ТорговаяПлощадка,
	|	ДанныеДокумента.ПодразделениеКомпании             КАК ПодразделениеКомпании,
	|	ДанныеДокумента.СкладКомпании                     КАК СкладКомпании
//	|	ДанныеДокумента.ЦенаВключаетНДС                   КАК ЦенаВключаетНДС,
	|ИЗ
	|	Документ.ЧекККМ КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Ссылка",                                   ДокументСсылка);
	
	Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	Запрос.УстановитьПараметр("НачалоМесяцаПериода",                      НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("Валюта",                                   Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Организация",                              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",                    Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",                         Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",                            Реквизиты.СкладКомпании);
	
	
КонецПроцедуры




Функция ТекстЗапросаТаблицаОстаткиТоваровКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиТоваровКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ВЫБОР
	              |		КОГДА ВтТаблицаТовары.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЧекНаВозврат)
	              |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	              |		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	              |	КОНЕЦ КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
	              |	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	              |	СУММА(ВтТаблицаТовары.Количество) КАК Количество,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Справочник.Контрагенты.ЧастноеЛицо) КАК Контрагент
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВтТаблицаТовары.ХозОперация,
	              |	ВтТаблицаТовары.Номенклатура,
	              |	ВтТаблицаТовары.СкладКомпании,
	              |	ВтТаблицаТовары.ХарактеристикаНоменклатуры"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваВКассахККМ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваВКассахККМ";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ЧекККМОплата.Ссылка.ХозОперация КАК ХозОперация,
	              |	ЧекККМОплата.Ссылка.Дата КАК Период,
	              |	ВЫБОР
	              |		КОГДА ЧекККМОплата.Ссылка.ХозОперация = ЗНАЧЕНИЕ(Справочник.ХозОперации.ЧекНаВозврат)
	              |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	              |		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	              |	КОНЕЦ КАК ВидДвижения,
	              |	ЧекККМОплата.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	              |	ЧекККМОплата.Ссылка.КассаККМ КАК КассаККМ,
	              |	СУММА(ЧекККМОплата.Сумма) КАК Сумма
	              |ИЗ
	              |	Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	              |ГДЕ
	              |	ЧекККМОплата.Ссылка = &Ссылка
	              |	И ЧекККМОплата.ВидОплаты = ЗНАЧЕНИЕ(Перечисление.ВидыОплатыККМ.Наличные)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ЧекККМОплата.Ссылка.Дата,
	              |	ЧекККМОплата.Ссылка.ТорговаяПлощадка,
	              |	ЧекККМОплата.Ссылка.КассаККМ,
	              |	ЧекККМОплата.Ссылка.ХозОперация" ;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции




#КонецОбласти


#Область ПрограммныйИнтерфейс

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом



#КонецОбласти
	
	
#КонецЕсли

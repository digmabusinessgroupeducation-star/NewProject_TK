#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;	
	ТекстЗапросаЗаказыПокупателей(Запрос, ТекстыЗапроса, Регистры);
	Если ЗначениеЗаполнено(ДокументСсылка.ТорговаяПлощадкаПолучатель) Тогда
		ТекстЗапросаТаблицаОбработкаЗаказовПоставщику(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;		
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПокупателя.Дата КАК Период,
	|	ЗаказПокупателя.Ссылка КАК Ссылка,
	|	ЗаказПокупателя.ХозОперация КАК ХозОперация,
	|	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПокупателя.Организация КАК Организация,
	|	ЗаказПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ЗаказПокупателя.СкладКомпании КАК СкладКомпании,
	|	ЗаказПокупателя.Контрагент КАК Контрагент,
	|	ЗаказПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказПокупателя.ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадкаПолучатель
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ТорговаяПлощадкаПолучатель",Реквизиты.ТорговаяПлощадкаПолучатель);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЗаказПокупателяТовары.Ссылка КАК Ссылка,
	|	ЗаказПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяТовары.Ссылка.Организация КАК Организация,
	|	ЗаказПокупателяТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ЗаказПокупателяТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ЗаказПокупателяТовары.Ссылка.СкладКомпании КАК СкладКомпании,
	|	ЗаказПокупателяТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПокупателяТовары.Коэффициент КАК Коэффициент,
	|	ЗаказПокупателяТовары.КоличествоБазовое КАК Количество,
	|	ЗаказПокупателяТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	ЗаказПокупателяТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	|	ЗаказПокупателяТовары.Цена КАК Цена,
	|	ЗаказПокупателяТовары.Сумма КАК Сумма,
	|	ЗаказПокупателяТовары.СуммаВсего КАК СуммаВсего,
	|	ЗаказПокупателяТовары.СуммаНДС КАК СуммаНДС,
	|	ЗаказПокупателяТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	|	ЗаказПокупателяТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказПокупателяТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ЗаказПокупателяТовары.Ссылка.ХозОперация КАК ХозОперация
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|ГДЕ
	|	ЗаказПокупателяТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаказыПокупателей(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЗаказыПокупателей";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ВтТаблицаТовары.Ссылка КАК Заказ,
	|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВтТаблицаТовары.Контрагент КАК Контрагент,
	|	ВтТаблицаТовары.СкладКомпании КАК СкладКомпании,
	|	ВтТаблицаТовары.КоличествоБазовое КАК Заказано,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	|	ВтТаблицаТовары.Сумма * ВтТаблицаТовары.КурсДокумента КАК СуммаУпр
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОбработкаЗаказовПоставщику(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОбработкаЗаказовПоставщику";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	//Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
	//	ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	//КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Параметры.Ссылка.РаспределениеЗаказовПоставщиков) Тогда
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ЗаказПокупателяРаспределениеЗаказовПоставщиков.ЗаказПоставщику КАК ЗаказПоставщику,
		|	&ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадка,
		|	1 КАК Обработан,
		|	&ХозОперация КАК ХозОперация
		|ИЗ
		|	Документ.ЗаказПокупателя.РаспределениеЗаказовПоставщиков КАК ЗаказПокупателяРаспределениеЗаказовПоставщиков
		|ГДЕ
		|	ЗаказПокупателяРаспределениеЗаказовПоставщиков.Ссылка = &Ссылка";
		
	ИначеЕсли ТипЗнч(Запрос.Параметры.Ссылка.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		ТекстЗапроса ="ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	ЗаказПокупателя.ДокументОснование КАК ЗаказПоставщику,
		|	&ТорговаяПлощадкаПолучатель КАК ТорговаяПлощадка,
		|	1 КАК Обработан,
		|	&ХозОперация КАК ХозОперация
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
		|	И ЗаказПокупателя.Ссылка = &Ссылка";
	Иначе
		Возврат "";
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Заказ покупателя
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПокупателя";
	КомандаПечати.Идентификатор = "ЗаказПокупателя";
	КомандаПечати.Представление = НСтр("ru = 'Заказ покупателя'; uk = 'Замовлення покупця'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Заказ покупателя Контроль
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПокупателя";
	КомандаПечати.Идентификатор = "ЗаказПокупателяКонтроль";
	КомандаПечати.Представление = НСтр("ru = 'Заказ покупателя (Контроль)'; uk = 'Замовлення покупця (Контроль)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 			
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ДанныеДляСопоставления
	|ИЗ
	|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Ссылка,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	|ИЗ
	|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	|			И (ДанныеДляСопоставления.Артикул <> """")
	|			И (НЕ спрНоменклатура.ЭтоГруппа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляСопоставления.Артикул КАК Артикул,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ДанныеДляСопоставленияПоАртикулуТЕДИС
	|ИЗ
	|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|ГДЕ
	|	ДанныеДляСопоставления.Артикул <> """"
	|	И НЕ ДанныеДляСопоставления.Идентификатор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Ссылка,
	|	ДанныеДляСопоставленияПоАртикулуТЕДИС.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ СопоставленнаяНоменклатураАртикулТЕДИС
	|ИЗ
	|	ДанныеДляСопоставленияПоАртикулуТЕДИС КАК ДанныеДляСопоставленияПоАртикулуТЕДИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ПО ДанныеДляСопоставленияПоАртикулуТЕДИС.Артикул = ДополнительныеСведения.Значение
	|			И (ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
	|				И ДополнительныеСведения.Свойство.Имя = ""КодТедис"")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	|ИЗ
	|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|ГДЕ
	|	НЕ ДанныеДляСопоставления.Идентификатор В
	|				(ВЫБРАТЬ
	|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					СопоставленнаяНоменклатураАртикулТЕДИС.Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураАртикулТЕДИС КАК СопоставленнаяНоменклатураАртикулТЕДИС)
	|	И ДанныеДляСопоставления.Штрихкод <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	|	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	|ИЗ
	|	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	|ИЗ
	|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	|ГДЕ
	|	НЕ ДанныеДляСопоставления.Идентификатор В
	|				(ВЫБРАТЬ
	|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					СопоставленнаяНоменклатураАртикулТЕДИС.Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураАртикулТЕДИС КАК СопоставленнаяНоменклатураАртикулТЕДИС
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	|				ИЗ
	|					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	|	И ДанныеДляСопоставления.Номенклатура <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Ссылка,
	|	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	|ИЗ
	|	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	|			И (НЕ спрНоменклатура.ЭтоГруппа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	|	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	|	""Наименование"" КАК ПолеПоиска,
	|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	|ИЗ
	|	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	|
	|СГРУППИРОВАТЬ ПО
	|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	|	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	|	""Артикул"",
	|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	|ИЗ
	|	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	|
	|СГРУППИРОВАТЬ ПО
	|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	|	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	|	""Штрихкод"",
	|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	|ИЗ
	|	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	|
	|СГРУППИРОВАТЬ ПО
	|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СопоставленнаяНоменклатураАртикулТЕДИС.Идентификатор,
	|	МАКСИМУМ(СопоставленнаяНоменклатураАртикулТЕДИС.Ссылка),
	|	""КодТедис"",
	|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураАртикулТЕДИС.Идентификатор)
	|ИЗ
	|	СопоставленнаяНоменклатураАртикулТЕДИС КАК СопоставленнаяНоменклатураАртикулТЕДИС
	|
	|СГРУППИРОВАТЬ ПО
	|	СопоставленнаяНоменклатураАртикулТЕДИС.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
	
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				
			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
	
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК спрНоменклатура
		|ГДЕ
		|	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
	ИначеЕсли ИмяКолонки = "Артикул" Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.Номенклатура) КАК Ссылка
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект ССЫЛКА Справочник.Номенклатура
		|	И ДополнительныеСведения.Свойство.Имя = ""КодТедис""
		|	И ДополнительныеСведения.Значение = &Значение";
		
		Запрос.УстановитьПараметр("Значение", ЗагружаемыеЗначенияСтрока.Артикул);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


Процедура ЗаполнитьТабличнуюЧастьИзЗаказовПоставщиков(Объект) Экспорт 
	
	ТипОбъекта = ТипЗнч(Объект);
	Если ТипОбъекта = Тип("ДанныеФормыСтруктура") Тогда 
		МассивЗаказов = Объект.РаспределениеЗаказовПоставщиков.Выгрузить(,"ЗаказПоставщику").ВыгрузитьКолонку("ЗаказПоставщику");	
	ИначеЕсли ТипОбъекта = Тип("ДокументОбъект.ЗаказПокупателя") Тогда 
		МассивЗаказов = Объект.РаспределениеЗаказовПоставщиков.ВыгрузитьКолонку("ЗаказПоставщику");
	Иначе
		Возврат;
	КонецЕсли;
	
	ТорговаяПлощадкаПолучатель = Объект.ТорговаяПлощадкаПолучатель;
	
	Объект.Товары.Очистить();
	
	Если Не ЗначениеЗаполнено(ТорговаяПлощадкаПолучатель) ИЛИ МассивЗаказов.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) > 0
	|				ТОГДА ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ ЗаказПоставщикуТовары.КоличествоБазовое
	|		КОНЕЦ) КАК Количество,
	|	МАКСИМУМ(ЗаказПоставщикуТовары.Цена) КАК Цена
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка В(&МассивЗаказов)
	|	И ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
	|	И ЗаказПоставщикуТовары.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщикуТовары.Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент";
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадкаПолучатель);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Пока Выборка.Следующий() Цикл 
		нСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(нСтрока, Выборка);
		
		//СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", нСтрока.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", нСтрока.ЕдиницаИзмерения);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(нСтрока, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьИзЗаказовПоставщиковСРазбивкой(Объект, Метка) Экспорт 
	
	ТипОбъекта = ТипЗнч(Объект);
	Если ТипОбъекта = Тип("ДанныеФормыСтруктура") Тогда 
		МассивЗаказов = Объект.РаспределениеЗаказовПоставщиков.Выгрузить(,"ЗаказПоставщику").ВыгрузитьКолонку("ЗаказПоставщику");	
	ИначеЕсли ТипОбъекта = Тип("ДокументОбъект.ЗаказПокупателя") Тогда 
		МассивЗаказов = Объект.РаспределениеЗаказовПоставщиков.ВыгрузитьКолонку("ЗаказПоставщику");
	Иначе
		Возврат;
	КонецЕсли;
	
	ТорговаяПлощадкаПолучатель = Объект.ТорговаяПлощадкаПолучатель;
	
	Объект.Товары.Очистить();
	
	Если Не ЗначениеЗаполнено(ТорговаяПлощадкаПолучатель) ИЛИ МассивЗаказов.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) > 0
	|				ТОГДА ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	|			ИНАЧЕ ЗаказПоставщикуТовары.КоличествоБазовое
	|		КОНЕЦ) КАК Количество,
	|	МАКСИМУМ(ЗаказПоставщикуТовары.Цена) КАК Цена
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка В(&МассивЗаказов)
	|	И ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = &ТорговаяПлощадка
	|	И ВЫБОР
	|			КОГДА ЗаказПоставщикуТовары.Номенклатура.ТоварнаяМарка В ИЕРАРХИИ (&тм_JTI)
	|				ТОГДА 0	//	Задача № 20162
	|			ИНАЧЕ 0
	|		КОНЕЦ = &Метка
	|	И ЗаказПоставщикуТовары.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщикуТовары.Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент";
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ТорговаяПлощадка", ТорговаяПлощадкаПолучатель);
	Запрос.УстановитьПараметр("тм_JTI", Справочники.ТоварныеМарки.НайтиПоКоду("Т20"));		//	Джапан Тобакко Інтернешнл
	Запрос.УстановитьПараметр("Метка", Метка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ЗаказПокупателяВнутренний") Тогда
		СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);		
		СтруктураПолейДляЗаполненияЦен = Новый Структура;	
		
		//СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
		СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект,,,,,Объект.Организация.Контрагент));
		
		СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Пока Выборка.Следующий() Цикл 
		нСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(нСтрока, Выборка);
		
		//СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", нСтрока.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", нСтрока.ЕдиницаИзмерения);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(нСтрока, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиЗаказАгента(Организация,УстройствоАгента,ИД)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказПокупателя.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.GuidСистемыСоздания = &GuidСистемыСоздания
	|	И ЗаказПокупателя.Организация = &Организация
	|	И ЗаказПокупателя.ПортативноеУстройствоВвода = &ПортативноеУстройствоВвода";
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ПортативноеУстройствоВвода",УстройствоАгента);
	Запрос.УстановитьПараметр("GuidСистемыСоздания",ИД);
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		Возврат Документы.ЗаказПокупателя.ПустаяСсылка();
	КонецЕсли;
	Выборка = Рез.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
	
КонецФункции

Процедура РазделитьЗаказПоОрганизациям(Объект) Экспорт 
	
	Организация = Объект.Организация;
	ТорговаяПлощадка = Объект.ТорговаяПлощадка;
	
	тзТовары = Объект.Товары.Выгрузить();
	ПродажиСервер.ЗаполнитьВТЧОрганизациюПоНоменклатуре(тзТовары, Организация, ТорговаяПлощадка);
	
	МассивОрганизаций = тзТовары.ВыгрузитьКолонку("Организация");
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивОрганизаций);
	
	ЕстьТекОрганизация = МассивОрганизаций.Найти(Организация) <> Неопределено;
	
	Если МассивОрганизаций.Количество() = 1 И ЕстьТекОрганизация Тогда 
		Возврат;		
	КонецЕсли;	
	
	Объект.Товары.Очистить();	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Объект.Ссылка, Объект.ХозОперация);
	
	Если Не ЕстьТекОрганизация Тогда 
		Организация = МассивОрганизаций[0];
		
		Объект.Организация = Организация;
		Объект.ДоговорВзаиморасчетов = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(
		Объект.Контрагент,
		Организация,
		Объект.ПодразделениеКомпании,
		СписокВидовДоговоров,,
		Объект.Дата);
	КонецЕсли;
	
	Объект.Товары.Загрузить(тзТовары.Скопировать(Новый Структура("Организация", Организация)));			
	
	Для Каждого нОрганизация Из МассивОрганизаций Цикл 
		Если нОрганизация = Организация Тогда 
			Продолжить;
		КонецЕсли;
		
		Заказ = Документы.ЗаказПокупателя.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(Заказ, Объект,, "Номер, ДоговорВзаиморасчетов");
		
		Заказ.Организация = нОрганизация;
		Договор = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(
		Заказ.Контрагент,
		нОрганизация,
		Заказ.ПодразделениеКомпании,
		СписокВидовДоговоров,,
		Заказ.Дата);
		
		Заказ.Товары.Загрузить(тзТовары.Скопировать(Новый Структура("Организация", нОрганизация)));
		
		Если ЗначениеЗаполнено(Договор) Тогда 
			Заказ.ДоговорВзаиморасчетов = Договор;			
			Заказ.ВалютаДокумента = Договор.ВалютаВзаиморасчетов;
			
			ВалютаРасчетовКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов));			
			Объект.КурсДокумента = ?(ВалютаРасчетовКурсКратность.Курс=0,1,ВалютаРасчетовКурсКратность.Курс)/?(ВалютаРасчетовКурсКратность.Кратность = 0, 1, ВалютаРасчетовКурсКратность.Кратность);
			
			
			
			ДатаСрезаЦен = Заказ.Дата;			
			
			СтруктураДействий = Новый Структура;			
			СтруктураДействий.Вставить("ЗаполнитьГруппуЦенообразования",	Новый Структура("Номенклатура","ГруппаЦенообразования"));
			СтруктураДействий.Вставить("ЦеныИзДоговора",ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровТипаЦенПоДоговору(Заказ,,ДатаСрезаЦен));
			СтруктураПолейДляЗаполненияЦен = Новый Структура;	
			
			СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Заказ,,ДатаСрезаЦен));
			СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
			СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);
			
			
			СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");	
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
			СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
			
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));	
			СтруктураДействий.Вставить("ПересчитатьСуммуВсегоОкругление",Объект.РегламентированныйУчет);			
			
			ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Заказ.Товары, СтруктураДействий, Неопределено);
		КонецЕсли;
		
		Если Заказ.ПроверитьЗаполнение() Тогда 
			Заказ.Записать(РежимЗаписиДокумента.Проведение);
		Иначе 
			Заказ.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;	
		
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Документ %1 %2.'; uk = 'Документ %1 %2.'"),
		Строка(Заказ.Ссылка),
		?(Заказ.Проведен, НСтр("ru = 'проведен'; uk = 'проведений'"), НСтр("ru = 'записан. Не удалось провести документ...'; uk = 'записаний. Не вдалося провести документ...'"))),
		Заказ);
		
	КонецЦикла;
	
КонецПроцедуры


Процедура СформироватьЗаказыПокупателейИзЗаказаПоставщика(ПараметрыКоманды,ВсеОК)	Экспорт
	
	Перем Заказ, СкладРезервирования, ТекПользователь, Адрес, Регламент, РезервироватьВОсновныхЕдиницах;
	
	Если Не ПараметрыКоманды.Свойство("Адрес", Адрес) Тогда 
		ВызватьИсключение "Не передан адрес для помещения результата!";
	КонецЕсли;
	Если Не ПараметрыКоманды.Свойство("Заказ", Заказ) Тогда 
		ВызватьИсключение "Не передан Заказ Поставщику!";
	КонецЕсли;
	
	Заказ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыКоманды, "Заказ", Документы.ЗаказПоставщику.ПустаяСсылка());
	
	текДата = ТекущаяДата();
	врПараметры = Новый Структура("Дата,РегламентированныйУчет,Контрагент,СкладКомпании,ДатаОтгрузкиРЦ");
	ЗаполнитьЗначенияСвойств(врПараметры,Заказ);
	
	Период = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "ДатаОтгрузкиРЦ", ТекущаяДата());
	СрокПоставки = КонецДня(врПараметры.Дата)+86400;
	Регламент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "РегламентированныйУчет", Ложь);
	СкладРезервирования = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "СкладКомпании", ПредопределенноеЗначение("Справочник.СкладыКомпании.ПустаяСсылка"));
	КонсолидированныйЗаказ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыКоманды, "КонсолидированныйЗаказ", Ложь);
	ТекПользователь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "ТекПользователь", Пользователи.ТекущийПользователь());	
	РезервироватьВОсновныхЕдиницах = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "РезервироватьВОсновныхЕдиницах",Ложь);	
	
	ИспользоватьОбъединениеРезервированиеЗаказов = ПолучитьФункциональнуюОпцию("ИспользоватьОбъединениеРезервированиеЗаказов");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СкладыКомпании.Подразделение КАК Подразделение,
	|	СкладыКомпании.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	СкладыКомпании.ТипЦенСебестоимости КАК ТипЦен,
	|	СкладыКомпании.ТипЦенСебестоимости.ВалютаЦены КАК Валюта
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СкладРезервирования);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда 
		ВызватьИсключение "Не удалось получить реквизиты склада!";
	КонецЕсли;	
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьЦенаБезНалога");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуБезНДС");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ЦеныБезХарактеристики",Истина);		
	
	КэшируемыеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ЭтоФабрикаСыра = ОбменФронтПовтИсп.ЭтоПодразделениеФабрикаСыра(Выборка.Подразделение);
	
	Организация = ?(ЭтоФабрикаСыра,
	УправлениеСвойствами.ЗначениеСвойства(врПараметры.Контрагент,"ОрганизацияКонтрагента"),
	ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(врПараметры, "Организация", Справочники.Организации.ПолучитьОрганизациюПоКонтрагенту(врПараметры.Контрагент)));
		
	ОсновныеРеквизиты = Новый Структура;
	ОсновныеРеквизиты.Вставить("СкладКомпании", СкладРезервирования);
	ОсновныеРеквизиты.Вставить("Организация", Организация);
	ОсновныеРеквизиты.Вставить("ПодразделениеКомпании", Выборка.Подразделение);
	ОсновныеРеквизиты.Вставить("ТорговаяПлощадка", Выборка.ТорговаяПлощадка);
	//ОсновныеРеквизиты.Вставить("ТипЦен", Выборка.ТипЦен);
	ОсновныеРеквизиты.Вставить("КурсДокумента", 1);
	ОсновныеРеквизиты.Вставить("ВалютаДокумента", Выборка.Валюта);
	ОсновныеРеквизиты.Вставить("Автор", Пользователи.ТекущийПользователь());
	ОсновныеРеквизиты.Вставить("ХозОперация", ?(ЭтоФабрикаСыра, Справочники.ХозОперации.ЗаказПокупателя, Справочники.ХозОперации.ЗаказПокупателяВнутренний));
	ОсновныеРеквизиты.Вставить("Дата", текДата);
	ОсновныеРеквизиты.Вставить("СрокПоставки", СрокПоставки);
	ОсновныеРеквизиты.Вставить("Комментарий", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Документ сформирован из АЗК %1'; uk = 'Документ сформовано з АЗК %1'"), текДата));
	ОсновныеРеквизиты.Вставить("РегламентированныйУчет", Регламент);
	ОсновныеРеквизиты.Вставить("РезервироватьВОсновныхЕдиницах", РезервироватьВОсновныхЕдиницах);
	
	Если КонсолидированныйЗаказ Тогда
		
		ПараметрыЗапроса = Новый Структура("Дата, Организация, СкладКомпании, ПодразделениеКомпании");
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Заказ);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщику.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ПО (ДополнительныеСведения.Объект ССЫЛКА Документ.ЗаказПоставщику)
		|			И (ДополнительныеСведения.Свойство.Имя = ""ОбработанДляРЦ"")
		|			И ЗаказПоставщику.Ссылка = ДополнительныеСведения.Объект
		|ГДЕ
		|	ЗаказПоставщику.Проведен
		|	И НАЧАЛОПЕРИОДА(ЗаказПоставщику.Дата, ДЕНЬ) = &Дата
		|	И ЕСТЬNULL(ДополнительныеСведения.Значение, ЛОЖЬ) <> ИСТИНА
		|	И ЗаказПоставщику.Организация = &Организация
		|	И ЗаказПоставщику.СкладКомпании = &СкладКомпании
		|	И ЗаказПоставщику.ПодразделениеКомпании = &ПодразделениеКомпании
		|	И ЗаказПоставщику.Контрагент.ВидКонтрагента = ЗНАЧЕНИЕ(Перечисление.ВидыКонтрагентов.Внутренний)";
		
		Запрос.УстановитьПараметр("Дата", НачалоДня(ПараметрыЗапроса.Дата));
		Запрос.УстановитьПараметр("Организация", ПараметрыЗапроса.Организация);
		Запрос.УстановитьПараметр("СкладКомпании", ПараметрыЗапроса.СкладКомпании);
		Запрос.УстановитьПараметр("ПодразделениеКомпании", ПараметрыЗапроса.ПодразделениеКомпании);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Заказы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	Иначе
		Заказы = Новый Массив;
		Заказы.Добавить(Заказ);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЗаказПоставщикуТовары.Ссылка КАК тЗаказПоставщику,
	|	ЗаказПоставщикуТовары.СкладКомпании КАК тСкладКомпании,
	|	ВЫБОР
	|		КОГДА ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВариантОкругления = ЗНАЧЕНИЕ(Перечисление.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону)
	|			ТОГДА ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент <> (ВЫРАЗИТЬ(ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент - 0.5 КАК ЧИСЛО(10, 0)))
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК тМетка,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК тЗаказПокупателя,
	|	ЛОЖЬ КАК КонсолидированныйЗаказПокупателя
	|ПОМЕСТИТЬ втТаблица
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ОптимальноеКоличествоНоменклатуры.СрезПоследних(&ДатаРЦ, ) КАК ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних
	|		ПО ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ТорговаяПлощадка
	|			И ЗаказПоставщикуТовары.Номенклатура = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Номенклатура
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
	|	ЗаказПоставщикуТовары.СкладКомпании КАК СкладКомпании,
	|	ЗаказПоставщикуТовары.СкладКомпании.ТипЦенРозничнойТорговли КАК ТипЦен,
	|	ВЫБОР
	|		КОГДА ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ВариантОкругления = ЗНАЧЕНИЕ(Перечисление.ВариантыОкругленияКоличества.БольшеПоловиныУпаковкиВБольшуюСторону)
	|			ТОГДА ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент <> (ВЫРАЗИТЬ(ЗаказПоставщикуТовары.КоличествоБазовое / ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент - 0.5 КАК ЧИСЛО(10, 0)))
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Метка,
	|	ЗаказПоставщикуТовары.Ссылка.Организация.Контрагент КАК Контрагент,
	|	ЗаказПоставщикуТовары.Ссылка.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.КоличествоБазовое КАК Количество,
	|	ЗаказПоставщикуТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	|	ЗаказПоставщикуТовары.КоличествоБазовое КАК КоличествоБазовое,
	|	ЗаказПоставщикуТовары.Цена КАК Цена,
	|	ЗаказПоставщикуТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	|	ЗаказПоставщикуТовары.Сумма КАК Сумма,
	|	ЗаказПоставщикуТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПоставщикуТовары.СуммаНДС КАК СуммаНДС,
	|	ЗаказПоставщикуТовары.Сумма КАК СуммаВсего,
	|	ЗаказПоставщикуТовары.СуммаБезНалогов КАК СуммаБезНалогов
	|ПОМЕСТИТЬ ТоварыЗП
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_ОптимальноеКоличествоНоменклатуры.СрезПоследних(&ДатаРЦ, ) КАК ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних
	|		ПО ЗаказПоставщикуТовары.Номенклатура = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.Номенклатура
	|			И ЗаказПоставщикуТовары.СкладКомпании.ТорговаяПлощадка = ТК_ОптимальноеКоличествоНоменклатурыСрезПоследних.ТорговаяПлощадка
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				втТаблица.тЗаказПоставщику КАК тЗаказПоставщику
	|			ИЗ
	|				втТаблица КАК втТаблица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыЗП.Ссылка КАК Ссылка,
	|	ТоварыЗП.СкладКомпании КАК СкладКомпании,
	|	ТоварыЗП.ТипЦен КАК ТипЦен,
	|	ТоварыЗП.Метка КАК Метка,
	|	ТоварыЗП.Контрагент КАК Контрагент,
	|	ТоварыЗП.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ТоварыЗП.Номенклатура КАК Номенклатура,
	|	ТоварыЗП.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварыЗП.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТоварыЗП.Количество КАК Количество,
	|	ТоварыЗП.Коэффициент КАК Коэффициент,
	|	ТоварыЗП.КоличествоБазовое КАК КоличествоБазовое,
	|	ТоварыЗП.Цена КАК Цена,
	|	ТоварыЗП.ЦенаБезНалогов КАК ЦенаБезНалогов,
	|	ТоварыЗП.Сумма КАК Сумма,
	|	ТоварыЗП.СтавкаНДС КАК СтавкаНДС,
	|	ТоварыЗП.СуммаНДС КАК СуммаНДС,
	|	ТоварыЗП.СуммаВсего КАК СуммаВсего,
	|	ТоварыЗП.СуммаБезНалогов КАК СуммаБезНалогов
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	ТоварыЗП КАК ТоварыЗП
	|ГДЕ
	|	НЕ ТоварыЗП.Метка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыЗП.Ссылка,
	|	ТоварыЗП.СкладКомпании,
	|	ТоварыЗП.ТипЦен,
	|	ТоварыЗП.Метка,
	|	ТоварыЗП.Контрагент,
	|	ТоварыЗП.РегламентированныйУчет,
	|	ТоварыЗП.Номенклатура,
	|	ТоварыЗП.ЕдиницаИзмерения,
	|	ТоварыЗП.ХарактеристикаНоменклатуры,
	|	ТоварыЗП.Количество - (ВЫРАЗИТЬ(ТоварыЗП.Количество / ТоварыЗП.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент - 0.5 КАК ЧИСЛО(10, 0))) * ТоварыЗП.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент,
	|	ТоварыЗП.Коэффициент,
	|	ТоварыЗП.КоличествоБазовое,
	|	ТоварыЗП.Цена,
	|	ТоварыЗП.ЦенаБезНалогов,
	|	ТоварыЗП.Сумма,
	|	ТоварыЗП.СтавкаНДС,
	|	ТоварыЗП.СуммаНДС,
	|	ТоварыЗП.СуммаВсего,
	|	ТоварыЗП.СуммаБезНалогов
	|ИЗ
	|	ТоварыЗП КАК ТоварыЗП
	|ГДЕ
	|	ТоварыЗП.Метка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыЗП.Ссылка,
	|	ТоварыЗП.СкладКомпании,
	|	ТоварыЗП.ТипЦен,
	|	ЛОЖЬ,
	|	ТоварыЗП.Контрагент,
	|	ТоварыЗП.РегламентированныйУчет,
	|	ТоварыЗП.Номенклатура,
	|	ТоварыЗП.ЕдиницаИзмерения,
	|	ТоварыЗП.ХарактеристикаНоменклатуры,
	|	(ВЫРАЗИТЬ(ТоварыЗП.Количество / ТоварыЗП.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент - 0.5 КАК ЧИСЛО(10, 0))) * ТоварыЗП.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент,
	|	ТоварыЗП.Коэффициент,
	|	ТоварыЗП.КоличествоБазовое,
	|	ТоварыЗП.Цена,
	|	ТоварыЗП.ЦенаБезНалогов,
	|	ТоварыЗП.Сумма,
	|	ТоварыЗП.СтавкаНДС,
	|	ТоварыЗП.СуммаНДС,
	|	ТоварыЗП.СуммаВсего,
	|	ТоварыЗП.СуммаБезНалогов
	|ИЗ
	|	ТоварыЗП КАК ТоварыЗП
	|ГДЕ
	|	ТоварыЗП.Метка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблица.тЗаказПоставщику КАК ЗаказПоставщику,
	|	втТаблица.тЗаказПоставщику.Контрагент КАК Поставщик,
	|	втТаблица.тСкладКомпании КАК СкладКомпании,
	|	втТаблица.тМетка КАК Метка,
	|	втТаблица.тЗаказПокупателя КАК ЗаказПокупателя,
	|	втТаблица.КонсолидированныйЗаказПокупателя КАК КонсолидированныйЗаказПокупателя,
	|	ВТ.ТипЦен КАК ТипЦен,
	|	ВТ.Контрагент КАК Контрагент,
	|	ВТ.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ВТ.Номенклатура КАК Номенклатура,
	|	ВТ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЕСТЬNULL(ВТ.Количество, 0) КАК Количество,
	|	ВТ.Коэффициент КАК Коэффициент,
	|	ВТ.КоличествоБазовое КАК КоличествоБазовое,
	|	ВТ.Цена КАК Цена,
	|	ВТ.ЦенаБезНалогов КАК ЦенаБезНалогов,
	|	ВТ.Сумма КАК Сумма,
	|	ВТ.СтавкаНДС КАК СтавкаНДС,
	|	ВТ.СуммаНДС КАК СуммаНДС,
	|	ВТ.СуммаВсего КАК СуммаВсего,
	|	ВТ.СуммаБезНалогов КАК СуммаБезНалогов,
	|	ВЫРАЗИТЬ(втТаблица.тСкладКомпании КАК Справочник.СкладыКомпании).ТочкаДоставки КАК ТочкаДоставки,
	|	ВЫРАЗИТЬ(втТаблица.тСкладКомпании КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадкаПолучатель,
	|	втТаблица.тЗаказПоставщику.СрокПоставки КАК СрокПоставки
	|ИЗ
	|	втТаблица КАК втТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ КАК ВТ
	|		ПО втТаблица.тЗаказПоставщику = ВТ.Ссылка
	|			И втТаблица.тСкладКомпании = ВТ.СкладКомпании
	|			И втТаблица.тМетка = ВТ.Метка
	|ИТОГИ
	|	МАКСИМУМ(КонсолидированныйЗаказПокупателя),
	|	МИНИМУМ(РегламентированныйУчет),
	|	МАКСИМУМ(ТочкаДоставки),
	|	МАКСИМУМ(ТорговаяПлощадкаПолучатель),
	|	МАКСИМУМ(СрокПоставки)
	|ПО
	|	Контрагент,
	|	СкладКомпании,
	|	Метка,
	|	ЗаказПокупателя,
	|	ЗаказПоставщику";
	
	Запрос.УстановитьПараметр("Ссылка", Заказы);
	Запрос.УстановитьПараметр("ДатаРЦ", Период);
	
	ВыборкаКонтрагент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//	Запрос.Выполнить().Выгрузить()
	
	Всего = 0;
	СОшибками = 0;
	КэшДоговоров = Неопределено;
	
	Пока ВыборкаКонтрагент.Следующий() Цикл 
		ВыборкаСклад = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСклад.Следующий() Цикл
			ОсновныеРеквизиты.Вставить("ТипЦен", ВыборкаСклад.СкладКомпании.ТипЦенСебестоимости);
			
			ВыборкаМетка = ВыборкаСклад.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМетка.Следующий() Цикл 				
				ВыборкаЗаказПокупателя = ВыборкаМетка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаЗаказПокупателя.Следующий() Цикл 				
					ЕстьЗаказПокупателя = ЗначениеЗаполнено(ВыборкаЗаказПокупателя.ЗаказПокупателя);
					ВыборкаЗаказПоставщику = ВыборкаЗаказПокупателя.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Если Не КонсолидированныйЗаказ И Не ЕстьЗаказПокупателя Тогда 
						Пока ВыборкаЗаказПоставщику.Следующий() Цикл 
							обЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
							ЗаполнитьРеквизитыЗаказаПокупателя(обЗаказПокупателя, ОсновныеРеквизиты, ВыборкаСклад, ЭтоФабрикаСыра, КэшДоговоров);
							СтруктураПолейДляЗаполненияЦен = Новый Структура;	
							
							СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(обЗаказПокупателя,,,,,?(ЭтоФабрикаСыра,Неопределено,ВыборкаЗаказПоставщику.Поставщик)));
							
							СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
							
							обЗаказПокупателя.ИДДокументаОтгрузки = Строка(ВыборкаЗаказПоставщику.ЗаказПоставщику.УникальныйИдентификатор());
							обЗаказПокупателя.ДокументОснование = ВыборкаЗаказПоставщику.ЗаказПоставщику;
							обЗаказПокупателя.СрокПоставки = ВыборкаЗаказПоставщику.СрокПоставки;
							
							ВыборкаТовары = ВыборкаЗаказПоставщику.Выбрать();
							Пока ВыборкаТовары.Следующий() Цикл 
								Если ВыборкаТовары.Количество > 0 Тогда
									НоваяСтрока = обЗаказПокупателя.Товары.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
									ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшируемыеЗначения);
								КонецЕсли;
							КонецЦикла;
							ЗаписатьЗаказПокупателя(обЗаказПокупателя, Всего, СОшибками, ВыборкаМетка, ЭтоФабрикаСыра, ВсеОК); 
						КонецЦикла;
					Иначе
						Если ЕстьЗаказПокупателя Тогда 
							обЗаказПокупателя = ВыборкаЗаказПокупателя.ЗаказПокупателя.ПолучитьОбъект();
						Иначе
							обЗаказПокупателя = Документы.ЗаказПокупателя.СоздатьДокумент();
						КонецЕсли;
						
						ЗаполнитьРеквизитыЗаказаПокупателя(обЗаказПокупателя, ОсновныеРеквизиты, ВыборкаСклад, ЭтоФабрикаСыра, КэшДоговоров);
						//обЗаказПокупателя.СрокПоставки = ВыборкаЗаказПоставщику.СрокПоставки;
						//СтруктураПолейДляЗаполненияЦен = Новый Структура;	
						//
						//СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(обЗаказПокупателя,,,,,?(ЭтоФабрикаСыра,Неопределено,ВыборкаЗаказПоставщику.Поставщик)));
						//
						//СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
						
						обЗаказПокупателя.Товары.Очистить();
						обЗаказПокупателя.РаспределениеЗаказовПоставщиков.Очистить();
						
						Если ЕстьЗаказПокупателя И Не ВыборкаЗаказПокупателя.КонсолидированныйЗаказПокупателя Тогда						
							Если ВыборкаЗаказПоставщику.Количество() > 1 Тогда 
								ОбщегоНазначения.СообщитьПользователю(
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Ошибка! Для не консолидированного %1 найдено %2 связанных заказов поставщику!
								|Обратитесь к разработчику!'; uk = 'Помилка! Для не консолідованого %1 знайдено %2 зв''язаних замовлень постачальнику!
								|Зверніться до розробника!'"),
								ВыборкаЗаказПокупателя.ЗаказПокупателя,
								ВыборкаЗаказПоставщику.Количество()));
							КонецЕсли;
							
							Если ВыборкаЗаказПоставщику.Следующий() Тогда
								обЗаказПокупателя.СрокПоставки = ВыборкаЗаказПоставщику.СрокПоставки;
								ВыборкаТовары = ВыборкаЗаказПоставщику.Выбрать();																				
								Пока ВыборкаТовары.Следующий() Цикл 
									Если ВыборкаТовары.Количество > 0 Тогда
										НоваяСтрока = обЗаказПокупателя.Товары.Добавить();
										ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
										ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшируемыеЗначения);
									КонецЕсли;
								КонецЦикла;							
							КонецЕсли;
						Иначе
							обЗаказПокупателя.ДокументОснование = Неопределено;
							обЗаказПокупателя.ИДДокументаОтгрузки = "";												
							
							Пока ВыборкаЗаказПоставщику.Следующий() Цикл 
								НоваяСтрока = обЗаказПокупателя.РаспределениеЗаказовПоставщиков.Добавить();
								НоваяСтрока.ЗаказПоставщику = ВыборкаЗаказПоставщику.ЗаказПоставщику;
							КонецЦикла;
							Документы.ЗаказПокупателя.ЗаполнитьТабличнуюЧастьИзЗаказовПоставщиковСРазбивкой(обЗаказПокупателя,Число(ВыборкаМетка.Метка));
						КонецЕсли;					
						ЗаписатьЗаказПокупателя(обЗаказПокупателя, Всего, СОшибками, ВыборкаМетка, ЭтоФабрикаСыра, ВсеОК); 
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого нЗаказ Из Заказы Цикл
		МассивСтруктур = Новый Массив;
		УстановитьПривилегированныйРежим(Истина);
		ДопСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ОбработанДляРЦ");
		МассивСтруктур.Добавить(Новый Структура("Свойство, Значение", ДопСвойство, Истина));
		УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(нЗаказ, МассивСтруктур);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЦикла;
	
	//СтруктураРезультат = Новый Структура;
	////СтруктураРезультат.Вставить("ТаблицаЗаказыПокупателей", ТаблицаЗаказыПокупателей);
	//СтруктураРезультат.Вставить("Сообщение", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	//НСтр("ru = 'Всего сформировано %1
	//| - из них с ошибками %2'; uk = 'Всього сформовано %1
	//| - з них із помилками %2'"),
	//Всего,
	//СОшибками));
	//
	//ПоместитьВоВременноеХранилище(СтруктураРезультат, Адрес);	
	
КонецПроцедуры


#КонецОбласти	


#Область ПрочиеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыЗаказаПокупателя(докОбъект, ОсновныеРеквизиты, ВыборкаСклад, ЭтоФабрикаСыра, КэшДоговоров) 
	
	Если КэшДоговоров = Неопределено Тогда 
		КэшДоговоров = Новый ТаблицаЗначений;
		КэшДоговоров.Колонки.Добавить("Контрагент");
		КэшДоговоров.Колонки.Добавить("Организация");
		КэшДоговоров.Колонки.Добавить("ПодразделениеКомпании");
		КэшДоговоров.Колонки.Добавить("ДоговорВзаиморасчетов");
		
		КэшДоговоров.Индексы.Добавить("Контрагент, Организация, ПодразделениеКомпании");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(докОбъект, ОсновныеРеквизиты);
	ЗаполнитьЗначенияСвойств(докОбъект, ВыборкаСклад, "Контрагент, ТочкаДоставки, ТорговаяПлощадкаПолучатель");					
	
	ДоговорПоУмолчанию = Неопределено;
	
	СтруктураОтбора = Новый Структура("Контрагент, Организация, ПодразделениеКомпании", докОбъект.Контрагент, докОбъект.Организация, докОбъект.ПодразделениеКомпании);
	нСтроки = КэшДоговоров.НайтиСтроки(СтруктураОтбора);
	Если нСтроки.Количество() > 0 Тогда 
		ДоговорПоУмолчанию = нСтроки[0].ДоговорВзаиморасчетов;
	Иначе		
		МенеджерСправочника = Справочники.ДоговорыКонтрагентов;			
		СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(докОбъект.Ссылка, докОбъект.ХозОперация);
		ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(докОбъект.Контрагент, докОбъект.Организация, докОбъект.ПодразделениеКомпании, СписокВидовДоговоров, , докОбъект.Дата);
		
		НовСтрока = КэшДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, СтруктураОтбора);
		НовСтрока.ДоговорВзаиморасчетов = ДоговорПоУмолчанию;
	КонецЕсли;
	
	докОбъект.ДоговорВзаиморасчетов = ДоговорПоУмолчанию;
	докОбъект.ТипЦен = ?(ЭтоФабрикаСыра,ДоговорПоУмолчанию.ТипЦен,ОсновныеРеквизиты.ТипЦен);
	Если НЕ ЗначениеЗаполнено(докОбъект.ВалютаДокумента) Тогда
		докОбъект.ВалютаДокумента = ДоговорПоУмолчанию.ВалютаВзаиморасчетов;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьЗаказПокупателя(обЗаказПокупателя, Всего, СОшибками, Выборка, ЭтоФабрикаСыра, ВсеОК)
	
	Проведен = Ложь;
	тСклад = Выборка.СкладКомпании;
	Метка = Выборка.Метка;
	
	Если обЗаказПокупателя.Товары.Количество() = 0 Тогда
		
		Если Не обЗаказПокупателя.ЭтоНовый() Тогда 
			обЗаказПокупателя.УстановитьПометкуУдаления(Истина);	
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ %1 помечен на удаление'; uk = 'Документ %1 відмічений на видалення'"),
			обЗаказПокупателя.Ссылка),
			обЗаказПокупателя.Ссылка);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Попытка
		Если обЗаказПокупателя.ПроверитьЗаполнение() Тогда 
			обЗаказПокупателя.Записать(РежимЗаписиДокумента.Проведение);
			Всего = Всего + 1;
			Проведен = Истина;
		Иначе
			ВсеОК = Ложь;
			//обЗаказПокупателя.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ ''%1'' не проведен.'; uk = 'Документ ''%1'' не проведений.'"),
			Строка(обЗаказПокупателя.Ссылка)), 
			обЗаказПокупателя.Ссылка);
			СОшибками = СОшибками + 1;
		КонецЕсли;
	Исключение
		ВсеОК = Ложь;
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Ошибка записи документа: 
		|%1'; uk = 'Помилка запису документу: 
		|%1'"),
		ОписаниеОшибки()), 
		обЗаказПокупателя);
		СОшибками = СОшибками + 1;
	КонецПопытки;
	
	Если Проведен Тогда
		тЗаказПокупателя = обЗаказПокупателя.Ссылка;
		
		Если Метка Тогда
			УстановитьПривилегированныйРежим(Истина);
			НоваяЗапись = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			НоваяЗапись.Объект = тЗаказПокупателя;
			НоваяЗапись.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","Попачечно_b9a6d6a63dc74ae1a8f7d6a75ea25654");
			НоваяЗапись.Значение = Истина;
			НоваяЗапись.Записать(Истина);
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
		Если ЭтоФабрикаСыра Тогда
			ДокументВыпускРезерв = Документы.ВыпускПродукции.СоздатьДокумент();				
			ДокументВыпускРезерв.Заполнить(тЗаказПокупателя);
			//ДокументВыпускРезерв.ДополнительныеСвойства.Вставить("НеКонтролироватьОстатки", Истина);
		Иначе
			ПорцияЗаказов = Новый Массив;
			ПорцияЗаказов.Добавить(тЗаказПокупателя);
			ДокументВыпускРезерв = Документы.РезервированиеЗаказовПокупателя.СоздатьДокумент();
			ДокументВыпускРезерв.Заполнить(ПорцияЗаказов);	
			ДокументВыпускРезерв.Дата = ТекущаяДатаСеанса();
			ДокументВыпускРезерв.УстановитьНовыйНомер();
			
			ОбработкаТабличнойЧастиСервер.СортироватьТЧ(ДокументВыпускРезерв.Товары);
			
			//НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваров");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Склад",ДокументВыпускРезерв.СкладКомпании);
			ЭлементБлокировки.ИсточникДанных  =  ДокументВыпускРезерв.Товары;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура","Номенклатура");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика","ХарактеристикаНоменклатуры");
			Блокировка.Заблокировать();
			
			Документы.РезервированиеЗаказовПокупателя.УдалитьСтрокиБезОстатка(ДокументВыпускРезерв);
			Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ДокументВыпускРезерв);
			Документы.РезервированиеЗаказовПокупателя.ЗаполнитьХарактеристикиПоОстаткамПартий(ДокументВыпускРезерв);
			//Проверим квоты после заполнения характеристик, может быть еще уменьшено кол-во
			Документы.РезервированиеЗаказовПокупателя.ОбработатьКвотируемыеПозиции(ДокументВыпускРезерв);
			
			Если ДокументВыпускРезерв.Товары.Количество() = 0 Тогда
				//ОтменитьТранзакцию();
				Возврат;
			КонецЕсли;		
			
		КонецЕсли;
		
		
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			Если ЭтоФабрикаСыра Тогда
				ДокументВыпускРезерв.Записать(РежимЗаписиДокумента.Запись);
			Иначе
				ДокументВыпускРезерв.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Оперативный); 
				
				//ЗафиксироватьТранзакцию();	
			КонецЕсли;
			УстановитьПривилегированныйРежим(Ложь);
		Исключение
			ВсеОК = Ложь;
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка записи документа: 
			|%1'; uk = 'Помилка запису документу: 
			|%1'"),
			ОписаниеОшибки()), 
			ДокументВыпускРезерв);
			СОшибками = СОшибками + 1;
		КонецПопытки;
		
		//МассивОсновний = Новый Массив;
		//
		//Если ЗначениеЗаполнено(обЗаказПокупателя.ДокументОснование) И ТипЗнч(обЗаказПокупателя.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда 
		//	МассивОсновний.Добавить(обЗаказПокупателя.ДокументОснование);
		//КонецЕсли;
		//
		//Для Каждого Стр Из обЗаказПокупателя.РаспределениеЗаказовПоставщиков Цикл
		//	Если ЗначениеЗаполнено(Стр.ЗаказПоставщику) Тогда 
		//		МассивОсновний.Добавить(Стр.ЗаказПоставщику);
		//	КонецЕсли;
		//КонецЦикла;
		
		//Если обЗаказПокупателя.Проведен Тогда 
		//	тКартинка = МассивКартинок[1];
		//ИначеЕсли обЗаказПокупателя.ПометкаУдаления Тогда 
		//	тКартинка = МассивКартинок[0];
		//Иначе
		//	тКартинка = МассивКартинок[2];
		//КонецЕсли;		
		
		//Для Каждого ЗаказПост Из МассивОсновний Цикл 
		//	МассивСтрок = ТаблицаЗаказыПокупателей.НайтиСтроки(Новый Структура("Выбор, ЗаказПоставщику, СкладКомпании, Метка", Истина, ЗаказПост, тСклад, Число(Метка)));			
		//	Для Каждого Стр Из МассивСтрок Цикл 
		//		Стр.Выбор = Ложь;
		//		Стр.ЗаказПокупателя = тЗаказПокупателя;
		//		Стр.Картинка = тКартинка;
		//	КонецЦикла;
		//КонецЦикла;
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Заказ покупателя
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПокупателя") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПокупателя",
		НСтр("ru = 'Заказ покупателя'; uk = 'Замовлення покупця'"), 
		ПечатьЗаказПокупателя(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗаказПокупателя.ПФ_MXL_ЗаказПокупателя");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПокупателяКонтроль") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЗаказПокупателяКонтроль",
		НСтр("ru = 'Заказ покупателя (Контроль)'; uk = 'Замовлення покупця (Контроль)'"), 
		ПечатьЗаказПокупателяКонтроль(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗаказПокупателя.ПФ_MXL_ЗаказПокупателя");		
		
	КонецЕсли;	
	
КонецПроцедуры

// Заказ покупателя
Функция ПечатьЗаказПокупателя(МассивОбъектов, ОбъектыПечати)
	
	ВыводитьQR = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка,
	|	ЗаказПокупателя.Номер КАК Номер,
	|	ЗаказПокупателя.Дата КАК Дата,
	|	ЗаказПокупателя.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	ЗаказПокупателя.СрокПоставки КАК СрокПоставки,
	|	ЗаказПокупателя.Контрагент.НаименованиеПолное КАК ПредставлениеПолучателя,
	|	ЗаказПокупателя.ТочкаДоставки КАК Адрес,
	|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказПокупателя.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура.Артикул КАК Код,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		Цена КАК Цена,
	|		СуммаНДС КАК СуммаНДС,
	|		Сумма КАК Сумма
	|	) КАК Товары,
	|	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	|	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПокупателя.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ЗаказПокупателя";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаказПокупателя.ПФ_MXL_ЗаказПокупателя");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокКод		= Макет.ПолучитьОбласть("ЗаголовокQR");
	ОбластьПоставщик		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьДопИнформация	= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого			= Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("ПодвалЗаказа");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заказ покупателя №%1 от %2'; uk = 'Замовлення покупця №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		СтруктураШапки = Новый Структура("ПредставлениеПоставщика, СрокПоставки, ПредставлениеПолучателя, Адрес");
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		
		Если ВыводитьQR	Тогда 
			ОбластьЗаголовокКод.Параметры.Заполнить(СтруктураШапки);			
			ГУИДДокумента = ВыборкаДокументов.Ссылка.УникальныйИдентификатор();		
			
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ГУИДДокумента),1,125);
				ОбластьЗаголовокКод.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовокКод);			
		Иначе
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);		
		КонецЕсли;
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		МассивДопИнформации = Новый Массив;
		Если Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.Комментарий)) Тогда 
			МассивДопИнформации.Добавить(
			Новый Структура("НазваниеПараметра, ЗначениеПараметра",
			НСтр("ru = 'Комментарий:'; uk = 'Коментар:'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Комментарий)));
		КонецЕсли;
		
		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		СуммаНДС		= 0;
		СуммаВсего 		= 0;
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Код, Количество, Коэффициент, ЕдиницаИзмерения, Цена, СуммаНДС, Сумма");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			стрНоменклатура = ?(ЗначениеЗаполнено(ВыборкаТовары.ТоварПредставление), СокрЛП(ВыборкаТовары.ТоварПредставление), Строка(ВыборкаТовары.Номенклатура));						
			СтруктураСтроки.Вставить("НоменклатураПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			СуммаНДС 	= СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего 	= СуммаВсего + ВыборкаТовары.Сумма;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		
		Если ВыборкаДокументов.РегламентированныйУчет Тогда 
			СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);		
			СтруктураПодвала.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
			
			ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		КонецЕсли;
		
		СтруктураПодвала.Вставить("ИтоговаяСтрока", 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Всего наименований %1 на сумму %2'; uk = 'Всього найменувань %1 на суму %2'", КодЯзыкаПечать),
		нпп,
		Формат(СуммаВсего, "ЧДЦ=2") + " " + СокрЛП(ВыборкаДокументов.ВалютаДокумента)));
		
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);		
		
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Заказ покупателя Контроль
Функция ПечатьЗаказПокупателяКонтроль(МассивОбъектов, ОбъектыПечати)
	
	ВыводитьQR = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказПокупателя.Ссылка КАК Ссылка,
	|	ЗаказПокупателя.Номер КАК Номер,
	|	ЗаказПокупателя.Дата КАК Дата,
	|	ЗаказПокупателя.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	ЗаказПокупателя.СрокПоставки КАК СрокПоставки,
	|	ЗаказПокупателя.Контрагент.НаименованиеПолное КАК ПредставлениеПолучателя,
	|	ЗаказПокупателя.ТочкаДоставки КАК Адрес,
	|	ЗаказПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗаказПокупателя.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура.Артикул КАК Код,
	|		Номенклатура КАК Номенклатура,
	|		Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		Цена КАК Цена,
	|		СуммаНДС КАК СуммаНДС,
	|		Сумма КАК Сумма,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	|		ЕСТЬNULL(ЗаказПокупателя.Товары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 1) КАК ЕдИзмЯщиковКоэффициент
	|	) КАК Товары,
	|	ЗаказПокупателя.ТочкаДоставки КАК ТочкаДоставки,
	|	ЗаказПокупателя.РегламентированныйУчет КАК РегламентированныйУчет,
	|	ЗаказПокупателя.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	|ГДЕ
	|	ЗаказПокупателя.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ЗаказПокупателяКонтроль";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаказПокупателя.ПФ_MXL_ЗаказПокупателя");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокКод		= Макет.ПолучитьОбласть("ЗаголовокQR");
	ОбластьПоставщик		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьДопИнформация	= Макет.ПолучитьОбласть("ДопИнформация");
	ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицыЯщ");	
	ОбластьСтрока 			= Макет.ПолучитьОбласть("СтрокаЯщ");
	ОбластьИтого			= Макет.ПолучитьОбласть("ИтогоЯщ");
	//ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодвал 			= Макет.ПолучитьОбласть("ПодвалЗаказа");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
		
		ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заказ покупателя №%1 от %2'; uk = 'Замовлення покупця №%1 від %2'", КодЯзыкаПечать),
		СокрЛП(ВыборкаДокументов.Номер),
		Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		СтруктураШапки = Новый Структура("ПредставлениеПоставщика, СрокПоставки, ПредставлениеПолучателя, Адрес");
		ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
		СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
		
		Если ВыводитьQR	Тогда 
			ОбластьЗаголовокКод.Параметры.Заполнить(СтруктураШапки);			
			ГУИДДокумента = ВыборкаДокументов.Ссылка.УникальныйИдентификатор();		
			
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ГУИДДокумента),1,125);
				ОбластьЗаголовокКод.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовокКод);			
		Иначе
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);		
		КонецЕсли;
		
		ОбластьПоставщик.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПоставщик);
		
		ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
		ТабличныйДокумент.Вывести(ОбластьПокупатель);
		
		МассивДопИнформации = Новый Массив;
		Если Не ПустаяСтрока(СокрЛП(ВыборкаДокументов.Комментарий)) Тогда 
			МассивДопИнформации.Добавить(
			Новый Структура("НазваниеПараметра, ЗначениеПараметра",
			НСтр("ru = 'Комментарий:'; uk = 'Коментар:'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Комментарий)));
		КонецЕсли;
		
		Для Каждого ДопИнформация Из МассивДопИнформации Цикл 
			ОбластьДопИнформация.Параметры.Заполнить(ДопИнформация);
			ТабличныйДокумент.Вывести(ОбластьДопИнформация);
		КонецЦикла;
		
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		нпп 			= 0;
		Количество 		= 0;
		КоличествоМест 	= 0;
		
		СуммаНДС		= 0;
		СуммаВсего 		= 0;
		
		ВыборкаТовары = ВыборкаДокументов.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			нпп = нпп + 1;			
			
			СтруктураСтроки = Новый Структура("Номенклатура, Код, Количество, Коэффициент, ЕдиницаИзмерения, Цена, СуммаНДС, Сумма");
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, ВыборкаТовары);			
			
			СтруктураСтроки.Вставить("НомерСтроки", нпп);
			
			стрНоменклатура = ?(ЗначениеЗаполнено(ВыборкаТовары.ТоварПредставление), СокрЛП(ВыборкаТовары.ТоварПредставление), Строка(ВыборкаТовары.Номенклатура));						
			СтруктураСтроки.Вставить("НоменклатураПредставление", стрНоменклатура + ?(Не ПустаяСтрока(ВыборкаТовары.ХарактеристикаНоменклатуры), ", " + СокрЛП(ВыборкаТовары.ХарактеристикаНоменклатуры), ""));
			
			Если ЗначениеЗаполнено(ВыборкаТовары.ЕдиницаИзмеренияЯщиков) Тогда 
				КолвоМест = ВыборкаТовары.КоличествоБазовое / ВыборкаТовары.ЕдИзмЯщиковКоэффициент;
			Иначе
				КолвоМест = 0;
			КонецЕсли;
			
			СтруктураСтроки.Вставить("КоличествоМест", ?(КолвоМест = 0, "-", КолвоМест));
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			КоличествоМест = КоличествоМест + КолвоМест;
			Количество 	= Количество + ВыборкаТовары.Количество;
			СуммаНДС 	= СуммаНДС + ВыборкаТовары.СуммаНДС;
			СуммаВсего 	= СуммаВсего + ВыборкаТовары.Сумма;
		КонецЦикла;
		
		СтруктураПодвала = Новый Структура;	
		СтруктураПодвала.Вставить("Всего", СуммаВсего);		
		СтруктураПодвала.Вставить("Количество", Количество);
		СтруктураПодвала.Вставить("КоличествоМест", КоличествоМест);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		
		//Если ВыборкаДокументов.РегламентированныйУчет Тогда 
		//	СтруктураПодвала.Вставить("ВсегоНДС", СуммаНДС);		
		//	СтруктураПодвала.Вставить("НДС", НСтр("ru = 'В том числе НДС:'; uk = 'У тому числі ПДВ:'", КодЯзыкаПечать));
		//	
		//	ОбластьИтогоНДС.Параметры.Заполнить(СтруктураПодвала);
		//	ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		//КонецЕсли;
		
		СтруктураПодвала.Вставить("ИтоговаяСтрока", 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Всего наименований %1 на сумму %2'; uk = 'Всього найменувань %1 на суму %2'", КодЯзыкаПечать),
		нпп,
		Формат(СуммаВсего, "ЧДЦ=2") + " " + СокрЛП(ВыборкаДокументов.ВалютаДокумента)));
		
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(СуммаВсего, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		СтруктураПодвала.Вставить("СуммаПрописью", СуммаПрописью);		
		
		
		ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Проводит корректировку ЗаказовПокупателя с неактуальным резервированием. Вызывается из обработчика исправления проблемы 
// Проверка резервирования заказов покупателя.
// Такие документы выявляются проверкой ЗапасыСервер.ПроверитьРезервированиеЗаказовПокупателя.
//
// Параметры:
//    ПараметрыПроверки - Структура:
//        * ВидПроверки - СправочникСсылка.ВидыПроверок - вид осуществляемой проверки.
//    АдресХранилища - Строка - адрес временного хранилища для возвращаемого значения.
//
Процедура СкорректироватьРезервирование(Знач ПараметрыПроверки, АдресХранилища = Неопределено) Экспорт
	
	ПодробнаяИнформацияПоПроблемам = Новый Соответствие;
	
	КоличествоИсправленных	= 0;
	КоличествоПропущенных	= 0;
	
	ВидПроверки = ПараметрыПроверки.ВидПроверки;
	
	НачатьТранзакцию();
	
	Попытка
		
		ПравилоПроверки = КонтрольВеденияУчета.ПроверкаПоИдентификатору("ПроверитьРезервированиеЗаказовПокупателя");
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РезультатыПроверкиУчета");
		ЭлементБлокировки.УстановитьЗначение("ПравилоПроверки", ПравилоПроверки);
		ЭлементБлокировки.УстановитьЗначение("ВидПроверки", ВидПроверки);
		ЭлементБлокировки.УстановитьЗначение("ИгнорироватьПроблему", Ложь);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		ЭлементБлокировкиВидаПроверки = БлокировкаДанных.Добавить("Справочник.ВидыПроверок");
		ЭлементБлокировкиВидаПроверки.УстановитьЗначение("Ссылка", ВидПроверки);
		ЭлементБлокировкиВидаПроверки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1000
		|	РезультатыПроверкиУчета.ПроблемныйОбъект КАК ПроблемныйОбъект
		|ПОМЕСТИТЬ ЗаказыПокупателя
		|ИЗ
		|	РегистрСведений.РезультатыПроверкиУчета КАК РезультатыПроверкиУчета
		|ГДЕ
		|	РезультатыПроверкиУчета.ПравилоПроверки = &ПравилоПроверки
		|	И НЕ РезультатыПроверкиУчета.ИгнорироватьПроблему
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатыПроверкиУчета.ПроблемныйОбъект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РезервыТоваровОстатки.Заказ КАК Заказ,
		|	РезервыТоваровОстатки.Склад КАК Склад,
		|	РезервыТоваровОстатки.Номенклатура КАК Номенклатура,
		|	РезервыТоваровОстатки.Характеристика КАК Характеристика,
		|	РезервыТоваровОстатки.РезервОстаток КАК РезервОстаток
		|ИЗ
		|	РегистрНакопления.РезервыТоваров.Остатки(
		|			,
		|			Заказ В
		|				(ВЫБРАТЬ
		|					ЗаказыПокупателя.ПроблемныйОбъект КАК ПроблемныйОбъект
		|				ИЗ
		|					ЗаказыПокупателя КАК ЗаказыПокупателя)) КАК РезервыТоваровОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказ");
		
		Запрос.УстановитьПараметр("ПравилоПроверки",  ПравилоПроверки);
		
		Результат = Запрос.Выполнить();	//	Результат.Выгрузить()
		Выборка = Результат.Выбрать();
		
		нпп = 0;
		НуженДок = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		
		Пока Выборка.СледующийПоЗначениюПоля("Заказ") Цикл
			
			Если НуженДок Тогда
				дОбъект = Документы.КорректировкаРегистров.СоздатьДокумент();
				дОбъект.Дата = ТекущаяДата();	
				дОбъект.Ответственный = Пользователи.ТекущийПользователь();
				дОбъект.Комментарий = "Исправление резервирования"+Символы.ПС+"Сформирован автоматически "+дОбъект.Дата;
				СтрокаТЧ = дОбъект.ТаблицаРегистров.Добавить();
				СтрокаТЧ.Имя = "РезервыТоваров";
				
				Попытка
					дОбъект.Записать();
					НуженДок = Ложь;
					НаборЗаписей = РегистрыНакопления.РезервыТоваров.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Значение = дОбъект.Ссылка;
				Исключение
					ОписаниеОшибки = ОписаниеОшибки();
					ЗаписьЖурналаРегистрации("Исправление резервирования (СоздатьДокумент)",УровеньЖурналаРегистрации.Ошибка,дОбъект,,ОписаниеОшибки);
					ПодробнаяИнформацияПоПроблемам.Вставить(дОбъект.Ссылка, ОписаниеОшибки);
					КоличествоПропущенных = КоличествоПропущенных + 1;
					Продолжить;
				КонецПопытки;
				
			КонецЕсли;
			
			
			
			Пока Выборка.Следующий() Цикл
				нпп = нпп + 1;
				Движение = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Движение,Выборка);
				Движение.Регистратор= дОбъект.Ссылка;
				Движение.Период		= дОбъект.Дата;
				Движение.ВидДвижения= ВидДвиженияНакопления.Приход;
				Движение.Резерв	= -Выборка.РезервОстаток;
				Движение.ХозОперация= Справочники.ХозОперации.КорректировкаУчетныхДанных;
			КонецЦикла;
			
			Если нпп > 7777 Тогда
				нпп = 0;
				НуженДок = Истина;
			КонецЕсли;
			
			Попытка
				НаборЗаписей.Записать(Истина);
				КоличествоИсправленных = КоличествоИсправленных + 1;
				
				Набор = РегистрыСведений.РезультатыПроверкиУчета.СоздатьНаборЗаписей();
				Набор.Отбор.ВидПроверки.Установить(ПараметрыПроверки.ВидПроверки);
				Набор.Отбор.ИгнорироватьПроблему.Установить(Ложь);
				Набор.Отбор.ПравилоПроверки.Установить(ПравилоПроверки);
				Набор.Отбор.ПроблемныйОбъект.Установить(Выборка.Заказ);
				Набор.Записать();
				
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
				ЗаписьЖурналаРегистрации("Исправление резервирования (НаборЗаписей.Записать)",УровеньЖурналаРегистрации.Ошибка,дОбъект,,ОписаниеОшибки);
				ПодробнаяИнформацияПоПроблемам.Вставить(Выборка.Заказ, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КоличествоПропущенных = КоличествоПропущенных + 1;
			КонецПопытки;
			
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Исправление резервирования'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	ИтоговыйРезультат = Новый Структура;
	ИтоговыйРезультат.Вставить("КоличествоИсправленных",		КоличествоИсправленных);
	ИтоговыйРезультат.Вставить("КоличествоПропущенных",			КоличествоПропущенных);
	ИтоговыйРезультат.Вставить("ПодробнаяИнформацияПоПроблемам",ПодробнаяИнформацияПоПроблемам);
	
	ПоместитьВоВременноеХранилище(ИтоговыйРезультат, АдресХранилища);
	
КонецПроцедуры




#КонецОбласти


#КонецЕсли
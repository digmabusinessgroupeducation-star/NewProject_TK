#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда 
		ЗаполнитьДокументНаОснованииПоступления(ДанныеЗаполнения);
	Иначе
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата 				= ТекущаяДата();
	СрокПоставки		= НачалоДня(Дата + 60*60*24);
	ДокументОснование 	= Неопределено;
	GuidСистемыСоздания = "";
	ИДДокументаОтгрузки = "";
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказПокупателя Тогда
		ТорговаяПлощадкаПолучатель = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТочкаДоставки) И ТочкаДоставки.Владелец <> Контрагент Тогда 
		ТочкаДоставки = Неопределено;
	КонецЕсли;
	
	РаспределениеЗаказовПоставщиков.Очистить();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	ДополнительныеСвойства.Вставить("ЭтоНовый"		, ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи"	, РежимЗаписи);
	СуммаДокумента = Товары.Итог("СуммаВсего");	
	
	Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
		ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения И ПометкаУдаления Тогда
		
		Запрос = ЗапросНайтиПодчиненныйПоТипу(Ссылка,?(ТК_СправочникиПовтИсп.ЭтоВнутреннийКонтрагент(Контрагент),"ПеремещениеТоваров","РеализацияТоваров"));
		Рез = Запрос.Выполнить();
		
		Если Не Рез.Пустой() Тогда
			Выборка = Рез.Выбрать();
			Пока Выборка.Следующий() Цикл 
				Если Выборка.Проведен Тогда
					Отказ = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОченьНужноУдалить = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ОченьНужноУдалить", Ложь);
			
			Запрос = ЗапросНайтиПодчиненныйПоТипу(Ссылка,"РезервированиеЗаказовПокупателя");
			Рез = Запрос.Выполнить();
			
			Если Не Рез.Пустой() И Не ОченьНужноУдалить Тогда
				Отказ = Истина;
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 имеет подчиненный резерв, используйте корректировку'; uk = '%1 має підлеглий резерв, використовуйте коригування'"),
				ЭтотОбъект),
				ЭтотОбъект,
				"Список",,
				Отказ);
				
				//		Выборка = Рез.Выбрать();
				//		Пока Выборка.Следующий() Цикл 
				//			Если Выборка.Проведен Тогда
				//				
				//				ДокПодчиенный = Выборка.ДокСсылка.ПолучитьОбъект();
				//				Попытка
				//					ДокПодчиенный.УстановитьПометкуУдаления(Истина);
				//				Исключение
				//					Отказ = Истина;
				//				КонецПопытки;
				//				
				//			КонецЕсли;
				//		КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	///Не регистрировать для выгрузки голобал центр
	Если НЕ ТорговаяПлощадка.РегистрироватьТК Тогда
		ДополнительныеСвойства.Вставить("ОбменТК");
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		// Выполнить контроль квот
		Пользователь 					= Пользователи.ТекущийПользователь();
		КонтролироватьКвоты				= Не ДоговорВзаиморасчетов.НеКонтролироватьКвоты И Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"НеКонтролироватьОтгрузкиПоКвотам",Ложь);			
		
		Если КонтролироватьКвоты Тогда 
			ЭтоВнутреннийКонтрагент 		= ЗначениеНастроекПовтИсп.ЭтоВнутреннийКонтрагент(Контрагент);
			КонтролироватьКвотыВнКонтр 		= Не ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"ОтключитьКонтрольКвотДляВнутреннихКонтрагентов",Ложь);	
			
			Если Не ЭтоВнутреннийКонтрагент ИЛИ ЭтоВнутреннийКонтрагент И КонтролироватьКвотыВнКонтр Тогда 
				ВыполнитьКонтрольКвот();	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ЗаказПокупателя.ИнициализироватьДанныеДокумента(Ссылка,ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаказыСервер.ОтразитьЗаказыПокупателей(ДополнительныеСвойства, Движения, Отказ);
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОбработкаЗаказовПоставщику") Тогда
		ЗаказыСервер.ОтразитьОбработкаЗаказовПоставщику(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	
	Если НЕ ДоговорВзаиморасчетов.НеПроверятьПросроченнуюЗадолженность  
		И НЕ (ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутренний 
		ИЛИ ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		ОтсрочкаПлатежа =	Справочники.ДоговорыКонтрагентов.СрокОплатыПоДоговору(Дата,ДоговорВзаиморасчетов);
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаЗапроса",Дата);
		Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",ДоговорВзаиморасчетов);
		//Запрос.УстановитьПараметр("РегламентированныйУчет",РегламентированныйУчет);
		//Запрос.УстановитьПараметр("СрокОплатыЗадолженности",?(РегламентированныйУчет,ОтсрочкаПлатежа.Ф1,ОтсрочкаПлатежа.Ф2));
		Запрос.УстановитьПараметр("СрокОплатыЗадолженности_Ф1",ОтсрочкаПлатежа.Ф1);
		Запрос.УстановитьПараметр("СрокОплатыЗадолженности_Ф2",ОтсрочкаПлатежа.Ф2);
		
		//Запрос.Текст = "ВЫБРАТЬ
		//|	СУММА(РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток) КАК СуммаОстаток
		//|ИЗ
		//|	РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(
		//|			&ДатаЗапроса,
		//|			ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов
		//|				И РегламентированныйУчет = &РегламентированныйУчет) КАК РасчетыСПокупателямиПоДокументамОстатки
		//|ГДЕ
		//|	ВЫБОР
		//|			КОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, &СрокОплатыЗадолженности + 1) <= НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ)
		//|				ТОГДА ИСТИНА
		//|			ИНАЧЕ ЛОЖЬ
		//|		КОНЕЦ";
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(РасчетыСПокупателямиПоДокументамОстатки.СуммаОстаток) КАК СуммаОстаток
		               |ИЗ
		               |	РегистрНакопления.РасчетыСПокупателямиПоДокументам.Остатки(&ДатаЗапроса, ДоговорВзаиморасчетов = &ДоговорВзаиморасчетов) КАК РасчетыСПокупателямиПоДокументамОстатки
		               |ГДЕ
		               |	ВЫБОР
		               |			КОГДА РасчетыСПокупателямиПоДокументамОстатки.Сделка ССЫЛКА Документ.РеализацияТоваров
		               |					И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Документ.РеализацияТоваров).Дата, ДЕНЬ), ДЕНЬ, ВЫБОР
		               |							КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет = ИСТИНА
		               |								ТОГДА &СрокОплатыЗадолженности_Ф1
		               |							ИНАЧЕ &СрокОплатыЗадолженности_Ф2
		               |						КОНЕЦ + 1) <= НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ)
		               |				ТОГДА ИСТИНА
		               |			КОГДА РасчетыСПокупателямиПоДокументамОстатки.Сделка ССЫЛКА Документ.ВыпускПродукции
		               |					И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(РасчетыСПокупателямиПоДокументамОстатки.Сделка КАК Документ.ВыпускПродукции).Дата, ДЕНЬ), ДЕНЬ, ВЫБОР
		               |							КОГДА РасчетыСПокупателямиПоДокументамОстатки.РегламентированныйУчет = ИСТИНА
		               |								ТОГДА &СрокОплатыЗадолженности_Ф1
		               |							ИНАЧЕ &СрокОплатыЗадолженности_Ф2
		               |						КОНЕЦ + 1) <= НАЧАЛОПЕРИОДА(&ДатаЗапроса, ДЕНЬ)
		               |				ТОГДА ИСТИНА
		               |			ИНАЧЕ ЛОЖЬ
		               |		КОНЕЦ";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			СуммаДолга = 0;
			Пока Выборка.Следующий() Цикл 
				СуммаДолга = ?(ЗначениеЗаполнено(Выборка.СуммаОстаток),Выборка.СуммаОстаток,0);
			КонецЦикла;
			
			Если СуммаДолга > 10 Тогда
				Отказ = Истина;
				ТекстСообщения = "ВНИМАНИЕ! Обнаружено превышение срока оплаты долга контрагента "+Строка(Контрагент);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения); 
				
				
				Если Не ДополнительныеСвойства.Свойство("РезультатОбработки") Тогда
					ДополнительныеСвойства.Вставить("РезультатОбработки",Новый ТекстовыйДокумент);//Результаты ошибок
				КонецЕсли;
				ДополнительныеСвойства.РезультатОбработки.ДобавитьСтроку(ТекстСообщения);
				
				
			КонецЕсли;
			
			
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	//Проверка заполнения ТочкиДоставки
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонтролироватьЗаполнениеТД = НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь, "НеКонтролироватьЗаполнениеТД", Ложь);
	
	Если ЗначениеЗаполнено(ТочкаДоставки) И ОбщегоНазначения.СсылкаСуществует(ТочкаДоставки) И ТочкаДоставки.Владелец <> Контрагент Тогда 
		
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Точка доставки ""%1"" не пренадлежит контрагенту ""%2""'; uk = 'Точка доставки ""%1"" не належить контрагенту ""%2""'"),
		ТочкаДоставки,
		Контрагент),
		ЭтотОбъект,
		"ТочкаДоставки",,
		Отказ);
		
		
	ИначеЕсли Не ЗначениеЗаполнено(ТочкаДоставки) И КонтролироватьЗаполнениеТД Тогда 
		
		ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru = 'Запрещено проводить Заказ покупателя с пустой точкой доставки'; uk = 'Заборонено проводити Заказ покупця з пустою точкою доставки'"),
		ЭтотОбъект,
		"ТочкаДоставки",,
		Отказ);
		
	КонецЕсли;
	
	// Выполнить контроль разделения Номенклатуры по Организациям Vov41k 08.01.2023
	Если ЗначениеНастроекПовтИсп.ДоступноРазделениеТоваровПоОрганизациям(ТорговаяПлощадка) Тогда 
		ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ);
	КонецЕсли;		
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутренний
		ИЛИ ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство Тогда 
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Цена");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ЦенаБезНалогов");
	КонецЕсли;
	
	Если ХозОперация = Справочники.ХозОперации.ЗаказПокупателяВнутреннийПроизводство Тогда 
		ПроверяемыеРеквизиты.Добавить("ТорговаяПлощадкаПолучатель");	
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ОбщегоНазначенияТК.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект,  ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти


#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Дата				  = ТекущаяДата();
	СрокПоставки		  = НачалоДня(Дата + 60*60*24);
	Автор 				  = Пользователи.ТекущийПользователь();
	ВалютаДокумента 	  = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ВалютаДокумента);
	КурсДокумента         = 1;
	Организация           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнаяОрганизация",,Организация);	
	ПодразделениеКомпании = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Автор, ПодразделениеКомпании);
	СкладКомпании 		  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор,"ОсновнойСклад",,СкладКомпании);
	ТорговаяПлощадка      = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(СкладКомпании);	
	ХозОперация           = Справочники.ХозОперации.ЗаказПокупателя;
	ТипЦен 				  = Справочники.ТипыЦен.ОсновнойТипЦенПродажи;	
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ЗаполнитьДокументНаОснованииПоступления(ДанныеЗаполнения)
	
	ИнициализироватьДокумент();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка КАК ДокументОснование,
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	ПоступлениеТоваров.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ПоступлениеТоваров.СкладКомпании КАК СкладКомпании,
	|	ПоступлениеТоваров.Контрагент КАК Контрагент,
	|	ПоступлениеТоваров.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	ПоступлениеТоваров.ВалютаДокумента КАК ВалютаДокумента,
	|	ПоступлениеТоваров.КурсДокумента КАК КурсДокумента,
	|	ПоступлениеТоваров.СуммаДокумента КАК СуммаДокумента,
	|	ПоступлениеТоваров.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		Количество КАК Количество,
	|		Коэффициент КАК Коэффициент,
	|		КоличествоБазовое КАК КоличествоБазовое,
	|		Цена КАК Цена,
	|		ЦенаБезНалогов КАК ЦенаБезНалогов,
	|		Сумма КАК Сумма,
	|		СуммаВсего КАК СуммаВсего,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		СуммаБезНалогов КАК СуммаБезНалогов,
	|		ПроцентСкидки КАК ПроцентСкидки,
	|		СуммаСкидки КАК СуммаСкидки
	|	) КАК Товары
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
		
		Товары.Загрузить(Выборка.Товары.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ЗапросНайтиПодчиненныйПоТипу(Знач ДокСсылка,Знач ТипДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст =     "ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК ДокСсылка,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"
	|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ."+ТипДокумента+").Проведен
	|	КОНЕЦ КАК Проведен
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Док) КАК СвязанныеДокументы
	|ГДЕ
	|	СвязанныеДокументы.Ссылка ССЫЛКА Документ."+ТипДокумента+"";
	Запрос.УстановитьПараметр("Док",ДокСсылка);
	
	Возврат Запрос;
	
КонецФункции	


#Область КонтрольКвот

Процедура ВыполнитьКонтрольКвот(Отказ = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ДополнительныеСвойства.Вставить("ВидКвоты", УправлениеСвойствами.ЗначениеСвойства(ТочкаДоставки, "ВидКвотыОтгрузки"));
	ДополнительныеСвойства.Вставить("ВидКвотыФМУ", УправлениеСвойствами.ЗначениеСвойства(ТочкаДоставки, "ВидКвотыОтгрузкиФМУ"));
	ДополнительныеСвойства.Вставить("Область", ЗначениеНастроекПовтИсп.ПолучитьОбластьТочкиДоставки(ТочкаДоставки));
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ПродажиСервер.ТаблицаОграниченийПоКвотамОтгрузки(Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	СообщитьОбОшибкахОграниченийКвот(МенеджерВременныхТаблиц, Отказ);
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&ХозОперация КАК ХозОперация,
	|	&Организация КАК Организация,
	|	&ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	&СкладКомпании КАК СкладКомпании,
	|	&Контрагент КАК Контрагент,
	|	&ТочкаДоставки КАК ТочкаДоставки
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).Производитель КАК Производитель,
	|	ТаблицаТоваров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаТоваров.КоличествоБазовое КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	Запрос.УстановитьПараметр("Дата",								Дата);
	Запрос.УстановитьПараметр("Организация",						Организация);
	Запрос.УстановитьПараметр("СкладКомпании",						СкладКомпании);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",				ПодразделениеКомпании);
	Запрос.УстановитьПараметр("Контрагент",							Контрагент);
	Запрос.УстановитьПараметр("ТочкаДоставки",						ТочкаДоставки);
	Запрос.УстановитьПараметр("ХозОперация",						ХозОперация);
	Запрос.УстановитьПараметр("ТаблицаТоваров",						Товары.Выгрузить());	
	
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Процедура СообщитьОбОшибкахОграниченийКвот(МенеджерВременныхТаблиц, ЕстьОшибки) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПревышенияПоКвотам.Номенклатура КАК Номенклатура,
	|	ПревышенияПоКвотам.Производитель КАК Производитель,
	|	ПревышенияПоКвотам.Количество КАК Количество,
	|	ПревышенияПоКвотам.текПроцент КАК текПроцент,
	|	ПревышенияПоКвотам.Порог КАК Порог,
	|	ПревышенияПоКвотам.Процент КАК Процент,
	|	ПревышенияПоКвотам.ИтогоПоПроизводителю КАК ИтогоПоПроизводителю,
	|	ПревышенияПоКвотам.ПревышенПорог КАК ПревышенПорог,
	|	ПревышенияПоКвотам.ПроверятьПревышениеПроцента КАК ПроверятьПревышениеПроцента,
	|	ПревышенияПоКвотам.ПревышенПроцент КАК ПревышенПроцент
	|ИЗ
	|	ПревышенияПоКвотам КАК ПревышенияПоКвотам";
	
	Результат = Запрос.Выполнить();	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;	
	
	ОбщегоНазначения.СообщитьПользователю(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'В документе превышена квота по отгрузке на точку доставки %1'; uk = 'У документі перевищена квота по відвантаженню на точку доставки %1'"),
	Строка(ТочкаДоставки)),
	ЭтотОбъект,
	,
	,
	ЕстьОшибки);
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 
		МаксКоличество = Макс(Цел(((Выборка.ИтогоПоПроизводителю - Выборка.Количество)* Выборка.Процент)/(100 - Выборка.Процент)), Выборка.Порог);
		
		ОбщегоНазначения.СообщитьПользователю(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Превышена квота по <%1> текущее кол-во: %2 (%3%), макс. кол-во: %4%5'; uk = 'Перевищена квота по <%1> поточна кіль-ть: %2 (%3%), макс. кіль-ть: %4%5'"),
		Выборка.Номенклатура,
		Выборка.Количество,
		Выборка.текПроцент,
		МаксКоличество,
		?(Выборка.ПроверятьПревышениеПроцента И Выборка.ПревышенПроцент,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '; установленная квота - %1%'; uk = '; встановлена квота - %1%'"),
		Выборка.Процент),
		"")
		),
		ЭтотОбъект); 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти


Процедура ВыполнитьКонтрольРазделенияПоОрганизациям(Отказ)
	
	СписокНоменклатуры = Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	
	СоответствиеНоменклатуры = РегистрыСведений.ТК_РазделениеНоменклатурыПоОрганизациям.ПолучитьОрганизациюСпискаНоменклатуры(
	Организация,
	СписокНоменклатуры,
	ТорговаяПлощадка);
	
	Для Каждого Стр Из СоответствиеНоменклатуры Цикл 
		Если Стр.Значение.Организация <> Организация Тогда 
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номенклатура: %1 закреплена за организацией ""%2""'; uk = 'Номенклатура: %1 закріплена за організацією ""%2""'"),
			Стр.Ключ,
			Стр.Значение.Организация),
			ЭтотОбъект,
			"Товары");
			
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти




#Иначе

ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли
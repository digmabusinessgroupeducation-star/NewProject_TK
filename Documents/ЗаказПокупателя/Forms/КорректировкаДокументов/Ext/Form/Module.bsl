///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьТекущуюСтраницу(ЭтотОбъект, Ложь, Ложь, Ложь);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Авто;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура КорректироватьЗаказыПокупателяНаСервере()
	
	 Документы.ЗаказПокупателя.СкорректироватьРезервирование(Новый Структура("ВидПроверки", Параметры.ВидПроверки));
КонецПроцедуры

&НаКлиенте
Процедура КорректироватьЗаказыПокупателя(Команда)
	
	//КорректироватьЗаказыПокупателяНаСервере();
	//Возврат;
	ДлительнаяОперация = СкорректироватьРезервированиеВФоне();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СкорректироватьРезервированиеВФонеЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущуюСтраницу(Форма, ИдетИсправлениеПроблемы, ИсправлениеУспешноВыполнено, СбойВАлгоритмеИсправления)
	
	ЭлементыФормы = Форма.Элементы;
	Если ИдетИсправлениеПроблемы Тогда
		ЭлементыФормы.ГруппаИндикаторИсправления.Видимость           = Истина;
		ЭлементыФормы.ГруппаИндикаторНачалаИсправления.Видимость     = Ложь;
		ЭлементыФормы.ГруппаИндикаторУспешноеИсправление.Видимость   = Ложь;
		ЭлементыФормы.ГруппаИндикаторНеуспешноеИсправление.Видимость = Ложь;
	ИначеЕсли ИсправлениеУспешноВыполнено Тогда
		ЭлементыФормы.ГруппаИндикаторИсправления.Видимость           = Ложь;
		ЭлементыФормы.ГруппаИндикаторНачалаИсправления.Видимость     = Ложь;
		ЭлементыФормы.ГруппаИндикаторУспешноеИсправление.Видимость   = Истина;
		ЭлементыФормы.Выполнить.Видимость                            = Ложь;
		ЭлементыФормы.ГруппаИндикаторНеуспешноеИсправление.Видимость = Ложь;
	ИначеЕсли СбойВАлгоритмеИсправления Тогда
		ЭлементыФормы.ГруппаИндикаторИсправления.Видимость           = Ложь;
		ЭлементыФормы.ГруппаИндикаторНачалаИсправления.Видимость     = Ложь;
		ЭлементыФормы.ГруппаИндикаторУспешноеИсправление.Видимость   = Ложь;
		ЭлементыФормы.ГруппаИндикаторНеуспешноеИсправление.Видимость = Истина;
	Иначе
		ЭлементыФормы.ГруппаИндикаторИсправления.Видимость         = Ложь;
		ЭлементыФормы.ГруппаИндикаторНачалаИсправления.Видимость   = Истина;
		ЭлементыФормы.ГруппаИндикаторУспешноеИсправление.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СкорректироватьРезервированиеВФоне()
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	УстановитьТекущуюСтраницу(ЭтотОбъект, Истина, Ложь, Ложь);
	
	НаименованиеФоновогоЗадания = НСтр("ru = 'Корректировка резервирования'");
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	ПараметрыПроверки = Новый Структура("ВидПроверки", Параметры.ВидПроверки);
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("Документы.ЗаказПокупателя.СкорректироватьРезервирование",
	ПараметрыПроверки, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СкорректироватьРезервированиеВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		УстановитьТекущуюСтраницу(ЭтотОбъект, Истина, Ложь, Ложь);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		УстановитьТекущуюСтраницу(ЭтотОбъект, Ложь, Ложь, Ложь);
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ОбработатьУспешноеВыполнениеИсправления(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьУспешноеВыполнениеИсправления(Результат)
	
	АдресРезультата      = Результат.АдресРезультата;
	РезультатыПроведения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	КоличествоПропущенных          = РезультатыПроведения.КоличествоПропущенных;
	КоличествоИсправленных            = РезультатыПроведения.КоличествоИсправленных;
	
	Если КоличествоПропущенных = 0 Тогда
		УстановитьТекущуюСтраницу(ЭтотОбъект, Ложь, Истина, Ложь);
		Элементы.НадписьУспешноеИсправление.Заголовок = ОписаниеУспеха(КоличествоИсправленных);
	Иначе
		УстановитьТекущуюСтраницу(ЭтотОбъект, Ложь, Ложь, Истина);
		Элементы.НадписьНеуспешноеИсправление.Заголовок = ОписаниеПричинНеуспеха(АдресРезультата, КоличествоИсправленных, КоличествоПропущенных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеПричинНеуспеха(АдресРезультата, КоличествоИсправленных, КоличествоПропущенных)
	
	Информация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Исправлено: %1
	|Не исправлено: %2'"),
	Формат(КоличествоИсправленных, "ЧН=0; ЧГ=0"), Формат(КоличествоПропущенных, "ЧН=0; ЧГ=0"));
	
	ГиперссылкаОтчета = Новый ФорматированнаяСтрока(НСтр("ru = 'Подробнее...'"), , , , АдресРезультата);
	
	Возврат Новый ФорматированнаяСтрока(Информация, Символы.ПС, ГиперссылкаОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеУспеха(КоличествоИсправленных)
	
	ТекстоваяИнформацияОПроведенных = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Успешно проведено документов: %1'"),
	Формат(КоличествоИсправленных, "ЧН=0; ЧГ=0"));
	
	Возврат Новый ФорматированнаяСтрока(ТекстоваяИнформацияОПроведенных, , ЦветаСтиля.РезультатУспехЦвет);
	
КонецФункции

&НаКлиенте
Процедура НадписьНеуспешноеИсправлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокПроблемныхЗаказов = СписокПроблемныхСчетовФактур(НавигационнаяСсылкаФорматированнойСтроки);
	СписокПроблемныхЗаказов.Показать(НСтр("ru = 'Список проблемных Заказов покупателя'"), , Истина);
	
КонецПроцедуры

&НаСервере
Функция СписокПроблемныхСчетовФактур(НавигационнаяСсылкаФорматированнойСтроки)
	
	РезультатыПроведения          	= ПолучитьИзВременногоХранилища(НавигационнаяСсылкаФорматированнойСтроки);
	ПодробнаяИнформацияПоПроблемам	= РезультатыПроведения.ПодробнаяИнформацияПоПроблемам;
	
	СписокПроблемныхЗаказов			= Новый ТабличныйДокумент;
	Макет                       	= Документы.ЗаказПокупателя.ПолучитьМакет("Проблемные");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	СписокПроблемныхЗаказов.Вывести(ОбластьШапка);
	
	Для Каждого ЭлементИнформации Из ПодробнаяИнформацияПоПроблемам Цикл
		
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		
		ОбластьСтрока.Параметры.СсылкаНаДокумент = ЭлементИнформации.Ключ;
		ОбластьСтрока.Параметры.ТекстИсключения  = ЭлементИнформации.Значение;
		
		СписокПроблемныхЗаказов.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	СписокПроблемныхЗаказов.ФиксацияСверху = 1;
	СписокПроблемныхЗаказов.Защита         = Истина;
	
	Возврат СписокПроблемныхЗаказов;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	МассивХозОпераций = ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("НазначениеСкидок"); 
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(МассивХозОпераций);
	
	ЗаполнитьДеревоТорговыхПолощадок();
	УстановитьВидимостьНаСервере();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	УстановитьСостояниеДокумента();
	
	Если Объект.Проведен И Объект.Скидки.Количество() > 0 Тогда//И НЕ РольДоступна("ПолныеПрава") Тогда
		Элементы.СкидкиУстановитьДляВсейТЧ.Видимость = Ложь;
		УстановитьЗапретНаИзменение();
	КонецЕсли;
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Скидки");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Скидки");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);

	УстановитьСостояниеДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаписатьДеревоТПВОбъект();
	
	МассивТП = Новый Массив;
	Для Каждого Стр Из Объект.ТорговыеПлощадки Цикл
		  МассивТП.Добавить(Стр.ТорговаяПлощадка);
	КонецЦикла; 
	
	Для Каждого стр Из Объект.Скидки Цикл
		СтруктураДействий = Новый Структура;		
		СтруктураДействий.Вставить("ПроверитьСкидкуПоИндикативу");
		СтруктураДействий.Вставить("ТорговыеПлощадки", МассивТП);
		
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(стр, СтруктураДействий, Неопределено);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	//Документы.ТК_НазначениеСкидок.ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты, ЭтаФорма, Истина);
	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//Отказ = Не	ПроверитьЗаполнение();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипСкидкиПриИзменении(Элемент)
	
	ТипСкидкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеКомпанииПриИзменении(Элемент)
	ЗаполнитьДеревоТорговыхПолощадок();
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеТорговыеПлощадкиВыбранаПриИзменении(Элемент)
	
	ИДТекущейСтроки =  Элементы.ВыбранныеТорговыеПлощадки.ТекущаяСтрока;
	Если ИДТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Модифицированность = Истина;
	
	ЭлементКоллекции = ЭтаФорма.ВыбранныеТорговыеПлощадки.НайтиПоИдентификатору(ИДТекущейСтроки);
	
	ЗначениеФлажка = ЭлементКоллекции.Выбрана;
	УстановкаФлажков(ЭлементКоллекции,ЗначениеФлажка);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСкидки

&НаКлиенте
Процедура СкидкиНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Скидки.ТекущиеДанные;	
	Если ПустаяСтрока(ТекущаяСтрока.ДниНедели) Тогда
		ТекущаяСтрока.ДниНедели = "1111111";
	КонецЕсли;
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкиКонецДействияПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура СкидкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Элемент.ТекущиеДанные.СпособВычисления = ПолучитьСпособВычисления(Объект.ТипСкидки);
КонецПроцедуры

&НаКлиенте
Процедура СкидкиПриАктивизацииСтроки(Элемент)
	Если Объект.Проведен И Объект.Скидки.Количество() > 0 И НЕ Элемент.ТекущиеДанные=Неопределено Тогда
		НачалоДействия = Элемент.ТекущиеДанные.НачалоДействия;
		КонецДействия = Элемент.ТекущиеДанные.КонецДействия;
		Если ЗначениеЗаполнено(НачалоДействия) И ЗначениеЗаполнено(НачалоДействия) Тогда
			Элементы.ГруппаСкидки.ТолькоПросмотр = КонецДействия < ТекущаяДата();
			Если НачалоДействия < ТекущаяДата() И КонецДействия > ТекущаяДата() Тогда
				УстановитьТолькоПросмотр(Элементы.Скидки,Истина);
			Иначе
				УстановитьТолькоПросмотр(Элементы.Скидки,Ложь);
			КонецЕсли;
		Иначе
			УстановитьТолькоПросмотр(Элементы.Скидки,Ложь);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура УстановитьДляВсейТЧ(Команда)
	
	ТекущаяСтрока  = Элементы.Скидки.ТекущиеДанные;	
	ТекущаяКолонка = Элементы.Скидки.ТекущийЭлемент;
	ИмяКолонки = ТекущаяКолонка.Имя;
	
	Если ИмяКолонки = "СкидкиЗначениеСкидки" Тогда
		ТекущееЗначение = ТекущаяСтрока.ЗначениеСкидки;
		УстановитьЗначениеДляВсейТЧ("ЗначениеСкидки",ТекущееЗначение);
		//ИначеЕсли ИмяКолонки = "СкидкиСпособВычисления" Тогда
		//	ТекущееЗначение = ТекущаяСтрока.СпособВычисления;
		//	УстановитьЗначениеДляВсейТЧ("СпособВычисления",ТекущееЗначение);
	ИначеЕсли ИмяКолонки = "СкидкиПравило" Тогда
		ТекущееЗначение = ТекущаяСтрока.Правило;
		УстановитьЗначениеДляВсейТЧ("Правило",ТекущееЗначение);
	ИначеЕсли ИмяКолонки = "СкидкиНачалоДействия" Тогда
		ТекущееЗначение = ТекущаяСтрока.НачалоДействия;
		УстановитьЗначениеДляВсейТЧ("НачалоДействия",ТекущееЗначение);
	ИначеЕсли ИмяКолонки = "СкидкиКонецДействия" Тогда
		ТекущееЗначение = ТекущаяСтрока.КонецДействия;
		УстановитьЗначениеДляВсейТЧ("КонецДействия",ТекущееЗначение);
		
	Иначе
		Сообщить("Для текущей колонки данная функция не доступна",СтатусСообщения.Информация);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеДляВсейТЧ(ИмяКолонки,НовоеЗначение)
	
	Для Каждого СтрокаТЧ Из Объект.Скидки Цикл 
		СтрокаТЧ[ИмяКолонки] = НовоеЗначение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ТК_НазначениеСкидок.Скидки";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка товаров из файла'; uk = 'Завантаження списка товарів із файлу'");		
	//ПараметрыЗагрузки.ДополнительныеПараметры = Новый структура;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТТПоТипуЦен(Команда)
	ЗаполнитьДеревоТорговыхПолощадокПоТипуЦен();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ТипСкидкиПриИзмененииНаСервере()
	УстановитьВидимостьНаСервере();
	
	СпособВыч = Объект.ТипСкидки.СпособВычисления;
	Для Каждого СтрСкидки  Из Объект.Скидки Цикл 
		СтрСкидки.СпособВычисления =  СпособВыч;
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНаСервере()
	
	Если Объект.ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаКоличествоТовара ИЛИ Объект.ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаНабор Тогда
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма.Элементы,"СкидкиПравило,СкидкиКоличество","Видимость",Истина);
	Иначе
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(ЭтаФорма.Элементы,"СкидкиПравило,СкидкиКоличество","Видимость",Ложь);
	КонецЕсли;
	
	ЭтаФорма.Элементы.СкидкиСпособВычисления.ТолькоПросмотр = Объект.ТипСкидки = Справочники.ТК_ТипыСкидок.СпециальнаяЦена;
	ЭтаФорма.Элементы.СкидкиСуммаЧека.Видимость = Объект.ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаСуммуЧека;
	ЭтаФорма.Элементы.СкидкиКоэффициентНабора.Видимость = Объект.ТипСкидки = Справочники.ТК_ТипыСкидок.СкидкаНаНабор;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДеревоТПВОбъект()
	
	Объект.ТорговыеПлощадки.Очистить();
	
	Для Каждого Стр Из ВыбранныеТорговыеПлощадки.ПолучитьЭлементы() Цикл 
		
		ЗаписатьСтрокуДерева(Объект, Стр);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСтрокуДерева(ТекущийОбъект,СтрокаДерева)
	
	// проверим, а надо ли записывать
	Если ТипЗнч(СтрокаДерева.ТорговаяПлощадка) = Тип("СправочникСсылка.ТорговыеПлощадки") Тогда 
		Если ЗначениеЗаполнено(СтрокаДерева.ТорговаяПлощадка) И СтрокаДерева.Выбрана Тогда
			НоваяЗапись = ТекущийОбъект.ТорговыеПлощадки.Добавить();
			НоваяЗапись.ТорговаяПлощадка = СтрокаДерева.ТорговаяПлощадка;
		КонецЕсли;
	КонецЕсли;
	
	// подчиненные строки
	Для Каждого СтрокаДереваПодч Из СтрокаДерева.ПолучитьЭлементы() Цикл 
		ЗаписатьСтрокуДерева(ТекущийОбъект, СтрокаДереваПодч);
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура УстановкаФлажков(ЭлементКоллекции,ЗначениеФлажка)
	ПодчинЭлементы = ЭлементКоллекции.ПолучитьЭлементы();
	Для Каждого ТекЭлемент Из ПодчинЭлементы Цикл
		ТекЭлемент.Выбрана = ЗначениеФлажка;
		УстановкаФлажков(ТекЭлемент, ЗначениеФлажка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоТорговыхПолощадок()
	
	ДеревоТорговыхПлощадок = РеквизитФормыВЗначение("ВыбранныеТорговыеПлощадки");
	ДеревоТорговыхПлощадок.Строки.Очистить();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СкладыКомпании.АссортиментнаяМатрица, """") КАК Матрица,
	|	ЕСТЬNULL(ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация, ЗНАЧЕНИЕ(Справочник.Организации.пустаяСсылка)) КАК Организация,
	|	ТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
	|	СкладыКомпании.Ссылка КАК Склад,
	|	ВЫБОР
	|		КОГДА ТП.ТорговаяПлощадка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выбрана,
	|	ВЫБОР
	|		КОГДА ТП.ТорговаяПлощадка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Выбранных,
	|	ТорговыеПлощадки.Ссылка КАК Количество,
	|	СкладыКомпании.Код КАК Код
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТК_НазначениеСкидок.ТорговыеПлощадки КАК ТП
	|		ПО (ТП.Ссылка = &Ссылка)
	|			И ТорговыеПлощадки.Ссылка = ТП.ТорговаяПлощадка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
	|		ПО ТорговыеПлощадки.Ссылка = СкладыКомпании.ТорговаяПлощадка
	|			И (СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация = Организации.Ссылка
	|		ПО ТорговыеПлощадки.Ссылка = ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.ТорговаяПлощадка
	|ГДЕ
	|	ТорговыеПлощадки.ПодразделениеКомпании = &Подразделение
	|	И ТорговыеПлощадки.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка КАК ТорговаяПлощадка
	|			ИЗ
	|				РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|			ГДЕ
	|				ДополнительныеСведения.Свойство.Имя = ""OS_АктивныеСклады""
	|				И ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Справочник.СкладыКомпании).ТорговаяПлощадка.ПодразделениеКомпании = &Подразделение)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Матрица УБЫВ,
	|	Организация,
	|	Склад,
	|	ТорговаяПлощадка
	|ИТОГИ
	|	СУММА(Выбранных),
	|	КОЛИЧЕСТВО(Количество)
	|ПО
	|	Матрица,
	|	Организация
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Объект.ПодразделениеКомпании);
	
	РезультатЗапроса = Запрос.Выполнить();	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаМатрица = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаМатрица.Следующий() Цикл
		Сч = 0;
		Выбрана =  ВыборкаМатрица.Выбранных > 0;
		
		стрМатрица = ДеревоТорговыхПлощадок.Строки.Добавить();
		стрМатрица.Выбрана = Выбрана;
		ВыборкаОрганизация = ВыборкаМатрица.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОрганизация.Следующий() Цикл
			Выбрана =  ВыборкаОрганизация.Выбранных > 0;
			стрОрганизация = стрМатрица.Строки.Добавить();
			стрОрганизация.ТорговаяПлощадка = ВыборкаОрганизация.Организация;
			стрОрганизация.Выбрана = Выбрана;
			ВыборкаСклад = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока ВыборкаСклад.Следующий()Цикл 
				стрСклад = стрОрганизация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(стрСклад,ВыборкаСклад);
				Сч = Сч + 1;
			КонецЦикла;
			стрОрганизация.ТорговаяПлощадка = ВыборкаОрганизация.Организация.Наименование + " ("+ВыборкаОрганизация.Выбранных+" из "+ВыборкаОрганизация.Количество+")";
		КонецЦикла;
		стрМатрица.ТорговаяПлощадка = ?(ВыборкаМатрица.Матрица="","ПустаЯ",ВыборкаМатрица.Матрица.Наименование) + " ("+ВыборкаМатрица.Выбранных+" из "+Сч+")";
	КонецЦикла;
	
	//Если ссылки ещё нет - установим галочки по ТЧ подразделений 
	Если Параметры.Ключ.Пустая() Тогда
		Для Каждого стчТП Из Объект.ТорговыеПлощадки Цикл
			НайденнаяСтрока = ДеревоТорговыхПлощадок.Строки.Найти(стчТП.ТорговаяПлощадка, "ТорговаяПлощадка", Истина);
			Если НайденнаяСтрока <> Неопределено Тогда
				НайденнаяСтрока.Выбрана = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	
	ЗначениеВРеквизитФормы(ДеревоТорговыхПлощадок,"ВыбранныеТорговыеПлощадки");
	
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоТорговыхПолощадокПоТипуЦен()
	
	ДеревоТорговыхПлощадок = РеквизитФормыВЗначение("ВыбранныеТорговыеПлощадки");
	ДеревоТорговыхПлощадок.Строки.Очистить();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА КАК Выбрана,
	|	СкладыКомпании.Ссылка КАК Склад
	|ПОМЕСТИТЬ Вт_Склады
	|ИЗ
	|	Справочник.СкладыКомпании КАК СкладыКомпании
	|ГДЕ
	|	СкладыКомпании.ТипЦенРозничнойТорговли = &ТипЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СкладыКомпании.АссортиментнаяМатрица, """") КАК Матрица,
	|	ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.Организация КАК Организация,
	|	ТорговыеПлощадки.Ссылка КАК ТорговаяПлощадка,
	|	СкладыКомпании.Ссылка КАК Склад,
	|	СкладыКомпании.Код КАК Код,
	|	ВЫБОР
	|		КОГДА Вт_Склады.Выбрана ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выбрана,
	|	ВЫБОР
	|		КОГДА Вт_Склады.Выбрана ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Выбранных
	|ИЗ
	|	Справочник.ТорговыеПлощадки КАК ТорговыеПлощадки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
	|			ЛЕВОЕ СОЕДИНЕНИЕ Вт_Склады КАК Вт_Склады
	|			ПО СкладыКомпании.Ссылка = Вт_Склады.Склад
	|		ПО ТорговыеПлощадки.Ссылка = СкладыКомпании.ТорговаяПлощадка
	|			И (СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТК_УчетнаяПолитикаТорговыхПлощадок.СрезПоследних КАК ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних
	|		ПО ТорговыеПлощадки.Ссылка = ТК_УчетнаяПолитикаТорговыхПлощадокСрезПоследних.ТорговаяПлощадка
	|ГДЕ
	|	ТорговыеПлощадки.ПодразделениеКомпании = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Матрица УБЫВ,
	|	Организация,
	|	Склад,
	|	ТорговаяПлощадка
	|ИТОГИ
	|	СУММА(Выбранных)
	|ПО
	|	Матрица,
	|	Организация
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Объект.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	
	РезультатЗапроса = Запрос.Выполнить();	//	РезультатЗапроса.Выгрузить()
	
	ВыборкаМатрица = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаМатрица.Следующий() Цикл
		Сч = 0;
		Выбрана =  ВыборкаМатрица.Выбранных > 0;
		
		стрМатрица = ДеревоТорговыхПлощадок.Строки.Добавить();
		стрМатрица.Выбрана = Выбрана;
		ВыборкаОрганизация = ВыборкаМатрица.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОрганизация.Следующий() Цикл
			стрОрганизация = стрМатрица.Строки.Добавить();
			стрОрганизация.ТорговаяПлощадка = ВыборкаОрганизация.Организация;
			стрОрганизация.Выбрана = Выбрана;
			ВыборкаСклад = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			Пока ВыборкаСклад.Следующий()Цикл 
				стрСклад = стрОрганизация.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(стрСклад,ВыборкаСклад);
				Сч = Сч + 1;
			КонецЦикла;
		КонецЦикла;
		стрМатрица.ТорговаяПлощадка = ?(ВыборкаМатрица.Матрица="","ПустаЯ",ВыборкаМатрица.Матрица.Наименование) + " ("+ВыборкаМатрица.Выбранных+" из "+Сч+")";
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоТорговыхПлощадок,"ВыбранныеТорговыеПлощадки");
	
	ОбщегоНазначения.СообщитьПользователю("Заполнение выполнено!!!");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСпособВычисления(ТипСкидки)
	Возврат ТипСкидки.СпособВычисления; 
КонецФункции

&НаСервере
Процедура УстановитьЗапретНаИзменение()
	тзСкидки = Объект.Скидки.Выгрузить(,"НачалоДействия,КонецДействия");
	тзСкидки.Сортировать("КонецДействия Убыв");
	Если тзСкидки[0].КонецДействия < ТекущаяДата() Тогда
		ЭтаФорма.ТолькоПросмотр = ИСТИНА;
		Элементы.ГруппаТорговыеПлощадки.ТолькоПросмотр = Истина;
	Иначе
		тзСкидки.Сортировать("НачалоДействия");
		Если тзСкидки[0].НачалоДействия < ТекущаяДата() Тогда
			Элементы.ГруппаНомерДатаХоз.ТолькоПросмотр = Истина;
			Элементы.ГруппаТорговыеПлощадки.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТолькоПросмотр(ЭлТЧ, Запрет)
	Для Каждого Колонка Из ЭлТЧ.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(Колонка) = Тип("ГруппаФормы") Тогда
			УстановитьТолькоПросмотр(Колонка,Запрет)
		Иначе
			Колонка.ТолькоПросмотр = НЕ Колонка.Имя = "СкидкиКонецДействия" И Запрет;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл         
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовары = Объект.Скидки.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.ЗначениеСкидки = СтрокаТаблицы.ЗначениеСкидки;
		НоваяСтрокаТовары.СпособВычисления = Перечисления.СкидкиСпособВычисления.ФиксированнаяЦена;
		НоваяСтрокаТовары.ДниНедели = "1111111";
		
		ТоварыДобавлены = Истина;
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьГруппуЦенообразования",	
											Новый Структура("Номенклатура","ГруппаЦенообразования"));											
												
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Скидки") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Скидки, ПараметрыЗаполненияРеквизитов);
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти 


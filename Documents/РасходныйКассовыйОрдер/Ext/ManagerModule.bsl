#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область Проведение


Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);

	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	Контрагент 	= ДополнительныеСвойства.ДляПроведения.Контрагент;
	Договор		= ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
	СтатьяДДС 	= ДополнительныеСвойства.ДляПроведения.СтатьяДДС;	
	ВидДоговора = ДополнительныеСвойства.ДляПроведения.ВидДоговора;
	
	Если ВидДоговора = Перечисления.ВидыДоговоров.Покупка
		ИЛИ ВидДоговора = Перечисления.ВидыДоговоров.Бонус Тогда
		
		ЭтоРасчетСПоставщиком = Истина;
	Иначе
		ЭтоРасчетСПоставщиком = Ложь;
	КонецЕсли;
	
	ДДС_Взаиморасчеты = ДополнительныеСвойства.ДляПроведения.ДДС_Взаиморасчеты;
	ДвиженияВзаиморасчеты = ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(Договор);
	
	ТекстыЗапроса = Новый СписокЗначений;	
	ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры);
	
	Если ДвиженияВзаиморасчеты Тогда 
		
		Если ЭтоРасчетСПоставщиком Тогда 
			ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);			
		Иначе
			ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры);	
		КонецЕсли;
	
	КонецЕсли;
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	Если ДвиженияВзаиморасчеты Тогда 
		Если ЭтоРасчетСПоставщиком Тогда 
			ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПоставщиками(Запрос, ДополнительныеСвойства);					
		Иначе 
			ПартионныйУчет.ПодготовитьТаблицуЗакрытияСделокСПокупателями(Запрос, ДополнительныеСвойства);						
		КонецЕсли;		
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	РасходныйКассовыйОрдер.Дата КАК Период,
	               |	РасходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	РасходныйКассовыйОрдер.ХозОперация КАК ХозОперация,
	               |	РасходныйКассовыйОрдер.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РасходныйКассовыйОрдер.Организация КАК Организация,
	               |	РасходныйКассовыйОрдер.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РасходныйКассовыйОрдер.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РасходныйКассовыйОрдер.Контрагент КАК Контрагент,
	               |	РасходныйКассовыйОрдер.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РасходныйКассовыйОрдер.ДоговорВзаиморасчетов.ВидДоговора.ВидДоговора КАК ВидДоговора,
	               |	РасходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента,
	               |	РасходныйКассовыйОрдер.МоментВремени КАК МоментВремени,
	               |	РасходныйКассовыйОрдер.КурсДокумента КАК КурсДокумента,
	               |	РасходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	               |	РасходныйКассовыйОрдер.ДокументОснование КАК Сделка,
	               |	РасходныйКассовыйОрдер.СтатьяДДС КАК СтатьяДДС,
	               |	РасходныйКассовыйОрдер.СтатьяДДС.Взаиморасчеты КАК ДДС_Взаиморасчеты
	               |ИЗ
	               |	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	               |ГДЕ
	               |	РасходныйКассовыйОрдер.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("РегламентированныйУчет",Реквизиты.РегламентированныйУчет);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ХозОперация", Реквизиты.ХозОперация);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегламентированныйУчет", Реквизиты.РегламентированныйУчет);
	ДополнительныеСвойства.ДляПроведения.Вставить("Контрагент", Реквизиты.Контрагент);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДоговорВзаиморасчетов", Реквизиты.ДоговорВзаиморасчетов);
	ДополнительныеСвойства.ДляПроведения.Вставить("ВидДоговора", Реквизиты.ВидДоговора);
	ДополнительныеСвойства.ДляПроведения.Вставить("СуммаДокумента", Реквизиты.СуммаДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("КурсДокумента", Реквизиты.КурсДокумента);
	ДополнительныеСвойства.ДляПроведения.Вставить("Сделка", Реквизиты.Сделка);
	ДополнительныеСвойства.ДляПроведения.Вставить("СтатьяДДС", Реквизиты.СтатьяДДС);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДДС_Взаиморасчеты", Реквизиты.ДДС_Взаиморасчеты);
КонецПроцедуры


Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	РасходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	РасходныйКассовыйОрдер.Номер КАК Номер,
	               |	РасходныйКассовыйОрдер.Дата КАК Дата,
	               |	РасходныйКассовыйОрдер.Организация КАК Организация,
	               |	РасходныйКассовыйОрдер.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	РасходныйКассовыйОрдер.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	РасходныйКассовыйОрдер.РегламентированныйУчет КАК РегламентированныйУчет,
	               |	РасходныйКассовыйОрдер.Контрагент КАК Контрагент,
	               |	РасходныйКассовыйОрдер.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	РасходныйКассовыйОрдер.КассаКомпании КАК КассаКомпании,
	               |	РасходныйКассовыйОрдер.ХозОперация КАК ХозОперация,
	               |	РасходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	               |	РасходныйКассовыйОрдер.СтавкаНДС КАК СтавкаНДС,
	               |	РасходныйКассовыйОрдер.СуммаНДС КАК СуммаНДС,
	               |	РасходныйКассовыйОрдер.СтатьяДДС КАК СтатьяДДС,
	               |	РасходныйКассовыйОрдер.ВалютаДокумента КАК ВалютаДокумента,
	               |	РасходныйКассовыйОрдер.КурсДокумента КАК КурсДокумента,
	               |	РасходныйКассовыйОрдер.Основание КАК Основание,
	               |	РасходныйКассовыйОрдер.КассаККМ КАК КассаККМ,
	               |	РасходныйКассовыйОрдер.СтаршийКассир КАК СтаршийКассир,
	               |	РасходныйКассовыйОрдер.ДокументОснование КАК ДокументОснование
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	               |ГДЕ
	               |	РасходныйКассовыйОрдер.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДенежныеСредстваКомпании(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДенежныеСредстваКомпании";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.ВалютаДокумента КАК Валюта,
	              |	ВтТаблицаТовары.КассаКомпании КАК СтруктурнаяЕдиница,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ВтТаблицаТовары.СтатьяДДС КАК СтатьяДДС
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.СуммаДокумента * ВтТаблицаТовары.КурсДокумента КАК СуммаБаз,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.ПогашениеКредиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСПокупателями(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПокупателями";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	&Период КАК Период,
	              |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	              |	ВтТаблицаТовары.Контрагент КАК Контрагент,
	              |	ВтТаблицаТовары.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	              |	ВтТаблицаТовары.РегламентированныйУчет КАК РегламентированныйУчет,
	              |	ВтТаблицаТовары.СуммаДокумента КАК Сумма,
	              |	ВтТаблицаТовары.СуммаДокумента * ВтТаблицаТовары.КурсДокумента КАК СуммаБаз,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаиморасчетов.НачислениеДебиторскойЗадолженности) КАК ВидОперации
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти


#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура;
		
	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("ДоговорКонтрагента");

	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.ДоговорКонтрагента = ДополнительныеСвойства.ДляПроведения.ДоговорВзаиморасчетов;
	НоваяСтрока.Период             = ДополнительныеСвойства.ДляПроведения.Дата;
	
	ТаблицыДляПоследовательности.Вставить("Взаиморасчеты", ТаблицаПоследовательности);
	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности", ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти

	
#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Расходный кассовый ордер
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.РасходныйКассовыйОрдер";
    КомандаПечати.Идентификатор = "РасходныйКассовыйОрдер";
    КомандаПечати.Представление = НСтр("ru = 'Расходный кассовый ордер'; uk = 'Расходный касовий ордер'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	//МассивУсловий = Новый Массив;
	//МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "РегламентированныйУчет", Истина, ВидСравнения.Равно));	
	//КомандаПечати.УсловияВидимости = МассивУсловий;

	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Расходный кассовый ордер
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходныйКассовыйОрдер") Тогда    
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РасходныйКассовыйОрдер",
		НСтр("ru = 'Расходный кассовый ордер'; uk = 'Расходный касовий ордер'"), 
		ПечатьРасходныйКассовыйОрдер(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.РасходныйКассовыйОрдер.ПФ_MXL_РасходныйКассовыйОрдер");
	КонецЕсли;	
	
КонецПроцедуры

// Расходный кассовый ордер
Функция ПечатьРасходныйКассовыйОрдер(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	РасходныйКассовыйОрдер.Ссылка КАК Ссылка,
	               |	РасходныйКассовыйОрдер.Дата КАК Дата,
	               |	РасходныйКассовыйОрдер.Номер КАК Номер,
	               |	РасходныйКассовыйОрдер.Организация КАК Организация,
	               |	РасходныйКассовыйОрдер.Организация.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	               |	РасходныйКассовыйОрдер.НомерКО КАК НомерОрдера,
	               |	РасходныйКассовыйОрдер.Организация.НаименованиеПолное КАК ПолноеНаименование,
	               |	РасходныйКассовыйОрдер.СтатьяДДС.КоррCчет КАК Счет,
	               |	РасходныйКассовыйОрдер.СуммаДокумента КАК Сумма,
	               |	РасходныйКассовыйОрдер.Основание КАК Основание,
	               |	РасходныйКассовыйОрдер.Приложение КАК Приложение,
	               |	РасходныйКассовыйОрдер.Директор.ФИО КАК ФИОРуководителя,
	               |	РасходныйКассовыйОрдер.ГлавныйБухгалтер.ФИО КАК ФИОГлавногоБухгалтера,
	               |	РасходныйКассовыйОрдер.СтаршийКассир.ФИО КАК ФИОКассира,
	               |	РасходныйКассовыйОрдер.Выдать КАК Выдать
	               |ИЗ
	               |	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	               |ГДЕ
	               |	РасходныйКассовыйОрдер.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
    ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РКО";
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_КО2");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьШапка 	= Макет.ПолучитьОбласть("Шапка");
	
	ПервыйДокумент = Истина;
	Пока ВыборкаДокументов.Следующий() Цикл		
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		
		ДопПараметрыСтроки = Новый Структура;
		ДопПараметрыСтроки.Вставить("ДатаДокумента", Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
		
		ДопПараметрСуммыПрописью = НСтр("ru = 'гривна,гривны,гривен,ж,копейка, копейки, копеек, ж'; uk = 'гривня,гривні,гривень,ж,копійка, копійки, копійок, ж'", Лев(КодЯзыкаПечать, 2));
		СуммаПрописью = ЧислоПрописью(ВыборкаДокументов.Сумма, "Л='" + КодЯзыкаПечать + "'", ДопПараметрСуммыПрописью);
		
		ДопПараметрыСтроки.Вставить("СуммаПрописью", СуммаПрописью);
		ДопПараметрыСтроки.Вставить("ФИОРуководителя", ОбщегоНазначенияТК.ФамилияИнициалы(ВыборкаДокументов.ФИОРуководителя));
		ДопПараметрыСтроки.Вставить("ФИОБухгалтера",  ОбщегоНазначенияТК.ФамилияИнициалы(ВыборкаДокументов.ФИОГлавногоБухгалтера));
		ДопПараметрыСтроки.Вставить("ФИОКассира",			  ОбщегоНазначенияТК.ФамилияИнициалы(ВыборкаДокументов.ФИОКассира));		
				
		ОбластьШапка.Параметры.Заполнить(ВыборкаДокументов);
		ОбластьШапка.Параметры.Заполнить(ДопПараметрыСтроки);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		// В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции


#КонецОбласти	
		
	
	
	
	
	
#КонецЕсли
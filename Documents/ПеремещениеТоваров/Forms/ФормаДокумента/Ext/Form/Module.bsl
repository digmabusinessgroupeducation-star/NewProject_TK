&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ



#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;	
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;		
	
	УстановитьВидимостьЭлементовПоОперацииСервер();

	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ПеремещениеТоваров"));

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
		// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
	
	УстановитьСостояниеДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЭтотОбъект.Модифицированность Тогда
		СортироватьТЧ_НаСервере();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СобытияТабличнойЧастиТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", ТекущаяСтрока.ЕдиницаИзмерения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
 
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.ХарактеристикаНоменклатуры);		
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики);	
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");		
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;

	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
				
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);
	//ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьЦенуИзСуммы");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");

	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);


КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	

КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
 	ОбработатьВыборНаСервере(ВыбранноеЗначение);         
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборНаСервере(АдресВременногоХранилища) Экспорт
		
	КорзниаТовара =  ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Если КорзниаТовара.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	СтруктураДействий = Новый Структура;

	СтруктураПолейДляЗаполненияЦен = Новый Структура;		
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	
	Для Каждого  СтрТовар Из КорзниаТовара Цикл 
		
		нСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", СтрТовар.Номенклатура));
		
		Если нСтроки.Количество() = 0 Тогда
			СтрокаТоваров  = Объект.Товары.Добавить();
			СтрокаТоваров.Номенклатура   =  СтрТовар.Номенклатура;
			СтрокаТоваров.Количество 	 =  СтрТовар.Количество;
			
		Иначе	
			СтрокаТоваров = нСтроки[0];
			СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрТовар.Количество;
		КонецЕсли;
		
		
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтрокаТоваров.ХарактеристикаНоменклатуры);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтрокаТоваров.ЕдиницаИзмерения);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;

	
КонецПроцедуры



#КонецОбласти


#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура ХозОперацияПриИзменении(Элемент)
	
	ХозОперацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ХозОперацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	
КонецПроцедуры



#КонецОбласти




#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УстановитьЦеныБезХарактеристики();
	
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
											Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));

	//ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакАртикул",
	//										Новый Структура("Номенклатура", "Артикул"));
	
												
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
		
		ЗаполнитьОстатокТовараНаСервере();
	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовПоОперацииСервер()
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваров") Тогда
		
		МассивЭлементовСкрыть = Новый Массив();
		МассивЭлементовПоказать = Новый Массив();
		
		//Если Справочники.СкладыКомпании.ЭтоСкладТНП(Объект.СкладКомпанииПолучатель) Тогда
		//	МассивЭлементовПоказать.Добавить("СтатьяВозврата");
		//Иначе
		//	МассивЭлементовСкрыть.Добавить("СтатьяВозврата");
		//КонецЕсли;
		
		
		МассивЭлементовСкрыть.Добавить("ТорговаяПлощадка");
		МассивЭлементовСкрыть.Добавить("ТорговаяПлощадкаПолучатель");
		МассивЭлементовСкрыть.Добавить("ИДДокументаОтгрузки");
		МассивЭлементовСкрыть.Добавить("ВхДокНомер");
		МассивЭлементовСкрыть.Добавить("ВхДокДата");
		МассивЭлементовПоказать.Добавить("СкладКомпании");
		МассивЭлементовПоказать.Добавить("СкладКомпанииПолучатель");

		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементовПоказать,"Видимость",Истина);	
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементовСкрыть,"Видимость",Ложь);	


	ИначеЕсли Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал") Тогда
		
		МассивЭлементов = Новый Массив();
		МассивЭлементов.Добавить("ТорговаяПлощадка");
		МассивЭлементов.Добавить("СкладКомпанииПолучатель");
		МассивЭлементов.Добавить("ВхДокНомер");
		МассивЭлементов.Добавить("ВхДокДата");
		//МассивЭлементов.Добавить("СтатьяВозврата");

		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементов,"Видимость",Ложь);	
		
		МассивЭлементов = Новый Массив();
		МассивЭлементов.Добавить("СкладКомпании");
		МассивЭлементов.Добавить("ТорговаяПлощадкаПолучатель");
		МассивЭлементов.Добавить("ИДДокументаОтгрузки");
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ИДДокументаОтгрузки","Доступность",Ложь);	
	
		
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементов,"Видимость",Истина);	

		
	ИначеЕсли Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
		
		МассивЭлементов = Новый Массив();
		МассивЭлементов.Добавить("СкладКомпании");
		МассивЭлементов.Добавить("ТорговаяПлощадкаПолучатель");
		//МассивЭлементов.Добавить("СтатьяВозврата");
		
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементов,"Видимость",Ложь);	

		
		МассивЭлементов = Новый Массив();
		МассивЭлементов.Добавить("ТорговаяПлощадка");
		МассивЭлементов.Добавить("СкладКомпанииПолучатель");
		МассивЭлементов.Добавить("ИДДокументаОтгрузки");
		МассивЭлементов.Добавить("ВхДокНомер");
		МассивЭлементов.Добавить("ВхДокДата");

	    ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"ИДДокументаОтгрузки","Доступность",Истина);	

		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементов,"Видимость",Истина);	

	КонецЕсли;
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаОснование", "Видимость", ЗначениеЗаполнено(Объект.ДокументОснование), Истина);
	
КонецПроцедуры

&НаСервере
Процедура ХозОперацияПриИзмененииНаСервере()
	УстановитьВидимостьЭлементовПоОперацииСервер();
	
	УсловиЗполнения = Новый Структура;
	УсловиЗполнения.Вставить("Дата", Объект.Дата);
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал") Тогда
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	ИначеЕсли  Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпанииПолучатель);
	Иначе
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	КонецЕсли;
	
	ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара",УсловиЗполнения);
	
	ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);

КонецПроцедуры

&НаСервере
Процедура СортироватьТЧ_НаСервере()
	ОбработкаТабличнойЧастиСервер.СортироватьТЧ(Объект.Товары);
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныБезХарактеристики()
	
	Если ЗначениеЗаполнено(Объект.СкладКомпанииПолучатель) Тогда 
		
		ЦеныБезХарактеристики = Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(Объект.СкладКомпанииПолучатель, Объект.Дата);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура ИДДокументаОтгрузкиПриИзменении(Элемент)
	//Если НЕ ЗначениеЗаполнено(Объект.ИДДокументаОтгрузки) Тогда
	//	Возврат;
	//КонецЕсли;
	//
	
КонецПроцедуры

&НаКлиенте
Процедура ИДДокументаОтгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.ТорговаяПлощадка) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Поле ""Объект"" не заполнено';uk='Поле ""Об'єкт"" не заповнено'"), Объект.Ссылка, "Объект.ТорговаяПлощадка");
		Возврат;
		
	КонецЕсли;
	
	
	ПараметрыОтбора= Новый Структура;
	ПараметрыОтбора.Вставить("ТорговаяПлощадка",Объект.ТорговаяПлощадка);
	ПараметрыОтбора.Вставить("ТорговаяПлощадкаПолучатель",Объект.ТорговаяПлощадкаПолучатель);
	ПараметрыФормы = Новый Структура("Отбор,Регистратор",ПараметрыОтбора,Объект.Ссылка);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПеремещениеИсходящееВыбрано", ЭтотОбъект);
	
	ОткрытьФорму("Документ.ПеремещениеТоваров.Форма.ФормаВыбораИсходящегоПеремещения",ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТовара(Команда)
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда 
		Склад = Объект.СкладКомпанииПолучатель;
	Иначе 
		Склад = Объект.СкладКомпании;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("СкладКомпании,НайтиПоТочномуСоответствию",Склад,Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбора",ПараметрыФормы,Элементы.Товары,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
&НаКлиенте
Процедура ЗагрузитьТоварыИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "ПеремещениеТоваров.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка списка товаров из файла'; uk = 'Завантаження списка товарів із файлу'");		
	//ПараметрыЗагрузки.ДополнительныеПараметры = Новый структура;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьТоварыИзФайлаЗавершение", ЭтотОбъект);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТоварыИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
    
    Если АдресЗагруженныхДанных = Неопределено Тогда 
        Возврат;
    КонецЕсли;
    
    ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТоварыИзФайлаНаСервере(АдресЗагруженныхДанных)
    
    ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
    ТоварыДобавлены = Ложь;
    Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл         
        Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) ИЛИ СтрокаТаблицы.Количество = 0 Тогда 
            Продолжить;
        КонецЕсли;
        
        НоваяСтрокаТовары = Объект.Товары.Добавить();
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		НоваяСтрокаТовары.Количество = СтрокаТаблицы.Количество;
		
		СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", НоваяСтрокаТовары.ЕдиницаИзмерения);				
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, КэшированныеЗначения); 
		
		ТоварыДобавлены = Истина;
    КонецЦикла;
    
    Если ТоварыДобавлены Тогда
        Модифицированность = Истина;
    КонецЕсли;
    
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

#КонецОбласти

&НаКлиенте
Процедура ПеремещениеИсходящееВыбрано(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ Результат = Неопределено Тогда	
		
		Объект.ДокументОтгрузки = Результат;
		Объект.ИДДокументаОтгрузки = Строка(Результат.УникальныйИдентификатор());

		
		Если Объект.Товары.Количество() = 0 Тогда
			ТекстВопроса = СтрЗаменить(НСтр("ru='Список Товары будет заполнен по документу %Перемещение%. Продолжить?';uk='Список Товари буде заповнений по документу %Перемещение%. Продовжити?'"), "%Перемещение%", Результат);
		Иначе
			ТекстВопроса = СтрЗаменить(НСтр("ru='Список Товары будет перезаполнен из документа %Перемещение%. Продолжить?';uk='Список Товари буде перенаповнений з документа %Перемещение%. Продовжити?'"), "%Перемещение%", Результат);
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнениеПоОснованиюЗавершение", ЭтотОбъект, Новый Структура("ДокументОснование", Результат)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
	
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнениеПоОснованиюЗавершение(РезультатВопроса,ДополнительныеПараметры)Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		 ЗаполнитьТоварыПоДокументуОтгрузки(КэшированныеЗначения);
	КонецЕсли;
	
		
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПоДокументуОтгрузки(КэшированныеЗначения)
	Объект.ВхДокДата  = Объект.ДокументОтгрузки.Дата;
	Объект.ВхДокНомер = Объект.ДокументОтгрузки.Номер;
	Объект.СтатьяВозврата = Объект.ДокументОтгрузки.СтатьяВозврата;

	Объект.Товары.Загрузить(Объект.ДокументОтгрузки.Товары.Выгрузить());
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(Новый Структура("Товары"))
КонецПроцедуры



#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура СкладКомпанииПолучательПриИзменении(Элемент)
	
	СкладПолучательПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТорговаяПлощадкаПолучательПриИзменении(Элемент)
	ТорговаяПлощадкаПолучательПриИзмененииСервер();
КонецПроцедуры

&НаСервере
Процедура ТорговаяПлощадкаПолучательПриИзмененииСервер()
	
	Объект.СкладКомпанииПолучатель = Справочники.СкладыКомпании.ОсновнойСкладТорговойПлощадки(Объект.ТорговаяПлощадкаПолучатель);
	УстановитьЦеныБезХарактеристики();
	
КонецПроцедуры

&НаСервере
Процедура СкладПолучательПриИзмененииСервер()
	
	Объект.ТорговаяПлощадкаПолучатель = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Объект.СкладКомпанииПолучатель);
	
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
		Объект.ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(Объект.СкладКомпанииПолучатель);
	КонецЕсли;
	СтатьяВозвратаВидимость = Ложь;
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваров") И Справочники.СкладыКомпании.ЭтоСкладТНП(Объект.СкладКомпанииПолучатель)  Тогда
		СтатьяВозвратаВидимость = Истина;
	Иначе
		Объект.СтатьяВозврата = Справочники.СтатьиВозврата.ПустаяСсылка();
	КонецЕсли;
		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,"СтатьяВозврата","Видимость",СтатьяВозвратаВидимость);

	
	ЗаполнитьОстатокТовараНаСервере();
	УстановитьЦеныБезХарактеристики();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладКомпанииПриИзменении(Элемент)
	  СкладКомпанииПриИзмененииСервер();
КонецПроцедуры
  
 &НаКлиенте
 Процедура ДатаПриИзменении(Элемент)
	 
	ЗаполнитьОстатокТовараНаСервере();
КонецПроцедуры
 
  
&НаСервере
Процедура СкладКомпанииПриИзмененииСервер()
	Объект.ТорговаяПлощадка = Справочники.СкладыКомпании.ТорговаяПлощадкаСклада(Объект.СкладКомпании);
	Объект.ТипЦен = Справочники.СкладыКомпании.ТипЦенСебестоимости(Объект.СкладКомпании);
	
	ЗаполнитьОстатокТовараНаСервере();
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьОстатокТовараНаСервере()
		
	//УсловиЗполнения = Новый Структура;
	//УсловиЗполнения.Вставить("Дата", Объект.Дата);
	//Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал") Тогда
	//	УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	//ИначеЕсли  Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
	//	УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпанииПолучатель);
	//Иначе
	//	УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	//КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	
	ПараметрыЗаполненияОстатков = Новый Структура("ЗаполнитьОстатокТовара", СтруктураДействий.ЗаполнитьОстаток);
	
	ОбработкаТабличнойЧастиСервер.ЗаполнитьОстаткиТабличнойЧасти(Объект.Товары,Неопределено,ПараметрыЗаполненияОстатков);
	
КонецПроцедуры



#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики)
		
	СтруктураПолейДляЗаполненияЦен = Новый Структура;		
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	Если ЦеныБезХарактеристики Тогда 
		СтруктураДействий.Вставить("ЦеныБезХарактеристики", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, БезУчетаХарактеристик)
	
	УсловиЗполнения = Новый Структура;
	УсловиЗполнения.Вставить("Дата", Объект.Дата);
	Если Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал") Тогда
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	ИначеЕсли  Объект.ХозОперация = ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала") Тогда
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпанииПолучатель);
	Иначе
		УсловиЗполнения.Вставить("СкладКомпании", Объект.СкладКомпании);
	КонецЕсли;
	
	Если БезУчетаХарактеристик Тогда 
		УсловиЗполнения.Вставить("БезУчетаХарактеристик", Истина);
	КонецЕсли;
	
	СтруктураДействий.Вставить("ЗаполнитьОстаток",УсловиЗполнения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействияПриИзмененииКоличества(СтруктураДействий)
	
	//СтруктураДействий.Вставить("ПересчитатьКоличество");
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуВсего");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", "СуммаВсего");
	
КонецПроцедуры       




&НаСервере
Процедура ЗаполнитьХарактеристикиНаСервере()
	
	Документы.ПеремещениеТоваров.ЗаполнитьХарактеристикиПоОстаткамПартий(Объект);
	
	Если Объект.Товары.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");	
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);

	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьХарактеристики(Команда)
	ЗаполнитьХарактеристикиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамСМрцНаСервере()
	
	Запрос=Новый Запрос("ВЫБРАТЬ
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	                    |	СУММА(ВЫБОР
	                    |			КОГДА ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
	                    |				ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
	                    |			ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	                    |		КОНЕЦ) КАК Количество,
	                    |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент КАК Коэффициент,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ИспользованиеХарактеристик КАК ХарактеристикиИспользуются
	                    |ИЗ
	                    |	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	                    |			&Момент,
	                    |			СкладКомпании = &СкладКомпании
	                    |				И НЕ Номенклатура.ВидНоменклатуры = &Услуга) КАК ОстаткиТоваровКомпанииОстатки
	                    |
	                    |СГРУППИРОВАТЬ ПО
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура,
	                    |	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.ИспользованиеХарактеристик
	                    |
	                    |ИМЕЮЩИЕ
	                    |	СУММА(ВЫБОР
	                    |			КОГДА ЕСТЬNULL(ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) = 0
	                    |				ТОГДА ОстаткиТоваровКомпанииОстатки.КоличествоОстаток
	                    |			ИНАЧЕ ОстаткиТоваровКомпанииОстатки.КоличествоОстаток / ОстаткиТоваровКомпанииОстатки.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент
	                    |		КОНЕЦ) > 0
	                    |
	                    |УПОРЯДОЧИТЬ ПО
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура.Наименование,
	                    |	ОстаткиТоваровКомпанииОстатки.Номенклатура");
		
		
		МоментВремени=?(Объект.Ссылка.Пустая(),Новый МоментВремени(КонецДня(Объект.Дата)),Объект.Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("Момент",МоментВремени);
		Если Объект.ХозОперация = Справочники.ХозОперации.ПеремещениеТоваровИзФилиала Тогда
			Запрос.УстановитьПараметр("СкладКомпании",Объект.СкладКомпанииПолучатель);
		Иначе
			Запрос.УстановитьПараметр("СкладКомпании",Объект.СкладКомпании);
		КонецЕсли;
		//Не понятно, зачем условие на услугу - если у нас в остатках их и быть не может, но, видимо, все продумано заранее...
		Запрос.УстановитьПараметр("Услуга", Перечисления.ВидыНоменклатуры.Услуга);
		//Выборка=Запрос.Выполнить().Выбрать();
		Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
		
		СтруктураДействий = Новый Структура;
	//СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу");
	//СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу");
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСПоНоменклатуре");
	
	ДобавитьДействиеЗаполненияОстатка(СтруктураДействий, Объект, ЦеныБезХарактеристики);
 
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект, ЦеныБезХарактеристики);
	ДобавитьДействияПриИзмененииКоличества(СтруктураДействий);
	
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамСМрц(Команда)
	ЗаполнитьПоОстаткамСМрцНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьВОчередьНаСборкуНаСервере()
	
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ  = Объект.Ссылка;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ОбщегоНазначения.СообщитьПользователю("Документ уже добавлен в очередь");
		Иначе
			ЗаписьВОчереди.Документ  = Объект.Ссылка;
			ЗаписьВОчереди.Статус    = Перечисления.СтатусыСборкиРезервов.ГотовКСборке;
			ЗаписьВОчереди.Приоритет = Перечисления.ПриоритетыСборки.ОченьНизкий;
			
			ЗаписьВОчереди.Записать(Истина);
			ОбщегоНазначения.СообщитьПользователю("Документ добавлен в очередь" + Объект.Ссылка);
			
			СтруктураСостояние = Новый  Структура("Состояние,ДатаПоследнегоИзменения,АвторИзменения,ТорговаяПлощадка"
				,Перечисления.СтатусыСборкиРезервов.ГотовКСборке,ТекущаяДатаСеанса(),Пользователи.ТекущийПользователь().ФизическоеЛицо,Объект.ТорговаяПлощадка);
			РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(Объект.Ссылка,СтруктураСостояние);

		КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВОчередьНаСборку(Команда)
	Если ЭтотОбъект.Модифицированность Тогда
		Сообщить("Документ был изменен, запишите документ перед добавлением");
	ИначеЕсли НЕ Объект.Проведен Тогда 
		Сообщить("Проведите документ перед добавлением!!!");
	КонецЕсли;
		
	ДобавитьВОчередьНаСборкуНаСервере();
	
КонецПроцедуры



#КонецОбласти
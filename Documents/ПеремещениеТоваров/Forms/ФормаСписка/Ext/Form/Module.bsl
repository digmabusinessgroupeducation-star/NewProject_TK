

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Элементы.ОтборХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ПеремещениеТоваров"));
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	//Заполним отбор списка
	ПараметрыОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
		ПараметрыОтбора.Вставить("ПериодВыборки", ПериодВыборки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборХозОперация) Тогда 
		ПараметрыОтбора.Вставить("ХозОперация", ОтборХозОперация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПодразделениеКомпании) Тогда 
		ПараметрыОтбора.Вставить("ПодразделениеКомпании", ОтборПодразделениеКомпании);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда 
		ПараметрыОтбора.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборСостояние) Тогда 
		ПараметрыОтбора.Вставить("Состояние", ОтборСостояние);
	КонецЕсли;
	

	УстановитьОтборСписка(Список, ПараметрыОтбора);			
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПериодВыборкиПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ПериодВыборки", ПериодВыборки));
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияПриИзменении(Элемент)

	УстановитьОтборСписка(Список, Новый Структура("ХозОперация", ОтборХозОперация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеКомпанииПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("ПодразделениеКомпании", ОтборПодразделениеКомпании));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборСписка(Список, Новый Структура("Организация", ОтборОрганизация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеПриИзменении(Элемент)
	УстановитьОтборСписка(Список, Новый Структура("Состояние", ОтборСостояние));
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СоздатьПеремещениеТоваровВФилиал(Команда)
	СоздатьПеремещение(ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровВФилиал"));	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПеремещениеИзФилиала(Команда)
	СоздатьПеремещение(ПредопределенноеЗначение("Справочник.ХозОперации.ПеремещениеТоваровИзФилиала"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПеремещение(ХозяйственнаяОперация)
	Основание = Новый Структура("ХозОперация", ХозяйственнаяОперация);
	СтруктураПараметры = Новый Структура("Основание", Основание);
	
	ОткрытьФорму("Документ.ПеремещениеТоваров.ФормаОбъекта", СтруктураПараметры, Элементы.Список);

КонецПроцедуры

&НаСервере
Процедура ДобавитьВОчередьНаСборкуНаСервере()
	
	МассивСтрок = Элементы.Список.ВыделенныеСтроки;
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КолДобавленыхДокументов = 0;
	
	для Каждого  Документ Из МассивСтрок Цикл 
		Количество = НаСборку(Документ);	
		КолДобавленыхДокументов = КолДобавленыхДокументов + Количество;
	КонецЦикла;

	ОбщегоНазначения.СообщитьПользователю("На сборку добавлено "+КолДобавленыхДокументов+" документов!!!");
КонецПроцедуры

&НаСервере
Функция  НаСборку(Документ)
	
		ЗаписьВОчереди = РегистрыСведений.ТК_ОчередьНаСборкуРезервов.СоздатьМенеджерЗаписи();
		ЗаписьВОчереди.Документ  = Документ.Ссылка;
		ЗаписьВОчереди.Прочитать();
		Если ЗаписьВОчереди.Выбран() Тогда
			ОбщегоНазначения.СообщитьПользователю("Документ уже добавлен в очередь "+ Документ.Ссылка);
			
			Возврат 0;
		Иначе
			ЗаписьВОчереди.Документ  = Документ.Ссылка;
			ЗаписьВОчереди.Статус    = Перечисления.СтатусыСборкиРезервов.ГотовКСборке;
			ЗаписьВОчереди.Приоритет = Перечисления.ПриоритетыСборки.ОченьНизкий;
			
			ЗаписьВОчереди.Записать(Истина);
			ОбщегоНазначения.СообщитьПользователю("Документ добавлен в очередь" + Документ.Ссылка);
			
			Возврат 1;
		КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура ДобавитьВОчередьНаСборку(Команда)
	ДобавитьВОчередьНаСборкуНаСервере();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ПериодВыборки = Неопределено;
	Если ПараметрыОтбора.Свойство("ПериодВыборки", ПериодВыборки) Тогда 
		
		ЭлементыОтбора = Список.Отбор.Элементы;
		
		ЭлементОтбораДанных = Неопределено;
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			Если ЭлементОтбора.Представление = "ОтборПериод" Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ПериодВыборки) Тогда 
			
			Если ЭлементОтбораДанных = Неопределено Тогда
				ГруппаОтборПериод = ЭлементыОтбора.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтборПериод.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли; 
				ГруппаОтборПериод.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный; 
				ГруппаОтборПериод.Использование = Истина;
				ГруппаОтборПериод.Представление = "ОтборПериод";
			Иначе
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Истина;
			КонецЕсли;	
			
			ГруппаДатаСортировки = ГруппаОтборПериод.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаДатаСортировки.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ; 
			ГруппаДатаСортировки.Использование = Истина;
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаНачала) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаНачала;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания) Тогда 
				ЭлементОтбораДанных = ГруппаДатаСортировки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
				ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбораДанных.ПравоеЗначение = ПериодВыборки.ДатаОкончания;
				ЭлементОтбораДанных.Использование = Истина;
			КонецЕсли;
			
			Если ГруппаДатаСортировки.Элементы.Количество() = 0 Тогда 
				ГруппаОтборПериод.Элементы.Удалить(ГруппаДатаСортировки);
			КонецЕсли;
			
		Иначе
			
			Если ЭлементОтбораДанных <> Неопределено Тогда
				ГруппаОтборПериод = ЭлементОтбораДанных;
				ГруппаОтборПериод.Элементы.Очистить();
				ГруппаОтборПериод.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Для Каждого текОтбор Из ПараметрыОтбора Цикл 		
		ИмяРеквизита = текОтбор.Ключ;
		Значение = текОтбор.Значение;
		
		Если текОтбор.Ключ = "ПериодВыборки" Тогда 
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, 
			ИмяРеквизита, 
			Значение, 
			ВидСравненияКомпоновкиДанных.Равно,
			, 
			ЗначениеЗаполнено(Значение));
		
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Процедура НазначитьСтатусНаСервере()
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	СтруктураСостояния = Новый Структура;
	СтруктураСостояния.Вставить("Состояние", НовыйСтатус);
	СтруктураСостояния.Вставить("АвторИзменения", Пользователи.АвторизованныйПользователь().ФизическоеЛицо);
	СтруктураСостояния.Вставить("ТорговаяПлощадка", Справочники.ТорговыеПлощадки.ПустаяСсылка());
	СтруктураСостояния.Вставить("ДатаПоследнегоИзменения", ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого Стр Из ВыделенныеСтроки Цикл
		
		РегистрыСведений.ТК_СостояниеДокументовИзменения.ЗаписатьСостояниеДокумента(стр.ссылка,СтруктураСостояния);
		РегистрыСведений.ТК_СостояниеДокументов.УстановитьСостояниеДокумента(стр.ссылка,СтруктураСостояния);
		
	КонецЦикла; 
	УстановитьПривилегированныйРежим(Ложь);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьСтатус(Команда)
	НазначитьСтатусНаСервере();
КонецПроцедуры



#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	
	Если РольДоступна("ПолныеПрава") ИЛИ РольДоступна("ТК_ПравоИзменятьСостояниеДокумента") Тогда
		
		МассивЭлементовПоказать = Новый Массив();
		МассивЭлементовПоказать.Добавить("НовыйСтатус");      
		МассивЭлементовПоказать.Добавить("СписокНазначитьСтатус");      

		ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы,МассивЭлементовПоказать,"Видимость",Истина);	
		
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

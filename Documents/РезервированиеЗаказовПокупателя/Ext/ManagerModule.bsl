#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;	
	ТекстЗапросаРезервыТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателя.Дата КАК Период,
	|	РезервированиеЗаказовПокупателя.Ссылка КАК Ссылка,
	|	РезервированиеЗаказовПокупателя.ХозОперация КАК ХозОперация,
	|	РезервированиеЗаказовПокупателя.Организация КАК Организация,
	|	РезервированиеЗаказовПокупателя.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	РезервированиеЗаказовПокупателя.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	РезервированиеЗаказовПокупателя.СкладКомпании КАК СкладКомпании,
	|	РезервированиеЗаказовПокупателя.Контрагент КАК Контрагент,
	|	РезервированиеЗаказовПокупателя.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	РезервированиеЗаказовПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	|	РезервированиеЗаказовПокупателя.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	|ГДЕ
	|	РезервированиеЗаказовПокупателя.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("СкладКомпании",Реквизиты.СкладКомпании);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("ЗаполненДокументОснование", ?(Реквизиты.ДокументОснование <> Неопределено И Не Реквизиты.ДокументОснование.Пустая(), Истина, Ложь));
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяТовары.Ссылка КАК Ссылка,
	|	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.Организация КАК Организация,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателяТовары.МестоРазмещения = ЗНАЧЕНИЕ(справочник.складыКомпании.пустаяСсылка)
	|			ТОГДА РезервированиеЗаказовПокупателяТовары.Ссылка.СкладКомпании
	|		ИНАЧЕ РезервированиеЗаказовПокупателяТовары.МестоРазмещения
	|	КОНЕЦ КАК СкладКомпании,
	|	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	|	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество,
	|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.Контрагент КАК Контрагент,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	|	РезервированиеЗаказовПокупателяТовары.Ссылка.ХозОперация КАК ХозОперация,
	|	ВЫБОР
	|		КОГДА &ЗаполненДокументОснование
	|			ТОГДА РезервированиеЗаказовПокупателяТовары.Ссылка.ДокументОснование
	|		ИНАЧЕ РезервированиеЗаказовПокупателяТовары.ЗаказПокупателя
	|	КОНЕЦ КАК Заказ
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
	|ГДЕ
	|	РезервированиеЗаказовПокупателяТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРезервыТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РезервыТоваров";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВтТаблицаТовары.СкладКомпании КАК Склад,
	|	ВтТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВтТаблицаТовары.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ВтТаблицаТовары.Заказ КАК Заказ,
	|	ВтТаблицаТовары.Количество * ВтТаблицаТовары.Коэффициент КАК Резерв,
	|	ВтТаблицаТовары.ХозОперация КАК ХозОперация
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары"; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти


#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Резервирование заказов по маршруту
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.РезервированиеЗаказовПокупателя";
	КомандаПечати.Идентификатор = "РезервированиеЗаказовПоМаршруту";
	КомандаПечати.Представление = НСтр("ru = 'Резервирование заказов'; uk = 'Резервування замовлень'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Резервирование заказов по производителям
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.РезервированиеЗаказовПокупателя";
	КомандаПечати.Идентификатор = "РезервированиеЗаказовПоПроизводителям";
	КомандаПечати.Представление = НСтр("ru = 'Резервирование заказов по производителям'; uk = 'Резервування замовлень по виробникам'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	// Резервирование заказов по производителям
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.РезервированиеЗаказовПокупателя";
	КомандаПечати.Идентификатор = "РезервированиеЗаказовПоПроизводителямРаздельно";
	КомандаПечати.Представление = НСтр("ru = 'Резервирование заказов по производителям (на отдельных листах)'; uk = 'Резервування замовлень по виробникам (на окремих сторінках)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 				
	
	// Резервирование заказов (Снеки)
	КомандаПечати = КомандыПечати.Добавить(); 
	КомандаПечати.МенеджерПечати = "Документ.РезервированиеЗаказовПокупателя";
	КомандаПечати.Идентификатор = "РезервированиеЗаказовСнеки";
	КомандаПечати.Представление = НСтр("ru = 'Резервирование заказов (Снеки)'; uk = 'Резервування замовлень (Снеки)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
	КонецЕсли;
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";	
	
	//"РазрешитьЧтение
	//|ГДЕ
	//|    ЧтениеОбъектаРазрешено(ДокументОснование)
	//|    ИЛИ ЧтениеОбъектаРазрешено(РаспределениеОтгрузки.ЗаказПокупателя)
	//|;
	//|РазрешитьИзменениеЕслиРазрешеноЧтение
	//|ГДЕ
	//|    ИзменениеОбъектаРазрешено(ДокументОснование)
	//|    ИЛИ ИзменениеОбъектаРазрешено(РаспределениеОтгрузки.ЗаказПокупателя)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если ТипЗнч(ПараметрыПечати) <> Тип("Структура") Тогда 
		ПараметрыПечати = Новый Структура;
	КонецЕсли;	
	
	// Резервирование заказов по маршруту
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезервированиеЗаказовПоМаршруту") Тогда
		
		ПараметрыПечати.Вставить("КлючПараметровПечати", "РезервированиеЗаказовПоМаршруту");			
		ПараметрыПечати.Вставить("ВыводитьТМ",			Ложь);			
		ПараметрыПечати.Вставить("ТМ_НаНовомЛисте", 	Ложь);
		ПараметрыПечати.Вставить("ВыводитьШК_ФМ", 		Истина);
		ПараметрыПечати.Вставить("ВыводитьДубликат",	Истина);
		
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РезервированиеЗаказовПоМаршруту",
		НСтр("ru = 'Резервирование заказов по маршруту'; uk = 'Резервування замовлень по маршруту'"), 
		ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезервированиеЗаказовПоПроизводителям") Тогда
		
		ПараметрыПечати.Вставить("КлючПараметровПечати", "РезервированиеЗаказовПоПроизводителям");			
		ПараметрыПечати.Вставить("ВыводитьТМ",			Истина);			
		ПараметрыПечати.Вставить("ТМ_НаНовомЛисте", 	Ложь);
		ПараметрыПечати.Вставить("ВыводитьШК_ФМ", 		Ложь);
		ПараметрыПечати.Вставить("ВыводитьДубликат",	Истина);
		
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РезервированиеЗаказовПоПроизводителям",
		НСтр("ru = 'Резервирование заказов по производителям'; uk = 'Резервування замовлень по виробникам'"), 
		ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезервированиеЗаказовПоПроизводителямРаздельно") Тогда
		
		ПараметрыПечати.Вставить("КлючПараметровПечати", "РезервированиеЗаказовПоПроизводителямРаздельно");			
		ПараметрыПечати.Вставить("ВыводитьТМ",			Истина);			
		ПараметрыПечати.Вставить("ТМ_НаНовомЛисте", 	Истина);
		ПараметрыПечати.Вставить("ВыводитьШК_ФМ", 		Ложь);
		ПараметрыПечати.Вставить("ВыводитьДубликат",	Истина);
		
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РезервированиеЗаказовПоПроизводителямРаздельно",
		НСтр("ru = 'Резервирование заказов по производителям (на отдельных листах)'; uk = 'Резервування замовлень по виробникам (на окремих сторінках)'"), 
		ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");				
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезервированиеЗаказовУниверсально") Тогда
		
		ПараметрыПечати.Вставить("КлючПараметровПечати", "РезервированиеЗаказовУниверсально");			
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РезервированиеЗаказовУниверсально",
		НСтр("ru = 'Резервирование заказов'; uk = 'Резервування замовлень'"), 
		ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РезервированиеЗаказовСнеки") Тогда
		
		ПараметрыПечати.Вставить("КлючПараметровПечати", "РезервированиеЗаказовСнеки");			
		ПараметрыПечати.Вставить("ВыводитьТМ",			Ложь);			
		ПараметрыПечати.Вставить("ТМ_НаНовомЛисте", 	Ложь);
		ПараметрыПечати.Вставить("ВыводитьШК_ФМ", 		Истина);
		ПараметрыПечати.Вставить("ВыводитьДубликат",	Истина);
		ПараметрыПечати.Вставить("БазовоеКоличество", 	Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РезервированиеЗаказовСнеки",
		НСтр("ru = 'Резервирование заказов (Снеки)'; uk = 'Резервування замовлень (Снеки)'"), 
		ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),
		,
		"Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");
		
	КонецЕсли;	
	
КонецПроцедуры

// Резервирование заказов по маршруту
Функция ПечатьРезервированиеЗаказовУниверсально(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Порядок 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Порядок", "");
	ВыводитьТМ 			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьТМ", Ложь);
	ТМ_НаНовомЛисте		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ТМ_НаНовомЛисте", Ложь);
	ВыводитьШК_ФМ		= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьШК_ФМ", Ложь);
	ВыводитьДубликат 	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "ВыводитьДубликат", Ложь);
	КлючПараметровПечати= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "КлючПараметровПечати", "РезервированиеЗаказов");
	БазовоеКоличество	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "БазовоеКоличество", Ложь);
	Сборщик				= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПечати, "Сборщик", Справочники.ФизическиеЛица.ПустаяСсылка());
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.РезервРаспечатан;
	
	Если Порядок = "Сигареты" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокСигаретный";
	ИначеЕсли Порядок = "Пиво" Тогда 
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	ПорядокПивной";
	Иначе
		СортировкаЗапроса = "УПОРЯДОЧИТЬ ПО
		|	Дата";
	КонецЕсли;		
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка КАК Ссылка,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	КОЛИЧЕСТВО(РезервированиеЗаказовПокупателяТовары.Номенклатура) КАК Дубли
	               |ПОМЕСТИТЬ Вт_Дубли
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка В(&МассивСсылок)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка КАК Ссылка,
	               |	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Артикул КАК Код,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.НаименованиеПолное КАК ТоварПредставление,
	               |	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество,
	               |	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	               |	РезервированиеЗаказовПокупателяТовары.Количество * РезервированиеЗаказовПокупателяТовары.Коэффициент КАК КоличествоБазовое,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.ТоварнаяМарка В ИЕРАРХИИ (&грТоварныхМарок) КАК ФМ,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.ЕдиницаИзмеренияЯщиков КАК ЕдиницаИзмеренияЯщиков,
	               |	ЕСТЬNULL(РезервированиеЗаказовПокупателяТовары.Номенклатура.ЕдиницаИзмеренияЯщиков.Коэффициент, 0) КАК ЕдиницаИзмеренияЯщиковКоэффициент,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.ЕдиницаИзмеренияУпаковки КАК ЕдиницаИзмеренияУпаковки,
	               |	ЕСТЬNULL(РезервированиеЗаказовПокупателяТовары.Номенклатура.ЕдиницаИзмеренияУпаковки.Коэффициент, 0) КАК ЕдиницаИзмеренияУпаковкиКоэффициент,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	               |	ЕСТЬNULL(РезервированиеЗаказовПокупателяТовары.Номенклатура.ОсновнаяЕдиницаИзмерения.Коэффициент, 0) КАК ОсновнаяЕдиницаИзмеренияКоэффициент,
	               |	ПРЕДСТАВЛЕНИЕССЫЛКИ(ВложенныйЗапрос.МестоХранения) КАК АдресХранения,
	               |	ЕСТЬNULL(ВложенныйЗапрос.МестоХранения.Порядок, 10000000) КАК ПорядокМестоХранения,
	               |	ЕСТЬNULL(ВложенныйЗапрос.Порядок, 0) КАК ПорядокРазмещения,
	               |	ВЫБОР
	               |		КОГДА &ВыводитьТМ
	               |			ТОГДА РезервированиеЗаказовПокупателяТовары.Номенклатура.ТоварнаяМарка.Родитель.НаименованиеПолное
	               |		ИНАЧЕ ВЫРАЗИТЬ("""" КАК СТРОКА(1))
	               |	КОНЕЦ КАК ТоварнаяМарка,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(Вт_Дубли.Дубли, 0) > 1
	               |			ТОГДА ИСТИНА
	               |		ИНАЧЕ ЛОЖЬ
	               |	КОНЕЦ КАК Дубль
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			МАКСИМУМ(ТК_РазмещениеВМестахХранения.МестоХранения) КАК МестоХранения,
	               |			ТК_РазмещениеВМестахХранения.МестоХранения.Владелец КАК Склад,
	               |			ТК_РазмещениеВМестахХранения.Номенклатура КАК Номенклатура,
	               |			МАКСИМУМ(ТК_РазмещениеВМестахХранения.Порядок) КАК Порядок
	               |		ИЗ
	               |			РегистрСведений.ТК_РазмещениеВМестахХранения КАК ТК_РазмещениеВМестахХранения
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ТК_РазмещениеВМестахХранения.Номенклатура,
	               |			ТК_РазмещениеВМестахХранения.МестоХранения.Владелец) КАК ВложенныйЗапрос
	               |		ПО РезервированиеЗаказовПокупателяТовары.Ссылка.СкладКомпании = ВложенныйЗапрос.Склад
	               |			И РезервированиеЗаказовПокупателяТовары.Номенклатура = ВложенныйЗапрос.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Вт_Дубли КАК Вт_Дубли
	               |		ПО РезервированиеЗаказовПокупателяТовары.Ссылка = Вт_Дубли.Ссылка
	               |			И РезервированиеЗаказовПокупателяТовары.Номенклатура = Вт_Дубли.Номенклатура
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателяТовары.Ссылка В(&МассивСсылок)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка,
	               |	ПорядокМестоХранения,
	               |	ПорядокРазмещения,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.ТоварнаяМарка.Родитель,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель.Наименование,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Родитель.Родитель.Родитель.Родитель.Наименование,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Родитель.Родитель.Родитель.Наименование,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Родитель.Родитель.Наименование,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Родитель.Наименование,
	               |	РезервированиеЗаказовПокупателяТовары.Номенклатура.Наименование
	               |ИТОГИ
	               |	СУММА(КоличествоБазовое)
	               |ПО
	               |	Ссылка,
	               |	ТоварнаяМарка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервированиеЗаказовПокупателя.Ссылка КАК Ссылка,
	               |	РезервированиеЗаказовПокупателя.Номер КАК Номер,
	               |	РезервированиеЗаказовПокупателя.Дата КАК Дата,
	               |	РезервированиеЗаказовПокупателя.Организация КАК Организация,
	               |	РезервированиеЗаказовПокупателя.Организация.НаименованиеПолное КАК ПредставлениеПоставщика,
	               |	РезервированиеЗаказовПокупателя.Контрагент КАК Контрагент,
	               |	РезервированиеЗаказовПокупателя.Контрагент.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	               |	РезервированиеЗаказовПокупателя.Контрагент.НаименованиеПолное КАК ПредставлениеПолучателя,
	               |	РезервированиеЗаказовПокупателя.ВалютаДокумента КАК ВалютаДокумента,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки КАК АдресДоставки,
	               |	РезервированиеЗаказовПокупателя.ДокументОснование КАК ЗаказПокупателя,
	               |	РезервированиеЗаказовПокупателя.СкладКомпании КАК ПредставлениеСклада,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки.Порядок КАК ПорядокПивной,
	               |	РезервированиеЗаказовПокупателя.ТочкаДоставки.ПорядокСигаретный КАК ПорядокСигаретный,
	               |	ВЫРАЗИТЬ(ЕСТЬNULL(РезервированиеЗаказовПокупателя.ТочкаДоставки.ПорядокСигаретный, 100000) / 100 КАК ЧИСЛО(5, 0)) КАК Порядок
	               |ИЗ
	               |	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	               |ГДЕ
	               |	РезервированиеЗаказовПокупателя.Ссылка В(&МассивСсылок)" + СортировкаЗапроса + ", Дата, ПредставлениеСклада";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("грТоварныхМарок", Справочники.ТоварныеМарки.НайтиПоНаименованию("Филип Моррис"));	
	Запрос.УстановитьПараметр("ВыводитьТМ", ВыводитьТМ);	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	//Установим блокировку 		
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДополнительныеСведения");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = МассивРезультатов[1];
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Объект", "Ссылка");
	ЭлементБлокировки.УстановитьЗначение("Свойство", Свойство);
	Блокировка.Заблокировать();	

	
	ВыборкаТЧ_Товары = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДокументов = МассивРезультатов[2].Выбрать();
	
	
	//Заполним параметры табличного документа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_" + КлючПараметровПечати;
	
	ТабличныйДокумент.ПолеСверху = 0;
	ТабличныйДокумент.ПолеСлева  = 0;
	ТабличныйДокумент.ПолеСнизу  = 0;
	ТабличныйДокумент.ПолеСправа = 0;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;	
	
	КодЯзыкаПечать = "uk"; //КодЛокализацииИнформационнойБазы();
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РезервированиеЗаказовПокупателя.ПФ_MXL_РезервированиеЗаказовПоМаршруту");
	Макет.КодЯзыка = КодЯзыкаПечать;
	
	ОбластьКолонтитул		= Макет.ПолучитьОбласть("Колонтитул");
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("ЗаголовокQR");
	ОбластьШтрихКод			= Макет.ПолучитьОбласть("ШтрихКод");
	ОбластьПокупатель		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьСклад			= Макет.ПолучитьОбласть("Склад");
	ОбластьМарка			= Макет.ПолучитьОбласть("Марка");
	
	Если Порядок = "Пиво" Тогда
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы1");
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка1");
		ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал1");	
		
	Иначе
		ОбластьСтрока 			= Макет.ПолучитьОбласть("Строка");
		ОбластьШапка 			= Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьПодвал 			= Макет.ПолучитьОбласть("Подвал");	
		
	КонецЕсли;
	
	ОбластьПодвалЗаказа		= Макет.ПолучитьОбласть("ПодвалЗаказа");
	ОбластьПустая			= Макет.ПолучитьОбласть("Пустая");
	
	КартинкаДубликат = БиблиотекаКартинок.Дубликат;
	
	ПервыйДокумент = Истина;
	
	Попытка
		Пока ВыборкаДокументов.Следующий() Цикл
						
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			
			РезервРаспечатан = УправлениеСвойствами.ЗначениеСвойства(ВыборкаДокументов.Ссылка, Свойство);			
			Если Не ЗначениеЗаполнено(РезервРаспечатан) Тогда 
				РезервРаспечатан = Ложь;	
			КонецЕсли;
			
			
			//Выводим верхний колонтитул
			СтруктураКолонтитула = Новый Структура("НомерСтраницы", 1);			
			Если ВыводитьТМ И ТМ_НаНовомЛисте Тогда 
				КоличествоТМ = 0;
				
				ВыборкаТЧ_Товары.Сбросить();
				Пока ВыборкаТЧ_Товары.НайтиСледующий(ВыборкаДокументов.Ссылка, "Ссылка") Цикл 								
					ВыборкаТМ = ВыборкаТЧ_Товары.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					КоличествоТМ = КоличествоТМ + ВыборкаТМ.Количество();
				КонецЦикла;
				СтруктураКолонтитула.Вставить("ВсегоСтраниц", КоличествоТМ);
				
				Если КоличествоТМ > 1 Тогда 
					ОбластьКолонтитул.Параметры.Заполнить(СтруктураКолонтитула);
					ТабличныйДокумент.Вывести(ОбластьКолонтитул);
				КонецЕсли;
			КонецЕсли;
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;								
			
			ЗаголовокЛок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Резервирование заказа №%1 
			|от %2'; uk = 'Резервування замовлення №%1
			|від %2'", КодЯзыкаПечать),
			СокрЛП(ВыборкаДокументов.Номер),
			Формат(ВыборкаДокументов.Дата, "Л="+КодЯзыкаПечать+"; ДЛФ=DD"));
			
			СтруктураШапки = Новый Структура("ПредставлениеПолучателя, АдресДоставки, ЗаказПокупателя, ПредставлениеСклада");
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			СтруктураШапки.Вставить("ТекстЗаголовка", ЗаголовокЛок);
			
			НомерМаршрута = "";
			Попытка
				Если Не ПустаяСтрока(Порядок) И Порядок = "Пиво" Тогда 
					НомерМаршрута = Цел(ВыборкаДокументов.ПорядокПивной / 1000);
				Иначе
					НомерМаршрута = Цел(ВыборкаДокументов.ПорядокСигаретный / 100);
				КонецЕсли;		
			Исключение
			КонецПопытки;
			
			СтруктураШапки.Вставить("Маршрут", "Маршрут " + НомерМаршрута);
			
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураШапки);
			
			ГУИДДокумента = ВыборкаДокументов.Ссылка.УникальныйИдентификатор();		
			Попытка
				ДанныеQRКода = УправлениеПечатью.ДанныеQRКода(Строка(ГУИДДокумента),1,125);
				ОбластьЗаголовок.Рисунки.QRКод.Картинка = Новый Картинка(ДанныеQRКода);
			Исключение КонецПопытки;
			
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);		
			
			//Проверка ФМ
			Если ВыводитьШК_ФМ Тогда 
				ЕстьФМ = Ложь;
				СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();
				
				ВыборкаФМ = МассивРезультатов[1].Выбрать();
				
				ВыборкаФМ.Сбросить();
				Пока ВыборкаФМ.НайтиСледующий(Новый Структура("Ссылка, ФМ", ВыборкаДокументов.Ссылка, Истина)) Цикл 
					ЕстьФМ = Истина;
					
					КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
					ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
					ВыборкаФМ.Номенклатура,
					ВыборкаФМ.КоличествоБазовое,
					ВыборкаФМ.ОсновнаяЕдиницаИзмерения,
					ВыборкаФМ.ЕдиницаИзмеренияЯщиков,
					ВыборкаФМ.ЕдиницаИзмеренияУпаковки,
					ВыборкаФМ.ОсновнаяЕдиницаИзмеренияКоэффициент,
					ВыборкаФМ.ЕдиницаИзмеренияЯщиковКоэффициент,
					ВыборкаФМ.ЕдиницаИзмеренияУпаковкиКоэффициент),
					СтруктураИтоговМест);
					
				КонецЦикла;
				
				Если ЕстьФМ Тогда 
					
					КодДоставки = "0";
					Если ЗначениеЗаполнено(ВыборкаДокументов.АдресДоставки) И ТипЗнч(ВыборкаДокументов.АдресДоставки) = Тип("СправочникСсылка.ТочкиДоставки") Тогда 					
						КодДоставки = Формат(ВыборкаДокументов.АдресДоставки.Global_ID,"ЧЦ=15; ЧГ=");
					КонецЕсли;
					
					
					ИтоговаяСтрока  = ВыборкаДокументов.Номер +"*"+ СокрЛП(ВыборкаДокументов.КодПоЕДРПОУ)+"*"+КодДоставки+"*"+СтруктураИтоговМест.КвоЕдЯщиковВсего+"*"+СтруктураИтоговМест.КвоЕдУпаковкиВсего+"";
					
					Попытка			
						ПараметрыШтрихКода = Новый Структура;
						ПараметрыШтрихКода.Вставить("ТипКода", 4);
						ПараметрыШтрихКода.Вставить("Высота", 60);
						ПараметрыШтрихКода.Вставить("Ширина", 800);
						ПараметрыШтрихКода.Вставить("Штрихкод", ИтоговаяСтрока);
						ПараметрыШтрихКода.Вставить("ОтображатьТекст", Истина);
						ПараметрыШтрихКода.Вставить("ВертикальноеВыравнивание", 2);
						ПараметрыШтрихКода.Вставить("Масштабировать", Истина);
						ПараметрыШтрихКода.Вставить("СохранятьПропорции", Ложь);			
						
						ДанныеШтрихКода = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
						
						ОбластьШтрихКод.Параметры.ШКФМУ = НСтр("ru = 'Штрих - код ФМУ'; uk = 'Штрих - код ФМУ'", КодЯзыкаПечать);
						ОбластьШтрихКод.Рисунки.ШК2.Картинка = ДанныеШтрихКода;
						
						ТабличныйДокумент.Вывести(ОбластьПустая);
						ТабличныйДокумент.Вывести(ОбластьПустая);
						
						ТабличныйДокумент.Вывести(ОбластьШтрихКод);				
					Исключение
						ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
					КонецПопытки;									
					
				КонецЕсли;        
			КонецЕсли;
			
			ОбластьПокупатель.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьПокупатель);
			
			ОбластьСклад.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьСклад);
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			нпп 			= 0;
			ВсегоКоличество = 0;		
			СтруктураИтоговМест = ТК_УправлениеПечатью.ПолучитьСтруктуруИтоговМест();		
			
			ВыборкаТЧ_Товары.Сбросить();			
			нпп = 0;
			Пока ВыборкаТЧ_Товары.НайтиСледующий(ВыборкаДокументов.Ссылка, "Ссылка") Цикл 				
				
				ВыборкаТМ = ВыборкаТЧ_Товары.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				ИтТМ = 0;
				Пока ВыборкаТМ.Следующий() Цикл 
					ИтТМ = ИтТМ + 1;
					
					Если ВыводитьТМ И ЗначениеЗаполнено(ВыборкаТМ.ТоварнаяМарка) Тогда 
						Если ТМ_НаНовомЛисте И ИтТМ > 1 Тогда
							ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
							
							СтруктураКолонтитула.Вставить("НомерСтраницы", ИтТМ);
							ОбластьКолонтитул.Параметры.Заполнить(СтруктураКолонтитула);
							
							ТабличныйДокумент.Вывести(ОбластьКолонтитул);
							ТабличныйДокумент.Вывести(ОбластьЗаголовок);
							ТабличныйДокумент.Вывести(ОбластьПокупатель);
							ТабличныйДокумент.Вывести(ОбластьСклад);
							ТабличныйДокумент.Вывести(ОбластьШапка);
						КонецЕсли;
						
						СтруктураТМ = Новый Структура;
						СтруктураТМ.Вставить("ТоварнаяМарка", ВыборкаТМ.ТоварнаяМарка);
						СтруктураТМ.Вставить("КоличествоМестИтог", ВыборкаТМ.КоличествоБазовое);
						
						ОбластьМарка.Параметры.Заполнить(СтруктураТМ);
						ТабличныйДокумент.Вывести(ОбластьМарка);
					КонецЕсли;
					
					ВыборкаТовары = ВыборкаТМ.Выбрать();					
					
					Пока ВыборкаТовары.Следующий() Цикл 
						нпп = нпп + 1;			
						
						СтруктураСтроки = Новый Структура;
						СтруктураСтроки.Вставить("НомерСтроки", нпп);
						СтруктураСтроки.Вставить("Адрес", ВыборкаТовары.АдресХранения);
						СтруктураСтроки.Вставить("Номенклатура", Строка(ВыборкаТовары.Номенклатура));
						СтруктураСтроки.Вставить("ТоварПредставление", ?(ЗначениеЗаполнено(ВыборкаТовары.ТоварПредставление), СокрЛП(ВыборкаТовары.ТоварПредставление), Строка(ВыборкаТовары.Номенклатура)));
						СтруктураСтроки.Вставить("МРЦ", ВыборкаТовары.ХарактеристикаНоменклатуры);			
						СтруктураСтроки.Вставить("КоличествоПредставление", ?(БазовоеКоличество, ВыборкаТовары.Количество * ВыборкаТовары.Коэффициент, ВыборкаТовары.Количество));
						
						КоличествоМестПредставление = ТК_УправлениеПечатью.ПредставлениеКоличестваМест(
						ТК_УправлениеПечатью.ПолучитьСтруктуруМест(
						ВыборкаТовары.Номенклатура,
						ВыборкаТовары.КоличествоБазовое,
						ВыборкаТовары.ОсновнаяЕдиницаИзмерения,
						ВыборкаТовары.ЕдиницаИзмеренияЯщиков,
						ВыборкаТовары.ЕдиницаИзмеренияУпаковки,
						ВыборкаТовары.ОсновнаяЕдиницаИзмеренияКоэффициент,
						ВыборкаТовары.ЕдиницаИзмеренияЯщиковКоэффициент,
						ВыборкаТовары.ЕдиницаИзмеренияУпаковкиКоэффициент),
						СтруктураИтоговМест);
						
						СтруктураСтроки.Вставить("КоличествоМестПредставление", КоличествоМестПредставление);
						
						Если ВыборкаТовары.Дубль Тогда
							ОбластьСтрока.Область(1,2,1,30).ЦветФона = WebЦвета.Голубой;
						Иначе
							ОбластьСтрока.Области.Строка.ЦветФона = WebЦвета.Белый;
						КонецЕсли;
						
						ОбластьСтрока.Параметры.Заполнить(СтруктураСтроки);
						ТабличныйДокумент.Вывести(ОбластьСтрока);
						
						ВсегоКоличество = ВсегоКоличество + ВыборкаТовары.КоличествоБазовое;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
			СтруктураПодвала = Новый Структура;	
			СтруктураПодвала.Вставить("ВсегоМест", ТК_УправлениеПечатью.ПредставлениеИтоговКоличестваМест(СтруктураИтоговМест, КодЯзыкаПечать));
			СтруктураПодвала.Вставить("ВсегоКво", Формат(ВсегоКоличество, "ЧДЦ=2"));
			Если ЗначениеЗаполнено(Сборщик) Тогда 
				СтруктураПодвала.Вставить("ФИОИсполнителя", Сборщик);
			КонецЕсли;
			
			ОбластьПодвал.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьПодвал);
			ОбластьПодвалЗаказа.Параметры.Заполнить(СтруктураПодвала);
			ТабличныйДокумент.Вывести(ОбластьПодвалЗаказа);
			
			Если ВыводитьДубликат И РезервРаспечатан Тогда
				
				ОбластьВыводаКартинки = ТабличныйДокумент.Область(НомерСтрокиНачало + 3, 22, НомерСтрокиНачало + 17, 30); //ТабличныйДокумент.Область(НомерСтрокиНачало, 1, ТабличныйДокумент.ВысотаТаблицы, 31)			
				РисунокДубликат = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);		
				РисунокДубликат.Картинка = КартинкаДубликат;
				РисунокДубликат.РазмерКартинки = РазмерКартинки.АвтоРазмер;
				РисунокДубликат.Расположить(ОбластьВыводаКартинки);
				РисунокДубликат.Узор = ТипУзораТабличногоДокумента.БезУзора;
				РисунокДубликат.ГраницаСверху = Ложь;
				РисунокДубликат.ГраницаСлева = Ложь;
				РисунокДубликат.ГраницаСправа = Ложь;
				РисунокДубликат.ГраницаСнизу = Ложь;
				РисунокДубликат.ВыводитьНаПечать = Истина;
				
			КонецЕсли;		
			
			Если Не РезервРаспечатан Тогда				
				ОбщегоНазначенияТК.ЗаписатьСвойствоУОбъекта(ВыборкаДокументов.Ссылка, Свойство, Истина);				
			КонецЕсли;		
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
				ТабличныйДокумент, 
				НомерСтрокиНачало, 
				ОбъектыПечати, 
				ВыборкаДокументов.Ссылка);
			
		КонецЦикла;
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());		
		
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
	
	Возврат ТабличныйДокумент;
	
КонецФункции	

#КонецОбласти	


#Область СлужебныеПроцедурыИФункции

Процедура УдалитьСтрокиБезОстатка(ДокументОбъект,Знач МассивСклад=Неопределено)Экспорт
	
	
	СкладДокумента = ДокументОбъект.СкладКомпании;
	Если МассивСклад = Неопределено И ЗначениеЗаполнено(СкладДокумента.ОбщийСклад) Тогда
		МассивСклад = Новый Массив;
		МассивСклад.Добавить(СкладДокумента);
		МассивСклад.Добавить(СкладДокумента.ОбщийСклад);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТоварыДокумента",ДокументОбъект.Товары.Выгрузить());
	ЗапросТекст = 	"ВЫБРАТЬ
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.Коэффициент КАК Коэффициент,
	|	ТоварыДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&ТоварыДокумента КАК ТоварыДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СУММА(ВложенныйЗапрос.КоличествоОстаток) КАК КвоСвободно
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|				,
	|				СкладКомпании = &СкладКомпании
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_Товары.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиТоваровКомпанииОстатки
	|	ГДЕ
	|		ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыТоваровОстатки.Номенклатура,
	|		-РезервыТоваровОстатки.РезервОстаток
	|	ИЗ
	|		РегистрНакопления.РезервыТоваров.Остатки(
	|				,
	|				Склад = &СкладКомпании
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ВТ_Товары.Номенклатура КАК Номенклатура
	|						ИЗ
	|							ВТ_Товары КАК ВТ_Товары)) КАК РезервыТоваровОстатки) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура";
	
	Если МассивСклад = Неопределено Тогда
		Запрос.УстановитьПараметр("СкладКомпании",СкладДокумента);
	Иначе
		ЗапросТекст = СтрЗаменить(ЗапросТекст,"= &СкладКомпании","В (&СкладКомпании)");
		Запрос.УстановитьПараметр("СкладКомпании",МассивСклад);
	КонецЕсли;
	Запрос.Текст = ЗапросТекст;
	
	
	///Получим оперативные остатки
	///Запрос.УстановитьПараметр("НаДату",ДокументОбъект.Дата);
	
	тзТовары = ДокументОбъект.Товары.Выгрузить();
	мНаУдаление = Новый Массив;
	
	тзОстатки = Запрос.Выполнить().Выгрузить();
	
	Для каждого стзТовары Из тзТовары Цикл
		стзОстатки = тзОстатки.Найти(стзТовары.Номенклатура);
		Если стзОстатки=Неопределено ИЛИ стзОстатки.КвоСвободно<=0 тогда
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 - Отказываем в резервировании товара %2 %3 - т.к. его нет на остатках, было %4...'; uk = '%1 - Відмовляємо в резервуванні товару %2 %3 -  тому що його немає на залишках, було %4...'"),
			ДокументОбъект.ДокументОснование,
			стзТовары.Номенклатура.Артикул,
			стзТовары.Номенклатура,
			стзТовары.Количество));
			
			мНаУдаление.Добавить(стзТовары);
			
		иначе
			Если стзТовары.Количество*стзТовары.Коэффициент>стзОстатки.КвоСвободно тогда 
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%5 - Уменьшаем резервируемое количество товара %6 %1 с %2 на %3 %4'; uk = '%5 - Зменшуємо резервовану кількість товару %6 %1 з %2 на %3 %4'"),
				стзТовары.Номенклатура,
				стзТовары.Количество,
				стзОстатки.КвоСвободно/стзТовары.Коэффициент,
				стзТовары.ЕдиницаИзмерения,
				ДокументОбъект.ДокументОснование,
				стзТовары.Номенклатура.Артикул));
				
				стзТовары.Количество = Цел(стзОстатки.КвоСвободно/стзТовары.Коэффициент);
				Если стзТовары.Количество=0 тогда 
					ОбщегоНазначения.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1 - Отказываем в резервировании товара %2 %3 - после округления получилось нулевое количество, было %4...'; uk = '%1 - Відмовляємо в резервуванні товару %2 %3 - після округлення вийшло нульова кількість, було %4 ...'"),
					ДокументОбъект.ДокументОснование,
					стзТовары.Номенклатура.Артикул,
					стзТовары.Номенклатура,
					стзТовары.Количество));
					
					мНаУдаление.Добавить(стзТовары);
				КонецЕсли;
			КонецЕсли;
			стзОстатки.КвоСвободно = стзОстатки.КвоСвободно - стзТовары.Количество*стзТовары.Коэффициент;
		КонецЕсли;
	КонецЦикла;
	Для каждого стзТовары Из мНаУдаление Цикл
		тзТовары.Удалить(стзТовары);
	КонецЦикла;
	
	ДокументОбъект.Товары.Загрузить(тзТовары);
	
КонецПроцедуры

Процедура ОбработатьКвотируемыеПозиции(ДокументОбъект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = Пользователи.ТекущийПользователь();
	НеКонтролироватьКвоты = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"НеКонтролироватьОтгрузкиПоКвотам",Ложь) ИЛИ ДокументОбъект.ДоговорВзаиморасчетов.НеКонтролироватьКвоты;			
	
	Если Не НеКонтролироватьКвоты Тогда 
		ЭтоВнутреннийКонтрагент 		= ЗначениеНастроекПовтИсп.ЭтоВнутреннийКонтрагент(ДокументОбъект.Контрагент);
		НеКонтролироватьКвотыВнКонтр 	= ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"ОтключитьКонтрольКвотДляВнутреннихКонтрагентов",Ложь);	
		
		Если ЭтоВнутреннийКонтрагент И НеКонтролироватьКвотыВнКонтр Тогда 
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;	
	
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	 
	
	тчТовары = ДокументОбъект.Товары.Выгрузить(); 
	тчТовары.Индексы.Добавить("Номенклатура");
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	|	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество
	|ПОМЕСТИТЬ тчТовары
	|ИЗ
	|	&ТаблицаТоваров КАК РезервированиеЗаказовПокупателяТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчТовары.НомерСтроки КАК НомерСтроки,
	|	тчТовары.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(тчТовары.Номенклатура КАК Справочник.Номенклатура).Производитель КАК Производитель,
	|	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	тчТовары.Количество * тчТовары.Коэффициент КАК Количество
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	тчТовары КАК тчТовары";
	
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;	
	Запрос.УстановитьПараметр("ТаблицаТоваров", тчТовары);		
	Запрос.Выполнить();
	
	ДополнительныеСвойства = Новый Структура;
	ДополнительныеСвойства.Вставить("ВидКвоты", УправлениеСвойствами.ЗначениеСвойства(ДокументОбъект.ТочкаДоставки, "ВидКвотыОтгрузки"));
	ДополнительныеСвойства.Вставить("ВидКвотыФМУ", УправлениеСвойствами.ЗначениеСвойства(ДокументОбъект.ТочкаДоставки, "ВидКвотыОтгрузкиФМУ"));
	ДополнительныеСвойства.Вставить("Область", ЗначениеНастроекПовтИсп.ПолучитьОбластьТочкиДоставки(ДокументОбъект.ТочкаДоставки));
	
	ПродажиСервер.ТаблицаОграниченийПоКвотамОтгрузки(ДокументОбъект.Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц);
	
	ЕстьПревышениеКвот = Ложь;
	ТаблицаПревышенияКвот = ПродажиСервер.ЗаполнитьТаблицуОшибокПоОграничениямКвот(МенеджерВременныхТаблиц, ЕстьПревышениеКвот);
	
	Если Не ЕстьПревышениеКвот Тогда 
		Возврат;
	КонецЕсли;
	
	мСтрокиУдалить = Новый Массив;
	
	Для Каждого СтрокаКвота Из ТаблицаПревышенияКвот Цикл 
		
		КоличествоБаз = 0;
		МаксКолвоБаз = Макс(Цел(((СтрокаКвота.ИтогоПоПроизводителю - СтрокаКвота.Количество)* СтрокаКвота.Процент)/(100 - СтрокаКвота.Процент)), СтрокаКвота.Порог);
		
		нСтроки = тчТовары.НайтиСтроки(Новый Структура("Номенклатура", СтрокаКвота.Номенклатура));		
		Для Каждого текСтрока Из нСтроки Цикл
			
			//ОбщегоНазначения.СообщитьПользователю(
			//СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			//НСтр("ru = 'Превышена квота по номенклатуре <%1> текущее кол-во: %2 (%3 %), макс кол-во: %4'; uk = 'Перевищена квота по номенклатурі <%1> поточний кол-во: %2 (%3 %), макс кол-во: %4'"),
			//текСтрока.Номенклатура,
			//текСтрока.Количество,
			//СтрокаКвота.текПроцент,
			//МаксКолвоБаз));
			
			Остаток = МаксКолвоБаз - КоличествоБаз;			
			тКоэффициент = ?(текСтрока.Коэффициент = 0,1,текСтрока.Коэффициент);
			КолВо = Цел(Мин(текСтрока.Количество * тКоэффициент, Остаток)/тКоэффициент);					
			
			Если Остаток > 0 И КолВо > 0 Тогда
				Если КолВо <> текСтрока.Количество Тогда 
					текСтрока.Количество = КолВо;
				КонецЕсли;
				
				КоличествоБаз = КоличествоБаз + КолВо * тКоэффициент;
			Иначе
				мСтрокиУдалить.Добавить(текСтрока);
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Уменьшаем по квотам с %1 до %2 %3 товара %4, %5'; uk = 'Зменшуємо за квотами з %1 до %2 %3 товара %4, %5'"),
			СтрокаКвота.Количество/тКоэффициент,
			текСтрока.Количество,
			текСтрока.ЕдиницаИзмерения,
			текСтрока.Номенклатура,
			текСтрока.ХарактеристикаНоменклатуры));
			
		КонецЦикла;		
	КонецЦикла;
	
	Для Каждого СтрУдалить Из мСтрокиУдалить Цикл 
		тчТовары.Удалить(СтрУдалить);
	КонецЦикла;
	
	ДокументОбъект.Товары.Загрузить(тчТовары);
	
КонецПроцедуры

//Vov41k 03.07.2023
// Процедура убирает товары по квотам в рамках заказов
// пока не используется
Процедура ОбработатьКвотируемыеПозицииПоЗаказам(ДокументОбъект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = Пользователи.ТекущийПользователь();
	НеКонтролироватьКвоты = ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"НеКонтролироватьОтгрузкиПоКвотам",Ложь) ИЛИ ДокументОбъект.ДоговорВзаиморасчетов.НеКонтролироватьКвоты;			
	
	Если Не НеКонтролироватьКвоты Тогда 
		ЭтоВнутреннийКонтрагент 		= ЗначениеНастроекПовтИсп.ЭтоВнутреннийКонтрагент(ДокументОбъект.Контрагент);
		НеКонтролироватьКвотыВнКонтр 	= ЗначениеНастроекПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь,"ОтключитьКонтрольКвотДляВнутреннихКонтрагентов",Ложь);	
		
		Если ЭтоВнутреннийКонтрагент И НеКонтролироватьКвотыВнКонтр Тогда 
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;	
	
	тчТовары = ДокументОбъект.Товары.Выгрузить(); 
	
	Для Каждого Стр Из тчТовары Цикл 
		Если Стр.ЗаказПокупателя.Пустая() Тогда 
			Стр.ЗаказПокупателя = ДокументОбъект.ДокументОснование;
		КонецЕсли;
	КонецЦикла;		
	
	тчТовары.Индексы.Добавить("Номенклатура, ЗаказПокупателя");
	
	МассивЗаказов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(тчТовары.ВыгрузитьКолонку("ЗаказПокупателя"));	
	
	ДополнительныеСвойства = Новый Структура;
	ДополнительныеСвойства.Вставить("ВидКвоты", УправлениеСвойствами.ЗначениеСвойства(ДокументОбъект.ТочкаДоставки, "ВидКвотыОтгрузки"));
	ДополнительныеСвойства.Вставить("ВидКвотыФМУ", УправлениеСвойствами.ЗначениеСвойства(ДокументОбъект.ТочкаДоставки, "ВидКвотыОтгрузкиФМУ"));
	ДополнительныеСвойства.Вставить("Область", ЗначениеНастроекПовтИсп.ПолучитьОбластьТочкиДоставки(ДокументОбъект.ТочкаДоставки));
	
	Для Каждого ЗаказПокупателя Из МассивЗаказов Цикл 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
		|	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
		|	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
		|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество
		|ПОМЕСТИТЬ тчТовары
		|ИЗ
		|	&ТаблицаТоваров КАК РезервированиеЗаказовПокупателяТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тчТовары.НомерСтроки КАК НомерСтроки,
		|	тчТовары.Номенклатура КАК Номенклатура,
		|	ВЫРАЗИТЬ(тчТовары.Номенклатура КАК Справочник.Номенклатура).Производитель КАК Производитель,
		|	тчТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	тчТовары.Количество * тчТовары.Коэффициент КАК Количество
		|ПОМЕСТИТЬ ВтТаблицаТоваров
		|ИЗ
		|	тчТовары КАК тчТовары";
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	 
		ВременнаяТаблица = тчТовары.Скопировать(Новый Структура("ЗаказПокупателя", ЗаказПокупателя));
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;	
		Запрос.УстановитьПараметр("ТаблицаТоваров", ВременнаяТаблица);		
		Запрос.Выполнить();
		
		ПродажиСервер.ТаблицаОграниченийПоКвотамОтгрузки(ДокументОбъект.Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц);
		
		ЕстьПревышениеКвот = Ложь;
		ТаблицаПревышенияКвот = ПродажиСервер.ЗаполнитьТаблицуОшибокПоОграничениямКвот(МенеджерВременныхТаблиц, ЕстьПревышениеКвот);
		
		Если Не ЕстьПревышениеКвот Тогда 
			Продолжить;
		КонецЕсли;
		
		мСтрокиУдалить = Новый Массив;
		
		Для Каждого СтрокаКвота Из ТаблицаПревышенияКвот Цикл 
			
			КоличествоБаз = 0;
			МаксКолвоБаз = Макс(Цел(((СтрокаКвота.ИтогоПоПроизводителю - СтрокаКвота.Количество)* СтрокаКвота.Процент)/(100 - СтрокаКвота.Процент)), СтрокаКвота.Порог);
			
			нСтроки = тчТовары.НайтиСтроки(Новый Структура("Номенклатура,ЗаказПокупателя", СтрокаКвота.Номенклатура, ЗаказПокупателя));		
			Для Каждого текСтрока Из нСтроки Цикл
				
				Остаток = МаксКолвоБаз - КоличествоБаз;			
				тКоэффициент = ?(текСтрока.Коэффициент = 0,1,текСтрока.Коэффициент);
				КолВо = Цел(Мин(текСтрока.Количество * тКоэффициент, Остаток)/тКоэффициент);					
				
				Если Остаток > 0 И КолВо > 0 Тогда
					Если КолВо <> текСтрока.Количество Тогда 
						текСтрока.Количество = КолВо;
					КонецЕсли;
					
					КоличествоБаз = КоличествоБаз + КолВо * тКоэффициент;
				Иначе
					мСтрокиУдалить.Добавить(текСтрока);
				КонецЕсли;
				
				ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Уменьшаем по квотам с %1 до %2 %3 товара %4, %5 (%6)'; uk = 'Зменшуємо за квотами з %1 до %2 %3 товара %4, %5 (%6)'"),
				СтрокаКвота.Количество/тКоэффициент,
				текСтрока.Количество,
				текСтрока.ЕдиницаИзмерения,
				текСтрока.Номенклатура,
				текСтрока.ХарактеристикаНоменклатуры,
				ЗаказПокупателя));
				
			КонецЦикла;		
		КонецЦикла;
		
		Для Каждого СтрУдалить Из мСтрокиУдалить Цикл 
			тчТовары.Удалить(СтрУдалить);
		КонецЦикла;
	КонецЦикла;
	
	ДокументОбъект.Товары.Загрузить(тчТовары);
	
КонецПроцедуры

Процедура ЗаполнитьХарактеристикиПоОстаткамПартий_OLD(ДокументОбъект) Экспорт 
	
	Если Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ДокументОбъект.СкладКомпании, ДокументОбъект.Дата) Тогда 
		Возврат;
	КонецЕсли;
	
	ТаблЧастьТовары = ДокументОбъект.Товары;
	
	Запрос = Новый Запрос;
	//Запрос.УстановитьПараметр("МоментВремени", ДокументОбъект.МоментВремени());
	Запрос.УстановитьПараметр("Склад", ДокументОбъект.СкладКомпании);
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("тчТовары", ТаблЧастьТовары.Выгрузить());
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	|	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество
	|ПОМЕСТИТЬ вт_Товары
	|ИЗ
	|	&тчТовары КАК РезервированиеЗаказовПокупателяТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(вт_Товары.НомерСтроки) КАК нСтр,
	|	вт_Товары.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
	|	вт_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	вт_Товары.Коэффициент КАК Коэффициент,
	|	СУММА(вт_Товары.Количество) КАК Количество,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ) КАК ХарактеристикиИспользуются
	|ПОМЕСТИТЬ сверн_Товары
	|ИЗ
	|	вт_Товары КАК вт_Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Товары.Номенклатура,
	|	вт_Товары.ЕдиницаИзмерения,
	|	вт_Товары.Коэффициент,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры.Сортировка КАК Сортировка,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ вт_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|			,
	|			СкладКомпании = &Склад
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						сверн_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						сверн_Товары КАК сверн_Товары
	|					ГДЕ
	|						сверн_Товары.ХарактеристикиИспользуются)) КАК ОстаткиТоваровКомпанииОстатки
	|ГДЕ
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервыТоваровОстатки.Номенклатура КАК Номенклатура,
	|	РезервыТоваровОстатки.Характеристика КАК Характеристика,
	|	РезервыТоваровОстатки.РезервОстаток КАК РезервОстаток
	|ПОМЕСТИТЬ вт_Резервы
	|ИЗ
	|	РегистрНакопления.РезервыТоваров.Остатки(
	|			,
	|			Склад = &Склад
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						сверн_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						сверн_Товары КАК сверн_Товары
	|					ГДЕ
	|						сверн_Товары.ХарактеристикиИспользуются)) КАК РезервыТоваровОстатки
	|ГДЕ
	|	РезервыТоваровОстатки.РезервОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Остатки.Номенклатура КАК Номенклатура,
	|	вт_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	вт_Остатки.КоличествоОстаток - ЕСТЬNULL(вт_Резервы.РезервОстаток, 0) КАК КолВоСвободно
	|ИЗ
	|	вт_Остатки КАК вт_Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Резервы КАК вт_Резервы
	|		ПО вт_Остатки.Номенклатура = вт_Резервы.Номенклатура
	|			И вт_Остатки.ХарактеристикаНоменклатуры = вт_Резервы.Характеристика
	|ГДЕ
	|	вт_Остатки.КоличествоОстаток - ЕСТЬNULL(вт_Резервы.РезервОстаток, 0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	вт_Остатки.Номенклатура,
	|	вт_Остатки.Сортировка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	сверн_Товары.нСтр КАК нСтр,
	|	сверн_Товары.Номенклатура КАК Номенклатура,
	|	сверн_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	сверн_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	сверн_Товары.Коэффициент КАК Коэффициент,
	|	сверн_Товары.Количество КАК Количество,
	|	сверн_Товары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
	|ИЗ
	|	сверн_Товары КАК сверн_Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	нСтр";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	тзХарактеристики = МассивРезультатов[4].Выгрузить();
	тзХарактеристики.Индексы.Добавить("Номенклатура");
	
	тзТовары = МассивРезультатов[5].Выгрузить();
	
	ТаблЧастьТовары.Очистить();
	
	Для Каждого стрТовары Из тзТовары Цикл 
		текКоэффициент = ?(стрТовары.Коэффициент = 0, 1, стрТовары.Коэффициент);
		КолВоСписатьБаз = стрТовары.Количество * текКоэффициент;
		
		нСтроки = тзХарактеристики.НайтиСтроки(Новый Структура("Номенклатура", стрТовары.Номенклатура));	
		Для Каждого стрХарактеристика Из нСтроки Цикл 
			текСвободно = Цел(стрХарактеристика.КолВоСвободно / текКоэффициент) * текКоэффициент;
			Если текСвободно < 1 Тогда 
				Продолжить;
			КонецЕсли;
			
			КолВоСписать = Цел(Мин(КолВоСписатьБаз, текСвободно)/текКоэффициент);
			Если КолВоСписать > 0 Тогда 
				НоваяСтрока = ТаблЧастьТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
				НоваяСтрока.ХарактеристикаНоменклатуры = стрХарактеристика.ХарактеристикаНоменклатуры;
				НоваяСтрока.Количество = КолВоСписать;
				
				КолВоСписатьБаз = КолВоСписатьБаз - КолВоСписать * текКоэффициент;
				стрХарактеристика.КолВоСвободно = текСвободно - КолВоСписать * текКоэффициент;
			КонецЕсли;
			
			Если КолВоСписатьБаз < 1 Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;			
		
		Если стрТовары.ХарактеристикиИспользуются = Ложь Тогда
			Если КолВоСписатьБаз > 0 Тогда 
				КолВоСписать = Цел(КолВоСписатьБаз/текКоэффициент);
				Если КолВоСписать > 0 Тогда 
					НоваяСтрока = ТаблЧастьТовары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
					НоваяСтрока.Количество = КолВоСписать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьХарактеристикиПоОстаткамПартий(ДокументОбъект,Знач МассивСклад=Неопределено) Экспорт 
	//
	//Если Справочники.СкладыКомпании.НеВестиУчетПоХарактеристикамНаСкладе(ДокументОбъект.СкладКомпании, ДокументОбъект.Дата) Тогда 
	//	Возврат;
	//КонецЕсли;
	
	ТолькоОдноМРЦ = "43094673" =ДокументОбъект.Контрагент.КодПоЕДРПОУ;	// пока так для РОСТа (Пилот ДЛК ООО)	//	Задача № 20873
	
	СкладДокумента = ДокументОбъект.СкладКомпании;
	Если МассивСклад = Неопределено И ЗначениеЗаполнено(СкладДокумента.ОбщийСклад) Тогда
		МассивСклад = Новый Массив;
		МассивСклад.Добавить(СкладДокумента);
		МассивСклад.Добавить(СкладДокумента.ОбщийСклад);
	КонецЕсли;
	
	ТаблЧастьТовары = ДокументОбъект.Товары;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("тчТовары", ТаблЧастьТовары.Выгрузить());
	
	ЗапросТекст = "ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателяТовары.ЗаказПокупателя КАК ЗаказПокупателя,
	|	РезервированиеЗаказовПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	РезервированиеЗаказовПокупателяТовары.Номенклатура КАК Номенклатура,
	|	РезервированиеЗаказовПокупателяТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РезервированиеЗаказовПокупателяТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РезервированиеЗаказовПокупателяТовары.Коэффициент КАК Коэффициент,
	|	РезервированиеЗаказовПокупателяТовары.Количество КАК Количество
	|ПОМЕСТИТЬ вт_Товары
	|ИЗ
	|	&тчТовары КАК РезервированиеЗаказовПокупателяТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(вт_Товары.НомерСтроки) КАК нСтр,
	|	вт_Товары.ЗаказПокупателя КАК ЗаказПокупателя,
	|	вт_Товары.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНоменклатуры,
	|	вт_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	вт_Товары.Коэффициент КАК Коэффициент,
	|	СУММА(вт_Товары.Количество) КАК Количество,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ) КАК ХарактеристикиИспользуются
	|ПОМЕСТИТЬ сверн_Товары
	|ИЗ
	|	вт_Товары КАК вт_Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	вт_Товары.Номенклатура,
	|	вт_Товары.ЕдиницаИзмерения,
	|	вт_Товары.Коэффициент,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(вт_Товары.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик, ЛОЖЬ),
	|	вт_Товары.ЗаказПокупателя
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровКомпанииОстатки.СкладКомпании КАК СкладКомпании,
	|	ОстаткиТоваровКомпанииОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиТоваровКомпанииОстатки.ХарактеристикаНоменклатуры.Сортировка КАК Сортировка,
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ вт_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваровКомпании.Остатки(
	|			,
	|			СкладКомпании = &Склад
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						сверн_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						сверн_Товары КАК сверн_Товары)) КАК ОстаткиТоваровКомпанииОстатки
	|ГДЕ
	|	ОстаткиТоваровКомпанииОстатки.КоличествоОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервыТоваровОстатки.Склад КАК СкладКомпании,
	|	РезервыТоваровОстатки.Номенклатура КАК Номенклатура,
	|	РезервыТоваровОстатки.Характеристика КАК Характеристика,
	|	РезервыТоваровОстатки.РезервОстаток КАК РезервОстаток
	|ПОМЕСТИТЬ вт_Резервы
	|ИЗ
	|	РегистрНакопления.РезервыТоваров.Остатки(
	|			,
	|			Склад = &Склад
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						сверн_Товары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						сверн_Товары КАК сверн_Товары)) КАК РезервыТоваровОстатки
	|ГДЕ
	|	РезервыТоваровОстатки.РезервОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Остатки.СкладКомпании КАК МестоРазмещения,
	|	вт_Остатки.Номенклатура КАК Номенклатура,
	|	вт_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	вт_Остатки.КоличествоОстаток - ЕСТЬNULL(вт_Резервы.РезервОстаток, 0) КАК КолВоСвободно
	|ИЗ
	|	вт_Остатки КАК вт_Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Резервы КАК вт_Резервы
	|		ПО вт_Остатки.Номенклатура = вт_Резервы.Номенклатура
	|			И вт_Остатки.ХарактеристикаНоменклатуры = вт_Резервы.Характеристика
	|			И вт_Остатки.СкладКомпании = вт_Резервы.СкладКомпании
	|ГДЕ
	|	вт_Остатки.КоличествоОстаток - ЕСТЬNULL(вт_Резервы.РезервОстаток, 0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	вт_Остатки.Номенклатура,
	|	вт_Остатки.Сортировка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	сверн_Товары.нСтр КАК нСтр,
	|	сверн_Товары.ЗаказПокупателя КАК ЗаказПокупателя,
	|	сверн_Товары.Номенклатура КАК Номенклатура,
	|	сверн_Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	сверн_Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	сверн_Товары.Коэффициент КАК Коэффициент,
	|	сверн_Товары.Количество КАК Количество,
	|	сверн_Товары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
	|ИЗ
	|	сверн_Товары КАК сверн_Товары
	|
	|УПОРЯДОЧИТЬ ПО
	|	нСтр";
	
	
	Если МассивСклад = Неопределено Тогда
		Запрос.УстановитьПараметр("Склад",СкладДокумента);
	Иначе
		ЗапросТекст = СтрЗаменить(ЗапросТекст,"= &Склад","В (&Склад)");
		Запрос.УстановитьПараметр("Склад",МассивСклад);
	КонецЕсли;
	Запрос.Текст = ЗапросТекст;
	
	//Запрос.УстановитьПараметр("Склад", ДокументОбъект.СкладКомпании);
	
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	тзХарактеристики = МассивРезультатов[4].Выгрузить();
	тзХарактеристики.Индексы.Добавить("Номенклатура");
	
	тзТовары = МассивРезультатов[5].Выгрузить();
	
	ТаблЧастьТовары.Очистить();
	
	Для Каждого стрТовары Из тзТовары Цикл 
		текКоэффициент = ?(стрТовары.Коэффициент = 0, 1, стрТовары.Коэффициент);
		КолВоСписатьБаз = стрТовары.Количество * текКоэффициент;
		
		МаксСвободно = 0;
		стрХарактеристикаСБольшимКолво = 0;
		нСтроки = тзХарактеристики.НайтиСтроки(Новый Структура("Номенклатура", стрТовары.Номенклатура));	
		Для Каждого стрХарактеристика Из нСтроки Цикл 
			текСвободно = Цел(стрХарактеристика.КолВоСвободно / текКоэффициент) * текКоэффициент;
			Если текСвободно < 1 Тогда 
				Продолжить;
			КонецЕсли;
			
			Если ТолькоОдноМРЦ Тогда
				//КолВоСписать = Цел(Макс(КолВоСписатьБаз, текСвободно)/текКоэффициент);
				Если текСвободно < КолВоСписатьБаз Тогда
					Если текСвободно > МаксСвободно Тогда
						МаксСвободно = текСвободно;
						стрХарактеристикаСБольшимКолво = стрХарактеристика;
					КонецЕсли;
					//Продолжить;
				Иначе
					КолВоСписать = КолВоСписатьБаз / текКоэффициент;
					НоваяСтрока = ТаблЧастьТовары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
					НоваяСтрока.ХарактеристикаНоменклатуры = стрХарактеристика.ХарактеристикаНоменклатуры;
					НоваяСтрока.МестоРазмещения = стрХарактеристика.МестоРазмещения;
					НоваяСтрока.Количество = КолВоСписать;
					
					КолВоСписатьБаз = КолВоСписатьБаз - КолВоСписать * текКоэффициент;
					стрХарактеристика.КолВоСвободно = текСвободно - КолВоСписать * текКоэффициент;
					Прервать;
				КонецЕсли;
			Иначе
				КолВоСписать = Цел(Мин(КолВоСписатьБаз, текСвободно)/текКоэффициент);
				Если КолВоСписать > 0 Тогда 
					НоваяСтрока = ТаблЧастьТовары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
					НоваяСтрока.ХарактеристикаНоменклатуры = стрХарактеристика.ХарактеристикаНоменклатуры;
					НоваяСтрока.МестоРазмещения = стрХарактеристика.МестоРазмещения;
					НоваяСтрока.Количество = КолВоСписать;
					
					КолВоСписатьБаз = КолВоСписатьБаз - КолВоСписать * текКоэффициент;
					стрХарактеристика.КолВоСвободно = текСвободно - КолВоСписать * текКоэффициент;
				КонецЕсли;
				Если КолВоСписатьБаз < 1 Тогда 
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ТолькоОдноМРЦ И КолВоСписатьБаз > 0 И стрХарактеристикаСБольшимКолво <> 0 Тогда
			КолВоСписать = Цел(Мин(КолВоСписатьБаз, текСвободно)/текКоэффициент);
			НоваяСтрока = ТаблЧастьТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
			НоваяСтрока.ХарактеристикаНоменклатуры = стрХарактеристикаСБольшимКолво.ХарактеристикаНоменклатуры;
			НоваяСтрока.МестоРазмещения = стрХарактеристикаСБольшимКолво.МестоРазмещения;
			НоваяСтрока.Количество = КолВоСписать;
			
			КолВоСписатьБаз = КолВоСписатьБаз - КолВоСписать * текКоэффициент;
			стрХарактеристика.КолВоСвободно = текСвободно - КолВоСписать * текКоэффициент;
		КонецЕсли;
		
		
		//Если стрТовары.ХарактеристикиИспользуются = Ложь Тогда
		//	Если КолВоСписатьБаз > 0 Тогда 
		//		КолВоСписать = Цел(КолВоСписатьБаз/текКоэффициент);
		//		Если КолВоСписать > 0 Тогда 
		//			НоваяСтрока = ТаблЧастьТовары.Добавить();
		//			ЗаполнитьЗначенияСвойств(НоваяСтрока, стрТовары);
		//			НоваяСтрока.Количество = КолВоСписать;
		//		КонецЕсли;
		//	КонецЕсли;
		//КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти


#Область ВыгрузкаEDI

Функция СформироватьСтруктуру_ORDRSP(ДокументСсылка) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Резерв",ДокументСсылка);
	Запрос.УстановитьПараметр("Заказ",ДокументСсылка.ДокументОснование);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РезервированиеЗаказовПокупателя.Номер КАК NUMBER,
	|	РезервированиеЗаказовПокупателя.Дата КАК DATE,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателя.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ.ЗаказПокупателя).ИДДокументаОтгрузки
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERNUMBER,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателя.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ.ЗаказПокупателя).ДатаСоздания
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ORDERDATE,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателя.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ.ЗаказПокупателя).СрокПоставки
	|		ИНАЧЕ РезервированиеЗаказовПокупателя.Дата
	|	КОНЕЦ КАК DELIVERYDATE,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателя.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ.ЗаказПокупателя).Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК НомерЗаказа,
	|	ВЫБОР
	|		КОГДА РезервированиеЗаказовПокупателя.ДокументОснование ССЫЛКА Документ.ЗаказПокупателя
	|			ТОГДА ВЫРАЗИТЬ(РезервированиеЗаказовПокупателя.ДокументОснование КАК Документ.ЗаказПокупателя).Дата
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаЗаказа,
	|	РезервированиеЗаказовПокупателя.Контрагент.GLN КАК BUYER,
	|	РезервированиеЗаказовПокупателя.Организация.GLN КАК SUPPLIER,
	|	РезервированиеЗаказовПокупателя.ТочкаДоставки.GLN КАК DELIVERYPLACE,
	|	"""" КАК SENDER,
	|	РезервированиеЗаказовПокупателя.Контрагент.GLN КАК RECIPIENT,
	|	РезервированиеЗаказовПокупателя.Контрагент.Код КАК CAMPAIGNNUMBER
	|ИЗ
	|	Документ.РезервированиеЗаказовПокупателя КАК РезервированиеЗаказовПокупателя
	|ГДЕ
	|	РезервированиеЗаказовПокупателя.Ссылка = &резерв
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СУММА(ВложенныйЗапрос.Заказанное) КАК Заказанное,
	|	СУММА(ВложенныйЗапрос.Имеющееся) КАК Имеющееся
	|ПОМЕСТИТЬ ВТ_Заказано
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	|		СУММА(ЗаказПокупателяТовары.КоличествоБазовое) КАК Заказанное,
	|		0 КАК Имеющееся
	|	ИЗ
	|		Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|	ГДЕ
	|		ЗаказПокупателяТовары.Ссылка = &Заказ
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗаказПокупателяТовары.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервированиеЗаказовПокупателяТовары.Номенклатура,
	|		0,
	|		СУММА(РезервированиеЗаказовПокупателяТовары.Количество * РезервированиеЗаказовПокупателяТовары.Коэффициент)
	|	ИЗ
	|		Документ.РезервированиеЗаказовПокупателя.Товары КАК РезервированиеЗаказовПокупателяТовары
	|	ГДЕ
	|		РезервированиеЗаказовПокупателяТовары.Ссылка = &Резерв
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РезервированиеЗаказовПокупателяТовары.Номенклатура) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяТовары.Номенклатура.ОсновнойШтрихКод КАК ШтрихКод,
	|	ЗаказПокупателяТовары.Номенклатура.БазоваяЕдиницаИзмерения.ORDERUNIT КАК ORDERUNIT,
	|	ЗаказПокупателяТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	ЗаказПокупателяТовары.ЦенаБезНалогов КАК ЦенаБезНалогов,
	|	ЗаказПокупателяТовары.Цена КАК Цена,
	|	ЗаказПокупателяТовары.СтавкаНДС.Ставка КАК СтавкаНДС,
	|	ВТ_Заказано.Заказанное КАК Заказанное,
	|	ВТ_Заказано.Имеющееся КАК Имеющееся
	|ИЗ
	|	Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Заказано КАК ВТ_Заказано
	|		ПО ЗаказПокупателяТовары.Номенклатура = ВТ_Заказано.Номенклатура
	|ГДЕ
	|	ЗаказПокупателяТовары.Ссылка = &Заказ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	
	МассивРезультат = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультат[0].Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	
	СтруктураДокумента = Новый Структура;
	СтруктураДокумента.Вставить("ДокументКорректен",1);
	
	
	//ORDRSP
	СтруктураДокумента.Вставить("Дата",ВыборкаДетальныеЗаписи.DATE);
	СтруктураДокумента.Вставить("NUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	СтруктураДокумента.Вставить("DATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	СтруктураДокумента.Вставить("TIME",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=ЧЧ:мм"));
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERNUMBER) Тогда
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.ORDERNUMBER));
	Иначе
		СтруктураДокумента.Вставить("ORDERNUMBER",СокрЛП(ВыборкаДетальныеЗаписи.НомерЗаказа));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ORDERDATE) Тогда
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ORDERDATE,"ДФ=""гггг-ММ-дд"""));
	Иначе
		СтруктураДокумента.Вставить("ORDERDATE",Формат(ВыборкаДетальныеЗаписи.ДатаЗаказа,"ДФ=""гггг-ММ-дд"""));
	КонецЕсли;
	СтруктураДокумента.Вставить("DELIVERYDATE",Формат(ВыборкаДетальныеЗаписи.DELIVERYDATE,"ДФ=""гггг-ММ-дд"""));
	
	
	//СтруктураДокумента.Вставить("DELIVERYNOTENUMBER",СокрЛП(ВыборкаДетальныеЗаписи.NUMBER));
	//СтруктураДокумента.Вставить("DELIVERYNOTEDATE",Формат(ВыборкаДетальныеЗаписи.DATE,"ДФ=""гггг-ММ-дд"""));
	
	//HEAD
	СтруктураДокумента.Вставить("BUYER",СокрЛП(ВыборкаДетальныеЗаписи.BUYER));
	СтруктураДокумента.Вставить("SUPPLIER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("DELIVERYPLACE",СокрЛП(ВыборкаДетальныеЗаписи.DELIVERYPLACE));
	СтруктураДокумента.Вставить("INVOICEPARTNER",СокрЛП(ВыборкаДетальныеЗаписи.BUYER));
	//СтруктураДокумента.Вставить("SENDER","9864232378839");
	СтруктураДокумента.Вставить("SENDER",СокрЛП(ВыборкаДетальныеЗаписи.SUPPLIER));
	СтруктураДокумента.Вставить("RECIPIENT",СокрЛП(ВыборкаДетальныеЗаписи.RECIPIENT));
	СтруктураДокумента.Вставить("EDIINTERCHANGEID",СтруктураДокумента.ORDERNUMBER);
	
	
	СтруктураДокумента.Вставить("CURRENCY","UAH");
	СтруктураДокумента.Вставить("DOCTYPE","O");
	
	//POSITION
	POSITION=Новый ТаблицаЗначений;
	POSITION.Колонки.Добавить("POSITIONNUMBER");
	POSITION.Колонки.Добавить("PRODUCT");
	
	POSITION.Колонки.Добавить("ORDRSPUNIT");
	POSITION.Колонки.Добавить("DESCRIPTION");
	POSITION.Колонки.Добавить("PRICE");
	POSITION.Колонки.Добавить("PRICEWITHVAT");
	POSITION.Колонки.Добавить("VAT");
	
	POSITION.Колонки.Добавить("PRODUCTTYPE");
	POSITION.Колонки.Добавить("ORDEREDQUANTITY");
	POSITION.Колонки.Добавить("ACCEPTEDQUANTITY");
	
	ВыборкаДетальныеЗаписи = МассивРезультат[2].Выбрать();
	//нпп = 1;
	ACTION = "29";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НоваяСтрока =  POSITION.Добавить();
		НоваяСтрока.POSITIONNUMBER	  = Формат(ВыборкаДетальныеЗаписи.НомерСтроки,"ЧЦ=15; ЧГ=");
		НоваяСтрока.PRODUCT        	  = СокрЛП(ВыборкаДетальныеЗаписи.ШтрихКод);
		НоваяСтрока.ORDRSPUNIT        = СокрЛП(ВыборкаДетальныеЗаписи.ORDERUNIT);
		НоваяСтрока.DESCRIPTION       = СокрЛП(ВыборкаДетальныеЗаписи.НоменклатураНаименование);
		
		НоваяСтрока.PRICE             = Формат(ВыборкаДетальныеЗаписи.ЦенаБезНалогов,"ЧЦ=15; ЧДЦ=3; ЧРД=.; ЧГ=");
		НоваяСтрока.PRICEWITHVAT      = Формат(ВыборкаДетальныеЗаписи.Цена,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧГ=");
		НоваяСтрока.VAT               = Формат(ВыборкаДетальныеЗаписи.СтавкаНДС,"ЧЦ=15; ЧГ=");
		
		//	НоваяСтрока.TAXAMOUNT 	      = Формат(ВыборкаДетальныеЗаписи.СтавкаНДС, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0" );
		
		PRODUCTTYPE = 1;
		Если ВыборкаДетальныеЗаписи.Имеющееся = 0 Тогда
			PRODUCTTYPE = 3;
			ACTION = "4";
		ИначеЕсли ВыборкаДетальныеЗаписи.Заказанное <> ВыборкаДетальныеЗаписи.Имеющееся Тогда
			PRODUCTTYPE = 2;
			ACTION = "4";
		КонецЕсли;
		НоваяСтрока.PRODUCTTYPE 	  = PRODUCTTYPE;
		
		НоваяСтрока.ORDEREDQUANTITY     =  Формат(ВыборкаДетальныеЗаписи.Заказанное,"ЧЦ=15; ЧН=0; ЧГ=");
		НоваяСтрока.ACCEPTEDQUANTITY    =  Формат(ВыборкаДетальныеЗаписи.Имеющееся,"ЧЦ=15; ЧН=0; ЧГ=");
		
		//нпп = нпп +1;
	КонецЦикла;
	
	СтруктураДокумента.Вставить("ACTION",ACTION);
	
	СтруктураДокумента.Вставить("POSITION",POSITION);	
	
	Возврат СтруктураДокумента;
	
	
	
КонецФункции

#КонецОбласти	



#КонецЕсли
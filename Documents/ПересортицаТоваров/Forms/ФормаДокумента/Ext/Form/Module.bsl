&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	Элементы.ХозОперация.СписокВыбора.ЗагрузитьЗначения(ЗначениеНастроекПовтИсп.ПолучитьХозОперацииДокумента("ПересортицаТоваров"));

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПриЧтенииСозданииНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);			
	// Конец СтандартныеПодсистемы.УправлениеДоступом	
	
	УстановитьСостояниеДокумента();
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТоварыНоменклатураРасходПриИзменении(Элемент)
	
	ВидДвижения 	= "Расход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтруктураСтроки.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтруктураСтроки.ЕдиницаИзмерения);
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);
	ДобавитьДействиеЗаполненияОстатков(СтруктураДействий, ВидДвижения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриходПриИзменении(Элемент)
	
	ВидДвижения 	= "Приход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтруктураСтроки.ХарактеристикаНоменклатуры);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьЕдиницуИзмеренийПоВладельцу", СтруктураСтроки.ЕдиницаИзмерения);
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	ДобавитьДействиеЗаполненияОстатков(СтруктураДействий, ВидДвижения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыРасходПриИзменении(Элемент)
	
	ВидДвижения 	= "Расход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтруктураСтроки.ХарактеристикаНоменклатуры);
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);
	ДобавитьДействиеЗаполненияОстатков(СтруктураДействий, ВидДвижения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНоменклатурыПриходПриИзменении(Элемент)

	ВидДвижения 	= "Приход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", СтруктураСтроки.ХарактеристикаНоменклатуры);
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект);
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	ДобавитьДействиеЗаполненияОстатков(СтруктураДействий, ВидДвижения);
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоРасходПриИзменении(Элемент)
	
	ВидДвижения 	= "Расход";		
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	ТекущаяСтрока.КоличествоПриход = ТекущаяСтрока.КоличествоРасход;
	
	СтруктураСтрокиРасход = ПолучитьСтруктуруТекущейСтроки("Расход", ТекущаяСтрока);
	СтруктураСтрокиПриход = ПолучитьСтруктуруТекущейСтроки("Приход", ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтрокиРасход, СтруктураДействий, КэшированныеЗначения);
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтрокиПриход, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтрокиРасход, "Расход");	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтрокиПриход, "Приход");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриходПриИзменении(Элемент)

	ВидДвижения 	= "Приход";
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;	
	ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий);	
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);		
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаРасходПриИзменении(Элемент)

	ВидДвижения 	= "Расход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриходПриИзменении(Элемент)

	ВидДвижения 	= "Приход";	
	
	ТекущаяСтрока 	= Элементы.Товары.ТекущиеДанные;		
	СтруктураСтроки = ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(СтруктураСтроки, СтруктураДействий, КэшированныеЗначения);
	
	ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения);	
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ГруппаОснование");
	
	МассивВидимыхРеквизитов = Новый Массив;

	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда 
		МассивВидимыхРеквизитов.Добавить("ГруппаОснование");
	КонецЕсли;	
	
	ОбщегоНазначенияТККлиентСервер.УстановитьВидимостьЭлементовПоМассиву(Элементы, МассивВсехРеквизитов, МассивВидимыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, "ТоварыХарактеристикаНоменклатурыРасход", "Объект.Товары.ХарактеристикиИспользуютсяРасход");
	спСправочники.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, "ТоварыХарактеристикаНоменклатурыПриход", "Объект.Товары.ХарактеристикиИспользуютсяПриход");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей)
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	
	СтруктураЗаполненияХарактеристик = Новый Структура;
	СтруктураЗаполненияХарактеристик.Вставить("НоменклатураРасход", "ХарактеристикиИспользуютсяРасход");
	СтруктураЗаполненияХарактеристик.Вставить("НоменклатураПриход", "ХарактеристикиИспользуютсяПриход");
	
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураЗаполненияХарактеристик);											
	
	Если СтруктураНаименованийТабличныхЧастей.Свойство("Товары") Тогда
		спСправочники.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(Объект.Товары, ПараметрыЗаполненияРеквизитов);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСтруктуруТекущейСтроки(ВидДвижения, ТекущаяСтрока)	
	СтруктураПолей = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Количество, КоличествоБазовое, Коэффициент, Цена, Сумма, ХарактеристикиИспользуются, Остаток");
	СтруктураСтроки = Новый Структура;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат СтруктураПолей;
	КонецЕсли;
		
	Для Каждого Поле Из СтруктураПолей Цикл 				
		Значение = Неопределено;			
		ТекущаяСтрока.Свойство(Поле.Ключ + ВидДвижения, Значение);
		СтруктураСтроки.Вставить(Поле.Ключ, Значение);			
	КонецЦикла;
	
	Возврат СтруктураСтроки;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаписатьДанныеВСтроку(ТекущаяСтрока, СтруктураСтроки, ВидДвижения)
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Поле Из СтруктураСтроки Цикл 
		
		ИмяПоля = Поле.Ключ + ВидДвижения;
		Если ТекущаяСтрока.Свойство(ИмяПоля) Тогда 
			ТекущаяСтрока[ИмяПоля] = Поле.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличества(СтруктураДействий, Форма = Неопределено)
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСумму");

КонецПроцедуры       

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьДействиеЗаполненияЦен(СтруктураДействий, Объект)
	
	СтруктураПолейДляЗаполненияЦен = Новый Структура;	
	СтруктураПолейДляЗаполненияЦен.Вставить("Цена", ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПараметровЗаполненияЦен(Объект));
	
	СтруктураДействий.Вставить("ЗаполнитьЦены", СтруктураПолейДляЗаполненияЦен);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДействиеЗаполненияОстатков(СтруктураДействий, ВидДвижения)
	
	Если Не ЗначениеЗаполнено(Объект.Дата) Тогда 
		Дата = ТекущаяДата();
	Иначе
		Дата = Объект.Дата;
	КонецЕсли;
	      
	СтруктураПараметровОстатков = Новый Структура;
	СтруктураПараметровОстатков.Вставить("Дата", Дата);
	СтруктураПараметровОстатков.Вставить("Ссылка", Объект.Ссылка);
	СтруктураПараметровОстатков.Вставить("СкладКомпании", Объект.СкладКомпании);	
	СтруктураПараметровОстатков.Вставить("ИмяПоляОстатка", "Остаток");
	
	СтруктураДействий.Вставить("ЗаполнитьОстаток", СтруктураПараметровОстатков);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияТК.СостояниеДокумента(Объект);
	
КонецПроцедуры


#КонецОбласти
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Проведение
	
	
	
	#КонецОбласти
	
	
	
	#Область ПрограммныйИнтерфейс	
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Документ.АссортиментныйКомитет";
		КомандаПечати.Идентификатор = "АссортиментныйКомитет";
		КомандаПечати.Представление = НСтр("ru = 'Ассортиментный комитет'; uk = 'Асортиментний комітет'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь; 		
	КонецПроцедуры
	
	// Определяет список команд отчетов.
	//
	// Параметры:
	//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
	//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
	//
	Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
		
		КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
		Если КомандаОтчет <> Неопределено Тогда
			КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
		КонецЕсли;
		
		
	КонецПроцедуры
	
	// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
	Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
		
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(ПодразделениеКомпании)";
		
		Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
	
	// Устанавливает параметры загрузки.
	//
	// Параметры:
	//  Параметры - Структура - параметры загрузки данных из файла.
	//
	Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
		
	КонецПроцедуры
	
	// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
	// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
	//
	// Параметры:
	//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
	//                               находятся загруженные данные из файла. Состав колонок:
	//     * Идентификатор - Число - порядковый номер строки;
	//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
	//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
	//                               являющейся копией табличной части документа, 
	//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
	//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
	//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
	//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
	//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
	//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
	//
	Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
		
		Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
		ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.ШтрихКодТовара КАК СТРОКА(200)) КАК Штрихкод,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.ЛокализованноеНаименование КАК СТРОКА(100)) КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
		|			И (ДанныеДляСопоставления.Артикул <> """")
		|			И (НЕ спрНоменклатура.ЭтоГруппа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|ГДЕ
		|	НЕ ДанныеДляСопоставления.Идентификатор В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
		|	И ДанныеДляСопоставления.Штрихкод <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
		|	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
		|ИЗ
		|	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|ГДЕ
		|	НЕ ДанныеДляСопоставления.Идентификатор В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|		
		|				ОБЪЕДИНИТЬ ВСЕ
		|		
		|				ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|				ИЗ
		|					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
		|	И ДанныеДляСопоставления.Номенклатура <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
		|			И (НЕ спрНоменклатура.ЭтоГруппа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
		|	""Наименование"" КАК ПолеПоиска,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
		|ИЗ
		|	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
		|	""Артикул"",
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
		|	""Штрихкод"",
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
		
		Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
		
		ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
		ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
		Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
			Товар = Товары.Добавить();
			
			Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
			//Товар.Количество = СтрокаТаблицы.Количество;
			
			нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
			Если нСтрока <> Неопределено Тогда 			
				Если нСтрока.Количество = 1 Тогда 
					
					Товар.Номенклатура = нСтрока.Ссылка;			
					
				ИначеЕсли нСтрока.Количество > 1 Тогда 
					
					ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
					ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
					ЗаписьОНеоднозначности.Колонка = "Номенклатура";
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;	
		
		ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
		
	КонецПроцедуры
	
	// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
	// 
	// Параметры:
	//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
	//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
	//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
	//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
	//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
	//
	Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
		
		Если ИмяКолонки = "Номенклатура" Тогда 
			Запрос = Новый Запрос;
			
			ТекстГде = "";
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
				ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
				Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
				ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
				Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
			КонецЕсли;
			
			Запрос.Текст = "ВЫБРАТЬ
			|	спрНоменклатура.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК спрНоменклатура
			|ГДЕ
			|	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл 
				СписокНеоднозначностей.Добавить(Выборка.Ссылка);
			КонецЦикла;		
		КонецЕсли;
		
	КонецПроцедуры
	
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла
	
	#КонецОбласти	
	
	
	#Область Печать
	
	// Формирует печатные формы.
	//
	// Параметры:
	//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
	//  ПараметрыПечати - Структура - дополнительные настройки печати;
	//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
	//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
	//                                            представление - имя области, в которой был выведен объект (выходной параметр);
	//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АссортиментныйКомитет") Тогда		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"АссортиментныйКомитет",
			НСтр("ru = 'Товаро-транспортная накладная'; uk = 'Товаро-транспортна накладна'"), 
			ПечатьАК(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.АссортиментныйКомитет.ПФ_MXL_АссортиментныйКомитет");
			
		КонецЕсли;
	КонецПроцедуры
	
	Функция ПечатьАК(МассивОбъектов, ОбъектыПечати)
		
		//Заполним параметры табличного документа
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АссортиментныйКомитет";
		
		ТабличныйДокумент.ПолеСверху = 0;
		ТабличныйДокумент.ПолеСлева  = 0;
		ТабличныйДокумент.ПолеСнизу  = 0;
		ТабличныйДокумент.ПолеСправа = 0;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АссортиментныйКомитет.ПФ_MXL_АссортиментныйКомитет");
		
		ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
		ОбластьШапка		= Макет.ПолучитьОбласть("Шапка");
		ОбластьТоварыШапка	= Макет.ПолучитьОбласть("ТоварыШапка");
		ОбластьТовары		= Макет.ПолучитьОбласть("Товары");
		
		ПервыйДокумент = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	АссортиментныйКомитетТовары.НомерСтроки КАК НомерСтроки,
		|	СкладыКомпании.АссортиментнаяМатрица КАК Матрица,
		|	АссортиментныйКомитетПлощадкиПоСтроке.КлючСтроки КАК КлючСтроки,
		|	СкладыКомпании.Код КАК КодСклада,
		|	СкладыКомпании.Ссылка КАК СкладКомпании,
		|	АссортиментныйКомитетПлощадкиПоСтроке.ТорговаяПлощадка КАК ТорговаяПлощадка,
		|	АссортиментныйКомитетТовары.Номенклатура.Артикул КАК Артикул,
		|	АссортиментныйКомитетТовары.Номенклатура КАК Номенклатура,
		|	АссортиментныйКомитетТовары.ВидАссортимента КАК ВидАссортимента,
		|	АссортиментныйКомитетТовары.Ссылка КАК Ссылка,
		|	АссортиментныйКомитетТовары.Ссылка.Номер КАК Номер,
		|	АссортиментныйКомитетТовары.Ссылка.Дата КАК Дата,
		|	АссортиментныйКомитетТовары.Ссылка.ХозОперация КАК ХозОперация,
		|	АссортиментныйКомитетТовары.Ссылка.ДатаНачалаДействия КАК ДатаНачалаДействия,
		|	АссортиментныйКомитетТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
		|	АссортиментныйКомитетТовары.Ссылка.Контрагент КАК Контрагент,
		|	АссортиментныйКомитетТовары.Ссылка.Комментарий КАК Комментарий
		|ИЗ
		|	Документ.АссортиментныйКомитет.Товары КАК АссортиментныйКомитетТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АссортиментныйКомитет.ПлощадкиПоСтроке КАК АссортиментныйКомитетПлощадкиПоСтроке
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СкладыКомпании КАК СкладыКомпании
		|			ПО АссортиментныйКомитетПлощадкиПоСтроке.ТорговаяПлощадка = СкладыКомпании.ТорговаяПлощадка
		|				И (НЕ СкладыКомпании.ПометкаУдаления)
		|				И (СкладыКомпании.АссортиментнаяМатрица <> ЗНАЧЕНИЕ(Справочник.АссортиментнаяМатрица.ПустаяСсылка))
		|		ПО АссортиментныйКомитетТовары.Ссылка = АссортиментныйКомитетПлощадкиПоСтроке.Ссылка
		|			И АссортиментныйКомитетТовары.КлючСтроки = АссортиментныйКомитетПлощадкиПоСтроке.КлючСтроки
		|ГДЕ
		|	АссортиментныйКомитетТовары.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ ПО
		|	Ссылка";
		
		Запрос.Параметры.Вставить("МассивСсылок", МассивОбъектов);
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДокументов.Следующий() Цикл 
			
			СтруктураЗаголовок = Новый Структура("Номер, Дата, ХозОперация, ДатаНачалаДействия");
			СтруктураШапки = Новый Структура("ПодразделениеКомпании, Контрагент, Комментарий");
			
			ЗаполнитьЗначенияСвойств(СтруктураЗаголовок, ВыборкаДокументов);
			СтруктураЗаголовок.Дата = Формат(ВыборкаДокументов.Дата, "ДФ=dd.MM.yyyy");
			ЗаполнитьЗначенияСвойств(СтруктураШапки, ВыборкаДокументов);
			
			Если Не ПервыйДокумент Тогда
				// Все документы нужно выводить на разных страницах.
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				ПервыйДокумент = Ложь;	
			КонецЕсли;		
			
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;				
			
			//Заполняем документ
			ОбластьЗаголовок.Параметры.Заполнить(СтруктураЗаголовок);
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			ОбластьШапка.Параметры.Заполнить(СтруктураШапки);
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			ТабличныйДокумент.Вывести(ОбластьТоварыШапка);
			
			ВыборкаТовары = ВыборкаДокументов.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				СтруктураСтроки = Новый Структура;
				СтруктураСтроки.Вставить("НомерСтроки", ВыборкаТовары.НомерСтроки);
				СтруктураСтроки.Вставить("Матрица", СокрЛП(ВыборкаТовары.Матрица));
				СтруктураСтроки.Вставить("ТорговаяПлощадка", СокрЛП(ВыборкаТовары.ТорговаяПлощадка));
				СтруктураСтроки.Вставить("КодСклада", СокрЛП(ВыборкаТовары.КодСклада));
				СтруктураСтроки.Вставить("СкладКомпании", СокрЛП(ВыборкаТовары.СкладКомпании));
				СтруктураСтроки.Вставить("Артикул", СокрЛП(ВыборкаТовары.Артикул));
				СтруктураСтроки.Вставить("Номенклатура", СокрЛП(ВыборкаТовары.Номенклатура));
				СтруктураСтроки.Вставить("ВидАссортимента", ВыборкаТовары.ВидАссортимента);
				
				ОбластьТовары.Параметры.Заполнить(СтруктураСтроки);
				ТабличныйДокумент.Вывести(ОбластьТовары);
				
			КонецЦикла;
			
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати комплектов документов.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);
			
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
	КонецФункции
	
	
	
	#КонецОбласти	
	
	Процедура СоздатьОбновитьНоменклатуру(Строка) Экспорт
		
		
	КонецПроцедуры
	
	
#Иначе
	
#КонецЕсли
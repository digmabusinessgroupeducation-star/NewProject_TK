

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);	
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды	
	
	Если Параметры.Ключ.Пустая() Тогда 
		Объект.ДатаНачала = ТекущаяДата();
		Объект.ДатаОкончания = КонецГода(ТекущаяДата());
	КонецЕсли;
	
	ПериодДействия.ДатаНачала = Объект.ДатаНачала;
	ПериодДействия.ДатаОкончания = Объект.ДатаОкончания;	
	Расхождение = Объект.Расхождение;
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ОстатокНаКонец = Объект.ОстатокНаНачало + Объект.ПоДаннымОрганизации.Итог("Дебет") - Объект.ПоДаннымОрганизации.Итог("Кредит");
	ОстатокНаКонецКонтрагент = ОстатокНаНачалоКонтрагент + Объект.ПоДаннымКонтрагента.Итог("Дебет") - Объект.ПоДаннымКонтрагента.Итог("Кредит");
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// ТК_УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ТК_УправлениеДоступом	
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения		
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьСостояниеДокумента();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодДействияПриИзменении(Элемент)
	Объект.ДатаНачала = ПериодДействия.ДатаНачала;
	Объект.ДатаОкончания = ПериодДействия.ДатаОкончания;
КонецПроцедуры

&НаКлиенте
Процедура НесколькоДоговоровПриИзменении(Элемент)	
	
	Если Не НесколькоДоговоров И Объект.ДоговорыВзаиморасчетов.Количество() > 1 Тогда 
		НесколькоДоговоров = Истина;
		Возврат;
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СверкаСогласованаПриИзменении(Элемент)
	УстановитьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ИтогОстатокНаНачалоПриИзменении(Элемент)
	ПересчитатьОстатки();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт

    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);

КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)

    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗаполнитьПоДаннымУправленческогоУчета(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;		
	КонецЕсли;
	
	Если Объект.ПоДаннымОрганизации.Количество() > 0 Тогда 
		ПоказатьВопросПользователю(НСтр("ru = 'Перед заполнением данные организации и контрагента будут очищены.
                                         |Продолжить?'; uk = 'Перед заповненням дані організації та контрагенту будуть очищені.
                                         |Продовжити?'"), Новый Структура("ЗаполнитьПоДаннымУправленческогоУчетаНаСервере"));	
	Иначе
		ЗаполнитьПоДаннымУправленческогоУчетаНаСервере();
	КонецЕсли;
	
	ПересчитатьОстатки();	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымОрганизацииНаКлиенте(Команда)
	
	Если Объект.ПоДаннымКонтрагента.Количество() > 0 Тогда 
		ПоказатьВопросПользователю(НСтр("ru = 'Перед заполнением данные контрагента будут очищены.
                                         |Продолжить?'; uk = 'Перед заповненням дані контрагенту будуть очищені.
                                         |Продовжити?'"), Новый Структура("ЗаполнитьПоДаннымОрганизации"));
	Иначе
		ЗаполнитьПоДаннымОрганизации();
	КонецЕсли;
	
	ПересчитатьОстатки();
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПоДаннымУправленческогоУчетаНаСервере()
	Документы.ТК_АктСверкиВзаиморасчетов.ЗаполнитьПоДаннымУправленческогоУчета(Объект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДаннымОрганизации()
	Документы.ТК_АктСверкиВзаиморасчетов.ЗаполнитьПоДаннымОрганизации(Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьОстатки()
	
	//Остаток на начало
	ОстатокНаНачало = Объект.ОстатокНаНачало;
	ОстатокНаНачалоКонтрагент = ОстатокНаНачало * -1;
	
	//Остаток на конец
	ОстатокНаКонец = ОстатокНаНачало + Объект.ПоДаннымОрганизации.Итог("Дебет") - Объект.ПоДаннымОрганизации.Итог("Кредит");
	ОстатокНаКонецКонтрагент = ОстатокНаНачалоКонтрагент + Объект.ПоДаннымКонтрагента.Итог("Дебет") - Объект.ПоДаннымКонтрагента.Итог("Кредит");
	
	//Расхождения
	Расхождение = ОстатокНаКонец + ОстатокНаКонецКонтрагент;
	РасхождениеКонтрагент = Расхождение * -1;
	
	Объект.Расхождение = Расхождение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоДаннымОрганизацииПослеУдаления(Элемент)
	ПересчитатьОстатки();
КонецПроцедуры


&НаКлиенте
Процедура ПоДаннымОрганизацииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПересчитатьОстатки();
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьВопросПользователю(ТекстВопроса, ДопПараметры)
	
	Оповещение = Новый ОписаниеОповещения("ВопросПользователюПродолжение", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПользователюПродолжение(Результат, ДопПараметры) Экспорт 
	
	Если Результат <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДопПараметры.Свойство("ЗаполнитьПоДаннымУправленческогоУчетаНаСервере") Тогда 	
		ЗаполнитьПоДаннымУправленческогоУчетаНаСервере();
	ИначеЕсли ДопПараметры.Свойство("ЗаполнитьПоДаннымОрганизации") Тогда 
		ЗаполнитьПоДаннымОрганизации();
	КонецЕсли;
	
	ПересчитатьОстатки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	Если Объект.ПометкаУдаления Тогда 
		СостояниеДокумента = 3;
	Иначе 
		СостояниеДокумента = 1;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	УстановитьВидимостьДоговоров();
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоговоров()
	
	ПоказатьСписок = НесколькоДоговоров ИЛИ Объект.ДоговорыВзаиморасчетов.Количество() > 1;
	
	Если ПоказатьСписок И Не НесколькоДоговоров Тогда 
		НесколькоДоговоров = Истина;
	ИначеЕсли Не ПоказатьСписок И Объект.ДоговорыВзаиморасчетов.Количество() = 0 Тогда 
		Объект.ДоговорыВзаиморасчетов.Добавить();
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДоговорВзаиморасчетов", "Видимость", Не ПоказатьСписок);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницаДоговорыВзаиморасчетов", "Видимость", ПоказатьСписок);

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	МассивРеквизитовДоступности = Новый Массив;
	МассивРеквизитовДоступности.Добавить("Контрагент");
	МассивРеквизитовДоступности.Добавить("ДоговорВзаиморасчетов");
	МассивРеквизитовДоступности.Добавить("ДоговорыВзаиморасчетов");
	МассивРеквизитовДоступности.Добавить("Сделка");
	МассивРеквизитовДоступности.Добавить("ВалютаДокумента");
	МассивРеквизитовДоступности.Добавить("ИтогОстатокНаНачало");
	МассивРеквизитовДоступности.Добавить("ПериодДействия");
	МассивРеквизитовДоступности.Добавить("Дата");
	МассивРеквизитовДоступности.Добавить("РегламентированныйУчет");
	МассивРеквизитовДоступности.Добавить("ПоДаннымОрганизации");
	МассивРеквизитовДоступности.Добавить("ПоДаннымКонтрагента");
	МассивРеквизитовДоступности.Добавить("ПоДаннымОрганизацииЗаполнитьПоДаннымУправленческогоУчета");
	МассивРеквизитовДоступности.Добавить("ПоДаннымКонтрагентаЗаполнитьПоДаннымОрганизации");
	
	ОбщегоНазначенияТККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивРеквизитовДоступности, "ТолькоПросмотр", Объект.СверкаСогласована);
	
КонецПроцедуры



#КонецОбласти


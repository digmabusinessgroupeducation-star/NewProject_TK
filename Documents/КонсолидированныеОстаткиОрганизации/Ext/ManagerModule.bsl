#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства);
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаОстаткиОрганизации(Запрос, ТекстыЗапроса, Регистры);
			
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
		
	

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка,ДополнительныеСвойства)

	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	КонсолидированныеОстаткиОрганизации.Дата КАК Период,
	               |	КонсолидированныеОстаткиОрганизации.Ссылка КАК Ссылка,
	               |	КонсолидированныеОстаткиОрганизации.ХозОперация КАК ХозОперация,
	               |	КонсолидированныеОстаткиОрганизации.Организация КАК Организация,
	               |	КонсолидированныеОстаткиОрганизации.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	КонсолидированныеОстаткиОрганизации.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	КонсолидированныеОстаткиОрганизации.Контрагент КАК Контрагент,
	               |	КонсолидированныеОстаткиОрганизации.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	КонсолидированныеОстаткиОрганизации.ВалютаДокумента КАК ВалютаДокумента,
	               |	КонсолидированныеОстаткиОрганизации.МоментВремени КАК МоментВремени
	               |ИЗ
	               |	Документ.КонсолидированныеОстаткиОрганизации КАК КонсолидированныеОстаткиОрганизации
	               |ГДЕ
	               |	КонсолидированныеОстаткиОрганизации.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ХозОперация",Реквизиты.ХозОперация);
	Запрос.УстановитьПараметр("Организация",Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПодразделениеКомпании",Реквизиты.ПодразделениеКомпании);
	Запрос.УстановитьПараметр("ТорговаяПлощадка",Реквизиты.ТорговаяПлощадка);
	Запрос.УстановитьПараметр("Контрагент",Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДоговорВзаиморасчетов",Реквизиты.ДоговорВзаиморасчетов);
	Запрос.УстановитьПараметр("ВалютаДокумента",Реквизиты.ВалютаДокумента);
	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);

	ПроводитьПоОстаткамОрганизации = ПроведениеСервер.ИспользуютсяОстаткиОрганизаций(Реквизиты.Период);
	Запрос.УстановитьПараметр("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации );
	ДополнительныеСвойства.ДляПроведения.Вставить("ПроводитьПоОстаткамОрганизации",ПроводитьПоОстаткамОрганизации);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрацияПоследовательности",ПроводитьПоОстаткамОрганизации);
	ДополнительныеСвойства.ДляПроведения.Вставить("ПодразделениеКомпании", 	Реквизиты.ПодразделениеКомпании);

	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаТовары";
	ТекстЗапроса = "ВЫБРАТЬ
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.Ссылка КАК Ссылка,
	               |	КонсолидированныеОстаткиОрганизацииТовары.НомерСтроки КАК НомерСтроки,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.Организация КАК Организация,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.ПодразделениеКомпании КАК ПодразделениеКомпании,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.ТорговаяПлощадка КАК ТорговаяПлощадка,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.Контрагент КАК Контрагент,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.ДоговорВзаиморасчетов КАК ДоговорВзаиморасчетов,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.ХозОперация КАК ХозОперация,
	               |	КонсолидированныеОстаткиОрганизацииТовары.ВидДвижения КАК ВидДвижения,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Номенклатура КАК Номенклатура,
	               |	КонсолидированныеОстаткиОрганизацииТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Количество КАК Количество,
	               |	КонсолидированныеОстаткиОрганизацииТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Коэффициент КАК Коэффициент,
	               |	КонсолидированныеОстаткиОрганизацииТовары.КоличествоБазовое КАК КоличествоБазовое,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Ссылка.КурсДокумента КАК КурсДокумента,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Цена КАК Цена,
	               |	КонсолидированныеОстаткиОрганизацииТовары.Сумма КАК Сумма,
	               |	КонсолидированныеОстаткиОрганизацииТовары.СуммаВсего КАК СуммаВсего,
	               |	КонсолидированныеОстаткиОрганизацииТовары.СуммаНДС КАК СуммаНДС,
	               |	КонсолидированныеОстаткиОрганизацииТовары.СуммаБезНалогов КАК СуммаБезНалогов,
	               |	КонсолидированныеОстаткиОрганизацииТовары.СтавкаНДС КАК СтавкаНДС,
	               |	КонсолидированныеОстаткиОрганизацииТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчетаНоменклатурыОрганизаций
	               |ПОМЕСТИТЬ ВтТаблицаТовары
	               |ИЗ
	               |	Документ.КонсолидированныеОстаткиОрганизации.Товары КАК КонсолидированныеОстаткиОрганизацииТовары
	               |ГДЕ
	               |	КонсолидированныеОстаткиОрганизацииТовары.КоличествоБазовое > 0
	               |	И КонсолидированныеОстаткиОрганизацииТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОстаткиОрганизации(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТаблицаПартииТоваровОрганизации";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаТовары(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ВЫБОР
	              |		КОГДА ВтТаблицаТовары.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвижений.Приход)
	              |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	              |		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	              |	КОНЕЦ КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	ВтТаблицаТовары.Количество КАК Количество,
	              |	ВтТаблицаТовары.Сумма КАК Сумма,
	              |	ВтТаблицаТовары.СуммаНДС КАК СуммаНДС,
	              |	ВтТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	              |	ВтТаблицаТовары.ХозОперация КАК ХозОперация,
	              |	ВтТаблицаТовары.Организация КАК Организация,
	              |	ВтТаблицаТовары.Ссылка КАК Партия,
	              |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартий.ТоварКупленный) КАК СтатусПартии,
	              |	ВтТаблицаТовары.АналитикаУчетаНоменклатурыОрганизаций КАК АналитикаУчета
	              |ИЗ
	              |	ВтТаблицаТовары КАК ВтТаблицаТовары
	              |ГДЕ
	              |	&ПроводитьПоОстаткамОрганизации"; 
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти




#Область ПрограммныйИнтерфейс	

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт недопоставки 
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
    КомандаПечати.Идентификатор = "АктНедопоставки";
    КомандаПечати.Представление = НСтр("ru = 'Акт недопоставки';uk='Акт недопостачання'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	//МассивУсловий = Новый Массив;
	//МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ИспользоватьЦеныИзПротоколов", Истина, ВидСравнения.Равно));	
	////МассивУсловий.Добавить(Новый Структура("Реквизит, Значение, ВидСравнения", "ЕстьРасхождения", Истина, ВидСравнения.Равно));	
	//КомандаПечати.УсловияВидимости = МассивУсловий;
	
	// Приходная накладная
    КомандаПечати = КомандыПечати.Добавить();
    КомандаПечати.МенеджерПечати = "Документ.ПоступлениеТоваров";
    КомандаПечати.Идентификатор = "ПриходнаяНакладная";
    КомандаПечати.Представление = НСтр("ru = 'Приходная накладная'; uk = 'Прибуткова накладна'");
    КомандаПечати.ПроверкаПроведенияПередПечатью = Истина; 		
	
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.МестоРазмещения = "ПодменюОтчеты";
	КонецЕсли;
	
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ПодразделениеКомпании)
	|	И ЗначениеРазрешено(ТорговаяПлощадка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом


// СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла

// Устанавливает параметры загрузки.
//
// Параметры:
//  Параметры - Структура - параметры загрузки данных из файла.
//
Процедура УстановитьПараметрыЗагрузкиИзФайлаВТЧ(Параметры) Экспорт
	
КонецПроцедуры

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
//
// Параметры:
//   АдресЗагружаемыхДанных    - Строка - адрес временного хранилища с таблицей значений, в которой
//                               находятся загруженные данные из файла. Состав колонок:
//     * Идентификатор - Число - порядковый номер строки;
//     * остальные колонки соответствуют колонкам макета ЗагрузкаИзФайла.
//   АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                               являющейся копией табличной части документа, 
//                                        которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
//   СписокНеоднозначностей - ТаблицаЗначений - список неоднозначных значений, для которых в ИБ имеется несколько подходящих вариантов.
//       * Колонка       - Строка - имя колонки, в которой была обнаружена неоднозначность;
//       * Идентификатор - Число  - идентификатор строки, в которой была обнаружена неоднозначность.
//   ПолноеИмяТабличнойЧасти   - Строка - полное имя табличной части, в которую загружаются данные.
//   ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения.
//
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	Товары = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления); // ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных); // ТаблицаЗначений
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
	               |	ВЫРАЗИТЬ(ДанныеДляСопоставления.Номенклатура КАК СТРОКА(100)) КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставления
	               |ИЗ
	               |	&ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставления.Артикул = спрНоменклатура.Артикул
	               |			И (ДанныеДляСопоставления.Артикул <> """")
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Штрихкод КАК Штрихкод,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоШтрихКоду
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу)
	               |	И ДанныеДляСопоставления.Штрихкод <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
	               |	ДанныеДляСопоставленияПоШтрихКоду.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
	               |ИЗ
	               |	ДанныеДляСопоставленияПоШтрихКоду КАК ДанныеДляСопоставленияПоШтрихКоду
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	               |		ПО ДанныеДляСопоставленияПоШтрихКоду.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДляСопоставления.Номенклатура КАК Номенклатура,
	               |	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	               |ГДЕ
	               |	НЕ ДанныеДляСопоставления.Идентификатор В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоАртикулу.Идентификатор КАК Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |		
	               |				ОБЪЕДИНИТЬ ВСЕ
	               |		
	               |				ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
	               |				ИЗ
	               |					СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду)
	               |	И ДанныеДляСопоставления.Номенклатура <> """"
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	спрНоменклатура.Ссылка КАК Ссылка,
	               |	ДанныеДляСопоставленияПоНаименованию.Идентификатор КАК Идентификатор
	               |ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
	               |ИЗ
	               |	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставленияПоНаименованию
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	               |		ПО ДанныеДляСопоставленияПоНаименованию.Номенклатура = спрНоменклатура.Наименование
	               |			И (НЕ спрНоменклатура.ЭтоГруппа)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор КАК Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка) КАК Ссылка,
	               |	""Наименование"" КАК ПолеПоиска,
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор) КАК Количество
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
	               |	""Артикул"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор,
	               |	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка),
	               |	""Штрихкод"",
	               |	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор)
	               |ИЗ
	               |	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор";
	
	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	
	ТаблицаНоменклатура = Запрос.Выполнить().Выгрузить();	
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
		
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл 
		Товар = Товары.Добавить();
		
		Товар.Идентификатор = СтрокаТаблицы.Идентификатор;
		Товар.Количество = СтрокаТаблицы.Количество;
		Товар.Цена = СтрокаТаблицы.Цена;
		Товар.СуммаБезНалогов = СтрокаТаблицы.СуммаБезНДС;
		Товар.Сумма = СтрокаТаблицы.Сумма;
		
		нСтрока = ТаблицаНоменклатура.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		Если нСтрока <> Неопределено Тогда 			
			Если нСтрока.Количество = 1 Тогда 
				
				Товар.Номенклатура = нСтрока.Ссылка;			
				Если Не ПустаяСтрока(СтрокаТаблицы.ХарактеристикаНоменклатуры) Тогда 
					знчМРЦ = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(СтрокаТаблицы.ХарактеристикаНоменклатуры, "МРЦ", "")));
				Иначе
					знчМРЦ = 0;
				КонецЕсли;
				Товар.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ВернутьХарактеристикуПоЗначениюМРЦ(нСтрока.Ссылка,знчМРЦ);

			ИначеЕсли нСтрока.Количество > 1 Тогда 
				
				ЗаписьОНеоднозначности = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = СтрокаТаблицы.Идентификатор;
				ЗаписьОНеоднозначности.Колонка = "Номенклатура";
				
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(СтрокаТаблицы.ВидДвижения) Тогда
			Если СокрЛП(СтрокаТаблицы.ВидДвижения) = "Приход" Тогда  
				Товар.ВидДвижения = Перечисления.ВидыДвижений.Приход;
			ИначеЕсли СокрЛП(СтрокаТаблицы.ВидДвижения) = "Расход" Тогда 
				Товар.ВидДвижения = Перечисления.ВидыДвижений.Расход;
			ИначеЕсли СокрЛП(СтрокаТаблицы.ВидДвижения) = "Прочее" Тогда 
				Товар.ВидДвижения = Перечисления.ВидыДвижений.Прочее;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;	
		
	ПоместитьВоВременноеХранилище(Товары, АдресТаблицыСопоставления);
	
КонецПроцедуры

// Возвращает список подходящих объектов ИБ для неоднозначного значения ячейки.
// 
// Параметры:
//    ПолноеИмяТабличнойЧасти  - Строка - Полное имя табличной части, в которую загружаются данные.
//    ИмяКолонки               - Строка - Имя колонки, в который возникла неоднозначность. 
//    СписокНеоднозначностей    - Массив  - Массив для заполнения с неоднозначными данными.
//    ЗагружаемыеЗначенияСтрока - Строка  - Загружаемые данные, на основании которых возникла неоднозначность.
//    ДополнительныеПараметры   - ЛюбойТип - Любые дополнительные сведения
//
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Если ИмяКолонки = "Номенклатура" Тогда 
		Запрос = Новый Запрос;
		
		ТекстГде = "";
		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Артикул) Тогда 
			ТекстГде = "И спрНоменклатура.Артикул = &Артикул";
			Запрос.УстановитьПараметр("Артикул", ЗагружаемыеЗначенияСтрока.Артикул);
		КонецЕсли;

		Если ЗначениеЗаполнено(ЗагружаемыеЗначенияСтрока.Номенклатура) Тогда 
			ТекстГде = ?(ПустаяСтрока(ТекстГде), "И ", ТекстГде + " ИЛИ ") + "спрНоменклатура.Наименование = &Наименование";
			Запрос.УстановитьПараметр("Наименование", ЗагружаемыеЗначенияСтрока.Номенклатура);
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	спрНоменклатура.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Номенклатура КАК спрНоменклатура
		               |ГДЕ
		               |	НЕ спрНоменклатура.ЭтоГруппа " + ТекстГде;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;		
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла


#КонецОбласти	


#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//                                            представление - имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//// Акт недопоставки
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктНедопоставки") Тогда    
	//	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	//	КоллекцияПечатныхФорм,
	//	"АктНедопоставки",
	//	НСтр("ru = 'Акт недопоставки';uk='Акт недопостачання'"), 
	//	ПечатьАктНедопоставки(МассивОбъектов, ОбъектыПечати),
	//	,
	//	"Документ.ПоступлениеТоваров.ПФ_MXL_АктПриемаПередачи");
	//	
	//ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходнаяНакладная") Тогда 		
	//	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
	//	КоллекцияПечатныхФорм,
	//	"ПриходнаяНакладная",
	//	НСтр("ru = 'Приходная накладная'; uk = 'Прибуткова накладна'"), 
	//	ПечатьПриходнаяНакладная(МассивОбъектов, ОбъектыПечати),
	//	,
	//	"Документ.ПоступлениеТоваров.ПФ_MXL_ПриходнаяНакладная");		
	//	
	//КонецЕсли;	
	
КонецПроцедуры



#КонецОбласти	

#Область Последовательность

Процедура ИнициалзироватьДанныеПоследовательности(Ссылка,ДополнительныеСвойства) Экспорт
	
	ТаблицыДляПоследовательности = Новый Структура("ПартииОрганизаций");	
	

	ТаблицаПоследовательности = Новый ТаблицаЗначений;
	ТаблицаПоследовательности.Колонки.Добавить("Период");
	ТаблицаПоследовательности.Колонки.Добавить("Регистратор");
	ТаблицаПоследовательности.Колонки.Добавить("Организация");
	
	НоваяСтрока = ТаблицаПоследовательности.Добавить();
	НоваяСтрока.Регистратор        = Ссылка;
	НоваяСтрока.Организация 	   = Ссылка.Организация;
	НоваяСтрока.Период             = Ссылка.Дата;
	
	ТаблицыДляПоследовательности.ПартииОрганизаций = ТаблицаПоследовательности;

	
	ДополнительныеСвойства.Вставить("ТаблицыДляПоследовательности",ТаблицыДляПоследовательности);
	
КонецПроцедуры

#КонецОбласти



#Иначе
	
#КонецЕсли